     h option(*nodebugio) datedit(*dmy)
      /title  ASOFAK Mapping av temp./EDI-flatfil mot Autofakt-format
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System  . . . . : ASOFAK
      *  Program . . . . : FO185R
      *  Beskrivelse . . : Mapping av temp./EDI-flatfil mot Autofakt-format
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  -------------------------------------------------
      *       080205 GRO  Nytt program
6.NU  * R6.30 230514 bsa  Programmet gjennomgått mtp nummerutvidelser.
      *
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner - COMP
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av linjenummersekvenser (2 første siffer i linjenr LIMLIN)
      *
      *    10 : Hodeinformasjon
      *    20 : Partsinformasjon
      *    30 : Referanser
      *    40 : Transportinformasjon
      *    50 : Linjer
      *    55 : Linjer fra best. (ikke match i EDI-melding)
      *    60 : Tillegg/fradrag hodenivå
      *    90 : Totaler
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffnaxpf    if   e           k disk
     ffwfalr    if a e           k disk    rename(fwfapfr:fwfalrr)
     ffwfalu    uf a e           k disk    rename(fwfapfr:fwfalur)
     ffnaupf    uf a e           k disk
     fsohel1    if   e           k disk    rename(sohepfr:sohel1r)
     fsstal7    if   e           k disk    rename(sstapfr:sstal7r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
      *
      * Definering av LDA
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d fwfalr_recn     s              4  0
     d fwfalu_recn     s              4  0
     d sohel1_aarr     s                   like(soaarr)
     d sohel1_binr     s                   like(sobinr)
     d sohel1_numm     s                   like(sonumm)
     d sohel1_suff     s                   like(sosuff)
     d sstal7_paar     s                   like(sfpaar)
     d sstal7_vkun     s                   like(sfvkun)
     d sstal7_binr     s                   like(sfbinr)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
      *
      * Definering av øvrige variable
      *
     d w_atel          s              1  0
     d w_blin          s              4  0
     d w_blin_a        s              6
     d w_lenh          s              3
     d w_pris          s             10  2
     d w_mvax          s              4  2
     d w_vbto          s             11  2
     d w_vnto          s             11  2
     d w_vrab          s             11  2
     d w_vrpw          s             15  5
     d w_vrpr          s              4  2
     d w_rect          s              4
     d w_anta          s              9  2
     d w_antfl         s              5  0
     d w_mdat_iso      s               d   datfmt(*iso)
     d w_mdat_alf      s              8
     d w_mdat_aar      s              4
     d w_ltyp          s              2
6.nu d w_binr          s              8
6.nu d w_numm          s              8
     d w_firm          s              3  0
     d w_kpro          s              6
     d w_posi          s              2  0
      *
     d                 ds
     d w_linr                         6  0
     d   w_lin1                       2  0 overlay(w_linr:1)
     d   w_lin2                       4  0 overlay(w_linr:3)
      *
     d w_recx          s              4  0
     d w_posx          s           1042
     d w_abel          s             11  2
     d w_abnu          s             11  2
     d w_kntr          s             11  0
     d w_recno         s              4  0
     d w_xstr          s             35
     d w_xavd          s              5
     d w_nkar          s              2  0
     d w_nsta          s              2  0
     d w_mpos          s              2  0
     d w_mail          s             15
      *
     d b_fakt          s              1    inz(*off)
     d b_sjek          s              1    inz(*off)
     d b_fhod          s              1    inz(*off)
     d b_flin          s              1    inz(*off)
     d b_ftot          s              1    inz(*off)
     d b_numm          s              1    inz(*off)
     d b_fref          s              1    inz(*off)
     d b_fprt          s              1    inz(*off)
     d b_ftra          s              1    inz(*off)
      *
     d c_digi          c                   '0123456789 '
     d c_char          c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅ ab+
     d                                      cdefghijklmnopqrstuvwxyzæøå.,/:-'
      *
      *  Definering av datastrukturer for melding inn
      *
      * FAKTURA
      *
     d                 ds
     d f_ed00                        80
     d  f_id00                        4    overlay(f_ed00)
     d  f_decs00                      1    overlay(f_ed00:5)
     d  f_send00                     13    overlay(f_ed00:6)
     d  f_mott00                     13    overlay(f_ed00:19)
     d  f_gdat00                      6    overlay(f_ed00:32)
     d  f_gtim00                      4    overlay(f_ed00:38)
     d  f_test00                      1    overlay(f_ed00:39)
      *
     d                 ds
     d f_ed01                        80
     d  f_id01                        4    overlay(f_ed01)
     d  f_mref01                     14    overlay(f_ed01:5)
     d  f_mtyp01                      6    overlay(f_ed01:19)
     d  f_mver01                      3    overlay(f_ed01:25)
     d  f_mrel01                      3    overlay(f_ed01:28)
     d  f_mkon01                      2    overlay(f_ed01:31)
     d  f_mfor01                      6    overlay(f_ed01:33)
     d  f_mkod01                      3    overlay(f_ed01:39)
     d  f_fanr01                     35    overlay(f_ed01:42)
     d  f_mfun01                      3    overlay(f_ed01:77)
      *
     d                 ds
     d f_ed02                        80
     d  f_id02                        4    overlay(f_ed02)
     d  f_kva102                      3    overlay(f_ed02:5)
     d  f_mdat02                      8    overlay(f_ed02:8)
     d  f_for102                      3    overlay(f_ed02:16)
     d  f_kva202                      3    overlay(f_ed02:19)
     d  f_levd02                      8    overlay(f_ed02:22)
     d  f_for202                      3    overlay(f_ed02:30)
      *
     d                 ds
     d f_ed03                        80
     d  f_id03                        4    overlay(f_ed03)
     d  f_ftxq03                      3    overlay(f_ed03:5)
     d  f_ftxt03                     70    overlay(f_ed03:8)
      *
     d                 ds
     d f_ed04                        80
     d  f_id04                        4    overlay(f_ed04)
     d  f_rkv104                      3    overlay(f_ed04:5)
     d  f_repa04                     35    overlay(f_ed04:8)
      *
     d                 ds
     d f_ed05                        80
     d  f_id05                        4    overlay(f_ed05)
     d  f_pkva05                      3    overlay(f_ed05:5)
     d  f_peana05                    13    overlay(f_ed05:8)
     d    f_pean05                   13  0 overlay(f_peana05:1)
     d  f_porg05                      3    overlay(f_ed05:21)
     d  f_pnav05                     30    overlay(f_ed05:24)
      *
     d                 ds
     d f_ed06                        80
     d  f_id06                        4    overlay(f_ed06)
     d  f_pad106                     30    overlay(f_ed06:5)
     d  f_pad206                     30    overlay(f_ed06:35)
      *
     d                 ds
     d f_ed07                        80
     d  f_id07                        4    overlay(f_ed07)
     d  f_ppon07                      4    overlay(f_ed07:5)
     d  f_ppst07                     30    overlay(f_ed07:9)
     d  f_lkod07                      3    overlay(f_ed07:39)
      *
     d                 ds
     d f_ed08                        80
     d  f_id08                        4    overlay(f_ed08)
     d  f_rkva08                      3    overlay(f_ed08:5)
     d  f_rref08                     35    overlay(f_ed08:8)
      *
     d                 ds
     d f_ed09                        80
     d  f_id09                        4    overlay(f_ed09)
     d  f_kfun09                      3    overlay(f_ed09:5)
     d  f_kkon09                     35    overlay(f_ed09:8)
      *
     d                 ds
     d f_ed10                        80
     d  f_id10                        4    overlay(f_ed10)
     d  f_vadk10                      3    overlay(f_ed10:5)
     d  f_vako10                      3    overlay(f_ed10:8)
     d  f_vakv10                      3    overlay(f_ed10:11)
     d  f_fdko10                      3    overlay(f_ed10:14)
     d  f_fdkv10                      3    overlay(f_ed10:17)
     d  f_fdat10                      8    overlay(f_ed10:20)
     d  f_fdfo10                      3    overlay(f_ed10:28)
     d  f_ffko10                      3    overlay(f_ed10:31)
     d  f_ffkv10                      3    overlay(f_ed10:34)
     d  f_ffat10                      8    overlay(f_ed10:37)
     d  f_fffo10                      3    overlay(f_ed10:45)
      *
     d                 ds
     d f_ed11                        80
     d  f_id11                        4    overlay(f_ed11)
     d  f_trkv11                      3    overlay(f_ed11:5)
     d  f_trmo11                      3    overlay(f_ed11:8)
     d  f_trna11                     35    overlay(f_ed11:11)
     d  f_leko11                      3    overlay(f_ed11:46)
     d  f_lebe11                      3    overlay(f_ed11:49)
     d  f_lekv11                      3    overlay(f_ed11:52)
      *
     d                 ds
     d f_ed12                        80
     d  f_id12                        4    overlay(f_ed12)
     d  f_rgkv12                      3    overlay(f_ed12:5)
     d  f_rgko12                      3    overlay(f_ed12:8)
     d  f_rgbe12                     35    overlay(f_ed12:11)
     d  f_beko12                      3    overlay(f_ed12:46)
     d  f_belp12                     18    overlay(f_ed12:49)
     d    f_belh12                   15  0 overlay(f_belp12:1)
     d    f_beld12                    2  2 overlay(f_belp12:17)
     d  f_takv12                      3    overlay(f_ed12:67)
     d  f_tako12                      3    overlay(f_ed12:70)
     d  f_tapr12                      6    overlay(f_ed12:73)
     d    f_taph12                    3  0 overlay(f_tapr12:1)
     d    f_tapd12                    2  2 overlay(f_tapr12:5)
     d  f_tafr12                      2    overlay(f_ed12:79)
      *
     d                 ds
     d f_ed13                        80
     d  f_id13                        4    overlay(f_ed13)
     d  f_vlina13                     6    overlay(f_ed13:5)
     d    f_vlin13                    6  0 overlay(f_vlina13:1)
     d  f_veana13                    13    overlay(f_ed13:11)
     d    f_vean13                   13  0 overlay(f_veana13:1)
     d  f_eank13                      3    overlay(f_ed13:24)
     d  f_piq113                      3    overlay(f_ed13:27)
     d  f_nobba13                    15    overlay(f_ed13:30)
     d    f_nobb13                    8  0 overlay(f_nobba13:1)
     d    f_nobx13                    7    overlay(f_nobba13:9)
     d  f_noko13                      3    overlay(f_ed13:45)
     d  f_piq213                      3    overlay(f_ed13:48)
     d  f_lvar13                     15    overlay(f_ed13:51)
     d  f_lvko13                      3    overlay(f_ed13:65)
      *
     d                 ds
     d f_ed14                        80
     d  f_id14                        4    overlay(f_ed14)
     d  f_txko14                      3    overlay(f_ed14:5)
     d  f_vtx114                     35    overlay(f_ed14:8)
     d  f_vtx214                     35    overlay(f_ed14:43)
      *
     d                 ds
     d f_ed15                        80
     d  f_id15                        4    overlay(f_ed15)
     d  f_kvko15                      3    overlay(f_ed15:5)
     d  f_kvan15                     15    overlay(f_ed15:8)
     d    f_kvah15                   11  0 overlay(f_kvan15:1)
     d    f_kvad15                    3  3 overlay(f_kvan15:13)
     d  f_enhk15                      3    overlay(f_ed15:23)
      *!! - fortegn/antall
     d  f_kvas15                      1    overlay(f_ed15:26)
      *
     d                 ds
     d f_ed16                        80
     d  f_id16                        4    overlay(f_ed16)
     d  f_fxlq16                      3    overlay(f_ed16:5)
     d  f_ftxl16                     70    overlay(f_ed16:8)
      *
     d                 ds
     d f_ed17                        80
     d  f_id17                        4    overlay(f_ed17)
     d  f_bonu17                      3    overlay(f_ed17:5)
     d  f_bekf17                      3    overlay(f_ed17:8)
     d  f_befo17                     18    overlay(f_ed17:11)
     d    f_befh17                   15  0 overlay(f_befo17:1)
     d    f_befd17                    2  2 overlay(f_befo17:17)
     d  f_beke17                      3    overlay(f_ed17:29)
     d  f_beet17                     18    overlay(f_ed17:32)
     d    f_beeh17                   15  0 overlay(f_beet17:1)
     d    f_beed17                    2  2 overlay(f_beet17:17)
      *!! - fortegn/varelinjebel. før/etter rabatt
     d  f_befs17                      1    overlay(f_ed17:50)
     d  f_bees17                      1    overlay(f_ed17:51)
      *
     d                 ds
     d f_ed18                        80
     d  f_id18                        4    overlay(f_ed18)
     d  f_prkv18                      3    overlay(f_ed18:5)
     d  f_pris18                     15    overlay(f_ed18:8)
     d    f_prih18                   11  0 overlay(f_pris18:1)
     d    f_prid18                    3  3 overlay(f_pris18:13)
     d  f_enha18                      9    overlay(f_ed18:23)
     d    f_enhh18                    9  0 overlay(f_enha18:1)
     d  f_enhp18                      3    overlay(f_ed18:32)
      *!! - fortegn/pris
     d  f_prix18                      1    overlay(f_ed18:35)
      *
     d                 ds
     d f_ed19                        80
     d  f_id19                        4    overlay(f_ed19)
     d  f_rekv19                      3    overlay(f_ed19:5)
     d  f_refn19                     35    overlay(f_ed19:8)
     d  f_relia19                     6    overlay(f_ed19:43)
     d    f_reli19                    6  0 overlay(f_relia19:1)
      *
     d                 ds
     d f_ed20                        80
     d  f_id20                        4    overlay(f_ed20)
     d  f_mvak20                      3    overlay(f_ed20:5)
     d  f_mvty20                      3    overlay(f_ed20:8)
     d  f_mvgr20                     15    overlay(f_ed20:11)
     d  f_mvap20                      6    overlay(f_ed20:26)
     d    f_mvah20                    3  0 overlay(f_mvap20:1)
     d    f_mvad20                    2  2 overlay(f_mvap20:5)
     d  f_mvko20                      3    overlay(f_ed20:32)
      *
     d                 ds
     d f_ed21                        80
     d  f_id21                        4    overlay(f_ed21)
     d  f_sakv21                      3    overlay(f_ed21:5)
     d  f_saty21                      3    overlay(f_ed21:8)
     d  f_mvbe21                     35    overlay(f_ed21:11)
     d  f_sagr21                     15    overlay(f_ed21:46)
     d    f_sagh21                   12  0 overlay(f_sagr21:1)
     d    f_sagd21                    2  2 overlay(f_sagr21:14)
     d  f_sasa21                     15    overlay(f_ed21:61)
     d    f_sash21                   12  0 overlay(f_sasa21:1)
     d    f_sasd21                    2  2 overlay(f_sasa21:14)
     d  f_same21                      3    overlay(f_ed21:76)
      *
     d                 ds
     d f_ed22                        80
     d  f_id22                        4    overlay(f_ed22)
     d  f_sako22                      3    overlay(f_ed22:5)
     d  f_sabe22                     18    overlay(f_ed22:8)
     d    f_sabh22                   15  0 overlay(f_sabe22:1)
     d    f_sabd22                    2  2 overlay(f_sabe22:17)
      *
     d                 ds
     d f_ed23                        80
     d  f_id23                        4    overlay(f_ed23)
     d  f_ftkv23                      3    overlay(f_ed23:5)
     d  f_ftrf23                      3    overlay(f_ed23:8)
     d  f_ftty23                      3    overlay(f_ed23:11)
     d  f_fttx23                     35    overlay(f_ed23:14)
     d  f_ftmk23                     15    overlay(f_ed23:49)
     d    f_ftmh23                   11  0 overlay(f_ftmk23:1)
     d    f_ftmd23                    3  3 overlay(f_ftmk23:13)
     d  f_ftme23                      3    overlay(f_ed23:64)
      *
     d                 ds
     d f_ed24                        80
     d  f_id24                        4    overlay(f_ed24)
     d  f_ftpr24                      8    overlay(f_ed24:5)
     d    f_ftph24                    5  0 overlay(f_ftpr24:1)
     d    f_ftpd24                    2  2 overlay(f_ftpr24:7)
     d  f_ftbe24                     18    overlay(f_ed24:13)
     d    f_ftbh24                   15  0 overlay(f_ftbe24:1)
     d    f_ftbd24                    2  2 overlay(f_ftbe24:17)
     d  f_ftko24                      3    overlay(f_ed24:31)
     d  f_ftsa24                     15    overlay(f_ed24:34)
     d    f_ftsh24                   12  0 overlay(f_ftsa24:1)
     d    f_ftsd24                    2  2 overlay(f_ftsa24:14)
      *
     d                 ds
     d f_ed25                        80
     d  f_id25                        4    overlay(f_ed25)
     d  f_toty25                      3    overlay(f_ed25:5)
     d  f_tobe25                     18    overlay(f_ed25:8)
     d    f_tobh25                   15  0 overlay(f_tobe25:1)
     d    f_tobd25                    2  2 overlay(f_tobe25:17)
      *!! - fortegn/tot.beløp
     d  f_tobs25                      1    overlay(f_ed25:26)
      *
     d                 ds
     d f_ed26                        80
     d  f_id26                        4    overlay(f_ed26)
     d  f_avkv26                      3    overlay(f_ed26:5)
     d  f_avty26                      3    overlay(f_ed26:8)
     d  f_avgr26                     18    overlay(f_ed26:11)
     d    f_avgh26                   15  0 overlay(f_avgr26:1)
     d    f_avgd26                    2  2 overlay(f_avgr26:17)
     d  f_avsa26                      6    overlay(f_ed26:29)
     d    f_avsh26                    3  0 overlay(f_avsa26:1)
     d    f_avsd26                    2  2 overlay(f_avsa26:5)
     d  f_avbk26                      3    overlay(f_ed26:35)
     d  f_avbe26                     18    overlay(f_ed26:38)
     d    f_avbh26                   15  0 overlay(f_avbe26:1)
     d    f_avbd26                    2  2 overlay(f_avbe26:17)
      *!! - fortegn/tot.beløp
     d  f_avgs26                      1    overlay(f_ed26:56)
     d  f_avbs26                      1    overlay(f_ed26:57)
      *
     d                 ds
     d f_ed27                        80
     d  f_id27                        4    overlay(f_ed27)
     d  f_tokv27                      3    overlay(f_ed27:5)
     d  f_toty27                      3    overlay(f_ed27:8)
     d  f_totx27                     35    overlay(f_ed27:11)
     d  f_tobe27                     18    overlay(f_ed27:46)
     d    f_tobh27                   15  0 overlay(f_tobe27:1)
     d    f_tobd27                    2  2 overlay(f_tobe27:17)
      *
      * Definering av outputrecord for EFO/NELFO
      * - Poster bygges i programmet (dynamisk recordlengde)
      *
      * - Fakturahode     (posttype FH, max. recordlengde 1042 ch.)
      * - Fakturalinje    (posttype FL, max. recordlengde 268 ch.)
      * - Faktura fritekst(posttype FT, max. recordlengde  32 ch.)
      * - Faktura avslutn.(posttype FA, max. recordlengde  23 ch.)
      *
     d d_hrec          s           1042
     d d_lrec          s            268
     d d_trec          s             32
     d d_arec          s             23
      *
      * Definering av arbeidsfelt ihht meldingsdefinisjon
      *
      * - Fakturahode
      *
     d                 ds
     d w_fakh                      1042
     d  w_fhcd                        2    overlay(w_fakh)
     d  w_form                        8    overlay(w_fakh:3)
     d  w_vers                        3    overlay(w_fakh:11)
     d  w_seid                       14    overlay(w_fakh:14)
     d  w_kjid                       14    overlay(w_fakh:28)
     d  w_lonu                       10    overlay(w_fakh:42)
     d  w_lknr                       10    overlay(w_fakh:52)
     d  w_ornm                       10    overlay(w_fakh:62)
     d  w_kavd                       10    overlay(w_fakh:72)
     d  w_proj                       10    overlay(w_fakh:82)
     d  w_sord                       10    overlay(w_fakh:92)
     d  w_klag                       10    overlay(w_fakh:102)
     d  w_eref                       10    overlay(w_fakh:112)
     d  w_kref                       25    overlay(w_fakh:122)
     d  w_sref                       25    overlay(w_fakh:147)
     d  w_mrkt                       25    overlay(w_fakh:172)
     d  w_best                        2    overlay(w_fakh:197)
     d  w_leid                       10    overlay(w_fakh:199)
     d  w_paks                        8    overlay(w_fakh:209)
     d  w_fanr                        8    overlay(w_fakh:217)
     d  w_sign                        1    overlay(w_fakh:225)
     d  w_delf                        1    overlay(w_fakh:226)
     d  w_fada                        8    overlay(w_fakh:227)
     d  w_foda                        8    overlay(w_fakh:235)
     d  w_fumv                       11    overlay(w_fakh:243)
     d  w_fmva                       11    overlay(w_fakh:254)
     d  w_avru                       11    overlay(w_fakh:265)
     d  w_avrs                        1    overlay(w_fakh:276)
     d  w_ftot                       11    overlay(w_fakh:277)
     d  w_kidn                       27    overlay(w_fakh:288)
     d  w_kont                       11    overlay(w_fakh:315)
     d  w_lfir                       35    overlay(w_fakh:326)
     d  w_lad1                       35    overlay(w_fakh:361)
     d  w_lad2                       35    overlay(w_fakh:396)
     d  w_lpon                        9    overlay(w_fakh:431)
     d  w_lpos                       35    overlay(w_fakh:440)
     d  w_llan                        2    overlay(w_fakh:475)
     d  w_kfir                       35    overlay(w_fakh:477)
     d  w_kad1                       35    overlay(w_fakh:512)
     d  w_kad2                       35    overlay(w_fakh:547)
     d  w_kpon                        9    overlay(w_fakh:582)
     d  w_kpos                       35    overlay(w_fakh:591)
     d  w_klan                        2    overlay(w_fakh:626)
     d  w_ktlf                       15    overlay(w_fakh:628)
     d  w_kfax                       15    overlay(w_fakh:643)
     d  w_kepo                       40    overlay(w_fakh:658)
     d  w_kweb                       40    overlay(w_fakh:698)
     d  w_ffir                       35    overlay(w_fakh:738)
     d  w_fad1                       35    overlay(w_fakh:773)
     d  w_fad2                       35    overlay(w_fakh:808)
     d  w_fpon                        9    overlay(w_fakh:843)
     d  w_fpos                       35    overlay(w_fakh:852)
     d  w_flan                        2    overlay(w_fakh:887)
     d  w_sfir                       35    overlay(w_fakh:889)
     d  w_sad1                       35    overlay(w_fakh:924)
     d  w_sad2                       35    overlay(w_fakh:959)
     d  w_spon                        9    overlay(w_fakh:994)
     d  w_spos                       35    overlay(w_fakh:1003)
     d  w_slan                        2    overlay(w_fakh:1038)
     d  w_valk                        3    overlay(w_fakh:1040)
      *
      * - Fakturalinje
      *
     d                 ds
     d w_fakl                       268
     d  w_flcd                        2    overlay(w_fakl)
     d  w_lfak                        8    overlay(w_fakl:3)
     d  w_linf                        4    overlay(w_fakl:11)
     d  w_linb                        4    overlay(w_fakl:15)
     d  w_vmrx                        1    overlay(w_fakl:19)
     d  w_vnum                       14    overlay(w_fakl:20)
     d  w_vlev                        9    overlay(w_fakl:34)
     d  w_pren                        1    overlay(w_fakl:43)
     d  w_enhp                       10    overlay(w_fakl:44)
     d  w_mvap                        4    overlay(w_fakl:54)
     d  w_rbp1                        4    overlay(w_fakl:58)
     d  w_rbe1                       11    overlay(w_fakl:62)
     d  w_rbp2                        4    overlay(w_fakl:73)
     d  w_rbe2                       11    overlay(w_fakl:77)
     d  w_tlgp                        4    overlay(w_fakl:88)
     d  w_tlgb                       11    overlay(w_fakl:92)
     d  w_avgp                        4    overlay(w_fakl:103)
     d  w_avgb                       11    overlay(w_fakl:107)
     d  w_belp                       11    overlay(w_fakl:118)
     d  w_kvar                       25    overlay(w_fakl:129)
     d  w_vabe                       30    overlay(w_fakl:154)
     d  w_tkbe                       25    overlay(w_fakl:184)
     d  w_vsig                        1    overlay(w_fakl:209)
     d  w_klrf                       25    overlay(w_fakl:210)
     d  w_vpak                        8    overlay(w_fakl:235)
     d  w_slag                       10    overlay(w_fakl:243)
     d  w_vkon                       16    overlay(w_fakl:253)
      *
      * - Faktura tekstlinje
      *
     d                 ds
     d w_fakt                        32
     d  w_ftcd                        2    overlay(w_fakt)
     d  w_txtl                       30    overlay(w_fakt:3)
      *
      * - Faktura avslutningslinje
      *
     d                 ds
     d w_faka                        23
     d  w_facd                        2    overlay(w_faka)
     d  w_afak                        8    overlay(w_faka:3)
     d  w_dats                        8    overlay(w_faka:11)
     d  w_antp                        5    overlay(w_faka:19)
      *
      * Meldingskonstanter
      *
     d c_fsep          c                   ';'
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hoved program styrerutine - les av innposter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   read      fnaxpf
      *
     c                   dow       not %eof
     c                   eval      w_rect = %subst(fnaxln:1:4)
      *
     c                   if        w_rect = 'ED01'
     c                   if        b_ftot = *on
     c                   exsr      skriv_tfakt
     c                   endif
     c                   exsr      init_arbf
     c                   endif
      *
     c                   if        b_fhod = *on
     c                   if        b_flin = *on
     c                   exsr      skriv_hfakt
     c                   endif
     c                   endif
      *
     c                   select
     c                   when      w_rect = 'ED00'
     c                   exsr      ed00
     c                   when      w_rect = 'ED01'
     c                   exsr      ed01
     c                   when      w_rect = 'ED02'
     c                   exsr      ed02
     c                   when      w_rect = 'ED03'
     c                   exsr      ed03
     c                   when      w_rect = 'ED04'
     c                   exsr      ed04
     c                   when      w_rect = 'ED05'
     c                   exsr      ed05
     c                   when      w_rect = 'ED06'
     c                   exsr      ed06
     c                   when      w_rect = 'ED07'
     c                   exsr      ed07
     c                   when      w_rect = 'ED08'
     c                   exsr      ed08
     c                   when      w_rect = 'ED09'
     c                   exsr      ed09
     c                   when      w_rect = 'ED10'
     c                   exsr      ed10
     c                   when      w_rect = 'ED11'
     c                   exsr      ed11
     c                   when      w_rect = 'ED12'
     c                   exsr      ed12
     c                   when      w_rect = 'ED13'
     c                   exsr      ed13
     c                   when      w_rect = 'ED14'
     c                   exsr      ed14
     c                   when      w_rect = 'ED15'
     c                   exsr      ed15
     c                   when      w_rect = 'ED16'
     c                   exsr      ed16
     c                   when      w_rect = 'ED17'
     c                   exsr      ed17
     c                   when      w_rect = 'ED18'
     c                   exsr      ed18
     c                   when      w_rect = 'ED19'
     c                   exsr      ed19
     c                   when      w_rect = 'ED20'
     c                   exsr      ed20
     c                   when      w_rect = 'ED21'
     c                   exsr      ed21
     c                   when      w_rect = 'ED22'
     c                   exsr      ed22
     c                   when      w_rect = 'ED23'
     c                   exsr      ed23
     c                   when      w_rect = 'ED24'
     c                   exsr      ed24
     c                   when      w_rect = 'ED25'
     c                   exsr      ed25
     c                   when      w_rect = 'ED26'
     c                   exsr      ed26
     c                   when      w_rect = 'ED27'
     c                   exsr      ed27
     c                   endsl
      *
     c                   read      fnaxpf
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   if        b_ftot = *on
     c                   exsr      skriv_tfakt
     c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  ED00 - Meldingsinit.post
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed00          begsr
      *
     c                   eval      f_ed00 = fnaxln
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  ED01 - Meldingshode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed01          begsr
      *
     c                   clear                   w_fakh
     c                   eval      w_recno = 0
     c                   eval      w_antfl = 0
      *
     c                   eval      w_fhcd = 'FH'
     c                   eval      w_form = 'EFONELFO'
     c                   eval      w_vers = '3.0'
      *
     c                   eval      f_ed01 = fnaxln
      *
     c                   if        f_mkod01 = '381'
     c                   eval      w_sign = '-'
     c                   else
     c                   eval      w_sign = '+'
     c                   endif
      *
     c                   eval      b_fhod = *on
     c                   eval      b_numm = *off
     c                   eval      w_fanr = %trim(f_fanr01)
6.nu c                   eval      w_binr = %subst(w_fanr:1:8)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  ED02 - Datoinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed02          begsr
      *
     c                   eval      f_ed02 = fnaxln
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  ED03 - Fritekstinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed03          begsr
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  ED04 - Referanserinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed04          begsr
      *
     c                   eval      f_ed04 = fnaxln
      *
      *  Referansekvalifikator (f_rkv104)
      *  --------------------------------------------------------------
      *  ON  = Bestillingsnr. (kjøpers)
      *  VN  = Ordrenummer (selgers)
      *  IV  = Ref. til fakturanr. som skal krediteres
      *  AAK = Pakkseddelnr.
      *  CR  = Kundereferansenr.
      *  AEP = Prosjektnr.
      *  ZSU = Fakt.nr. fra lev. ved viderefakt.
      *  ZIV = Lev. orig. fakt.nr. ved viderekreditering
      *  PL  = Prislistenr.
      *  CT  = Kontraktsnr.
      *  PQ  = KID nr.
      *
     c                   eval      b_fref = *on
      *
     c                   if        f_rkv104 = 'ON '
     c                   eval      b_numm = *on
     c                   if        %check(c_digi : %trim(f_repa04)) > 0
     c                   eval      w_lonu = *blank
     c                   else
     c                   eval      w_lonu = 'O' + %trim(f_repa04)
     c                   endif
     c                   endif
      *
     c                   if        f_rkv104 = 'CT '
     c                   eval      w_lknr = %trim(f_repa04)
     c                   eval      w_kavd = %trim(f_repa04)
     c                   endif
      *
     c                   if        f_rkv104 = 'VN '
     c                   eval      w_sord = %trim(f_repa04)
6.nu c                   eval      w_numm = %subst(w_sord:1:8)
     c*                  eval      w_kref = 'O' + %trim(f_repa04)
     c                   endif
      *
     c*                  if        f_rkv104 = 'AEP'
     c*                  eval      w_proj = %trim(f_repa04)
     c*                  endif
      *
     c                   if        f_rkv104 = 'PQ '
     c                   eval      w_kidn = %trim(f_repa04)
     c                   endif
      *
     c*                  if        f_rkv104 = 'CR '
     c*                  eval      w_kref = %trim(f_repa04)
     c*                  endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  ED05 - Partsinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed05          begsr
      *
      *  Partskvalifikator (f_pkva05)
      *  ----------------------------------
      *  BY  = Kjøper
      *  SU  = Selger, leverandør
      *  DL  = Leverandørens factoringselskap
      *  DP  = Mottager av varen, leveringssted
      *  IV  = Fakturamottager
      *  OB  = Bestiller
      *  CA  = Transportør
      *  SF  = Utleveringssted, hentested ved EXW
      *  II  = Fakturautsteder
      *  UD  = Sluttmottager for merking
      *  PD  = Avdelingskontor hos kjøper
      *
     c                   if        b_numm = *off
     c                   eval      b_numm = *on
     c                   endif
      *
     c                   eval      f_ed05 = fnaxln
     c     ' ':'0'       xlate     f_peana05     f_peana05
      *
     c                   if        f_pkva05 = 'DP '
     c                   eval      w_lfir = %trim(f_pnav05)
     c                   endif
      *
     c                   if        f_pkva05 = 'BY '
     c                   eval      w_kfir = %trim(f_pnav05)
     c                   endif
      *
     c                   if        f_pkva05 = 'IV '
     c                   eval      w_ffir = %trim(f_pnav05)
     c                   endif
      *
     c                   if        f_pkva05 = 'SU '
     c                   eval      w_sfir = %trim(f_pnav05)
     c                   endif
      *
     c                   eval      b_fprt = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  ED06 - Partsinformasjon adresse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed06          begsr
      *
     c                   eval      f_ed06 = fnaxln
      *
     c                   if        f_pkva05 = 'DP '
     c                   eval      w_lad1 = %trim(f_pad106)
     c                   eval      w_lad2 = %trim(f_pad206)
     c                   endif
      *
     c                   if        f_pkva05 = 'BY '
     c                   eval      w_kad1 = %trim(f_pad106)
     c                   eval      w_kad2 = %trim(f_pad206)
     c                   endif
      *
     c                   if        f_pkva05 = 'IV '
     c                   eval      w_fad1 = %trim(f_pad106)
     c                   eval      w_fad2 = %trim(f_pad206)
     c                   endif
      *
     c                   if        f_pkva05 = 'SU '
     c                   eval      w_sad1 = %trim(f_pad106)
     c                   eval      w_sad2 = %trim(f_pad206)
     c                   endif
      *
     c                   eval      b_fprt = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  ED07 - Partsinformasjon adresse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed07          begsr
      *
     c                   eval      f_ed07 = fnaxln
      *
     c                   if        f_pkva05 = 'DP '
     c                   eval      w_lpon = %trim(f_ppon07)
     c                   eval      w_lpos = %trim(f_ppst07)
     c                   endif
      *
     c                   if        f_pkva05 = 'BY '
     c                   eval      w_kpon = %trim(f_ppon07)
     c                   eval      w_kpos = %trim(f_ppst07)
     c                   endif
      *
     c                   if        f_pkva05 = 'IV '
     c                   eval      w_fpon = %trim(f_ppon07)
     c                   eval      w_fpos = %trim(f_ppst07)
     c                   endif
      *
     c                   if        f_pkva05 = 'SU '
     c                   eval      w_spon = %trim(f_ppon07)
     c                   eval      w_spos = %trim(f_ppst07)
     c                   endif
      *
     c                   eval      b_fprt = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  ED08 - Partsinformasjon referanse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed08          begsr
      *
     c                   eval      f_ed08 = fnaxln
      *
     c                   if        f_pkva05 = 'BY '
     c                   if        f_rkva08 = 'GN ' or
     c                             f_rkva08 = 'VA '
     c*                  eval      w_seid = %trim(f_rref08)
     c                   eval      w_seid = %subst(f_rref08:3:9)
     c                   endif
     c                   endif
      *
     c                   if        f_pkva05 = 'SU '
     c                   if        f_rkva08 = 'GN ' or
     c                             f_rkva08 = 'VA '
     c*                  eval      w_kjid = %trim(f_rref08)
     c                   eval      w_kjid = %subst(f_rref08:3:9)
     c                   eval      w_leid = %subst(f_rref08:3:9)
     c                   endif
     c                   endif
      *
     c                   eval      b_fprt = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  ED09 - Partsinformasjon kontaktperson
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed09          begsr
      *
     c                   eval      f_ed09 = fnaxln
      *
      * - Her skal saksbehandlernr. ligge...
      *
     c*                  if        f_kfun09 = 'PD '
     c*                  exsr      sjk_sbeh
     c*                  endif
      *
     c                   if        f_kfun09 = 'AD '
     c                   eval      w_sref = %trim(f_kkon09)
     c*                  eval      w_kref = %trim(f_kkon09)
     c                   endif
      *
     c                   if        f_kfun09 = 'PD '
     c*                  eval      w_kref = 'O' + %trim(f_kkon09)
     c                   eval      w_paks = %subst(f_kkon09:1:8)
     c                   endif
      *
     c                   eval      b_fprt = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  ED10 - Valuta/forfalls - informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed10          begsr
      *
     c                   eval      f_ed10 = fnaxln
      *
     c                   eval      w_valk = %trim(f_vako10)
     c                   eval      w_fada = %trim(f_fdat10)
     c                   eval      w_foda = %trim(f_ffat10)
      *
     c*                  if        w_kavd = *blank
     c*                  eval      w_kavd = '00000'
     c*                  endif
      *
     c                   if        w_binr <> *blank and
     c                             w_numm <> *blank
     c                   if        %check(c_digi : w_binr) > 0 or
     c                             %check(c_digi : w_numm) > 0
     c                   eval      sohel1_binr = *zero
     c                   eval      sohel1_numm = *zero
     c                   else
     c                   eval      w_firm = l_firm
     c                   move      w_binr        sohel1_binr
     c                   move      w_numm        sohel1_numm
     c                   eval      sohel1_suff = *zero
     c                   time                    w_mdat_iso
     c     *iso0         move      w_mdat_iso    w_mdat_alf
     c                   eval      w_mdat_aar = %subst(w_mdat_alf:1:4)
     c                   move      w_mdat_aar    sohel1_aarr
      *
     c     sohel1_key    chain     sohel1
     c                   if        not %found
     c                   eval      sohel1_aarr = sohel1_aarr - 1
     c     sohel1_key    chain     sohel1
     c                   endif
     c                   endif
      *
     c                   if        sorekv <> *blank
     c                   eval      w_eref = %trim(sorekv)
     c                   endif
      *
     c                   eval      w_kref = *blank
      *
     c                   if        sokpro <> *zero
     c                   eval      fkprl1_kund = sokund
     c                   eval      fkprl1_kpro = sokpro
     c     fkprl1_key    chain     fkprl1
     c                   if        %found
     c                   if        flepro <> *blank
     c                   eval      w_kref = 'O' + %trim(flepro)
     c                   endif
     c                   endif
     c                   endif
      *
     c                   if        w_kref = *blank
     c                   if        sonavn <> *blank
     c                   eval      w_posi = %scan(' ':sonavn)
     c                   if        w_posi > *zero
     c                   eval      w_kpro = %trim(%subst(sonavn:1:w_posi))
     c                   if        %check(c_digi : w_kpro) = 0
     c                   eval      w_kref = 'O' + %trim(w_kpro)
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endif
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  ED11 - Transport/leverings - informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed11          begsr
      *
     c                   eval      f_ed11 = fnaxln
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED12 - Fradrags og tilleggsposter på hodenivå
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed12          begsr
      *
     c                   eval      f_ed12 = fnaxln
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED13 - Varelinjer/vareidentifikasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed13          begsr
      *
     c                   if        b_flin = *on
     c                   exsr      skriv_lfakt
     c                   else
     c                   exsr      null_lfakt
     c                   endif
      *
     c                   clear                   w_fakl
     c                   eval      w_flcd = 'FL'
      *
     c                   eval      w_lfak = w_fanr
      *
     c                   eval      f_ed13 = fnaxln
      *
     c     ' ':'0'       xlate     f_vlina13     f_vlina13
     c                   eval      w_linr = f_vlin13
     c                   move      w_lin2        w_linf
      *
     c                   eval      w_vmrx = *blank
     c                   eval      w_vnum = *blank
      *
     c     ' ':'0'       xlate     f_nobba13     f_nobba13
     c                   if        %check(c_digi : f_nobba13) > 0
     c                   eval      f_nobb13 = 0
     c                   endif
     c                   if        f_nobb13 <> *zero
     c                   eval      w_vmrx = '1'
     c                   movel     f_nobb13      w_vnum
     c                   endif
      *
     c                   if        w_vmrx = *blank
     c                   if        f_lvar13 <> *blank
     c                   eval      w_vmrx = '3'
     c                   eval      w_vnum = %trim(f_lvar13)
     c                   endif
     c                   endif
      *
     c                   if        w_vmrx = *blank
     c     ' ':'0'       xlate     f_veana13     f_veana13
     c                   if        f_vean13 <> *zero
     c                   eval      w_vmrx = '2'
     c                   eval      w_vnum = %trim(f_veana13)
     c                   endif
     c                   endif
      *
     c                   eval      b_flin = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED14 - Varelinjer/varetekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed14          begsr
      *
     c                   eval      f_ed14 = fnaxln
      *
     c                   eval      w_vabe = %trim(f_vtx114)
      *
     c                   if        f_vtx214 <> *blank
     c                   eval      w_tkbe = %trim(f_vtx214)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED15 - Varelinjer/kvantumsinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed15          begsr
      *
     c                   eval      f_ed15 = fnaxln
     c     ' ':'0'       xlate     f_kvan15      f_kvan15
      *
     c                   if        f_kvko15 = '47 '
     c                   eval(h)   w_anta = f_kvah15 + f_kvad15
     c                   move      w_anta        w_vlev
      *
      * Enhetskoder (kodifisert) etter EFO/NELFO std.
      * 1=stk., 2=meter, 3=liter, 4=kilo, 5=lengde, 6=sett/par
      * 7=rull, 8=boks/fl., 9=pakke
      *
     c                   select
     c                   when      f_enhk15 = 'STK'
     c                   eval      w_pren = '1'
     c                   when      f_enhk15 = 'M  '
     c                   eval      w_pren = '2'
     c                   when      f_enhk15 = 'MTR'
     c                   eval      w_pren = '2'
     c                   when      f_enhk15 = 'LTR'
     c                   eval      w_pren = '3'
     c                   when      f_enhk15 = 'KG '
     c                   eval      w_pren = '4'
     c                   when      f_enhk15 = 'KGM'
     c                   eval      w_pren = '4'
     c                   when      f_enhk15 = 'LGD'
     c                   eval      w_pren = '5'
     c                   when      f_enhk15 = 'SET'
     c                   eval      w_pren = '6'
     c                   when      f_enhk15 = 'PAR'
     c                   eval      w_pren = '6'
     c                   when      f_enhk15 = 'RUL'
     c                   eval      w_pren = '7'
     c                   when      f_enhk15 = 'BOX'
     c                   eval      w_pren = '8'
     c                   when      f_enhk15 = 'FL '
     c                   eval      w_pren = '8'
     c                   when      f_enhk15 = 'PAK'
     c                   eval      w_pren = '9'
     c                   other
     c                   eval      w_pren = '0'
     c                   endsl
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED16 - Varelinjer/fritekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed16          begsr
      *
     c                   eval      f_ed16 = fnaxln
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED17 - Varelinjer/rabatt & gebyrer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed17          begsr
      *
     c                   eval      f_ed17 = fnaxln
      *
     c     ' ':'0'       xlate     f_befo17      f_befo17
     c     ' ':'0'       xlate     f_beet17      f_beet17
      *
     c                   eval(h)   w_vnto = f_beeh17 + f_beed17
     c                   move      w_vnto        w_belp
      *
     c                   eval(h)   w_vbto = f_befh17 + f_befd17
      *
     c                   eval      w_vrab = w_vbto - w_vnto
      *
     c                   if        w_vrab > *zero
     c                   eval      w_vrpw = (w_vrab / w_vbto) * 100
     c                   if        w_vrpw < 100
     c                   eval(h)   w_vrpr = w_vrpw
     c                   else
     c                   eval      w_vrpr = 99.99
     c                   endif
     c                   else
     c                   eval      w_vrpr = *zero
     c                   endif
      *
     c                   move      w_vrab        w_rbe1
     c                   move      w_vrpr        w_rbp1
      *
      *
     c*                  if        w_vnto < *zero
     c                   if        f_bees17 = '-'
     c                   eval      w_vsig = '-'
     c                   else
     c                   eval      w_vsig = '+'
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED18 - Varelinjer/priser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed18          begsr
      *
     c                   eval      f_ed18 = fnaxln
      *
     c     ' ':'0'       xlate     f_pris18      f_pris18
     c     ' ':'0'       xlate     f_enha18      f_enha18
      *
     c                   eval(h)   w_pris = f_prih18 + f_prid18
     c                   move      w_pris        w_enhp
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED19 - Varelinjer/kjøpers referanse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed19          begsr
      *
     c                   eval      f_ed19 = fnaxln
      *
     c                   if        f_rekv19 = 'ON '
     c                   evalr     w_blin_a = %trim(f_relia19)
     c                   move      w_blin_a      f_relia19
     c     ' ':'0'       xlate     f_relia19     f_relia19
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED20 - Varelinjer/mva prosent
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed20          begsr
      *
     c                   eval      f_ed20 = fnaxln
      *
     c                   if        f_mvty20 = 'VAT'
     c     ' ':'0'       xlate     f_mvap20      f_mvap20
     c                   eval      w_mvax = f_mvah20 + f_mvad20
     c                   move      w_mvax        w_mvap
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED21 - Varelinjer/avgiftsinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed21          begsr
      *
     c                   eval      f_ed21 = fnaxln
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED22 - Varelinjer/avgiftsbeløp
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed22          begsr
      *
     c                   eval      f_ed22 = fnaxln
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED23 - Varelinjer/fradrag & tilleggskoder
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed23          begsr
      *
     c                   eval      f_ed23 = fnaxln
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED24 - Varelinjer/fradrag & tilleggsbeløp / prosent
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed24          begsr
      *
     c                   eval      f_ed24 = fnaxln
      *
     c     ' ':'0'       xlate     f_ftbe24      f_ftbe24
     c     ' ':'0'       xlate     f_ftpr24      f_ftpr24
     c     ' ':'0'       xlate     f_ftsa24      f_ftsa24
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED25 - Fakturatotaler
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed25          begsr
      *
     c                   eval      f_ed25 = fnaxln
      *
     c     ' ':'0'       xlate     f_tobe25      f_tobe25
      *
     c                   if        b_flin = *on
     c                   exsr      skriv_lfakt
     c                   endif
      *
      *  Fakturatotal beløpstype (f_toty25) :
      *  ------------------------------------
      *  66  = Sum alle varelinjer før evt. tillegg/fradrag på linjenivå
      *  86  = Fakturatotal (Sluttotal hele fakt. inkl MVA)
      *  125 = Sum MVA grunnlag
      *  150 = Totalt MVA beløp for hele fakturaen
      *  203 = Sum alle varelinjer etter evt. tillegg/fradrag på linjenivå
      *  NET = Nettototal (eks. MVA)
      *  259 = Samlet tillegg for fakturaen
      *  260 = Samlet fradrag for fakturaen
      *
     c                   eval      b_ftot = *on
      *
     c                   if        f_toty25 = 'NET'
     c                   eval      fwfalu_recn = 0
     c     fwfalu_key    chain     fwfalu
      *
     c                   if        %found
     c                   eval      w_fakh = ffarec
     c                   eval      w_abnu = 0
     c                   eval      w_abel = f_tobh25 + f_tobd25
     c                   if        f_tobs25 = '-'
     c                   eval      w_sign = '-'
     c                   endif
      *
     c                   move      w_abel        w_fumv
     c                   move      w_abnu        w_avru
     c                   eval      ffarec = w_fakh
     c                   update    fwfalur
     c                   endif
     c                   endif
      *
     c                   if        f_toty25 = '150'
     c                   eval      fwfalr_recn = 0
     c     fwfalu_key    chain     fwfalu
      *
     c                   if        %found
     c                   eval      w_fakh = ffarec
     c                   eval      w_abel = f_tobh25 + f_tobd25
     c                   move      w_abel        w_fmva
     c                   eval      ffarec = w_fakh
     c                   update    fwfalur
     c                   endif
     c                   endif
      *
     c                   if        f_toty25 = '86 '
     c                   eval      fwfalr_recn = 0
     c     fwfalu_key    chain     fwfalu
      *
     c                   if        %found
     c                   eval      w_fakh = ffarec
     c                   eval      w_abel = f_tobh25 + f_tobd25
     c                   move      w_abel        w_ftot
     c                   eval      ffarec = w_fakh
     c                   update    fwfalur
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED26 - Fakturatotalavgift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed26          begsr
      *
     c                   eval      f_ed26 = fnaxln
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ED27 - Fakturatot. fradrag/tillegg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ed27          begsr
      *
     c                   eval      f_ed27 = fnaxln
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutiner for å skrive faktura hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_hfakt   begsr
      *
     c                   eval      w_kntr = 0
     c                   move      w_kntr        w_kont
      *
     c                   eval      fwfalr_recn = 0
     c                   eval      recno = 0
     c                   eval      ffarec = w_fakh
     c                   write     fwfalur
     c                   eval      w_antfl = w_antfl + 1
     c                   eval      b_fhod = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å skrive faktura linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_lfakt   begsr
      *
     c                   eval      w_recno = w_recno + 1
      *
     c                   eval      recno = w_recno
     c                   eval      ffarec = w_fakl
     c                   write     fwfalur
     c                   eval      w_antfl = w_antfl + 1
      *
     c                   eval      b_flin = *off
     c                   exsr      null_lfakt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutiner for å skrive faktura total
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_tfakt   begsr
      *
     c                   eval      w_facd = 'FA'
     c                   eval      w_afak = w_fanr
      *
     c                   time                    w_mdat_iso
     c     *iso0         move      w_mdat_iso    w_mdat_alf
     c                   move      w_mdat_alf    w_dats
      *
     c                   eval      w_antfl = w_antfl + 1
     c                   move      w_antfl       w_antp
      *
     c                   eval      w_recno = w_recno + 1
      *
     c                   eval      recno = w_recno
     c                   eval      ffarec = w_faka
     c                   write     fwfalur
      *
     c                   eval      b_ftot = *off
      *
     c                   exsr      skr_arbf
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å nullstille faktura hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     null_hfakt    begsr
      *
     c                   clear                   f_ed00
     c                   clear                   f_ed01
     c                   clear                   f_ed02
     c                   clear                   f_ed03
     c                   clear                   f_ed04
     c                   clear                   f_ed05
     c                   clear                   f_ed06
     c                   clear                   f_ed07
     c                   clear                   f_ed08
     c                   clear                   f_ed09
     c                   clear                   f_ed10
     c                   clear                   f_ed11
     c                   clear                   f_ed12
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å nullstille faktura linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     null_lfakt    begsr
      *
     c                   clear                   f_ed13
     c                   clear                   f_ed14
     c                   clear                   f_ed15
     c                   clear                   f_ed16
     c                   clear                   f_ed17
     c                   clear                   f_ed18
     c                   clear                   f_ed19
     c                   clear                   f_ed20
     c                   clear                   f_ed21
     c                   clear                   f_ed22
     c                   clear                   f_ed23
     c                   clear                   f_ed24
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å nullstille faktura total
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     null_tfakt    begsr
      *
     c                   clear                   f_ed25
     c                   clear                   f_ed26
     c                   clear                   f_ed27
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å init. av arbeidsfil
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     init_arbf     begsr
      *
     c                   eval      fwfalu_recn = 0
     c     fwfalu_key    setll     fwfalu
     c                   read      fwfalu
     c                   dow       not %eof
     c                   eval      w_recx = recno
     c                   eval      w_posx = ffarec
     c                   delete    fwfalu
     c                   read      fwfalu
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å lesing fra arb.fil og skriv til EFO/NELFO-fil
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_arbf      begsr
      *
     c                   eval      fwfalu_recn = 0
     c     fwfalu_key    setll     fwfalu
     c                   read      fwfalu
     c                   dow       not %eof
     c                   eval      w_recx = recno
     c                   eval      w_ltyp = %subst(ffarec:1:2)
      *
     c                   select
     c                   when      w_ltyp = 'FH'
     c                   clear                   w_fakh
     c*                  move      ffarec        w_fakh
     c                   eval      w_fakh = %trim(ffarec)
     c                   exsr      bygg_fakh
     c                   when      w_ltyp = 'FL'
     c                   clear                   w_fakl
     c*                  move      ffarec        w_fakl
     c                   eval      w_fakl = %trim(ffarec)
     c                   exsr      bygg_fakl
     c                   when      w_ltyp = 'FA'
     c                   clear                   w_faka
     c*                  move      ffarec        w_faka
     c                   eval      w_faka = %trim(ffarec)
     c                   exsr      bygg_faka
     c                   endsl
      *
     c                   write     fnaupfr
      *
     c                   read      fwfalu
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Bygg outputrecord - fakturahode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bygg_fakh     begsr
      *
     c                   eval      fnauln = w_fhcd + c_fsep +
     c                                      w_form + c_fsep +
     c                                      w_vers + c_fsep +
     c                                      %trim(w_seid) + c_fsep +
     c                                      %trim(w_kjid) + c_fsep +
     c                                      %trim(w_lonu) + c_fsep +
     c                                      %trim(w_lknr) + c_fsep +
     c                                      %trim(w_ornm) + c_fsep +
     c                                      %trim(w_kavd) + c_fsep +
     c                                      %trim(w_proj) + c_fsep +
     c                                      %trim(w_sord) + c_fsep +
     c                                      %trim(w_klag) + c_fsep +
     c                                      %trim(w_eref) + c_fsep +
     c                                      %trim(w_kref) + c_fsep +
     c                                      %trim(w_sref) + c_fsep +
     c                                      %trim(w_mrkt) + c_fsep +
     c                                      %trim(w_best) + c_fsep +
     c                                      %trim(w_leid) + c_fsep +
     c                                      %trim(w_paks) + c_fsep +
     c                                      %trim(w_fanr) + c_fsep +
     c                                      %trim(w_sign) + c_fsep +
     c                                      %trim(w_delf) + c_fsep +
     c                                      %trim(w_fada) + c_fsep +
     c                                      %trim(w_foda) + c_fsep +
     c                                      %trim(w_fumv) + c_fsep +
     c                                      %trim(w_fmva) + c_fsep +
     c                                      %trim(w_avru) + c_fsep +
     c                                      %trim(w_avrs) + c_fsep +
     c                                      %trim(w_ftot) + c_fsep +
     c                                      %trim(w_kidn) + c_fsep +
     c                                      %trim(w_kont) + c_fsep +
     c                                      %trim(w_lfir) + c_fsep +
     c                                      %trim(w_lad1) + c_fsep +
     c                                      %trim(w_lad2) + c_fsep +
     c                                      %trim(w_lpon) + c_fsep +
     c                                      %trim(w_lpos) + c_fsep +
     c                                      %trim(w_llan) + c_fsep +
     c                                      %trim(w_kfir) + c_fsep +
     c                                      %trim(w_kad1) + c_fsep +
     c                                      %trim(w_kad2) + c_fsep +
     c                                      %trim(w_kpon) + c_fsep +
     c                                      %trim(w_kpos) + c_fsep +
     c                                      %trim(w_klan) + c_fsep +
     c                                      %trim(w_ktlf) + c_fsep +
     c                                      %trim(w_kfax) + c_fsep +
     c                                      %trim(w_kepo) + c_fsep +
     c                                      %trim(w_kweb) + c_fsep +
     c                                      %trim(w_ffir) + c_fsep +
     c                                      %trim(w_fad1) + c_fsep +
     c                                      %trim(w_fad2) + c_fsep +
     c                                      %trim(w_fpon) + c_fsep +
     c                                      %trim(w_fpos) + c_fsep +
     c                                      %trim(w_flan) + c_fsep +
     c                                      %trim(w_sfir) + c_fsep +
     c                                      %trim(w_sad1) + c_fsep +
     c                                      %trim(w_sad2) + c_fsep +
     c                                      %trim(w_spon) + c_fsep +
     c                                      %trim(w_spos) + c_fsep +
     c                                      %trim(w_slan) + c_fsep +
     c                                      %trim(w_valk)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Bygg outputrecord - fakturalinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bygg_fakl     begsr
      *
     c                   eval      fnauln = w_flcd + c_fsep +
     c                                      %trim(w_lfak) + c_fsep +
     c                                      %trim(w_linf) + c_fsep +
     c                                      %trim(w_linb) + c_fsep +
     c                                      %trim(w_vmrx) + c_fsep +
     c                                      %trim(w_vnum) + c_fsep +
     c                                      %trim(w_vlev) + c_fsep +
     c                                      %trim(w_pren) + c_fsep +
     c                                      %trim(w_enhp) + c_fsep +
     c                                      %trim(w_mvap) + c_fsep +
     c                                      %trim(w_rbp1) + c_fsep +
     c                                      %trim(w_rbe1) + c_fsep +
     c                                      %trim(w_rbp2) + c_fsep +
     c                                      %trim(w_rbe2) + c_fsep +
     c                                      %trim(w_tlgp) + c_fsep +
     c                                      %trim(w_tlgb) + c_fsep +
     c                                      %trim(w_avgp) + c_fsep +
     c                                      %trim(w_avgb) + c_fsep +
     c                                      %trim(w_belp) + c_fsep +
     c                                      %trim(w_kvar) + c_fsep +
     c                                      %trim(w_vabe) + c_fsep +
     c                                      %trim(w_tkbe) + c_fsep +
     c                                      %trim(w_vsig) + c_fsep +
     c                                      %trim(w_klrf) + c_fsep +
     c                                      %trim(w_vpak) + c_fsep +
     c                                      %trim(w_slag) + c_fsep +
     c                                      %trim(w_vkon)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Bygg outputrecord - fakturaavslutningslinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bygg_faka     begsr
      *
     c                   eval      fnauln = w_facd + c_fsep +
     c                                      %trim(w_afak) + c_fsep +
     c                                      %trim(w_dats) + c_fsep +
     c                                      %trim(w_antp)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekk saksbehandlernr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_sbeh      begsr
      *
     c                   eval      w_kavd = *blank
      *
     c                   eval      w_xstr = %trim(f_kkon09)
     c                   eval      w_nkar = %len(%trim(f_kkon09))
      *
     c                   if        %check(c_digi : w_xstr) > 0 or
     c                             w_nkar = *zero
     c                   eval      w_kavd = '00000'
     c                   leavesr
     c                   endif
      *
     c                   if        w_nkar = 5
     c                   eval      w_kavd = %subst(w_xstr:1:5)
     c                   endif
      *
     c                   if        w_nkar > 5
     c                   eval      w_nsta = (w_nkar - 5) + 1
     c                   eval      w_kavd = %subst(w_xstr:w_nsta:5)
     c                   endif
      *
     c                   if        w_nkar < 5
     c                   eval      w_kavd = '00000'
     c                   eval      w_nsta = (5 - w_nkar) + 1
     c                   eval      %subst(w_kavd:w_nsta:w_nkar) =
     c                             %subst(w_xstr:1:w_nkar)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekk statistikkfil for prosjektnr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_stat      begsr
      *
     c                   eval      sstal7_paar = soaarr
     c                   eval      sstal7_vkun = sokund
     c                   eval      sstal7_binr = sobinr
      *
     c     sstal7_key    setll     sstal7
     c     sstal7_key    reade     sstal7
     c                   if        not %eof
     c                   if        sfkpro <> *zero
     c                   eval      fkprl1_kund = sfvkun
     c                   eval      fkprl1_kpro = sfkpro
     c     fkprl1_key    chain     fkprl1
     c                   if        %found
     c                   if        flmail = *blank
     c                   eval      w_kavd = 'UKJENT'
     c                   endif
     c                   if        flmail <> *blank
     c                   eval      flmail = %xlate(Lo:Up:flmail)
     c                   eval      w_mpos = %scan('@':flmail)
     c                   if        w_mpos > 1
     c                   eval      w_mpos = w_mpos - 1
     c                   eval      w_mail = %trim(%subst(flmail:1:w_mpos))
     c*                  eval      w_kref = %trim(w_kref) + ' ' +
     c*                                     %trim(w_mail)
     c                   eval      w_kavd = %trim(w_mail)
     c                   else
     c                   eval      w_kavd = %trim(%subst(flmail:1:10))
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av arbeidsfil
      *
     c     fwfalr_key    klist
     c                   kfld                    fwfalr_recn
      *
      *  Key for oppd. av arbeidsfil
      *
     c     fwfalu_key    klist
     c                   kfld                    fwfalu_recn
      *
      * Key for Heading-file
      *
     c     sohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sohel1_aarr
     c                   kfld                    sohel1_binr
     c                   kfld                    sohel1_numm
     c                   kfld                    sohel1_suff
      *
      * Key for posisjonering på fakturanummer
      *
     c     sstal7_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sstal7_paar
     c                   kfld                    sstal7_vkun
     c                   kfld                    sstal7_binr
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
     c                   endsr
