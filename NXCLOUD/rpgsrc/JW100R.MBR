     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JW100R
      * Beskrivelse . . : Vare-pakning, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       180899 HKO  Nytt program
      *       280100 HKO  Rettet heng ved avslutt uten forpakninger
      *       260803 HKO  Endret logikk for valg av rekkefølge på enheter,
      *                   og utvidet med valg av innkjøpsenhet og prisbok-
      *                   enhet
      *       020605 HKO  Lagt inn PSE
      *       090805 HKO  Lagt inn krav om at prisbokenhet også må være
      *                   valgt som salgenhet
      *       110805 HKO  Oppdaterer ikke dersom det ikke er blitt trykket
      *                   endter
      *       130905 HKO  Lagt inn DUNN-nr, sammensatt av enhet, antall i
      *                   pakning og opprettet dato
      *       160905 HKO  Oppdaterer søketekst
      *       290208 bof  utvidet forpakning med modulnr
6.10  *       260809 bof  Endringer vedr. GTIN og fjerning av DUNN
6.11  *       100909 bof  Utvidet med pakningsklasse
6.12  *       280909 bof  en del felter fjernet fra JVPKPF til JVPPPF
6.20  *       310511 bof  Lagt på skrivebeskyttelse på utvalgte felter ihht.
6.20  *                   AVALPF
6.21  *       301214 bkv  Lagt på status, U, hvis pakning utgått
6.30  *       130214 bof  Utvidet med div. felter ihht. xml 8.0
6.31  *       250415 bof  Åpnet for endring av gtin-felt
6.32  *       220415 bof  Ved oppretting og kopiering må man få overstyre prisgiver
6.33  *       010816 bof  Hente riktig enhet også for modul
6.34  *       080317 nho  Ikke test på EAN nr dersom leverandør
      *                   9XXXXX
7.xx  * 7.00  060616 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvpklr    if   e           k disk    rename(jvpkpfr:jvpklrr)
     fjvpklu    uf a e           k disk    rename(jvpkpfr:jvpklur)
     fjvpkl1    if   e           k disk    rename(jvpkpfr:jvpkl1r)
6.31 fjvpkl3    if   e           k disk    rename(jvpkpfr:jvpkl3r)
     fjforl5    if   e           k disk    rename(jforpfr:jforl5r)
     fjforl1    if   e           k disk    rename(jforpfr:jforl1r)
     fjforlu    uf a e           k disk    rename(jforpfr:jforlur)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
6.30 fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fvenhl1    if   e           k disk    rename(venhpfr:venhl1r)
6.20 favall1    if   e           k disk    rename(avalpfr:avall1r)
      *
     fjw100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(jzfirm)
     d p_vare          s                   like(jwvare)
6.30 d p_ldor          s                   like(jwldor)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
6.32 d jvpklr_ldor     s                   like(jwldor)
     d jvpklr_pakn     s                   like(jwpakn)
     d jvpklu_pakn     s                   like(jwpakn)
     d jvpkl1_pakn     s                   like(jwpakn)
6.31 d jvpkl3_gtin     s                   like(jwgtin)
     d jforl5_vare     s                   like(jzvare)
     d jforl1_ogrp     s                   like(jzogrp)
     d jforl1_hgrp     s                   like(jzhgrp)
     d jforl1_ugrp     s                   like(jzugrp)
     d jforl1_ldor     s                   like(jzldor)
     d jforl1_modn     s                   like(jzmodn)
     d jforl1_vare     s                   like(jzvare)
     d jforlu_ogrp     s                   like(jzogrp)
     d jforlu_hgrp     s                   like(jzhgrp)
     d jforlu_ugrp     s                   like(jzugrp)
     d jforlu_ldor     s                   like(jzldor)
     d jforlu_modn     s                   like(jzmodn)
     d jforlu_vare     s                   like(jzvare)
     d jvarl1_vare     s                   like(jvvare)
6.30 d jlevl1_ldor     s                   like(jlldor)
     d venhl1_eenh     s                   like(vaeenh)
6.20 d avall1_apgm     s                   like(avapgm)
      *
      * Definering av variabler
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_srrn_w        s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_enter         s              1    inz(*off)
     d b_inlr          s              1    inz(*off)
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_subf          s              1    inz(*off)
6.31 d b_eaok          s              1    inz(*off)
      *
     d w_firm          s                   like(jzfirm)
     d w_vare          s                   like(jwvare)
6.30 d w_ldor          s                   like(jwldor)
6.32 d w_ldor2         s                   like(jwldor)
      *
     d w_enh1          s                   like(jzenh1)
     d w_enh2          s                   like(jzenh2)
     d w_enh3          s                   like(jzenh3)
     d w_eni1          s                   like(jzeni1)
     d w_eni2          s                   like(jzeni2)
     d w_eni3          s                   like(jzeni3)
     d w_enp1          s                   like(jzenp1)
     d w_enp2          s                   like(jzenp2)
     d w_enp3          s                   like(jzenp3)
6.31 d w_rkod          s              1
6.34 d w_tldor         s              1  0
      *
6.32 d s_vare          s                   like(jwvare)
6.32 d s_ldor          s                   like(jwldor)
     d s_pakn          s                   like(jwpakn)
6.31 d s_gtin          s                   like(jwgtin)
      *
     d s_enh1          s                   like(jzenh1)
     d s_enh2          s                   like(jzenh2)
     d s_enh3          s                   like(jzenh3)
     d s_eni1          s                   like(jzeni1)
     d s_eni2          s                   like(jzeni2)
     d s_eni3          s                   like(jzeni3)
     d s_enp1          s                   like(jzenp1)
     d s_enp2          s                   like(jzenp2)
     d s_enp3          s                   like(jzenp3)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(11)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   if        b_subf = *on
     c                   goto      b2taga
     c                   endif
      *
     c                   eval      b_enter = *on
      *
      * Validering av input-felter
      *
     c                   exsr      sjekk_forpakn
     c                   if        b_feil = *on
     c                   goto      b2tagb
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
      * Oppdaterer endringer i forpakning
      *
     c                   if        b_enter = *on
     c                   exsr      oppd_forpakn
     c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      jvpkl1_pakn = b1pakn
     c     jvpkl1_key    setll     jvpkl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_subf = *off
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1vare = b2vare
6.30 c                   eval      c1ldor = b2ldor
     c                   eval      c1pakn = b1pakn
     c                   exsr      xc2bld
     c                   eval      b_subf = *on
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1vare = b2vare
6.30 c                   eval      k1ldor = b2ldor
     c                   eval      k1pakn = b1pakn
     c                   exsr      xk1win
     c                   eval      b_subf = *on
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1vare = b2vare
6.30 c                   eval      d1ldor = b2ldor
     c                   eval      d1pakn = b1pakn
     c                   exsr      xd1win
     c                   eval      b_subf = *on
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1vare = b2vare
6.30 c                   eval      c1ldor = b2ldor
     c                   eval      c1pakn = b1pakn
     c                   exsr      xc2bld
     c                   eval      b_subf = *on
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Pr. prisgiver
      *
6.30 c*                  if        b1valg = 7
6.30 c*                  eval      c1vare = b2vare
6.30 c*                  eval      c1pakn = b1pakn
6.30 c*                  call      'JW110R  '
6.30 c*                  parm                    w_firm
6.30 c*                  parm                    w_vare
6.30 c*                  parm                    c1pakn
6.30 c*                  eval      b_subf = *on
6.30 c*                  eval      b1valg = 0
6.30 c*                  update    b1sfl
6.30 c*                  iter
6.30 c*                  endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1vare = b2vare
6.30 c                   eval      c1ldor = b2ldor
6.30 c                   eval      c1lnav = b2lnav
     c                   eval      c1pakn = *blank
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   goto      end_c1bld
     c                   endif
      *
     c                   if        c1pakn = *blank
     c                   goto      end_c1bld
     c                   endif
6.32  *
6.32  * Validering
6.32  *
6.32 c                   eval      w_ldor2 = c1ldor
6.32 c                   exsr      sjekk_input
6.32 c                   if        b_feil = *on
6.32 c                   goto      c1taga
6.32 c                   endif
      *
      * Sender opp informasjonsbildet dersom rad finnes fra før
      *
6.32 c                   eval      w_ldor  = w_ldor2
6.32  *
6.32  * Henter leverandør info
6.32  *
6.32 c                   eval      jlevl1_ldor = w_ldor
6.32 c     jlevl1_key    chain     jlevl1                             90
6.32 c                   if        *in90 = *off
6.32 c                   eval      b2ldor = jlldor
6.32 c                   eval      b2lnav = jlnavn
6.32 c                   endif
      *
     c                   eval      jvpklr_pakn = c1pakn
     c     jvpklr_key    chain     jvpklr                             90
     c                   if        *in90 = *off
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1pakn = *blank
6.32 c                   eval      c1ldor = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * C2BLD Behandle bilde for ny - endring - vise
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
6.20  * henter evt. oversikt over skrivebeskyttelse for C2BLD
6.20 c                   exsr      skrb_C2
6.20  *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
     c                   eval      b_eaok = *off
      *
     c                   eval      jvpklr_pakn = c1pakn
     c     jvpklr_key    chain     jvpklr                             90
      *
     c                   if        *in90 = *off
     c                   eval      c2enhe = jwenhe
     c                   eval      c2ofak = jwofak
     c                   eval      c2ofap = jwofap
     c                   eval      c2bred = jwbred
     c                   eval      c2leng = jwleng
     c                   eval      c2hoyd = jwhoyd
     c                   eval      c2vekt = jwvekt
     c                   eval      c2volu = jwvolu
     c                   eval      c2paks = jwpaks
     c                   eval      c2antp = jwantp
     c                   eval      c2psen = jwpsen
6.30 c                   eval      c2pkla = jwpkla
6.30 c                   eval      c2papp = jwpapp
6.30 c                   eval      c2pmsb = jwpmsb
6.30 c                   eval      c2gtin = jwgtin
6.31 c                   eval      s_gtin = jwgtin
6.30 c                   if        jwfdat <> *loval
6.30 c     *dmy          move      jwfdat        c2fdat
6.30 c                   else
6.30 c                   eval      c2fdat = 0
6.30 c                   endif
6.30 c                   if        jwtdat <> *loval
6.30 c     *dmy          move      jwtdat        c2tdat
6.30 c                   else
6.30 c                   eval      c2tdat = 0
6.30 c                   endif
6.30 c                   eval      c2lfor = jwlfor
     c                   eval      c2eusr = jweusr
     c     *dmy          move      jwodat        c2odat
     c     *dmy          move      jwedat        c2edat
     c                   else
     c                   eval      c2enhe = *blank
     c                   eval      c2etxt = *blank
     c                   eval      c2ofak = 0
     c                   eval      c2ofap = 0
     c                   eval      c2bred = 0
     c                   eval      c2leng = 0
     c                   eval      c2hoyd = 0
     c                   eval      c2vekt = 0
     c                   eval      c2volu = 0
     c                   eval      c2paks = *blank
     c                   eval      c2antp = 0
     c                   eval      c2psen = 0
     c                   eval      c2eusr = *blank
     c                   eval      c2odat = 0
     c                   eval      c2edat = 0
6.30 c                   eval      c2pkla = *blank
6.30 c                   eval      c2papp = 0
6.30 c                   eval      c2pmsb = 0
6.30 c                   eval      c2gtin = 0
6.31 c                   eval      s_gtin = 0
6.30 c                   eval      c2fdat = 0
6.30 c                   eval      c2tdat = 0
6.30 c                   eval      c2lfor = 0
     c                   endif
      *
     c                   if        b1valg = 3
6.32 c                   eval      c1vare = k1vare
6.32 c                   eval      c1ldor = k1ldor
     c                   eval      c1pakn = k1pakn
6.32 c                   eval      w_ldor = k1ldor
6.32  *
6.32  * Henter leverandør info
6.32  *
6.32 c                   eval      jlevl1_ldor = w_ldor
6.32 c     jlevl1_key    chain     jlevl1                             90
6.32 c                   if        *in90 = *off
6.32 c                   eval      b2ldor = jlldor
6.32 c                   eval      b2lnav = jlnavn
6.32 c                   endif
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
      * Henter beskrivelse
      *
     c                   eval      c2etxt = *blank
      *
     c                   eval      venhl1_eenh = c2enhe
     c     venhl1_key    chain     venhl1r                            91
     c                   if        *in91 = *off
     c                   eval      c2etxt = vaetxt
     c                   endif
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd = *on
      *
     c                   if        w_afld = 'C2ENHE'
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    c2enhe
     c                   parm                    c2etxt
     c                   goto      c2tagb
     c                   endif
      *
     c                   endif
      *
      * Validering av input
      *
      * Sjekk om enhet finnes i enhet-register
      *
     c                   eval      venhl1_eenh = c2enhe
     c     venhl1_key    chain     venhl1r                            91
     c                   if        *in91 = *on
     c                   eval      *in31 = *on
     c                   goto      c2tagb
     c                   endif
      *
      * Omregningsfaktor volum må oppgis
      *
     c                   if        c2ofak = 0
     c                   eval      *in32 = *on
     c                   goto      c2tagb
     c                   endif
6.31  *
6.31  * sjekker om gyldig enhet
6.31  *
6.34  *
6.34 c                   movel     c1ldor        w_tldor
6.34 c                   if        w_tldor = 9
6.34 c                   goto      ikk_ean
6.34 c                   end
6.31 c                   if        c2gtin <> 0
6.31 c                   exsr      sjekk_ean
6.31 c                   if        b_eaok = *off
6.31 c                   eval      *in33 = *on
6.31 c                   goto      c2tagb
6.31 c                   endif
6.31 c                   endif
6.31  *
6.34 c     ikk_ean       tag
6.34  *
6.31  * Sjekker om inntastet ean-nr allerede ligger på vare-enhet
6.31  *
6.31 c                   if        c2gtin <> 0 and
6.31 c                             c2gtin <> s_gtin
6.31 c                   eval      jvpkl3_gtin = c2gtin
6.31 c     jvpkl3_key    chain     jvpkl3
6.31 c                   if        %found(jvpkl3) and
6.31 c                             jwvare <> w_vare
6.31 c                   eval      *in34 = *on
6.31 c                   goto      c2tagb
6.31 c                   endif
6.31 c                   endif
      *
      * Oppdatering
      *
     c                   clear                   jvpklur
      *
     c                   eval      jvpklu_pakn = c1pakn
     c     jvpklu_key    chain     jvpklur                            90
      *
     c                   eval      jwenhe = c2enhe
     c                   eval      jwofak = c2ofak
     c                   eval      jwofap = c2ofap
     c                   eval      jwbred = c2bred
     c                   eval      jwleng = c2leng
     c                   eval      jwhoyd = c2hoyd
     c                   eval      jwvekt = c2vekt
     c                   eval      jwvolu = c2volu
     c                   eval      jwpaks = c2paks
     c                   eval      jwantp = c2antp
     c                   eval      jwpsen = c2psen
6.31 c                   eval      jwgtin = c2gtin
     c                   eval      jweusr = l_user
      *
     c                   time                    jwedat
     c                   time                    jwetim
      *
     c                   if        *in90 = *off
     c                   update    jvpklur
     c                   else
     c                   eval      jwvare = w_vare
     c                   eval      jwpakn = c1pakn
6.30 c                   eval      jwldor = w_ldor
     c                   eval      jwrsts = 1
     c                   time                    jwodat
     c                   write     jvpklur
     c                   endif
      *
      * Oppdatering av søketekster
      *
     c                   call      'JV790R'
     c                   parm                    w_firm
     c                   parm                    jwvare
     c                   parm                    b_inlr
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1pakn = c1pakn
     c                   eval      b1enhe = c2enhe
     c                   eval      b1ofak = c2ofak
     c                   eval      b1ofap = c2ofap
     c                   eval      b1psen = c2psen
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      jvpklr_pakn = d1pakn
     c     jvpklr_key    chain     jvpklr                             90
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      jvpklu_pakn = d1pakn
     c     jvpklu_key    chain     jvpklur                            90
     c                   delete    jvpklur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
6.32 c                   eval      s_vare = b2vare
6.32 c                   eval      s_ldor = b2ldor
     c                   eval      s_pakn = k1pakn
      *
      * Fyller kopierings-bildet
      *
     c                   eval      jvpklr_pakn = k1pakn
     c     jvpklr_key    chain     jvpklr                             90
      *
     c     k1tagb        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Sjekker input
      *
     c                   if        k1pakn = *blank
     c                   eval      *in31 = *on
     c                   goto      k1tagb
     c                   endif
6.32  *
6.32 c                   eval      w_ldor2 = k1ldor
6.32 c                   exsr      sjekk_input
6.32 c                   if        b_feil = *on
6.32 c                   goto      k1tagb
6.32 c                   endif
      *
      * Sjekker om rad finnes fra før
      *
6.32 c                   eval      jvpklr_ldor = k1ldor
     c                   eval      jvpklr_pakn = k1pakn
     c     jvpklr_key    chain     jvpklr                             90
     c                   if        *in90 = *off
     c                   eval      *in32 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1pakn = s_pakn
6.32 c                   eval      c1ldor = s_ldor
6.32 c                   eval      c1vare = s_vare
      *
     c                   exsr      xc2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Fyll opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      jvpkl1                                 15
      *
     c                   if        *in15
     c                   leave
     c                   endif
      *
     c                   if        jwvare <> w_vare  or
     c                             jwldor <> w_ldor
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      jvpkl1_pakn = jwpakn
      *
     c                   eval      b1pakn = jwpakn
     c                   eval      b1enhe = jwenhe
     c                   eval      b1ofak = jwofak
     c                   eval      b1ofap = jwofap
     c                   eval      b1saen = 0
     c                   eval      b1inen = 0
     c                   eval      b1pben = 0
6.30 c                   eval      b1pkla = jwpkla
6.30 c                   eval      b1gtin = jwgtin
6.30 c                   eval      b1best = jwbest
     c                   eval      b1psen = jwpsen
     c                   eval      b1rsts = *blank
      *
     c                   select
     c                   when      jwenhe = s_enh1
     c                   eval      b1saen = 1
     c                   when      jwenhe = s_enh2
     c                   eval      b1saen = 2
     c                   when      jwenhe = s_enh3
     c                   eval      b1saen = 3
     c                   endsl
      *
     c                   select
     c                   when      jwenhe = s_eni1
     c                   eval      b1inen = 1
     c                   when      jwenhe = s_eni2
     c                   eval      b1inen = 2
     c                   when      jwenhe = s_eni3
     c                   eval      b1inen = 3
     c                   endsl
      *
     c                   select
     c                   when      jwenhe = s_enp1
     c                   eval      b1pben = 1
     c                   when      jwenhe = s_enp2
     c                   eval      b1pben = 2
     c                   when      jwenhe = s_enp3
     c                   eval      b1pben = 3
     c                   endsl
      *
     c                   if        jwrsts = 1
     c                   eval      b1rsts = 'L'
6.21 c                   elseif    jwtdat <> *loval and
6.21 c                             jwtdat < %date
6.21 c                   eval      b1rsts = 'U'
     c                   endif
      *
     c                   write     b1sfl
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     jvpkl1_key    setgt     jvpkl1
     c                   readp     jvpkl1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     jvpkl1
      *
     c                   if        %eof or
     c                             jwvare <> w_vare or
     c                             jwldor <> w_ldor
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      jvpkl1_pakn = jwpakn
      *
     c                   enddo
      *
     c                   if        %eof
     c     jvpkl1_key    setll     jvpkl1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_forpakn begsr
      *
     c                   eval      b_feil = *off
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser alle rader i subfile
      *
     c                   eval      w_srrn_w = 0
      *
     c                   do        w_ssrn
      *
     c                   eval      w_srrn_w = w_srrn_w + 1
     c     w_srrn_w      chain     b1sfl
      *
     c                   if        not %found
     c                   leave
     c                   endif
      *
     c                   eval      w_virn = w_srrn
      *
     c                   if        b1pben <> 0 and
     c                             b1saen = 0
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av forpakning
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_forpakn  begsr
      *
      * Avslutter dersom subfilen er tom
      *
     c                   if        w_ssrn <= 0
     c                   leavesr
     c                   endif
      *
     c                   eval      w_enh1 = *blank
     c                   eval      w_enh2 = *blank
     c                   eval      w_enh3 = *blank
     c                   eval      w_eni1 = *blank
     c                   eval      w_eni2 = *blank
     c                   eval      w_eni3 = *blank
     c                   eval      w_enp1 = *blank
     c                   eval      w_enp2 = *blank
     c                   eval      w_enp3 = *blank
      *
      * Leser komplett subfile, og henter valgte enheter
      *
     c                   eval      w_srrn = 1
     c     w_srrn        chain     b1sfl                              90
     c                   dow       *in90 = *off
      *
     c                   select
     c                   when      b1saen = 1
     c                   eval      w_enh1 = b1enhe
     c                   when      b1saen = 2
     c                   eval      w_enh2 = b1enhe
     c                   when      b1saen = 3
     c                   eval      w_enh3 = b1enhe
     c                   endsl
      *
     c                   select
     c                   when      b1inen = 1
     c                   eval      w_eni1 = b1enhe
     c                   when      b1inen = 2
     c                   eval      w_eni2 = b1enhe
     c                   when      b1inen = 3
     c                   eval      w_eni3 = b1enhe
     c                   endsl
      *
     c                   select
     c                   when      b1pben = 1
     c                   eval      w_enp1 = b1enhe
     c                   when      b1pben = 2
     c                   eval      w_enp2 = b1enhe
     c                   when      b1pben = 3
     c                   eval      w_enp3 = b1enhe
     c                   endsl
      *
     c                   eval      w_srrn = w_srrn + 1
     c     w_srrn        chain     b1sfl                              90
     c                   enddo
      *
      * Salgsenhet
      *
     c                   if        w_enh1 = *blank and
     c                             w_enh2 = *blank and
     c                             w_enh3 = *blank
     c                   eval      w_enh1 = s_enh1
     c                   eval      w_enh2 = s_enh2
     c                   eval      w_enh3 = s_enh3
     c                   endif
      *
      * Innkjøpsenhet
      *
     c                   if        w_eni1 = *blank and
     c                             w_eni2 = *blank and
     c                             w_eni3 = *blank
     c                   eval      w_eni1 = s_eni1
     c                   eval      w_eni2 = s_eni2
     c                   eval      w_eni3 = s_eni3
     c                   endif
      *
      * Prisbokenhet
      *
     c                   if        w_enp1 = *blank and
     c                             w_enp2 = *blank and
     c                             w_enp3 = *blank
     c                   eval      w_enp1 = s_enp1
     c                   eval      w_enp2 = s_enp2
     c                   eval      w_enp3 = s_enp3
     c                   endif
      *
      * Dersom ingen enheter er valgt, eller dersom det ikke har vært
      * endring av enheter, beholdes styringene på forpakning
      *
     c                   if        w_enh1 = *blank and
     c                             w_enh2 = *blank and
     c                             w_enh3 = *blank and
     c                             w_eni1 = *blank and
     c                             w_eni2 = *blank and
     c                             w_eni3 = *blank and
     c                             w_enp1 = *blank and
     c                             w_enp2 = *blank and
     c                             w_enp3 = *blank
     c                   leavesr
     c                   endif
      *
     c                   if        w_enh1 = s_enh1 and
     c                             w_enh2 = s_enh2 and
     c                             w_enh3 = s_enh3 and
     c                             w_eni1 = s_eni1 and
     c                             w_eni2 = s_eni2 and
     c                             w_eni3 = s_eni3 and
     c                             w_enp1 = s_enp1 and
     c                             w_enp2 = s_enp2 and
     c                             w_enp3 = s_enp3
     c                   leavesr
     c                   endif
      *
      * Oppdaterer forpakningsregisteret med forpakning på varenivå dersom
      * det har vært endringer i forpakningen av varen.
      *
     c                   if        w_enh2 = *blank and
     c                             w_enh3 <> *blank
     c                   eval      w_enh2 = w_enh3
     c                   eval      w_enh3 = *blank
     c                   endif
      *
     c                   if        w_enh1 = *blank and
     c                             w_enh2 <> *blank
     c                   eval      w_enh1 = w_enh2
     c                   eval      w_enh2 = w_enh3
     c                   eval      w_enh3 = *blank
     c                   endif
      *
     c                   if        w_eni2 = *blank and
     c                             w_eni3 <> *blank
     c                   eval      w_eni2 = w_eni3
     c                   eval      w_eni3 = *blank
     c                   endif
      *
     c                   if        w_eni1 = *blank and
     c                             w_eni2 <> *blank
     c                   eval      w_eni1 = w_eni2
     c                   eval      w_eni2 = w_eni3
     c                   eval      w_eni3 = *blank
     c                   endif
      *
     c                   if        w_enp2 = *blank and
     c                             w_enp3 <> *blank
     c                   eval      w_enp2 = w_enp3
     c                   eval      w_enp3 = *blank
     c                   endif
      *
     c                   if        w_enp1 = *blank and
     c                             w_enp2 <> *blank
     c                   eval      w_enp1 = w_enp2
     c                   eval      w_enp2 = w_enp3
     c                   eval      w_enp3 = *blank
     c                   endif
      *
     c                   eval      jforl5_vare = jvvare
     c     jforl5_key    chain     jforl5
      *
     c                   if        %found
     c                   eval      jforlu_ogrp = jzogrp
     c                   eval      jforlu_hgrp = jzhgrp
     c                   eval      jforlu_ugrp = jzugrp
     c                   eval      jforlu_ldor = jzldor
     c                   eval      jforlu_modn = jzmodn
     c                   eval      jforlu_vare = jzvare
     c     jforlu_key    chain     jforlur
     c                   endif
      *
     c                   eval      jzogrp = jvogrp
     c                   eval      jzhgrp = jvhgrp
     c                   eval      jzugrp = jvugrp
     c                   eval      jzldor = jvldor
     c                   eval      jzmodn = jvmodn
     c                   eval      jzenh1 = w_enh1
     c                   eval      jzenh2 = w_enh2
     c                   eval      jzenh3 = w_enh3
     c                   eval      jzeni1 = w_eni1
     c                   eval      jzeni2 = w_eni2
     c                   eval      jzeni3 = w_eni3
     c                   eval      jzenp1 = w_enp1
     c                   eval      jzenp2 = w_enp2
     c                   eval      jzenp3 = w_enp3
     c                   eval      jzeusr = l_user
      *
     c                   time                    jzedat
     c                   time                    jzetim
      *
     c                   if        %found(jforlu)
     c                   update    jforlur
     c                   else
     c                   eval      jzfirm = p_firm
     c                   eval      jzvare = jvvare
     c                   time                    jzodat
     c                   write     jforlur
     c                   endif
      *
     c                   endsr
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  * Subrutine for lage oversikt over felter som skal skrivebeskyttes
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     skrb_c2       begsr
6.20 c                   eval      *in60  = *off
6.20 c                   eval      *in61  = *off
6.20 c                   eval      *in62  = *off
6.20 c                   eval      *in63  = *off
6.20 c                   eval      *in64  = *off
6.20 c                   eval      *in65  = *off
6.20 c                   eval      *in66  = *off
6.20 c                   eval      *in67  = *off
6.20 c                   eval      *in68  = *off
6.20 c                   eval      *in69  = *off
6.20 c                   eval      *in70  = *off
6.20 c                   eval      *in71  = *off
6.20  *
6.20 c                   if        b2vare >= '90000000'
6.20 c                   leavesr
6.20 c                   endif
6.20  *
6.20  * Firmastyrt validering av utvalgte felter
6.20  *
6.20  *
6.20 c                   eval      avall1_apgm = 'JW100R'
6.20 c     avall1_key    setll     avall1
6.20 c     avall1_key    reade     avall1
6.20 c                   dow       not %eof(avall1)
6.20 c                   if        avaflt = 'JWENHE'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in60  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JWOFAK'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in61  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JWOFAP'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in62  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JWBRED'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in63  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JWLENG'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in64  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JWHOYD'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in65  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JWVEKT'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in66  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JWVOLU'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in67  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JWPAKS'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in68  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JWANTP'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in69  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JWPSEN'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in70  = *on
6.20 c                   endif
6.20 c     avall1_key    reade     avall1
6.20 c                   enddo
6.20  *
6.20 c                   endsr
6.31  *
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  * Subrutine for å sjekke om gyldig ean-nr er angitt
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *
6.31 c     sjekk_ean     begsr
6.31  *
6.31 c                   eval      w_rkod = '1'
6.31 c                   call      'FÅ900R'
6.31 c                   parm                    c2gtin
6.31 c                   parm                    w_rkod
6.31  *
6.31 c                   if        w_rkod = ' '
6.31 c                   eval      b_eaok = *on
6.31 c                   endif
6.31  *
6.31 c                   endsr
6.32  *
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * Sjekker inntastede verdier, og henter beskrivelser
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     sjekk_input   begsr
6.32  *
6.32 c                   eval      b_feil = *off
6.32  *
6.32  * Sjekk nivå
6.32  *
6.32 c                   if        w_ldor = 0
6.32 c                   eval      *in31 = *on
6.32 c                   eval      b_feil = *on
6.32 c                   goto      end_sjekk
6.32 c                   endif
6.32  *
6.32  * Leverandør
6.32  *
6.32 c                   eval      jlevl1_ldor = w_ldor2
6.32 c     jlevl1_key    chain     jlevl1                             35
6.32 c                   if        *in35 = *on
6.32 c                   eval      b_feil = *on
6.32 c                   goto      end_sjekk
6.32 c                   endif
6.32  *
6.32 c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
6.30 c                   parm                    p_ldor
      *
      * Key for oppslag på vare-pakning
      *
     c     jvpklr_key    klist
     c                   kfld                    w_vare
6.30 c                   kfld                    w_ldor
     c                   kfld                    jvpklr_pakn
      *
      * Key for oppdatering av vare-pakning
      *
     c     jvpklu_key    klist
     c                   kfld                    w_vare
6.30 c                   kfld                    w_ldor
     c                   kfld                    jvpklu_pakn
      *
      * Key for posisjonering på pakningskode
      *
     c     jvpkl1_key    klist
     c                   kfld                    w_vare
6.30 c                   kfld                    w_ldor
     c                   kfld                    jvpkl1_pakn
6.31  *
6.31  * Key for posisjonering på pakningskode
6.31  *
6.31 c     jvpkl3_key    klist
6.31 c                   kfld                    jvpkl3_gtin
6.32  *
6.32  * Key for oppslag på vare-pakning
6.32  *
6.32 c     jvpklr_key2   klist
6.32 c                   kfld                    w_vare
6.32 c                   kfld                    jvpklr_ldor
6.32 c                   kfld                    jvpklr_pakn
      *
      * Key for oppslag på forpakning m/varenummer
      *
     c     jforl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jforl5_vare
      *
      * Key for å oppslag på forpakning
      *
     c     jforl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jforl1_ogrp
     c                   kfld                    jforl1_hgrp
     c                   kfld                    jforl1_ugrp
     c                   kfld                    jforl1_ldor
     c                   kfld                    jforl1_modn
     c                   kfld                    jforl1_vare
      *
      * Key for oppdatering av forpakning
      *
     c     jforlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jforlu_ogrp
     c                   kfld                    jforlu_hgrp
     c                   kfld                    jforlu_ugrp
     c                   kfld                    jforlu_ldor
     c                   kfld                    jforlu_modn
     c                   kfld                    jforlu_vare
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
6.30  *
6.30  * Key for oppslag på leverandør
6.30  *
6.30 c     jlevl1_key    klist
6.30 c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på enhet
      *
     c     venhl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    venhl1_eenh
6.20  *
6.20  * Key for file : Firmastyrt validering
6.20  *
6.20 c     avall1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    avall1_apgm
      *
      *
      * Henter firma og vare fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_vare = p_vare
6.30 c                   eval      w_ldor = p_ldor
      *
      * Henter vareinformasjon
      *
     c                   eval      jvarl1_vare = p_vare
     c     jvarl1_key    chain     jvarl1                             90
     c                   if        *in90 = *off
     c                   eval      b2vare = jvvare
     c                   eval      b2tek1 = jvtek1
     c                   endif
6.30  *
6.30  * Henter leverandør info
6.30  *
6.30 c                   eval      jlevl1_ldor = p_ldor
6.30 c     jlevl1_key    chain     jlevl1                             90
6.30 c                   if        *in90 = *off
6.30 c                   eval      b2ldor = jlldor
6.30 c                   eval      b2lnav = jlnavn
6.30 c                   endif
      *
      * Henter enheter fra forpakning
      *
     c                   eval      jforl5_vare = jvvare
     c     jforl5_key    chain     jforl5                             90
     c                   if        *in90 = *on
     c                   eval      jforl1_ogrp = jvogrp
     c                   eval      jforl1_hgrp = jvhgrp
     c                   eval      jforl1_ugrp = jvugrp
     c                   eval      jforl1_ldor = jvldor
6.33 c                   eval      jforl1_modn = jvmodn
     c                   eval      jforl1_vare = *blank
     c     jforl1_key    chain     jforl1                             90
6.33 c                   if        *in90 = *on
6.33 c                   eval      jforl1_modn = 0
6.33 c     jforl1_key    chain     jforl1                             90
     c                   if        *in90 = *on
     c                   eval      jforl1_ldor = 0
     c     jforl1_key    chain     jforl1                             90
     c                   if        *in90 = *on
     c                   eval      jforl1_ugrp = 0
     c     jforl1_key    chain     jforl1                             90
     c                   if        *in90 = *on
     c                   eval      jforl1_hgrp = 0
     c     jforl1_key    chain     jforl1                             90
     c                   endif
     c                   endif
     c                   endif
     c                   endif
6.31 c                   endif
      *
     c                   if        *in90 = *off
     c                   eval      s_enh1 = jzenh1
     c                   eval      s_enh2 = jzenh2
     c                   eval      s_enh3 = jzenh3
     c                   eval      s_eni1 = jzeni1
     c                   eval      s_eni2 = jzeni2
     c                   eval      s_eni3 = jzeni3
     c                   eval      s_enp1 = jzenp1
     c                   eval      s_enp2 = jzenp2
     c                   eval      s_enp3 = jzenp3
     c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      *in22 = *on
      *
     c     jvpkl1_key    setll     jvpkl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
