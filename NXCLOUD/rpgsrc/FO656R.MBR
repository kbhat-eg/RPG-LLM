     h option(*nodebugio) decedit('0,') datedit(*dmy.)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FO646R
      * Beskrivelse . . : Utskrift av kjøre-liste oversikt pr. kjørerute
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       191110 bof  Nytt program
6.21  *       190511 bof  Feilretting vedr. kundeprosjekt og telefon
6.nu  *       230614 bof  Gjennomgått mtp. nummerutvidelse
pdf   * R7.00 150819 BSA  Innført PDF utskrift. Innebærer primært å finne
pdf   *                   generert spool, og kall av AP620R med dette som
pdf   *                   parametre.
7.01  *       170613 bof  Lista ut på regneark
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     ffohelb    if   e           k disk    rename(fohepfr:fohelbr)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fra17l1    if   e           k disk    rename(ra17pfr:ra17l1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     faforl1    if   e           k disk    rename(aforpfr:aforl1r)
7.01 flwexpf    uf a e           k disk    rename(lexcpfr:lwexpfr)
      *
     ffo656p    o    e             printer
pdf  f                                     usropn
      *
      * - - - - - - - - - - - - - -
      * Henter inn data fra LDA
      * - - - - - - - - - - - - - -
      *
     d                uds
pdf  d l_poff                584    584
pdf  d l_bach                595    635
pdf  d l_skje                637    637  0
     d l_ruti                800    809
     d l_alin                856    858  0
      *
     d l_wsid                901    910
     d l_user                        10
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d fohelb_kjor     s                   like(fokjor)
     d votyl1_otyp     s                   like(vaotyp)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d rkunl1_kund     s                   like(rkkund)
     d ra17l1_qkod     s                   like(raqkod)
      *
      * Definering av variable
      *
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_firm          s                   like(fofirm)
     d w_aarr          s              4  0
     d w_kode          s              1
     d w_retk          s              1
     d w_gtxt          s             20
     d w_itxt          s             20
     d w_jtxt          s             20
     d w_ytxt          s             30
      *
      * Definering av variable
      *
     d p_firm          s                   like(fofirm)
     d p_kjor          s                   like(fokjor)
     d p_lage          s                   like(folage)
     d p_ldat          s                   like(foldat)
7.01 d p_excl          s              1  0
      *
     d b_first         s              1    inz(*off)
     d b_list          s              1    inz(*off)
      *
7.01 d w_ex            s           1024
      *
pdf  d p_btyp          s            100
      *
pdf  d splfname2       s             10a
pdf  d jobname2        s             10a
pdf  d username2       s             10a
pdf  d jobnbr2         s              6a
pdf  d splfnbr2        s             10i 0
pdf  d splfname3       s             10a
pdf  d jobname3        s             10a
pdf  d username3       s             10a
pdf  d jobnbr3         s              6a
pdf  d splfnbr3        s             10i 0
pdf  d p_tagg0         s             20
pdf  d p_tagg0val      s             50
pdf  d p_tagg1         s             20
pdf  d p_tagg1val      s             50
pdf  d p_tagg2         s             20
pdf  d p_tagg2val      s             50
pdf  d p_tagg3         s             20
pdf  d p_tagg3val      s             50
pdf  d p_tagg4         s             20
pdf  d p_tagg4val      s             50
pdf  d p_tagg5         s             20
pdf  d p_tagg5val      s             50
pdf  d p_tagg6         s             20
pdf  d p_tagg6val      s             50
pdf  d p_tagg7         s             20
pdf  d p_tagg7val      s             50
pdf  d p_tagg8         s             20
pdf  d p_tagg8val      s             50
pdf  d p_tagg9         s             20
pdf  d p_tagg9val      s             50
pdf  d p_refn          s             50
pdf  d p_dspl          s            100
pdf   *
pdf  d qsprilsp        pr                  extpgm('QSPRILSP')
pdf  d rcvvar                     32767a   options(*varsize)
pdf  d rcvvarlen                     10i 0 const
pdf  d format                         8a   const
pdf  d errorcode                  32767a   options(*varsize)
pdf   *
pdf  d sprl0100        ds
pdf  d  bytesrtn                     10i 0
pdf  d  bytesavl                     10i 0
pdf  d  splfname                     10a
pdf  d  jobname                      10a
pdf  d  username                     10a
pdf  d  jobnbr                        6a
pdf  d  splfnbr                      10i 0
pdf  d  systemname                    8a
pdf  d  createdate                    7a
pdf  d                                1a
pdf  d  createtime                    6a
pdf   *
pdf  d errorcode       ds
pdf  d  bytesprov                    10i 0 inz(0)
pdf  d  byteavail                    10i 0 inz(0)
pdf   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Styre-rutine                                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c                   if        p_kjor = 0
     c                   eval      fohelb_kjor = 0
     c                   else
     c                   eval      fohelb_kjor = p_kjor
     c                   endif
     c     fohelb_key    setll     fohelb
     c                   read      fohelb
     c                   dow       not %eof(fohelb)
     c                   if        p_kjor <> 0  and
     c                             fokjor >  p_kjor
     c                   goto      avslutt
     c                   endif
     c                   exsr      chk_ordre
     c                   if        b_list = *on
     c                   exsr      behandle
     c                   endif
     c                   read      fohelb
     c                   enddo
      *
      *
      *
      * Avslutt program
      *
     c     avslutt       tag
pdf   * Generer xml for videre utskrift
pdf  c                   if        l_poff = '1'
pdf  c                   exsr      spoolnbr
pdf  c                   exsr      xml_create
pdf  c                   close     fo656p
pdf  c                   exsr      pdf_preview
pdf   * Avslutt jobbiden
pdf  c                   call      'AB710R'
pdf  c                   parm                    l_bach
pdf  c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandle                                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     behandle      begsr
      *
     c                   if        b_first = *off
     c                   exsr      a1hode
7.01 c                   if        p_excl  = 1
7.01 c                   exsr      xls_hode
7.01 c                   endif
     c                   eval      b_first = *on
     c                   endif
      *
     c                   exsr      d1det
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A1HDR Behandle heading A1                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     a1hode        begsr
      *
      *
      *  Hent tekst for kjørerute
      *
     c                   eval      a1ktxt = *blank
     c                   eval      a1ltxt = *blank
      *
     c                   if        p_kjor <> 0
     c                   call      'RS725R'
     c                   parm                    w_retk
     c                   parm                    p_kjor
     c                   parm                    w_ytxt
     c                   move      fokjor        a1kjor
     c                   movel     w_ytxt        a1ktxt
     c                   endif
      *
     c                   if        p_lage <> 0
     c                   call      'RS710R'
     c                   parm                    w_retk
     c                   parm                    p_lage
     c                   parm                    w_jtxt
     c                   move      folage        a1lage
     c                   movel     w_jtxt        a1ltxt
     c                   endif
      *
     c                   write     A1hdr                                30
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A1HDR Behandle heading A1                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     d1det         begsr
      *
      *
      * Snu dato
      *
      *
     c                   if        foldat <> *loval
     c     *dmy          move      foldat        d1ldat
     c                   endif
      *
      * Legg inn tilbudsnummer
      *
     c                   move      fonumm        d1numm
     c                   movel     '/'           d1suff
     c                   move      fosuff        d1suff
      *
      * Hente inn kunderegister
      *
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1                             64
     c                   movel     rknavn        d1knav
      *
6.21 c                   clear                   fkprl1r
6.21 c                   if        fokpro <> 0
6.21 c                   eval      fkprl1_kund = fokund
6.21 c                   eval      fkprl1_kpro = fokpro
6.21 c     fkprl1_key    chain     fkprl1r
6.21 c                   endif
      *
      *
      * Flytte øvrige felter
      *
     c                   if        foadr1 <> *blank
     c                   movel     foadr1        d1ladr
     c                   else
     c                   movel     fonavn        d1ladr
     c                   endif
6.21 c                   eval      d1mobi = *blank
     c                   movel     fomobi        d1mobi
6.21 c                   if        d1mobi = *blank
6.21 c                   if        fltlfm<> *blank
6.21 c                   movel     fltlfm        d1mobi
6.21 c                   else
6.21 c                   movel     rkmobn        d1mobi
6.21 c                   endif
6.21 c                   endif
     c                   movel     fovref        d1vref
     c                   eval      d1kjnr = fokjnr
     c                   if        fomerk = 1
     c                   eval      d1merk = 'X'
     c                   endif
      *
     c                   write     d1det1                               30
      *
     c                   if        *in30 = *on
     c                   exsr      overflow
     c                   endif
      *
7.01 c                   if        p_excl = 1
7.01 c                   exsr      skriv_list
7.01 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke om rad skal være med i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     chk_ordre     begsr
      *
     c                   eval      b_list = *off
      *
      * Viser ikke ordre dersom det ikke kjøres med kjørekontorløsning
      *
     c                   if        faakjk = 0
     c                   leavesr
     c                   endif
      *
      * Sjekker om lager r riktig
      *
     c                   if        p_lage <> 0
     c                   if        folage <> p_lage
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Sjekker om lev.dato er riktig
      *
     c                   if        p_ldat <> *loval
     c                   if        foldat <> p_ldat
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Viser kun ordre med leveringstype med kode for beh. i kjørekontor
      *
     c                   eval      ra17l1_qkod = folmat
     c     ra17l1_key    chain     ra17l1
     c                   if        not %found or
     c                             raqkjo <> 1
     c                   leavesr
     c                   endif
      *
      * Viser kun ordre med behandlingskode 1
      *
     c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1
     c                   if        not %found or
     c                             (vaoakk <> 1 and
     c                             vaoakk <> 2)
     c                   leavesr
     c                   endif
      *
5.54  *
5.54  * Skal bare vise ordre som er
      * - klar for plukking 2
      * - plukkliste kjørt  3
      * - under plukking    4
      * - ankommet lager    5
      * - ferdig plukket    6
      *
5.54  * Henter inn siste kode  og sjekker om
5.54  * koden er som over
5.54  *
5.54 c                   eval      w_kode = *blank
5.60 c                   eval      w_aarr = %subdt(fobdat:*years)
5.54 c                   call      'FU706R'
5.60 c                   parm                    w_aarr
5.54 c                   parm                    fonumm
5.54 c                   parm                    fosuff
5.54 c                   parm                    w_kode
5.54  *
5.54 c                   if        w_kode = '2' or
5.55 c                             w_kode = '3' or
5.54 c                             w_kode = '4' or
5.54 c                             w_kode = '5' or
5.54 c                             w_kode = '6' or
5.54 c                             w_kode = '7'
5.54 c                   else
5.54 c                   leavesr
5.54 c                   endif
5.55  *
5.54  *
     c                   eval      b_list = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  OVFLOW    Behandling av overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overflow      begsr
      *
      *  Skriver heading
      *
     c                   write     a1hdr                                30
      *
     c                   endsr
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Finner spool som er generert av jobben
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     spoolnbr      begsr
pdf   /Free
pdf
pdf            QSPRILSP( SPRL0100
pdf                    : %size(SPRL0100)
pdf                    : 'SPRL0100'
pdf                    : errorcode );
pdf   /End-free
pdf  c*
pdf   *
pdf  c                   endsr
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Kaller eget program for generering av xml fil. Eget pgm         *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     xml_create    begsr
pdf   *
pdf  c                   eval      splfname2 = *blanks
pdf  c                   eval      splfname3 = *blanks
pdf  c                   eval      jobname2 = *blanks
pdf  c                   eval      jobname3 = *blanks
pdf  c                   eval      username2 = *blanks
pdf  c                   eval      username3 = *blanks
pdf  c                   eval      jobnbr2 = *blanks
pdf  c                   eval      jobnbr3 = *blanks
pdf  c                   eval      splfnbr2 = *zeros
pdf  c                   eval      splfnbr3 = *zeros
pdf  c                   eval      p_btyp = 'KJORELISTE_PR_ORDRE'
pdf  c                   eval      p_dspl = *blanks
pdf  c                   eval      p_refn = 'KJORELISTE ' + %char(a1date)
pdf  c                   eval      p_tagg0 = *blanks
pdf  c                   eval      p_tagg0val = *blanks
pdf  c                   eval      p_tagg1 = *blanks
pdf  c                   eval      p_tagg1val = *blanks
pdf  c                   eval      p_tagg2 = *blanks
pdf  c                   eval      p_tagg2val = *blanks
pdf  c                   eval      p_tagg3 = *blanks
pdf  c                   eval      p_tagg3val = *blanks
pdf  c                   eval      p_tagg4 = *blanks
pdf  c                   eval      p_tagg4val = *blanks
pdf  c                   eval      p_tagg5 = *blanks
pdf  c                   eval      p_tagg5val = *blanks
pdf  c                   eval      p_tagg6 = *blanks
pdf  c                   eval      p_tagg6val = *blanks
pdf  c                   eval      p_tagg7 = *blanks
pdf  c                   eval      p_tagg7val = *blanks
pdf  c                   eval      p_tagg8 = *blanks
pdf  c                   eval      p_tagg8val = *blanks
pdf  c                   eval      p_tagg9 = *blanks
pdf  c                   eval      p_tagg9val = *blanks
pdf   *
pdf  c                   call      'AP627R'
pdf  c                   parm                    splfname
pdf  c                   parm                    jobname
pdf  c                   parm                    username
pdf  c                   parm                    jobnbr
pdf  c                   parm                    splfnbr
pdf  c                   parm                    splfname2
pdf  c                   parm                    jobname2
pdf  c                   parm                    username2
pdf  c                   parm                    jobnbr2
pdf  c                   parm                    splfnbr2
pdf  c                   parm                    splfname3
pdf  c                   parm                    jobname3
pdf  c                   parm                    username3
pdf  c                   parm                    jobnbr3
pdf  c                   parm                    splfnbr3
pdf  c                   parm                    p_tagg0
pdf  c                   parm                    p_tagg0val
pdf  c                   parm                    p_tagg1
pdf  c                   parm                    p_tagg1val
pdf  c                   parm                    p_tagg2
pdf  c                   parm                    p_tagg2val
pdf  c                   parm                    p_tagg3
pdf  c                   parm                    p_tagg3val
pdf  c                   parm                    p_tagg4
pdf  c                   parm                    p_tagg4val
pdf  c                   parm                    p_tagg5
pdf  c                   parm                    p_tagg5val
pdf  c                   parm                    p_tagg6
pdf  c                   parm                    p_tagg6val
pdf  c                   parm                    p_tagg7
pdf  c                   parm                    p_tagg7val
pdf  c                   parm                    p_tagg8
pdf  c                   parm                    p_tagg8val
pdf  c                   parm                    p_tagg9
pdf  c                   parm                    p_tagg9val
pdf  c                   parm                    l_bach
pdf  c                   PARM                    p_btyp
pdf  c                   PARM                    p_refn
pdf  c                   PARM                    p_dspl
pdf   *
pdf  c                   endsr
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Subrutiner for XML/jasper integrasjon - Skjermvisning           *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     pdf_preview   begsr
pdf  c                   if        l_skje = 1
pdf   * Vi kaller program for launch av PDF i browser
pdf  c                   call      'AP621R'
pdf  c                   parm                    l_bach
pdf   *
pdf  c                   endif
pdf   *
pdf  c                   endsr
pdf   *
      *
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Subrutine for å skrive xls- linje
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01 c     skriv_list    begsr
7.01  *
7.01 c                   eval      w_ex = *blank
7.01 c                   eval      w_ex = %char(d1numm) + X'05' +
7.01 c                                    %trim(%subst(d1suff:2:2)) +
7.01 c                                                    X'05' +
7.01 c                                    %char(d1ldat) + X'05' +
7.01 c                                    %trim(d1mobi) + X'05' +
7.01 c                                    %trim(d1ladr) + X'05' +
7.01 c                                    %trim(d1knav) + X'05' +
7.01 c                                    %trim(d1vref) + X'05' +
7.01 c                                    %char(d1kjnr) + X'05' +
7.01 c                                    %trim(d1merk) + X'05'
7.01 c     'Æ':X'46'     xlate     w_ex          w_ex
7.01 c     'æ':X'A0'     xlate     w_ex          w_ex
7.01 c     'Ø':X'77'     xlate     w_ex          w_ex
7.01 c     'ø':X'90'     xlate     w_ex          w_ex
7.01 c     'Å':X'2A'     xlate     w_ex          w_ex
7.01 c     'å':X'EF'     xlate     w_ex          w_ex
7.01 c                   eval      exclin   = w_ex
7.01 c                   write     lwexpfr
7.01  *
7.01 c                   endsr
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Subrutine for å skrive xls - heading
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01 c     xls_hode      begsr
7.01  *
7.01 c                   eval      w_ex = *blank
7.01 c                   eval      w_ex = 'Ordrenr'     + X'05' +
7.01 c                                    'Suffix'      + X'05' +
7.01 c                                    'Lev.sdato' + X'05' +
7.01 c                                    'Mobil'       + X'05' +
7.01 c                                    'Lev.adresse' + X'05' +
7.01 c                                    'Kundenavn'   + X'05' +
7.01 c                                    'Vår ref.'    + X'05' +
7.01 c                                    'Kjørerute'   + X'05' +
7.01 c                                    'Merket'      + X'05'
7.01 c     'Æ':X'46'     xlate     w_ex          w_ex
7.01 c     'æ':X'A0'     xlate     w_ex          w_ex
7.01 c     'Ø':X'77'     xlate     w_ex          w_ex
7.01 c     'ø':X'90'     xlate     w_ex          w_ex
7.01 c     'Å':X'2A'     xlate     w_ex          w_ex
7.01 c     'å':X'EF'     xlate     w_ex          w_ex
7.01 c                   eval      exclin   = w_ex
7.01 c                   write     lwexpfr
7.01  *
7.01 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_lage
     c                   parm                    p_kjor
     c                   parm                    p_ldat
7.01 c                   parm                    p_excl
      *
      *
      * Key for Heading-file
      *
     c     fohelb_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelb_kjor
      *
      * Key for kunderegister
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for leveringsmåte
      *
     c     ra17l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra17l1_qkod
      *
      * Key for kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for statusreg
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for firmareg
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på vare-enhet
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      *
      * Legger inn arb.stasjon og bruker i faste felter
      *
     c                   move      l_user        a1user
     c                   move      l_wsid        a1wsid
     c                   time                    w_date
     c                   time                    w_time
     c     *dmy          move      w_date        a1date
     c     *hms          move      w_time        a1time
pdf  c                   open      fo656p
      *
      * Legger inn firma i nøkkel
      *
     c                   eval      w_firm  = l_firm
     c                   eval      a1firm  = l_firm
      *
      * Hente status
      *
     c     fstsl1_key    chain     fstsl1r
      *
      *
      * Klargjøring dersom firma -
      * opplysninger skal printes ut
      *
     c     afirl1_key    chain     afirl1r                            66
     c                   move      aafnav        a1fnav
      *
     c                   endsr
