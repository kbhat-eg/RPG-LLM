     h dftactgrp(*no)
      /free
        dcl-s receiver char(16);
        dcl-s sender   char(16);
        dcl-s text     char(1024);
        dcl-s token    varchar(1024);
        dcl-s url      varchar(1024);
        dcl-s params   varchar(1500);
        dcl-s request  varchar(1024);
        dcl-s response varchar(1024);
        //Get url
        exec SQL
          select trim(VERDI2) into :url from SKTIST
           where nokkel='SYSTEM_SMS_URL';
        //Get system token from file in ADB
        exec SQL
          select trim(VERDI2) into :token from SKTIST
           where nokkel='SYSTEM_TOKEN';
        //Put together some config for the REST call
        params = '{"sslTolerate":"true", ' +
                   '"header":"eg-apps-token,' + %trim(token)+'", ' +
                   '"header":"Accept,application/json", ' +
                   '"header":"Content-Type,application/json"' +
                  '}';
        //Wash text
        exec SQL SET :text = REGEXP_REPLACE(:text,
           '[^A-Za-z0-9æøåÆØÅ.,_/?+!* :;%@\-]' , '');
        //Call the REST endpoint
        request = '{' +
                     '"receivers":[' +
                        '"' + %trim(receiver) + '"' +
                     '],' +
                     '"sender":"' + %trim(sender) + '",' +
                     '"text":"' + %trim(text) + '"' +
                  '}';
        exec SQL
          VALUES HTTP_POST(trim(:url), trim(:request),
           trim(:params)) into :response;

        *inlr = *on;
        return;
      /end-free
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    receiver
     c                   parm                    sender
     c                   parm                    text
      *
     c                   endsr
