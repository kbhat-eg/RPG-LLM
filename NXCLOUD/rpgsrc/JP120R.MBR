     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JP120R
      * Beskrivelse . . : Vedlikehold av frakt betingelser
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       030714 bof  Nytt program (tatt utgangspunkt i JP102R)
6.3X  *       160615 bof  Utvidet med prisgruppe
6.30  *       161215 nho  Kopiering av frakt funket ikke
6.31  *       221215 bsa  Sjekk mot prisgruppe fungerer ikke
6.32  *       040315 bof  Feilretting vedr. tekster i c2-bildet (fjernet linjer)
6.33  *       211216 nho  Test om varenr ligger inne på lev.nr/prisgiv
6.34  * 6.30  011118 bkv  Utvidet med logistikk-vare behandling.
6.35  * 6.30  011118 bof  Utvidet nytt pkt 8, vise varer under modul
6.36  * 6.30  011118 bof  Feilretting på store kronetillegg
7.xx  * 7.00  290716 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
8.01  *PRG2   300622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjfbel1    if   e           k disk    rename(jfbepfr:jfbel1r)
     fjfbelr    if   e           k disk    rename(jfbepfr:jfbelrr)
     fjfbelu    uf a e           k disk    rename(jfbepfr:jfbelur)
6.3x fjfbela    if   e           k disk    rename(jfbepfr:jfbelar)
     fjfbel2    if   e           k disk    rename(jfbepfr:jfbel2r)
     fjfbel3    if   e           k disk    rename(jfbepfr:jfbel3r)
     fjfbel4    if   e           k disk    rename(jfbepfr:jfbel4r)
     fjfbel5    if   e           k disk    rename(jfbepfr:jfbel5r)
     fjfbel6    if   e           k disk    rename(jfbepfr:jfbel6r)
     fjfbel7    if   e           k disk    rename(jfbepfr:jfbel7r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
6.3x fra30l1    if   e           k disk    rename(ra30pfr:ra30l1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fjmodl1    if   e           k disk    rename(jmodpfr:jmodl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fvvtyl1    if   e           k disk    rename(vvtypfr:vvtyl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
6.33 fjvlel1    if   e           k disk    rename(jvlepfr:jvlel1r)
      *
     fjp120d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_ffir          s                   like(jpffir)
     d p_fldo          s                   like(jpfldo)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
6.3x d jfbel1_fprg     s                   like(jpfprg)
     d jfbel1_fogr     s                   like(jpfogr)
     d jfbel1_fhgr     s                   like(jpfhgr)
     d jfbel1_fugr     s                   like(jpfugr)
     d jfbel1_fmod     s                   like(jpfmod)
     d jfbel1_fvar     s                   like(jpfvar)
     d jfbel1_fvty     s                   like(jpfvty)
     d jfbel1_flet     s                   like(jpflet)
6.3x d jfbelu_fprg     s                   like(jpfprg)
     d jfbelu_fogr     s                   like(jpfogr)
     d jfbelu_fhgr     s                   like(jpfhgr)
     d jfbelu_fugr     s                   like(jpfugr)
     d jfbelu_fmod     s                   like(jpfmod)
     d jfbelu_fvar     s                   like(jpfvar)
     d jfbelu_fvty     s                   like(jpfvty)
     d jfbelu_flet     s                   like(jpflet)
6.3x d jfbelr_fprg     s                   like(jpfprg)
     d jfbelr_fogr     s                   like(jpfogr)
     d jfbelr_fhgr     s                   like(jpfhgr)
     d jfbelr_fugr     s                   like(jpfugr)
     d jfbelr_fmod     s                   like(jpfmod)
     d jfbelr_fvar     s                   like(jpfvar)
     d jfbelr_fvty     s                   like(jpfvty)
     d jfbelr_flet     s                   like(jpflet)
6.3x d jfbela_fogr     s                   like(jpfogr)
     d jfbel2_fhgr     s                   like(jpfhgr)
     d jfbel3_fugr     s                   like(jpfugr)
     d jfbel4_fmod     s                   like(jpfmod)
     d jfbel5_fvar     s                   like(jpfvar)
     d jfbel6_fvty     s                   like(jpfvty)
     d jfbel7_flet     s                   like(jpflet)
     d jlevl1_ldor     s                   like(jlldor)
6.33 d jvlel1_lldo     s                   like(jvlldo)
6.33 d jvlel1_lvnr     s                   like(jvlvnr)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d jmodl1_fmod     s                   like(jumodn)
     d jvarl1_fvar     s                   like(jvvare)
     d vvtyl1_fvty     s                   like(vavtyp)
     d vltpl1_flet     s                   like(vylety)
6.3x d ra30l1_dkod     s                   like(redkod)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_visn          s              1    inz(*off)
     d b_element       s              1    inz(*off)
      *
     d w_ffda          s               d   datfmt(*iso)
     d w_ftda          s               d   datfmt(*iso)
      *
     d w_firm          s                   like(jpffir)
     d w_ldor          s                   like(jpfldo)
      *
6.3x d w_fprg          s                   like(jpfprg)
     d w_fogr          s                   like(jpfogr)
     d w_fhgr          s                   like(jpfhgr)
     d w_fugr          s                   like(jpfugr)
     d w_fmod          s                   like(jpfmod)
     d w_fvar          s                   like(jpfvar)
     d w_fvty          s                   like(jpfvty)
     d w_flet          s                   like(jpflet)
      *
     d w_oogr          s                   like(vgoogr)
     d w_hhgr          s                   like(vghhgr)
     d w_uugr          s                   like(vguugr)
     d w_otxt          s                   like(vgotxt)
     d w_htxt          s                   like(vghtxt)
     d w_utxt          s                   like(vgutxt)
     d w_mte1          s                   like(jumte1)
     d w_tek1          s                   like(jvtek1)
     d w_ttxt          s                   like(vavtxt)
     d w_lbes          s                   like(vybesk)
6.3x d w_ptxt          s                   like(vgotxt)
6.33 d w_date          s                   like(jpfeda)
6.34 d w_teller        s              8  0
6.34 d w_lv_nobb       s                   like(jvnobb)
6.34 d w_undv          s              8  0
6.34 d w_logl          s             15
6.34 d w_jvqvnr        s                   like(jpfvar)
6.33 d w_jvquno        s              8  0
      *
6.3x d s_fprg          s                   like(jpfprg)
     d s_fogr          s                   like(jpfogr)
     d s_fhgr          s                   like(jpfhgr)
     d s_fugr          s                   like(jpfugr)
     d s_fmod          s                   like(jpfmod)
     d s_fvar          s                   like(jpfvar)
     d s_fvty          s                   like(jpfvty)
     d s_flet          s                   like(jpflet)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(11)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34 C/Exec SQL
6.34 C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
6.34 C/End-Exec
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
6.30 c                   when      *inka
6.30 c                   exsr      xf1win
6.30 c                   exsr      forny
6.30 c                   goto      b2taga
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
6.30 c                   when      *inki
6.30 c                   exsr      xc3win
6.30 c                   exsr      forny
6.30 c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile                                  CRT SFL
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
6.3x c                   if        b2fprg <> *blank or
6.3x c                             b2fogr <> 0      or
     c                             b2fhgr <> 0      or
     c                             b2fugr <> 0      or
     c                             b2fmod <> 0      or
     c                             b2fvar <> *blank or
     c                             b2fvty <> *blank or
     c                             b2flet <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'H'
     c                   eval      jfbel2_fhgr = b1fhgr
     c     jfbel2_key    setll     jfbel2
     c                   when      w_seqe = 'U'
     c                   eval      jfbel3_fugr = b1fugr
     c     jfbel3_key    setll     jfbel3
     c                   when      w_seqe = 'M'
     c                   eval      jfbel4_fmod = b1fmod
     c     jfbel4_key    setll     jfbel4
     c                   when      w_seqe = 'V'
     c                   eval      jfbel5_fvar = b1fvar
     c     jfbel5_key    setll     jfbel5
     c                   when      w_seqe = 'T'
     c                   eval      jfbel6_fvty = b1fvty
     c     jfbel6_key    setll     jfbel6
     c                   when      w_seqe = 'L'
     c                   eval      jfbel7_flet = b1flet
     c     jfbel7_key    setll     jfbel7
6.3x c                   when      w_seqe = 'O'
6.3x c                   eval      jfbela_fogr = b1fogr
6.3x c     jfbela_key    setll     jfbela
     c                   other
6.3x c                   eval      jfbel1_fprg = b1fprg
     c                   eval      jfbel1_fogr = b1fogr
     c                   eval      jfbel1_fhgr = b1fhgr
     c                   eval      jfbel1_fugr = b1fugr
     c                   eval      jfbel1_fmod = b1fmod
     c                   eval      jfbel1_fvar = b1fvar
     c                   eval      jfbel1_fvty = b1fvty
     c                   eval      jfbel1_flet = b1flet
     c     jfbel1_key    setll     jfbel1
     c                   endsl
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Overgruppe
      *
     c                   if        b2fprg <> *blank
     c                   eval      w_seqe      = *blank
6.3x c                   eval      jfbel1_fprg = b2fprg
     c                   eval      jfbel1_fogr = b2fogr
     c                   eval      jfbel1_fhgr = b2fhgr
     c                   eval      jfbel1_fugr = b2fugr
     c                   eval      jfbel1_fmod = b2fmod
     c                   eval      jfbel1_fvar = b2fvar
     c                   eval      jfbel1_fvty = b2fvty
     c                   eval      jfbel1_flet = b2flet
     c     jfbel1_key    setll     jfbel1
     c                   goto      end_pos
     c                   endif
6.3x  *
6.3x  * Overgruppe
6.3x  *
6.3x c                   if        b2fogr <> 0
6.3x c                   eval      w_seqe      = 'O'
6.3x c                   eval      jfbela_fogr = b2fogr
6.3x c     jfbela_key    setll     jfbela
6.3x c                   goto      end_pos
6.3x c                   endif
      *
      * Hovedgruppe
      *
     c                   if        b2fhgr <> 0
     c                   eval      w_seqe      = 'H'
     c                   eval      jfbel2_fhgr = b2fhgr
     c     jfbel2_key    setll     jfbel2
     c                   goto      end_pos
     c                   endif
      *
      * Undergruppe
      *
     c                   if        b2fugr <> 0
     c                   eval      w_seqe      = 'U'
     c                   eval      jfbel3_fugr = b2fugr
     c     jfbel3_key    setll     jfbel3
     c                   goto      end_pos
     c                   endif
      *
      * Modulnummer
      *
     c                   if        b2fmod <> 0
     c                   eval      w_seqe      = 'M'
     c                   eval      jfbel4_fmod = b2fmod
     c     jfbel4_key    setll     jfbel4
     c                   goto      end_pos
     c                   endif
      *
      * Varenummer
      *
     c                   if        b2fvar <> *blank
     c                   eval      w_seqe      = 'V'
     c                   eval      jfbel5_fvar = b2fvar
     c     jfbel5_key    setll     jfbel5
     c                   goto      end_pos
     c                   endif
      *
      * Varetype
      *
     c                   if        b2fvty <> *blank
     c                   eval      w_seqe      = 'T'
     c                   eval      jfbel6_fvty = b2fvty
     c     jfbel6_key    setll     jfbel6
     c                   goto      end_pos
     c                   endif
      *
      * Leveringstype
      *
     c                   if        b2flet <> 0
     c                   eval      w_seqe      = 'L'
     c                   eval      jfbel7_flet = b2flet
     c     jfbel7_key    setll     jfbel7
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
      * Blanker posisjoneringsfelter
      *
6.3x c                   eval      b2fprg = *blank
     c                   eval      b2fogr = 0
     c                   eval      b2fhgr = 0
     c                   eval      b2fugr = 0
     c                   eval      b2fmod = 0
     c                   eval      b2fvar = *blank
     c                   eval      b2fvty = *blank
     c                   eval      b2flet = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1fldo = b2fldo
6.3x c                   eval      c1fprg = b1fprg
     c                   eval      c1fogr = b1fogr
     c                   eval      c1fhgr = b1fhgr
     c                   eval      c1fugr = b1fugr
     c                   eval      c1fmod = b1fmod
     c                   eval      c1fvar = b1fvar
     c                   eval      c1fvty = b1fvty
     c                   eval      c1flet = b1flet
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
6.34 c                   eval      w_jvqvnr = b1fvar
6.34 c                   exsr      finn_logivar
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
6.3x c                   eval      k1fprg = b1fprg
     c                   eval      k1fogr = b1fogr
     c                   eval      k1fhgr = b1fhgr
     c                   eval      k1fugr = b1fugr
     c                   eval      k1fmod = b1fmod
     c                   eval      k1fvar = b1fvar
     c                   eval      k1fvty = b1fvty
     c                   eval      k1flet = b1flet
     c                   exsr      xk1win
     c                   eval      b1valg = 0
6.34 c                   eval      w_jvqvnr = b1fvar
6.34 c                   exsr      finn_logivar
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
6.3x c                   eval      d1fprg = b1fprg
     c                   eval      d1fogr = b1fogr
     c                   eval      d1fhgr = b1fhgr
     c                   eval      d1fugr = b1fugr
     c                   eval      d1fmod = b1fmod
     c                   eval      d1fvar = b1fvar
     c                   eval      d1fvty = b1fvty
     c                   eval      d1flet = b1flet
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1fldo = b2fldo
6.3x c                   eval      c1fprg = b1fprg
     c                   eval      c1fogr = b1fogr
     c                   eval      c1fhgr = b1fhgr
     c                   eval      c1fugr = b1fugr
     c                   eval      c1fmod = b1fmod
     c                   eval      c1fvar = b1fvar
     c                   eval      c1fvty = b1fvty
     c                   eval      c1flet = b1flet
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
6.34 c                   eval      w_jvqvnr = b1fvar
6.34 c                   exsr      finn_logivar
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis vareavtale
      *
6.35 c                   if        b1valg = 8
6.35 c                   call      'JV548R'
6.35 c                   parm                    w_firm
6.35 c                   parm                    b1fmod
6.35 c                   eval      b1valg = 0
6.34 c                   eval      w_jvqvnr = b1fvar
6.34 c                   exsr      finn_logivar
6.35 c                   update    b1sfl
6.35 c                   iter
6.35 c                   endif
6.35  **
6.35  ** Aktiviser/passiviser betingelse (ikke aktivt)
6.35  **
6.35 c*                   if        b1valg = 9
6.35 c*                   exsr      aktiviser_bet
6.35 c*                   eval      b1valg = 0
6.35 c*                   update    b1sfl
6.35 c*                   iter
6.35 c*                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1fldo = b2fldo
6.3x c                   eval      c1fprg = b2fprg
     c                   eval      c1fogr = b2fogr
     c                   eval      c1fhgr = b2fhgr
     c                   eval      c1fugr = b2fugr
     c                   eval      c1fmod = b2fmod
     c                   eval      c1fvar = b2fvar
     c                   eval      c1fvty = b2fvty
     c                   eval      c1flet = b2flet
      *
     c                   eval      c1lnav = b2lnav
      *
6.3x c                   eval      c1ptxt = *blank
     c                   eval      c1otxt = *blank
     c                   eval      c1htxt = *blank
     c                   eval      c1utxt = *blank
     c                   eval      c1mte1 = *blank
     c                   eval      c1tek1 = *blank
     c                   eval      c1ttxt = *blank
     c                   eval      c1lbes = *blank
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      c1taga
     c                   endsl
      *
      * Validering av nøkkel
      *
6.3x c                   eval      w_fprg = c1fprg
     c                   eval      w_fogr = c1fogr
     c                   eval      w_fhgr = c1fhgr
     c                   eval      w_fugr = c1fugr
     c                   eval      w_fmod = c1fmod
     c                   eval      w_fvar = c1fvar
     c                   eval      w_fvty = c1fvty
     c                   eval      w_flet = c1flet
      *
6.3x c                   eval      w_ptxt = *blank
     c                   eval      w_otxt = *blank
     c                   eval      w_htxt = *blank
     c                   eval      w_utxt = *blank
     c                   eval      w_mte1 = *blank
     c                   eval      w_tek1 = *blank
     c                   eval      w_ttxt = *blank
     c                   eval      w_lbes = *blank
      *
     c                   exsr      sjekk_key
      *
6.3x c                   eval      c1fprg = w_fprg
     c                   eval      c1fogr = w_fogr
     c                   eval      c1fhgr = w_fhgr
     c                   eval      c1fugr = w_fugr
     c                   eval      c1fmod = w_fmod
     c                   eval      c1fvar = w_fvar
     c                   eval      c1fvty = w_fvty
     c                   eval      c1flet = w_flet
      *
6.3x c                   eval      c1ptxt = w_ptxt
     c                   eval      c1otxt = w_otxt
     c                   eval      c1htxt = w_htxt
     c                   eval      c1utxt = w_utxt
     c                   eval      c1mte1 = w_mte1
     c                   eval      c1tek1 = w_tek1
     c                   eval      c1ttxt = w_ttxt
     c                   eval      c1lbes = w_lbes
      *
     c                   if        b_feil = *on
     c                   goto      c1taga
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
6.3x c                   eval      jfbelr_fprg = c1fprg
     c                   eval      jfbelr_fogr = c1fogr
     c                   eval      jfbelr_fhgr = c1fhgr
     c                   eval      jfbelr_fugr = c1fugr
     c                   eval      jfbelr_fmod = c1fmod
     c                   eval      jfbelr_fvar = c1fvar
     c                   eval      jfbelr_fvty = c1fvty
     c                   eval      jfbelr_flet = c1flet
     c     jfbelr_key    chain     jfbelr                             90
     c                   if        *in90 = *off
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
6.3x c                   eval      b2fprg = *blank
     c                   eval      b2fogr = 0
     c                   eval      b2fhgr = 0
     c                   eval      b2fugr = 0
     c                   eval      b2fmod = 0
     c                   eval      b2fvar = *blank
     c                   eval      b2fvty = *blank
     c                   eval      b2flet = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbilde legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle skjermbilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      *in90 = *on
     c                   eval      b_element = *off
      *
6.3x c                   eval      jfbelr_fprg = c1fprg
     c                   eval      jfbelr_fogr = c1fogr
     c                   eval      jfbelr_fhgr = c1fhgr
     c                   eval      jfbelr_fugr = c1fugr
     c                   eval      jfbelr_fmod = c1fmod
     c                   eval      jfbelr_fvar = c1fvar
     c                   eval      jfbelr_fvty = c1fvty
     c                   eval      jfbelr_flet = c1flet
     c     jfbelr_key    chain     jfbelr
      *
     c                   if        %found(jfbelr)
     c                   eval      c2fkrt = jpfkrt
     c                   eval      c2fprt = jpfprt
     c                   eval      c2fton = jpfton
     c                   eval      c2fkub = jpfkub
     c                   eval      c2feus = jpfeus
     c                   eval      c2ffda = 0
     c                   eval      c2ftda = 0
     c     *dmy          move      jpfoda        c2foda
     c     *dmy          move      jpfeda        c2feda
     c                   if        jpffda <> *loval
     c     *dmy          move      jpffda        c2ffda
     c                   endif
     c                   if        jpftda <> *loval
     c     *dmy          move      jpftda        c2ftda
     c                   endif
     c                   else
     c                   eval      c2fkrt = 0
     c                   eval      c2fprt = 0
     c                   eval      c2fton = 0
     c                   eval      c2fkub = 0
     c                   eval      c2ffda = 0
     c                   eval      c2ftda = 0
     c                   eval      c2foda = 0
     c                   eval      c2feda = 0
     c                   eval      c2feus = *blank
     c                   endif
      *
      * Kopiering
      *
     c                   if        b1valg = 3
6.3x c                   eval      c1fprg = k1fprg
     c                   eval      c1fogr = k1fogr
     c                   eval      c1fhgr = k1fhgr
     c                   eval      c1fugr = k1fugr
     c                   eval      c1fmod = k1fmod
     c                   eval      c1fvar = k1fvar
     c                   eval      c1fvty = k1fvty
     c                   eval      c1flet = k1flet
6.3x c                   eval      jfbelr_fprg = k1fprg
     c                   eval      jfbelr_fogr = k1fogr
     c                   eval      jfbelr_fhgr = k1fhgr
     c                   eval      jfbelr_fugr = k1fugr
     c                   eval      jfbelr_fmod = k1fmod
     c                   eval      jfbelr_fvar = k1fvar
     c                   eval      jfbelr_fvty = k1fvty
     c                   eval      jfbelr_flet = k1flet
     c                   endif
      *
      * Henter beskrivelse av nøkkelfelter
      *
6.32 c*                  if        b1valg = 2 or
6.32 c*                            b1valg = 5
      *
     c                   eval      c1lnav = b2lnav
      *
6.3x c                   eval      c1ptxt = *blank
     c                   eval      c1otxt = *blank
     c                   eval      c1htxt = *blank
     c                   eval      c1utxt = *blank
     c                   eval      c1mte1 = *blank
     c                   eval      c1tek1 = *blank
     c                   eval      c1ttxt = *blank
     c                   eval      c1lbes = *blank
      *
6.3x c                   if        c1fprg <> *blank
6.3x c                   eval      ra30l1_dkod = c1fprg
6.3x c     ra30l1_key    chain     ra30l1
6.3x c                   eval      c1ptxt = redtxt
6.3x c                   endif
      *
     c                   if        c1fogr <> 0
     c                   eval      vogrl1_oogr = c1fogr
     c     vogrl1_key    chain     vogrl1
     c                   eval      c1otxt = vgotxt
     c                   endif
      *
     c                   if        c1fhgr <> 0
     c                   eval      vhgrl1_hogr = c1fogr
     c                   eval      vhgrl1_hhgr = c1fhgr
     c     vhgrl1_key    chain     vhgrl1
     c                   eval      c1htxt = vghtxt
     c                   endif
      *
     c                   if        c1fugr <> 0
     c                   eval      vugrl1_uogr = c1fogr
     c                   eval      vugrl1_uhgr = c1fhgr
     c                   eval      vugrl1_uugr = c1fugr
     c     vugrl1_key    chain     vugrl1
     c                   eval      c1utxt = vgutxt
     c                   endif
      *
     c                   if        c1fmod <> 0
     c                   eval      jmodl1_fmod = c1fmod
     c     jmodl1_key    chain     jmodl1
     c                   eval      c1mte1 = jumte1
     c                   endif
      *
     c                   if        c1fvty <> *blank
     c                   eval      vvtyl1_fvty = c1fvty
     c     vvtyl1_key    chain     vvtyl1
     c                   eval      c1ttxt = vavtxt
     c                   endif
      *
     c                   if        c1flet <> 0
     c                   eval      vltpl1_flet = c1flet
     c     vltpl1_key    chain     vltpl1
     c                   eval      c1lbes = vybesk
     c                   endif
      *
6.32 c*                  endif
      *
      * Henter vareinformasjon
      *
     c                   if        c1fvar <> *blank
     c                   eval      jvarl1_fvar = c1fvar
     c     jvarl1_key    chain     jvarl1
     c                   eval      c1tek1 = jvtek1
     c                   endif
      *
      *
      * Sperrer felter dersom 'vise'
      *
     c                   if        b1valg = 5
     c                   eval      *in30  = *on
     c                   endif
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2ffda <> 0
     c     *dmy          test (d)                c2ffda                 32
     c                   if        *in32
     c                   goto      c2tagb                                                    .
     c                   endif
     c                   endif
      *
     c                   if        c2ftda <> 0
     c     *dmy          test (d)                c2ftda                 33
     c                   if        *in33
     c                   goto      c2tagb                                                    .
     c                   endif
     c                   endif
      *
     c                   if        c2ffda <> 0 and
     c                             c2ftda <> 0
     c     *dmy          move      c2ffda        w_ffda
     c     *dmy          move      c2ftda        w_ftda
     c                   if        w_ffda > w_ftda
     c                   eval      *in34 = *on
     c                   goto      c2tagb                                                    .
     c                   endif
     c                   endif
      *
      * sjekker om fler enn 1 fraktbetingelse er utfylt
      *
     c                   if        c2fkrt = 0 and
     c                             c2fprt = 0 and
     c                             c2fton = 0 and
     c                             c2fkub = 0
     c                   eval      *in35 = *on
     c                   goto      c2tagb                                                    .
     c                   endif
     c                   if        c2fkrt <> 0 and
     c                             (c2fprt <> 0 or
     c                             c2fton  <> 0 or
     c                             c2fkub  <> 0)
     c                   eval      *in35 = *on
     c                   goto      c2tagb                                                    .
     c                   endif
     c                   if        c2fprt <> 0 and
     c                             (c2fkrt <> 0 or
     c                             c2fton  <> 0 or
     c                             c2fkub  <> 0)
     c                   eval      *in35 = *on
     c                   goto      c2tagb                                                    .
     c                   endif
     c                   if        c2fton <> 0 and
     c                             (c2fprt <> 0 or
     c                             c2fkrt  <> 0 or
     c                             c2fkub  <> 0)
     c                   eval      *in35 = *on
     c                   goto      c2tagb                                                    .
     c                   endif
     c                   if        c2fkub <> 0 and
     c                             (c2fprt <> 0 or
     c                             c2fkrt  <> 0 or
     c                             c2fton  <> 0)
     c                   eval      *in35 = *on
     c                   goto      c2tagb                                                    .
     c                   endif
      *
      * kronetillegg må kun gjelde på varenivå
      *
     c                   if        c2fkrt <> 0 and
     c                             c1fvar = *blank
     c                   eval      *in36 = *on
     c                   goto      c2tagb                                                    .
     c                   endif
      * Oppdatering
      *
     c                   clear                   jfbelur
      *
     c                   eval      *in90 = *on
      *
6.3x c                   eval      jfbelu_fprg = c1fprg
     c                   eval      jfbelu_fogr = c1fogr
     c                   eval      jfbelu_fhgr = c1fhgr
     c                   eval      jfbelu_fugr = c1fugr
     c                   eval      jfbelu_fmod = c1fmod
     c                   eval      jfbelu_fvar = c1fvar
     c                   eval      jfbelu_fvty = c1fvty
     c                   eval      jfbelu_flet = c1flet
     c     jfbelu_key    chain     jfbelur                            90
      *
     c                   eval      jpffda = *loval
     c                   if        c2ffda <> 0
     c     *dmy          move      c2ffda        jpffda
     c                   endif
      *
     c                   eval      jpftda = *loval
     c                   if        c2ftda <> 0
     c     *dmy          move      c2ftda        jpftda
     c                   endif
     c                   eval      jpfkrt = c2fkrt
     c                   eval      jpfprt = c2fprt
     c                   eval      jpfton = c2fton
     c                   eval      jpfkub = c2fkub
      *
      *
     c                   eval      jpfeus = l_user
     c                   time                    jpfeda
     c                   time                    jpfeti
      *
     c                   if        *in90 = *off
     c                   update    jfbelur
     c                   else
      *
     c                   eval      jpffir = w_firm
     c                   eval      jpfldo = w_ldor
6.3x c                   eval      jpfprg = c1fprg
     c                   eval      jpfogr = c1fogr
     c                   eval      jpfhgr = c1fhgr
     c                   eval      jpfugr = c1fugr
     c                   eval      jpfmod = c1fmod
     c                   eval      jpfvar = c1fvar
     c                   eval      jpfvty = c1fvty
     c                   eval      jpflet = c1flet
     c                   time                    jpfoda
      *
     c                   write     jfbelur
      *
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 2 or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
6.30  *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  * C3 Behandle bilde  å aktivsere alle betingelser for en leverandør
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  *
6.30 c     xc3win        begsr
6.30  *
6.30  *  Send Alt
6.30  *
6.30 c     xc3tag        tag
6.30  *
6.30 c     c3taga        tag
6.30 c                   exfmt     c3win
6.30  *
6.30  * Behandling av funksjonstaster
6.30  *
6.30 c                   select
6.30 c                   when      *inkc or *inkl
6.30 c                   goto      end_c3win
6.30 c                   endsl
6.30  *
6.30  * aktivisere alle betingelser for leverandør
6.30  *
6.30 c     jfbel1_key2   setll     jfbel1
6.30 c     jfbel1_key2   reade     jfbel1
6.30 c                   dow       not %eof(jfbel1)
6.30 c                   if        jpfvar <> *blank
6.3x c                   eval      jfbelu_fprg = jpfprg
6.30 c                   eval      jfbelu_fogr = jpfogr
6.30 c                   eval      jfbelu_fhgr = jpfhgr
6.30 c                   eval      jfbelu_fugr = jpfugr
6.30 c                   eval      jfbelu_fmod = jpfmod
6.30 c                   eval      jfbelu_fvar = jpfvar
6.30 c                   eval      jfbelu_fvty = jpfvty
6.30 c                   eval      jfbelu_flet = jpflet
6.30 c     jfbelu_key    chain     jfbelur
6.30 c                   if        %found(jfbelu)
6.30 c                   time                    jpfeda
6.30 c                   time                    jpfeti
6.30 c                   eval      jpfeus = l_user
6.30 c                   update    jfbelur
6.30 c                   endif
6.40 c                   endif
6.30 c     jfbel1_key2   reade     jfbel1
6.30 c                   enddo
6.30  *
6.30 c     end_c3win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
6.3x c                   eval      jfbelr_fprg = d1fprg
     c                   eval      jfbelr_fogr = d1fogr
     c                   eval      jfbelr_fhgr = d1fhgr
     c                   eval      jfbelr_fugr = d1fugr
     c                   eval      jfbelr_fmod = d1fmod
     c                   eval      jfbelr_fvar = d1fvar
     c                   eval      jfbelr_fvty = d1fvty
     c                   eval      jfbelr_flet = d1flet
     c     jfbelr_key    chain     jfbelr                             90
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
6.3x c                   eval      jfbelu_fprg = d1fprg
     c                   eval      jfbelu_fogr = d1fogr
     c                   eval      jfbelu_fhgr = d1fhgr
     c                   eval      jfbelu_fugr = d1fugr
     c                   eval      jfbelu_fmod = d1fmod
     c                   eval      jfbelu_fvar = d1fvar
     c                   eval      jfbelu_fvty = d1fvty
     c                   eval      jfbelu_flet = d1flet
     c     jfbelu_key    chain     jfbelur                            90
     c                   delete    jfbelur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
6.3x c                   eval      s_fprg = k1fprg
     c                   eval      s_fogr = k1fogr
     c                   eval      s_fhgr = k1fhgr
     c                   eval      s_fugr = k1fugr
     c                   eval      s_fmod = k1fmod
     c                   eval      s_fvar = k1fvar
     c                   eval      s_fvty = k1fvty
     c                   eval      s_flet = k1flet
      *
      * Fyller kopierings-bildet
      *
6.3x c                   eval      jfbelr_fprg = k1fprg
     c                   eval      jfbelr_fogr = k1fogr
     c                   eval      jfbelr_fhgr = k1fhgr
     c                   eval      jfbelr_fugr = k1fugr
     c                   eval      jfbelr_fmod = k1fmod
     c                   eval      jfbelr_fvar = k1fvar
     c                   eval      jfbelr_fvty = k1fvty
     c                   eval      jfbelr_flet = k1flet
     c     jfbelr_key    chain     jfbelr                             90
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   exsr      spØrring
     c                   goto      k1taga
     c                   endif
      *
      * Validering av nøkkel
      *
6.3x c                   eval      w_fprg = k1fprg
     c                   eval      w_fogr = k1fogr
     c                   eval      w_fhgr = k1fhgr
     c                   eval      w_fugr = k1fugr
     c                   eval      w_fmod = k1fmod
     c                   eval      w_fvar = k1fvar
     c                   eval      w_fvty = k1fvty
     c                   eval      w_flet = k1flet
      *
6.3x c                   eval      w_ptxt = *blank
     c                   eval      w_otxt = *blank
     c                   eval      w_htxt = *blank
     c                   eval      w_utxt = *blank
     c                   eval      w_mte1 = *blank
     c                   eval      w_tek1 = *blank
     c                   eval      w_ttxt = *blank
     c                   eval      w_lbes = *blank
      *
     c                   exsr      sjekk_key
      *
6.3x c                   eval      k1fprg = w_fprg
     c                   eval      k1fogr = w_fogr
     c                   eval      k1fhgr = w_fhgr
     c                   eval      k1fugr = w_fugr
     c                   eval      k1fmod = w_fmod
     c                   eval      k1fvar = w_fvar
     c                   eval      k1fvty = w_fvty
     c                   eval      k1flet = w_flet
      *
     c                   if        b_feil = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
      * Kaller vedlikeholdsprogram
      *
6.3x c                   eval      c1fprg = s_fprg
     c                   eval      c1fogr = s_fogr
     c                   eval      c1fhgr = s_fhgr
     c                   eval      c1fugr = s_fugr
     c                   eval      c1fmod = s_fmod
     c                   eval      c1fvar = s_fvar
     c                   eval      c1fvty = s_fvty
     c                   eval      c1flet = s_flet
      *
     c                   exsr      xc2bld
      *
     c                   endsr
6.30  *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  * F1 Behandle bilde for bla-alternativer
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  *
6.30 c     xf1win        begsr
6.30  *
6.30  *  Send Alt
6.30  *
6.30 c     xf1tag        tag
6.30  *
6.30 c     f1taga        tag
6.30 c                   exfmt     f1win
6.30  *
6.30  * Behandling av funksjonstaster
6.30  *
6.30 c                   select
6.30 c                   when      *inkc or *inkl
6.30 c                   goto      end_f1win
6.30 c                   when      *inkd
6.30 c                   exsr      spØrring
6.30 c                   goto      f1taga
6.30 c                   endsl
6.30  *
6.30  * Validering
6.30  *
6.30 c                   if        f1fogr = 0 and
6.30 c                             (f1fhgr <> 0 or
6.30 c                              f1fugr <> 0)
6.30 c                   eval      *in33 = *on
6.30 c                   goto      f1taga
6.30 c                   endif
6.30  *
6.30 c                   if        f1fugr <> 0 and
6.30 c                             (f1fogr <> 0 or
6.30 c                              f1fhgr <> 0)
6.30 c                   eval      *in35 = *on
6.30 c                   goto      f1taga
6.30 c                   endif
6.30  *
6.30 c                   if        f1fmod <> 0 and
6.30 c                             (f1fogr <> 0 or
6.30 c                              f1fhgr <> 0 or
6.30 c                              f1fugr <> 0)
6.30 c                   eval      *in36 = *on
6.30 c                   goto      f1taga
6.30 c                   endif
6.30  *
6.30 c     end_f1win     endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
6.3x c     w_seqe        wheneq    'O'
6.3x c                   read      jfbela                                 15
     c     w_seqe        wheneq    'H'
     c                   read      jfbel2                                 15
     c     w_seqe        wheneq    'U'
     c                   read      jfbel3                                 15
     c     w_seqe        wheneq    'M'
     c                   read      jfbel4                                 15
     c     w_seqe        wheneq    'V'
     c                   read      jfbel5                                 15
     c     w_seqe        wheneq    'T'
     c                   read      jfbel6                                 15
     c     w_seqe        wheneq    'L'
     c                   read      jfbel7                                 15
     c                   other
     c                   read      jfbel1                                 15
     c                   endsl
      *
     c                   if        *in15                                        EOF
     c                   leave
     c                   endif
      *
     c                   if        jpffir <> p_ffir or
     c                             jpfldo <> p_fldo
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
6.30  *
6.30  * Filter
6.30  *
6.3x c                   if        f1fprg <> *blank and
6.3x c                             f1fprg <> jpfprg
6.3x c                   iter
6.3x c                   endif
      *
6.30 c                   if        f1fogr <> 0
6.30  *
6.30 c                   if        f1fogr <> jpfogr
6.30 c                   iter
6.30 c                   endif
6.30  *
6.30 c                   if        f1fhgr <> 0
6.30  *
6.30 c                   if        f1fhgr <> jpfhgr
6.30 c                   iter
6.30 c                   endif
6.30  *
6.30 c                   if        f1fugr <> 0 and
6.30 c                             f1fhgr <> jpfhgr
6.30 c                   iter
6.30 c                   endif
6.30  *
6.30 c                   endif
6.30  *
6.30 c                   endif
6.30  *
6.30 c                   if        f1fmod <> 0 and
6.30 c                             f1fmod <> jpfmod
6.30 c                   iter
6.30 c                   endif
      *
6.34 c                   if        f1flva <> *blank
6.34 c                   movel     f1flva        w_lv_nobb
6.34 c/exec sql
6.34 c+ select count(*)
6.34 c+ into   :w_teller
6.34 c+ from   jvklpf
6.34 c+ where  jvqvnr = :jpfvar and
6.34 c+        jvquno = :w_lv_nobb and
     c+        jvqldo = :w_ldor
6.34 c/end-exec
6.34 c                   if        w_teller = 0
6.34 c                   iter
6.34 c                   endif
6.34 c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
6.3x c                   when      w_seqe = 'O'
6.3X c                   eval      jfbela_fogr = jpfogr
     c                   when      w_seqe = 'H'
     c                   eval      jfbel2_fhgr = jpfhgr
     c                   when      w_seqe = 'U'
     c                   eval      jfbel3_fugr = jpfugr
     c                   when      w_seqe = 'M'
     c                   eval      jfbel4_fmod = jpfmod
     c                   when      w_seqe = 'V'
     c                   eval      jfbel5_fvar = jpfvar
     c                   when      w_seqe = 'T'
     c                   eval      jfbel6_fvty = jpfvty
     c                   when      w_seqe = 'L'
     c                   eval      jfbel7_flet = jpflet
     c                   other
6.3X c                   eval      jfbel1_fprg = jpfprg
     c                   eval      jfbel1_fogr = jpfogr
     c                   eval      jfbel1_fhgr = jpfhgr
     c                   eval      jfbel1_fugr = jpfugr
     c                   eval      jfbel1_fmod = jpfmod
     c                   eval      jfbel1_fvar = jpfvar
     c                   eval      jfbel1_fvty = jpfvty
     c                   eval      jfbel1_flet = jpflet
     c                   endsl
      *
     c                   eval      b1valg = 0
6.3x c                   eval      b1fprg = jpfprg
     c                   eval      b1fogr = jpfogr
     c                   eval      b1fhgr = jpfhgr
     c                   eval      b1fugr = jpfugr
     c                   eval      b1fmod = jpfmod
     c                   eval      b1fvar = jpfvar
     c                   eval      b1fvty = jpfvty
     c                   eval      b1flet = jpflet
6.36 c                   if        jpfkrt > 9999999
6.36 c                   eval      b1fkrt = 9999999
6.36 c                   else
     c                   eval      b1fkrt = jpfkrt
6.36 c                   endif
     c                   eval      b1fprt = jpfprt
     c                   eval      b1fton = jpfton
     c                   eval      b1fkub = jpfkub
     c*                  eval      b1fakt = *blank
      *
6.34 c                   eval      w_jvqvnr = jpfvar
6.34 c                   exsr      finn_logivar
      *
      * Skriver til subfile
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
6.3x c                   when      w_seqe = 'O'
6.3x c     jfbela_key    setgt     jfbela
6.3x c                   readp     jfbela
     c                   when      w_seqe = 'H'
     c     jfbel2_key    setgt     jfbel2
     c                   readp     jfbel2
     c                   when      w_seqe = 'U'
     c     jfbel3_key    setgt     jfbel3
     c                   readp     jfbel3
     c                   when      w_seqe = 'M'
     c     jfbel4_key    setgt     jfbel4
     c                   readp     jfbel4
     c                   when      w_seqe = 'V'
     c     jfbel5_key    setgt     jfbel5
     c                   readp     jfbel5
     c                   when      w_seqe = 'T'
     c     jfbel6_key    setgt     jfbel6
     c                   readp     jfbel6
     c                   when      w_seqe = 'L'
     c     jfbel7_key    setgt     jfbel7
     c                   readp     jfbel7
     c                   other
     c     jfbel1_key    setgt     jfbel1
     c                   readp     jfbel1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
6.3x c                   when      w_seqe = 'O'
6.3x c                   readp     jfbela
     c                   when      w_seqe = 'H'
     c                   readp     jfbel2
     c                   when      w_seqe = 'U'
     c                   readp     jfbel3
     c                   when      w_seqe = 'M'
     c                   readp     jfbel4
     c                   when      w_seqe = 'V'
     c                   readp     jfbel5
     c                   when      w_seqe = 'T'
     c                   readp     jfbel6
     c                   when      w_seqe = 'L'
     c                   readp     jfbel7
     c                   other
     c                   readp     jfbel1
     c                   endsl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   if        jpffir <> p_ffir or
     c                             jpfldo <> p_fldo
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
6.3x c                   when      w_seqe = 'O'
6.3x c                   eval      jfbela_fogr = jpfogr
     c                   when      w_seqe = 'H'
     c                   eval      jfbel2_fhgr = jpfhgr
     c                   when      w_seqe = 'U'
     c                   eval      jfbel3_fugr = jpfugr
     c                   when      w_seqe = 'M'
     c                   eval      jfbel4_fmod = jpfmod
     c                   when      w_seqe = 'V'
     c                   eval      jfbel5_fvar = jpfvar
     c                   when      w_seqe = 'T'
     c                   eval      jfbel6_fvty = jpfvty
     c                   when      w_seqe = 'L'
     c                   eval      jfbel7_flet = jpflet
     c                   other
6.3x c                   eval      jfbel1_fprg = jpfprg
     c                   eval      jfbel1_fogr = jpfogr
     c                   eval      jfbel1_fhgr = jpfhgr
     c                   eval      jfbel1_fugr = jpfugr
     c                   eval      jfbel1_fmod = jpfmod
     c                   eval      jfbel1_fvar = jpfvar
     c                   eval      jfbel1_fvty = jpfvty
     c                   eval      jfbel1_flet = jpflet
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
6.3x c                   when      w_seqe = 'O'
6.3x c     jfbela_key    setll     jfbela
     c                   when      w_seqe = 'H'
     c     jfbel2_key    setll     jfbel2
     c                   when      w_seqe = 'U'
     c     jfbel3_key    setll     jfbel3
     c                   when      w_seqe = 'M'
     c     jfbel4_key    setll     jfbel4
     c                   when      w_seqe = 'V'
     c     jfbel5_key    setll     jfbel5
     c                   when      w_seqe = 'T'
     c     jfbel6_key    setll     jfbel6
     c                   when      w_seqe = 'L'
     c     jfbel7_key    setll     jfbel7
     c                   other
     c     jfbel1_key    setll     jfbel1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
6.3x  *
6.3x  * Prisgruppe
6.3x  *
6.3x c                   if        w_afld = 'B2FPRG'
6.3x c                   call      'RA530R'
6.3x c                   parm                    w_firm
6.3x c                   parm                    b2fprg
6.3x c                   parm                    w_ptxt
6.3x c                   leavesr
6.3x c                   endif
6.3x  *
6.3x c                   if        w_afld = 'C1FPRG'
6.3x c                   call      'RA530R'
6.3x c                   parm                    w_firm
6.3x c                   parm                    c1fprg
6.3x c                   parm                    c1ptxt
6.3x c                   leavesr
6.3x c                   endif
6.3x  *
6.3x c                   if        w_afld = 'K1FPRG'
6.3x c                   call      'RA530R'
6.3x c                   parm                    w_firm
6.3x c                   parm                    k1fprg
6.3x c                   parm                    w_ptxt
6.3x c                   leavesr
6.3x c                   endif
      *
      * Overgruppe
      *
     c                   if        w_afld = 'B2FOGR'
     c                   eval      b2fhgr = 0
     c                   eval      b2fugr = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b2fogr
     c                   parm                    w_otxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1FOGR'
     c                   eval      c1fhgr = 0
     c                   eval      c1htxt = *blank
     c                   eval      c1fugr = 0
     c                   eval      c1utxt = *blank
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    c1fogr
     c                   parm                    c1otxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1FOGR'
     c                   eval      k1fhgr = 0
     c                   eval      k1fugr = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    k1fogr
     c                   parm                    w_otxt
     c                   leavesr
     c                   endif
6.30  *
6.30 c                   if        w_afld = 'F1FOGR'
6.30 c                   eval      f1fhgr = 0
6.30 c                   eval      f1fugr = 0
6.30 c                   call      'VG510R'
6.30 c                   parm                    w_firm
6.30 c                   parm                    f1fogr
6.30 c                   parm                    w_otxt
6.30 c                   leavesr
6.30 c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_afld = 'B2FHGR'
     c                   eval      b2fugr = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b2fogr
     c                   parm                    b2fhgr
     c                   parm                    w_htxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1FHGR'
     c                   eval      c1fugr = 0
     c                   eval      c1utxt = *blank
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    c1fogr
     c                   parm                    c1fhgr
     c                   parm                    c1htxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1FHGR'
     c                   eval      k1fugr = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    k1fogr
     c                   parm                    k1fhgr
     c                   parm                    w_htxt
     c                   leavesr
     c                   endif
6.30  *
6.30 c                   if        w_afld = 'F1FHGR'
6.30 c                   eval      f1fugr = 0
6.30 c                   call      'VG511R'
6.30 c                   parm                    w_firm
6.30 c                   parm                    f1fogr
6.30 c                   parm                    f1fhgr
6.30 c                   parm                    w_htxt
6.30 c                   leavesr
6.30 c                   endif
      *
      * Undergruppe
      *
     c                   if        w_afld = 'B2FUGR'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b2fogr
     c                   parm                    b2fhgr
     c                   parm                    b2fugr
     c                   parm                    w_utxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1FUGR'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    c1fogr
     c                   parm                    c1fhgr
     c                   parm                    c1fugr
     c                   parm                    c1utxt
     c                   leavesr
     c                   endif
6.30  *
6.30 c                   if        w_afld = 'K1FUGR'
6.30 c                   call      'VG512R'
6.30 c                   parm                    w_firm
6.30 c                   parm                    k1fogr
6.30 c                   parm                    k1fhgr
6.30 c                   parm                    k1fugr
6.30 c                   parm                    w_utxt
6.30 c                   leavesr
6.30 c                   endif
      *
     c                   if        w_afld = 'K1FUGR'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    k1fogr
     c                   parm                    k1fhgr
     c                   parm                    k1fugr
     c                   parm                    w_utxt
     c                   leavesr
     c                   endif
      *
      * Modulnummer
      *
     c                   if        w_afld = 'B2FMOD'
     c                   call      'JU500R'
     c                   parm                    w_firm
     c                   parm                    b2fmod
     c                   parm                    w_mte1
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1FMOD'
     c                   call      'JU500R'
     c                   parm                    w_firm
     c                   parm                    c1fmod
     c                   parm                    c1mte1
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1FMOD'
     c                   call      'JU500R'
     c                   parm                    w_firm
     c                   parm                    k1fmod
     c                   parm                    w_mte1
     c                   leavesr
     c                   endif
6.30  *
6.30 c                   if        w_afld = 'F1FMOD'
6.30 c                   call      'JU500R'
6.30 c                   parm                    w_firm
6.30 c                   parm                    f1fmod
6.30 c                   parm                    w_mte1
6.30 c                   leavesr
6.30 c                   endif
      *
      * Varenummer
      *
     c                   if        w_afld = 'B2FVAR'
     c                   eval      w_fvar = *blank
     c                   call      'JV500R'
     c                   parm                    w_firm
     c                   parm                    w_fvar
     c                   parm                    w_tek1
     c                   eval      b2fvar = w_fvar
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1FVAR'
     c                   eval      w_fvar = *blank
     c                   call      'JV500R'
     c                   parm                    w_firm
     c                   parm                    w_fvar
     c                   parm                    c1tek1
     c                   eval      c1fvar = w_fvar
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1FVAR'
     c                   eval      w_fvar = *blank
     c                   call      'JV500R'
     c                   parm                    w_firm
     c                   parm                    w_fvar
     c                   parm                    w_tek1
     c                   eval      k1fvar = w_fvar
     c                   leavesr
     c                   endif
      *
      * Varetype
      *
     c                   if        w_afld = 'B2FVTY'
     c                   call      'VA570R'
     c                   parm                    w_firm
     c                   parm                    b2fvty
     c                   parm                    w_ttxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1FVTY'
     c                   call      'VA570R'
     c                   parm                    w_firm
     c                   parm                    c1fvty
     c                   parm                    c1ttxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1FVTY'
     c                   call      'VA570R'
     c                   parm                    w_firm
     c                   parm                    k1fvty
     c                   parm                    w_ttxt
     c                   leavesr
     c                   endif
      *
      * Leveringstype
      *
     c                   if        w_afld = 'B2FLET'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    b2flet
     c                   parm                    w_lbes
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1FLET'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    c1flet
     c                   parm                    c1lbes
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1FLET'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    k1flet
     c                   parm                    w_lbes
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for validering av nøkkel
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_key     begsr
      *
     c                   eval      b_feil = *off
      *
      * Modul
      *
     c                   if        w_fmod <> 0
      *
     c                   eval      jmodl1_fmod = w_fmod
     c     jmodl1_key    chain     jmodl1
     c                   if        not %found
     c                   eval      *in38 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      w_fogr = juogrp
     c                   eval      w_fhgr = juhgrp
     c                   eval      w_fugr = juugrp
     c                   eval      w_mte1 = jumte1
      *
     c                   endif
      *
      * Vare
      *
     c                   if        w_fvar <> *blank
      *
     c                   eval      jvarl1_fvar = w_fvar
     c     jvarl1_key    chain     jvarl1
     c                   if        not %found
     c                   eval      *in39 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      w_fogr = jvogrp
     c                   eval      w_fhgr = jvhgrp
     c                   eval      w_fugr = jvugrp
     c                   eval      w_fmod = jvmodn
     c                   eval      w_tek1 = jvtek1
      *
     c                   endif
      *
6.33  * Vare/leverandør
6.33  *
6.33 c                   if        c1fldo <> 0 and
6.33 c                             w_fvar <> *blank
6.33 c                   movel     w_fvar        jvlel1_lvnr
6.33 c                   eval      jvlel1_lldo = c1fldo
6.33 c     jvlel1_key    chain     jvlel1
6.33 c                   if        not %found
6.33 c                   eval      *in44 = *on
6.33 c                   eval      b_feil = *on
6.33 c                   leavesr
6.33 c                   endif
6.33  * Dato
6.33 c                   time                    w_date
6.33 c                   if        jvluda <> *loval and
6.33 c                             w_date >= jvluda
6.33 c                   eval      *in45 = *on
6.33 c                   eval      b_feil = *on
6.33 c                   leavesr
6.33 c                   endif
6.33  *
6.33 c                   eval      w_fogr = jvogrp
6.33 c                   eval      w_fhgr = jvhgrp
6.33 c                   eval      w_fugr = jvugrp
6.33 c                   eval      w_fmod = jvmodn
6.33 c                   eval      w_tek1 = jvtek1
6.33  *
6.33 c                   endif
6.3x  *
6.3x  * Prisgruppe
6.3x  *
6.3x c                   if        w_fprg <> *blank
6.31 c**                 eval      ra30l1_dkod = c1fprg
6.31 c**                 eval      ra30l1_dkod = k1fprg
6.31 c                   eval      ra30l1_dkod = w_fprg
6.3x c     ra30l1_key    chain     ra30l1
6.3x c                   if        not %found
6.3x c                   eval      *in43 = *on
6.3x c                   eval      b_feil = *on
6.3x c                   leavesr
6.3x c                   endif
6.3x c                   eval      w_ptxt = redtxt
6.3x c                   endif
      *
      * Overgruppe
      *
     c                   if        w_fogr <> 0
     c                   eval      vogrl1_oogr = w_fogr
     c     vogrl1_key    chain     vogrl1
     c                   if        not %found
     c                   eval      *in32 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_otxt = vgotxt
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_fhgr <> 0
      *
     c                   if        w_fogr = 0
     c                   eval      *in33 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      vhgrl1_hogr = w_fogr
     c                   eval      vhgrl1_hhgr = w_fhgr
     c     vhgrl1_key    chain     vhgrl1
     c                   if        not %found
     c                   eval      *in34 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      w_htxt = vghtxt
      *
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_fugr <> 0
      *
     c                   if        w_fogr = 0 or
     c                             w_fhgr = 0
     c                   eval      *in35  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      vugrl1_uogr = w_fogr
     c                   eval      vugrl1_uhgr = w_fhgr
     c                   eval      vugrl1_uugr = w_fugr
     c     vugrl1_key    chain     vugrl1
     c                   if        not %found
     c                   eval      *in36 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      w_utxt = vgutxt
      *
     c                   endif
      *
      * Varetype
      *
     c                   if        w_fvty <> *blank
     c                   eval      vvtyl1_fvty = w_fvty
     c     vvtyl1_key    chain     vvtyl1                             40
     c                   if        *in40 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_ttxt = vavtxt
     c                   endif
      *
     c                   if        w_fvty <> *blank and
     c                             w_fvty <> 'S'
     c                   eval      *in41 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Leveringstype
      *
     c                   if        w_flet <> 0
     c                   eval      vltpl1_flet = w_flet
     c     vltpl1_key    chain     vltpl1                             42
     c                   if        *in42 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_lbes = vybesk
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for aktivisering/passivisering av betingelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     aktiviser_bet begsr
      *
6.3x c                   eval      jfbelu_fprg = b1fprg
     c                   eval      jfbelu_fogr = b1fogr
     c                   eval      jfbelu_fhgr = b1fhgr
     c                   eval      jfbelu_fugr = b1fugr
     c                   eval      jfbelu_fmod = b1fmod
     c                   eval      jfbelu_fvar = b1fvar
     c                   eval      jfbelu_fvty = b1fvty
     c                   eval      jfbelu_flet = b1flet
     c     jfbelu_key    chain     jfbelur                            91
     c                   if        *in91 = *on
     c                   leavesr
     c                   endif
      *
      * Oppdaterer betingelse med valg
      *
     c                   update    jfbelur
      *
      * Oppdater subfile
      *
     c                   if        b1valg <> 0
     c                   eval      b1fkrt = jpfkrt
     c                   eval      b1fprt = jpfprt
     c                   eval      b1fton = jpfton
     c                   eval      b1fkub = jpfkub
     c                   endif
      *
     c                   endsr
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34  * Finn info om logistikkvare for riktig prisgiver
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34  *
6.34 c     finn_logivar  begsr
6.34  *
6.34 c                   eval      *in50 = *off
6.34 c                   eval      *in51 = *off
6.34 c
6.34 c                   if        %trim(w_jvqvnr) = ''
6.34 c                   leavesr
6.34 c                   endif
6.34 c
6.34 c                   eval      w_logl = ''
6.34 c                   eval      w_undv = 0
     c                   movel     w_jvqvnr      w_jvquno
6.34 c/exec sql
6.34 c+                  select jvquno, jvqvnr
6.34 c+                  into :w_undv, :w_logl
6.34 c+                  from jvklpf
6.34 c+                  where (jvquno = :w_jvquno or
6.34 c+                  jvqvnr = :w_jvqvnr)
6.34 c+                  and jvqfir = :w_firm
6.34 c+                  and jvqldo = :w_ldor
6.34 c+                  fetch first 1 rows only
6.34 c/end-exec
6.34 c                   if        w_logl = w_jvqvnr
6.34 c                   eval      *in51 = *on
6.34 c                   elseif    w_undv > 0
6.34 c                   eval      *in50 = *on
6.34 c                   endif
6.34 c
6.34 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_ffir
     c                   parm                    p_fldo
      *
6.33  * Key for posisjonering på avgifstkode
6.33  *
6.33 c     jvlel1_key    klist
6.33 c                   kfld                    jvlel1_lvnr
6.33 c                   kfld                    jvlel1_lldo
      *
      * Key for les på betingelse
      *
     c     jfbel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
6.3x c                   kfld                    jfbel1_fprg
     c                   kfld                    jfbel1_fogr
     c                   kfld                    jfbel1_fhgr
     c                   kfld                    jfbel1_fugr
     c                   kfld                    jfbel1_fmod
     c                   kfld                    jfbel1_fvar
     c                   kfld                    jfbel1_fvty
     c                   kfld                    jfbel1_flet
      *
      * Key for oppslag på betingelse
      *
     c     jfbelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
6.3x c                   kfld                    jfbelr_fprg
     c                   kfld                    jfbelr_fogr
     c                   kfld                    jfbelr_fhgr
     c                   kfld                    jfbelr_fugr
     c                   kfld                    jfbelr_fmod
     c                   kfld                    jfbelr_fvar
     c                   kfld                    jfbelr_fvty
     c                   kfld                    jfbelr_flet
      *
      * Key for les av betingelse
      *
     c     jfbel1_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
      *
      * Key for oppdatering av betingelse
      *
     c     jfbelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
6.3x c                   kfld                    jfbelu_fprg
     c                   kfld                    jfbelu_fogr
     c                   kfld                    jfbelu_fhgr
     c                   kfld                    jfbelu_fugr
     c                   kfld                    jfbelu_fmod
     c                   kfld                    jfbelu_fvar
     c                   kfld                    jfbelu_fvty
     c                   kfld                    jfbelu_flet
6.3x  *
6.3x  * Key for posisjonering på overgruppe
6.3x  *
6.3x c     jfbela_key    klist
6.3x c                   kfld                    w_firm
6.3x c                   kfld                    w_ldor
6.3x c                   kfld                    jfbela_fogr
      *
      * Key for posisjonering på hovedgruppe
      *
     c     jfbel2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    jfbel2_fhgr
      *
      * Key for posisjonering på undergruppe
      *
     c     jfbel3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    jfbel3_fugr
      *
      * Key for posisjonering på modulnummer
      *
     c     jfbel4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    jfbel4_fmod
      *
      * Key for posisjonering på varenummer
      *
     c     jfbel5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    jfbel5_fvar
      *
      * Key for posisjonering på varetype
      *
     c     jfbel6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    jfbel6_fvty
      *
      * Key for posisjonering på varenummer
      *
     c     jfbel7_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    jfbel7_flet
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på modul
      *
     c     jmodl1_key    klist
     c                   kfld                    jmodl1_fmod
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_fvar
      *
      * Key for oppslag på varetype
      *
     c     vvtyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvtyl1_fvty
      *
      * Key for oppslag på leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_flet
6.3x  *
6.3x  * Key for oppslag på prisgruppe
6.3x  *
6.3x c     ra30l1_key    klist
6.3x c                   kfld                    w_firm
6.3x c                   kfld                    ra30l1_dkod
      *
      *
      * Henter firma og leverandør fra parameter, og initierer skjermbildet
      *
     c                   eval      w_firm = p_ffir
     c                   eval      w_ldor = p_fldo
     c                   eval      b2fldo = p_fldo
      *
     c                   eval      jlevl1_ldor = p_fldo
     c     jlevl1_key    chain     jlevl1                             90
     c                   eval      b2lnav = jlnavn
      *
     c                   eval      b_visn   = *off
6.10 c                   eval      *in35 = *off
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     jfbel1_key    setll     jfbel1
      *
     c                   eval      *in22 = *on
     c                   exsr      crt_subfile
      *
     c                   endsr
