      /TITLE  ASOFAK Utskrift av fakturakopi
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FO681R
      * Beskrivelse . . : Utskrift av fakturakopi
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       110698 HKO  Nytt program
5.45  * R5.45 300605 AMD  Lagt inn dummy-parameter for å hindre
5.45  *                   forurensning av parameter-lister
6.20  * R6.20 030112 BSA  Vi lager egen rutine for kopi utskrift. Legger på et 1-tall bak
6.20  *                   rutinenavnet som ligger ordretypen. NB Forutsetter at ikke mer enn
6.20  *                   9 posisjoner er benyttet til rutinenavnet fra før.
6.nu  * R6.30 040614 bsa  Programmet gjennomgått mtp nummerutvidelser.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
6.20 faforl1    if   e           k disk    rename(aforpfr:aforl1r)
      *
      * Definering av datastrukturer
      *
      *  Datastruktur - parameterliste for kall av utskriftsprogram
      *
     d                 ds
6.nu d d_list                        34
     d  d_aarr                        4  0 overlay(d_list)
     d  d_otyp                        2    overlay(d_list:5)
6.nu d  d_fbin                        8  0 overlay(d_list:7)
6.nu d  d_tbin                        8  0 overlay(d_list:15)
6.nu d  d_prog                       10    overlay(d_list:23)
6.nu d  d_kopi                        1    overlay(d_list:33)
6.nu d  d_dumy                        1    overlay(d_list:34)
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_aarr          s              4  0
6.nu d p_binr          s              8  0
     d p_otyp          s              2
      *
     d w_firm          s              3  0
      *
      * Definering av variabler i nøkler
      *
     d votyl1_otyp     s                   like(vaotyp)
6.20 d aforl1_ruti     s                   like(afruti)
6.20 d d_orut          s                   like(vaorut)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter rutinenavn
      *
     c                   eval      votyl1_otyp = p_otyp
     c     votyl1_key    chain     votyl1                             60
     c                   if        *in60 = *on or
     c                             vaoakk <> 3
     c                   goto      avslutt
     c                   endif
6.20  * Hent "kopi" rutine hvis den finnes - hvis ikke benyttes den fra ordretypen
6.20 c                   eval      d_orut = vaorut
6.20 c                   eval      aforl1_ruti = %trim(vaorut) + '1'
6.20 c     aforl1_key    chain     aforl1                             61
6.20 c                   if        *in61 = *off
6.20 c                   eval      d_orut = aforl1_ruti
6.20 c                   endif
      *
      * Fyller datastruktur, og kaller styreprogram
      *
     c                   eval      d_aarr = p_aarr
     c                   eval      d_otyp = p_otyp
     c                   eval      d_fbin = p_binr
     c                   eval      d_tbin = p_binr
     c                   eval      d_kopi = 'K'
5.45 c                   eval      d_dumy = '*'
      *
     c                   call      'FO681C'
6.nu c                   parm                    d_list
6.20 c                   parm                    d_orut
6.20 c**                 parm                    vaorut
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_aarr
6.nu c                   parm                    p_binr
     c                   parm                    p_otyp
      *
      * Key for oppslag på ordretype-regsiter
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
6.20  *
6.20  * Key for oppslag på rutineregisteret
6.20  *
6.20 c     aforl1_key    klist
6.20 c                   kfld                    aforl1_ruti
      *
      *
      * Henter firmakode fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
