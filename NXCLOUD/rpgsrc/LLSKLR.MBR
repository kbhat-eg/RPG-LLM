     H/TITLE  ASLAGR: Startprogram for oppdatering/utskrift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  System. . . . . : ASLAGR                                       *
      *  Program . . . . : LLSKLR                                       *
      *  Beskrivelse . . : Startprogram for oppdatering/utskrift        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  Spesialversjon. :                                              *
      *  Tilpasset for . :                                              *
      *                                                                 *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.*
      *  --------- ------  ---------------------------------------------*
      *                                                                 *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     D                 DS
      *                                                      Parameter-UT
     D  DSPREC                 1     64
     D  DSNUMM                 1      6  0
     D  DSPERI                 7     12  0
     D  DSDATO                13     22d   datfmt(*iso)
      *
      *
     D LDA            UDS
     D  DSPRTF               800    809
     D  DSCHGV               844    844
      *
     D  l_firm               944    946  0
     D  l_fnav               951    980
      *
      * Definering av variable
      *
     d wwdato          s               d   datfmt(*iso)
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  B8 Behandle bilde for utskrift av lagerbunt                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C                   MOVE      *ZERO         WWVALG
     C                   MOVE      *BLANK        WWSVAR
     C                   MOVE      WPNUMM        DSNUMM
      *
     C     WPKODE        IFEQ      1
     C                   MOVE      *BLANK        WWKODE
     C                   ENDIF
     C     WPKODE        IFEQ      2
     C                   MOVE      '1'           WWKODE
     C                   ENDIF
     C     WPKODE        IFEQ      3
     C                   MOVE      '1'           WWSVAR
     C                   MOVE      '1'           WWKODE
     C                   ENDIF
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Henter inn periode og dato for transer m/regnskapsoverføring   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *  Nullstiller felter
      *
     C                   MOVE      *BLANK        WRETUR
     C                   MOVE      *ZERO         wwperi
     C                   MOVE      *loval        wwdato
      *
     C     WPKODE        IFEQ      3
      *
      *  Kaller opp program for henting av periode og dato
      *
     C                   CALL      'LQPERR'
     C                   PARM                    WRETUR
     C                   PARM                    wwperi
     C                   PARM                    wwdato
      *
      *  F3=Gå tilabke
      *
     C     WRETUR        CABEQ     '9'           XSLUTT
      *
     C                   ENDIF
      *
      *  Legger over informasjon til neste program
      *
     C                   MOVE      wwperi        DSPERI
     C                   MOVE      wwdato        DSDATO
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Inntasting er ferdig !  Oppstart av utvalg/utskriftsprogrammer *
      *  Kaller opp neste 'CL'-program                                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *  Legg inn verdi for bare henting av opplysninger
      *
     C                   MOVE      *BLANK        DSCHGV                          KODE VISE BILDE
     C                   MOVEL     'LL118'       DSPRTF                          RUTINE
     C                   OUT       *DTAARA
      *
      *  Kaller opp program før utskrift
      *
     C                   CALL      'AP100R'
     C                   PARM                    WPIN03
     C                   PARM                    WPIN12
      *
      *  Kaller opp subprogram
      *
     C                   CALL      'LL114C'
     C                   PARM                    DSPREC
     C                   PARM                    WWVALG
     C                   PARM                    WWSVAR
     C                   PARM                    WWKODE
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSLUTT        TAG
      *
     C                   SETON                                        LR
     C                   RETURN
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     *INZSR        BEGSR
      *
      *  Definisjon av local data area for output
      *
     C     *DTAARA       DEFINE    *LDA          LDA
      *
     C                   MOVE      *ZERO         WPIN03            1 0
     C                   MOVE      *ZERO         WPIN12            1 0
      *
     C                   MOVE      *BLANK        WRETUR            1
     C                   MOVE      *ZERO         wwperi            6 0
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     C                   MOVE      *ZERO         WWVALG            1 0
     C                   MOVE      *BLANK        WWSVAR            1
     C                   MOVE      *BLANK        WWKODE            1
      *
      *
      *  Redefinering av felter
      *
      *  Redefinering av parameterfeltene
      *
      *  Redefinering av nøkler
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    WPSTAT            1
     C                   PARM                    WPNUMM            6 0
     C                   PARM                    WPKODE            1 0
      *
     C                   ENDSR
