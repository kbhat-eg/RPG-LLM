     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VV561R
      * Beskrivelse . . : Vare, legg til sortiment
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       100903 HKO  Nytt program
      * 6.10  260609 BSA  Oppdatert med sporingsinformasjon
6.20  * 6.20  210212 BSA  Utvider med lager
6.30  * 6.30  280514 BKV  LVAR økt fra 15 til 20
6.31  * 6.30  171018 bof  Utvidet med trelast sortiment
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarla    if   e           k disk    rename(vvarpfr:vvarlar)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvsdtl1    if   e           k disk    rename(vsdtpfr:vsdtl1r)
     fvsdtlu    uf a e           k disk    rename(vsdtpfr:vsdtlur)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
      *
     fvv561d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vgufir)
     d p_vsor          s                   like(vdvsor)
     d p_ldor          s                   like(vdldor)
     d p_ogrp          s                   like(vdogrp)
     d p_hgrp          s                   like(vdhgrp)
     d p_ugrp          s                   like(vdugrp)
     d p_modn          s                   like(vdmodn)
6.20 d p_lage          s                   like(vdlage)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
6.20 d l_lage               1001   1002  0
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d vvarla_ldor     s                   like(vvldor)
     d vvarla_ogrp     s                   like(vvogrp)
     d vvarla_hgrp     s                   like(vvhgrp)
     d vvarla_ugrp     s                   like(vvugrp)
     d vvarla_nmod     s                   like(vvnmod)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d vsdtl1_vsor     s                   like(vdvsor)
6.20 d vsdtl1_lage     s                   like(vdlage)
     d vsdtl1_ldor     s                   like(vdldor)
     d vsdtl1_ogrp     s                   like(vdogrp)
     d vsdtl1_hgrp     s                   like(vdhgrp)
     d vsdtl1_ugrp     s                   like(vdugrp)
     d vsdtl1_modn     s                   like(vdmodn)
     d vsdtl1_vare     s                   like(vdvare)
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
      *
     d w_firm          s                   like(vvfirm)
     d w_vare          s                   like(vvvare)
6.31 d w_lv_lvar       s                   like(vvlvar)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(11)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.31 C/Exec SQL
6.31 C+  Set Option DatFmt=*ISO
6.31 C/End-Exec
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c     vvarla_key    setll     vvarla
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Legg til sortiment
      *
     c                   if        b1valg = 1
     c                   exsr      opprett
     c                   move      '->'          b1tek1
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Varedetaljer
      *
     c                   if        b1valg = 5
     c                   eval      w_vare = b1vare
     c                   call      'VV510R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c     vvarla_key    reade     vvarla
      *
     c                   if        %eof
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *

     c                   eval      b1valg = 0
     c                   eval      b1tek1 = vvtek1
     c                   eval      b1vare = vvvare
     c                   eval      b1nobb = vvnobb
6.30 c*                  eval      b1lvar = vvlvar
6.31  * sjekker om logistikk-vare
6.31 c                   eval      w_lv_lvar = *blank
6.31 c/exec sql
6.31 c+ select vvllva
6.31 c+ into   :w_lv_lvar
6.31 c+ from   vvlepf
6.31 c+ where  vvlvnr = :vvvare and
6.31 c+        vvlldo = :vvldor
6.31 c+ fetch first 1 rows only
6.31 c/end-exec
6.31 c                   if        w_lv_lvar <> *blank
6.31 c                   movel     w_lv_lvar     b1lvar
6.31 c                   else
6.30 c                   movel     vvlvar        b1lvar
6.31 c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å opprette i varesortimnet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opprett       begsr
      *
     c                   eval      vsdtl1_vsor = p_vsor
6.20 c                   eval      vsdtl1_lage = p_lage
     c                   eval      vsdtl1_ldor = p_ldor
     c                   eval      vsdtl1_ogrp = p_ogrp
     c                   eval      vsdtl1_hgrp = p_hgrp
     c                   eval      vsdtl1_ugrp = p_ugrp
     c                   eval      vsdtl1_modn = p_modn
     c                   eval      vsdtl1_vare = b1vare
     c     vsdtl1_key    chain     vsdtl1
     c                   if        %found
     c                   goto      end_opprett
     c                   endif
      *
      * Oppretter varesortiment-linje
      *
     c                   clear                   vsdtlur
      *
     c                   eval      vdvsor = p_vsor
     c                   eval      vdldor = p_ldor
     c                   eval      vdogrp = p_ogrp
     c                   eval      vdhgrp = p_hgrp
     c                   eval      vdugrp = p_ugrp
     c                   eval      vdmodn = p_modn
     c                   eval      vdvare = b1vare
      *
     c                   time                    vdodat
6.10 c                   time                    vdotim
6.10 c                   eval      vdousr = l_user
     c                   time                    vdedat
     c                   time                    vdetim
     c                   eval      vdeusr = l_user
      *
     c                   write     vsdtlur
      *
     c     end_opprett   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vsor
     c                   parm                    p_ldor
     c                   parm                    p_ogrp
     c                   parm                    p_hgrp
     c                   parm                    p_ugrp
     c                   parm                    p_modn
      *
      * Key for les på vare
      *
     c     vvarla_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarla_ldor
     c                   kfld                    vvarla_ogrp
     c                   kfld                    vvarla_hgrp
     c                   kfld                    vvarla_ugrp
     c                   kfld                    vvarla_nmod
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på varesortiment-linje
      *
     c     vsdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vsdtl1_vsor
6.20 c                   kfld                    vsdtl1_lage
     c                   kfld                    vsdtl1_ldor
     c                   kfld                    vsdtl1_ogrp
     c                   kfld                    vsdtl1_hgrp
     c                   kfld                    vsdtl1_ugrp
     c                   kfld                    vsdtl1_modn
     c                   kfld                    vsdtl1_vare
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      *
      * Henter parametre, og initierer skjermbildet
      *
     c                   eval      w_firm = p_firm
      *
     c                   eval      *in22 = *on
      *
     c                   eval      rlevl1_levr = p_ldor
     c     rlevl1_key    chain     rlevl1
      *
     c                   eval      vugrl1_uogr = p_ogrp
     c                   eval      vugrl1_uhgr = p_hgrp
     c                   eval      vugrl1_uugr = p_ugrp
     c     vugrl1_key    chain     vugrl1
      *
     c                   eval      b2vsor = p_vsor
     c                   eval      b2ldor = p_ldor
     c                   eval      b2lnav = rlnavn
     c                   eval      b2ogrp = p_ogrp
     c                   eval      b2hgrp = p_hgrp
     c                   eval      b2ugrp = p_ugrp
     c                   eval      b2utxt = vgutxt
     c                   eval      b2modn = p_modn
      *
     c                   call      'FO714C'
     c                   parm                    w_firm
     c                   parm                    p_modn
     c                   parm                    b2mte1
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      vvarla_ldor = p_ldor
     c                   eval      vvarla_ogrp = p_ogrp
     c                   eval      vvarla_hgrp = p_hgrp
     c                   eval      vvarla_ugrp = p_ugrp
     c                   eval      vvarla_nmod = p_modn
     c     vvarla_key    setll     vvarla
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
