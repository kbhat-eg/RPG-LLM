     h datedit(*dmy.) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  System. . . . . : ASLAGR                                       *
      *  Program . . . . : LO630R                                       *
      *  Beskrivelse . . : Utskrift av kontroll-liste                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  Spesialversjon. :                                              *
      *  Tilpasset for . :                                              *
      *                                                                 *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * --------- ------  ----------------------------------------------*
      * R5.21 200803 AMD  Lagt om til AP600R i stedet for AP100R
5.61  * R5.61 200902 AHX  Lister avvik på totalliste dersom < > enn     *
5.61  *                   inntastet                                     *
6.10  * R6.10 020713 bsa  Sjekker at ordretypen er av type fakt/kred type
6.nu  * R6.30 100614 bof  Utvidelser iforb. med nummerutvidelse         *
      *                                                                 *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     FLO630D    CF   E             WORKSTN
      *
     FVOTYL1    IF   E           K DISK    RENAME(VOTYPFR:VOTYL1R)
      *
     D                 DS
      *                                                      Filemember
     D  LDAMBR                 1     10
     D  LDABOK                 1      1
     D  LDAFIR                 2      4  0
     D  LDALST                 5     10  0
      *
     D                 DS
      *                                                      Parameter-inn
     D  DSPREC                 1     64
     D  DSOTYP                 1      2
6.nu D  DSFBES                 5     12  0
6.nu D  DSTBES                13     20  0
6.nu D  DSFLEV                21     26  0
6 nu D  DSTLEV                27     32  0
6.nu D  DSFDAT                33     42d   datfmt(*iso)
6.nu D  DSTDAT                43     52d   datfmt(*iso)
6.nu D  DSVAL1                53     53  0
6.nu D  DSVAL2                54     54  0
6.nu D  DSVAL3                55     55  0
6.nu D  DSVAL4                56     56  0
6.nu D  DSVAL5                57     57  0
6.nu D  DSVAL6                58     58  0
6.nu D  DSVAL7                59     59  0
6.un D  dsvva1                60     64  2
      *
      * Definering av Local Data Area
      *
     d lda            uds
     d  l_ruti               800    809
     d  l_chgv               844    844
     d  l_firm               944    946  0
     d  l_fnav               951    980
5.21  *
5.21  * Definering av variable
5.21  *                                                                -----
5.21 d w_in03          s              1
5.21 d w_tvbt          s              1
5.21 d w_btch          s              1
5.21  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandle bilde for registrering av parametre                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     B1TAG         TAG
      *
      *  Nullstiller variable som skal vises i skjermbildet
      *
     C                   MOVE      *BLANK        B1OTYP
     C                   MOVE      *ZERO         B1FBES
     C                   MOVE      *ZERO         B1TBES
     C                   MOVE      *ZERO         B1FLEV
     C                   MOVE      *ZERO         B1TLEV
     C                   MOVE      *ZERO         B1FDAT
     C                   MOVE      *ZERO         B1TDAT
      *
     C     B1TAGA        TAG
      *
     C     B1TAGB        TAG
     C                   EXFMT     B1BLD
      *
      *  F3=Gå tilbake  (Avslutt)
      *
     C     *INKC         CABEQ     *ON           XSLUTT
      *
      *  Sjekk   Finnes ordretype i ORDRE-TYPE-REGISTER
      *
     C                   MOVE      B1OTYP        OTYTYP
     C     OTYKEY        CHAIN     VOTYL1R                            90
     C     *IN90         IFEQ      *ON
     C                   MOVE      *ON           *IN31
     C                   GOTO      B1TAGB
     C                   END
6.10  *
6.10  *  Kontroll om ordre-
6.10  *  type er faktura-
6.10  *  ordretype
6.10  *
6.10 C     VAOAKK        IFNE      3
6.10 C                   MOVE      *ON           *IN32
6.10 C                   GOTO      B1TAGB
6.10 C                   ENDIF
      *
      *  Sjekk   Er dato fra større enn dato til
      *
     c                   eval      wwfdat = *loval
     c                   eval      wwtdat = *loval
      *
     C     B1FDAT        IFNE      *ZERO
     c     *dmy          move      b1fdat        wwfdat
     C                   END
      *
     C     B1TDAT        IFNE      *ZERO
     c     *dmy          move      b1tdat        wwtdat
     C                   END
      *
     C     WWFDAT        IFGT      WWTDAT
     C                   MOVE      *ON           *IN35
     C                   GOTO      B1TAGB
     C                   END
      *
      *  Legger inntastede opplysninger i Hjelpe-felter for Parameter
      *
     C                   MOVE      B1OTYP        DSOTYP
     C                   MOVE      B1FBES        DSFBES
     C                   MOVE      B1TBES        DSTBES
     C                   MOVE      B1FLEV        DSFLEV
     C                   MOVE      B1TLEV        DSTLEV
     C                   MOVE      WWFDAT        DSFDAT
     C                   MOVE      WWTDAT        DSTDAT
      *
      *  Dersom bestillingsnr til=0 skal t.o.m. alle bestillingsnr velges
      *
     C     DSTBES        IFEQ      *ZERO
     C                   MOVE      999999        DSTBES
     C                   END
      *
      *  Dersom leverandørnummer til=0 skal t.o.m. alle leverandører velges
      *
     C     DSTLEV        IFEQ      *ZERO
     C                   MOVE      999999        DSTLEV
     C                   END
      *
      *  Dersom dato til=0 skal t.o.m. alle datoer velges
      *
     C     B1TDAT        IFEQ      *ZERO
     C                   MOVE      *hival        DSTDAT
     C                   END
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandle bilde for valg av sortering/uttrekk osv.              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     C1TAG         TAG
      *
      *  Nullstiller variable som skal vises i skjermbildet
      *
5.61 C                   MOVE      5             C1VAL1
     C                   MOVE      *ZERO         C1VAL2
     C                   MOVE      *ZERO         C1VAL3
     C                   MOVE      *ZERO         C1VAL7
      *
     C     C1TAGA        TAG
      *
     C     C1TAGB        TAG
     C                   EXFMT     C1BLD
      *
      *  F3=Gå tilbake  (Avslutt)
      *
     C     *INKC         CABEQ     *ON           B1TAG
      *
      *  Legger inntastede opplysninger i Hjelpe-felter for Parameter
      *
     C                   MOVE      C1VAL1        DSVAL1
     C                   MOVE      C1VAL2        DSVAL2
     C                   MOVE      C1VAL3        DSVAL3
     C                   MOVE      0             DSVAL4
     C                   MOVE      C1VAL5        DSVAL5
     C                   MOVE      0             DSVAL6
     C                   MOVE      C1VAL7        DSVAL7
5.61 C                   z-add     c1avv1        dsvva1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Inntasting er ferdig !  Oppstart av utvalg/utskriftsprogrammer *
      *  Kaller opp neste 'CL'-program                                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *  Legg inn verdi for bare henting av opplysninger
      *
     C                   MOVE      *BLANK        l_chgv
     C                   MOVEL     'LO136'       l_ruti
     C                   OUT       *DTAARA
      *
      *  Kaller opp program før utskrift
      *
5.21 c                   call      'AP600R'
5.21 c                   parm                    l_ruti
5.21 c                   parm                    l_chgv
5.21 c                   parm                    w_tvbt
5.21 c                   parm                    w_in03
5.21 c                   parm                    w_btch
      *
      *  F4=Legge jobb på jobbkø
      *
     C     *INKD         IFEQ      *ON
     C                   MOVE      '1'           WWVALG
     C                   END
      *
      *  Henter siste nummer for File-member
      *
     C                   CALL      'LÅ900R'
     C                   PARM                    WCQNNN
     C                   PARM                    WCNUMM
      *
     C                   MOVE      'K'           LDABOK
     C                   MOVE      WCNUMM        LDALST
      *
      *  Kaller opp subprogram
      *
     C                   CALL      'LO632C'
     C                   PARM                    DSPREC
     C                   PARM                    LDAMBR
     C                   PARM                    WWVALG
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSLUTT        TAG
      *
     C                   SETON                                        LR
     C                   RETURN
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *INZSR        BEGSR
      *
      *  Definisjon av local data area for output
      *
     C     *DTAARA       DEFINE    *LDA          LDA
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     C                   MOVE      *ZERO         WWVALG            1
     C                   MOVE      *ZERO         WCQNNN            1
     C                   MOVE      *ZERO         WCNUMM            6 0
      *
      *  Redefinering av felter
      *
     C     *LIKE         DEFINE    DSFDAT        WWFDAT
     C     *LIKE         DEFINE    DSTDAT        WWTDAT
      *
      *  Redefinering av nøkler
      *
     C     *LIKE         DEFINE    VAOFIR        OTYFIR
     C     *LIKE         DEFINE    VAOTYP        OTYTYP
      *
      *  Key for file : ORDRE-TYPE-REGISTER
      *
     C     OTYKEY        KLIST
     C                   KFLD                    OTYFIR
     C                   KFLD                    OTYTYP
      *
      *  Legge inn firmanummer i nøkklene
      *
     C                   MOVE      l_firm        OTYFIR
     C                   MOVE      l_firm        LDAFIR
      *
     CSR                 ENDSR
