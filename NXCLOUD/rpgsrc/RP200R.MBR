     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RP200R
      * Beskrivelse . . : prosjekt-rapport vedlikehold 2013
      *
      * Spesialversjon. :             Prosk.leder
      * Tilpasset for . :             opprettet dato
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       250913 sst  Nytt program
6.20  *       280616 sst  Åpnet for kopiering mellom filgrupper
7.xx  * 7.xx  090516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frprrl1    if   e           k disk    rename(rprrpfr:rprrl1r)
     frprrl2    if   e           k disk    rename(rprrpfr:rprrl2r)
     frprrlr    if   e           k disk    rename(rprrpfr:rprrlrr)
     frprrlu    uf a e           k disk    rename(rprrpfr:rprrlur)
     filonl1    if   e           k disk    rename(ilonpfr:ilonl1r)
     fra07l1    if   e           k disk    rename(ra07pfr:ra07l1r)
     f
      *
     frp200d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
4.44 d lo              c                   'abcdefghijklmnopqrstuvwxyzæøå'
4.44 d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅ'
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
6.20 d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d rprrl1_firm     s                   like(rphfir)
     d rprrl1_rapp     s                   like(rphrap)
     d rprrl1_linj     s                   like(rphlin)
     d rprrl1_ulin     s                   like(rphuli)
     d rprrl2_firm     s                   like(rphfir)
     d rprrl2_besk     s                   like(rphtxt)
     d rprrl2_rapp     s                   like(rphrap)
     d rprrl3_firm     s                   like(rphfir)
     d rprrl3_rapp     s                   like(rphrap)
     d rprrlr_firm     s                   like(rphfir)
     d rprrlr_rapp     s                   like(rphrap)
     d rprrlr_linj     s                   like(rphlin)
     d rprrlr_ulin     s                   like(rphuli)
     d rprrlu_firm     s                   like(rphfir)
     d rprrlu_rapp     s                   like(rphrap)
     d rprrlu_linj     s                   like(rphlin)
     d rprrlu_ulin     s                   like(rphuli)
     d ilonl1_kli      s                   like(ilrkli)
     d ilonl1_lnr      s                   like(ilrlnr)
     d ra07l1_frm      s                   like(ragfir)
     d ra07l1_kod      s                   like(ragkod)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_oppr          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_subf          s              1    inz(*off)
      *
     d w_firm          s              3  0
     d s_rapp          s                   like(rphrap)
     d s_linj          s                   like(rphlin)
     d s_ulin          s                   like(rphuli)
6.20 d s_filg          s                   like(l_filg)
6.20 d s_firm          s                   like(rphfir)
      *
     d w_dato          s             10
     d w_datx          s             10
     d w_datn          s              6  0
     d BareLin00       s              1    inz('J')
     d BareRappX       s                   like(b1rap)
     d p_tx30          s             30
     d p_avd           s                   like(ilravd)
     d w_fper          s              6  0
     d w3avde          s              4
     d w3prol          s              5
     d w_lnr           s                   like(ilrlnr)
     d w_lnav          s                   like(ilrnav)
6.20 d w_sts           s              3
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(12)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   if        BareRappX > 0
     c                   eval      BareRappX = 0
     c                   eval      BareLin00 = 'J'
     c                   exsr      forny
     c                   goto      b2tagb
     c                   else
     c                   goto      avslutt
     c                   endif
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2rap  <> *zero  or
     c                             b2besk <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   if        b_subf = *on
     c                   goto      b2taga
     c                   endif
      *
      * Retur til første bilde
      *
     c                   goto      b2tagb
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   select
     c                   when      w_seqe = 'B'
     c     rprrl2_key    setll     rprrl2
     c                   other
     c     rprrl1_key    setll     rprrl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på rappjekt
      *
     c                   if        b2rap  <> *zero
     c                   eval      w_seqe = ' '
     c                   eval      rprrl1_rapp = b2rap
     c     rprrl1_key    setll     rprrl1
     c                   endif
      *
      * Posisjoner startverdi på teskt
      *
     c                   if        b2besk <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      rprrl2_besk = b2besk
     c                   eval      rprrl2_rapp = *zero
     c     rprrl2_key    setll     rprrl2
     c                   endif
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2rap  = *zero
     c                   eval      b2besk = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
     c                   eval      b_subf = *off
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      b_subf = *on
     c                   eval      w_virn = w_srrn
      *
      * Endre post
      *
     c                   if        b1valg = 2
     c                   if        b1rap  <> 9700
     c                   eval      c1rap  = b1rap
     c                   eval      c1lin  = b1lin
     c                   eval      c1ulin = b1ulin
     c                   exsr      xc2bld
     c                   endif
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopiere post
      *
     c                   if        b1valg = 3
     c                   eval      k1rap  = b1rap
     c                   eval      k1lin  = b1lin
     c                   eval      k1ulin = b1ulin
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slette post
      *
     c                   if        b1valg = 4
     c                   eval      d1rap = b1rap
     c                   eval      d1lin  = b1lin
     c                   eval      d1ulin = b1ulin
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise post
      *
     c                   if        b1valg = 5
     c                   eval      c1rap  = b1rap
     c                   eval      c1lin  = b1lin
     c                   eval      c1ulin = b1ulin
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Jobbe med detaljer
      *
     c                   if        b1valg = 6
     c                   eval      BareLin00 = 'N'
     c                   eval      BareRAppx = b1rap
     c                   eval      rprrl1_rapp = b1rap
     c* sven             exsr      forny
     c                   eval      b_forn = *on
     c                   eval      b1valg = 0
     c                   leave
     c                   endif
      *
      * Skrive rapport
      *
     c                   if        b1valg = 7
     c                   exsr      xc3bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1rap  = *zero
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Validering
      *
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      rprrlr_rapp = c1rap
     c     rprrlr_key    chain     rprrlr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      *in23 = *on
     c                   exsr      xc2bld
      *
     c     end_c1bld     tag
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   eval      b_oppr = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      rprrlr_rapp = c1rap
     c                   eval      rprrlr_linj = c1lin
     c                   eval      rprrlr_ulin = c1ulin
     c     rprrlr_key    chain     rprrlr
      *
     c                   if        %found
     c                   eval      c2besk = rphtxt
     c                   eval      c2fkto = rphktf
     c                   eval      c2fber = rphbef
     c                   eval      c2tkto = rphktt
     c                   eval      c2tber = rphbet
     c                   eval      c2lin1 = rphli1
     c                   eval      c2lin2 = rphli2
     c                   eval      c2frti = rphfrt
     c                   eval      c2funk = rphfun
     c                   eval      c2pros = rphpro
     c                   eval      c2spos = rphlpo
     c                   eval      c2skto = rphlkt
     c                   eval      c2slin = rphskr
     c                   eval      c2eusr = rpheus
     c     *dmy          move      rpheda        c2edat
     c                   else
     c                   eval      c2besk = *blank
     c                   eval      c2fkto = *zero
     c                   eval      c2fber = *zero
     c                   eval      c2tkto = *zero
     c                   eval      c2tber = *zero
     c                   eval      c2lin1 = *zero
     c                   eval      c2lin2 = *zero
     c                   eval      c2funk = *blank
     c                   eval      c2pros = *zero
     c                   eval      c2spos = *blank
     c                   eval      c2skto = *blank
     c                   eval      c2slin = *blank
     c                   eval      c2eusr = l_user
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1rap  = k1rap
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   setoff                                       232425
     c                   setoff                                       262728
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
      * Validering av input
      *
     c                   if        c2besk = *blank
     c                   eval      *in23 = *on
     c                   goto      c2tagb
     c                   endif
      *  Konto og linje
     c                   if        (c2fkto <> *zero or
     c                              c2tkto <> *zero)  and
     c                             (c2lin1 <> *zero or
     c                              c2lin2 <> *zero)
     c                   eval      *in28 = *on
     c                   goto      c2tagb
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   rprrlur
      *
     c                   eval      rprrlu_rapp = c1rap
     c                   eval      rprrlu_linj = c1lin
     c                   eval      rprrlu_ulin = c1ulin
     c     rprrlu_key    chain     rprrlur
      *
     c                   eval      rphtxt = c2besk
5.4d c*    lo:up         xlate     c2besk        rp1alf
     c                   eval      rphktf = c2fkto
     c                   eval      rphbef = c2fber
     c                   eval      rphktt = c2tkto
     c                   eval      rphbet = c2tber
     c                   eval      rphli1 = c2lin1
     c                   eval      rphli2 = c2lin2
     c                   eval      rphfrt = c2frti
     c                   eval      rphfun = c2funk
     c                   eval      rphpro = c2pros
     c                   eval      rphlpo = c2spos
     c                   eval      rphlkt = c2skto
     c                   eval      rphskr = c2slin
     c                   eval      rphfap = *blank
     c                   eval      rphfam = *blank
      *
     c                   time                    rpheda
     c                   time                    rpheti
     c                   eval      rpheus = l_user
      *
     c                   if        %found
     c                   update    rprrlur
     c                   else
     c                   eval      rphfir = w_firm
     c                   eval      rphrap = c1rap
     c                   eval      rphlin = c1lin
     c                   eval      rphuli = c1ulin
     c                   time                    rphoda
     c                   eval      rpheus = l_user
     c                   write     rprrlur
     c                   endif
      *
      * Oppdater subfile
      *    sven
     c                   if        b1valg = 2
     c                   eval      b1besk = rphtxt
     c                   eval      b1lin  = rphlin
     c                   eval      b1ulin = rphuli
     c                   eval      %subst(b1besk:19:42) = *blank
     c                   if        rphktf <> 0
     c                   eval      %subst(b1besk:20:4) = %char(rphktf)
     c                   endif
     c                   if        rphbef <> 0
     c                   eval      %subst(b1besk:25:4) = %char(rphbef)
     c                   endif
     c                   if        rphktt <> 0
     c*                  eval      %subst(b1besk:26:4) = %char(rphktt)
     c                   eval      %subst(b1besk:30:4) = %char(rphktt)
     c                   endif
     c                   if        rphbet <> 0
     c                   eval      %subst(b1besk:35:4) = %char(rphbet)
     c                   endif
     c                   if        rphli1 <> 0
     c                   eval      %subst(b1besk:40:4) = %char(rphli1)
     c                   endif
     c                   if        rphfrt  = 'J'
     c                   eval      %subst(b1besk:44:1) = '-'
     c                   endif
     c                   if        rphli2 <> 0
     c                   eval      %subst(b1besk:45:4) = %char(rphli2)
     c                   endif
     c                   eval      %subst(b1besk:50:1) = rphfun
     c                   if        rphpro <> 0
     c                   eval      %subst(b1besk:52:5) = %char(rphpro)
     c                   endif
     c                   eval      %subst(b1besk:58:1) = rphlpo
     c                   eval      %subst(b1besk:59:1) = rphlkt
     c                   eval      %subst(b1besk:60:1) = rphskr
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle utskrift av prosjekt  (C3BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc3bld        begsr
      *
     c                   eval      w_arow = 3
     c                   eval      w_acol = 5
     c                   eval      c3frap = *blank
     c                   eval      c3fper = *blank
     c                   eval      w_fper = 200000 + uyear*100 + umonth
     c                   move      w_fper        c3fper
     c                   move      w_fper        c3tper
     c                   eval      c3avde = *zero
     c                   eval      c3prol = *zero
     c                   move      b1rap         c3rapp
      *
     c     c3tagb        tag
      *
     c                   exfmt     c3bld
      *
     c                   if        *inkc or *inkl
     c                   goto      end_c3bld
     c                   endif
      *
      * Spørring
      *
      *  Prosjektleder
     c                   if        *inkd = *on and
5.33 c                             w_afld = 'C3PROL'
     c                   eval      w_lnr       = *zero
     c                   eval      w_lnav      = *blank
4.88 c                   call      'RP603R  '
4.55 c                   PARM                    w_firm
     c                   PARM                    w_lnr
     c                   PARM                    w_lnav
     c                   if        w_lnr <> *zero
     c                   move      w_lnr         c3prol
     c                   eval      c3prox = w_lnav
     c                   endif
     c                   goto      c3tagb
     c                   endif
      *  Avdeling
     c                   if        *inkd = *on and
5.33 c                             w_afld = 'C3AVDE'
     c                   eval      p_avd       = *zero
     c                   eval      p_tx30      = *blank
4.88 c                   call      'RA507R  '
4.55 c                   PARM                    w_firm
     c                   PARM                    p_avd
     c                   PARM                    p_tx30
     c                   if        p_avd <> *zero
     c                   move      p_avd         c3avde
     c                   eval      c3avdx = p_tx30
     c                   endif
     c                   goto      c3tagb
     c                   endif
      *
     c                   move      c3avde        w3avde
     c                   move      c3prol        w3prol
      *
     c                   select
     c                   when      c3rapp = '1'
     c                   call      'RP801C '
     c                   parm                    c3fper
     c                   parm                    c3tper
     c                   parm                    c3frap
     c                   parm                    c3tilp
     c                   parm                    c3rapp
     c                   parm                    w3avde
     c                   parm                    w3prol
     c                   when      c3rapp = '2'
     c                   call      'RP802C '
     c                   parm                    c3fper
     c                   parm                    c3tper
     c                   parm                    c3frap
     c                   parm                    c3rapp
     c                   parm                    w3avde
     c                   parm                    w3prol
     c                   other
     c                   endsl
      *
     c     end_c3bld     tag
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      rprrlr_rapp = d1rap
     c     rprrlr_key    chain     rprrlr
      *
     c                   if        d1lin > 0
     c                   eval      *in41 = *on
     c                   else
     c                   eval      *in41 = *off
     c                   endif
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      rprrlu_rapp = d1rap
     c                   eval      rprrlu_linj = d1lin
     c                   eval      rprrlu_ulin = d1ulin
     C                   if        d1lin <> 0
      *    Slette en enkelt linje
     c     rprrlu_key    chain     rprrlur
     c                   if        %found(rprrlu)
     c                   delete    rprrlur
     c                   endif
     c                   else
      *    Slette en en hel rapport
     c                   eval      rprrlu_linj = *zero
     c                   eval      rprrlu_ulin = *zero
     c     rprrlu_key    setll     rprrlu
     c                   read      rprrlu
     c                   dow       not %eof(rprrlu) and
     c                             rphrap = d1rap
     c                   delete    rprrlur
     c                   read      rprrlu
     c                   enddo
     c                   endif
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_rapp = k1rap
     c                   eval      s_linj = k1lin
     c                   eval      s_ulin = k1ulin
6.20 c                   eval      s_filg = l_filg
6.20 c                   eval      s_firm = l_firm
6.20 c                   eval      k1filg = l_filg
6.20 c                   eval      k1firm = l_firm
      *
      * Fyller kopierings-bildet
      *
     c                   eval      rprrlr_rapp = k1rap
     c                   eval      rprrlr_linj = k1lin
     c                   eval      rprrlr_ulin = k1ulin
     c     rprrlr_key    chain     rprrlr
      *
     c     k1tagb        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1rap  = *zero
     c                   eval      *in31 = *on
     c                   goto      k1tagb
     c                   endif
6.20  *    Kopiere til ny filgruppe sven
6.20 c                   if        k1filg <> s_filg or
6.20 c                             k1firm <> s_firm
6.20 c                   eval      w_sts = *blank
6.20 c                   call      'RP201C'
6.20 c                   parm                    k1filg
6.20 c                   parm                    k1firm
6.20 c                   parm                    s_rapp
6.20 c                   parm                    k1rap
6.20 c                   parm                    w_sts
6.20 c                   goto      end_k1win
6.20 c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      rprrlr_rapp = k1rap
     c                   eval      rprrlr_linj = k1lin
     c                   eval      rprrlr_ulin = k1ulin
     c     rprrlr_key    chain     rprrlr
     c                   if        %found
     c                   eval      *in33 = *on
     c                   goto      k1tagb
     c                   endif
      *    NB!  Kopiere hele rapport
     c                   if        k1lin = *zero
     c                   eval      rprrlr_rapp = s_rapp
     c                   eval      rprrlr_linj = s_linj
     c                   eval      rprrlr_ulin = *zero
     c     rprrlr_key    setll     rprrlr
     c                   read      rprrlr
     c                   dow       not %eof(rprrlr) and
     c                             rphrap = s_rapp
     c                   eval      rphrap = k1rap
     c                   write     rprrlur
     c                   read      rprrlr
     c                   enddo
      *
     c                   else
     c                   eval      rphrap = k1rap
     c                   eval      rphlin = k1lin
     c                   eval      rphuli = k1ulin
     c                   write     rprrlur
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c*                  eval      c1rap  = s_rapp
     c*                  eval      c1lin  = s_linj
     c*                  eval      c1ulin = s_ulin
     c*                  exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      rprrl2
     c                   other
     c                   read      rprrl1                                 15
     c                   endsl
      *
5.61 c                   if        *in15  or
5.61 c                             rphfir <> w_firm
     c                   leave
     c                   endif
      *
     c                   if        BareLin00 = 'J' and
     c                             rphlin <> *zero
     c                   iter
     c                   endif
      *
     c                   if        BareRappX > 0   and
     c                             rphrap <> BareRappX
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1rap  = rphrap
     c                   eval      b1besk = rphtxt
     c                   eval      b1lin  = rphlin
     c                   eval      b1ulin = rphuli
     c                   eval      %subst(b1besk:19:42) = *blank
     c                   if        rphktf <> 0
     c                   eval      %subst(b1besk:20:4) = %char(rphktf)
     c                   endif
     c                   if        rphbef <> 0
     c                   eval      %subst(b1besk:25:4) = %char(rphbef)
     c                   endif
     c                   if        rphktt <> 0
     c                   eval      %subst(b1besk:30:4) = %char(rphktt)
     c                   endif
     c                   if        rphbet <> 0
     c                   eval      %subst(b1besk:35:4) = %char(rphbet)
     c                   endif
     c                   if        rphli1 <> 0
     c                   eval      %subst(b1besk:40:4) = %char(rphli1)
     c                   endif
     c                   if        rphfrt  = 'J'
     c                   eval      %subst(b1besk:44:1) = '-'
     c                   endif
     c                   if        rphli2 <> 0
     c                   eval      %subst(b1besk:45:4) = %char(rphli2)
     c                   endif
     c                   eval      %subst(b1besk:50:1) = rphfun
     c                   if        rphpro <> 0
     c                   eval      %subst(b1besk:52:5) = %char(rphpro)
     c                   endif
     c                   eval      %subst(b1besk:58:1) = rphlpo
     c                   eval      %subst(b1besk:59:1) = rphlkt
     c                   eval      %subst(b1besk:60:1) = rphskr
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på prosjekt-rapport
      *
     c     rprrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rprrl1_rapp
     c                   kfld                    rprrl1_linj
     c                   kfld                    rprrl1_ulin
      *
      * Key for posisjonering på tekst
      *
     c     rprrl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rprrl2_besk
     c                   kfld                    rprrl2_rapp
      *
      * Key for les på rapport
      *
     c     rprrlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rprrlr_rapp
     c                   kfld                    rprrlr_linj
     c                   kfld                    rprrlr_ulin
      *
      * Key for oppdatering av rappjket
      *
     c     rprrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rprrlu_rapp
     c                   kfld                    rprrlu_linj
     c                   kfld                    rprrlu_ulin
      *
      * Key for oppslag på Prosjektleder
      *
     c     ilonl1_key    klist
     c                   kfld                    ilonl1_kli
     c                   kfld                    ilonl1_lnr
      *
      * Key for oppslag på avdeling
      *
     c     ra07l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra07l1_kod
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjermbildet
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      w_seqe      = *blank
     c                   eval      rprrl1_rapp = *zero
     c                   eval      rprrl1_linj = *zero
     c                   eval      rprrl1_ulin = *zero
     c     rprrl1_key    setll     rprrl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
