     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : EK715R
      * Beskrivelse . . : Kunde bonus, vedlikehold - parameter (liste)
      *
      *                   Bonusberegning m/kontrolliste og/eller kreeditnota
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       070911 bsa  Nytt program
6.20  *       020513 bsa  Lagt til periode for bonusberegning
6.21  *       261113 bsa  Lagt til fra-periode på bonusberegning
6.30  *       140318 bsa  Feil i frimakode
7.xx  * 7.00  150316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  090919 bkv  Aktivert tekster til kundebrev
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvbotl1    if   e           k disk    rename(vbotpfr:vbotl1r)
     fek715d    cf   e             workstn
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d lda            uds
     d  l_dato               101    108
     d  l_tek1               201    270
     d paarf                 300    303  0                                      Pr. år
     d paart                 304    307  0                                      Pr. år
     d pkatf                 308    311  0                                      Kategori f.o.m.
     d pkatt                 312    315  0                                      Kategori t.o.m.
     d pknrf                 316    321  0                                      Knr. f.o.m.
     d pknrt                 322    327  0                                      Knr. t.o.m.
     d ptbot                 328    329                                         Bonustype
     d  l_tek2               401    470
     d  l_tek3               501    570
     d  l_user               911    920
7.00 d l_filg                931    933
     d  l_firm               944    946  0
     d  l_fnav               951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
      *
     d w_udat          s               d   datfmt(*iso)
     d w_udat_8        s              8
     d w_firm_alf      s              3
     d w_kund_alf      s              6
     d w_paar_alf      s              4
6.11 d w_tbot          s              2
     d w_m1ar          s              4  0
     d w_naar          s              4  0
6.20 d w_peri          s              2  0
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
      *
     d w_firm          s                   like(vatfir)
     d w_tbes          s             35
     d w_text          s             35
     d b_kuti          s              1    inz(*off)
      *
7.00   dcl-s co402_verdi1  char(1);
7.00   dcl-s co402_verdi2  char(2048);
7.00   DCL-PR CO402R EXTPGM('CO402R');
7.00     p_filgrp            char(3)     const;
7.00     p_firm              packed(3:0) const;
7.00     p_lager             packed(2:0) const;
7.00     p_nokkel            char(50)    const;
7.00     p_verdi1            char(1);
7.00     p_verdi2            char(2048);
7.00   END-PR;
7.01 d u_701           s              1    inz(*off)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * P1WIN  - P1 Vindu for starte beregnign av årsbonus for alle kunder
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
      * Fyller bildet
      *
     c                   exfmt     p1win
      *
     c                   if        *inkc or *inkl
     c                   goto      avslutt
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd = *on
     c                   exsr      sr_spor
     c                   goto      b2taga
     c                   endif
      *
      * Validering av valg
      *
      * År
     c                   move      p1paar        w_naar
     c                   if        w_naar < 2000
6.21 c                   eval      *in30 = *on
     c                   goto      b2taga
     c                   endif
6.21  * Fra periode
6.21 c                   move      p0peri        w_peri
6.21 c                   if        w_peri < 1   or
6.21 c                             w_peri > 12
6.21 c                   eval      *in36 = *on
6.21 c                   goto      b2taga
6.21 c                   endif
6.20  * Periode
6.20 c                   move      p1peri        w_peri
6.20 c                   if        w_peri < 1   or
6.20 c                             w_peri > 12
6.20 c                   eval      *in36 = *on
6.20 c                   goto      b2taga
6.20 c                   endif
6.21  * Periode fra/til
6.21 c                   if        %int(p0peri) > %int(p1peri)
6.21 c                   eval      *in36 = *on
6.21 c                   goto      b2taga
6.21 c                   endif
      * Alt1 = 1 Ny beregning først
     c                   if        p1alt1 <> '0' and
     c                             p1alt1 <> ' ' and
     c                             p1alt1 <> '1'
     c                   eval      *in32 = *on
     c                   goto      b2taga
     c                   endif
      * Alt2 = 1 Kontrolliste
     c                   if        p1alt2 <> '0' and
     c                             p1alt2 <> ' ' and
     c                             p1alt2 <> '1'
     c                   eval      *in33 = *on
     c                   goto      b2taga
     c                   endif
      * Alt3 = 1 Kreditnota
     c                   if        p1alt3 <> '0' and
     c                             p1alt3 <> ' ' and
     c                             p1alt3 <> '1'
     c                   eval      *in34 = *on
     c                   goto      b2taga
     c                   endif
      *
      * Info til kundebrev. Er ikke aktivert (enda)
      *
7.01 c                   if        u_701 = *on
     c                   if        p1alt3 = '1'
     c                   exfmt     p2win
     c                   endif
      *
     c                   if        *inkc or *inkl
     c                   goto      b2taga
     c                   endif
      *
7.01 c                   if        u_701 = *on
7.01 c                   eval      l_dato = p2paar
7.01 c                   eval      l_tek1 = p2tek1
7.01 c                   eval      l_tek2 = p2tek2
7.01 c                   eval      l_tek3 = p2tek3
7.01 c                   endif
      *
     c                   endif
      *
6.30 c                   move      l_firm        w_firm_alf
     c                   if        p1alt2 = '1' or
7.01 c                             (p1alt3 = '1' and u_701 = *on)
     c                   eval      paarf = %int(p1paar)
     c                   eval      paart = %int(p1paar)
     c                   eval      pkatf = 0
     c                   eval      pkatt = 9999
     c                   eval      pknrf = 0
     c                   eval      pknrt = 999999
     c                   eval      ptbot = p1tbot
     c                   out       *dtaara
     c                   endif
      *
     c                   call      'EK720C'
     c                   parm                    w_firm_alf
     c                   parm                    p1paar
     c                   parm                    p1alt1
     c                   parm                    p1alt2
     c                   parm                    p1alt3
6.11 c                   parm                    p1tbot
6.21 c                   parm                    p0peri
6.20 c                   parm                    p1peri
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for soørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sr_spor       begsr
      *
     c                   if        w_afld = 'P1TBOT'
     c                   eval      p1ttxt = *blank
     c                   call      'VA565R'
     c                   parm                    w_firm
     c                   parm                    w_tbot
     c                   parm                    w_tbes
      *
     c                   if        w_tbot <> *blank
     c                   eval      p1tbot = w_tbot
     c                   movel     w_tbes        p1ttxt
     c                   endif
     c                   endif
      *
     c*                  if        w_afld = 'P1OTYP'
     c*                  eval      p1otyp = *blank
     c*                  call      'VA550R'
     c*                  parm                    w_firm
     c*                  parm                    p1otyp
     c*                  endif
      *
     c*                  if        w_afld = 'P1VARE'
     c*                  eval      p1vare = *blank
     c*                  eval      w_text = *blank
     c*                  call      'VV500R'
     c*                  parm                    w_firm
     c*                  parm                    p1vare
     c*                  parm                    w_text
     c*                  endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *dtaara       define    *lda          lda
      *
      * Henter firma fra LDA
      *
6.20 c                   eval      p1paar = %char(uyear+2000)
6.20 c                   if        umonth-1 > 9
6.20 c                   eval      p1peri = %char(umonth-1)
6.20 c                   else
6.20 c                   eval      p1peri = '0' + %char(umonth-1)
6.20 c                   endif
6.21 c                   eval      p0peri = '01'
     c                   eval      w_firm = l_firm
     c                   time                    w_date
     c                   time                    w_time
7.01  *
7.01  * Endring 7.01
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'EK715R_701':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_701 = *on;
7.01   endif;
      *
      * Initierer skjermbildet
      *
     c                   endsr
