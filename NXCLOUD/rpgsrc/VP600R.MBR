     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VP600R
      * Beskrivelse . . : Vare-pris, utskrift av prisbok - parameter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       270599 HKO  Nytt program
      *       230101 HKO  Lagt inn membernavn ved kall til utskriftsprogram
      *       270606 HKO  Lagt inn valg av leveringstype
5.70  * prgr  280509 HKO  Lagt inn valg av lager i forb. prisgruppe
6.20  *       111110 bof  Utvidet med lager på varesortiment
7.xx  * 7.00  290916 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  *k00  030620 BOF NXS-1195  kompilert pga tab.utvidelse
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvshel1    if   e           k disk    rename(vshepfr:vshel1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
      *
     fvp600d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Datastruktur for parameterliste -ut
      *
     d                 ds
     d d_list                        64
     d  d_firm                        3  0 overlay(d_list)
     d  d_vsor                       10    overlay(d_list:4)
     d  d_ogrp                        2  0 overlay(d_list:14)
     d  d_hgrp                        2  0 overlay(d_list:16)
     d  d_ugrp                        3  0 overlay(d_list:18)
     d  d_ldor                        6  0 overlay(d_list:21)
     d  d_sval                        1  0 overlay(d_list:27)
     d  d_tval                        1  0 overlay(d_list:28)
     d  d_rval                        1  0 overlay(d_list:29)
     d  d_2val                        1  0 overlay(d_list:30)
     d  d_lval                        1  0 overlay(d_list:31)
     d  d_lety                        1  0 overlay(d_list:32)
     d  d_dato                         d   overlay(d_list:33) datfmt(*iso)
5.70 d  d_lage                        2  0 overlay(d_list:45)
      *
      * Definering av variabler i nøkler
      *
     d vshel1_vsor     s                   like(vcvsor)
6.20 d vshel1_lage     s                   like(vclage)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d vltpl1_lety     s                   like(vylety)
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_udat          s               d   datfmt(*iso)
     d w_tmst          s               z
     d w_tmst_20       s             20
     d w_memb          s             10
      *
     d w_firm          s                   like(vcfirm)
5.70 d w_jkod          s              2  0
5.70 d w_jtxt          s             20
5.70 d w_stat          s              1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandling av skjermbilde
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
      * Kaller utskriftsprogram
      *
     c                   eval      d_firm = w_firm
     c                   eval      d_vsor = b1vsor
     c                   eval      d_ogrp = b1ogrp
     c                   eval      d_hgrp = b1hgrp
     c                   eval      d_ugrp = b1ugrp
     c                   eval      d_ldor = b1ldor
     c                   eval      d_sval = b1sval
     c                   eval      d_tval = b1tval
     c                   eval      d_rval = b1rval
     c                   eval      d_2val = b12val
     c                   eval      d_lval = b1lval
     c                   eval      d_lety = b1lety
     c     *dmy          move      b1dato        d_dato
5.70 c                   eval      d_lage = b1lage
      *
     c                   time                    w_tmst
     c     *iso0         move      w_tmst        w_tmst_20
      *
     c                   eval      w_memb = 'P' + %subst(w_tmst_20:9:9)
      *
     c                   call      'VP600C'
     c                   parm                    d_list
     c                   parm                    w_memb
      *
     c                   goto      b1taga
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av spørringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Varesortiment
      *
     c                   if        w_afld = 'B1VSOR'
     c                   call      'VC500R'
     c                   parm                    w_firm
     c                   parm                    b1vsor
6.20 c                   parm                    b1lage
     c                   parm                    b1besk
     c                   leavesr
     c                   endif
      *
      * Overgruppe
      *
     c                   if        w_afld = 'B1OGRP'
     c                   eval      b1hgrp = 0
     c                   eval      b1ugrp = 0
     c                   eval      b1htxt = *blank
     c                   eval      b1utxt = *blank
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1otxt
     c                   leavesr
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_afld = 'B1HGRP'
     c                   eval      b1ugrp = 0
     c                   eval      b1utxt = *blank
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    b1htxt
     c                   leavesr
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_afld = 'B1UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    b1ugrp
     c                   parm                    b1utxt
     c                   leavesr
     c                   endif
      *
      * Leverandør
      *
     c                   if        w_afld = 'B1LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    b1ldor
     c                   parm                    b1lnav
     c                   leavesr
     c                   endif
      *
      * Leveringstype
      *
     c                   if        w_afld = 'B1LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    b1lety
     c                   parm                    b1lbes
     c                   leavesr
     c                   endif
      *
5.70 c                   if        w_afld = 'B1LAGE'
5.70 c                   call      'RA510R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    b1lage
5.70 c                   parm                    b1lana
5.70 c                   leavesr
5.70 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
     c                   eval      b1besk = *blank
     c                   eval      b1otxt = *blank
     c                   eval      b1htxt = *blank
     c                   eval      b1utxt = *blank
     c                   eval      b1lnav = *blank
     c                   eval      b1lbes = *blank
      *
      * Varesortiment
      *
     c                   if        b1vsor <> *blank
     c                   if        b1ogrp <> 0 or
     c                             b1hgrp <> 0 or
     c                             b1ugrp <> 0 or
     c                             b1ldor <> 0
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b1vsor <> *blank
     c                   eval      vshel1_vsor = b1vsor
6.20 c                   eval      vshel1_lage = b1lage
6.20 c     vshel1_key    chain     vshel1
6.20 c                   if        not %found(vshel1)
6.20 c                   eval      vshel1_lage = 0
6.20 c     vshel1_key    chain     vshel1
6.20 c                   if        not %found(vshel1)
6.20 c                   eval      *in32 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
6.20 c                   endif
     c                   eval      b1besk = vcbesk
     c                   endif
      *
      * Overgruppe
      *
     c                   if        b1ogrp <> 0
     c                   eval      vogrl1_oogr = b1ogrp
     c     vogrl1_key    chain     vogrl1                             33
     c                   if        *in33 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1otxt = vgotxt
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        b1hgrp <> 0
     c                   eval      vhgrl1_hogr = b1ogrp
     c                   eval      vhgrl1_hhgr = b1hgrp
     c     vhgrl1_key    chain     vhgrl1                             34
     c                   if        *in34 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1htxt = vghtxt
     c                   endif
      *
      * Undergruppe
      *
     c                   if        b1ugrp <> 0
     c                   eval      vugrl1_uogr = b1ogrp
     c                   eval      vugrl1_uhgr = b1hgrp
     c                   eval      vugrl1_uugr = b1ugrp
     c     vugrl1_key    chain     vugrl1                             35
     c                   if        *in35 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1utxt = vgutxt
     c                   endif
      *
      * Leverandør
      *
     c                   if        b1ldor <> 0
     c                   eval      rlevl1_levr = b1ldor
     c     rlevl1_key    chain     rlevl1                             36
     c                   if        *in36 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1lnav = rlnavn
     c                   endif
      *
      * Leveringstype
      *
     c                   eval      vltpl1_lety = b1lety
     c     vltpl1_key    chain     vltpl1                             37
     c                   if        *in37 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      b1lbes = vybesk
5.70  *
5.70  * Lager
5.70  *
5.70 c                   eval      w_jkod = b1lage
5.70 c                   call      'RS710R  '
5.70 c                   parm                    w_stat
5.70 c                   parm                    w_jkod
5.70 c                   parm                    w_jtxt
5.70 c                   if        w_stat <> *blank
5.70 c                   eval      *in39  = *on
5.70 c                   eval      b_feil = *on
5.70 c                   leavesr
5.70 c                   endif
5.70 c                   movel     w_jtxt        b1lana
      *
      * Dato
      *
     c                   if        b1dato = 0
     c     *dmy          move      w_udat        b1dato
     c                   endif
      *
     c                   if        b1dato <> 0
     c     *dmy          test(d)                 b1dato                 38
     c                   if        *in38
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på varesortiment-hode
      *
     c     vshel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vshel1_vsor
6.20 c                   kfld                    vshel1_lage
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      *
      * Henter firmakode fra parameter
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjerm-felter
      *
     c                   time                    w_udat
     c     *dmy          move      w_udat        b1dato
      *
     c                   endsr
