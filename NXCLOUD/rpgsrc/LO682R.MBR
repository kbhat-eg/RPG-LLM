     H/TITLE  ASOFAK: Inntasting av parameter for ordre-utskrift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO682R
      * Beskrivelse . . : Inntasting av parametre ved faktura-kopier
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     FLO682D    CF   E             WORKSTN
     FVOTYL1    IF   E           K DISK
     F                                     RENAME(VOTYPFR:VOTYL1R)
      *
      *
     D                 DS
     D  DSPARM                 1     30
     D  DSAARR                 1      4  0
     D  DSOTYP                 5      6
     D  DSFBIN                 7     12  0
     D  DSTBIN                13     18  0
     D  DSPROG                19     28
     D  DSKOPI                29     29
      *
      * - - - - - - - - - - - - - -
      * Henter inn data fra LDA
      * - - - - - - - - - - - - - -
      *
     D                UDS
     D  DSRUTI               800    809
     D  DSSFIR               944    946  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  A1 Behandler valg for utskrift av FAKTURA/KREDITNOTA-kopier    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     A1TAG         TAG
      *
      *  Nullstiller felter som benyttes
      *
     C                   MOVE      UYEAR         A1AARR
     C                   MOVE      *ZERO         A1FBIN
     C                   MOVE      *ZERO         A1TBIN
     C                   MOVE      *BLANK        A1OTYP
      *
     C     A1AARR        IFGT      80
     C                   ADD       1900          A1AARR
     C                   ELSE
     C                   ADD       2000          A1AARR
     C                   ENDIF
      *
     C     A1TAGA        TAG
     C                   EXFMT     A1BLD
      *
      *  F3=Avslutt
      *
     C     *INKC         IFEQ      *ON
     C                   Z-ADD     1             WPPF03
     C                   GOTO      XSLUTT
     C                   END
      *
      *  F4=Spørring
      *
     C     *INKD         IFEQ      *ON
     C                   CALL      'VA550R'
     C                   PARM                    OTYFIR
     C                   PARM                    A1OTYP
      *
     C                   GOTO      A1TAGA
     C                   END
      *
      *  Kontroll om det
      *  er oppgitt riktig
      *  ordretype
      *
     C                   MOVE      DSSFIR        OTYFIR
     C                   MOVE      A1OTYP        OTYTYP
     C     OTYKEY        CHAIN     VOTYL1R                            61
     C     *IN61         IFEQ      *ON
     C                   MOVE      *ON           *IN31
     C                   GOTO      A1TAGA
     C                   ENDIF
      *
      *  Kontroll om ordre-
      *  type er faktura-
      *  ordretype
      *
     C     VAOAKK        IFNE      3
     C                   MOVE      *ON           *IN32
     C                   GOTO      A1TAGA
     C                   ENDIF
      *
      *  Kontroll om fra
      *  er større enn til
      *  dersom til er oppgitt
      *
     C     A1TBIN        IFNE      *ZERO
     C     A1FBIN        IFGT      A1TBIN
     C                   MOVE      *ON           *IN33
     C                   GOTO      A1TAGA
     C                   ENDIF
     C                   ENDIF
      *
      *  Dersom til ikke er
      *  oppgitt, legges fra
      *  ut i til
      *
     C     A1TBIN        IFEQ      *ZERO
     C                   Z-ADD     A1FBIN        A1TBIN
     C                   ENDIF
      *
     C                   MOVE      A1OTYP        DSOTYP
     C                   MOVE      A1AARR        DSAARR
     C                   MOVE      A1FBIN        DSFBIN
     C                   MOVE      A1TBIN        DSTBIN
     C                   MOVE      'K'           DSKOPI
     C                   MOVE      DSPARM        WPPARM
      *
     C                   MOVE      VAORUT        DSRUTI
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSLUTT        TAG
      *
     C                   SETON                                        LR
     C                   RETURN
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     *INZSR        BEGSR
      *
      *  Definering av nøkler
      *
     C     *LIKE         DEFINE    VAOFIR        OTYFIR
     C     *LIKE         DEFINE    VAOTYP        OTYTYP
      *
      * Key for file : ORDRE-TYPE-REGISTER
      *
     C     OTYKEY        KLIST
     C                   KFLD                    OTYFIR
     C                   KFLD                    OTYTYP
      *
      *  Parameterliste
      *
     C     *ENTRY        PLIST
     C                   PARM                    WPPARM           30
     C                   PARM                    WPPF03            1 0
      *
     C                   ENDSR
