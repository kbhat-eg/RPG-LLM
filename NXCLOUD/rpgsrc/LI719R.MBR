     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LI719R
      * Beskrivelse . . : Sjekker og setter aktiv status for automatisk EDI innlesing
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       071116 bsa  Nytt program
9.xx  *       101116 bsa  Kun for test
7.01  * K000  050620 bsa  Overføringsanmodning kommer som parameter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffjoblu    uf   e           k disk    rename(fjobpfr:fjoblur)
     fgedglu    uf   e           k disk    rename(gedgpfr:gedglur)
     ffjobl1    if   e           k disk    rename(fjobpfr:fjobl1r)
      *
      * Definering av Local Data Arae (LDA)
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Parameter-inn
      *
7.01 d p_ovrf          s             10
     d p_stat          s              1
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variable i parametre
      *
      *
      * Definering av variable
      *
     d b_forn          s              1    inz(*off)
      *
     d w_firm          s                   like(fajfir)
     d w_stat          s              1
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_year          s              4  0
     d w_jsta          s              2  0
     d w_onof          s              1  0
7.01 d w_ovrf          s             10
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(10)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandle åpningsopplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      p_stat = *blanks
      *
     c                   exsr      sjekk_sub
      *
      * Avslutt program
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for automatisk oppdatering av godkjente EDI meldinger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_sub     begsr
      *
      * Sjekk om overføring allerede pågår. (jobbstatus register)
      *
     c     fjobl1_key    chain     fjobl1r
     c                   if        %found     and
     c                             fajb07 = 1
     c                   eval      p_stat = '1'
     c                   goto      end_sjekk
     c                   endif
      *
      * Sjekk om overføring allerede pågår. (overføringsanm er aktiv) - se LI700C
      *
     c                   eval      w_stat = *blanks
      *
     c                   call      'AF775C'
7.01 c                   parm                    w_ovrf
     c                   parm                    w_stat
      *
      * Overføring pågår eller finnes ikke.
      *
     c                   if        w_stat  = '1'
     c                   eval      p_stat = '1'
     c                   goto      end_sjekk
     c                   endif
      *
      * Sjekk om det ligger igjen data fra forrige kjøring - venter
      *
     c                   eval      w_stat = *blanks
      *
9.xx c**                 call      'XF771C'
9.xx c**                 parm                    w_stat
      *
      * File fra forrige kjøring ligger igjen - LEDITMP
      *
     c                   if        w_stat  = '1'
     c                   eval      p_stat = '1'
     c                   goto      end_sjekk
     c                   endif
      *
      * Oppdater jobb-register med aktiv-flagg
      *
     c     fjoblu_key    chain     fjoblur
     c                   if        %found
     c                   eval      fajb07 = 1
     c                   update    fjoblur
     c                   endif
      *
      * Oppdater automatisk oppdatering med starttidspunkt - FTP overføring
      *
     c     gedglu_key    chain     gedglur
     c                   if        %found    and
     c                             ggstat = 1
     c                   time                    ggfdat
     c                   time                    ggftid
     c                   update    gedglur
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
7.01 c                   parm                    p_ovrf
     c                   parm                    p_stat
      *
      * Key for file : Jobb-register
      *
     c     fjobl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for file : Jobb-register
      *
     c     fjoblu_key    klist
     c                   kfld                    w_firm
      *
      * Key for file : Automatisdk oppdatering register
      *
     c     gedglu_key    klist
     c                   kfld                    w_firm
      *
      * Henter verdier fra parmametre
      *
     c                   time                    w_date
     c                   time                    w_time
     c                   eval      w_year = %subdt(w_date:*years)
     c                   eval      w_firm = l_firm
7.01 c                   eval      w_ovrf = p_ovrf
      *
     c                   endsr
