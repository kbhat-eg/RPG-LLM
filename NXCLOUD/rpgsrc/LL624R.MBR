      /TITLE  Utskrift av lagerverdiliste
     h datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Program . . . . : LE226R                                        *
      * Beskrivelse . . : Utskrift av lagerverdiliste                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      *                                                                 *
      *                                                                 *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
     flwa1l1    ip   e           k disk
     f                                     rename(lwa1pfr:lwa1l1r)
      *                                                                 *
     fvvarl1    if   e           k disk
     f                                     rename(vvarpfr:vvarl1r)
     fvogrl1    if   e           k disk
     f                                     rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk
     f                                     rename(vhgrpfr:vhgrl1r)
      *                                                                 *
     fliste     o    f  132        printer oflind(*inof)
      *
     d                uds
     d lcfvar                300    314
     d lctvar                        15
     d lcflag                         2  0
     d lctlag                         2  0
     d lcfogr                         2  0
     d lctogr                         2  0
     d lcfhgr                         2  0
     d lcthgr                         2  0
     d lcfugr                         3  0
     d lctugr                         3  0
     d lcvari                         1  0
     d lctdat                        10d   datfmt(*iso)
     d lcaltp                         1
     d lcmvap                         1
     d lctype                         1  0
     d lcanta                         6  0
     d lclevn                         6  0
      *
     d lcwsid                901    910
     d lcuser                        10
      *
     d lcfirm                944    946  0
     d lcnavn                951    980
      *---------------------------------------------------------------------
      * Stand Alone Fields - TOP
      *---------------------------------------------------------------------
     d ant30           s              8  0
     d ant31           s              9  1
     d ant32           s             10  2
     d ant33           s             11  3
     d ant40           s              8  0
     d ant41           s              9  1
     d ant42           s             10  2
     d ant43           s             11  3
     d ant50           s              8  0
     d ant51           s              9  1
     d ant52           s             10  2
     d ant53           s             11  3
     d ant60           s              8  0
     d ant61           s              9  1
     d ant62           s             10  2
     d ant63           s             11  3
     d bev3            s             11  2
     d bev4            s             11  2
     d bev5            s             11  2
     d bev6            s             11  2
     d bev7            s             11  2
     d kos3            s             11  2
     d kos4            s             11  2
     d kos5            s             11  2
     d kos6            s             11  2
     d kos7            s             11  2
     d sal3            s             11  2
     d sal4            s             11  2
     d sal5            s             11  2
     d sal6            s             11  2
     d sal7            s             11  2
     d teller          s              3  0
     d utime           s              6  0
     d waar            s              2  0
     d wdag            s              2  0
     d wudate          s              6  0
     d wwfirm          s                   like(vvfirm)
     d wwoogr          s                   like(vgoogr)
     d wwotxt          s                   like(vgotxt)
     d wwtdat          s              6  0
     d wwhogr          s                   like(vghogr)
     d wwhhgr          s                   like(vghogr)
     d wwhtxt          s                   like(vghtxt)
     d wwvare          s                   like(vvvare)
      *---------------------------------------------------------------------
      * Stand Alone Fields - BOTTOM
      *---------------------------------------------------------------------
      *
      /EJECT
      ********************  I N P U T  S E C T I O N  ********************
      *
     ilwa1l1r
     i                                          mlfirm        l7
     i                                          mlogrp        l6
     i                                          mlhgrp        l5
     i                                          mlvare        l4
     i                                          mllage        l3
      *
      /eject
      ***************************  CALCULATION  **************************
      *
      *   Behandler lager                                           **
      *
      * Setter av indikator for heading
      *
     c                   eval      *in20 = *off
      *
      * Nullstiller
      *
      * Brudd på hovedgruppe
      *
     c   l6              do
     c                   eval      sal6 = 0
     c                   eval      kos6 = 0
     c                   eval      bev6 = 0
     c                   eval      ant60 = 0
     c                   eval      ant61 = 0
     c                   eval      ant62 = 0
     c                   eval      ant63 = 0
     c                   enddo
      *
      * Brudd på hovedgruppe
      *
     c   l5              do
     c                   eval      sal5 = 0
     c                   eval      kos5 = 0
     c                   eval      bev5 = 0
     c                   eval      ant50 = 0
     c                   eval      ant51 = 0
     c                   eval      ant52 = 0
     c                   eval      ant53 = 0
     c                   enddo
      *
      * Brudd på varenummer
      *
     c   l4              do
     c                   eval      sal4 = 0
     c                   eval      kos4 = 0
     c                   eval      bev4 = 0
     c                   eval      ant40 = 0
     c                   eval      ant41 = 0
     c                   eval      ant42 = 0
     c                   eval      ant43 = 0
     c                   eval      teller = 0
     c                   enddo
      *
      * Brudd på lager
      *
     c   l3              do
     c                   eval      sal3 = 0
     c                   eval      kos3 = 0
     c                   eval      bev3 = 0
     c                   eval      ant30 = 0
     c                   eval      ant31 = 0
     c                   eval      ant32 = 0
     c                   eval      ant33 = 0
     c                   enddo
      *
      * Slår opp varenummer
      *
     c                   movel     mlvare        wwvare
     c     varkey        chain     vvarl1                             80
      *
      * Sjekker antall desimaler
      *
     c                   eval      *in40 = vvdesi = 0
     c                   eval      *in41 = vvdesi = 1
     c                   eval      *in42 = vvdesi = 2
     c                   eval      *in43 = vvdesi = 3
      *
      * Bygger opp kvantum
      *
     c                   if        lcvari <> 7
     c                   eval      ant33 = ant33 + mlbehl
     c                   else
     c                   eval      ant33 = ant33 + mlb101
     c                   endif
      *
      * Bygger opp kostverdi og salgsverdi
      *
     c                   eval      mlomst = ant33 * mlspri
     c                   eval      mlkost = ant33 * mlkpri
      *
     c                   eval      kos3 = mlkost
     c                   eval      sal3 = mlomst
      *
      * Bevegelse før dato
      *
     c                   eval      wwtdat = 0
      *
     c                   if        lctdat <> *loval
     c     *dmy          move      lctdat        wwtdat
     c                   endif
      *
     c                   if        mltrda <= lctdat
     c                   eval      bev3 = kos3
     c                   endif
      *
      /SPACE 3
      *
      *   Slutt
      *
     c     slutt         tag
      /EJECT
      *
      * Brudd på lager
      *
     cl3                 eval      *in34 = *off
     cl3                 eval      teller = teller + 1
     cl3                 eval      *in34 = teller > 1
     cl3                 eval      *in38 = bev3 = 0
     cl3                 eval      ant43 = ant43 + ant33
     cl3                 eval      ant30 = ant33
     cl3                 eval      ant31 = ant33
     cl3                 eval      ant32 = ant33
     cl3                 eval      sal4 = sal4 + sal3
     cl3                 eval      kos4 = kos4 + kos3
     cl3                 eval      bev4 = bev4 + bev3
      *
      * Brudd på varenummer
      *
     cl4                 eval      ant53 = ant53 + ant43
     cl4                 eval      ant40 = ant43
     cl4                 eval      ant41 = ant43
     cl4                 eval      ant42 = ant43
     cl4                 eval      sal5 = sal5 + sal4
     cl4                 eval      kos5 = kos5 + kos4
     cl4                 eval      bev5 = bev5 + bev4
      *
      * Brudd på hovedgruppe
      *
     cl5                 move      mlogrp        wwhogr
     cl5                 move      mlhgrp        wwhhgr
     cl5   hgrkey        chain     vhgrl1                             80
     cl5                 eval      wwhtxt = *blank
     cl5                 if        *in80 = *off
     cl5                 movel     vghtxt        wwhtxt
     cl5                 endif
     cl5                 eval      ant63 = ant63 + ant53
     cl5                 eval      ant50 = ant53
     cl5                 eval      ant51 = ant53
     cl5                 eval      ant52 = ant53
     cl5                 eval      sal6 = sal6 + sal5
     cl5                 eval      kos6 = kos6 + kos5
     cl5                 eval      bev6 = bev6 + bev5
      *
      * Brudd på overgruppe
      *
     cl6                 move      mlogrp        wwoogr
     cl6   ogrkey        chain     vogrl1                             80
     cl6                 eval      wwotxt = *blank
     cl6                 if        *in80 = *off
     cl6                 movel     vgotxt        wwotxt
     cl6                 endif
     cl6                 eval      ant60 = ant63
     cl6                 eval      ant61 = ant63
     cl6                 eval      ant62 = ant63
     cl6                 eval      sal7 = sal7 + sal6
     cl6                 eval      kos7 = kos7 + kos6
     cl6                 eval      bev7 = bev7 + bev6
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      *
      *
      *
      * Legge inn firmanummer
      *
     c                   move      lcfirm        wwfirm
      *
      * Riktig selektering
      *
     c                   eval      *in71 = lcvari = 1
     c                   eval      *in72 = lcvari = 2
     c                   eval      *in73 = lcvari = 3
     c                   eval      *in74 = lcvari = 4
     c                   eval      *in75 = lcvari = 5
     c                   eval      *in76 = lcvari = 6
      *
     c                   eval      *in77 = lctype = 1
      *
     c                   eval      *in20 = *on
      *
      * Snu dagens dato
      *
     c                   move      udate         wudate
     c                   move      wudate        waar
     c                   movel     wudate        wdag
     c                   movel     waar          wudate
     c                   move      wdag          wudate
      *
     c                   time                    utime
      *
      * Key for vareregister
      *
     c     varkey        klist
     c                   kfld                    wwfirm
     c                   kfld                    wwvare
      *
      * Key for overgruppe
      *
     c     ogrkey        klist
     c                   kfld                    wwfirm
     c                   kfld                    wwoogr
      *
      * Key for overrgruppe
      *
     c     hgrkey        klist
     c                   kfld                    wwfirm
     c                   kfld                    wwhogr
     c                   kfld                    wwhhgr
      *
     c                   endsr
      /EJECT
      *******************  O U T P U T  S E C T I O N  *******************
      *
      *
     oliste     d    20                     1 03
     o         or    of
     o                                            6 'LE228R'
     o                                           28 'ASP Lager/Innkjøp'
     o                                           92 'Bruker'
     o                       lcuser             103
     o                                          119 'Dato'
     o                       udate         y    129
      *
     o          d    20                     1
     o         or    of
     o                                           92 'Skjerm'
     o                       lcwsid             103
     o                                          119 'Kl. '
     o                       utime              129 '  :  :  '
      *
     o          d    20                     1
     o         or    of
     o                                            6 'Firma:'
     o                       lcfirm              10
     o                       lcnavn              41
     o                    n77                    75 'V E R D I   A V   L A G '
     o                    n77                    98 'E R B E H O L D N I N G'
     o                     77                    65 'V E R D I   A V   L A G '
     o                     77                    88 'E R B E H O L D N I N G'
     o                     77                   102 'P R.  01.01.'
     o                                          119 'Side'
     o                       page               129
      *
     o          d    20                     1
     o         or    of
     o                                           24 '------------------------'
     o                                           48 '------------------------'
     o                                           72 '------------------------'
     o                                           96 '------------------------'
     o                                          120 '------------------------'
     o                                          132 '------------'
      *
     o          d    20                     1
     o         or    of
     o                                            4 'Vare'
     o                                           20 'Betegnelse'
     o                                           50 'Enh'
     o                                           63 'Salgs-'
     o                                           74 'Kost-'
     o                                           96 '     Ant.    Salgsve'
     o                     74                    96 'Lag  Ant.    Salgsve'
     o                                          114 'rdi      Kostverdi'
     o                                          132 'Eldre enn'
      *
     o          d    20                     1
     o         or    of
     o                                            4 'nr.'
     o                                           62 'pris'
     o                                           73 'pris'
     o                     74                    78 'er'
     o                                          123 'bev.dato'
     o                       wwtdat        y    132
      *
     o          d    20                     1
     o         or    of
     o                                           24 '------------------------'
     o                                           48 '------------------------'
     o                                           72 '------------------------'
     o                                           96 '------------------------'
     o                                          120 '------------------------'
     o                                          132 '------------'
      *
     o          t    l3 74                  1
     o         or    l3 75
     o         or    l3 76
     o                 n34   mlvare              15
     o                 n34   vvtek1              45
     o                 n34   mlenhl              50
     o                 n34   mlspri        3     63
     o                       mlkpri        3     74
     o                       sal3          j    100
     o                       kos3          j    115
     o                       bev3          j    132
     o                  38                      132 '      '
     o                       ant30         m     85
     o                       mllage        x     78
      *
     o          t    l4                     1
     o              n74n75n76mlvare              15
     o              n74n75n76vvtek1              45
     o              n74n75n76mlenhl              50
     o              n74n75n76mlspri        3     63
     o              n74n75n76mlkpri        3     74
     o                       sal4          j    100
     o                       kos4          j    115
     o                       bev4          j    132
     o                       ant40         m     85
      *
     o          t    l4 34 74               1
     o         or    l4 34 75
     o         or    l4 34 76
      *
     o          t    l5 73                  2
     o         or    l5 76
     o                       mlogrp               2
     o                       mlhgrp               5
     o                       wwhtxt              45
     o                       sal5          j    100
     o                       kos5          j    115
     o                       bev5          j    132
     o                       ant50         m     85
      *
     o          t    l6 72                  2
     o         or    l6 75
     o                       mlogrp               2
     o                       wwotxt              45
     o                       sal6          j    100
     o                       kos6          j    115
     o                       bev6          j    132
     o                       ant60         m     85
      *
     o          t    l6 72 73               1
     o         or    l6 75 76
      *
     o          t    l7                  1  2
     o                                           13 'Sum firma '
     o                       lcfirm              17
     o                       sal7          j    100
     o                       kos7          j    115
     o                       bev7          j    132
