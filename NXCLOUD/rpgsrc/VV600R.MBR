     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VV600R
      * Beskrivelse . . : Vare, utskrift - parameter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       270999 HKO  Nytt program
      *       161103 HKO  Fjernet behandling av rabattgruppe
      *       180204 HKO  Lagt inn valg av minimun dekningsgrad
6.11  * 6.10  060110 bof  Fjernet utskrift av lagerinfo fra vare
7.xx  * 7.00  310116 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvv600d    cf   e             workstn
      *
      * Definering av LDA (Local Data Area)
      *
     d                uds
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av datastruktur - parameterliste ut
      *
     d                 ds
     d d_list                        80
     d  d_firm                        3  0 overlay(d_list:1)
     d  d_fogr                        2  0 overlay(d_list:4)
     d  d_fhgr                        2  0 overlay(d_list:6)
     d  d_fugr                        3  0 overlay(d_list:8)
     d  d_togr                        2  0 overlay(d_list:11)
     d  d_thgr                        2  0 overlay(d_list:13)
     d  d_tugr                        3  0 overlay(d_list:15)
     d  d_flev                        6  0 overlay(d_list:18)
     d  d_tlev                        6  0 overlay(d_list:24)
     d  d_fvar                       15    overlay(d_list:30)
     d  d_tvar                       15    overlay(d_list:45)
     d  d_dekn                        4  2 overlay(d_list:60)
     d  d_utsk                        1  0 overlay(d_list:64)
     d  d_srt1                        1  0 overlay(d_list:65)
     d  d_srt2                        1  0 overlay(d_list:66)
     d  d_srt3                        1  0 overlay(d_list:67)
     d  d_srt4                        1  0 overlay(d_list:68)
     d  d_dumm                        1    overlay(d_list:80) inz('*')
      *
      * Definering av datastruktur - varegruppe
      *
     d d_vgrp                         7  0
     d  d_ogrp                        2  0 overlay(d_vgrp:1)
     d  d_hgrp                        2  0 overlay(d_vgrp:3)
     d  d_ugrp                        3  0 overlay(d_vgrp:5)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_timestamp     s               z
     d w_member        s             10
     d w_time_20       s             20
     d w_time_12       s             12
     d w_time_9        s              9
     d w_otxt          s             35
     d w_htxt          s             35
     d w_utxt          s             35
     d w_rtxt          s             35
     d w_vare          s             15
     d w_tek1          s             35
     d w_lnav          s             30
     d w_fvgr          s              7  0
     d w_tvgr          s              7  0
      *
     d w_firm          s              3  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1BLD Behandle bilde for valg av utskriftskriteria
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endsl
      *
      * Validering av input
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
      * Kopierer parametre til datastruktur
      *
     c                   if        b1srt1 = 0 and
     c                             b1srt2 = 0 and
     c                             b1srt3 = 0 and
     c                             b1srt4 = 0
     c                   eval      b1srt1 = 1
     c                   eval      b1srt2 = 2
     c                   eval      b1srt3 = 3
     c                   endif
      *
     c                   eval      d_firm = w_firm
     c                   eval      d_fogr = b1fogr
     c                   eval      d_fhgr = b1fhgr
     c                   eval      d_fugr = b1fugr
     c                   eval      d_togr = b1togr
     c                   eval      d_thgr = b1thgr
     c                   eval      d_tugr = b1tugr
     c                   eval      d_flev = b1flev
     c                   eval      d_tlev = b1tlev
     c                   eval      d_fvar = *blank
     c                   eval      d_tvar = *blank
     c                   eval      d_dekn = b1dekn
     c                   eval      d_utsk = b1utsk
     c                   eval      d_srt1 = b1srt1
     c                   eval      d_srt2 = b1srt2
     c                   eval      d_srt3 = b1srt3
     c                   eval      d_srt4 = b1srt4
      *
     c                   if        b1fvar <> 0
     c                   movel     b1fvar        d_fvar
     c                   endif
      *
     c                   if        b1tvar <> 0
     c                   movel     b1tvar        d_tvar
     c                   endif
      *
      * Finner unikt member-navn
      *
     c                   time                    w_timestamp
     c     *iso0         move      w_timestamp   w_time_20
     c                   move      w_time_20     w_time_12
     c                   movel     w_time_12     w_time_9
      *
     c                   movel     'W'           w_member
     c                   move      w_time_9      w_member
      *
      * Kaller utskriftsprogrammer
      *
     c                   call      'VV600C'
     c                   parm                    d_list
     c                   parm                    w_member
      *
     c                   goto      b1taga
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av spørringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Leverandør
      *
     c                   if        w_afld = 'B1FLEV'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    b1flev
     c                   parm                    w_lnav
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'B1TLEV'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    b1tlev
     c                   parm                    w_lnav
     c                   leavesr
     c                   endif
      *
      * Overgruppe
      *
     c                   if        w_afld = 'B1FOGR'
     c                   eval      b1fhgr = 0
     c                   eval      b1fugr = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b1fogr
     c                   parm                    w_otxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'B1TOGR'
     c                   eval      b1thgr = 0
     c                   eval      b1tugr = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b1togr
     c                   parm                    w_otxt
     c                   leavesr
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_afld = 'B1FHGR'
     c                   eval      b1fugr = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b1fogr
     c                   parm                    b1fhgr
     c                   parm                    w_htxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'B1THGR'
     c                   eval      b1tugr = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b1togr
     c                   parm                    b1thgr
     c                   parm                    w_htxt
     c                   leavesr
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_afld = 'B1FUGR'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b1fogr
     c                   parm                    b1fhgr
     c                   parm                    b1fugr
     c                   parm                    w_utxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'B1TUGR'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b1togr
     c                   parm                    b1thgr
     c                   parm                    b1tugr
     c                   parm                    w_utxt
     c                   leavesr
     c                   endif
      *
      * Varenummer
      *
     c                   if        w_afld = 'B1FVAR'
     c                   eval      w_vare = *blank
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   movel     w_vare        b1fvar
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'B1TVAR'
     c                   eval      w_vare = *blank
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   movel     w_vare        b1tvar
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for 7jekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
      * Initierer variable
      *
     c                   if        b1flev <> 0 and
     c                             b1tlev = 0
     c                   eval      b1tlev = b1flev
     c                   endif
      *
     c                   if        b1fvar <> 0 and
     c                             b1tvar = 0
     c                   eval      b1tvar = b1fvar
     c                   endif
      *
      * Sjekker om gyldige verdier
      *
     c                   if        b1fogr = 0 and
     c                             b1fhgr <> 0
     c                   eval      *in31 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b1fhgr = 0 and
     c                             b1fugr <> 0
     c                   eval      *in32 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b1togr = 0 and
     c                             b1thgr <> 0
     c                   eval      *in33 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b1thgr = 0 and
     c                             b1tugr <> 0
     c                   eval      *in34 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b1togr = 0 and
     c                             b1thgr = 0 and
     c                             b1tugr = 0
     c                   if        b1fogr <> 0 or
     c                             b1fhgr <> 0 or
     c                             b1fugr <> 0
     c                   eval      b1togr = b1fogr
     c                   eval      b1thgr = b1fhgr
     c                   eval      b1tugr = b1fugr
     c                   endif
     c                   endif
      *
     c                   if        b1fogr <> 0 or
     c                             b1fhgr <> 0 or
     c                             b1fugr <> 0
     c                   if        b1togr = 0
     c                   eval      b1togr = 99
     c                   endif
     c                   if        b1thgr = 0
     c                   eval      b1thgr = 99
     c                   endif
     c                   if        b1tugr = 0
     c                   eval      b1tugr = 999
     c                   endif
     c                   endif
      *
     c                   if        b1fogr <> 0
     c                   eval      d_ogrp = b1fogr
     c                   eval      d_hgrp = b1fhgr
     c                   eval      d_ugrp = b1fugr
     c                   eval      w_fvgr = d_vgrp
     c                   eval      d_ogrp = b1togr
     c                   eval      d_hgrp = b1thgr
     c                   eval      d_ugrp = b1tugr
     c                   eval      w_tvgr = d_vgrp
     c                   if        w_fvgr > w_tvgr
     c                   eval      *in35 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b1flev > b1tlev
     c                   eval      *in36 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b1fvar > b1tvar
     c                   eval      *in38 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b1dekn <> 0 and
     c                             b1utsk <> 1
     c                   eval      *in45 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b1utsk < 1 or
6.11 c                             b1utsk > 2
     c                   eval      *in39 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b1srt1 <> 0
     c                   if        b1srt1 < 1 or
     c                             b1srt1 > 3
     c                   eval      *in40 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b1srt2 <> 0
     c                   if        b1srt2 < 1 or
     c                             b1srt2 > 3
     c                   eval      *in41 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b1srt3 <> 0
     c                   if        b1srt3 < 1 or
     c                             b1srt3 > 3
     c                   eval      *in42 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b1srt4 <> 0
     c                   if        b1srt4 < 1 or
     c                             b1srt4 > 3
     c                   eval      *in43 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjermbildet
      *
     c                   eval      b1utsk = 1
     c                   eval      b1srt1 = 1
     c                   eval      b1srt2 = 2
     c                   eval      b1srt3 = 3
      *
     c                   endsr
