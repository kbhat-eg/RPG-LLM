      /TITLE  Tilrettelegger trans fra Ordre/Fakturering
     h decedit(',') datedit(*dmy.)
     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * System. . . . . : ASOKON
      * Program . . . . : RFA15R
      * Beskrivelse . . : Tilrettelegger trans fra Ordre/Fakturering
      *                   Avvikende periode i følge koderegister
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       060104 KOB  Lagt inn overføring av mva-grunnlag
      * V5.30 200404 KOB  Endret beregning av buntsummer
6.10  * V6.10 291009 NHO  Kun innestående firma skal være på overføring
      *
6.20  * 6.20  151110 NHO  Legger underprosjekt ut på rtrapf
6.21  * 6.20  160410 NHO  Henting av siste buntnr er feil må teste
      *                   på om READP-post = member i key
6.nu  * 6.30  230614 bof  sjekket mtp. nummerutvidelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.10 ffovfl1    uf   e           k disk    rename(fovfpfr:fovfl1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fraa3pf    if   e           k disk
     frbunlu    uf a e           k disk    rename(rbunpfr:rbunlur)
     frtrapf    o    e           k disk
      *
      * Definering av array's
610  d                uds
610  d l_user                911    920
610  d l_firm                944    946  0
      *
     d per             s              2  0 dim(13)                              Perioder
      *
      * Definering av variable i nøkler
      *
610  d rbunlu_nivo     s                   like(rsnivo)
610  d rbunlu_memb     s                   like(rsmemb)
610  d w_memb          s                   like(rsmemb)
     d w_firm          s                   like(fffirm)
     d fovfl1_peri     s                   like(ffperi)
     d rbunlu_bunt     s                   like(rsbunt)
      *
      * Definering av variable
      *
     d w_bunt          s                   like(rtbunt)
     d w_xper          s              8  0
     d w_linj          s              8  0
     d w_arec          s              9  0
     d w_Årm1          s              2  0
     d wpe             s              2  0
     d w_rper          s              2  0
     d w_raar          s              4  0
     d w_paar          s              2  0
      *
     d w_bsum          s                   like(rsbsum)
     d w_psum          s                   like(rspsum)
     d w_pdeb          s                   like(rspdeb)
     d w_pkre          s                   like(rspkre)
      *
      * Definering av save-variable
      *
     d s_firm          s                   like(fffirm)
     d s_peri          s                   like(ffperi)
      *
      *  Statusopplysninger - avvikende perioder
      *
     iraa3pfr
     i              ra3p01                      per(01)
     i              ra3p02                      per(02)
     i              ra3p03                      per(03)
     i              ra3p04                      per(04)
     i              ra3p05                      per(05)
     i              ra3p06                      per(06)
     i              ra3p07                      per(07)
     i              ra3p08                      per(08)
     i              ra3p09                      per(09)
     i              ra3p10                      per(10)
     i              ra3p11                      per(11)
     i              ra3p12                      per(12)
     i              ra3p13                      per(13)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedrutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Les transaksjonsfile til e.o.f.
      *
6.10 c                   eval      w_firm = l_firm
     c                   eval      fovfl1_peri = 0
     c     fovfl1_key    setll     fovfl1
     c                   read      fovfl1                                 90
6.10 c                   if        *in90 = *on
6.10 c                   goto      slutt1
6.10 c                   end
6.10  *
6.10 c                   if        fffirm > l_firm
6.10 c                   goto      slutt1
6.10 c                   end
6.10  *
     c                   eval      w_firm = fffirm
     c                   eval      s_firm = fffirm
     c                   eval      s_peri = ffperi
     c                   exsr      nytt_firma
     c                   exsr      ny_periode
      *
6.10 c                   if         *in90 = *off
6.10 c                   delete    fovfl1r
6.10 c                   endif
     c                   dow       *in90 = *off
      *
6.10  * Nytt firma ?
6.10  *
6.10 c**                 if        fffirm <> s_firm
6.10 c**                 exsr      nytt_firma
6 10 c**                 endif
      *
      * Ny periode ?
      *
     c                   if        fffirm <> s_firm or
     c                             ffperi <> s_peri
     c                   exsr      ny_periode
     c                   endif
      *
      * Ta vare på gamle verdier
      *
     c                   eval      w_firm = fffirm
     c                   eval      s_firm = fffirm
     c                   eval      s_peri = ffperi
      *
      * Redigerer regnskapsperiode og år
      *
     c                   movel     ffperi        w_rper
     c                   move      ffperi        w_paar
     c                   movel     20            w_raar
     c                   move      w_paar        w_raar
      *
      * Oppdater RTRAPF
      *
     c                   exsr      opdat_rtra
     c                   eval      w_arec = w_arec + 1
      *
     c                   read      fovfl1                                 90
6.10 c                   if        *in90 = *on
6.10 c                   goto      slutt
6.10 c                   end
6.10  *
6.10  *
6.10 c                   if        fffirm > l_firm
6.10 c                   goto      slutt
6.10 c                   end
6.10  *
      *
      * Dersom brudd,
      * oppdater RBUNPF
      *
     c                   if        fffirm <> s_firm or
     c                             ffperi <> s_peri
     c                   exsr      opdat_rbun
     c                   endif
      *
6.10 c                   if        *in90 = *off
6.10 c                   delete    fovfl1r
6.10 c                   end
     c                   enddo
      *
6.10 c     slutt         tag
      * Siste buntsum,
      * oppdater RBUNPF
      *
     c                   if        w_arec <> 0
     c                   exsr      opdat_rbun
     c                   endif
      *
      * Avslutt program
      *
6.10 c     slutt1        tag
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for brudd ved nytt firmanr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     nytt_firma    begsr
      *
      * Hent inn firmaregister
      *
     c     afirl1_key    chain     afirl1                             91
      *
      * Hent inn statuskoder (avvikende perioder)
      *
     c     raa3pf_key    chain     raa3pf                             91         Avvikende perioder
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for brudd periode / år
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ny_periode    begsr
      *
      * Brudd periode/år
      * Finn neste ledige buntnr
      *
610  c                   eval      rbunlu_nivo = *blank
610  c                   eval      rbunlu_memb = w_memb
     c                   eval      rbunlu_bunt = 99999                          Type
     c     rbunlu_key    setgt     rbunlu
     c                   readp     rbunlu                                 91
      *
     c                   if        *in91 = *on
     c                   eval      w_bunt = 1
     c                   else
6.21 c                   if        w_memb <> rsmemb
6.21 c                   eval      w_bunt = 1
6.21 c                   else
     c                   eval      w_bunt = rsbunt + 1
     c                   endif
6.21 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av RTRAPF
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opdat_rtra    begsr
      *
      * Beregn buntsummer
      *
     c                   if        ffdkto <> 0
     c                   eval      w_psum = w_psum + ffbelp
     c                   eval      w_bsum = w_bsum + ffbelp
     c                   eval      w_pdeb = w_pdeb + ffbelp
     c                   endif
      *
     c                   if        ffckto <> 0
     c                   eval      w_psum = w_psum + ffbelp
     c                   eval      w_pkre = w_pkre + ffbelp
     c                   endif
      *
     c                   eval      w_linj = w_linj + 1                          Linjenr.
      *
     c                   eval      rtreid = 'T'                                 Rec.-id
     c                   eval      rtfirm = fffirm                              Firma
     c                   eval      rtbunt = w_bunt                              Bunt-nr.
     c                   eval      rtlinj = w_linj                              Linjenr.
     c                   eval      rtstat = *blank                              Buntstatus
     c                   eval      rtraar = w_raar                              Regn.år
     c                   eval      rtrper = w_rper                              Regn.år
     c                   eval      rtbiln = ffbiln                              Bilag-nr.
     c                   eval      rtbill = ffbill                              Bilag-nr.
     c                   eval      rtdavd = ffdavd                              Debet-avd.
     c                   eval      rtdkto = ffdkto                              Debet-kto.
     c                   eval      rtdsps = ffdsps                              Debet-spes.
     c                   eval      rtdmva = ffdmom                              Debet-mva.
     c                   eval      rtbelØ = ffbelp                              Beløp
     c                   eval      rtgmva = ffmvag                              Beløp
     c                   eval      rtcavd = ffcavd                              Kredit-avd.
     c                   eval      rtckto = ffckto                              Kredit-kto.
     c                   eval      rtcsps = ffcsps                              Kredit-spes.
     c                   eval      rtcmva = ffcmom                              Kredit-mva.
     c                   eval      rtpros = ffproj                              Prosjekt
6.20 c                   eval      rtupro = ffupro
     c                   eval      rtrefn = ffkref                              Referanse
     c                   eval      rtkrab = 0                                   Rabatt
     c                   eval      rtvalk = ffvalk                              Valuta-kode
     c                   eval      rtvalb = ffvalb                              Valuta-beløp
     c                   eval      rtakti = ffakti                              Aktivitet
     c                   eval      rtmngd = ffmngd                              Mengde
     c                   eval      rtmngk = ffmngk                              Mengde-kode
     c                   eval      rtresn = ffkund                              Reskontronr.
     c                   eval      rtvirk = *blank                              Virksomhet
     c                   eval      rtautx = *blank                              Avkryssing     V2.03
     c                   eval      rtatte = *blank                              Attestert
      *
     c                   eval      rtbdat = ffbdat                              Forfall
     c                   eval      rtfdat = fffdat                              Forfall
      *
     c                   if        ffautx = 'K'                                                V2.03
     c                   eval      rtautx = ffautx                              Avkryssing     V2.03
     c                   else                                                     automatisk   V2.03
     c                   eval      rtkrys = ffautx                              Avkryssing
     c                   endif                                                                 V2.03
      *
     c                   eval      rtkidi = *blank                              KID-referanse
     c                   eval      rtbilk = ffbilk                              Bilagskode
     c                   eval      rttext = fftext                              Bilagstekst
     c                   eval      rtbetb = 0                                   Betalingbet.
     c                   eval      rtbref = *blank                              Bank-referanse
610  c                   eval      rtnivo = *blank
610  c                   eval      rtmemb = w_memb
610  c                   time                    rtedat
610  c                   time                    rtetim
610  c                   eval      rteusr = l_user
     c                   write     rtrapfr                                      Skriv post
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av RBUNPF
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opdat_rbun    begsr
      *
     c                   xfoot     per           w_xper                         Tabell finne
     c                   movel     rtrper        wpe                            Per på trans
      *
     c                   if        ra3stp > 01 and                              Start r.år
     c                             w_xper > *zero                                tab.utfyllt
     c                   movel     per(wpe)      rtrper                           bruk p.fra tab.
     c                   if        wpe    < ra3stp                              og fjorår
     c                   move      w_Årm1        rtrper                          bruk fjorår
     c                   endif
     c                   endif
      *
     c                   eval      rsfirm = w_firm                              Bunt-nr.
     c                   eval      rsbunt = w_bunt                              Bunt-nr.
     c                   eval      rsstat = *blank                              Buntstatus
     c                   eval      rsraar = rtraar                              Periode/År
     c                   eval      rsrper = rtrper                              Periode/År
     c                   eval      rsfØrs = 1                                   Første linje
     c                   eval      rssist = w_linj                              Siste linje
     c                   eval      rsbsum = w_bsum                              Beløp
     c                   eval      rspsum = w_psum                              Beløp
     c                   eval      rspdeb = w_pdeb                              Beløp
     c                   eval      rspkre = w_pkre                              Beløp
     c                   time                    rspdat                         Postert dato
610  c                   eval      rsnivo = *blank
610  c                   eval      rsmemb = w_memb
610  c                   time                    rsedat
610  c                   time                    rsetim
610  c                   eval      rseusr = l_user
     c                   write     rbunlur                                      Skriv post
      *
      * Nullstill buntsum
      *
     c                   eval      w_bsum = 0
     c                   eval      w_psum = 0
     c                   eval      w_pdeb = 0
     c                   eval      w_pkre = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
610  c     *entry        plist
610  c                   parm                    w_memb                         user
      *
     c                   eval      wpe = 0
     c                   eval      w_bsum = 0
     c                   eval      w_Årm1 = uyear - 1
      *
      * Key for les av transaksjonsfile
      *
     c     fovfl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fovfl1_peri
      *
      * Key for les av firmaregister
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les av statuskoder 3 (avvikende perioder)
      *
     c     raa3pf_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppdatering av buntfile
      *
     c     rbunlu_key    klist
610  c                   kfld                    rbunlu_nivo
610  c                   kfld                    rbunlu_memb
     c                   kfld                    w_firm
     c                   kfld                    rbunlu_bunt
      *
     c                   endsr
