      /title  ASOFAK Lev.vare, henter vareregister - Felleskjøpet Maskin
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FW715R
      * Beskrivelse . . : Danning av leverandør vareregister,
      *                   Felleskjøpet Maskin
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       310800 HKO  Nytt program
      * 6.10  230609 BSA  Oppdatert med sporingsinformasjon
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflvwpf    if   e           k disk
     ffenkl1    if   e           k disk    rename(fenkpfr:fenkl1r)
     fflvapf    o  a e             disk
      *
6.10  * Local data area
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av datastruktur
      *
      *  Recordbeskrivelse - Felleskjøpet Maskin
      *
     d                 ds
     d d_inpu                       662
     d  d_ekod                        1    overlay(d_inpu:2)                    Endringskode
     d  d_vare                       21    overlay(d_inpu:14)                   Varenr
     d  d_tek1                       35    overlay(d_inpu:80)                   Varetekst-1
     d  d_tek2                       35    overlay(d_inpu:136)                  Varetekst-2
     d  d_eann                       13    overlay(d_inpu:213)                  EAN-nr
     d  d_enhe                        4    overlay(d_inpu:382)                  Enhet
     d  d_dato                       14    overlay(d_inpu:445)                  Endringsdato
     d   d_dag                        2  0 overlay(d_dato:1)
     d   d_mnd                        2  0 overlay(d_dato:3)
     d   d_aar                        2  0 overlay(d_dato:7)
     d  d_inpr                       10    overlay(d_inpu:501)                  Innkjøpspris
     d  d_sapr                       10    overlay(d_inpu:578)                  Salgspris
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_ldor          s              6  0
      *
      * Definering av variable i nøkler
      *
     d fenkl1_enhf     s                   like(feenhf)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_dato          s              6  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser input-fil, og legger data ut på leverandør vareregister
      *
     c                   read      flvwpf                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      d_inpu = fwirad
      *
      * Skriver til leverandør vareregister
      *
     c                   eval      fwfirm = p_firm
     c                   eval      fwldor = p_ldor
     c                   eval      fwekod = d_ekod
     c                   eval      fwtek1 = %trim(d_tek1)
     c                   eval      fwtek2 = %trim(d_tek2)
     c                   eval      fwlvar = d_vare
     c                   eval      fweann = 0
     c                   eval      fwenhe = d_enhe
     c                   eval      fwinpr = 0
     c                   eval      fwsapr = 0
     c                   eval      fwdato = *loval
     c                   eval      fwhkod = 1
      *
     c                   eval      fwline = fwline + 1
      *
     c                   if        fwekod = 'X'
     c                   eval      fwekod = *blank
     c                   endif
      *
     c     X'46':'Æ'     xlate     fwtek1        fwtek1
     c     X'A0':'æ'     xlate     fwtek1        fwtek1
     c     X'77':'Ø'     xlate     fwtek1        fwtek1
     c     X'90':'ø'     xlate     fwtek1        fwtek1
     c     X'2A':'Å'     xlate     fwtek1        fwtek1
     c     X'EF':'å'     xlate     fwtek1        fwtek1
      *
     c     X'46':'Æ'     xlate     fwtek2        fwtek2
     c     X'A0':'æ'     xlate     fwtek2        fwtek2
     c     X'77':'Ø'     xlate     fwtek2        fwtek2
     c     X'90':'ø'     xlate     fwtek2        fwtek2
     c     X'2A':'Å'     xlate     fwtek2        fwtek2
     c     X'EF':'å'     xlate     fwtek2        fwtek2
      *
     c                   if        fwtek2 = fwtek1
     c                   eval      fwtek2 = *blank
     c                   endif
      *
     c                   move      d_eann        fweann
     c                   move      d_inpr        fwinpr
     c                   move      d_sapr        fwsapr
      *
     c                   eval      fenkl1_enhf = d_enhe
     c     fenkl1_key    chain     fenkl1                             91
     c                   if        *in91 = *off
     c                   eval      fwenhe = feenht
     c                   endif
      *
     c                   if        d_dato <> *blank
     c                   eval      w_dato = (d_dag*10000) + (d_mnd*100) +
     c                                      d_aar
     c     *dmy          move      w_dato        fwdato
     c                   endif
      *
6.10 c                   time                    fwodat
6.10 c                   time                    fwotim
6.10 c                   eval      fwousr = l_user
6.10 c                   time                    fwedat
6.10 c                   time                    fwetim
6.10 c                   eval      fweusr = l_user
     c                   write     flvapfr
      *
     c                   read      flvwpf                                 90
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
      *
      * Key for oppslag på enhet-konvertering
      *
     c     fenkl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fenkl1_enhf
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
