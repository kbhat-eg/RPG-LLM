     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VL717R
      * Beskrivelse . . : Henter varetype,utg.dato, lev.nr og beholdning
      *                   bestilling og
      *                   min og maks og omregningsfaktor mellom enhet og lager
      *
      *                   beholdning = lagerbeholdning - i ordre
      *                   Hvis lagerenhet <> salgsenhet 1, så omberegnes beholdn
      *                   i bestilling, min og maks
      *
      *                   Input : firma, vare, lager
      *                   Output: varetype, utg.dato, lev.nr, beholdning,
      *                           i bestilling, minim. og maksim. og
      *                           omrening enhet vs lager enhet
      *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       241019 bkv  Kopiert fra VL716R
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
      *
      * Definering av variable i nøkler
      *
     d vlagl1_firm     s                   like(vlfirm)
     d vlagl1_vare     s                   like(vlvare)
     d vlagl1_lage     s                   like(vllage)
     d vvarl1_firm     s                   like(vvfirm)
     d vvarl1_vare     s                   like(vvvare)
     d fstsl1_firm     s                   like(faafir)
     d vvenl2_vare     s                   like(vevare)
     d vvenl2_saen     s                   like(vesaen)
     d vvenl1_vare     s                   like(vevare)
     d vvenl1_enhe     s                   like(veenhe)
      *
      * Definering av pamametre
      *
     d p_firm          s                   like(vlfirm)
     d p_vare          s                   like(vlvare)
     d p_lage          s                   like(vllage)
     d p_enhe          s                   like(vvenhe)
     d p_vtyp          s                   like(vlvtyp)
     d p_udat          s                   like(vludat)
     d p_ldor          s                   like(vlbldo)
     d p_behl          s                   like(vlbehl)
     d p_bbes          s                   like(vlbbes)
     d p_mini          s                   like(vlmini)
     d p_maks          s                   like(vlmaks)
     d p_bmen          s                   like(vlbmen)
     d p_bmin          s                   like(vlbmin)
     d p_omre          s             16 10
     d p_inlr          s              1
      *
      * Definering av variable
      *
     d w_firm          s                   like(vefirm)
     d w_enhe          s                   like(veenhe)
     d w_omre          s                   like(veomre)
     d w_enh4          s                   like(veenhe)
     d w_enhl          s                   like(veenhe)
     d w_omrl          s                   like(veomre)
      *
     d                uds
     d l_user                911    920
     d l_sfir                944    946  0
     d l_savd                947    950  0
     d l_lagr               1001   1002  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      p_behl = 0
     c                   eval      p_bbes = 0
     c                   eval      p_mini = 0
     c                   eval      p_maks = 0
     c                   eval      p_bmen = 0
     c                   eval      p_bmin = 0
     c                   eval      p_omre = 1
      *
      * Initierer output-parametre
      *
     c                   eval      p_vtyp = *blank
     c                   eval      p_udat = *loval
     c                   eval      p_ldor = 0
      *
     c                   if        faalag = 1
      *
     c                   eval      vvarl1_firm = p_firm
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
      *
     c                   exsr      hent_enhe
      *
      * Henter prisgruppe
      *
     c                   eval      vlagl1_firm = p_firm
     c                   eval      vlagl1_vare = p_vare
     c                   eval      vlagl1_lage = p_lage
     c     vlagl1_key    chain     vlagl1
     c                   if        %found(vlagl1)
     c                   if        vlvtyp <> *blank
     c                   eval      p_vtyp = vlvtyp
     c                   endif
     c                   if        vludat <> *loval
     c                   eval      p_udat = vludat
     c                   endif
     c                   if        vlbldo <> 0
     c                   eval      p_ldor = vlbldo
     c                   endif
      *
     c                   if        vlvtyp <> 'D' and
     c                             vlvtyp <> 'T' and
     c                             vlvtyp <> 'S'
     c                   eval      p_behl = vlbehl - vloord
     c                   eval      p_bbes = vlbbes
     c                   eval      p_mini = vlmini
     c                   eval      p_maks = vlmaks
     c                   eval      p_bmen = vlbmen
     c                   eval      p_bmin = vlbmin
     c                   endif
      *
     c                   endif
      *
     c                   eval      vvarl1_firm = p_firm
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found(vvarl1)
      *
     c                   if        vvenhl <> p_enhe  and
     c                             w_omre <> 0
     c                   eval      p_behl = p_behl *  w_omrl/w_omre
     c                   eval      p_bbes = p_bbes *  w_omrl/w_omre
     c                   eval      p_mini = p_mini *  w_omrl/w_omre
     c                   eval      p_maks = p_maks *  w_omrl/w_omre
     c                   eval      p_bmen = p_bmen *  w_omrl/w_omre
     c                   eval      p_bmin = p_bmin *  w_omrl/w_omre
     c                   eval(h)   p_omre = w_omrl/w_omre
     c                   endif
      *
     c                   if        p_vtyp = *blank
     c                   eval      p_vtyp = vvvtyp
     c                   endif
      *
     c                   if        p_udat = *loval
     c                   eval      p_udat = vvudat
     c                   endif
      *
     c                   if        p_ldor = 0
     c                   eval      p_ldor = vvldor
     c                   endif
      *
     c                   endif
      *
     c                   else
      *
     c                   if        p_vtyp = *blank
     c                   eval      vvarl1_firm = p_firm
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found(vvarl1)
     c                   eval      p_vtyp = vvvtyp
     c                   endif
     c                   endif
      *
     c                   if        p_udat = *loval
     c                   eval      vvarl1_firm = p_firm
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found(vvarl1)
     c                   eval      p_udat = vvudat
     c                   endif
     c                   endif
      *
     c                   if        p_ldor = 0
     c                   eval      vvarl1_firm = p_firm
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found(vvarl1)
     c                   eval      p_ldor = vvldor
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
      *
     c                   if        p_inlr <> *on
     c                   eval      *inlr = *on
     c                   endif
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_enhe     begsr
      * finner lager enhet
     c                   eval      w_omrl = 1
     c                   eval      vvenl1_vare = p_vare
     c                   eval      vvenl1_enhe = vvenhl
     c     vvenl1_key    chain     vvenl1
     c                   if        %found(vvenl1)
     c                   eval      w_enhl = veenhe
     c                   eval      w_omrl = veomre
     c                   endif
      *
      * finner vist enhet
     c                   eval      w_omre = 1
     c                   eval      vvenl1_vare = p_vare
     c                   eval      vvenl1_enhe = p_enhe
     c     vvenl1_key    chain     vvenl1
     c                   if        %found(vvenl1)
     c                   eval      w_enhe = veenhe
     c                   eval      w_omre = veomre
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_lage
     c                   parm                    p_enhe
     c                   parm                    p_vtyp
     c                   parm                    p_udat
     c                   parm                    p_ldor
     c                   parm                    p_behl
     c                   parm                    p_bbes
     c                   parm                    p_mini
     c                   parm                    p_maks
     c                   parm                    p_bmen
     c                   parm                    p_bmin
     c                   parm                    p_omre
     c                   parm                    p_inlr
      *
      * Key for les på lager-register
      *
     c     vlagl1_key    klist
     c                   kfld                    vlagl1_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
      *
      * Key for les på vare-register
      *
     c     vvarl1_key    klist
     c                   kfld                    vvarl1_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på OFAK-kode (til)
      *
     c     fstsl1_key    klist
     c                   kfld                    fstsl1_firm
      *
      * Key for les på vare-enhet
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
     c                   kfld                    vvenl2_saen
      *
     c     vvenl2_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
      *
     c                   eval      w_firm = p_firm
      *
     c                   eval      fstsl1_firm = p_firm
     c     fstsl1_key    chain     fstsl1                             91
      *
     c                   endsr
