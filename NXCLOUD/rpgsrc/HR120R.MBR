     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : HR120R
      * Beskrivelse . . : Radioterminal trans, lagertelling - param
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
6.20  * 6.20  100112 bof  Hvis batch er sperret blir man stoppet.
6.20  *       270213 bof  Ikke tillate å taste inn bunt-nr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     flbchl1    if   e           k disk    rename(lbchpfr:lbchl1r)
     fhr120d    cf   e             workstn
      *
      * Local Data Area
      *
     d                uds
     d l_firm                944    946  0
     d l_lage               1001   1002  0
      *
      * Definering av variable i nøkler
      *
     d lbchl1_batc     s                   like(lcbatc)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_adrs          s             30
     d w_ponr          s              4  0
     d w_pste          s             30
     d w_avde          s              4  0
     d w_text          s             30
      *
     d w_firm          s                   like(lcfirm)
     d w_kode          s              1
     d w_tims          s               z
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer skjermbildet
      *
     c     b1taga        tag
     c                   eval      b1bes1 = *blank
     c                   eval      b1bes2 = *blank
     c                   eval      b1lage = l_lage
     c                   eval      b1numm = 0
      *
     c     b1tagb        tag
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b1tagb
     c                   endsl
      *
      * Sjekker input
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1tagb
     c                   endif
      *
      * Finner unik nøkkel
      *
     c                   eval      w_kode = 'T'
     c                   time                    w_tims
      *
      * Kaller registeringsprogram
      *
     c                   call      'HR121R'
     c                   parm                    w_firm
     c                   parm                    w_kode
     c                   parm                    w_tims
     c                   parm                    b1batc
     c                   parm                    b1lage
     c                   parm                    b1numm
     c                   parm                    w_text
      *
     c                   eval      *in33 = *on
     c                   goto      b1taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Batchnummer
      *
     c                   if        wactfe = 'B1BATC'
     c                   call      'HR122R'
     c                   parm                    w_firm
     c                   parm                    b1batc
     c                   goto      end_spØrr
     c                   endif
      *
      * Lager
      *
     c                   if        wactfe = 'B1LAGE'
     c                   call      'RA580R'
     c                   parm                    w_firm
     c                   parm                    b1lage
     c                   parm                    b1ltxt
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
      * Batchnummer
      *
     c                   eval      lbchl1_batc = b1batc
     c     lbchl1_key    chain     lbchl1                             90
     c                   if        *in90 = *on
     c                   eval      *in31 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
6.20 c                   if        %found(lbchl1) and
6.20 c                             lcsper = 1
6.20 c                   eval      *in34 = *on
6.20 c                   eval      b_feil = *on
6.20 c                   goto      end_sjekk
6.20 c                   endif
      *
      * Lager
      *
     c                   call      'FÅ720R'
     c                   parm                    w_firm
     c                   parm                    b1lage
     c                   parm                    b1ltxt
     c                   parm                    w_adrs
     c                   parm                    w_ponr
     c                   parm                    w_pste
     c                   parm                    w_avde
     c                   parm                    b_feil
      *
     c                   if        b_feil = *on
     c                   eval      *in32 = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Tekst
      *
     c                   eval      w_text = b1bes1 + b1bes2
     c                   if        w_text = *blank
     c                   eval      w_text = 'REGISTRERT FRA RADIOTERMINAL'
     c                   endif
      *
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av LAGER-KODE
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for les av BATCH
      *
     c     lbchl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbchl1_batc
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      *  Hent opplysninger fra LAGER-KODE-REGISTER
      *
     c     lstsl1_key    chain     lstsl1r                            90
      *
     c                   endsr
