     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VP704R
      * Beskrivelse . . : Vare-pris, henter priser for enhet
      *
      *                   Input:  Firma, prisgr, vare, enhet, leveringstype,
      *                           dato, leverandør, lager og LR-flagg
      *                   Output: Salgspris, kostpris, innkjøpspris og
      *                           butikkpris
      *
      *                   LR-flagg må være lik '1' for at *inLR ikke skal
      *                   settes
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
8.01  *       240222 bsa  Nytt program - tatt utgangspunkt i VP703. Lagt på
      *                   leverandør og lager som parametre inn.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
      *
      * Definering av pamametre
      *
     d p_firm          s                   like(vpfirm)
     d p_vare          s                   like(vpvare)
     d p_prgr          s                   like(vpprgr)
     d p_enhe          s                   like(vpenhe)
     d p_lety          s                   like(vplety)
     d p_dato          s                   like(vppdat)
     d p_lage          s              2  0
     d p_ldor          s                   like(vpldor)
     d p_sapr          s                   like(vpsapr)
     d p_bupr          s                   like(vpbupr)
     d p_kopr          s                   like(vpkopr)
     d p_inpr          s                   like(vpinpr)
      *
     d p_inlr          s              1
      *
      * Definering av variable i nøkler
      *
     d vvprl1_vare     s                   like(vpvare)
     d vvprl1_ldor     s                   like(vpldor)
     d vvprl1_prgr     s                   like(vpprgr)
     d vvprl1_enhe     s                   like(vpenhe)
     d vvprl1_lety     s                   like(vplety)
     d vvarl1_vare     s                   like(vvvare)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
      *
      * Definering av variable
      *
     d b_pris          s              1    inz(*off)
     d b_inlr          s              1    inz(*off)
      *
     d w_firm          s                   like(vpfirm)
     d w_dato          s                   like(vppdat)
     d w_ldor          s                   like(vvldor)
     d w_lage          s              2  0
      *
     d                uds
     d l_lage               1001   1002  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_lage = p_lage
     c                   eval      w_ldor = p_ldor
      *
      * Henter leverandør for vare og lager
      *
     c*                  call      'VL721R  '
     c*                  parm                    w_firm
     c*                  parm                    p_vare
     c*                  parm                    w_lage
     c*                  parm                    w_ldor
     c*                  parm                    b_inlr
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
      *
      * Initierer dato
      *
     c                   eval      w_dato = p_dato
      *
     c                   if        w_dato = *loval
     c                   time                    w_dato
     c                   endif
      *
      * Initierer output-parametre
      *
     c                   eval      p_sapr = 0
     c                   eval      p_bupr = 0
     c                   eval      p_kopr = 0
     c                   eval      p_inpr = 0
      *
      * Henter pris for vare, leveringstype og enhet
      *
      * 1. leverandør for lageret
     c                   eval      vvprl1_ldor = w_ldor
     c                   exsr      hent_pris
     c                   if        b_pris = *off  and
     c                             w_ldor <> vvldor
      * 2. hovedleverandør
     c                   eval      vvprl1_ldor = vvldor
     c                   exsr      hent_pris
     c                   if        b_pris = *off
      * siste mulighet...
     c                   eval      vvprl1_ldor = 0
     c                   exsr      hent_pris
     c                   endif
     c                   endif
      *
      *
      * Avbryter dersom pris ikke ble funnet
      *
     c                   if        b_pris = *off
     c                   goto      avslutt
     c                   endif
      *
      * Dersom kostpris er 0 hentes kostpris på bakgrunn av DG-% på vare
      * eller undergruppe
      *
     c                   if        p_kopr = 0
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1                             90
     c                   if        *in90 = *on or
     c                             vvdekn = 0
     c                   eval      vugrl1_uogr = vvogrp
     c                   eval      vugrl1_uhgr = vvhgrp
     c                   eval      vugrl1_uugr = vvugrp
     c     vugrl1_key    chain     vugrl1r                            90
     c                   if        *in90 = *off
     c                   eval      vvdekn = vgudek
     c                   endif
     c                   endif
      *
     c                   eval(h)   p_kopr = p_sapr * vvdekn / 100
     c                   if        p_kopr <> 0
     c                   eval      p_kopr = p_sapr - p_kopr
     c                   endif
      *
     c                   endif
      *
      * Innkjøpspris settes lik kostpris dersom innkjøpspris er 0
      *
     c                   if        p_inpr = 0
     c                   eval      p_inpr = p_kopr
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   if        p_inlr <> *on
     c                   eval      *inlr = *on
     c                   endif
      *
     c                   return
      *--------------------------------------------------------------------------------
      * Henter priser med evt. valgt prisgruppe, evt. uten
      *--------------------------------------------------------------------------------
      *
     c     hent_pris     begsr
      *
     c                   eval      b_pris = *off
      *
     c                   eval      vvprl1_vare = p_vare
     c                   eval      vvprl1_prgr = p_prgr
     c                   eval      vvprl1_enhe = p_enhe
     c                   eval      vvprl1_lety = p_lety
     c     vvprl1_key    setll     vvprl1
     c     vvprl1_key    reade     vvprl1                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      b_pris = *on
     c                   eval      p_sapr = vpsapr
     c                   eval      p_bupr = vpbupr
     c                   eval      p_kopr = vpkopr
     c                   eval      p_inpr = vpinpr
      *
     c                   if        w_dato >= vppdat
     c                   leave
     c                   endif
      *
     c     vvprl1_key    reade     vvprl1                                 90
     c                   enddo
      *
      * Dersom pris ikke finnes for oppgitt leveringstype, hentes pris fra
      * leveringstype 0
      *
     c                   if        b_pris = *off and
     c                             p_lety <> 0
      *
     c                   eval      vvprl1_vare = p_vare
     c                   eval      vvprl1_prgr = p_prgr
     c                   eval      vvprl1_enhe = p_enhe
     c                   eval      vvprl1_lety = 0
     c     vvprl1_key    setll     vvprl1
     c     vvprl1_key    reade     vvprl1                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      b_pris = *on
     c                   eval      p_sapr = vpsapr
     c                   eval      p_bupr = vpbupr
     c                   eval      p_kopr = vpkopr
     c                   eval      p_inpr = vpinpr
      *
     c                   if        w_dato >= vppdat
     c                   leave
     c                   endif
      *
     c     vvprl1_key    reade     vvprl1                                 90
     c                   enddo
      *
     c                   endif
      *
      * Dersom pris ikke finnes for oppgitt prisgruppe, hentes pris fra
      * prisgruppe = *blank
      *
     c                   if        b_pris = *off and
     c                             p_prgr <> *blank
      *
     c                   eval      vvprl1_vare = p_vare
     c                   eval      vvprl1_prgr = *blank
     c                   eval      vvprl1_enhe = p_enhe
     c                   eval      vvprl1_lety = p_lety
     c     vvprl1_key    setll     vvprl1
     c     vvprl1_key    reade     vvprl1                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      b_pris = *on
     c                   eval      p_sapr = vpsapr
     c                   eval      p_bupr = vpbupr
     c                   eval      p_kopr = vpkopr
     c                   eval      p_inpr = vpinpr
      *
     c                   if        w_dato >= vppdat
     c                   leave
     c                   endif
      *
     c     vvprl1_key    reade     vvprl1                                 90
     c                   enddo
      *
      * Dersom pris ikke finnes for oppgitt leveringstype, hentes pris fra
      * leveringstype 0
      *
     c                   if        b_pris = *off and
     c                             p_lety <> 0
      *
     c                   eval      vvprl1_vare = p_vare
     c                   eval      vvprl1_prgr = *blank
     c                   eval      vvprl1_enhe = p_enhe
     c                   eval      vvprl1_lety = 0
     c     vvprl1_key    setll     vvprl1
     c     vvprl1_key    reade     vvprl1                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      b_pris = *on
     c                   eval      p_sapr = vpsapr
     c                   eval      p_bupr = vpbupr
     c                   eval      p_kopr = vpkopr
     c                   eval      p_inpr = vpinpr
      *
     c                   if        w_dato >= vppdat
     c                   leave
     c                   endif
      *
     c     vvprl1_key    reade     vvprl1                                 90
     c                   enddo
      *
     c                   endif
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_prgr
     c                   parm                    p_enhe
     c                   parm                    p_lety
     c                   parm                    p_dato
     c                   parm                    p_ldor
     c                   parm                    p_lage
     c                   parm                    p_inlr
     c                   parm                    p_sapr
     c                   parm                    p_bupr
     c                   parm                    p_kopr
     c                   parm                    p_inpr
      *
      * Key for les på vare-pris-register
      *
     c     vvprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprl1_vare
     c                   kfld                    vvprl1_ldor
     c                   kfld                    vvprl1_prgr
     c                   kfld                    vvprl1_enhe
     c                   kfld                    vvprl1_lety
      *
      * Key for oppslag på vare-register
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på undergruppe-register
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
     c                   endsr
