     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VV580R
      * Beskrivelse . . : Vare, spørring - radioterminal
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       260900 HKO  Nytt program
      *       230301 HKO  Kommuniserer søketekst som parameter for å kunne
      *                   gjenta søk ved retur til programmet
      *       251103 HKO  Viser ** UTGÅTT ** i vareteksten på utgåtte varer
6.10  *       260210 bof  Viser bare lagervarer på eget lager
6.20  * 6.20  051110 bof  Utvidet med lager
6.21  * 6.20  190511 bø   Utvidet vl710r med div. info
6.22  * 6.20  250511 bof  Hvis nobb utg.dato på varen,så vises *UTG.NOBB*
6.3q  *       260515 bof  Datoformat sql
6.31  * 6.30  170918 bkv  Utvidet med trelast sortiment
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvsopf    if   e           k disk
     fvvarlr    if   e           k disk    rename(vvarpfr:vvarlrr)
     fvvarl2    if   e           k disk    rename(vvarpfr:vvarl2r)
6.31 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
     fvv580d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vvfirm)
     d p_vare          s                   like(vvvare)
     d p_stek          s             35
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d vvarlr_vare     s                   like(vvvare)
     d vvarl2_tek1     s                   like(vvtek1)
     d vvarl3_nobb     s                   like(vvnobb)
     d vvarl4_lvar     s                   like(vvlvar)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_open_sok      s              1    inz(*off)
     d b_valg_ok       s              1    inz(*off)
6.20 d b_inlr          s              1    inz(*off)
      *
6.20 d w_ddat          s               d   datfmt(*iso)
6.20 d w_udat          s               d   datfmt(*iso)
6.21 d w_ldor          s                   like(vvldor)
6.31 d w_lv_nobb       s                   like(vvlnob)
6.31 d w_lv_modn       s                   like(vvlmod)
6.31 d w_lv_lldo       s                   like(vvlnle)
6.31 d w_lv_llva       s                   like(vvllva)
     d w_ste1          s             35
     d w_ste2          s             35
     d w_ste3          s             35
     d w_ste4          s             35
     d w_ste5          s             35
     d w_posi          s              3  0
     d w_stek          s             35
     d s_stek          s             35
      *
     d w_firm          s                   like(vvfirm)
     d w_vare          s                   like(vvvare)
6.10 d w_lage          s              2  0
6.10 d w_vtyp          s                   like(vvvtyp)
      *
      * Konstanter
      *
     d c_sfil          s              2  0 inz(04)
6.10  *
6.10  * Definering av Local data area
6.10  *
6.10 d                uds
6.10 d l_lagr               1001   1002  0
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO
6.3q C/End-Exec
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   endsl
      *
      * F2=Gjenta søk
      *
     c                   if        *inkb = *on
     c                   eval      *in22 = *on
     c                   eval      b2stek = s_stek
     c                   goto      b2tagb
     c                   endif
      *
      * Søking
      *
     c                   if        b2stek <> *blank
     c                   exsr      sØk
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   if        b_valg_ok = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      *in22 = *on
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'S'
     c                   eval      b_open_sok = *on
     c                   other
     c     vvarl2_key    setll     vvarl2
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   eval      *in22 = *on
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for søking i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sØk           begsr
      *
     c                   eval      w_seqe = 'S'
     c                   eval      b_open_sok = *on
      *
     c                   eval      w_stek = b2stek
     c                   eval      s_stek = b2stek
     c                   eval      p_stek = b2stek
      *
      * Utleder søketekster ut av søketekst
      *
     c                   call      'AS700R'
     c                   parm                    w_stek
     c                   parm                    w_ste1
     c                   parm                    w_ste2
     c                   parm                    w_ste3
     c                   parm                    w_ste4
     c                   parm                    w_ste5
      *
      * Blanker subfile, og fyller opp i.h.t. søk
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker søkefelt
      *
     c                   eval      b2stek = *blank
      *
     c     end_sØk2      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      w_virn = w_srrn
      *
      * Velge post
      *
     c                   if        b1valg = 1
     c                   eval      p_vare = b1vare
     c                   eval      b_valg_ok = *on
     c                   leave
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
      * Initierer SQL for søk
      *
     c                   if        w_seqe = 'S' and
     c                             b_open_sok = *on
      *
     c/exec sql
     c+                  declare sok cursor for
     c+                  select vvsfir,
     c+                         vvstek,
     c+                         vvsvar
     c+                  from   vvsopf
     c+                  where  vvsfir = :w_firm and
     c+                         vvstek like :w_ste1
     c+                  order by vvsfir, vvstek
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok
     c/end-exec
     c/exec sql
     c+                  open sok
     c/end-exec
      *
     c                   endif
      *
      * Fyller opp subfile
      *
     c                   dou       w_srrn = w_spge
      *
     c                   if        w_seqe = 'S'
     c/exec sql
     c+                  fetch next
     c+                  from  sok
     c+                  into  :vvsfir,
     c+                        :vvstek,
     c+                        :vvsvar
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   else
     c                   read      vvarl2                                 15
     c                   endif
      *
     c                   if        *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        w_seqe = 'S'
      *
     c                   if        w_ste2 <> *blank
      *
     c                   eval      w_posi = %scan(%trim(w_ste2):%trim(vvstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
      *
     c                   if        w_ste3 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste3):%trim(vvstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste4 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste4):%trim(vvstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste5 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste5):%trim(vvstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c                   eval      vvarlr_vare = vvsvar
     c     vvarlr_key    chain     vvarlr                             91
     c                   if        *in91 = *on
     c                   iter
     c                   endif
      *
     c                   endif
      *
     c                   if        vvfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
6.10  *
6.10  * henter varetype
6.10  *
6.10 c                   call      'VL710R'
6.10 c                   parm                    w_firm
6.10 c                   parm                    vvvare
6.10 c                   parm                    w_lage
6.10 c                   parm                    w_vtyp
6.21 c                   parm                    w_udat
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.31 c                   parm                    w_lv_nobb
6.31 c                   parm                    w_lv_modn
6.31 c                   parm                    w_lv_lldo
6.31 c                   parm                    w_lv_llva
6.10  *
6.10 c                   if        w_vtyp = 'S'
6.10 c                   iter
6.10 c                   endif
      *
      * Skriver til subfile
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1tek1 = %subst(vvtek1:1:17)
     c                   eval      b1tek2 = %subst(vvtek1:18:17)
     c                   eval      b1vare = vvvare
      *
6.21 c                   if        w_udat <> *loval
6.21 c                   if        w_udat <= w_ddat
     c                   eval      b1tek2 = %subst(b1tek2:1:4) +
     c                                      ' ** UTGÅTT **'
6.21 c                   else
6.21 c                   eval      b1tek1 = %subst(vvtek1:1:22) +
6.21 c                                      ' ** UTGÅR  **'
6.21 c                   endif
     c                   endif
6.21 c                   if        vvnuda <> *loval and
6.21 c                             vvnuda <= w_ddat
6.21 c                   eval      b1tek1 = %subst(vvtek1:1:22) +
6.21 c                                      ' **NOBB.UTG**'
6.21 c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   eval      b_open_sok = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_stek
      *
      * Key for oppslag på vare
      *
     c     vvarlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlr_vare
      *
      * Key for posisjonering på varetekst
      *
     c     vvarl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl2_tek1
      *
      *
      * Henter parametre, og initierer skjermbildet
      *
     c                   eval      w_firm = p_firm
     c                   eval      s_stek = p_stek
      *
     c                   eval      *in22 = *on
      *
      * Henter dagens dato
      *
6.20 c                   time                    w_ddat
      *
6.10 c                   eval      w_lage = l_lagr
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     vvarl2_key    setll     vvarl2
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
