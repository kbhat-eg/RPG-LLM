     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : EK602R
      * Beskrivelse . . : Kunde bonus, skriver ut kundebrev
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       191011 bsa  Nytt program
6.10  * 6.10  140312 bsa  Feil i parametre
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fekbti3    if   e           k disk    rename(ekbtst:ekbti3r)
     fekbti1    if   e           k disk    rename(ekbtst:ekbti1r)
     fekboir    if   e           k disk    rename(ekbost:ekboirr)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fvbotl1    if   e           k disk    rename(vbotpfr:vbotl1r)
      *
     fek602p    o    e             printer oflind(*in30)
      *
      * Definering av parametre
      *
     d pkatt           s              4  0
     d pkatf           s              4  0
     d pknrf           s              6  0
     d pknrt           s              6  0
     d p_firm          s              3
     d p_paar          s              4
     d p_tbot          s              2
     d p_kfra          s              4
     d p_ktil          s              4
     d p_knrf          s              6
     d p_knrt          s              6
     d b_bonu          s              1    inz(*off)
     d b_kund          s              1    inz(*off)
      *
      * Definering av variabler i nøkler
      *
     d ekbti3_tkka     s                   like(ektkka)
     d ekbti1_tbku     s                   like(ektbku)
     d ekboir_bkun     s                   like(ekbkun)
     d rkunl1_kund     s                   like(rkkund)
     d vbotl1_tbot     s                   like(vatbot)
      *
      * Definering av variable
      *
     d w_firm          s                   like(ektfir)
     d w_tbot          s              2
     d w_kund          s              6  0
     d w_bgrl          s             11  2
6.10 d t1subg          s             11  2
     d w_aar           s              4  0
      *
      * Parameter overføringer
      *
     d                uds
     d l_wsid                901    910                                         Bruker
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980                                         Navn
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      b_bonu = *off
      * Skriv ut heading
      *
     c                   eval      t1subg = 0
     c                   eval      t1subn = 0
      * Hvis kundekategori er utfylt, leses på denne - alle kunder
     c                   if        pkatt <> 0
     c                   exsr      kal_kkat
     c                   else
     c                   exsr      kal_kund
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
      *
     c                   return
      *--------------------------------------------------------------
      * Bonus på kundekategori                                      *
      *--------------------------------------------------------------
     c     kal_kkat      begsr
      *
     c                   eval      w_tbot = *blank
     c                   eval      w_kund = 0
      *
     c                   eval      ekbti3_tkka  =  pkatf
     c     ekbti3_key    setll     ekbti3
     c     ekbti3_keyl   reade     ekbti3
      *
     c                   dow       not  %eof(ekbti3)
     c                   if        ektkka > pkatt
     c                   goto      end_kkat
     c                   endif
      *
     c                   if        p_tbot <> *blank and
     c                             ektbot <> p_tbot
     c                   goto      nyles
     c                   endif
      * Hent bonustypetekst
     c                   if        w_tbot <> ektbot
     c                   exsr      finn_btext
     c                   endif
      * Hent kundetekst
     c                   if        w_kund <> ektbku
     c                   if        b_bonu = *on
     c                   write     t1tot
     c                   eval      t1subg = 0
     c                   eval      t1subn = 0
     c                   eval      b_bonu = *off
     c                   endif
     c                   exsr      finn_ktext
     c                   endif
      * Skriv heading
     c                   if        b_kund = *on
     c                   write     a1hdr
     c                   eval      b_kund = *off
     c                   endif
      *
     c                   eval      w_bgrl = 0
     c                   exsr      kal_bgrl
     c                   eval      d1bgru = w_bgrl
     c                   eval      d1sbon = ektsbo
      *
     c                   if        ektsbo = 0 and
     c                             w_bgrl = 0
     c                   goto      nyles
     c                   endif
      *
     c     skriv_detalj  tag
     c                   eval      t1subg = t1subg + w_bgrl
     c                   eval      t1subn = t1subn + ektsbo
     c                   write     d1det                                30
     c                   eval      b_bonu = *on
     c                   if        *in30 = *on
     c                   exsr      xovrfl
     c                   endif
      *
     c     nyles         tag
      *
     c     ekbti3_keyl   reade     ekbti3
     c                   enddo
      *
     c     end_kkat      tag
      *
     c                   if        b_bonu = *on
     c                   write     t1tot
     c                   endif
      *
     c                   endsr
      *--------------------------------------------------------------
      * Bonus på kundekategori                                      *
      *--------------------------------------------------------------
     c     kal_kund      begsr
      *
     c                   eval      w_tbot = *blank
     c                   eval      w_kund = 0
     c                   eval      b_kund = *off
      *
6.10 c                   eval      ekbti1_tbku  =  0
     c     ekbti1_key    setll     ekbti1
     c     ekbti1_keyl   reade     ekbti1
      *
     c                   dow       not  %eof(ekbti1)
6.10 c**                 if        ektbku > pknrt
6.10 c**                 goto      end_kund
6.10 c**                 endif
      *
6.10 c                   if        p_tbot <> *blank and
     c                             ektbot <> p_tbot
     c                   goto      nyles2
     c                   endif
      * Hent bonustypetekst
     c                   if        w_tbot <> ektbot
     c                   exsr      finn_btext
     c                   endif
      * Hent kundetekst
     c                   if        w_kund <> ektbku
     c                   if        b_bonu = *on
     c                   write     t1tot
     c                   eval      t1subg = 0
     c                   eval      t1subn = 0
     c                   eval      b_bonu = *off
     c                   endif
     c                   exsr      finn_ktext
     c                   endif
      *
     c                   eval      w_bgrl = 0
     c                   exsr      kal_bgrl
     c                   eval      d1bgru = w_bgrl
     c                   eval      d1sbon = ektsbo
      *
     c                   if        ektsbo = 0 and
     c                             w_bgrl = 0
     c                   goto      nyles2
     c                   endif
      *
     c     skriv_detalj2 tag
     c                   eval      t1subg = t1subg + w_bgrl
     c                   eval      t1subn = t1subn + ektsbo
      * Skriv heading
     c                   if        b_kund = *on
     c                   write     a1hdr
     c                   eval      b_kund = *off
     c                   endif
      *
     c                   write     d1det                                30
     c                   eval      b_bonu = *on
      *
     c                   if        *in30 = *on
     c                   exsr      xovrfl
     c                   endif
      *
     c     nyles2        tag
      *
     c     ekbti1_keyl   reade     ekbti1
     c                   enddo
      *
     c     end_kund      tag
      *
     c                   if        b_bonu = *on
     c                   write     t1tot
     c                   endif
      *
     c                   endsr
      *--------------------------------------------------------------
      * Bonus på kundekategori                                      *
      *--------------------------------------------------------------
     c     kal_bgrl      begsr
      *
     c                   eval      ekboir_bkun  =  ektbku
     c     ekboir_key    setll     ekboir
     c     ekboir_key    reade     ekboir
      *
     c                   dow       not  %eof(ekboir)
     c                   if        ekbdat <> *loval
     c                   extrct    ekbdat:*y     w_aar
     c                   else
6.10 c                   move      p_paar        w_aar
     c                   endif
      *
6.10 c**                 if        w_aar = %int(p_paar) and
6.10 c**                           ektbot = ekboty
6.10 c                   if        ektbot = ekboty
     c                   eval      w_bgrl = w_bgrl + ekbgru
     c                   endif
     c     ekboir_key    reade     ekboir
     c                   enddo
      *
     c                   endsr
      *--------------------------------------------------------------
      * Finn kundetekst
      *--------------------------------------------------------------
     c     finn_ktext    begsr
      *
     c                   eval      w_kund = ektbku
     c                   eval      a1kunr = ektbku
     c                   eval      a1kuna = *blank
     c                   eval      a1kuga = *blank
     c                   eval      a1kuad = *blank
     c                   eval      a1kups = *blank
     c                   eval      rkunl1_kund = ektbku
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   movel     rknavn        a1kuna
     c                   movel     rkgate        a1kuga
     c                   movel     rkadr2        a1kuad
     c                   movel     rksted        a1kups
     c                   endif
      *
     c                   eval      b_kund = *on
      *
     c                   endsr
      /eject
      *--------------------------------------------------------------
      * Finn tekst for bonustype
      *--------------------------------------------------------------
     c     finn_btext    begsr
      *
     c                   eval      w_tbot = ektbot
     c                   eval      d1boty = ektbot
     c                   eval      d1besk = *blank
     c                   eval      vbotl1_tbot = ektbot
     c     vbotl1_key    chain     vbotl1
     c                   if        %found
     c                   movel     vatbes        d1besk
     c                   endif
      *
     c                   endsr
      /eject
      *--------------------------------------------------------------
      * Rutine for håndtering av ny side                            *
      *--------------------------------------------------------------
     c     xovrfl        begsr
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   eval      *in30 = *off
     c                   endif
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_paar
     c                   parm                    p_tbot
6.10 c                   parm                    p_kfra
6.10 c                   parm                    p_ktil
6.10 c                   parm                    p_knrf
6.10 c                   parm                    p_knrt
      *
      * Key for oppslag kunder
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag bonustype
      *
     c     vbotl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vbotl1_tbot
      *
      * Key for les av bonusgrunnlag
      *
     c     ekbti1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ekbti1_tbku
      *
      * Key for les av bonusgrunnlag
      *
     c     ekbti1_keyl   klist
     c                   kfld                    w_firm
      *
      * Key for les av bonusgrunnlag
      *
     c     ekboir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ekboir_bkun
      *
      * Key for les av bonusgrunnlag
      *
     c     ekbti3_keyl   klist
     c                   kfld                    w_firm
      *
      * Key for les av bonusgrunnlag
      *
     c     ekbti3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ekbti3_tkka
      *
      * Henter parameter
      *
     c                   move      l_firm        w_firm
     c                   eval      a1aarr = %int(p_paar)
     c                   move      p_kfra        pkatf
     c                   move      p_ktil        pkatt
     c                   move      p_knrf        pknrf
     c                   move      p_knrt        pknrt
      *
     c                   endsr
