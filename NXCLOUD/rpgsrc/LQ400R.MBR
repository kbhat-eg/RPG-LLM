     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LQ400R
      *  Beskrivelse . . : Sletting av lagertransaksjonsbunter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  -------------------------------------------------
6.nu  *  6.30      040614  Sjekket mtp. nummerutvidelse            bof
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fllhelu    uf   e           k disk    rename(llhepfr:llhelur)
     flldtl1    if   e           k disk    rename(lldtpfr:lldtl1r)
     flldtlu    uf   e           k disk    rename(lldtpfr:lldtlur)
      *
      * Parametre inn
      *
     d p_numm          s                   like(lqnumm)
      *
      * Local Data Area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d llhelu_numm     s                   like(lqnumm)
     d lldtl1_numm     s                   like(lsnumm)
     d lldtlu_numm     s                   like(lsnumm)
     d lldtlu_line     s                   like(lsline)
      *
      * Definering av øvrige variable
      *
     d w_firm          s                   like(lqfirm)
     d w_parm          s             64
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sletting av lagertransaksjoner : Linjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lldtl1_key    setll     lldtl1r
      *
     c     lldtl1_key    reade     lldtl1r
      *
     c                   dow       not %eof
     c                   eval      lldtlu_line = lsline
     c     lldtlu_key    chain     lldtlur
     c                   if        %found
     c                   delete    lldtlur
     c                   endif
      *
     c     lldtl1_key    reade     lldtl1r
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sletting av lagertransaksjoner : Hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     llhelu_key    chain     llhelur
     c                   if        %found
     c                   delete    llhelur
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Parameter
      *
     c     *entry        plist
     c                   parm                    p_numm
      *
      *  Key for oppdatering av LAGERTRANSASJONS-HODE
      *
     c     llhelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    llhelu_numm
      *
      *  Key for les av LAGERTRANSAKSJONS-LINJE
      *
     c     lldtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lldtl1_numm
      *
      *  Key for oppdatering av LAGERTRANSAKSJONS-LINJE
      *
     c     lldtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lldtlu_numm
     c                   kfld                    lldtlu_line
      *
      *  Legger inn verdier i nøklene
      *
     c                   eval      w_firm = l_firm
     c                   eval      llhelu_numm = p_numm
     c                   eval      lldtl1_numm = p_numm
     c                   eval      lldtlu_numm = p_numm
      *
     c                   endsr
