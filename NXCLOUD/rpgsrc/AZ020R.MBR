      /TITLE  Utskrift av FRF-definisjoner
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : AZ020R
      * Beskrivelse . . : Utskrift av FRF-definisjoner
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.
      * --------- ------  -----------------------------------------------
      *     R3.10 040600  Ny versjon av programmet er utarbeidet
      *
      *
      *
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  BRUK AV INDIKATORER
      *
      *       LR = Avslutt program
      *    60-69 = Fileindikatorer chain etc.
      *    70-79 = Arbeidsindikatorer i sub-rutiner
      *    80-89 = Arbeidsindikatorer ordinære
      *    90-99 = Benyttes ved std. sub-rutiner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     faddspf    if   e             disk
     faz020p    o    e             printer
      *
     d hlp             s              1    dim(40)                               Hjelp-array
      *
      *-----------------------------
      * Datastruktur for kommentarer
      *-----------------------------
      *
     d                 ds
     d d_hele                        88
     d  azcol7                        1    overlay(d_hele)
     d  azf002                       11    overlay(d_hele:2)
     d  azfelt                        6    overlay(d_hele:13)
     d  azf003                        6    overlay(d_hele:19)
     d  azflen                        4    overlay(d_hele:25)
     d  azanum                        1    overlay(d_hele:29)
     d  azf004                        1    overlay(d_hele:30)
     d  azades                        1    overlay(d_hele:31)
     d  azf005                        7    overlay(d_hele:32)
     d  azbesk                       30    overlay(d_hele:39)
     d  azf006                       20    overlay(d_hele:69)
      *
      * Definering av parametre
      *
     d p_bibl          s             10
     d p_brud          s              1  0
     d p_kod1          s              2
     d p_kod2          s              2
     d p_kod3          s              2
     d p_nfrf          s              4
      *
      * Definering av variable
      *
     d swtch1          s              1  0
     d swtch2          s              1  0
     d w_head          s              1  0
     d w_hlp1          s              1
     d w_hlp2          s              2
     d w_hlp4          s              4
     d w_hlp6          s              6
     d w_hlp8          s              8
     d w_flen          s              4  0
     d w_ovfl          s              1  0
     d w_side          s              1  0
     d w_sav2          s              2
     d w_koda          s              4
     d n               s              1  0
     d x               s              2  0
     d y               s              2  0
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Definering av formater
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Benyttede formater
      * - - - - - - - - - -
      *
      * A1HDR  - Heading A1 på rapport
      * D1DET  - Detail  D1 på rapport
      * T1TOT  - Total   T1 på rapport
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * A1HDR Behandle heading A1
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Les første post
      *
     c                   read      addspf                                 90                 .
      *
      * Klargjør heading
      *
     c                   eval      a1nfrf = p_nfrf                                           .
     c                   eval      a1bibl = p_bibl                                           .
      *
      * Dersom alle er blanke
      * skal alt tas med
      *
     c                   if        p_kod1 = *blank                                           .
     c                   if        p_kod2 = *blank                                           .
     c                   if        p_kod3 = *blank                                           .
     c                   eval      w_koda = 'ALLE'                                           .
     c                   endif                                                               .
     c                   endif                                                               .
     c                   endif                                                               .
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * D1DET Behandle detail D1
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser file
      *
     c                   dow       *in90 = *off                                          .....
      *
     c                   if        azcol7 = '*'                                              .
     c                   exsr      xtagms                                                    .
     c                   else                                                                .
     c                   exsr      xtagus                                                    .
     c                   endif                                                               .
      *
     c                   read      addspf                                 90                 .
     c                   enddo                                                           .....
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * T1TOT Behandle total T1
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Skrive totalsum
      *
     c                   write     t1tot                                30
     c                   goto      xslutt
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandle kommentarlinjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xtagms        begsr                                                               .
      *
      * Kode er ikke lagt på ..
      *
     c                   if        azbesk = *blank                                           .
     c                   goto      xendms                                                    .
     c                   endif                                                               .
      *
      * Kode starter ikke med parentes
      *
     c                   eval      w_hlp1 = %subst(azbesk:1:1)                               .
     c                   if        w_hlp1 <> '('                                             .
     c                   goto      xendms                                                    .
     c                   endif                                                               .
      *
      * Skal alt skrives ?
      *
     c                   if        w_koda = 'ALLE'                                           .
     c                   goto      xprtms                                                    .
     c                   endif                                                               .
      *
      * Hent ut recordkode i arbeidsfelt
      *
     c                   eval      w_hlp2 = %subst(azbesk:2:2)                               .
      *
      * Sjekke om kode 1
      * er oppgitt
      *
     c                   if        p_kod1 <> *blank                                          .
     c                   if        p_kod1 = w_hlp2                                           .
     c                   goto      xprtms                                                    .
     c                   endif                                                               .
     c                   endif                                                               .
      *
      * Sjekke om kode 2
      * er oppgitt
      *
     c                   if        p_kod2 <> *blank                                          .
     c                   if        p_kod2 = w_hlp2                                           .
     c                   goto      xprtms                                                    .
     c                   endif                                                               .
     c                   endif                                                               .
      *
      * Sjekke om kode 3
      * er oppgitt
      *
     c                   if        p_kod3 <> *blank                                          .
     c                   if        p_kod3 = w_hlp2                                           .
     c                   goto      xprtms                                                    .
     c                   endif                                                               .
     c                   endif                                                               .
      *
      * Har ikke fått tilslag,
      * ny runde...
      *
     c                   goto      xendms                                                    .
      *
      * Post skal printes
      *
     c     xprtms        tag                                                                 .
      *
      * Skal det være ny side ?
      *
     c                   if        p_brud = 1                                                .
     c                   write     a1hdr                                                     .
     c                   eval      w_side = 1                                                .
     c                   endif                                                               .
      *
      * Er første side skrevet ?
      *
     c                   if        w_side = 0                                                .
     c                   write     a1hdr                                                     .
     c                   eval      w_side = 1                                                .
     c                   endif                                                               .
      *
      * Skrive overskrifter
      *
     c                   eval      d0hed1 = d_hele
     c                   write     d0det                                30
      *
      * Overflow ?
      *
     c                   if        *in30  = *on                                              .
     c                   write     a1hdr                                                     .
     c                   eval      *in30  = *off                                             .
     c                   endif                                                               .
      *
     c     xendms        endsr                                                               .
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandle ordinære linjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xtagus        begsr                                                               .
      *
     c                   if        azfelt <> *blank                                          .
     c                   movel     azfelt        w_hlp2                                      .
     c                   endif                                                               .
      *
      * Dersom alle er blanke
      * skal alt tas med
      *
     c                   if        p_kod1 = *blank                                           .
     c                   if        p_kod2 = *blank                                           .
     c                   if        p_kod3 = *blank                                           .
     c                   goto      xprtus
     c                   endif                                                               .
     c                   endif                                                               .
     c                   endif                                                               .
      *
      * Sjekke om kode 1
      * er oppgitt
      *
     c                   if        p_kod1 <> *blank                                          .
     c                   if        p_kod1 = w_hlp2                                           .
     c                   goto      xprtus                                                    .
     c                   endif                                                               .
     c                   endif                                                               .
      *
      * Sjekke om kode 2
      * er oppgitt
      *
     c                   if        p_kod2 <> *blank                                          .
     c                   if        p_kod2 = w_hlp2                                           .
     c                   goto      xprtus                                                    .
     c                   endif                                                               .
     c                   endif                                                               .
      *
      * Sjekke om kode 3
      * er oppgitt
      *
     c                   if        p_kod3 <> *blank                                          .
     c                   if        p_kod3 = w_hlp2                                           .
     c                   goto      xprtus                                                    .
     c                   endif                                                               .
     c                   endif                                                               .
      *
      * Har ikke fått tilslag,
      * ny runde...
      *
     c                   goto      xendus                                                    .
      *
      * Utskrift skal utføres
      *
     c     xprtus        tag                                                                 .
      *
      * Er første side skrevet ?
      *
     c                   if        w_side = 0                                                .
     c                   write     a1hdr                                                     .
     c                   eval      w_side = 1                                                .
     c                   endif                                                               .
      *
      * Overflow ?
      * Nb! Det leses flere records
      * pr. skrevet linje, derfor
      * denne logikk
      *
     c                   if        azfelt <> *blank                                          .
     c                   if        w_ovfl = 1                                                .
     c                   move      0             w_ovfl                                      .
     c                   move      *off          *in30                                       .
     c                   move      1             w_head                                      .
     c                   endif                                                               .
     c                   endif                                                               .
      *
      * Skrive heading
      *
     c                   if        w_head = 1                                                .
     c                   write     a1hdr                                                     .
     c                   move      0             w_head                                      .
     c                   endif                                                               .
      *
      * Skrive navn
      * på felter
      *
     c                   if        azfelt <> *blank
3.10  *
3.10 c                   movel     azbesk        w_hlp6
3.10 c                   if        w_hlp6 = 'REFFLD'
3.10 c                   movea     azbesk        hlp
3.10 c                   movea     hlp(7)        d1reff
3.10 c                   endif
3.10  *
     c                   move      azflen        d1flen
     c                   write     d1det                                30
     c                   if        *in30 = *on
     c                   move      1             w_ovfl
     c                   endif
     c                   eval      t1anta = t1anta + 1
     c                   eval      d1reff = *blank
     c                   endif
      *
      * Skrive editkode
      *
     c                   movel     azbesk        w_hlp6
     c                   if        w_hlp6 = 'EDTCDE'
     c                   movea     azbesk        hlp
     c                   move      hlp(8)        d2edtc
     c                   write     d2det
     c                   endif
      *
      * Skrive kolonne-
      * heading
      *
     c                   movel     azbesk        w_hlp6
      *
     c                   if        w_hlp6 = 'COLHDG'
     c                   movea     azbesk        hlp
     c                   movea     hlp(9)        d3besk
     c                   movea     d3besk        hlp
     c     1             do        40            x
     c                   if        hlp(x) = ')'
     c                   eval      hlp(x) = *blank
     c                   eval      y = x - 1
     c                   eval      hlp(y) = *blank
     c                   endif
     c                   enddo
     c                   movea     hlp           d3besk
     c                   write     d3det
     c                   endif
      *
      * Skrive tekst
      *
     c                   movel     azbesk        w_hlp4
      *
     c                   if        w_hlp4 = 'TEXT'
     c                   movea     azbesk        hlp
     c                   movea     hlp(7)        d4text
     c                   movea     d4text        hlp
     c     1             do        40            x
     c                   if        hlp(x) = ')'
     c                   eval      hlp(x) = *blank
     c                   eval      y = x - 1
     c                   eval      hlp(y) = *blank
     c                   endif
     c                   enddo
     c                   movea     hlp           d4text
     c                   write     d4det
     c                   endif
      *
      * Skrive values
      *
     c                   movel     azbesk        w_hlp6
      *
     c                   if        w_hlp6 = 'VALUES'
     c                   movea     azbesk        hlp
     c                   movea     hlp(7)        d5valu
     c                   write     d5det
     c                   endif
      *
      * Skrive msgid
      *
     c                   movel     azbesk        w_hlp8
      *
     c                   if        w_hlp8 = 'CHKMSGID'
     c                   movea     azbesk        hlp
     c                   movea     hlp(9)        d6msid
     c                   write     d6det
     c                   endif
      *
     c     xendus        endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c                   eval      w_sav2 = *blank
     c                   eval      w_hlp2 = *blank
     c                   eval      w_hlp4 = *blank
     c                   eval      w_hlp6 = *blank
     c                   eval      w_hlp8 = *blank
     c                   eval      w_head = *zero
     c                   eval      w_ovfl = *zero
     c                   eval      swtch1 = *zero
     c                   eval      swtch2 = *zero
     c                   eval      n = *zero
     c                   eval      x = *zero
     c                   eval      y = *zero
     c                   eval      t1anta = *zero
     c                   eval      w_flen = *zero
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_bibl
     c                   parm                    p_nfrf
     c                   parm                    p_kod1
     c                   parm                    p_kod2
     c                   parm                    p_kod3
     c                   parm                    p_brud
      *
     c                   endsr
      *
