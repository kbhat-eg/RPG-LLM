     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLOGI
      * Program . . . . : mm401R
      * Beskrivelse . . : Rydder i gamle abckoder pr. parameter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       020409 bø   Nytt program
6.11  *       291112 bø   Feilretting vedr. komb. vsor + varegrupper
6.20  *       231215 bsa  Utvider med parameter som sier hvilken abc kode som
6.20  *                   skal nullstilles.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarlr    if   e           k disk    rename(vvarpfr:vvarlrr)
     fvvarl8    if   e           k disk    rename(vvarpfr:vvarl8r)
     fvvarla    if   e           k disk    rename(vvarpfr:vvarlar)
     fvsdtl6    if   e           k disk    rename(vsdtpfr:vsdtl6r)
     fvlaglu    uf   e           k disk    rename(vlagpfr:vlaglur)
     fvvarlu    uf   e           k disk    rename(vvarpfr:vvarlur)
      *
      *
     d vvarlu_vare     s                   like(vvvare)
     d vlaglu_vare     s                   like(vlvare)
     d vlaglu_lage     s                   like(vllage)
     d vvarl8_ogrp     s                   like(vvogrp)
     d vvarl8_hgrp     s                   like(vvhgrp)
     d vvarl8_ugrp     s                   like(vvugrp)
     d vvarla_ldor     s                   like(vvldor)
     d vvarlx_ogrp     s                   like(vvogrp)
     d vvarlx_hgrp     s                   like(vvhgrp)
     d vvarly_ogrp     s                   like(vvogrp)
     d vvarlr_vare     s                   like(vvvare)
     d vsdtl6_vsor     s                   like(vdvsor)
      *
      * Parameter-inn
     d p_dato          s               d   datfmt(*iso)
     d p_antu          s              3  0
      *
      * Local Data Area
      *
     d                uds
      *
     d l_user                911    920
     d l_firm                944    946  0
      *
     d                 ds
     d d_list                       128
     d  d_vsor                       10    overlay(d_list)
     d  d_ogrp                        2  0 overlay(d_list:11)
     d  d_hgrp                        2  0 overlay(d_list:13)
     d  d_ugrp                        3  0 overlay(d_list:15)
     d  d_lage                        2    overlay(d_list:18)
6.20 d  d_pabc                        1    overlay(d_list:20)
     d  d_dumm                        1    overlay(d_list:126)
      *
      * Definering av variable
      *
     d w_list          s            128
     d w_ogrp          s                   like(vvogrp)
     d w_hgrp          s                   like(vvhgrp)
     d w_ugrp          s                   like(vvugrp)
     d w_ldor          s                   like(vvldor)
     d w_firm          s                   like(vvfirm)
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   if        d_vsor <> *blank or
     c                             d_ogrp <> 0 or
     c                             d_hgrp <> 0 or
     c                             d_ugrp <> 0
     c                   exsr      les_utvalg
     c                   else
      *
     c                   read      vvarl1
     c                   dow       not %eof
      *
     c                   exsr      beh_vare
      *
     c                   read      vvarl1
     c                   enddo
      *
     c                   endif
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
      *
     c                   return
      * --------------------------------------------------------------
      *  leser utvalg av varer
      * --------------------------------------------------------------
      *
     c     les_utvalg    begsr
      *
     c                   if        d_vsor <> *blank
6.11 c                   eval      w_ogrp = d_ogrp
6.11 c                   eval      w_hgrp = d_hgrp
6.11 c                   eval      w_ugrp = d_ugrp
     c                   exsr      les_vsor
     c                   goto      end_utvalg
     c                   endif
      *
     c                   if        d_ugrp <> 0
     c                   eval      w_ogrp = d_ogrp
     c                   eval      w_hgrp = d_hgrp
     c                   eval      w_ugrp = d_ugrp
     c                   exsr      les_ugr
     c                   goto      end_utvalg
     c                   endif
      *
     c                   if        d_hgrp <> 0
     c                   eval      w_ogrp = d_ogrp
     c                   eval      w_hgrp = d_hgrp
     c                   exsr      les_hgr
     c                   goto      end_utvalg
     c                   endif
      *
     c                   if        d_ogrp <> 0
     c                   eval      w_ogrp = d_ogrp
     c                   exsr      les_ogr
     c                   goto      end_utvalg
     c                   endif
      *
     c     end_utvalg    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser varesortiment
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_vsor      begsr
      *
     c                   eval      vsdtl6_vsor = d_vsor
     c     vsdtl6_key    setll     vsdtl6
     c     vsdtl6_key    reade     vsdtl6
     c                   dow       not %eof(vsdtl6)
6.11  *
6.11  * kombinasjon leverandør og vare / varegruppe
6.11  *
6.11 c                   if        vdldor <> 0
6.11 c                   eval      w_ldor = vdldor
6.11 c                   select
6.11 c                   when      vdvare <> *blank
6.11 c                   eval      vvarlr_vare = vdvare
6.11 c     vvarlr_key    chain     vvarlr
6.11 c                   if        %found(vvarlr) and
6.11 c                             vvldor = w_ldor
6.11 c                   exsr      beh_vare
6.11 c                   endif
6.11  *
6.11 c                   when      vdugrp <> 0
6.11 c                   eval      w_ogrp = vdogrp
6.11 c                   eval      w_hgrp = vdhgrp
6.11 c                   eval      w_ugrp = vdugrp
6.11 c                   exsr      les_ugr_l
6.11 c                   when      vdhgrp <> 0
6.11 c                   eval      w_ogrp = vdogrp
6.11 c                   eval      w_hgrp = vdhgrp
6.11 c                   exsr      les_hgr_l
6.11 c                   when      vdogrp <> 0
6.11 c                   eval      w_ogrp = vdogrp
6.11 c                   exsr      les_ogr_l
6.11 c                   when      vdldor <> 0
6.11 c                   eval      w_ldor = vdldor
6.11 c                   exsr      les_ldor
6.11 c                   endsl
6.11  *
6.11 c                   else
      *
     c                   select
     c                   when      vdvare <> *blank
6.11 c*                  exsr      beh_vare
     c                   eval      vvarlr_vare = vdvare
     c     vvarlr_key    chain     vvarlr
     c                   if        %found(vvarlr)
     c                   exsr      beh_vare
     c                   endif
      *
     c                   when      vdugrp <> 0
     c                   eval      w_ogrp = vdogrp
     c                   eval      w_hgrp = vdhgrp
     c                   eval      w_ugrp = vdugrp
     c                   exsr      les_ugr
     c                   when      vdhgrp <> 0
     c                   eval      w_ogrp = vdogrp
     c                   eval      w_hgrp = vdhgrp
     c                   exsr      les_hgr
     c                   when      vdogrp <> 0
     c                   eval      w_ogrp = vdogrp
     c                   exsr      les_ogr
     c                   when      vdldor <> 0
     c                   eval      w_ldor = vdldor
     c                   exsr      les_ldor
     c                   endsl
      *
6.11 c                   endif
      *
     c     vsdtl6_key    reade     vsdtl6
     c                   enddo
      *
     c                   endsr
      *
6.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.11  *  Leser tom ugr på vareregister kaller behandling av vare
6.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.11  *
6.11 c     les_ugr_l     begsr
6.11  *
6.11 c                   eval      vvarl8_ogrp = w_ogrp
6.11 c                   eval      vvarl8_hgrp = w_hgrp
6.11 c                   eval      vvarl8_ugrp = w_ugrp
6.11 c     vvarl8_key    setll     vvarl8
6.11 c     vvarl8_key    reade     vvarl8
6.11 c                   dow       not %eof(vvarl8)
6.11 c                   if        vvldor = w_ldor
6.11 c                   exsr      beh_vare
6.11 c                   endif
6.11 c     vvarl8_key    reade     vvarl8
6.11 c                   enddo
6.11  *
6.11 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser tom ugr på vareregister kaller behandling av vare m/leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_ugr       begsr
      *
     c                   eval      vvarl8_ogrp = w_ogrp
     c                   eval      vvarl8_hgrp = w_hgrp
     c                   eval      vvarl8_ugrp = w_ugrp
     c     vvarl8_key    setll     vvarl8
     c     vvarl8_key    reade     vvarl8
     c                   dow       not %eof(vvarl8)
     c                   exsr      beh_vare
     c     vvarl8_key    reade     vvarl8
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser tom hgr på vareregister kaller behandling av vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_hgr       begsr
      *
     c                   eval      vvarlx_ogrp = w_ogrp
     c                   eval      vvarlx_hgrp = w_hgrp
     c     vvarlx_key    setll     vvarl8
     c     vvarlx_key    reade     vvarl8
     c                   dow       not %eof(vvarl8)
     c                   exsr      beh_vare
     c     vvarlx_key    reade     vvarl8
     c                   enddo
      *
     c                   endsr
6.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.11  *  Leser tom hgr på vareregister kaller behandling av vare  m/leverandør
6.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.11  *
6.11 c     les_hgr_l     begsr
6.11  *
6.11 c                   eval      vvarlx_ogrp = w_ogrp
6.11 c                   eval      vvarlx_hgrp = w_hgrp
6.11 c     vvarlx_key    setll     vvarl8
6.11 c     vvarlx_key    reade     vvarl8
6.11 c                   dow       not %eof(vvarl8)
6.11 c                   if        vvldor = w_ldor
6.11 c                   exsr      beh_vare
6.11 c                   endif
6.11 c     vvarlx_key    reade     vvarl8
6.11 c                   enddo
6.11  *
6.11 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser tom ogr på vareregister kaller oppdatering av lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_ogr       begsr
      *
     c                   eval      vvarly_ogrp = w_ogrp
     c     vvarly_key    setll     vvarl8
     c     vvarly_key    reade     vvarl8
     c                   dow       not %eof(vvarl8)
     c                   exsr      beh_vare
     c     vvarly_key    reade     vvarl8
     c                   enddo
      *
     c                   endsr
6.11  *
6.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.11  *  Leser tom ogr på vareregister kaller oppdatering av lager m/leverandør
6.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.11  *
6.11 c     les_ogr_l     begsr
6.11  *
6.11 c                   eval      vvarly_ogrp = w_ogrp
6.11 c     vvarly_key    setll     vvarl8
6.11 c     vvarly_key    reade     vvarl8
6.11 c                   dow       not %eof(vvarl8)
6.11 c                   if        vvldor = w_ldor
6.11 c                   exsr      beh_vare
6.11 c                   endif
6.11 c     vvarly_key    reade     vvarl8
6.11 c                   enddo
6.11  *
6.11 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser varer pr. leverandør - behandling av vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_ldor      begsr
      *
     c                   eval      vvarla_ldor = w_ldor
     c     vvarla_key    setll     vvarla
     c     vvarla_key    reade     vvarla
     c                   dow       not %eof(vvarla)
     c                   exsr      beh_vare
     c     vvarla_key    reade     vvarla
     c                   enddo
      *
     c                   endsr
      *
      *
      *
      * --------------------------------------------------------------
      *  behandler lest vare
      * --------------------------------------------------------------
      *
     c     beh_vare      begsr
      *
     c                   if        d_lage <>*blank
     c                   eval      vlaglu_vare = vvvare
     c                   move      d_lage        vlaglu_lage
     c     vlaglu_key    chain     vlaglu
     c                   if        %found(vlaglu) and
     c                             vllabc  = 0
6.20  *
6.20 c                   if        d_pabc  = 'P'
     c                   eval      vlkabc  = *blank
6.20 c                   elseif    d_pabc  = 'S'
6.20 c                   eval      vltabc  = *blank
6.20 c                   endif
6.20  *
     c                   update    vlaglur
     c                   endif
      *
     c                   else
     c                   eval      vvarlu_vare = vvvare
     c     vvarlu_key    chain     vvarlu
     c                   if        %found(vvarlu) and
     c                             vvlabc  = 0
     c                   eval      vvkabc  = *blank
     c                   update    vvarlur
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av varesortiment
      *
     c     vsdtl6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vsdtl6_vsor
      *
      *  Key for les av vareregister
      *
     c     vvarlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlr_vare
      *
      *  Key for les av vareregister
      *
     c     vvarl8_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl8_ogrp
     c                   kfld                    vvarl8_hgrp
     c                   kfld                    vvarl8_ugrp
      *
      *  Key for les av vareregister  pr. lev.
      *
     c     vvarla_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarla_ldor
      *
      *  Key for les av vareregister
      *
     c     vvarlx_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlx_ogrp
     c                   kfld                    vvarlx_hgrp
      *
      *  Key for les av vareregister
      *
     c     vvarly_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarly_ogrp
      *
      *
      *  Key for oppdater vare
      *
     c     vvarlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlu_vare
      *
      *  Key for oppdater lager
      *
     c     vlaglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglu_vare
     c                   kfld                    vlaglu_lage
      *
     c     *entry        plist
     c                   parm                    w_list
     c                   eval      d_list = w_list
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
