     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : HR731R
      * Beskrivelse . . : 2DPOINT HH - varemottak oppdatering
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       210110 ASK  Nytt program
6.20  *       230114 bsa  Må omregne antall til enhet på bestilling,
6.20  *                   siden enhet ikke tas med videre.
6.nu  *       060614 bof  Gjennomgått mtp. nummerutvidelse
      *
8.01  *K000   100822 bsa  Skal automatisk oppdatere og flytte tilknyttet
8.01  *K000               salgsordre. NB Styres av switch, om det er noen som
8.01  *K000               ikke ønsker funksjonen.
8.02  *K000   120822 bsa  Kjør egen rutine for oppdatering av salsgordre
8.03  *K000   220822 bsa  Legg også ut tekstlinjer som er lagt på ifbm
8.03  *K000               plukking, ref kolliinfo.
8.04  *8.xx   300822 bsa  Plukk også alle tjenestelinjer, hvis alle
8.04  *8.xx               L/S varer på ordren er plukket.
8.05  *8.xx   310822 bsa  Sørg for å frigi poster, hvis ikke er det
8.05  *8.xx               fare for låsing videre.
8.06  *8.xx   280922 bsa  Hvis ikke det skrives bestillingslinje, kan heller
8.06  *8.xx               ikke tilhørende ordrelinje skrives.
8.07  *8.xx   310810 bsa  Flyttet sjekk mot synkronsisering av lager. Siden dette
8.07  *8.xx               ikke nødvendigvis skal kjøres for alle lagre, sjekk
8.07  *8.xx               mot enkeltlager.
8.08  *8.xx   091122 bsa  Hvis tekstlinje er med i plukketabellen, skal den
8.08  *8.xx               alltid plukkes med ut.
8.09  *8.xx   091222 bsa  Samkjøre koder for samme restbehandling mellom B og SO
8.10  *8.xx   151222 bsa  Regn også om hvis enheten på SO ikke er ihht plukket
8.10  *8.xx               antall.
8.11  *K000   100123 bsa  Send transportrequest hvis tilhørende ordre oppfyller
8.11  *K000               krav til kjøring. Det er mulig at bestillingen er gått uten
8.11  *K000               utenom "radaren" så langt.
8.12  *K000   130123 bsa  Endre sjekken for å finne ut om alle linjer er plukket
8.12  *K000               på tilhørende salgsordre.
8.13  *K000   170123 bsa  Endret program for kalkulering av enheter.
8.14  *K000   180123 bsa  Flagg om skaffetekst i transportlogg.
8.15  *K000   190123 bsa  Send ikke transportrequest, hvis den finnes fra før. Da skal
8.15  *K000               oppdateringer skjer via SO (FO808R)
8.16  *K000   130223 bsa  Flagg loggekoden.
8.17  *  K000  010623 Bsa  Utfordring om samme varelinje varemottas over
8.17  *  K000              flere kolli. Da må det akkumuleres opp.
8.18  *  K000 080923 mst   Initiere switch til '*off'
8.19  *PRG2    050124 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
8.20  *K0000   260924 bsa  Hvis plukket er 0, kan det være du får låsing
8.20  *K0000               av utplukkstabell. Kombinasjon levert/resting
8.20  *K0000               kan gjøre at du ikke får frigjort låsing av post.
8.21  *K0000   201124 bsa  Neste ordrtype på tilhørende SO kan legges i switch
8.21  *K0000               Gjelder bare PDA og varemottak.
8.22  *K0000  140624 bsa  Hvis varen kommer fra VA, skal denne behandles som L/S vare
8.23  *K0000  161224 bsa  Hvis ordretypen er intern, skal neste ordretype fra opprinnelig
8.23  *K0000              ordretype benyttes, og ikke hentes fra switchen.
      *
9.xx  *K000   xxxxxx bsa  Kode benyttet for debug og hjelp.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     flwvalu    uf a e           k disk    rename(lwvapfr:lwvalur)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     flplolu    uf a e           k disk    rename(lplopfr:lplolur)
     flplllu    uf a e           k disk    rename(lpllpfr:lplllur)
8.01 ffplolu    uf a e           k disk    rename(fplopfr:fplolur)
8.01 ffplllu    uf a e           k disk    rename(fpllpfr:fplllur)
8.01 ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
8.03 ffodtlr    if   e           k disk    rename(fodtpfr:fodtlrr)
8.09 ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
8.04 fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
8.15 fftrpi2    if   e           k disk    rename(ftrpst:ftrpi2r)
8.21 frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
8.23 fvot1l1    if   e           k disk    rename(vot1pfr:vot1l1r)
      *
      * Local Data Area
      *
     d                uds
     d l_user                911    920
8.01 d l_filg                931    933
      *
      * Definering av parametre
      *
     d p_firm          s                   like(ldfirm)
     d p_numm          s                   like(ldnumm)
     d p_suff          s                   like(ldsuff)
     d p_user          s                   like(l_user)
     d p_stat          s              1
      *
      * Definering av variabler i nøkler
      *
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d lodtl1_line     s                   like(ldline)
     d lwvalu_numm     s                   like(mvnumm)
     d lwvalu_suff     s                   like(mvsuff)
     d lwvalu_line     s                   like(mvline)
     d votyl1_otyp     s                   like(vaotyp)
     d lplolu_ponr     s                   like(lkponr)
     d lplolu_psuf     s                   like(lkpsuf)
     d lplllu_lonr     s                   like(lklonr)
     d lplllu_lsuf     s                   like(lklsuf)
     d lplllu_llin     s                   like(lkllin)
      *
8.01 d fplllu_lonr     s                   like(fklonr)
8.01 d fplllu_lsuf     s                   like(fklsuf)
8.01 d fplllu_llin     s                   like(fkllin)
8.01 d fplol1_ponr     s                   like(fkponr)
8.01 d fplol1_psuf     s                   like(fkpsuf)
8.01 d fohel1_numm     s                   like(fonumm)
8.01 d fohel1_suff     s                   like(fosuff)
8.03 d fodtlr_numm     s                   like(fonumm)
8.03 d fodtlr_suff     s                   like(fosuff)
8.03 d fodtlr_line     s                   like(fdline)
8.09 d fodtl1_numm     s                   like(fonumm)
8.09 d fodtl1_suff     s                   like(fosuff)
8.09 d fodtl1_line     s                   like(fdline)
8.04 d vvarl1_vare     s                   like(vvvare)
8.15 d ftrpi2_rnum     s                   like(ftrnum)
8.15 d ftrpi2_rsuf     s                   like(ftrsuf)
8.21 d rkunl1_kund     s                   like(rkkund)
      *
      * Definering av variable
      *
     d w_firm          s                   like(lofirm)
     d w_dirk          s              1  0
     d w_stat          s              1
     d w_outq          s             10
6.20 d w_vare          s                   like(ldvare)
6.20 d w_anta          s                   like(ldanta)
6.20 d w_antu          s                   like(ldanta)
6.20 d w_enhe          s                   like(ldenh1)
6.20 d w_enhu          s                   like(ldenh1)
8.01 d w_otyp          s                   like(lootyp)
8.10 d w_want          s                   like(ldanta)
8.11 d w_reqs          s              5
8.13 d w_levr          s                   like(ldldor)
8.14 d w_skaf          s              1  0
8.16 d w_logg          s              1  0
8.21 d w_pda_otyp_0    s              2
8.21 d w_pda_otyp_2    s              2
      *
     d b_pluk          s              1    inz(*off)
8.01 d b_ordr          s              1    inz(*off)
8.04 d b_allo          s              1    inz(*off)
      *
8.01 d u_ordr          s              1    inz(*off)
8.18 d*u_flat          s              1    inz(*on)
8.18 d u_flat          s              1    inz(*off)
      *
8.01   dcl-s co402_verdi1  char(1);
8.01   dcl-s co402_verdi2  char(2048);
8.01   dcl-pr co402r extpgm('CO402R');
8.01     p_filgrp            char(3)     const;
8.01     p_firm              packed(3:0) const;
8.01     p_lager             packed(2:0) const;
8.01     p_nokkel            char(50)    const;
8.01     p_verdi1            char(1);
8.01     p_verdi2            char(2048);
8.01   end-pr;
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer varemottak
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      p_stat = ' '
      *
      * Leser bestillingshode
      *
     c     lohel1_key    chain     lohel1
8.07  *
8.07  * Sjekk om synkronisering skal gjøres ?
8.07  *
8.07   CallP CO402R(l_filg:w_firm:lolage:'SYNKE_ORDRE':
8.07                    co402_verdi1:co402_verdi2);
8.07   if co402_verdi1 = '1';
8.07     u_ordr = *on;
8.21     w_pda_otyp_0 = %subst(co402_verdi2:%scan('PDA0:':co402_verdi2) + 5:2);
8.21     w_pda_otyp_2 = %subst(co402_verdi2:%scan('PDA2:':co402_verdi2) + 5:2);
8.07   endif;
8.07  *
8.07  * Sjekk om synkronisering skal gjøres ?
8.07  *
8.01 c                   if        u_ordr = *off
8.07  *
8.07   CallP CO402R(l_filg:w_firm:0:'SYNKE_ORDRE':
8.07                    co402_verdi1:co402_verdi2);
8.07   if co402_verdi1 = '1';
8.07     u_ordr = *on;
8.21     w_pda_otyp_0 = %subst(co402_verdi2:%scan('PDA0:':co402_verdi2) + 5:2);
8.21     w_pda_otyp_2 = %subst(co402_verdi2:%scan('PDA2:':co402_verdi2) + 5:2);
8.07   endif;
8.07  *
8.07 c                   endif
8.07  *
8.01 c                   if        u_ordr = *on
8.01  *
8.01  * Er det tilhørende salgsordre
8.01  *
8.01 c                   eval      w_otyp = *blanks
8.01 c                   eval      b_ordr = *off
8.01 c                   eval      fohel1_numm = loornr
8.01 c                   eval      fohel1_suff = loorsu
8.01 c     fohel1_key    chain     fohel1
8.01 c                   if        %found
8.01 c                   eval      b_ordr = *on
8.21  *
8.21  * Sjekk kreditkoden på kunden.
8.21  *
8.21 c                   eval      rkunl1_kund = fokund
8.21 c     rkunl1_key    chain     rkunl1
8.21 c                   if        %found
8.21 c                   if        rkkres = 0
8.21 c                   eval      w_otyp = %trim(w_pda_otyp_0)
8.21 c                   elseif    rkkres = 2
8.21 c                   eval      w_otyp = %trim(w_pda_otyp_2)
8.21 c                   endif
8.21 c                   endif
8.21  *
8.21  * Sjekk ordretypen fra switchen
8.21  *
8.21 c                   if        %trim(w_otyp) <> *blanks
8.21 c                   eval      votyl1_otyp = %trim(w_otyp)
8.21 c     votyl1_key    chain     votyl1r
8.21 c                   if        %found
8.21 c                   if        vaoakk < 1 or
8.21 c                             vaoakk > 2
8.21 c                   eval      w_otyp = *blanks
8.21 c                   endif
8.21 c                   else
8.21 c                   eval      w_otyp = *blanks
8.21 c                   endif
8.21 c                   endif
8.23  *
8.23  * Hvis ordretypen fra salgsordren, er intern, skal neste ordretype
8.23  * hentes fra ordrtypen ikke fra SWITCH.
8.23  *
8.23 c                   eval      votyl1_otyp = footyp
8.23 c     votyl1_key    chain     vot1l1
8.23 c                   if        %found and
8.23 c                             va1int = 1
8.23 c                   eval      w_otyp = *blanks
8.23 c                   endif
8.01  *
8.01  * Finn neste ordretype til evnt salgsordre.
8.01  *
8.21 c                   if        w_otyp = *blanks
8.01 c                   eval      votyl1_otyp = footyp
8.01 c     votyl1_key    chain     votyl1
8.01 c                   if        %found and
8.01 c                             vaoakk = 1
8.01 c                   eval      w_otyp = vaonxt
8.01 c                   else
8.01 c                   eval      w_otyp = *blanks
8.01 c                   eval      b_ordr = *off
8.01 c                   endif
8.21 c                   endif
8.01 c                   endif
      *
8.01 c                   endif
      *
      * Finn neste ordretype
      *
     c                   eval      votyl1_otyp = lootyp
     c     votyl1_key    chain     votyl1r
      *
      * Leser bestillingslinjer
      *
     c                   eval      lodtl1_line = 0
     c     lodtl1_key    setll     lodtl1
     c                   read      lodtl1
      *
     c                   dow       not %eof  and
     c                             ldnumm = p_numm and
     c                             ldsuff = p_suff
      *
      * Leser innmeldt og skriver plukklinje
      *
     c                   eval      lwvalu_line = ldline
     c     lwvalu_key    chain     lwvalu
     c                   if        not %found
     c                   eval      mvlevr = 0
     c                   eval      mvrkod = 0
     c                   endif
      *
8.17 c                   eval      lplllu_lonr = ldnumm
8.17 c                   eval      lplllu_lsuf = ldsuff
8.17 c                   eval      lplllu_llin = ldline
8.17 c     lplllu_key    chain     lplllu
8.17 c                   if        not %found
8.17 c                   clear                   lplllur
8.17 c                   eval      lklfir = w_firm
8.17 c                   eval      lklonr = ldnumm
8.17 c                   eval      lklsuf = ldsuff
8.17 c                   eval      lkllin = ldline
8.17 c                   endif
6.20  *
6.20  * Behandle innkommet antall
6.20  *
8.10 c                   eval      w_want = mvlevr
6.20  *
6.20 c                   if        mvenhe <> *blanks and
6.20 c                             mvenhe <> ldenh1
6.20 c                   exsr      omr_antall
6.20 c                   endif
      *
     c                   if        mvrkod = 1 or
     c                             mvlevr > 0
     c                   eval      b_pluk = *on
     c                   eval      lklfir = w_firm
     c                   eval      lklonr = p_numm
     c                   eval      lklsuf = p_suff
     c                   eval      lkllin = ldline
8.17 c*                  eval      lklant = mvlevr
8.17 c                   eval      lklant = lklant + mvlevr
      *
     c                   if        mvrkod = 0
     c                   eval      lklkod = '5'
     c                   else
     c                   eval      lklkod = *blank
     c                   endif
      *
8.08 c                   if        %trim(ldvare) = *blanks
8.08 c                   eval      lklkod = '1'
8.08 c                   endif
      *
8.20 c                   endif
      *
8.17 c                   if        %found
8.17 c                   update    lplllur
8.17 c                   else
     c                   write     lplllur
8.17 c                   endif
8.20 c*                  endif
8.01  *
8.01 c                   if        u_ordr = *on
8.01  *
8.01  * Hvis tilknyttet salgsordre, legg ut samme antall her
8.01  *
8.01 c                   if        b_ordr = *on and
8.01 c                             ldornu <> 0
8.09  * Ordrelinje må finnes
8.09 c                   eval      fodtl1_numm = ldornu
8.09 c                   eval      fodtl1_suff = ldorsu
8.09 c                   eval      fodtl1_line = ldorli
8.09 c     fodtl1_key    chain     fodtl1
8.09 c                   if        %found
8.01  *
8.06  * Samme betingelser for å legge ut tilhørende ordrelinje
8.06  *
8.06 c                   if        mvrkod = 1 or
8.06 c                             mvlevr > 0
8.01  *
8.01 c                   eval      fplllu_lonr = ldornu
8.01 c                   eval      fplllu_lsuf = ldorsu
8.01 c                   eval      fplllu_llin = ldorli
8.01 c     fplllu_key    chain     fplllu
8.01 c                   if        not %found
8.01 c                   eval      fklfir = w_firm
8.01 c                   eval      fklonr = ldornu
8.01 c                   eval      fklsuf = ldorsu
8.01 c                   eval      fkllin = ldorli
8.09 c**                 eval      fklkod = '1'
8.09 c                   if        mvrkod = 0
8.09 c                   eval      fklkod = '5'
8.09 c                   else
8.09 c                   eval      fklkod = *blank
8.09 c                   endif
8.09 c                   if        %trim(fdvare) = *blanks
8.09 c                   eval      fklkod = '1'
8.09 c                   endif
6.20  *
8.10  * Antall må sjekkes for omregning.
8.10  * Behandle innkommet antall
6.20  *
8.10 c                   if        mvenhe <> *blanks and
8.10 c                             mvenhe <> fdenh1
8.10 c                   exsr      omr_antall2
8.10 c                   eval      fklant = w_want
8.10 c                   else
8.10 c                   eval      fklant = lklant
8.10 c                   endif
8.10  *
8.10 c**                 if        ldvare = *blank
8.10 c                   if        fdvare = *blank
8.01 c                   eval      fklant = 0
8.01 c                   endif
8.01 c                   write     fplllur
8.01 c                   endif
8.01  *
8.06 c                   endif
8.01  *
8.09 c                   endif
8.01  *
8.01 c                   endif
      *
8.01 c                   endif
      *
      * Sletter linje fra innmeldingsfil
      *
     c                   if        %found (lwvalu)
9.xx c                   delete    lwvalur
     c                   endif
      *
     c                   read      lodtl1
     c                   enddo
8.03  *
8.03  * Sjekk om det er lagt til tekstlinjer på ordren ifbm plukking.
8.03  *
8.03 c                   if        u_ordr = *on and
8.03 c                             b_ordr = *on and
8.03 c                             loornr <> 0
8.03  *
8.03 c                   eval      fodtlr_numm = loornr
8.03 c                   eval      fodtlr_suff = loorsu
8.03 c                   eval      fodtlr_line = 9000
8.03 c     fodtlr_key    setll     fodtlr
8.03 c     fodtlr_key2   reade     fodtlr
8.03 c                   dow       not %eof
8.03 c                   if        %trim(fdvare) = *blanks and
8.03 c                             fdltyp = 'TX'
8.03  *
8.03 c                   eval      fplllu_lonr = fdnumm
8.03 c                   eval      fplllu_lsuf = fdsuff
8.03 c                   eval      fplllu_llin = fdline
8.03 c     fplllu_key    chain     fplllu
8.03 c                   if        not %found
8.03 c                   eval      fklfir = w_firm
8.03 c                   eval      fklonr = fdnumm
8.03 c                   eval      fklsuf = fdsuff
8.03 c                   eval      fkllin = fdline
8.03 c                   eval      fklkod = '1'
8.03 c                   eval      fklant = 0
8.01 c                   write     fplllur
8.01 c                   endif
8.03  *
8.03 c                   endif
8.03  *
8.03 c     fodtlr_key2   reade     fodtlr
8.03 c                   enddo
8.04  *
8.04  * Sjekk en ekstra runde for tjenestevarer. De
8.04  * må også plukkes, hvis ikke kan de bli liggende igjen
8.04  * på en "tom" ordre.
8.04  *
8.04 c                   eval      b_allo = *on
8.04 c                   eval      fodtlr_numm = loornr
8.04 c                   eval      fodtlr_suff = loorsu
8.04 c                   eval      fodtlr_line = 0
8.04 c     fodtlr_key    setll     fodtlr
8.04 c     fodtlr_key2   reade     fodtlr
8.04 c                   dow       not %eof
      *
8.04 c                   if        %trim(fdvare) <> *blanks
8.04 c                   eval      vvarl1_vare = fdvare
8.04 c     vvarl1_key    chain     vvarl1
8.04 c                   if        %found
8.12 c                   if        vvvtyp = 'L' or
8.12 c                             vvvtyp = 'D' or
8.04 c                             vvvtyp = 'S'
8.04 c                   eval      fplllu_lonr = fdnumm
8.04 c                   eval      fplllu_lsuf = fdsuff
8.04 c                   eval      fplllu_llin = fdline
8.04 c     fplllu_key    chain     fplllu
8.04 c                   if        not %found
8.04 c                   eval      b_allo = *off
8.04 c                   else
8.04 c                   unlock    fplllu
8.04 c                   endif
8.04 c                   endif
8.22 c                   else
8.22 c                   if        fdlvre = 1
8.22 c                   eval      fplllu_lonr = fdnumm
8.22 c                   eval      fplllu_lsuf = fdsuff
8.22 c                   eval      fplllu_llin = fdline
8.22 c     fplllu_key    chain     fplllu
8.22 c                   if        not %found
8.22 c                   eval      b_allo = *off
8.22 c                   else
8.22 c                   unlock    fplllu
8.22 c                   endif
8.22 c                   endif
8.04 c                   endif
8.04 c                   endif
      *
8.04 c     fodtlr_key2   reade     fodtlr
8.04 c                   enddo
8.04  *
8.04  * Hvis alle S/L varer er plukket, ta da også med alle resterende
8.04  * linjer som ikke er merket.
8.04  *
8.04 c                   if        b_allo = *on
8.04 c                   eval      fodtlr_numm = loornr
8.04 c                   eval      fodtlr_suff = loorsu
8.04 c                   eval      fodtlr_line = 0
8.04 c     fodtlr_key    setll     fodtlr
8.04 c     fodtlr_key2   reade     fodtlr
8.04 c                   dow       not %eof
      *
8.04 c                   eval      fplllu_lonr = fdnumm
8.04 c                   eval      fplllu_lsuf = fdsuff
8.04 c                   eval      fplllu_llin = fdline
8.04 c     fplllu_key    chain     fplllu
8.04 c                   if        not %found
8.04 c                   eval      fklfir = w_firm
8.04 c                   eval      fklonr = fdnumm
8.04 c                   eval      fklsuf = fdsuff
8.04 c                   eval      fkllin = fdline
8.04 c                   eval      fklkod = '1'
8.04  * Antall hentes fra ordren
8.04 c                   eval      fklant = fdanta
8.04 c                   if        %trim(fdvare) = *blank
8.01 c                   eval      fklant = 0
8.01 c                   endif
8.01 c                   write     fplllur
8.05 c                   else
8.05 c                   unlock    fplllu
8.04 c                   endif
      *
8.04 c     fodtlr_key2   reade     fodtlr
8.04 c                   enddo
      *
8.04 c                   endif
8.04  *
8.03 c                   endif
      *
      * Skriv plukk heading
      *
     c                   if        b_pluk = *on
     c                   eval      lkpfir = w_firm
     c                   eval      lkponr = p_numm
     c                   eval      lkpsuf = p_suff
     c                   eval      lkpoty = vaonxt
     c                   eval      lkplag = lolage
     c                   eval      lkpwsd = '2DPOINT'
     c                   eval      lkpusr = l_user
     c                   eval      lkpdir = 1
     c                   eval      lkpres = 1
      *
     c                   write     lplolur
     c                   endif
8.01  *
8.01 c                   if        u_ordr = *on
8.01  *
8.01  * Hvis tilknyttet salgsordre, legg ut samme antall her
8.01  *
8.01 c                   if        b_ordr = *on and
8.01 c                             loornr <> 0
8.01  *
8.01 c                   eval      fohel1_numm = loornr
8.01 c                   eval      fohel1_suff = loorsu
8.01 c     fohel1_key    chain     fohel1
8.01 c                   if        %found
8.01  *
8.01  *  Oppdater UTPLUKK-PARAM-REGISTER med ny ordre
8.01  *
8.01 c                   eval      fplol1_ponr = fonumm
8.01 c                   eval      fplol1_psuf = fosuff
8.01 c     fplol1_key    chain     fplolur
8.01 c                   if        not %found
8.01 c                   eval      fkpfir = w_firm
8.01 c                   eval      fkponr = loornr
8.01 c                   eval      fkpsuf = loorsu
8.01 c                   eval      fkpoty = w_otyp
8.01 c                   eval      fkplag = folage
8.01 c                   eval      fkpwsd = '2DPOINT'
8.01 c                   eval      fkpusr = l_user
8.01 c                   eval      fkpalt = 0
8.01 c                   eval      fkpdir = 1
8.01 c                   eval      fkpres = 1
8.01 c                   eval      fkptxt = 0
8.01 c                   eval      fkpkol = 0
8.01 c                   eval      fkpnet = 0
8.01 c                   eval      fkpbru = 0
8.01 c                   eval      fkplmt = *blanks
8.01 c                   eval      fkpfpr = 0
8.01 c                   eval      fkpfte = *blanks
8.01 c                   eval      fkpepr = 0
8.01 c                   eval      fkpete = *blanks
8.01 c                   write     fplolur
8.01 c                   else
8.01 c                   update    fplolur
8.01 c                   endif
8.01  *
8.01 c                   endif
8.01 c                   endif
      *
8.01 c                   endif
8.11  *
8.11  * Hvis flåtestyring, sjekk om vi skal sende transportrequest.
8.11  *
8.11 c                   if        u_flat = *on and
8.11 c                             loornr <> 0
8.11  * Skal ikke sende ny hvis den allerede finnes. Da skjer det via oppdateringen
8.11  * til transportsystem av salgsordren (FO808R).
8.15 c                   eval      ftrpi2_rnum = loornr
8.15 c                   eval      ftrpi2_rsuf = loorsu
8.15 c     ftrpi2_key    chain     ftrpi2
8.15 c                   if        not %found
8.11  *
8.11 c                   eval      w_reqs = *blanks
8.14 c                   eval      w_skaf = 1
8.16 c                   eval      w_logg = 0
8.11 c                   call      'AP058R '
8.11 c                   parm                    w_firm
8.11 c                   parm                    loornr
8.11 c                   parm                    loorsu
8.14 c                   parm                    w_skaf
8.16 c                   parm                    w_logg
8.11 c                   parm                    w_reqs
8.11  *
8.15 c                   endif
8.11  *
8.11 c                   endif
      *
      * Skriv inngående pakkseddel
      *
     c                   eval      w_dirk = 1
     c                   eval      w_outq = *blank
      *
     c                   call      'LO608R'
     c                   parm                    w_stat
     c                   parm                    w_firm
     c                   parm                    lonumm
     c                   parm                    losuff
     c                   parm                    vaonxt
     c                   parm                    w_dirk
     c                   parm                    w_outq
      *
      *  Kaller opp subprogram for å slette utvalg og blanke merking
      *
     c                   call      'LO402R'
     c                   parm                    w_stat
     c                   parm                    w_firm
     c                   parm                    lonumm
     c                   parm                    losuff
8.01  *
8.01  * Så må vi oppdatere ordren, hvis bestilling er knyttet
8.01  *
8.01 c                   if        u_ordr = *on and
8.01 c                             b_ordr = *on
8.01  *
8.01 c                   eval      p_stat = *blanks
8.01 c                   eval      p_firm = w_firm
8.01 c                   eval      p_numm = loornr
8.01 c                   eval      p_suff = loorsu
8.02 c                   call      'FO805R   '
8.01 c                   parm                    p_stat
8.01 c                   parm                    p_firm
8.01 c                   parm                    p_numm
8.01 c                   parm                    p_suff
8.01  *
8.01 c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
     c                   eval      p_stat = '1'
     c                   eval      *inlr = *on
     c                   return
      *
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  * Omregning av antall ifht enhet
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     omr_antall    begsr
6.20  *
6.20 c                   eval      w_vare = mvvare
6.20 c                   eval      w_anta = mvlevr
6.20 c                   eval      w_enhe = mvenhe
6.20 c                   eval      w_antu = 0
6.20 c                   eval      w_enhu = ldenh1
8.13 c                   eval      w_levr = ldldor
6.20  *
8.13 c**                 call      'VO700R'
8.13 c                   call      'VA771R'
8.13 c                   parm                    w_firm
8.13 c                   parm                    w_levr
6.20 c                   parm                    w_vare
6.20 c                   parm                    w_enhe
6.20 c                   parm                    w_anta
6.20 c                   parm                    w_enhu
6.20 c                   parm                    w_antu
6.20  *
6.20 c                   if        w_antu <> 0
6.20 c                   eval      mvlevr = w_antu
6.20 c                   endif
6.20  *
6.20 c                   endsr
6.20  *
8.10  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.10  * Omregning av antall ifht enhet - salgsordre
8.10  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.10  *
8.10 c     omr_antall2   begsr
8.10  *
8.10 c                   eval      w_vare = mvvare
8.10 c                   eval      w_anta = w_want
8.10 c                   eval      w_enhe = mvenhe
8.10 c                   eval      w_antu = 0
8.10 c                   eval      w_enhu = fdenh1
8.13 c                   eval      w_levr = fdldor
8.10  *
8.13 c**                 call      'VO700R'
8.13 c                   call      'VA771R'
8.13 c                   parm                    w_firm
8.13 c                   parm                    w_levr
8.10 c                   parm                    w_vare
8.10 c                   parm                    w_enhe
8.10 c                   parm                    w_anta
8.10 c                   parm                    w_enhu
8.10 c                   parm                    w_antu
8.10  *
8.10 c                   if        w_antu <> 0
8.10 c                   eval      w_want = w_antu
8.10 c                   endif
8.10  *
8.10 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c*    *pssr         begsr
      *
     c*                  eval      p_stat = ' '
610  c*                  move      *on           *inlr
610  c*                  return
      *
     c*                  endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_user
     c                   parm                    p_stat
      *
      * Key for les av bestillingshode
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      * Key for les av bestillingslinje
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
     c                   kfld                    lodtl1_line
      *
      *  Key for oppdatering av arbeidsregister
      *
     c     lwvalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwvalu_numm
     c                   kfld                    lwvalu_suff
     c                   kfld                    lwvalu_line
      *
      * Key for oppslag på ordre-type
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      *  Key for UTPLUKK-PARAM
      *
     c     lplolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lplolu_ponr
     c                   kfld                    lplolu_psuf
      *
      *  Key for UTPLUKK-LINJE
      *
     c     lplllu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lplllu_lonr
     c                   kfld                    lplllu_lsuf
     c                   kfld                    lplllu_llin
8.01  *
8.01  *  Key for file : UTPLUKK-PARAM-REGISTER
8.01  *
8.01 c     fplol1_key    klist
8.01 c                   kfld                    w_firm
8.01 c                   kfld                    fplol1_ponr
8.01 c                   kfld                    fplol1_psuf
8.01  *
8.01  * Key for les av plukkede linjer
8.01  *
8.01 c     fplllu_key    klist
8.01 c                   kfld                    w_firm
8.01 c                   kfld                    fplllu_lonr
8.01 c                   kfld                    fplllu_lsuf
8.01 c                   kfld                    fplllu_llin
8.01  *
8.01  * Key for les av ordrehodet
8.01  *
8.01 c     fohel1_key    klist
8.01 c                   kfld                    w_firm
8.01 c                   kfld                    fohel1_numm
8.01 c                   kfld                    fohel1_suff
8.03  *
8.03  * Key for les av ordrelinjer
8.03  *
8.03 c     fodtlr_key    klist
8.03 c                   kfld                    w_firm
8.03 c                   kfld                    fodtlr_numm
8.03 c                   kfld                    fodtlr_suff
8.03 c                   kfld                    fodtlr_line
8.09  *
8.09  * Key for les av ordrelinjer
8.09  *
8.09 c     fodtl1_key    klist
8.09 c                   kfld                    w_firm
8.09 c                   kfld                    fodtl1_numm
8.09 c                   kfld                    fodtl1_suff
8.09 c                   kfld                    fodtl1_line
8.03  *
8.03  * Key for les av ordrelinjer
8.03  *
8.03 c     fodtlr_key2   klist
8.03 c                   kfld                    w_firm
8.03 c                   kfld                    fodtlr_numm
8.03 c                   kfld                    fodtlr_suff
8.04  *
8.04  * Key for oppslag på vareregister
8.04  *
8.04 c     vvarl1_key    klist
8.04 c                   kfld                    w_firm
8.04 c                   kfld                    vvarl1_vare
8.15  *
8.15  * Key for posisjonering på transportoppdrag
8.15  *
8.15 c     ftrpi2_key    klist
8.15 c                   kfld                    w_firm
8.15 c                   kfld                    ftrpi2_rnum
8.15 c                   kfld                    ftrpi2_rsuf
8.21  *
8.21  * Key for oppslag på kunder
8.21  *
8.21 c     rkunl1_key    klist
8.21 c                   kfld                    w_firm
8.21 c                   kfld                    rkunl1_kund
      *
      * Henter firma og bruker fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      l_user = p_user
      *
      * Legger inn bestillingsnummer i nøkler
      *
     c                   eval      lohel1_numm = p_numm
     c                   eval      lohel1_suff = p_suff
     c                   eval      lodtl1_numm = p_numm
     c                   eval      lodtl1_suff = p_suff
     c                   eval      lwvalu_numm = p_numm
     c                   eval      lwvalu_suff = p_suff
8.11  *
8.11  * Endring 8.11 - Koblet til ekstern flåtestyring
8.11  *
8.11   CallP CO402R(l_filg:w_firm:0:'EKSTERN_FLATESTYRING':
8.11                    co402_verdi1:co402_verdi2);
8.11   if co402_verdi1 = '1';
8.11     u_flat = *on;
8.11   endif;
      *
     c                   endsr
