     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JO650R
      * Beskrivelse . . : Oppdatering av matchede varer i EVR,
      *                   parameterprogram
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       200197 HKO  Nytt program
      *       170397 HKO  Lagt inn overstyring av leverandør EVR
      *       250897 HKO  Lagt inn overstyring for å kunne beholde alle
      *                   eksisterende søketeskter i EVR
      *       050798 HKO  Lagt inn valgt for å kopiere priskode og lagerkode
      *                   fra EVR til LVR.
      *                   Erstattet funksjonstast for jobbkø med standard-
      *                   bilde ved utskrift.
7.xx  * 7.00  020816 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
      *
     fjo650d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Datastruktur for parameterliste -ut
      *
     d                 ds
     d d_list                        32
     d  d_firm                        3  0 overlay(d_list)
     d  d_ldo2                        6  0 overlay(d_list:4)
     d  d_ogrp                        2  0 overlay(d_list:10)
     d  d_hgrp                        2  0 overlay(d_list:12)
     d  d_ugrp                        3  0 overlay(d_list:14)
     d  d_ldo3                        6  0 overlay(d_list:17)
     d  d_behp                        1  0 overlay(d_list:23)
     d  d_beht                        1  0 overlay(d_list:24)
     d  d_behg                        1  0 overlay(d_list:25)
     d  d_oppd                        1  0 overlay(d_list:26)
      *
      * Definering av variable i nøkler
      *
     d jlevl1_ldor     s                   like(jlldor)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_navn          s                   like(jlnavn)
     d w_utxt          s                   like(vgutxt)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send bilde
      *
     c     b1tag         tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b1tag
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1tag
     c                   endif
      *
      * Flytter skjermfelter til datastruktur -ut
      *
     c                   eval      d_firm = w_firm
     c                   eval      d_ldo2 = b1ldo2
     c                   eval      d_ogrp = b1ogrp
     c                   eval      d_hgrp = b1hgrp
     c                   eval      d_ugrp = b1ugrp
     c                   eval      d_ldo3 = b1ldo3
     c                   eval      d_behp = b1behp
     c                   eval      d_beht = b1beht
     c                   eval      d_behg = b1behg
     c                   eval      d_oppd = b1oppd
      *
      * Kaller overføringsprogram
      *
     c                   call      'JO650C'
     c                   parm                    d_list
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørringer (FD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Leverandør
      *
     c                   if        w_afld = 'B1LDO2'
     c                   eval      w_navn = *blank
     c                   call      'JL500R'
     c                   parm                    b1ldo2
     c                   parm                    w_navn
     c                   eval      b1nav2 = w_navn
     c                   goto      end_spØrr
     c                   endif
      *
      * Overgruppe
      *
     c                   if        w_afld = 'B1OGRP'
     c                   eval      b1hgrp = 0
     c                   eval      b1ugrp = 0
     c                   eval      b1utxt = *blank
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    w_utxt
     c                   eval      b1utxt = w_utxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_afld = 'B1HGRP'
     c                   eval      b1ugrp = 0
     c                   eval      b1utxt = *blank
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    w_utxt
     c                   eval      b1utxt = w_utxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_afld = 'B1UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    b1ugrp
     c                   parm                    w_utxt
     c                   eval      b1utxt = w_utxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Prisgivende leverandør
      *
     c                   if        w_afld = 'B1LDO3'
     c                   eval      w_navn = *blank
     c                   call      'JL500R'
     c                   parm                    b1ldo3
     c                   parm                    w_navn
     c                   eval      b1nav3 = w_navn
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
     c                   eval      b1nav2 = *blank
     c                   eval      b1utxt = *blank
     c                   eval      b1nav3 = *blank
      *
      * Leverandør
      *
     c                   if        b1ldo2 = 0
     c                   eval      *in31 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   eval      jlevl1_ldor = b1ldo2
     c     jlevl1_key    chain     jlevl1                             32
     c                   if        *in32 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1nav2 = jlnavn
      *
      * Overgruppe
      *
     c                   if        b1ogrp <> 0
     c                   eval      vogrl1_oogr = b1ogrp
     c     vogrl1_key    chain     vogrl1                             33
     c                   if        *in33 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1utxt = vgotxt
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        b1hgrp <> 0
     c                   eval      vhgrl1_hogr = b1ogrp
     c                   eval      vhgrl1_hhgr = b1hgrp
     c     vhgrl1_key    chain     vhgrl1                             34
     c                   if        *in34 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1utxt = vghtxt
     c                   endif
      *
      * Undergruppe
      *
     c                   if        b1ugrp <> 0
     c                   eval      vugrl1_uogr = b1ogrp
     c                   eval      vugrl1_uhgr = b1hgrp
     c                   eval      vugrl1_uugr = b1ugrp
     c     vugrl1_key    chain     vugrl1                             35
     c                   if        *in35 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1utxt = vgutxt
     c                   endif
      *
      * Prisgivende leverandør
      *
     c                   if        b1ldo3 <> 0
     c                   eval      jlevl1_ldor = b1ldo3
     c     jlevl1_key    chain     jlevl1                             38
     c                   if        *in38 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1nav3 = jlnavn
     c                   endif
      *
      * Sjekk på at eget leverandørnummer finnes på leverandør eller
      * prisgivende leverandør
      *
     c                   if        jlenum = 0
     c                   if        b1ldo3 <> 0
     c                   eval      *in40 = *on
     c                   else
     c                   eval      *in39 = *on
     c                   endif
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Kopierer priskode/lagerkode fra EVR til LVR
      *
     c                   if        b1behp <> 0 and
     c                             b1behp <> 1
     c                   eval      *in41 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Beholde varetekst
      *
     c                   if        b1beht <> 0 and
     c                             b1beht <> 1
     c                   eval      *in42 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Beholde varegruppe
      *
     c                   if        b1behg <> 0 and
     c                             b1behg <> 1
     c                   eval      *in43 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjermbildet
      *
     c                   eval      b1oppd = 1
     c                   eval      b1behp = 1
      *
     c                   endsr
