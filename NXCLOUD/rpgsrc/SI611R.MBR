      /title  Utskrift av innkjøpsstatistikk
     H datedit(*dmy.) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  System. . . . . : ASSTAT                                       *
      *  Program . . . . : SI611R                                       *
      *  Beskrivelse . . : Utskrift av innkjøpsstatistikk               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  Spesialversjon. :                                              *
      *  Tilpasset for . :                                              *
      *                                                                 *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.*
      *  --------- ------  ---------------------------------------------*
      *  V5.30     260304  Fjernet kode for leveringsadresse       KOB  *
      *  V5.30     200904  Fjernet rabattgruppe                    KOB  *
      *  V5.40     230305  Henter tekst fra statisikkfil hvis vare KOB  *
      *                    ikke finnes i varereg/slettet register  KOB  *
      *  V5.40     240305  Lagt ut statistikkenhet på varelinje    KOB  *
      *  V5.40     240305  Lagt ut "dekn." på rapporten            KOB  *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     Fslwwpf    ip   e           k disk
     Fslpml1    if   f  512    15aidisk    keyloc(1)
      *
     Frlevl1    if   e           k disk
     Fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     Fvvasl1    if   e           k disk    rename(vvaspfr:vvasl1r)
     Fvahgl1    if   e           k disk
     Fvaugl1    if   e           k disk
     Fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     Fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     Fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     Fvotyl1    if   e           k disk
     Fra06pf    if   e           k disk
     Fra07pf    if   e           k disk
     Fra08pf    if   e           k disk
     Fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
     Fra10pf    if   e           k disk
      *
     Fra12pf    if   e           k disk
     Fra14pf    if   e           k disk
      *
      *
     Fsi611p    o    e             printer oflind(*in30)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tabeller                                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     D rt              s             25    dim(60) ctdata perrcd(1)              RAPPORT TEKSTER
      *
     D w_uhgrp         s              5
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Data Strukturer                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     D                 ds
     D slpml1_key                    15
     D  w1firm                        3  0 overlay(slpml1_key:1)
     D  w_type                       10    overlay(slpml1_key:4)
     D  w_kode                        2    overlay(slpml1_key:14)
      *
     D                 ds
     D beg                           15
     D  begnum                       15  0 overlay(beg)
     D   k6                           6  0 overlay(begnum)
      *
     D                 ds
     D w_xgrp                         7  0
     D  w_xogr                        2  0 overlay(w_xgrp:1)
     D  w_xhgr                        2  0 overlay(w_xgrp:3)
     D  w_xugr                        3  0 overlay(w_xgrp:5)
      *
     D                uds
     D l_wsid                901    910
     D l_user                911    920
     D l_firm                944    946  0
     D l_navn                951    980
      *
     D ant4            s                   like(ant7)
     D ant4u           s              8  0
     D ant5            s                   like(ant7)
     D ant5u           s                   like(ant4u)
     D ant6            s                   like(ant7)
     D ant6u           s                   like(ant4u)
     D ant7            s             10  2
     D ant7u           s                   like(ant4u)
     D begtek          s             30
     D begenh          s              3
     D brtt2           s             30
     D brte2           s              3
     D brtt3           s             30
     D brte3           s              3
     D bru2            s             11  2
     D bru4            s                   like(bru7)
     D bru5            s                   like(bru7)
     D bru6            s                   like(bru7)
     D bru7            s              9  0
     D dek4            s              9  0
     D dek5            s                   like(dek4)
     D dek6            s                   like(dek4)
     D dek7            s                   like(dek4)
     D kst2            s             11  2
     D kst4            s                   like(kst7)
     D kst5            s                   like(kst7)
     D kst6            s                   like(kst7)
     D kst7            s              9  0
     D net4            s              9  0
     D net5            s                   like(net4)
     D net6            s                   like(net4)
     D net7            s                   like(net4)
     D pde4            s              7  2
     D pde5            s                   like(pde4)
     D pde6            s                   like(pde4)
     D pde7            s                   like(pde4)
     D pra4            s              7  2
     D pra5            s                   like(pra4)
     D pra6            s                   like(pra4)
     D pra7            s                   like(pra4)
     D rap             s              2  0
      *
     D ra12            s             11  2
     D ra14            s                   like(ra17)
     D ra15            s                   like(ra17)
     D ra16            s                   like(ra17)
     D ra17            s              8  0
     D ra22            s             11  2
     D ra24            s                   like(ra27)
     D ra25            s                   like(ra27)
     D ra26            s                   like(ra27)
     D ra27            s              8  0
     D ra32            s             11  2
     D ra34            s                   like(ra37)
     D ra35            s                   like(ra37)
     D ra36            s                   like(ra37)
     D ra37            s              8  0
     D tra4            s              9  0
     D tra5            s                   like(tra4)
     D tra6            s                   like(tra4)
     D tra7            s                   like(tra4)
     D wde4            s             11  4
     D wde5            s                   like(wde4)
     D wde6            s                   like(wde4)
     D wde7            s                   like(wde4)
     D wra4            s             11  4
     D wra5            s                   like(wra4)
     D wra6            s                   like(wra4)
     D wra7            s                   like(wra4)
      *
     D rptek           s             30
     D rpenh           s              3
     D utime           s              6  0
     D w_fkod          s                   like(rafkod)
     D w_gkod          s                   like(ragkod)
     D w_hkod          s                   like(rahkod)
     D w_ikod          s                   like(raikod)
     D w_jkod          s                   like(rajkod)
     D w_nkod          s                   like(rankod)
      *
     D w_levr          s                   like(rllevr)
     D w_lkod          s                   like(ralkod)
     D w_otyp          s                   like(vaotyp)
     D w_oogr          s                   like(vgoogr)
     D w_hogr          s                   like(vghogr)
     D w_hhgr          s                   like(vghhgr)
     D w_uogr          s                   like(vguogr)
     D w_uhgr          s                   like(vguhgr)
     D w_uugr          s                   like(vguugr)
     D w_vare          s                   like(vvvare)
     D w_firm          s                   like(srfirm)
      *
      *
     D w_timestamp     s             12  0
     D w_date          s              6  0
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Input                                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     Islwwpfr       01
     I                                          sifirm        l7
     I                                          swsrt1        l6
     I                                          swsrt2        l5
     I                                          swsrt3        l4
      *
     Islpml1    ns  01   14 c0   15 c0
     I                                  1    3 0srfirm
     I                                  4   13  srtype
     I                                 14   15 0srkode
     I                                 31   60  srtext
     I                                 61   62 0rp1
     I                                 63   64 0rp2
     I                                 65   66 0rp3
     I                                 67   67  spes1
     I                                 68   68  spes2
     I                                 69   69  spes3
     I                                 70   70  spes4
     I                                 71   71  spes5
     I                                 73   74 0spmnd
     I                                 75   78 0spaar
     I                                 79   79  srsmli
     I                                 80   80  srsps7
     I                                 81   81  srsss1
     I                                 82   82  srsss2
     I                                 83   83  srsss3
     I                                 84   84  srslet
      *
     I          ns  01   14 c4   15 c1
     I                                  1    3 0srfirm
     I                                  4   13  srtype
     I                                 14   15 0srkode
     I                                 31   60  srtext
     I                                 61   64 0srfaar
     I                                 65   66 0srfmnd
     I                                 67   70 0srtaar
     I                                 71   72 0srtmnd
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Cal_ulationn                                                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     dummy         tag
      *
     C   l6              do
     C                   eval      ant6 = 0
     C                   eval      bru6 = 0
     C                   eval      ra16 = 0
     C                   eval      ra26 = 0
     C                   eval      ra36 = 0
     C                   eval      kst6 = 0
      *
     C                   move      swsrt1        beg
     C                   eval      rap = rp1
     C                   exsr      raprut
     C                   movel     rptek         begtek
     C                   movel     rpenh         begenh
      *
     C                   enddo
      *
B001 C   l5              do
 001 C                   eval      ant5 = 0
 001 C                   eval      bru5 = 0
 001 C                   eval      ra15 = 0
 001 C                   eval      ra25 = 0
 001 C                   eval      ra35 = 0
 001 C                   eval      kst5 = 0
 001 C                   eval      brtt2 = *blank
 001  *
B002 C                   if        rp2 <> *zero
 002 C                   move      swsrt2        beg
 002 C                   eval      rap = rp2
 002 C                   exsr      raprut
 002 C                   movel     rptek         brtt2
 002 C                   movel     rpenh         brte2
E002 C                   endif
 001  *
E001 C                   enddo
      *
B001 C   l4              do
 001 C                   eval      ant4 = 0
 001 C                   eval      bru4 = 0
 001 C                   eval      ra14 = 0
 001 C                   eval      ra24 = 0
 001 C                   eval      ra34 = 0
 001 C                   eval      kst4 = 0
 001  *
 001 C                   eval      bru2 = 0
 001 C                   eval      ra12 = 0
 001 C                   eval      ra22 = 0
 001 C                   eval      ra32 = 0
 001 C                   eval      kst2 = 0
 001  *
 001 C                   eval      brtt3 = *blank
 001  *
B002 C                   if        rp3 <> *zero
 002 C                   move      swsrt3        beg
 002 C                   eval      rap = rp3
 002 C                   exsr      raprut
 002 C                   movel     rptek         brtt3
 002 C                   movel     rpenh         brte3
E002 C                   endif
 001  *
E001 C                   enddo
      *
      * Summering av enkeltposter
      *
     C                   eval      ant4 = ant4 + sianta
     C                   eval      bru2 = bru2 + sisumb
     C                   eval      ra12 = ra12 + sirab1
     C                   eval      ra22 = ra22 + sirab2
     C                   eval      ra32 = ra32 + sirabo
     C                   eval      kst2 = kst2 + sisumk
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     xslutt        tag
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Brudd                                                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      * Brudd på L4 (SRT3)
      *
     CL4                 exsr      brdl4
     CL4                 if        swsrt3 <> *blank
     C                             and spes3 = ' '
     CL4                 write     tol41                                30
     CL4 73              move      *on           *in30
     CL4                 exsr      overflow
     CL4                 endif
      *
      * BRUDD PÅ L5  (SRT2)
      *
     CL5                 exsr      brdl5
     CL5                 if        swsrt2 <> *blank
     C                             and spes2 = ' '
     CL5                 write     tol51                                30
     CL5 72              move      *on           *in30
     CL5                 exsr      overflow
     CL5                 endif
      *
      * BRUDD PÅ L6  (SRT1)
      *
     CL6                 exsr      brdl6
      *
     CL6                 if        swsrt1 <> *blank
     C                             and swsrt2 <> *blank and spes1 = ' '
     CL6                 write     tol62A                               30
     CL6 71              move      *on           *in30
     CL6                 exsr      overflow
     CL6                 else
     CL6                 write     tol62b                               30
     CL6 71              move      *on           *in30
     CL6                 exsr      overflow
     CL6                 endif
      *
      * BRUDD PÅ L7  (FIRMA)
      *
     CL7                 if        *in64 = *off
     CL7                 exsr      brdl7
     CL7                 write     tol71                                30
     CL7                 endif
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for behandling av rapport                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     raprut        begsr
      *
     C                   eval      rptek = *blank
     C                   eval      rpenh = *blank
      *
      * -----------------
      * Alt. hovedgruppe
      * -----------------
      *
B001 C                   if        rap = 11
 001 C                   movel     beg           w_hhgr
 001 C     hgrkey        chain     vahgl1                             50
B002 C                   if        *in50 = *off
 002 C                   movel     vfhtxt        rptek
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif
      *
      * -----------------
      * Alt. undergruppe
      * -----------------
      *
B001 C                   if        rap = 12
     C                   movel     beg           w_uhgrp
 001 C                   movel     w_uhgrp       w_uhgr
 001 C                   move      w_uhgrp       w_uugr
 001 C     ugrkey        chain     vaugl1                             50
B002 C                   if        *in50 = *off
 002 C                   movel     vfutxt        rptek
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif
      *
      * ------------------
      * Overgruppe
      * ------------------
      *
B001 C                   if        rap = 14
 001 C                   movel     beg           w_xgrp
 001 C                   eval      w_oogr = w_xogr
 001 C     vogrl1_key    chain     vogrl1                             50
B002 C                   if        *in50 = *off
 002 C                   movel     vgotxt        rptek
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif
      *
      * ------------------
      * Hovedgruppe
      * ------------------
      *
B001 C                   if        rap = 15
 001 C                   movel     beg           w_xgrp
 001 C                   eval      w_hogr = w_xogr
 001 C                   eval      w_hhgr = w_xhgr
 001 C     vhgrl1_key    chain     vhgrl1                             50
B002 C                   if        *in50 = *off
 002 C                   movel     vghtxt        rptek
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif
      *
      * ------------------
      * Undergruppe
      * ------------------
      *
B001 C                   if        rap = 16
 001 C                   movel     beg           w_xgrp
 001 C                   eval      w_uogr = w_xogr
 001 C                   eval      w_uhgr = w_xhgr
 001 C                   eval      w_uugr = w_xugr
 001 C     vugrl1_key    chain     vugrl1                             50
B002 C                   if        *in50 = *off
 002 C                   movel     vgutxt        rptek
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif
      *
      * ------------------
      * Leverandørkategori
      * ------------------
      *
B001 C                   if        rap = 21
 001 C                   movel     beg           w_nkod
 001 C     ra14pf_key    chain     ra14pf                             50
B002 C                   if        *in50 = *off
 002 C                   movel     rantxt        rptek
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif
      *
      * ---------------
      * Rabattkategori
      * ---------------
      *
B001 C                   if        rap = 22
 001 C                   movel     beg           w_nkod
 001 C     ra06pf_key    chain     ra06pf                             50
B002 C                   if        *in50 = *off
 002 C                   movel     raftxt        rptek
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif
      *
      * ---------------
      * Distrikt
      * ---------------
      *
B001 C                   if        rap = 23
 001 C                   movel     beg           w_hkod
 001 C     ra08pf_key    chain     ra08pf                             50
B002 C                   if        *in50 = *off
 002 C                   movel     rahtxt        rptek
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif
      *
      * ---------------
      * Selger
      * ---------------
      *
B001 C                   if        rap = 24
 001 C                   movel     beg           w_ikod
 001 C     ra09l1_key    chain     ra09l1                             50
B002 C                   if        *in50 = *off
 002 C                   movel     raitxt        rptek
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif
      *
      * ---------------
      * Land
      * ---------------
      *
B001 C                   if        rap = 25
 001 C                   move      beg           w_lkod
 001 C     ra12pf_key    chain     ra12pf                             50
B002 C                   if        *in50 = *off
 002 C                   movel     raltxt        rptek
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif
      *
      * ---------------
      * Varenummer
      * ---------------
      *
     C                   if        rap = 31
     C                   movel     beg           w_vare
     C     vvarl1_key    chain     vvarl1                             50
     C                   if        *in50 = *off
     C                   movel     vvtek1        rptek
     C                   move      vvenhs        rpenh
     C                   endif
     C                   if        *in50 = *on
     C     vvarl1_key    chain     vvasl1                             50
     C                   if        *in50 = *off
     C                   movel     vvtek1        rptek
     C                   move      vvenhs        rpenh
     C                   else
     C                   movel     sitek1        rptek
     C                   move      sivenh        rpenh
     C                   endif
     C                   endif
     C                   goto      endrut
     C                   endif
      *
      * ----------------
      * Leverandørnummer
      * -----------------
      *
B001 C                   if        rap = 32
 001 C                   movel     k6            w_levr
 001 C     rlevl1_key    chain     rlevl1                             50
B002 C                   if        *in50 = *off
 002 C                   movel     rlnavn        rptek
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif
      *
      * ---------------
      * Periode
      * ---------------
      *
B001 C                   if        rap = 41
 001 C                   movel     rt(41)        rptek
E001 C                   endif
      *
      * ---------------
      * Fakturanr/
      * Kreditnotanr.
      * ---------------
      *
B001 C                   if        rap = 42
 001 C                   movel     rt(42)        rptek
E001 C                   endif
      *
      * ---------------
      * Fakturadato
      * ---------------
      *
B001 C                   if        rap = 43
 001 C                   movel     rt(43)        rptek
E001 C                   endif
      *
      * ---------------
      * Ordretype
      * ---------------
      *
B001 C                   if        rap = 51
 001 C                   movel     beg           w_otyp
 001 C     votyl1_key    chain     votyl1                             50
B002 C                   if        *in50 = *off
 002 C                   movel     vaotxt        rptek
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif
      *
      * ---------------
      * Avdeling
      * ---------------
      *
B001 C                   if        rap = 52
 001 C                   movel     beg           w_gkod
 001 C     ra07pf_key    chain     ra07pf                             50
B002 C                   if        *in50 = *off
 002 C                   movel     ragtxt        rptek
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif
      *
      * ---------------
      * Lager
      * ---------------
      *
B001 C                   if        rap = 53
 001 C                   movel     beg           w_jkod
 001 C     ra10pf_key    chain     ra10pf                             50
B002 C                   if        *in50 = *off
 002 C                   movel     rajtxt        rptek
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif
      *
      * ------------------
      * 1. siffer
      * leverandørkategori
      * ------------------
      *
B001 C                   if        rap = 26
 001 C                   movel     rt(26)        rptek
 001 C                   movel     beg           w_nkod
 001 C     ra14pf_key    chain     ra14pf                             50
B002 C                   if        *in50 = *off
 002 C                   movel     rantxt        rptek
E002 C                   endif
 001 C                   goto      endrut
E001 C                   endif
      *
     C     endrut        endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for brudd på L4 (SRT3)                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     brdl4         begsr
      *
     C                   eval(h)   bru4 = bru2
     C                   eval(h)   ra14 = ra12
     C                   eval(h)   ra24 = ra22
     C                   eval(h)   ra34 = ra32
     C                   eval(h)   kst4 = kst2
      *
     C                   eval      pde4 = 0
     C                   eval      net4 = 0
     C                   eval      net4 = net4 + bru4
     C                   eval      net4 = net4 - ra14
     C                   eval      net4 = net4 - ra24
     C                   eval      net4 = net4 - ra34
     C                   eval      dek4 = net4 - kst4
     C                   eval      tra4 = ra14 + ra24
     C                   eval      tra4 = tra4 + ra34
      *
B001 C                   if        net4 <> *zero
 001 C                   eval      wde4 = dek4 / net4
 001 C                   eval(h)   pde4 = wde4 * 100
E001 C                   endif
      *
B001 C                   if        bru4 <> *zero
 001 C                   eval      wra4 = tra4 / bru4
 001 C                   eval(h)   pra4 = wra4 * 100
E001 C                   endif
      *
     C                   eval      ant5 = ant5 + ant4
     C                   eval      bru5 = bru5 + bru4
     C                   eval      ra15 = ra15 + ra14
     C                   eval      ra25 = ra25 + ra24
     C                   eval      ra35 = ra35 + ra34
     C                   eval      kst5 = kst5 + kst4
 001 C                   eval      ant4u = ant4
      *
     C                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for brudd på L5 (SRT2)                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     brdl5         begsr
      *
     C                   eval      pde5 = 0
     C                   eval      net5 = 0
     C                   eval      net5 = net5 + bru5
     C                   eval      net5 = net5 - ra15
     C                   eval      net5 = net5 - ra25
     C                   eval      net5 = net5 - ra35
     C                   eval      dek5 = net5 - kst5
     C                   eval      tra5 = ra15 + ra25
     C                   eval      tra5 = tra5 + ra35
      *
B001 C                   if        net5 <> *zero
 001 C                   eval      wde5 = dek5 / net5
 001 C                   eval(h)   pde5 = wde5 * 100
E001 C                   endif
      *
B001 C                   if        bru5 <> *zero
 001 C                   eval      wra5 = tra5 / bru5
 001 C                   eval(h)   pra5 = wra5 * 100
E001 C                   endif
      *
     C                   eval      ant6 = ant6 + ant5
     C                   eval      bru6 = bru6 + bru5
     C                   eval      ra16 = ra16 + ra15
     C                   eval      ra26 = ra26 + ra25
     C                   eval      ra36 = ra36 + ra35
     C                   eval      kst6 = kst6 + kst5
 001 C                   eval      ant5u = ant5
      *
     C                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for brudd på L6 (SRT1)                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     brdl6         begsr
      *
     C                   eval      pde6 = 0
     C                   eval      net6 = 0
     C                   eval      net6 = net6 + bru6
     C                   eval      net6 = net6 - ra16
     C                   eval      net6 = net6 - ra26
     C                   eval      net6 = net6 - ra36
     C                   eval      dek6 = net6 - kst6
     C                   eval      tra6 = ra16 + ra26
     C                   eval      tra6 = tra6 + ra36
      *
B001 C                   if        net6 <> *zero
 001 C                   eval      wde6 = dek6 / net6
 001 C                   eval(h)   pde6 = wde6 * 100
E001 C                   endif
      *
B001 C                   if        bru6 <> *zero
 001 C                   eval      *in36 = bru6 <> 0
 001 C                   eval      wra6 = tra6 / bru6
 001 C                   eval(h)   pra6 = wra6 * 100
E001 C                   endif
      *
     C                   eval      ant7 = ant7 + ant6
     C                   eval      bru7 = bru7 + bru6
     C                   eval      ra17 = ra17 + ra16
     C                   eval      ra27 = ra27 + ra26
     C                   eval      ra37 = ra37 + ra36
     C                   eval      kst7 = kst7 + kst6
 001 C                   eval      ant6u = ant6
      *
     C                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for brudd på L7 (Firma)                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     brdl7         begsr
      *
     C                   eval      pde7 = 0
     C                   eval      net7 = 0
     C                   eval      net7 = net7 + bru7
     C                   eval      net7 = net7 - ra17
     C                   eval      net7 = net7 - ra27
     C                   eval      net7 = net7 - ra37
     C                   eval      dek7 = net7 - kst7
     C                   eval      tra7 = ra17 + ra27
     C                   eval      tra7 = tra7 + ra37
      *
B001 C                   if        net7 <> *zero
 001 C                   eval      wde7 = dek7 / net7
 001 C                   eval(h)   pde7 = wde7 * 100
E001 C                   endif
      *
B001 C                   if        bru7 <> *zero
 001 C                   eval      wra7 = tra7 / bru7
 001 C                   eval(h)   pra7 = wra7 * 100
E001 C                   endif
      *
     C                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for behandling av overflow                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     overflow      begsr
      *
     C                   if        *in30 = *on
     C                   if        rp1 > 0
     C                   eval      h1srt1 = rt(rp1)
     C                   endif
     C                   if        rp2 > 0
     C                   eval      h1srt2 = rt(rp2)
     C                   endif
     C                   if        rp3 > 0
     C                   eval      h1srt3 = rt(rp3)
     C                   endif
 001 C                   write     HD1A
     C                   endif
     C                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *inzsr        begsr
      *
     C                   eval      begtek = *blank
     C                   eval      begenh = *blank
     C                   eval      rptek = *blank
     C                   eval      rpenh = *blank
     C                   eval      brtt2 = *blank
     C                   eval      brte2 = *blank
     C                   eval      brtt3 = *blank
     C                   eval      brte3 = *blank
     C                   eval      rap = *zero
      *
     C                   eval      net4 = 0
     C                   eval      dek4 = 0
     C                   eval      tra4 = 0
     C                   eval      wde4 = 0
     C                   eval      pde4 = 0
     C                   eval      wra4 = 0
     C                   eval      pra4 = 0
     C                   eval      ant4u = 0
      *
     C                   eval      bru2 = 0
     C                   eval      ra12 = 0
     C                   eval      ra22 = 0
     C                   eval      ra32 = 0
     C                   eval      kst2 = 0
      *
     C                   eval      ant7 = 0
     C                   eval      bru7 = 0
     C                   eval      ra17 = 0
     C                   eval      ra27 = 0
     C                   eval      ra37 = 0
     C                   eval      kst7 = 0
      *
     C                   eval      *in20 = *off
     C                   eval      *in96 = *off
      *
      * Behandle kriterier for valg
      *
     C     *entry        plist
     C                   parm                    dstype           10
      *
     C                   time                    w_timestamp
     C                   move      w_timestamp   w_date
      *
     C                   eval      slpml1_key = *blank
      *
     C                   move      l_firm        w1firm
     C                   move      l_firm        w_firm
     C                   movel     dstype        w_type
     C                   move      '00'          w_kode
     C     slpml1_key    chain     slpml1                             50
      *
     C                   eval      *in61 = spes1 <> ' '                          BEGREP 1
     C                   eval      *in62 = spes2 <> ' '                          BEGREP 2
     C                   eval      *in63 = spes3 <> ' '                          BEGREP 3
     C                   eval      *in64 = spes4 <> ' '                          FIRMA
     C                   eval      *in65 = spes5 <> ' '                          TOTALT
      *
     C                   eval      *in57 = rp2 <> 0
     C                   eval      *in58 = rp3 <> 0
      *
     C                   eval      *in70 = srsps7 <> ' '                         BEGREP 1
     C                   eval      *in71 = srsss1 <> ' '                         BEGREP 1
     C                   eval      *in72 = srsss2 <> ' '                         BEGREP 1
     C                   eval      *in73 = srsss3 <> ' '                         BEGREP 1
      *
     C                   time                    utime
      *
     C                   if        rp1 > 0
     C                   eval      h1srt1 = rt(rp1)
     C                   endif
      *
     C                   if        rp2 > 0
     C                   eval      h1srt2 = rt(rp2)
     C                   endif
      *
     C                   if        rp3 > 0
     C                   eval      h1srt3 = rt(rp3)
     C                   endif
      *
     C                   eval      h1type = srtype
     C                   eval      h1text = srtext
      *
      * Hent periode fra - til
      *
     C                   move      '41'          w_kode
     C     slpml1_key    chain     slpml1                             50
      *
     C                   eval      h1fmnd = srfmnd
     C                   eval      h1faar = srfaar
     C                   eval      h1tmnd = srtmnd
     C                   eval      h1taar = srtaar
      *
 001 C                   write     HD1A
      *
      * Key for vareregister
      *
     C     vvarl1_key    klist
     C                   kfld                    w_firm
     C                   kfld                    w_vare
      *
      * Key for leverandør-register
      *
     C     rlevl1_key    klist
     C                   kfld                    w_firm
     C                   kfld                    w_levr
      *
      *
      *
      *
      *
      *
      *
      *
      * Key for alt. hovedgruppe
      *
     C     hgrkey        klist
     C                   kfld                    w_firm
     C                   kfld                    w_hhgr
      *
      * Key for alt. undergruppe
      *
     C     ugrkey        klist
     C                   kfld                    w_firm
     C                   kfld                    w_uhgr
     C                   kfld                    w_uugr
      *
      * Key for overgruppe
      *
     C     vogrl1_key    klist
     C                   kfld                    w_firm
     C                   kfld                    w_oogr
      *
      * Key for hovedgruppe
      *
     C     vhgrl1_key    klist
     C                   kfld                    w_firm
     C                   kfld                    w_hogr
     C                   kfld                    w_hhgr
      *
      * Key for undergruppe
      *
     C     vugrl1_key    klist
     C                   kfld                    w_firm
     C                   kfld                    w_uogr
     C                   kfld                    w_uhgr
     C                   kfld                    w_uugr
      *
      * Key for ordretype
      *
     C     votyl1_key    klist
     C                   kfld                    w_firm
     C                   kfld                    w_otyp
      *
      * Key for rabattkategori
      *
     C     ra06pf_key    klist
     C                   kfld                    w_firm
     C                   kfld                    w_fkod
      *
      * Key for AVDELING
      *
     C     ra07pf_key    klist
     C                   kfld                    w_firm
     C                   kfld                    w_gkod
      *
      * Key for DISTRIKT
      *
     C     ra08pf_key    klist
     C                   kfld                    w_firm
     C                   kfld                    w_hkod
      *
      * Key for SELGER
      *
     C     ra09l1_key    klist
     C                   kfld                    w_firm
     C                   kfld                    w_ikod
      *
      * Key for LAGER
      *
     C     ra10pf_key    klist
     C                   kfld                    w_firm
     C                   kfld                    w_jkod
      *
      * Key for LEVERANDØRKATEGORI
      *
     C     ra14pf_key    klist
     C                   kfld                    w_firm
     C                   kfld                    w_nkod
      *
      * Key for LAND
      *
     C     ra12pf_key    klist
     C                   kfld                    w_firm
     C                   kfld                    w_lkod
      *
      *
      *
      *
      *
      *
      *
     C                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Tabeller                                                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
**       RAPPORT  TEKSTER
 Ledig                      01
 Ledig                      02
 Ledig                      03
 Ledig                      04
 Ledig                      05
 Ledig                      06
 Ledig                      07
 Ledig                      08
 Ledig                      09
 Ledig                      10
 Alt. hovedgruppe           11
 Alt. undergruppe           12
 Rabattgruppe               13
 Overgruppe                 14
 Hovedgruppe                15
 Undergruppe                16
 Ledig                      17
 Ledig                      18
 Ledig                      19
 Ledig                      20
 Leverandørkategori         21
 Rabattkategori             22
 Distrikt                   23
 Selger                     24
 Land                       25
 Hgr.-leverandørkat.        26
 Ledig                      27
 Ledig                      28
 Ledig                      29
 Ledig                      30
 Varenummer                 31
 Leverandør                 32
 Ledig                      33
 Ledig                      34
 Ledig                      35
 Ledig                      36
 Ledig                      37
 Ledig                      38
 Ledig                      39
 Ledig                      40
 Periode                    41
 Faktnr/Krednr              42
 Fakturadato                43
 Ledig                      44
 Ledig                      45
 Ledig                      46
 Ledig                      47
 Ledig                      48
 Ledig                      49
 Ledig                      50
 Ordretype                  51
 Avdeling                   52
 Lager                      53
 Produktansvarlig           54
 Ledig                      55
 Ledig                      56
 Ledig                      57
 Ledig                      58
 Ledig                      59
 Ledig                      60
