     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FJ100R
      * Beskrivelse . . : Fast ordretekst, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * 6.10  220609 BSA  Oppdatert med sporingsinformasjon
6.NU  * R6.30 260514 bsa  Programmet gjennomgått mtp nummerutvidelser.
7.xx  * 7.00  140316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.01  161116 bkv  Lagt inn H felt i DDS for bruk i OA
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffftxl1    if   e           k disk    rename(fftxpfr:fftxl1r)
     ffftxlr    if   e           k disk    rename(fftxpfr:fftxlrr)
     ffftxlu    uf a e           k disk    rename(fftxpfr:fftxlur)
      *
     ffj100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d fftxl1_numm     s                   like(fjnumm)
     d fftxlr_numm     s                   like(fjnumm)
     d fftxlu_numm     s                   like(fjnumm)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(fjfirm)
      *
     d s_numm          s                   like(fjnumm)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meleingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
7.xx c*                  when      *in21
7.xx c*                  eval      *in22 = *on
7.xx c*                  goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2numm <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      fftxl1_numm = b1numm
     c     fftxl1_key    setll     fftxl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Kode
      *
     c                   if        b2numm <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      fftxl1_numm = b2numm
     c     fftxl1_key    setll     fftxl1
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2numm = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1numm = b1numm
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1numm = b1numm
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1numm = b1numm
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1numm = b1numm
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1numm = b2numm
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
     c                   if        c1numm = *blank
     c                   goto      end_c1bld
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      fftxlr_numm = c1numm
     c     fftxlr_key    chain     fftxlr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1numm = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2numm = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      fftxlr_numm = c1numm
     c     fftxlr_key    chain     fftxlr
      *
     c                   if        %found
     c                   eval      c2refx = fjrefx
     C                   MOVE      FJSL01        C2SL01
     C                   MOVE      FJSL02        C2SL02
     C                   MOVE      FJSL03        C2SL03
     C                   MOVE      FJSL04        C2SL04
     C                   MOVE      FJSL05        C2SL05
     C                   MOVE      FJSL06        C2SL06
     C                   MOVE      FJSL07        C2SL07
     C                   MOVE      FJSL08        C2SL08
     C                   MOVE      FJSL09        C2SL09
     C                   MOVE      FJSL10        C2SL10
     C                   MOVE      FJSL11        C2SL11
     C                   MOVE      FJSL12        C2SL12
     C                   MOVE      FJSL13        C2SL13
     C                   MOVE      FJSL14        C2SL14
     C                   MOVE      FJSL15        C2SL15
     C                   MOVE      FJSL16        C2SL16
     c                   else
     c                   eval      c2refx = *blank
     C                   MOVE      *BLANK        C2SL01
     C                   MOVE      *BLANK        C2SL02
     C                   MOVE      *BLANK        C2SL03
     C                   MOVE      *BLANK        C2SL04
     C                   MOVE      *BLANK        C2SL05
     C                   MOVE      *BLANK        C2SL06
     C                   MOVE      *BLANK        C2SL07
     C                   MOVE      *BLANK        C2SL08
     C                   MOVE      *BLANK        C2SL09
     C                   MOVE      *BLANK        C2SL10
     C                   MOVE      *BLANK        C2SL11
     C                   MOVE      *BLANK        C2SL12
     C                   MOVE      *BLANK        C2SL13
     C                   MOVE      *BLANK        C2SL14
     C                   MOVE      *BLANK        C2SL15
     C                   MOVE      *BLANK        C2SL16
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1numm = k1numm
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   fftxlur
      *
     c                   eval      fftxlu_numm = c1numm
     c     fftxlu_key    chain     fftxlur
      *
     c                   eval      fjrefx = c2refx
     C                   MOVE      C2SL01        FJSL01
     C                   MOVE      C2SL02        FJSL02
     C                   MOVE      C2SL03        FJSL03
     C                   MOVE      C2SL04        FJSL04
     C                   MOVE      C2SL05        FJSL05
     C                   MOVE      C2SL06        FJSL06
     C                   MOVE      C2SL07        FJSL07
     C                   MOVE      C2SL08        FJSL08
     C                   MOVE      C2SL09        FJSL09
     C                   MOVE      C2SL10        FJSL10
     C                   MOVE      C2SL11        FJSL11
     C                   MOVE      C2SL12        FJSL12
     C                   MOVE      C2SL13        FJSL13
     C                   MOVE      C2SL14        FJSL14
     C                   MOVE      C2SL15        FJSL15
     C                   MOVE      C2SL16        FJSL16
      *
     c                   if        %found
6.10 c                   time                    fjedat
6.10 c                   time                    fjetim
6.10 c                   eval      fjeusr = l_user
     c                   update    fftxlur
     c                   else
     c                   eval      fjfirm = w_firm
     c                   eval      fjnumm = c1numm
6.10 c                   time                    fjodat
6.10 c                   time                    fjotim
6.10 c                   eval      fjousr = l_user
6.10 c                   time                    fjedat
6.10 c                   time                    fjetim
6.10 c                   eval      fjeusr = l_user
     c                   write     fftxlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1refx = fjrefx
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      fftxlr_numm = d1numm
     c     fftxlr_key    chain     fftxlr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      fftxlu_numm = d1numm
     c     fftxlu_key    chain     fftxlur
      *
     c                   delete    fftxlur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_numm = k1numm
      *
      * Fyller kopierings-bildet
      *
     c                   eval      fftxlr_numm = k1numm
     c     fftxlr_key    chain     fftxlr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1numm = *blank
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      fftxlr_numm = k1numm
     c     fftxlr_key    chain     fftxlr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1numm = s_numm
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      fftxl1
      *
     c                   if        %eof or
     c                             fjfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      fftxl1_numm = fjnumm
      *
     c                   eval      b1valg = 0
     c                   eval      b1numm = fjnumm
     c                   eval      b1refx = fjrefx
7.01 c                   eval      b1sl01 = fjsl01
7.01 c                   eval      b1sl02 = fjsl02
7.01 c                   eval      b1sl03 = fjsl03
7.01 c                   eval      b1sl04 = fjsl04
7.01 c                   eval      b1sl05 = fjsl05
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     fftxl1_key    setgt     fftxl1
     c                   readp     fftxl1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     fftxl1
      *
     c                   if        %eof or
     c                             fjfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      fftxl1_numm = fjnumm
      *
     c                   enddo
      *
     c                   if        %eof
     c     fftxl1_key    setll     fftxl1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på kode
      *
     c     fftxl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fftxl1_numm
      *
      * Key for les på fast ordretekst
      *
     c     fftxlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fftxlr_numm
      *
      * Key for oppdatering av fast ordretekst
      *
     c     fftxlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fftxlu_numm
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjermbildet
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      fftxl1_numm = *blank
     c     fftxl1_key    setll     fftxl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
