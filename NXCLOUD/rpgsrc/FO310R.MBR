     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO310R
      * Beskrivelse . . : Kopiering av ordre (hele)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * R3.10 180200 AMD  Lagt inn nullstilling av bestillingsnummer
      *                   slik at dette ikke dras med over ved kopiering.
      * R3.11 220300 AMD  Leveringstype er tatt med i datastruktur hlrec
      *                   I tillegg legges det ut referanse på 10 posisjoner
5.01  * R5.01 241002 AMD  Lagt om rutiner for oppdatering av lager
5.02  * R5.02 260503 AMD  Lagt inn nullstilling av prosjektnummer
5.02  *                   slik at dette ikke dras med over ved kopiering.
5.03  * R5.03 100903 AMD  Lagt inn oppdatering av bruker og skjerm
5.21  * R5.21 261103 AMD  Lagt inn nullstilling av mobilnummer
5.22  * R5.22 041203 AMD  Lagt inn nullstilling av kode for skrevet pakks.
      *       191203 HKO  Tar hensyn til bruksrett-rabatt
      *                   Bruksrett-rabatt fjernes og hentes inn igjen på
      *                   nytt i forhold til kunden det kopieres til
5.31  * R5.31 100104 ASK  Lagt inn endringer for lager på linjenivå.
      *                   Overstyrte lager kopieres over på ny ordre.
5.41  * R5.41 050506 AMD  Lagt inn kopiering av lengdespesifikasjoner
      *                   dersom det er registrert slike. Også sletting
      *                   dersom sletting etter kopiering er valgt.
5.42  * R5.42 300606 AMD  Momskoden (FDOMVA) ble nullstillt ubetinget
      *                   og kom inn igjen bare ved almenningsløsningen
5.51  * R5.51 201005 AMD  Lagt inn styring på om kundeprosjekt skal tas
      *                   med eller ikke, ved kopiering til samme kunde
      *                   + nullstiller kode for restordre
5.52  *       281005 BOF  Nullstilling av bestillingsreferanser
      *       281105 HKO  Ved kopiering til tilbud, hentes oppfølgingsdato
      *                   og utgår-dato på nytt i.h.t. antall dager satt i
      *                   kode/status-registeret
      *       130907 bof  Fakturatype skal tas fra kunden              ato
5.60  * R5.60 091107 AMD  Lagt inn løsning for kontroll av grense for
      *                   almenningsrabatter
      * 6.10  240609 BSA  Oppdatert med sporingsinformasjon
      * 6.10  220909 ASK  Nye parametre til VL001R
6.11  * 6.10  110610 bof  Justert i utvidelse av parm VL001R
6.12  * 6.10  180711 bsa  Nullstiller felt for kort.
6.13  * 6.10  181011 bsa  Sjekk om "ny" kunde har tilsvarende kortkode
6.20  * 6.10  030112 bof  Tar med lev.dato når kopierer
6.21  * 6.10  240214 nho  Henter ikke inn bruksrett dersom p_xrab = 0
      *                   dvs at kopi skal være lik tilbud
6.nu  * R6.30 270514 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.30  * 6.30  120914 BKV  Henter mva kode fra kundepro
6.31  * 6.30  040615 bkv  Ordre økt fra 6 til 8
6.32  * 6.31  260719 bof  Setter riktig mvakode til ordrelinj. FDOMVA
7.01  * 6.30  311019 bkv  Legg på info kode ved overgang otyp1 til otyp2
7.02  * 6.30  311019 bkv  Rettelse på 7.01 .... feil ordre fikk info kode
7.03  * 6.30  311019 bkv  Rettelse på 7.02 .... feil ordre fikk info kode
7.04  *       070420 bof  FOPROJ skal også nullstilles på lik linje med FOKPRO
7.05  *       130520 bof  Hvis PN  legg *H i info-type
7.06  * 6.30  091018 bsa  Logger ordrelinjer i skyggetabell.
7.07  * 6.30  170119 bsa  Hvis pris ikke er opprettet tidligere, må
7.07  *                   "bruttopris" også kalkuleres.
7.08  * 7.00  021120 bsa  Ulogisk at mva kode på prosjektet skal overstryre
7.08  * 7.00              mva koden på ny kunde. Sjekkes kun hvis kundenummer
7.08  * 7.00              er det samme. (Ref 6.32)
      *
8.01  * 8.xx  180322 bsa  Nullstiller referanse og system kilde
8.02  * 8.xx  150822 bsa  Sjekk om kunden er satt opp med kortkode, og setter
8.02  * 8.xx              evnt sperrekode.(NB Tar gyldige korttype av type kredit)
8.03  * 8.xx  080623 bsa  Nye felter på tabellen (oppr. bestilt og oppr enhet)
8.04  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frmknl2    if   e           k disk    rename(rmknpfr:rmknl2r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffohelu    uf a e           k disk    rename(fohepfr:fohelur)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffodtlu    uf a e           k disk    rename(fodtpfr:fodtlur)
     ffotxl1    if   e           k disk    rename(fotxpfr:fotxl1r)
     ffotxlu    uf a e           k disk    rename(fotxpfr:fotxlur)
5.41 ffvlsl1    if   e           k disk    rename(fvlspfr:fvlsl1r)
5.41 ffvlslu    uf a e           k disk    rename(fvlspfr:fvlslur)
6.13 frukrl5    if   e           k disk    rename(rukrpfr:rukrl5r)
8.02 frukrlr    if   e           k disk    rename(rukrpfr:rukrlrr)
8.02 fruksl1    if   e           k disk    rename(rukspfr:ruksl1r)
6.30 ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
6.32 fvmval1    if   e           k disk    rename(vmvapfr:vmval1r)
6.32 fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
7.01 fvinfl1    if   e           k disk    rename(vinfpfr:vinfl1r)
7.06 ffodmiu    uf a e           k disk    rename(fodmst:fodmiur)
5.03  *
5.03  * Definering av Local data area
5.03  *
5.03 d                uds
5.03 d l_wsid                901    910
5.03 d l_user                911    920
7.01 d l_filg                931    933
      *
      * Definering av datastruktur
      *
     d                 ds
6.nu d hlrec                        128
     d  hlvare                       15    overlay(hlrec)
     d  hllage                        2  0 overlay(hlrec:16)
     d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
     d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
     d  hlotyp                        2    overlay(hlrec:36)
     d  hlltyp                        2    overlay(hlrec:38)
     d  hlanta                       11  3 overlay(hlrec:40)
     d  hlkopr                        9  2 overlay(hlrec:51)
     d  hlrefr                       20    overlay(hlrec:60)
     d  hlsyst                        1    overlay(hlrec:80)
     d  hlaltk                        2    overlay(hlrec:81)
     d  hlrest                        1    overlay(hlrec:83)
     d  hltype                        1    overlay(hlrec:84)
     d  hlenhe                        3    overlay(hlrec:85)
     d  hlinlr                        1    overlay(hlrec:88)
     d  hllety                        1    overlay(hlrec:89)
6.nu d  hlonum                        8  0 overlay(hlrec:90)
6.nu d  hlolin                        4  0 overlay(hlrec:98)
      *
      * Definering av parametere
      *
     d p_coor          s                   like(w_alf1)
     d p_firm          s                   like(fofirm)
     d p_kund          s                   like(fokund)
6.nu d p_numm          s                   like(fonumm)
6.nu d p_nyor          s                   like(fonumm)
     d p_otyp          s                   like(footyp)
     d p_suff          s                   like(fosuff)
     d p_xrab          s              1  0
5.51 d p_xpro          s              1  0
     d p_xtxt          s              1  0
7.06 d p_inlr          s              1
      *
      * Definering av variable i nøkler
      *
     d rkunl1_kund     s                   like(rkkund)
     d rmknl2_xknr     s                   like(rmxknr)
     d votyl1_otyp     s                   like(vaotyp)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fodtl1_line     s                   like(fdline)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d fotxl1_numm     s                   like(ftnumm)
     d fotxl1_suff     s                   like(ftsuff)
     d fotxl1_line     s                   like(ftline)
5.41 d fvlsl1_numm     s                   like(fvnumm)
5.41 d fvlsl1_suff     s                   like(fvsuff)
     d fohelu_numm     s                   like(fonumm)
     d fohelu_suff     s                   like(fosuff)
     d fodtlu_numm     s                   like(fdnumm)
     d fodtlu_suff     s                   like(fdsuff)
     d fodtlu_line     s                   like(fdline)
     d fotxlu_numm     s                   like(ftnumm)
     d fotxlu_suff     s                   like(ftsuff)
     d fotxlu_line     s                   like(ftline)
5.41 d fvlslu_numm     s                   like(fvnumm)
5.41 d fvlslu_suff     s                   like(fvsuff)
5.41 d fvlslu_line     s                   like(fvline)
5.41 d fvlslu_lpnr     s                   like(fvlpnr)
6.13 d rukrl5_ktyp     s                   like(ruktyp)
6.13 d rukrl5_kkun     s                   like(rukkun)
6.13 d rukrl5_kpro     s                   like(rukpro)
6.30 d fkprl1_kund     s                   like(flkund)
6.30 d fkprl1_kpro     s                   like(flkpro)
6.32 d vmval1_mkod     s                   like(vamkod)
6.32 d vvarl1_vare     s                   like(vvvare)
7.01 d vinfl1_ityp     s                   like(vaityp)
8.02 d rukrlr_kkun     s                   like(rukkun)
8.02 d ruksl1_skty     s                   like(ruskty)
8.02 d ruksl1_slag     s                   like(ruslag)
      *
      * Definering av variable
      *
     d w_alf1          s              1
     d w_enor          s              1
     d w_sald          s             11  2
5.60 d w_sal2          s             11  2
6.nu d wlrec           s            128
     d wv001r          s              1
5.01 d w_oakk          s              1  0
6.31 d w_numa          s              8
5.02 d w_lina          s              4
6.30 d w_mkod          s                   like(rkmkod)
6.30 d w_betb          s                   like(rkbetb)
7.06 d w_time          s               t   timfmt(*iso)
7.06 d w_rabt          s             15  4
8.02 d w_dato          s               d   datfmt(*iso)
      *
     d w_firm          s                   like(fofirm)
     d w_alme          s                   like(rmxalm)
6.13 d b_kort          s              1    inz(*off)
      *
7.06 d s_osap          s                   like(fdosap)
7.06 d s_otip          s                   like(fdotip)
7.06 d s_okop          s                   like(fdokop)
7.06 d s_oinp          s                   like(fdoinp)
7.06 d s_oppk          s                   like(fdoppk)
7.06 d s_okpr          s                   like(fdokpr)
7.06 d s_okr1          s                   like(fdokr1)
7.06 d s_okr2          s                   like(fdokr2)
7.06 d s_okpk          s                   like(fdokpk)
      *
7.06 d x_sapr          s                   like(fdsapr)
7.06 d x_bupr          s                   like(fdsapr)
7.06 d x_tipr          s                   like(fdsapr)
7.06 d x_kopr          s                   like(fdsapr)
7.06 d x_inpr          s                   like(fdsapr)
      *
7.01 d w_otype         s                   like(footyp)
7.01 d w_otyp1         s                   like(footyp)
7.01 d w_otyp2         s                   like(footyp)
7.01 d w_ityp          s                   like(foityp)
7.01 d w_pos           s              2  0
      *
7.00   dcl-s co402_verdi1  char(1);
7.00   dcl-s co402_verdi2  char(2048);
7.00   DCL-PR CO402R EXTPGM('CO402R');
7.00     p_filgrp            char(3)     const;
7.00     p_firm              packed(3:0) const;
7.00     p_lager             packed(2:0) const;
7.00     p_nokkel            char(50)    const;
7.00     p_verdi1            char(1);
7.00     p_verdi2            char(2048);
7.00   END-PR;
7.01 d u_701           s              1    inz(*off)
7.05 d u_705           s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter ordre-hode
      *
     c     fohel1_key    chain     fohel1r                            90
      *
7.01 c                   eval      w_otype = footyp
      *
      * Henter almenning
      *
     c                   eval      rmknl2_xknr = p_kund
     c     rmknl2_key    chain     rmknl2
     c                   if        %found
     c                   eval      w_alme = rmxalm
     c                   endif
      *
      * Sjekk, Skal Faste tekstlinjer kopieres med   0=Nei, 1=Ja
      *
     c                   if        p_xtxt <> *zero
      *
     c                   eval      fotxl1_line = *zero
     c     fotxl1_key    setll     fotxl1r
      *
     c     tag01         tag
     c                   eval      *in90 = *off
     c                   read      fotxl1r                                90
      *
     c                   if        *in90 = *off
     c                   if        fotxl1_numm = ftnumm
     c                   if        fotxl1_suff = ftsuff
      *
     c                   eval      fotxlu_line = ftline
     c     fotxlu_key    chain     fotxlur                            92
     c                   if        *in92 = *on
     c                   eval      ftnumm = fotxlu_numm
     c                   eval      ftsuff = fotxlu_suff
     c                   eval      ftline = fotxlu_line
6.10 c                   time                    ftodat
6.10 c                   time                    ftotim
6.10 c                   eval      ftousr = l_user
6.10 c                   time                    ftedat
6.10 c                   time                    ftetim
6.10 c                   eval      fteusr = l_user
     c                   write     fotxlur
     c                   endif
      *
     c                   goto      tag01
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
6.32  * Flyttet les av kunde hit
6.32  * Hent opplysninger fra KUNDE-REGISTER
6.32  *
6.32 c                   eval      rkunl1_kund = p_kund
6.32 c     rkunl1_key    chain     rkunl1r                            94
6.32  * Slå opp på kundeprosjekt for å finne om prosjekt er
6.32  * overstyrer verdi på kunde
6.32 c                   eval      w_mkod = rkmkod
6.32 c                   eval      w_betb = rkbetb
7.08 c                   if        fokpro > 0 and
7.08 c                             fokund = p_kund
6.32 c                   eval      fkprl1_kund = fokund
6.32 c                   eval      fkprl1_kpro = fokpro
6.32 c     fkprl1_key    chain     fkprl1
6.32 c                   if        %found(fkprl1)
6.32 c                   eval      w_mkod = flmkod
6.32 c                   eval      w_betb = flbetb
6.32 c                   endif
6.32 c                   endif
6.32  *

      * Leser og kopierer alle ordre-linjer
      *
     c                   eval      fodtl1_line = *zero
     c     fodtl1_key    setll     fodtl1r
      *
     c     tag02         tag
     c                   eval      *in90 = *off
     c                   read      fodtl1r                                90
      *
     c                   if        *in90 = *off
     c                   if        fodtl1_numm     = fdnumm
     c                   if        fodtl1_suff     = fdsuff
      *
     c                   eval      fodtlu_line = fdline
     c     fodtlu_key    chain     fodtlur                            92
     c                   if        *in92 = *on
      *
      * Legger inn nye opplysninger (Ordretype + Kundenummer)
      *
     c                   eval      fdkund = p_kund
      *
      * Kode=2, Nullstiller rabatt
      *
     c                   if        p_xrab = 2
     c                   eval      fdrab1 = *zero
     c                   eval      fdrab2 = *zero
     c                   eval      fdsumr = *zero
     c                   endif
      *
6.32  * henter riktig momskode ihht. kode på vare.
6.32  *
      *
6.32 c                   if        w_mkod = 1
6.32 c                   eval      fdomva = '4'
6.32 c                   else
6.32 c                   eval      vvarl1_vare = fdvare
6.32 c     vvarl1_key    chain     vvarl1
6.32 c                   if        %found(vvarl1)
6.32 c                   eval      vmval1_mkod = vvkmva
6.32 c                   else
6.32 c                   eval      vmval1_mkod = 0
6.32 c                   endif
6.32 c     vmval1_key    chain     vmval1
6.32 c                   if        %found
6.32 c                   eval      fdomva = vamkof
6.32 c                   if        w_mkod = 2 and
6.32 c                             vamkuo <> *blank
6.32 c                   eval      fdomva = vamkuo
6.32 c                   endif
6.32 c                   else
6.32 c                   select
6.32 c                   when      w_mkod = 1
6.32 c                   eval      fdomva = '4'
6.32 c                   when      w_mkod = 0
6.32 c                   eval      fdomva = '3'
6.32 c                   endsl
6.32 c                   endif
6.32 c                   endif
6.32  *
6.21 c                   if        p_xrab = 0
6.21 c                   goto      ikbruk
6.21 c                   endif
      * Behandler bruksrett-rabatt
      *
     c                   eval      fdalme = 0
5.42  ***>               eval      fdomva = *blank
     c                   eval      fdbrty = 0
     c                   eval      fdbrr1 = 0
     c                   eval      fdbrr2 = 0
     c                   eval      fdpriv = 0
      *
     c                   if        w_alme <> 0 and
     c                             fdvare <> *blank
     c                   call      'VB700R'
     c                   parm                    w_firm
     c                   parm                    w_alme
     c                   parm                    fdvare
     c                   parm                    fdbrty
     c                   parm                    fdbrr1
     c                   parm                    fdbrr2
     c                   parm                    fdomva
     c                   if        fdbrty <> 0 or
     c                             fdbrr1 <> 0 or
     c                             fdbrr2 <> 0 or
     c                             fdomva <> *blank
     c                   eval      fdalme = w_alme
     c                   endif
     c                   endif
      *
6.21 c     ikbruk        tag
      * Nullstiller øvrige felter
      *
     c                   eval      fdantb = *zero
     c                   eval      fdantc = *zero
3.10 c                   eval      fdbenr = *zero
5.52 c                   eval      fdbsuf = *zero
5.52 c                   eval      fdblin = *zero
      *
     c                   eval      fdnumm = fodtlu_numm
     c                   eval      fdsuff = fodtlu_suff
     c                   eval      fdline = fodtlu_line
      *
6.10 c                   time                    fdodat
6.10 c                   time                    fdotim
6.10 c                   eval      fdoous = l_user
6.10 c                   time                    fdedat
6.10 c                   time                    fdetim
6.10 c                   eval      fdeusr = l_user
     c                   write     fodtlur
7.06  *
7.06  * Legg ut varelinje i skyggetabell
7.06  *
7.06 c                   if        fdvare <> *blanks
7.06 c                   eval      s_okpr = fdsapr
7.06 c                   eval      s_okr1 = fdrab1
7.06 c                   eval      s_okr2 = fdrab2
7.06 c                   eval      s_okpk = fdkpri
7.06 c                   eval      s_okop = fdkopr
7.06  *
7.06 c                   exsr      lag_skygge
7.06 c                   endif
      *
      * Kaller opp subrutine for oppdatering av lager
5.31  * Lagre overstyrt på linjenivå kopieres over på ny ordre.
      *
     c                   if        faalag <> *zero
     c                   exsr      opplag
     c                   endif
      *
     c                   endif
      *
     c                   goto      tag02
     c                   endif
     c                   endif
     c                   endif
5.41  *
5.41  * Leser og kopierer alle lengdespesifikasjoner
5.41  *
5.41 c     fvlsl1_key    setll     fvlsl1r
5.41  *
5.41 c     fvlsl1_key    reade     fvlsl1r                                90
5.41  *
5.41 c                   dow       not %eof
5.41  *
5.41 c                   eval      fvlslu_line = fvline
5.41 c                   eval      fvlslu_lpnr = fvlpnr
5.41 c     fvlslu_key    chain     fvlslur
5.41  *
5.41 c                   if        not %found
5.41 c                   eval      fvnumm = fvlslu_numm
5.41 c                   eval      fvsuff = fvlslu_suff
5.41 c                   eval      fvline = fvlslu_line
5.41 c                   eval      fvlpnr = fvlslu_lpnr
6.10 c                   time                    fvodat
6.10 c                   time                    fvotim
6.10 c                   eval      fvousr = l_user
6.10 c                   time                    fvedat
6.10 c                   time                    fvetim
6.10 c                   eval      fveusr = l_user
5.41 c                   write     fvlslur
5.41 c                   endif
5.41  *
5.41 c     fvlsl1_key    reade     fvlsl1r                                90
5.41  *
5.41 c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Kopiering av ordre     ORDRE-HODE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Sjekk om kunde fra er lik kunde til
      *
     c                   eval      *in66 = *off
     c                   if        fokund <> p_kund
     c                   eval      *in66 = *on
     c                   endif
      *
      * Legger ut igjen ordren med nytt ordrenummer
      *
     c     fohelu_key    chain     fohelur                            92
     c                   if        *in92 = *on
      *
      * Legger inn nye opplysninger (Ordretype + Kundenummer)
      *
     c                   eval      footyp = p_otyp
     c                   eval      fokund = p_kund
      *
      * Legger inn dagens dato + skjerm og bruker
      *
     c                   time                    fodate
     c                   time                    fotime
5.03 c                   eval      fowsid = l_wsid
5.03 c                   eval      fouser = l_user
      *
     c                   move      fodate        fobdat
5.01  *
5.01  * Dersom behandlingskode 2 eller 3,
5.01  * skal det settes kode for at lager
5.01  * er oppdatert, i motsatt fall, ikke.
5.01  *
5.01 c                   call      'VA750R'
5.01 c                   parm                    w_firm
5.01 c                   parm                    p_otyp
5.01 c                   parm                    w_oakk
5.01  *
5.01 c                   if        w_oakk = 2 or
5.01 c                             w_oakk = 3
5.01 c                   eval      foolag = 1
5.01 c                   else
5.01 c                   eval      foolag = 0
5.01 c                   endif
      *
      * Nullstiller diverse felter
      *
6.20 c*                  eval      foldat = *loval
     c                   eval      fofkda = *loval
     c                   eval      foffda = *loval
     c                   eval      foddat = *loval
     c                   eval      fotdat = *loval
     c                   eval      foadat = *loval
      *
     c                   eval      fobinr = *zero
     c                   eval      fokref = *zero
     c                   eval      fokfak = *zero
     c                   eval      fokspr = *zero
8.01 c                   eval      fonref = *blanks
8.01 c                   eval      fofsys = *blanks
6.13  * Sjekk om det skal kortkoden på ordrehodet skal "leve" videre.
6.13 c                   if        fopsuf <> 0
6.13 c                   eval      b_kort = *off
6.13 c                   exsr      sjk_kort
6.13 c                   if        b_kort = *off
6.13 c                   eval      fopsuf = *zero
6.13 c                   endif
6.13 c                   endif
8.02  *
8.02  * Hvis kortkode ikke er stt, sjekk om kunden er satt op med en.
8.02  *
8.02 c                   if        fopsuf = 0
8.02 c                   exsr      sjk_kort2
8.02 c                   endif
8.02  *
5.51 c                   if        p_xpro = 1
5.02 c                   eval      fonavn = *blank
5.02 c                   eval      foadr1 = *blank
5.02 c                   eval      foadr2 = *blank
5.02 c                   eval      fosted = *blank
5.51 c                   eval      fokpro = *zero
7.04 c                   eval      foproj = *blank
5.02 c                   endif
5.02  *
      * Dersom det kopieres til tilbud hentes oppfølgingsdato og utgår-dato
      * i.h.t. antall dager i kode/status-register
      *
     c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1
     c                   if        %found and
     c                             vaoakk = 0
     c                   if        faaopf <> 0
     c     fobdat        adddur    faaopf:*d     foddat
     c                   endif
     c                   if        faautg <> 0
     c     fobdat        adddur    faautg:*d     fotdat
     c                   endif
     c                   endif
      *
      * Hent opplysninger fra KUNDE-REGISTER
      *
6.32 c*                   eval      rkunl1_kund = fokund
6.32 c*     rkunl1_key    chain     rkunl1r                            94
6.32  ** Slå opp på kundeprosjekt for å finne om prosjekt er
6.32  ** overstyrer verdi på kunde
6.32 c*                   eval      w_mkod = rkmkod
6.32 c*                   eval      w_betb = rkbetb
6.32 c*                   if        fokpro > 0
6.32 c*                   eval      fkprl1_kund = fokund
6.32 c*                   eval      fkprl1_kpro = fokpro
6.32 c*     fkprl1_key    chain     fkprl1
6.32 c*                   if        %found(fkprl1)
6.32 c*                   eval      w_mkod = flmkod
6.32 c*                   eval      w_betb = flbetb
6.32 c*                   endif
6.32 c*                   endif
6.32  *
      *
      * Legger inn kundeopplysninger i ordre-felter
      *
     c                   eval      fokuna = *blank
     c                   movel     rkalfa        fokuna
6.30 c                   eval      fobetb = w_betb
     c                   eval      fovalk = rkvalk
     c                   eval      forkat = rkrabk
6.30 c                   eval      fomkod = w_mkod
     c                   eval      fofaty = rkfaty
      *
      * Blanker felter ved kopiering til annen kunde
      *
     c                   if        *in66 = *on
     c                   eval      folkun = *zero
     c                   eval      fonavn = *blank
     c                   eval      foadr1 = *blank
     c                   eval      foadr2 = *blank
     c                   eval      fosted = *blank
5.21 c                   eval      fomobi = *blank
5.51 c                   eval      fokpro = 0
7.04 c                   eval      foproj = *blank
     c                   endif
      *
7.03  ** Hvis oppr otyp = w_otyp1 og p_otyp = w_otyp2, legg infokode på opprinnelig
7.03  **
7.03 c*                   if        u_701 = *on
7.03 c*                   if        w_otype = w_otyp1 and p_otyp = w_otyp2
7.03 c*                   if        foityp = *blank
7.03 c*                   eval      foityp = w_ityp
7.03 c*                   endif
7.03 c*                   endif
7.03 c*                   endif
      *
7.05  * Legg inn hold kode hvis ordretype = PN
7.05 c                   if        u_705 = *on
7.05 c                   if        footyp = 'PN'
7.05 c                   eval      foityp = faak98
7.05 c                   endif
7.05 c                   endif
      *
5.22 c                   eval      fopaks = 0
5.51 c                   eval      forest = 0
      *
     c                   eval      fofirm = w_firm
     c                   eval      fonumm = fohelu_numm
     c                   eval      fosuff = fohelu_suff
6.10 c                   time                    fodate
6.10 c                   time                    fotime
6.10 c                   eval      fooous = l_user
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   write     fohelur
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling  av ordren etter kopieringen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
7.03  * Hvis oppr otyp = w_otyp1 og p_otyp = w_otyp2, legg infokode på opprinnelig
7.03  *
7.03 c                   if        u_701 = *on
7.03 c                   if        w_otype = w_otyp1 and p_otyp = w_otyp2
7.03 c                   if        foityp = *blank
7.03 c                   call      'FO721R'
7.03 c                   parm                    fohel1_numm
7.03 c                   parm                    fohel1_suff
7.03 c                   parm                    w_ityp
7.03 c                   endif
7.03 c                   endif
7.03 c                   endif                                                  u_703
      *
      * Oppdatering av memo-saldo
      *
     c                   eval      w_sald = fotots - fototr
5.60 c                   eval      w_sal2 = fobrra
      *
     c                   call      'FA922R'
     c                   parm                    w_firm
     c                   parm                    p_otyp
     c                   parm                    p_kund
     c                   parm                    w_sald
5.60 c                   parm                    w_sal2
      *
      * Beregning av priser, summer og totaler
      *
      * Statuskode = 0  gir beregning av nye summer og totaler
      * Statuskode = 1  gir henting av nye priser/rabatter samt beregning
      *
     c                   eval      w_enor = *blank
      *
      * Subprogram kalles opp dersom statuskode er oppgitt
      *
     c                   if        p_xrab <> 0 or
     c                             w_alme <> 0
      *
     c                   if        p_xrab = 1
     c                   eval      w_enor = '1'
     c                   endif
      *
      * Kaller opp subprogram
      *
     c                   call      'FO730R'
     c                   parm                    w_enor
     c                   parm                    w_firm
     c                   parm                    fohelu_numm
     c                   parm                    fohelu_suff
      *
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatering av Lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opplag        begsr
      *
      * Legger over verdier i overførings-felter
      *
     c                   move      fdvare        hlvare
5.31 c                   eval      hllage = fdlage
     c                   eval      hlenhe = fdenh1
     c                   eval      hldato = *loval
     c                   eval      hltime = *loval
     c                   eval      hlotyp = p_otyp
     c                   eval      hlltyp = fdltyp
      *
     c                   eval      hlanta = fdanta
     c                   eval      hlkopr = fdkopr
     c                   eval      hlonum = fdnumm
     c                   eval      hlolin = fdline
     c                   eval      hlsyst = 'F'
     c                   eval      hlaltk = *blank
     c                   eval      hlrest = *blank
     c                   move      fdnumm        w_numa
     c                   move      fdline        w_lina
     c                   eval      hlrefr = 'O:' + w_numa +
     c                                      ' L:' + w_lina
3.11 c                   move      fdlety        hllety
      *
      * Kaller opp lageroppdaterings-program
      *
     c                   eval      wlrec = hlrec
      *
     c                   call      'VL001R'
     c                   parm                    wv001r
     c                   parm                    wlrec
      *
     c                   endsr
      *
6.13  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.13  * Sjekker at kortinformasjon finnes på kunde. Legger ut info på BOKK
6.13  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.13 c     sjk_kort      begsr
6.13  *
6.13  * Les kortregister for kunde, og sjekk om det skal legges ut info.
6.13  *
6.13 c                   eval      rukrl5_ktyp = fopsuf
6.13 c                   eval      rukrl5_kpro = 0
6.13 c                   eval      rukrl5_kkun = fokund
6.13 c     rukrl5_key    chain     rukrl5
6.13 c                   if        %found
6.13  * Kortet kan ikke være sperres
6.13 c                   if        rukspr <> 'J' and
6.13 c                             rukobl = 1
6.13 c                   eval      b_kort = *on
6.13 c                   endif
6.13 c                   endif
6.13  *
6.13 c                   endsr
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02  * Sjekker at kortinformasjon finnes på kunde. Legger ut info på BOKK
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02 c     sjk_kort2     begsr
8.02  *
8.02  * Les kortregister for kunde, og sjekk om det skal legges ut info.
8.02  *
8.02 c                   eval      rukrlr_kkun = fokund
8.02 c     rukrlr_key    setll     rukrlr
8.02 c     rukrlr_key    reade     rukrlr
8.02 c                   dow       not %eof
8.02  * Kortet kan ikke være sperret, og tillatelse for trekk må være satt
8.02 c                   if        rukspr <> 'J' and
8.02 c                             rukobl = 1
8.02  * Hent info fra kort
8.02 c                   eval      ruksl1_skty = ruktyp
8.02 c                   eval      ruksl1_slag = folage
8.02 c     ruksl1_key    chain     ruksl1
8.02 c                   if        %found
8.02 c                   if        w_dato > rusfra and
8.02 c                             rustil <> *loval and
8.02 c                             rushld = 'A'     and
8.02 c                             rustyp = 'K'     and
8.02 c                             w_dato < rustil  or
8.02 c                             rushld = 'A'     and
8.02 c                             rustyp = 'K'     and
8.02 c                             w_dato > rusfra  and
8.02 c                             rustil = *loval
8.02 c                   eval      fopsuf = ruskty
8.02 c                   eval      foityp = rusity
8.02 c                   leave
8.02 c                   endif
8.02 c                   else
8.02 c                   eval      ruksl1_slag = 0
8.02 c     ruksl1_key    chain     ruksl1
8.02 c                   if        %found
8.02 c                   if        w_dato > rusfra and
8.02 c                             rustil <> *loval and
8.02 c                             rushld = 'A'     and
8.02 c                             rustyp = 'K'     and
8.02 c                             w_dato < rustil  or
8.02 c                             rushld = 'A'     and
8.02 c                             rustyp = 'K'     and
8.02 c                             w_dato > rusfra  and
8.02 c                             rustil = *loval
8.02 c                   eval      fopsuf = ruskty
8.02 c                   eval      foityp = rusity
8.02 c                   leave
8.02 c                   endif
8.02 c                   endif
8.02 c                   endif
8.02  *
8.02 c                   endif
8.02  *
8.02 c     rukrlr_key    reade     rukrlr
8.02 c                   enddo
8.02  *
8.02 c                   endsr
7.06  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.06  * Lager skyggetabell for senere oppdatering ved fakturering
7.06  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.06  *
7.06 c     lag_skygge    begsr
7.06  *
7.06 c                   eval      p_inlr = *blanks
7.06  * Hent inn std priser
7.06 c                   call      'VP700R'
7.06 c                   parm                    w_firm
7.06 c                   parm                    fdvare
7.06 c                   parm                    fdprgr
7.06 c                   parm                    fdenh1
7.06 c                   parm                    fdlety
7.06 c                   parm                    fobdat
7.06 c                   parm                    w_time
7.06 c                   parm                    p_inlr
7.06 c                   parm                    x_sapr
7.06 c                   parm                    x_bupr
7.06 c                   parm                    x_tipr
7.06 c                   parm                    x_kopr
7.06  *
7.07 c                   if        x_sapr <> 0
7.06 c                   eval      s_osap = x_sapr
7.06 c                   eval      s_otip = x_tipr
7.06 c                   eval      s_okop = x_kopr
7.07 c                   else
7.07 c                   eval      s_osap = fdsapr
7.07 c                   eval      s_otip = 0
7.07 c                   eval      s_okop = fdkopr
7.07 c                   endif
7.07  *
7.06 c                   if        x_tipr <> 0
7.06 c                   eval      s_oppk = 'T'
7.06 c                   else
7.06 c                   eval      s_oppk = ' '
7.06 c                   endif
7.06  * Hent inn innkjøpspris
7.06 c                   call      'VP703R'
7.06 c                   parm                    w_firm
7.06 c                   parm                    fdvare
7.06 c                   parm                    fdprgr
7.06 c                   parm                    fdenh1
7.06 c                   parm                    fdlety
7.06 c                   parm                    fobdat
7.06 c                   parm                    p_inlr
7.06 c                   parm                    x_sapr
7.06 c                   parm                    x_bupr
7.06 c                   parm                    x_kopr
7.06 c                   parm                    x_inpr
7.06  *
7.07 c                   if        x_inpr <> 0
7.06 c                   eval      s_oinp = x_inpr
7.07 c                   else
7.07 c                   eval      s_oinp = fdinpr
7.07 c                   endif
7.06  *
7.06 c     fodtlu_key    chain     fodmiu
7.06 c                   if        not %found(fodmiu)
7.06 c                   clear                   fodmiur
7.06  *
7.06 c                   eval      fdfirm = w_firm
7.06 c                   eval      fdnumm = fodtlu_numm
7.06 c                   eval      fdsuff = fodtlu_suff
7.06 c                   eval      fdline = fodtlu_line
7.06  *
7.06 c                   time                    fdodat
7.06 c                   time                    fdotim
7.06 c                   eval      fdousr = l_user
7.06 c                   endif
7.06  * Beregn nettopris - kun hvis
7.06 c                   if        s_okpr <> 0
7.06  *
7.06 c                   if        s_okr1 <> 0
7.06 c                   eval      w_rabt = (s_okpr * s_okr1) / 100
7.06 c                   eval      s_okpr = s_okpr - w_rabt
7.06 c                   endif
7.06 c                   if        s_okr2 <> 0
7.06 c                   eval      w_rabt = (s_okpr * s_okr2) / 100
7.06 c                   eval      s_okpr = s_okpr - w_rabt
7.06 c                   endif
7.06  *
7.06 c                   endif
7.06  *
7.06 c                   eval      fdosap = s_osap
7.06 c                   eval      fdotip = s_otip
7.06 c                   eval      fdokop = s_okop
7.06 c                   eval      fdoinp = s_oinp
7.06 c                   eval      fdoppk = s_oppk
7.06 c                   eval      fdokpr = s_okpr
7.06 c                   eval      fdokr1 = s_okr1
7.06 c                   eval      fdokr2 = s_okr2
7.06 c                   eval      fdokpk = s_okpk
8.03 c                   eval      fdoant = fdanta
8.03 c                   eval      fdoenh = fdenh1
7.06 c                   time                    fdtmst
7.06 c                   time                    fdedat
7.06 c                   time                    fdetim
7.06 c                   eval      fdeusr = l_user
7.06  *
7.06 c                   if        not %found(fodmiu)
7.06 c                   write     fodmiur
7.06 c                   else
7.06 c                   update    fodmiur
7.06 c                   endif
7.06  *
7.06 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Nullstilling av felter som ikke kan redefineres
      *
     c                   eval      w_enor = *blank
     c                   eval      w_sald = *zero
5,60 c                   eval      w_sal2 = *zero
      *
      * Key for file : KUNDE-REGISTER
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på almenning, eiendomsknytning
      *
     c     rmknl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rmknl2_xknr
      *
      * Key for file : STATUS-KODER
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for file : ORDRE-TYPE-REGISTER
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for file : ORDRE-HODE-REGISTER
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for file : ORDRE-HODE-REGISTER   (TIL)
      *
     c     fohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelu_numm
     c                   kfld                    fohelu_suff
      *
      * Key for file : ORDRE-LINJE-REGISTER
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
     c                   kfld                    fodtl1_line
      *
      * Key for file : ORDRE-LINJE-REGISTER  (TIL)
      *
     c     fodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlu_numm
     c                   kfld                    fodtlu_suff
     c                   kfld                    fodtlu_line
      *
      * Key for file : ORDRE-TEKST-REGISTER
      *
     c     fotxl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fotxl1_numm
     c                   kfld                    fotxl1_suff
     c                   kfld                    fotxl1_line
      *
      * Key for file : ORDRE-TEKST-REGISTER  (TIL)
      *
     c     fotxlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fotxlu_numm
     c                   kfld                    fotxlu_suff
     c                   kfld                    fotxlu_line
5.41  *
5.41  * Key for file : LENGDE-SPESIFIKASJON
5.41  *
5.41 c     fvlsl1_key    klist
5.41 c                   kfld                    w_firm
5.41 c                   kfld                    fvlsl1_numm
5.41 c                   kfld                    fvlsl1_suff
5.41  *
5.41  * Key for file : LENGDE-SPESIFIKASJON  (TIL)
5.41  *
5.41 c     fvlslu_key    klist
5.41 c                   kfld                    w_firm
5.41 c                   kfld                    fvlslu_numm
5.41 c                   kfld                    fvlslu_suff
5.41 c                   kfld                    fvlslu_line
5.41 c                   kfld                    fvlslu_lpnr
6.13  *
6.13  * Key for file : Kundekort
6.13  *
6.13 c     rukrl5_key    klist
6.13 c                   kfld                    w_firm
6.13 c                   kfld                    rukrl5_ktyp
6.13 c                   kfld                    rukrl5_kkun
6.13 c                   kfld                    rukrl5_kpro
6.30  *
6.30  * Key for oppslag på kundeprosjekt
6.30  *
6.30 c     fkprl1_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    fkprl1_kund
6.30 c                   kfld                    fkprl1_kpro
6.32  *
6.32  * Key for oppslag på mva.-kode
6.32  *
6.32 c     vmval1_key    klist
6.32 c                   kfld                    w_firm
6.32 c                   kfld                    vmval1_mkod
6.32  *
6.32  * Key for oppslag på vare
6.32  *
6.32 c     vvarl1_key    klist
6.32 c                   kfld                    w_firm
6.32 c                   kfld                    vvarl1_vare
8.02  *
8.02  * Key for file : Kundekort
8.02  *
8.02 c     rukrlr_key    klist
8.02 c                   kfld                    w_firm
8.02 c                   kfld                    rukrlr_kkun
8.02  *
8.02  * Key for file : Kort
8.02  *
8.02 c     ruksl1_key    klist
8.02 c                   kfld                    w_firm
8.02 c                   kfld                    ruksl1_skty
8.02 c                   kfld                    ruksl1_slag
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_coor
     c                   parm                    p_firm
6.nu c                   parm                    p_numm
     c                   parm                    p_suff
6.nu c                   parm                    p_nyor
     c                   parm                    p_otyp
     c                   parm                    p_kund
     c                   parm                    p_xrab
5.51 c                   parm                    p_xpro
     c                   parm                    p_xtxt
      *
      * Legger inn type for oppdatering av lager  R=Registrering
      *
5.01 c                   eval      hltype = ' '
      *
      * Legger inn firmanummer i nøkklene
      *
     c                   eval      w_firm = p_firm
      *
      * Legger inn ordrenummer og suffix inn i nøkklene
      *
     c                   eval      fohel1_numm = p_numm
     c                   eval      fohel1_suff = p_suff
      *
     c                   eval      fodtl1_numm = p_numm
     c                   eval      fodtl1_suff = p_suff
     c                   eval      fodtl1_line = *zero
      *
     c                   eval      fotxl1_numm = p_numm
     c                   eval      fotxl1_suff = p_suff
     c                   eval      fotxl1_line = *zero
      *
5.41 c                   eval      fvlsl1_numm = p_numm
5.41 c                   eval      fvlsl1_suff = p_suff
      *
      * Legger inn ordrenummer og suffix for ordre til
      *
     c                   eval      fohelu_numm = p_nyor
     c                   eval      fohelu_suff = *zero
      *
     c                   eval      fodtlu_numm = p_nyor
     c                   eval      fodtlu_suff = *zero
     c                   eval      fodtlu_line = *zero
      *
     c                   eval      fotxlu_numm = p_nyor
     c                   eval      fotxlu_suff = *zero
     c                   eval      fotxlu_line = *zero
      *
5.41 c                   eval      fvlslu_numm = p_nyor
5.41 c                   eval      fvlslu_suff = *zero
5.41 c                   eval      fvlslu_line = *zero
5.41 c                   eval      fvlslu_lpnr = *zero
      *
      * Hent opplysninger fra OFAK-KODE-REGISTER
      *
     c     fstsl1_key    chain     fstsl1r
7.06  *
7.06 c                   eval      s_osap = 0
7.06 c                   eval      s_otip = 0
7.06 c                   eval      s_okop = 0
7.06 c                   eval      s_oinp = 0
7.06 c                   eval      s_oppk = *blanks
7.06 c                   eval      s_okpr = 0
7.06 c                   eval      s_okr1 = 0
7.06 c                   eval      s_okr2 = 0
7.06 c                   eval      s_okpk = *blanks
7.06 c                   time                    w_time
8.02 c                   time                    w_dato
7.01  *
7.01  * Key for oppslag på ordre-info-type
7.01  *
7.01 c     vinfl1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    vinfl1_ityp
7.01  *
7.01  *
7.01  * Endring 7.01
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'FO310R_701':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     w_pos = %scan(':' : co402_verdi2);
7.01     if (w_pos > 0);
7.01       w_otyp1 = %subst(co402_verdi2 : 1 : w_pos - 1);
7.01       co402_verdi2 =  %subst(co402_verdi2:w_pos+1:512-w_pos);
7.01       w_pos = %scan(':' : co402_verdi2);
7.01       w_otyp2 = %subst(co402_verdi2 : 1 : w_pos - 1);
7.01       co402_verdi2 =  %subst(co402_verdi2:w_pos+1:512-w_pos);
7.01       w_pos = %scan(':' : co402_verdi2);
7.01       w_ityp = %subst(co402_verdi2 : 1 : w_pos - 1);
7.01       if (w_otyp1 <> *blank and w_otyp2 <> *blank  and w_ityp <> *blank);
7.01  * Sjekk ordre-info-kode i inforegister
7.01         vinfl1_ityp = w_ityp;
7.01         chain vinfl1_key vinfl1r;
7.01         if %found(vinfl1);
7.01           u_701 = *on;
7.01         endif;
7.01         clear vinfl1r;
7.01       endif;
7.01     endif;
7.01   endif;
7.05  *
7.05  * Endring 7.05
7.05  *
7.05   CallP CO402R(l_filg:w_firm:0:'FO310R_705':
7.05                    co402_verdi1:co402_verdi2);
7.05   if co402_verdi1 = '1';
7.05     u_705 = *on;
7.05   endif;
      *
     c                   endsr
