     H/TITLE  Utskrift av Bestilling
     H DECEDIT('0,') DATEDIT(*DMY.)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : FO980R
      * Beskrivelse . . : Utskrift av Bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
3.13  * R3.13 110900 AMD  Dersom vare er hentet fra LVR settes det inn
3.13  *                   2 desimaler som standard (finnes jo ikke i EVR)
6.nu  * R6.30 190614 bof  Sjekket mtp. nummerutvidelse
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     FFPSOL1    IPE  E             DISK    RENAME(FPSOPFR:FPSOL1R)
     FVOTYL1    IF   E           K DISK    RENAME(VOTYPFR:VOTYL1R)
     FVLTYL1    IF   E           K DISK    RENAME(VLTYPFR:VLTYL1R)
     FFOHEL1    IF   E           K DISK    RENAME(FOHEPFR:FOHEL1R)
     FFODTL1    IF   E           K DISK    RENAME(FODTPFR:FODTL1R)
     FFOTXL1    IF   E           K DISK    RENAME(FOTXPFR:FOTXL1R)
     FVVARL1    IF   E           K DISK    RENAME(VVARPFR:VVARL1R)
     Frlevl1    IF   E           K DISK    RENAME(RLEVPFR:rlevl1r)
     FAFIRL1    IF   E           K DISK    RENAME(AFIRPFR:AFIRL1R)
     FAFORL1    IF   E           K DISK    RENAME(AFORPFR:AFORL1R)
      *
     FFO980P    O    E             PRINTER
      *
      * - - - - - - - - - - - - - - - -
      * Datastruktur for foretaksnummer
      * - - - - - - - - - - - - - - - -
      *
     D                 DS
     D  DSFFTX                 1     30
     D  DSFF01                 1      8
     D  DSFF02                 9     11
     D  DSFF03                12     21
     D  DSFF04                22     25
     D  DSFF05                22     25
      *
      * - - - - - - - - - - - - - -
      * Henter inn data fra LDA
      * - - - - - - - - - - - - - -
      *
     D                UDS
     D  DSRUTI               800    809
     D  DSALIN               856    858  0
     D  DSFIRM               944    946  0
      *
     IFPSOL1R       01
     I                                          FKSSRT        L4
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av formater                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede formater
      * - - - - - - - - - -
      *
      * A1HDR  - Heading A1 på rapport
      * D1DET  - Detail  D1 på rapport, Varelinje
      * E1DET  - Detail  E1 på rapport, Tekstlinje
      * T1TOT  - Total   T1 på rapport
      *
      *
      * Forklaring på variabler
      * - - - - - - - - - - - -
      *
      * D1ANT0 - Antall med 0 desimaler
      * D1ANT1 - Antall med 1 desimaler
      * D1ANT2 - Antall med 2 desimaler
      * D1ANT3 - Antall med 3 desimaler
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Styre-rutine                                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Initierings-rutine
      *
     C   01FCYCLE        CASEQ     *ZERO         XSTRSR
     C                   ENDCS
      *
      * Ordrehode
      *
     C     FKSKOD        CABEQ     'H'           TAG01
      *
      * Varelinje
      *
     C     FKSKOD        CABEQ     'V'           TAG02
      *
      * Kommentar/
      * tekstlinje
      *
     C     FKSKOD        CABEQ     'K'           TAG03
      *
      * Ferdig
      *
     C                   GOTO      XSLUTT
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A1HDR Behandle heading A1                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     TAG01         TAG
      *
      * Hent ordre-heading
      *
     C                   MOVE      FKSONR        WWNUMM
     C                   MOVE      FKSSUF        WWSUFF
     C     KEY01         CHAIN     FOHEL1R                            61
      *
      * Snur datoer
      *
     c                   eval      a1bdat = 0
     c                   eval      a1ldat = 0
      *
     c                   if        fobdat <> *loval
     c     *dmy          move      fobdat        a1bdat
     c                   endif
      *
     c                   if        foldat <> *loval
     c     *dmy          move      foldat        a1ldat
     c                   endif
      *
      * Legg inn ordrenummer
      *
     C                   MOVE      FONUMM        A1NUMM
     C                   MOVEL     '/'           A1SUFF
     C                   MOVE      FOSUFF        A1SUFF
      *
      * Flere ordre på samme
      * bestilling ?
      *
     C  NL4              GOTO      TAG01A
      *
      * Hente inn leverandørregister
      *
     C                   MOVE      FOLDOR        WWLEVR
     C     KEY04         CHAIN     rlevl1r                            64
     C                   MOVE      RLNAVN        A1NAVN
     C                   MOVE      RLGATE        A1GATE
     C                   MOVE      RLADR2        A1ADR2
     C                   MOVE      RLSTED        A1STED
     C                   MOVE      RLTLFN        A1LTLF
     C                   MOVE      RLTLFX        A1LFAX
      *
      * Er det registrert
      * leveringsadresse?
      *
     C                   MOVE      *OFF          *IN31
     C     FONAVN        IFNE      *BLANK
     C                   MOVE      *ON           *IN31
     C                   MOVE      FONAVN        A2NAVN
     C                   MOVE      FOADR1        A2ADR1
     C                   MOVE      FOADR2        A2ADR2
     C                   MOVE      FOSTED        A2STED
     C                   ENDIF
      *
      * Er det registrert
      * kundens referanse?
      *
     C                   MOVE      *OFF          *IN33
     C     FODREF        IFNE      *BLANK
     C                   MOVE      *ON           *IN33
     C                   MOVE      FODREF        A1DREF
     C                   ENDIF
      *
      * Flytte øvrige felter
      *
     C***                  MOVE FOSELG    A1SELG
     C                   MOVE      FOVREF        A1VREF
      *
      * Skrive heading
      *
     C                   Z-ADD     0             WLTELL
     C                   WRITE     A1HDR
      *
     C                   GOTO      XSLUTT
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Heading U/L4-brudd                                              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     TAG01A        TAG
      *
     C                   GOTO      XSLUTT
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * D1DET Behandle detail D1                                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     TAG02         TAG
      *
      * Nullstilling av variabler
      *
     C                   MOVE      *BLANK        D1VARE
     C                   MOVE      *BLANK        D1TEK1
     C                   MOVE      *BLANK        D1TEK2
     C                   MOVE      *BLANK        D1ENH1
     C                   MOVE      *ZERO         D1ANT0
     C                   MOVE      *ZERO         D1ANT1
     C                   MOVE      *ZERO         D1ANT2
     C                   MOVE      *ZERO         D1ANT3
     C                   MOVE      *OFF          *IN44
      *
     C                   MOVE      FKSONR        WWNUMM
     C                   MOVE      FKSSUF        WWSUFF
     C                   MOVE      FKSLIN        WWLINE
     C     KEY02         CHAIN     FODTL1R                            62
      *
      * Hent linje-type
      *
     C                   MOVE      FDLTYP        WWLTYP
     C     KEY08         CHAIN     VLTYL1R                            68
      *
      * Sjekker om linjen ikke skrives på ordretypen
      *
     C     FOOTYP        CABEQ     VALO01        XSLUTT
      *
     C     FOOTYP        CABEQ     VALO02        XSLUTT
      *
     C     FOOTYP        CABEQ     VALO03        XSLUTT
      *
     C     FOOTYP        CABEQ     VALO04        XSLUTT
      *
      * Sjekker om tekstlinjer
      *
     C     VALKTX        CABEQ     1             TAG02A
      *
      * Oppslag mot vareregister
      *
     C                   MOVE      FDVARE        WWVARE
     C     KEY11         CHAIN     VVARL1R                            69
3.13  *
3.13  * Dersom vare er hentet fra LVR,
3.13  * sett inn  2 desimaler som std.
3.13  *
3.13 c                   if        fdlvre = 1
3.13 c                   eval      vvdesi = 2
3.13 c                   endif
      *
      * Test på antall desimaler
      *
     C     VVDESI        COMP      0                                      40
     C     VVDESI        COMP      1                                      41
     C     VVDESI        COMP      2                                      42
     C     VVDESI        COMP      3                                      43
     C   40              Z-ADD     FDANTA        D1ANT0
     C   41              Z-ADD     FDANTA        D1ANT1
     C   42              Z-ADD     FDANTA        D1ANT2
     C   43              Z-ADD     FDANTA        D1ANT3
      *
      * Varelinje
      *
     C                   MOVE      FDLVAR        D1VARE
     C                   MOVEL     FDTEK1        D1TEK1
     C                   MOVE      FDENH1        D1ENH1
      *
      * Skriv varelinjer
      *
     C     TAG02A        TAG
     C                   ADD       1             WLTELL
     C     VALKTX        IFNE      1
     C                   WRITE     D1DET                                30
     C                   ELSE
     C                   MOVEL     FDTEK1        D1TEK1
     C                   MOVEL     FDTEK2        D1TEK2
     C                   MOVE      *ON           *IN44
     C                   WRITE     D2DET                                30
     C                   END
      *
      * Er det overflow ?
      *
     C     *IN30         IFEQ      *ON
     C                   WRITE     A1HDR
     C                   Z-ADD     0             WLTELL
     C                   ENDIF
      *
     C                   GOTO      XSLUTT
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Behandling av tekstlinjer                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     TAG03         TAG
      *
     C                   MOVE      FKSONR        WWNUMM
     C                   MOVE      FKSSUF        WWSUFF
     C                   MOVE      FKSLIN        WWLINE
     C     KEY03         CHAIN     FOTXL1R                            63
      *
      * Gå ut dersom det
      * ikke er oppsamling,
      * og linje-nummer er
      * større enn 5000.
      * Da skal print fore-
      * gå tilslutt på L4.
      *
     C     VAOSAM        IFEQ      0
     C     FKSLIN        IFGE      5000
     C                   GOTO      XSLUTT
     C                   END
     C                   ENDIF
      *
     C                   MOVEL     FTTEXT        E1TEXT
     C                   ADD       1             WLTELL
     C                   WRITE     E1DET                                30
      *
      * Er det overflow ?
      *
     C     *IN30         IFEQ      *ON
     C                   WRITE     A1HDR
     C                   Z-ADD     0             WLTELL
     C                   ENDIF
      *
     C     XSLUTT        TAG
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * T1TOT Behandle total T1                                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      * Finne ut om det er
      * plass til å skrive
      * avslutning uten å
      * skifte til ny side
      * først.
      *
      * Trekker ut heading
      *
     CL4   DSALIN        SUB       24            WLTEST
      *
      * Trekker ut antall
      * linjer skrevet
      *
     CL4                 SUB       WLTELL        WLTEST
      *
      * Trekker ut plassbehov
      * for avslutning
      *
     CL4                 SUB       10            WLTEST
      *
      * Skrive ny heading ?
      *
     CL4   WLTEST        IFLT      0
     CL4                 WRITE     A1HDR
     CL4                 Z-ADD     0             WLTELL
     CL4                 ENDIF
      *
      * Skrive totalsum
      *
     CL4                 WRITE     T1TOT                                30
      *
      * Skrive tekstlinjer
      * dersom det ikke er
      * oppsamling og linje
      * nummer er større enn
      * 5000.
      *
     CL4   VAOSAM        IFEQ      0                                                     .....
     CL4                 MOVE      FKSONR        WWNUMM                                      .
     CL4                 MOVE      FKSSUF        WWSUFF                                      .
     CL4                 Z-ADD     5000          WWLINE                                      .
     CL4   KEY03         SETLL     FOTXL1R                                                   .
     CL4                 READ      FOTXL1R                                63                 .
     CL4   *IN63         CABEQ     *ON           TAG99                                       .
      *                                                                 .
     CL4   WWNUMM        DOWEQ     FTNUMM                                                ... .
     CL4   WWSUFF        IFEQ      FTSUFF                                                  . .
      *                                                               . .
     CL4                 MOVEL     FTTEXT        E1TEXT                                    . .
     CL4                 ADD       1             WLTELL                                    . .
     CL4                 WRITE     E1DET                                30                 . .
      *                                                               . .
      * Er det overflow ?                                             . .
      *                                                               . .
     CL4   *IN30         IFEQ      *ON                                                     . .
     CL4                 WRITE     A1HDR                                                   . .
     CL4                 Z-ADD     0             WLTELL                                    . .
     CL4                 ENDIF                                                             . .
     CL4                 ENDIF                                                             . .
      *                                                               . .
     CL4                 READ      FOTXL1R                                63               . .
     CL4   *IN63         CABEQ     *ON           TAG99                                     . .
     CL4                 ENDDO                                                           ... .
     CL4                 ENDIF                                                           .....
      *
     CL4   TAG99         TAG
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for oppstart                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XSTRSR        BEGSR
      *
     C                   Z-ADD     1             FCYCLE            1 0
      *
      * Legger inn firma i nøkkel
      *
     C                   MOVE      FKSFIR        WWFIRM
      *
      * Klargjøring dersom firma -
      * opplysninger skal printes ut
      *
     C     AFPRFI        IFEQ      1
     C     KEY06         CHAIN     AFIRL1R                            66
     C                   MOVE      AAFNAV        A1FNAV
     C                   MOVE      AAFAD1        A1FAD1
     C                   MOVE      AAFAD2        A1FAD2
     C                   MOVE      AAFSTE        A1FSTE
     C                   MOVE      'Foretak:'    DSFF01
     C                   MOVE      ' NO'         DSFF02
     C                   MOVE      AAFFTN        DSFF03
     C                   MOVE      ' MVA'        DSFF04
     C                   MOVE      DSFFTX        A1FFTX
     C                   MOVE      AAFTLF        A1FTLF
     C                   MOVE      AAFFAX        A1FFAX
     C                   MOVE      *ON           *IN37
     C                   ENDIF
      *
      * Slår opp ordretype
      *
     C                   MOVE      FKSOTY        WWOTYP
     C     KEY07         CHAIN     VOTYL1R                            67
      *
      * Er ordretype negativ ?
      *
     C     VAOKRE        IFEQ      '-'
     C                   MOVE      *ON           *IN36
     C                   ENDIF
      *
     C                   ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     *INZSR        BEGSR
      *
     C     *LIKE         DEFINE    VAOTYP        WWOTYP
     C     *LIKE         DEFINE    FOLDOR        WWLEVR
     C     *LIKE         DEFINE    FOFIRM        WWFIRM
     C     *LIKE         DEFINE    FONUMM        WWNUMM
     C     *LIKE         DEFINE    FOSUFF        WWSUFF
     C     *LIKE         DEFINE    FDLINE        WWLINE
     C     *LIKE         DEFINE    FDLTYP        WWLTYP
     C     *LIKE         DEFINE    FDNUMM        SANUMM
     C     *LIKE         DEFINE    VVVARE        WWVARE
      *
     C                   MOVE      *BLANK        WWRUTI           10
     C                   MOVE      *ZERO         WLTELL            3 0
     C                   MOVE      *ZERO         WLTEST            3 0
      *
      * Key for Heading-file
      *
     C     KEY01         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWNUMM
     C                   KFLD                    WWSUFF
      *
      * Key for Detalj-file
      *
     C     KEY02         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWNUMM
     C                   KFLD                    WWSUFF
     C                   KFLD                    WWLINE
      *
      * Key for Tekst-linjer
      *
     C     KEY03         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWNUMM
     C                   KFLD                    WWSUFF
     C                   KFLD                    WWLINE
      *
      * Key for leverandørregister
      *
     C     KEY04         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWLEVR
      *
      * Key for firmaregister
      *
     C     KEY06         KLIST
     C                   KFLD                    WWFIRM
      *
      * Key for ordretyper
      *
     C     KEY07         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWOTYP
      *
      * Key for linjetyper
      *
     C     KEY08         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWLTYP
      *
      * Key for statusregister
      *
     C     KEY09         KLIST
     C                   KFLD                    WWFIRM
      *
      * Key for formular-register
      *
     C     KEY10         KLIST
     C                   KFLD                    WWRUTI
      *
      * Key for vare-register
      *
     C     KEY11         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWVARE
      *
      * Hente inn rutinenavn
      * og meldingstekst
      *
     C                   MOVE      DSRUTI        WWRUTI
      *
     C     KEY10         CHAIN     AFORL1R                            69
     C  N69              MOVE      AFTXT1        A1TXT1
     C  N69              MOVE      AFMLD1        T1MLD1
      *
     C                   ENDSR
      *
