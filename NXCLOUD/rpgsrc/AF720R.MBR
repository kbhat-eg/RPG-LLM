     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASPGPL
      * Program . . . . : AF720R
      * Beskrivelse . . : Skrive logg for FTP overføring
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       270104 MHA  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fafthlu    uf a e           k disk    rename(afthpfr:afthlur)
     faftllu    uf a e           k disk    rename(aftlpfr:aftllur)
     faflopf    if a e             disk      rename(aflopf:aflopfr)
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_fgr                 931    933
     d l_user                911    920
      *
      * Definering av datastrukturer
      *
      * Definering av variabler i nøkler
      *
      * Definering av variable
      *
     d w_firm          s                   like(adhfir)
     d w_uidn          s                   like(adhuid)
     d w_tell          s                   like(adllin) INZ(1)
      *
      * Definering av parametre
     d p_navn          s                   like(adhnav)
     d p_kom1          s                   like(adhkom)
     d p_stat          s                   like(adhsta)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Skriv hode
     c                   read      afthlur
     c                   eval      adhfir = w_firm
     c                   eval      adhnav = p_navn
     c                   eval      w_uidn = %timestamp
     c                   eval      adhuid = w_uidn
     c                   eval      adhusr = l_user
     c                   eval      adhkom = p_kom1
     c                   eval      adhsta = p_stat
     c                   write     afthlur
      *
      * Skriv linjer
      *
     c                   read      aftllur
     c                   read      aflopf
     c                   dou       %eof
     c                   movel     SrcDta        adltxt
     c                   eval      adlfir = w_firm
     c                   eval      adlnav = p_navn
     c                   eval      adluid = w_uidn
     c                   eval      adllin = w_tell
     c                   write     aftllur
     c                   eval      w_tell = w_tell + 1
     c                   read      aflopf
     c                   enddo
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_navn
     c                   parm                    p_kom1
     c                   parm                    p_stat
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
