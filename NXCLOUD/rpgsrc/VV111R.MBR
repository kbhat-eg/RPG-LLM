     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VV111R
      * Beskrivelse . . : Vare, bytter varenummer på vare
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       210699 HKO  Nytt program
      *       130102 HKO  Fjernet bytting av varenummer i S36-statistikken
      *       041204 HKO  Endret parameter ved kall av VV740R og FO740C
      * 6.10  290609 BSA  Oppdatert med sporingsinformasjon
6.31  *       151118 bof  Endre modulnr ihht. VA
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarlu    uf   e           k disk    rename(vvarpfr:vvarlur)
6.31 fjvarl1    uf   e           k disk    rename(jvarpfr:jvarl1r)
      *
6.10  * Definering av Local data area
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vvfirm)
     d p_gvar          s                   like(vvvare)
     d p_nvar          s                   like(vvvare)
      *
      * Definering av variable i nøkler
      *
     d vvarlu_vare     s                   like(vvvare)
6.31 d jvarl1_vare     s                   like(jvvare)
      *
      * Definering av variable
      *
     d b_inlr          s              1    inz(*off)
      *
     d w_firm          s                   like(vvfirm)
6.31 d w_modn          s                   like(jvmodn)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.31  * les VA var
6.31 c                   eval      w_modn = 0
6.31 c                   eval      jvarl1_vare = p_nvar
6.31 c     jvarl1_key    chain     jvarl1
6.31 c                   if        %found(jvarl1)
6.31 c                   eval      w_modn = jvmodn
6.31 c                   endif
      *
      * Bytter varenummer i vareregister
      *
     c                   eval      vvarlu_vare = p_gvar
     c     vvarlu_key    chain     vvarlur                            90
     c                   if        *in90 = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      vvvare = p_nvar
6.31 c                   if        w_modn <> 0
6.31 c                   eval      vvnmod = w_modn
6.31 c                   endif
      *
6.10 c                   time                    vvedat
6.10 c                   time                    vvetim
6.10 c                   eval      vveusr = l_user
     c                   update    vvarlur
      *
      * Kaller program for bytting av varenummer i tabeller
      *
     c                   call      'VV740R'
     c                   parm                    p_firm
     c                   parm                    p_gvar
     c                   parm                    p_nvar
     c                   parm                    b_inlr
      *
      * Kaller program for bytting av varenummer i lager/innkjøp
      *
     c                   call      'FO740C'
     c                   parm                    p_firm
     c                   parm                    p_gvar
     c                   parm                    p_nvar
     c                   parm                    b_inlr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_gvar
     c                   parm                    p_nvar
      *
      * Key for oppslag på vareregister
      *
     c     vvarlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlu_vare
6.31  *
6.31  * les av VA vare
6.31  *
6.31 c     jvarl1_key    klist
6.31 c                   kfld                    jvarl1_vare
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
