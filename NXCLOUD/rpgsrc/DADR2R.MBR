     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * System  . . . . : ASBANK                                        *
      * Program . . . . : DADR2R                                        *
      * Beskrivelse . . : Utplukk til ADRA AccountMatch                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KC = F3  = Exit                                           *
      *       KE = F5  = Forny                                          *
      *       KL = F12 = Annuller                                       *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       10 = Roll                                                 *
      *       12 = Sfldsp                                               *
      *       13 = Sfldspctl                                            *
      *       14 = Sflclr                                               *
      *       15 = Sflend                                               *
      *       16 = Readc 16 on, no more records                         *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fdadtl1    uf a e           k disk
     fdadrl1    uf a e           k disk
     fdadr2d    cf   e             workstn
     f                                     sfile(b1sfl:srrn01)
      *
     d mld             s             11    dim(4) ctdata perrcd(1)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av felter                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      *     nn - er 01 for subfile 01,
      *             02 for subfile 02,
      *             03 for subfile 03, o.s.v.
      * SRRNnn - Relativt recordnummer i subfile
      * SPGEnn - Sidestørrelse i subfile (page)
      * SFRNnn - Første recordnummer på siden
      * SSRNnn - Siste  recordnummer på siden
      * SVRNnn - Vis den side som inneholder denne record.
      *
      * Forklaring på spesielle felter
      * som benyttes ved windows.
      * - - - - - - - - - - - - - - - - -
      *
      * WACTLI - Actual line, angir startlinje for vindu.
      * WACTPO - Actual posision, angir startposisjon for vindu
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for Kontonummer
      * C1WIN  - C1 Vindu for opprette/ endre/vise
      * K1WIN  - K1 Vindu for kopiere
      *
      * Henter opplysninger fra LDA:
      *
     d                uds
     d l_l1ko                         2
     d l_l1fi                         2
     d l_fgrp                931    933
     d l_firm                944    946
      *
     d sfrn01          s                   like(srrn01)
     d spge01          s              3  0
     d srrn01          s              4  0
     d ssrn01          s                   like(srrn01)
     d wactli          s              2  0
     d wactpo          s              3  0
     d w_arow          s              2  0
     d w_acol          s              3  0
     d wacxli          s              2  0
     d wacxpo          s              3  0
      *
     d p_fira          s              2
     d p_firm          s              3  0
      *
     d wpin03          s              1
     d w_flag          s              1
     d w_filg          s              3
     d w_firm          s              3  0
     d w_hbok          s              5  0
     d w_avde          s              4  0
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * B2CTL Behandle åpningsbildet med subfile                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   eval      w_arow = 2
     c                   eval      w_acol = 2
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      xdsp01
      *
      * Avslutt program
      *
     c     *inkc         cabeq     *on           xslutt
      *
      * Ny kobling, dvs HB-konto
      *
     c     *inkf         ifeq      *on
     c                   move      *off          *in34
     c                   exsr      xc1win
     c                   exsr      xnysub
     c                   goto      b2taga
     c                   endif
      *
      * Forny (Ikke i bruk, dvs tatt vekk F5 fra bildet)
      *
     c                   if        *inke = *on
     c                   if        sfrn01 <> *zero
     c     sfrn01        chain     b1sfl                              60
     c                   endif
     c                   move      *blanks       w_filg
     c     dadtpf_key    setll     dadtl1
     c                   exsr      xclr01
     c                   exsr      xcrt01
     c                   goto      b2tagb
     c                   endif
      *
      * Start utplukk:
      *
     c     *inkj         ifeq      *on
      *
     c                   eval      wpin03 = *blanks
     c                   call      'DADR1C'
     c                   parm                    wpin03
     c     wpin03        cabeq     '1'           b2taga
     c     wpin03        cabne     '1'           xslutt
     c                   endif
      *
      * Roll
      *
     c                   if        *in10 = *on
     c                   exsr      xcrt01
     c                   goto      b2tagb
     c                   endif
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Behandling av subfile                                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Sjekk om valg
      * er foretatt i
      * subfile
     c                   eval      w_flag = *blanks
      *
     c                   do        ssrn01
     c                   readc     b1sfl                                  16
     c                   if        *in16 = *on
     c                   leave
     c                   endif
      *
      * Velg post som det skal hentes kontoinformasjon for:
      *
     c                   if        b1valg = 1
      *
     c                   eval      w_filg = b1filg
     c                   eval      w_firm = b1firm
     c                   eval      w_hbok = b1hbok
     c                   eval      w_avde = b1avde
      *
     c                   exsr      xoppdat
     c                   iter
     c                   endif
      *
      * Slette post
      *
     c                   if        b1valg = 4
     c                   eval      w_flag = '1'
     c                   eval      w_filg = b1filg
     c                   eval      w_firm = b1firm
     c                   eval      w_hbok = b1hbok
     c                   eval      w_avde = b1avde
     c                   eval      c3filg = b1filg
     c                   eval      c3firm = b1firm
     c                   eval      c3hbok = b1hbok
     c                   eval      c3avde = b1avde
     c                   exsr      xc3win
     c                   iter
     c                   endif
      *
     c                   enddo
      *
      * Er det slettet poster fra subfile?
      *
     c     w_flag        ifeq      '1'
     c                   eval      w_flag = *blanks
     c                   exsr      xnysub
     c                   endif
      *
     c                   goto      b2taga
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * C2BLD Behandle bilde for NY                                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xc1win        begsr
      *
     c                   eval      c1filg = *blanks
     c                   eval      c1firm = *zeros
     c                   eval      c1hbok = *zeros
     c                   eval      c1avde = *zeros
      *
     c     x0001         tag
     c                   eval      wactpo = 26
     c                   eval      wactli = 11
     c                   exfmt     c1win
      *
     c     *inkc         cabeq     *on           c1tag1
      *
      * Tester på om gyldige verdier:
      *
     c                   move      *off          *in35
      *
     c                   if        c1filg = *blanks or
     c                             c1hbok = 0
     c                   eval      wactpo = 3
     c                   eval      wactli = 6
     c                   exfmt     c2win
     c                   goto      x0001
     c                   endif
      *
     c                   move      c1filg        w_filg
     c                   z-add     c1firm        w_firm
     c                   z-add     c1hbok        w_hbok
     c                   z-add     c1avde        w_avde
      *
      * Oppdatere file:
      *
     c     dadtpf_key    chain     dadtl1                             60
      *
     c                   if        *in60 = *on
     c                   move      w_filg        dzfilg
     c                   z-add     w_firm        dzfirm
     c                   z-add     w_hbok        dzhbok
     c                   z-add     w_avde        dzavde
     c                   write     dadtpfr
     c                   endif
      *
      * Fyller opp subfile med vindu for å vise koblinger:
      *
     c     c1tag1        tag
     c                   eval      w_filg = *blanks
     c                   eval      w_firm = *zeros
     c                   eval      w_hbok = *zeros
     c                   eval      w_avde = *zeros
      *
     c     dadtpf_key    setll     dadtl1
     c                   exsr      xclr01
     c                   exsr      xcrt01
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * C3WIN Behandle bilde for slette                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xc3win        begsr
      *
     c     dadtpf_key    chain     dadtl1                             60
      *
     c                   eval      wactpo = 26
     c                   eval      wactli = 11
      *
     c                   exfmt     c3win
      *
     c     *inkc         cabeq     *on           d1tag1
      *
     c     dadtpf_key    chain     dadtl1                             60
      *
     c                   if        *in60 = *off
     c                   delete    dadtpfr
     c                   endif
      *
     c     d1tag1        tag
      *
     c                   if        b1valg = 4
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Oppdater parameterfil med konto som skal plukkes:               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xoppdat       begsr
      *
     c     dadtpf_key    chain     dadrl1                             60
      *
      * Skrur av/på om posten er valgt eller ikke:
      *
     c                   if        b1avst = 'Valgt'
     c                   eval      b1avst = *blanks
     c                   else
     c                   eval      b1avst = 'Valgt'
     c                   endif
      *
     c                   if        *in60 = *on
     c                   move      b1filg        dyfilg
     c                   z-add     b1firm        dyfirm
     c                   z-add     b1hbok        dyhbok
     c                   z-add     b1avde        dyavde
     c                   write     dadrpfr
     c                   else
     c                   delete    dadrpfr
     c                   endif
      *
      * Oppdater subfile
      *
     c                   eval      b1valg = 0
     c                   update    b1sfl
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Tøm subfile                                                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xclr01        begsr
      *
     c                   move      *on           *in14
     c                   write     b2ctl
     c                   move      *off          *in14
      *
     c                   move      *off          *in15
     c                   eval      srrn01 = 0
     c                   eval      sfrn01 = 0
     c                   eval      ssrn01 = 0
     c                   eval      spge01 = 0
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Fyll opp subfile                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xnysub        begsr
     c                   exsr      xclear
     c                   exsr      xclr01
     c                   eval      w_filg = *blanks
     c                   eval      w_firm = *zeros
     c                   eval      w_hbok = *zeros
     c                   eval      w_avde = *zeros
     c     dadtpf_key    setll     dadtl1
     c                   exsr      xcrt01
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Fyll opp subfile                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xcrt01        begsr
     c                   eval      spge01 = spge01 + 10
     c                   eval      srrn01 = ssrn01
      *
     c                   dou       srrn01 = spge01
     c**   dadtpf_key    reade     dadtl1                                 15
     c                   read      dadtl1                                 15
     c     *in15         cabeq     *on           xend01
     c                   eval      srrn01 = srrn01 + 1
     c                   move      dzfilg        b1filg
     c                   move      dzfirm        b1firm
     c                   move      dzhbok        b1hbok
     c                   move      dzavde        b1avde
     c                   eval      b1avst = *blanks
     c                   write     b1sfl
     c                   enddo
      *
     c     xend01        tag
      *
     c                   if        srrn01 > ssrn01
     c                   eval      sfrn01 = ssrn01 + 1
     c                   endif
      *
     c                   eval      ssrn01 = srrn01
     c                   eval      svrn01 = sfrn01
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Display subfile                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xdsp01        begsr
      *
     c                   if        srrn01 <> 0
     c                   move      *on           *in12
     c                   endif
      *
     c                   move      *on           *in13
     c                   exfmt     b2ctl
     c                   move      *off          *in12
     c                   move      *off          *in13
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Tømmer parameterfile DADRPF                                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xclear        begsr
      *
     c                   eval      w_filg = *blanks
     c                   eval      w_firm = *zeros
     c                   eval      w_hbok = *zeros
     c                   eval      w_avde = *zeros
     c     dadtpf_key    setll     dadrl1
      *
     c                   read      dadrl1                                 60
     c                   dow       *in60 = *off
     c                   delete    dadrpfr
     c                   read      dadrl1                                 60
     c                   enddo
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   eval      spge01 = 0
     c                   eval      srrn01 = 0
     c                   eval      wactli = 0
     c                   eval      wactpo = 0
      *
     c                   eval      wacxli = 0
     c                   eval      wacxpo = 0
      *
     c                   eval      p_fira = *blanks
     c                   eval      p_firm = 0
      *
     c                   movel     l_fgrp        w_filg
     c                   movel     l_firm        w_firm
      *
      * Overfører Filgruppe og Firmanummer til felter:
      *
     c                   eval      w_filg = *blanks
     c                   eval      w_firm = *zeros
     c                   eval      w_hbok = *zeros
     c                   eval      w_avde = *zeros
      *
      * Key for les på dadtl1:
      *
     c     dadtpf_key    klist
     c                   kfld                    w_filg
     c                   kfld                    w_firm
     c                   kfld                    w_hbok
     c                   kfld                    w_avde
      *
      * Tømmer parameterfile:
      *
     c                   exsr      xclear
      *
      * Fyller opp subfile med vindu for å vise koblinger:
      *
     c     dadtpf_key    setll     dadtl1
     c                   exsr      xcrt01
      *
     c                   endsr
      *
**
  Ny post                  001
  Endring                  002
  Vise                     003
* Slettet *                004
