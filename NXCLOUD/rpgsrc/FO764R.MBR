     H/TITLE  ASOFAK: Oppdatering av ordre-linjer fra ordre-hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO764R
      * Beskrivelse . . : Oppdatering av ordre-linjer fra ordre-hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * 6.10  240609 BSA  Oppdatert med sporingsinformasjon
6.nu  * 6.30  240614 bof  Gjennomgått mtp. nummerutvidelse
      *
8.01  * 8.xx  290921 bsa  Sjekker om endring, da det blir unødvendig
8.01  * 8.xx              replikering pga at alt settes som endret.
8.02  * 8.xx  160622 bsa  Hvis ordrelinja ikke har tilhørende hode, hoppes
8.02  * 8.xx              den over.
8.03  * 8.xx  051022 bsa  Logger at jobben er kjørt og antall linjer som er
8.03  * 8.xx              oppdatert.
8.04  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     FFOHEL1    IF   E           K DISK
     F                                     RENAME(FOHEPFR:FOHEL1R)
      *
     FFODTLU    UF   E           K DISK
     F                                     RENAME(FODTPFR:FODTLUR)
     FFODTL1    IF   E           K DISK
     F                                     RENAME(FODTPFR:FODTL1R)
      *
8.01  * Definering av variable
      *
9.01 d w_kund          s              6  0
8.03 d w_anta          s              8  0
8.03 d w_kode          s              1
8.03 d w_firm          s              3  0
8.03 d w_fell          s              6
8.03 d w_type          s              2
8.03 d w_fast          s             10
8.03 d w_numm          s              8  0
8.03 d w_jnav          s             15
8.03 d w_jkey          s             41
8.03 d w_jstat         s              2  0
8.03 d w_jtext         s            200
8.03 d w_jtxt          s             35
      *
     D                UDS
      *
6.10 d l_user                911    920
     D  DSSFIR               944    946  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Oppdaterer ORDRE-LINJE-REGISTER med nye opplysninger           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     TAG02         TAG
      *
     C     LINKEY        SETLL     FODTL1R
      *
     C     TAG03         TAG
      *
     C                   SETOFF                                           90
     C                   READ      FODTL1R                                90
     C     *IN90         CABEQ     *ON           XSLUTT
     C     FDFIRM        CABNE     LINFIR        XSLUTT
      *
      *  Hent opplysninger fra ORDRE-HODE-REGISTER
      *
     C                   MOVE      FDNUMM        HEDNUM
     C                   MOVE      FDSUFF        HEDSUF
8.02 C     HEDKEY        CHAIN     FOHEL1R
8.02  * Hode må finnes
8.02 c                   if        %found
      *
      *  Oppdaterer ORDRE-LINJE-REGISTER med nye opplysninger
      *
     C                   MOVE      FDNUMM        LINNUM
     C                   MOVE      FDSUFF        LINSUF
     C                   MOVE      FDLINE        LINLIN
     C     LINKEY        CHAIN     FODTLUR                            94
      *
8.01 c                   if        %found
      *
8.01 c                   eval      w_kund = fokund
     C     FOLKUN        IFNE      *ZERO
8.01 c                   eval      w_kund = folkun
     C                   END
      *
8.01 c                   if        foavde <> fdavde or
8.01 c                             w_kund <> fdkund
      *
     C                   MOVE      FOAVDE        FDAVDE
8.01 c                   eval      fdkund = w_kund
8.03 c                   eval      w_anta = w_anta + 1
      *
6.10 c                   time                    fdedat
6.10 c                   time                    fdetim
6.10 c                   eval      fdeusr = l_user
     C                   UPDATE    FODTLUR
8.01 c                   else
8.01 c                   unlock    fodtlu
8.01 C                   endif
      *
8.01 C                   endif
      *
8.02 c                   endif
      *
     C                   GOTO      TAG03
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSLUTT        TAG
      *
8.03  * Kvittering
8.03 c                   eval      w_jstat = 10
8.03 c                   eval      w_jtext = 'Oppdatert ' + %char(w_anta) +
8.03 c                                       'ordrelinjer'
8.03  *
8.03 c                   call      'AB705R'
8.03 c                   parm                    w_jkey
8.03 c                   parm                    w_jstat
8.03 c                   parm                    w_jtext
8.03  * Avslutt
8.03 c                   call      'AB710R'
8.03 c                   parm                    w_jkey
      *
     C                   SETON                                        LR
     C                   RETURN
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     CSR   *INZSR        BEGSR
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
      *
      *  Redefinering av nøkler
      *
     C     *LIKE         DEFINE    FOFIRM        HEDFIR
     C     *LIKE         DEFINE    FONUMM        HEDNUM
     C     *LIKE         DEFINE    FOSUFF        HEDSUF
      *
     C     *LIKE         DEFINE    FDFIRM        LINFIR
     C     *LIKE         DEFINE    FDNUMM        LINNUM
     C     *LIKE         DEFINE    FDSUFF        LINSUF
     C     *LIKE         DEFINE    FDLINE        LINLIN
      *
      *  Key for file : ORDRE-HODE-REGISTER
      *
     C     HEDKEY        KLIST
     C                   KFLD                    HEDFIR
     C                   KFLD                    HEDNUM
     C                   KFLD                    HEDSUF
      *
      *  Key for file : ORDRE-LINJE-REGISTER
      *
     C     LINKEY        KLIST
     C                   KFLD                    LINFIR
     C                   KFLD                    LINNUM
     C                   KFLD                    LINSUF
     C                   KFLD                    LINLIN
      *
      *  Legger inn firmanummer i nøkklene
      *
     C                   MOVE      DSSFIR        HEDFIR
     C                   MOVE      DSSFIR        LINFIR
8.03 c                   eval      w_anta = 0
      *
8.03 c                   eval      w_kode = '0'
8.03 c                   eval      w_firm = dssfir
8.03 c                   eval      w_fell = 'NXTKLI'
8.03 c                   eval      w_type = *blank
8.03 c                   eval      w_fast = *blank
8.03 c                   eval      w_numm = *zero
      *
8.03 c                   call      'AS100R'
8.03 c                   parm                    w_kode
8.03 c                   parm                    w_firm
8.03 c                   parm                    w_fell
8.03 c                   parm                    w_type
8.03 c                   parm                    w_fast
8.03 c                   parm                    w_numm
8.03  *
8.03  * Vi logger at jobben har startet
8.03 c                   eval      w_jnav = 'LOGG' + %char(w_numm)
8.03 c                   eval      w_jtxt = 'FO764 oppdatering av ordrelinjer !'
8.03  *
8.03 c                   call      'AB700R'
8.03 c                   parm                    w_jnav
8.03 c                   parm                    w_jkey
8.03 c                   parm                    w_jtxt
8.03  * Trigg start
8.03 c                   eval      w_jstat = 10
8.03 c                   eval      w_jtext = 'Masseoppdatering av ordrelinjer !'
8.03  *
8.03 c                   call      'AB705R'
8.03 c                   parm                    w_jkey
8.03 c                   parm                    w_jstat
8.03 c                   parm                    w_jtext
      *
     CSR                 ENDSR
