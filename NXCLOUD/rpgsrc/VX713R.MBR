     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VE713R
      * Beskrivelse . . : Henter GTIN nr ut fra gitt varenr + enhet + leverandør
      *
      *                   Input : firma, leverandør, varenr., enhet
      *                   Output: GTIN nr  status (1 = ok)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       190112 bsa  Nytt program
6.10  * NEB   230512 bsa  Sjekker også opp uten leverandør, samt mot
6.10  *                   vareadministrasjon.
6.11  * NEB   050712 bsa  Må sjekke videre, hvis det er en VA vare
6.30  * 6.30  260113 bof  Omskriving i forb. endring pakning VA
6.31  * 6.30  100414 bof  Feilretting vedr. hente gtin fra va
6.32  * 6.30  090514 bkv  Feilretting vedr. hente gtin fra va
6.33  * 6.30  190514 bkv  Feilretting vedr. hente gtin fra va
9.01  *       090418 bsa  Innfører manuell lukking av filer
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarlr    if   e           k disk    rename(vvarpfr:vvarlrr)
     fvveplr    if   e           k disk    rename(vveppfr:vveplrr)
6.10 fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
6.10 fjvpkl1    if   e           k disk    rename(jvpkpfr:jvpkl1r)
6.30 fjlevl3    if   e           k disk    rename(jlevpfr:jlevl3r)
      *
      * Definering av variable i nøkler
      *
     d vvarlr_firm     s                   like(vvfirm)
     d vvarlr_vare     s                   like(vvvare)
     d vveplr_pfir     s                   like(vepfir)
     d vveplr_pvar     s                   like(vepvar)
     d vveplr_penh     s                   like(vepenh)
     d vveplr_pldo     s                   like(vepldo)
6.10 d vvepl1_pfir     s                   like(vepfir)
6.10 d vvepl1_pvar     s                   like(vepvar)
6.10 d vvepl1_penh     s                   like(vepenh)
6.10 d jvpkl1_vare     s                   like(jwvare)
6.30 d jvpkl1_ldor     s                   like(jwldor)
6.11 d jvarl1_vare     s                   like(jvvare)
6.30 d jlevl3_enum     s                   like(jlenum)
      *
      * Definering av pamametre
      *
     d p_firm          s                   like(vepfir)
     d p_eann          s                   like(vepgti)
     d p_vare          s                   like(vepvar)
     d p_ldor          s                   like(vvldor)
     d p_enhe          s                   like(vepenh)
     d p_stat          s              1
9.01 d p_inlr          s              1
      *
6.33 d w_ldor          s                   like(vvldor)
      *
     d                uds
     d l_user                911    920
     d l_sfir                944    946  0
     d l_savd                947    950  0
     d l_lagr               1001   1002  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
9.01  * "Ekstrarunde" for å stenge åpne filer?
9.01  *
9.01 c                   if        p_inlr = *on
9.01 c                   goto      avslutt
9.01 c                   endif
      *
      * Initierer output-parametre
      *
     c                   eval      p_eann = 0
     c                   eval      p_stat = *blank
      *
     c                   eval      vvarlr_firm = p_firm
     c                   eval      vvarlr_vare = p_vare
     c     vvarlr_key    chain     vvarlr
6.10 c                   if        not %found(vvarlr)
6.11 c                   goto      sjk_VA
     c                   endif
      *
6.10 c                   if        p_ldor <> 0
      * Henter GTIN kode fra VVEP på angitt enhet og leverandør
      *
     c                   eval      vveplr_pfir = p_firm
     c                   eval      vveplr_pvar = p_vare
     c                   eval      vveplr_penh = p_enhe
     c                   eval      vveplr_pldo = p_ldor
     c     vveplr_key    chain     vveplr
     c                   if        %found(vveplr) and
     c                             vepgti <> 0
     c                   eval      p_eann = vepgti
     c                   eval      p_stat = '1'
     c                   goto      avslutt
     c                   endif
6.10 c                   endif
6.10  *
6.10  * Sjekk en gang til på VVEP uavhengig av leverandør, men med angitt enhet
6.10  *
6.10 c                   eval      vvepl1_pfir = p_firm
6.10 c                   eval      vvepl1_pvar = p_vare
6.10 c                   eval      vvepl1_penh = p_enhe
6.10 c     vvepl1_key    setll     vveplr
6.10 c     vvepl1_key    reade     vveplr                                 91
6.10 c                   dow       *in91 = *off
6.10  * Har vi match på leverandør ?
6.10 c                   if        p_enhe = vepenh and
6.10 c                             vepgti <> 0
6.10 c                   eval      p_eann = vepgti
6.10 c                   eval      p_stat = '1'
6.10 c                   goto      avslutt
6.10 c                   endif
6.10 c     vvepl1_key    reade     vveplr                                 91
6.10 c                   enddo
6.11  *
6.11 c                   goto      sjk_gtinVA
6.11  *
6.11  * -----------------------------------------
6.11  * VA håndtering                           *
6.11  * -----------------------------------------
6.11  *
6.11 c     sjk_VA        tag
6.11  *
6.11 c                   eval      jvarl1_vare = p_vare
6.11 c     jvarl1_key    chain     jvarl1
6.11 c                   if        not %found(jvarl1)
6.11 c                   goto      avslutt
6.11 c                   endif
6.10  *
6.11 c     sjk_gtinVA    tag
6.10  *
6.10  * Hvis ikke funnet - sjekk mot vareadministrasjon.
6.10  * Først forutsetter vi match på leverandør
6.10  *
6.30 c                   eval      jvpkl1_vare = p_vare
6.33 c                   eval      jvpkl1_ldor = w_ldor
6.30 c     jvpkl1_key    setll     jvpkl1
6.30 c     jvpkl1_key    reade     jvpkl1
6.30 c                   dow       not %eof(jvpkl1)
6.33 c                   if        jwldor = w_ldor and
6.30 c                             jwenhe = p_enhe and
6.30 c                             jwgtin <> 0
6.30  * Har vi match på enhet, hent enheten fra
6.30 c                   eval      p_eann = jwgtin
6.30 c                   eval      p_stat = '1'
6.30 c                   goto      avslutt
6.30 c                   endif
6.30 c     jvpkl1_key    reade     jvpkl1
6.30 c                   enddo
6.10  *
6.10  * Prøver til slutt uten krav om leverandør
6.10  *
6.30 c                   eval      jvpkl1_vare = p_vare
6.32 c     jvpkl1_key2   setll     jvpkl1
6.32 c     jvpkl1_key2   reade     jvpkl1
6.30 c                   dow       not %eof(jvpkl1)
6.30 c                   if        jwgtin <> 0   and
6.30 c                             jwenhe = p_enhe
6.30 c                   eval      p_eann = jwgtin
6.30 c                   eval      p_stat = '1'
6.30 c                   goto      avslutt
6.30 c                   endif
6.32 c     jvpkl1_key2   reade     jvpkl1
6.30 c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
9.01 c                   if        p_inlr = *on
     c                   eval      *inlr = *on
9.01 c                   endif
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
     c                   parm                    p_vare
     c                   parm                    p_enhe
     c                   parm                    p_eann
     c                   parm                    p_stat
9.01 c                   parm                    p_inlr
      *
      * Key for les på vareenhets-register
      *
     c     vveplr_key    klist
     c                   kfld                    vveplr_pfir
     c                   kfld                    vveplr_pvar
     c                   kfld                    vveplr_penh
     c                   kfld                    vveplr_pldo
      *
      * Key for les på vareregister
      *
     c     vvarlr_key    klist
     c                   kfld                    vvarlr_firm
     c                   kfld                    vvarlr_vare
6.10  *
6.10  * Key for les på VVEP-register
6.10  *
6.10 c     vvepl1_key    klist
6.10 c                   kfld                    vvepl1_pfir
6.10 c                   kfld                    vvepl1_pvar
6.10 c                   kfld                    vvepl1_penh
6.10  *
6.10  * Key for les på JVPK-register
6.10  *
6.10 c     jvpkl1_key    klist
6.10 c                   kfld                    jvpkl1_vare
6.30 c                   kfld                    jvpkl1_ldor
6.32  * Key for les på JVPK-register
6.32  *
6.32 c     jvpkl1_key2   klist
6.32 c                   kfld                    jvpkl1_vare
6.11  *
6.11  * Key for les på JVAR-register
6.11  *
6.11 c     jvarl1_key    klist
6.11 c                   kfld                    jvarl1_vare
6.30  *
6.30  * Key for les på leverandør i VA
6.30  *
6.30 c     jlevl3_key    klist
6.30 c                   kfld                    jlevl3_enum
6.30  *
6.30  * leser leverandør i VA
6.33 c                   eval      w_ldor = p_ldor
6.30 c                   eval      jlevl3_enum = p_ldor
6.30 c     jlevl3_key    chain     jlevl3
6.32 c                   if        %found
6.33 c                   eval      w_ldor = jlldor
6.32 c                   endif
      *
     c                   endsr
6.30  *
