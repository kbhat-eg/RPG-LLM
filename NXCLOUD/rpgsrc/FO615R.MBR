     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO614RMG
      * Beskrivelse . . : Henter inn tidligere fakturert fra SODTPF v/
      *                     Sluttfakturering av prosjekt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * 6.30  061218 sst  Nytt program
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffodtlu    uf a e           k disk    rename(fodtpfr:fodtlur)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     fsohelr    if   e           k disk    rename(sohepfr:sohelrr)
     fsodtpf    if   e           k disk    rename(sodtpfr:sodtpfr)
      *
      * Definering av data-struktur
      * for sorteringsbegrep
      *
     d                 ds
     d sorter                       114
     d  sor001                        8  0 overlay(sorter)
     d  sor002                       60    overlay(sorter:9)
     d  sor003                       16    overlay(sorter:69)
     d  sor004                        3    overlay(sorter:85)
     d  sor005                       17    overlay(sorter:88)
     d  sor006                        8  0 overlay(sorter:105)
     d  sor007                        2  0 overlay(sorter:113)
      *
     d                 ds
     d d_urec                        80
     d  d_ufir                        3  0 overlay(d_urec)
     d  d_aarr                        4  0 overlay(d_urec:4)
     d  d_unum                        8  0 overlay(d_urec:8)
     d  d_usuf                        2  0 overlay(d_urec:16)
     d  d_ukod                        1    overlay(d_urec:18)
     d  d_date                         d   overlay(d_urec:19) datfmt(*iso)
     d  d_time                         t   overlay(d_urec:29) timfmt(*iso)
     d  d_user                       10    overlay(d_urec:37)
     d  d_wsid                       10    overlay(d_urec:47)
      *
      *
      * Definering av local data area
      *
     d                uds
      *
     d l_user                911    920
     d l_filg                931    933
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d sohelr_aarr     s                   like(soaarr)
     d sohelr_binr     s                   like(sobinr)
     d sohelr_numm     s                   like(sonumm)
     d sohelr_suff     s                   like(sosuff)
      *
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d fodtl1_line     s                   like(fdline)
      *
      * Definering av variable
      *
     d w_kode          s              1
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
     d w_firm          s                   like(sdfirm)
     d wfd             s             10
     d w_line          s                   like(fdline)
      *
     d w_bnum          s              6
      *
     d p_numm          s                   like(fdnumm)
     d p_suff          s                   like(fdsuff)
     d p_line          s                   like(fdline)
     d p_proj          s                   like(soproj)
     d p_upro          s                   like(soupro)
     d p_akti          s                   like(soakti)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av utplukk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *   Sjekk om det finnes tidligere fakturaer på dette prosjekt
      *
     c/exec sql
     c+                  declare sok_s cursor for
     c+                  select sdfirm,
     c+                         sdaarr,
     c+                         sdbinr,
     c+                         sdnumm,
     c+                         sdsuff,
     c+                         sdline,
     c+                         sdvare,
     c+                         sdproj,
     c+                         sdtek1,
     c+                         sdanta,
     c+                         sdsalg
     c+                  from   sodtpf
     c+                     where sdfirm = :l_firm and
     c+                           sdproj = :p_proj
      *
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_s
     c/end-exec
     c/exec sql
     c+                  open sok_s
     c/end-exec
      *
     c     fetch_fakt    tag
     c/exec sql
     c+                  fetch next
     c+                  from  sok_s
     c+                  into  :sdfirm,
     c+                        :sdaarr,
     c+                        :sdbinr,
     c+                        :sdnumm,
     c+                        :sdsuff,
     c+                        :sdline,
     c+                        :sdvare,
     c+                        :sdproj,
     c+                        :sdtek1,
     c+                        :sdanta,
     c+                        :sdsalg
     c/end-exec
      *
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                             or sdfirm <> l_firm
     c                             or sdproj <> p_proj
     c                   goto      end
     c                   endif
      *
     c                   if            sdnumm = p_numm
     c                             and sdsuff = p_suff
     c**                 goto      end
     c                   endif
      *
      *  Sjekk om denne linja er en akonto faktura
     c                   if            %subst(sdtek1:1:6) <> 'Akonto'
     c                             and %subst(sdtek1:1:6) <> 'AKONTO'
     c                             and %subst(sdtek1:1:6) <> 'A-KONT'
     c                             and %subst(sdtek1:1:6) <> 'A-Kont'
     c                   goto      fetch_fakt
     c                   endif
      *  Sjekk om denne ordre hører til dette prosjektet
     c                   eval      sohelr_aarr = sdaarr
     c                   eval      sohelr_binr = sdbinr
     c                   eval      sohelr_numm = sdnumm
     c                   eval      sohelr_suff = sdsuff
     c     sohelr_key    chain     sohelr
     c                   if        not %found(sohelr)
     c                             or  soproj <> p_proj
     c                             or  soupro <> p_upro
     c                             or  soakti <> p_akti
     c                   goto      fetch_fakt
     c                   endif
      *  Finn siste linje på ordren
     c                   eval      fodtl1_numm = p_numm
     c                   eval      fodtl1_suff = p_suff
     c                   eval      fodtl1_line = p_line
     c     fodtl1_key    chain     fodtl1
     c                   if        %found(fodtl1)
      *  Hopp over et linjenr første gangen for plass til '-Tidl.fakt'
     c                   if        p_line = w_line
     c                   eval      p_line =p_line + 40
     c                   else
     c                   eval      p_line = p_line + 20
     c                   endif
     c                   eval      fdline = p_line
     c                   move      sofkda        wfd
     c                   eval      fdtek1 = 'Akonto ' +
     c                                      %trim(%char(sobinr)) +
     c                                      ' pr. ' +
     c                                      %subst(wfd:9:2) + '.' +
     c                                      %subst(wfd:6:2) + '.' +
     c                                      %subst(wfd:1:4)
     c                   eval      fdanta = sdanta * -1
     c                   eval      fdsapr = sdsalg
     c                   write     fodtlur
     c                   goto      fetch_fakt
     c                   endif
      *
      * Leser record fra UTPLUKK-PARAM-REGISTER
      *
      *
     c                   time                    fdodat
     c                   time                    fdotim
     c                   eval      fdoous = l_user
     c                   time                    fdedat
     c                   time                    fdetim
     c                   eval      fdeusr = l_user
     c                   write     fodtlur
      *
     c     end           tag
      *  Har det blitt funnet tidlierer fakturert
     c                   if        p_line <> w_line
      *  Legg ut '- Tidligere Fakturert'
     c                   clear                   fodtlur
     c                   eval      fdnumm = p_numm
     c                   eval      fdsuff = p_suff
     c                   eval      fdline = w_line + 20
     c                   eval      fdotyp = sootyp
     c                   eval      fdltyp = 'TX'
     c                   eval      fdlage = solage
     c                   eval      fdkund = sokund
     c                   eval      fdtek1 = '- Tidligere Fakturert'
     c                   time                    fdodat
     c                   time                    fdotim
     c                   eval      fdoous = l_user
     c                   time                    fdedat
     c                   time                    fdetim
     c                   eval      fdeusr = l_user
     c                   write     fodtlur
     c                   endif
      *  Legg ut 'S L U T T F A K T U R A'
     c                   eval      fdline = 19
     c                   eval      fdtek1 = 'S L U T T F A K T U R A'
     c                   write     fodtlur
      *
     c                   eval      *inlr =  *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
6.NU c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_line
     c                   parm                    p_proj
     c                   parm                    p_upro
     c                   parm                    p_akti
      *
      * Key for file : ORDRE-Hode
      *
     c     sohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sohelr_aarr
     c                   kfld                    sohelr_binr
     c                   kfld                    sohelr_numm
     c                   kfld                    sohelr_suff
      *
      * Key for file : ORDRE-LINJE-REGISTER
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
     c                   kfld                    fodtl1_line
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_line = p_line
      *
     c                   endsr
