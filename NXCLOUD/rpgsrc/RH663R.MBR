     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RH663R                                              *
      * Beskrivelse . . : Hovedbok - mva-oppgave på nytt, utskrift            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      *  V5.40    110806  Nytt program                                   HFL  *
      *  V5.50    170807  Beh.kode 10 med i omsetn.oppgave linje 1.      HFL  *
6.14  *  R6.10    050612  Bare compliert pga. justeringer i printfilen   AMD  *
6.30  *  R6.10    040719  Utvidet saldofelter                            bsa  *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                                                       *
      *  BRUK AV INDIKATORER                                                  *
      *                                                                       *
      *       KA = F1  = Valg av firma                                        *
      *       KC = F3  = Exit                                                 *
      *                                                                       *
      *       LR = Avslutt program                                            *
      *       30 = Protect av felter ved vise                                 *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer                   *
      *    60-69 = Fileindikatorer chain etc.                                 *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                           *
      *    80-89 = Arbeidsindikatorer ordinære                                *
      *    90-99 = Benyttes ved std. sub-rutiner                              *
      *                                                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frhtrl6    if   e           k disk    rename(rhtrpfr:rhtrl6r)
     frhsal1    if   e           k disk    rename(rhsapfr:rhsal1r)
     frhovlr    if   e           k disk    rename(rhovpfr:rhovlrr)
     fra04l1    if   e           k disk    rename(ra04pfr:ra04l1r)
     fra05lr    if   e           k disk    rename(ra05pfr:ra05lrr)
E5.40fraa4l1    if   e           k disk    rename(raa4pfr:raa4l1r)
     frh663p    o    e             printer oflind(*in30)
      *
     d mk              s              1    dim(99)
     d bto             s             13  2 dim(99)
     d nto             s             13  2 dim(99)
     d grl             s             13  2 dim(99)
     d imv             s             11  2 dim(99)
     d imu             s             11  2 dim(99)
     d umv             s             11  2 dim(99)
6.30 d sal             s             15  2 dim(14)
      *
     d rhtrlu_raar     s                   like(rbraar)
     d rhtrlu_kont     s                   like(rbkont)
     d rhtrlu_spes     s                   like(rbspes)
     d rhtrlu_avde     s                   like(rbavde)
     d rhtrlu_rper     s                   like(rbrper)
     d rhtrlu_bdat     s                   like(rbbdat)
     d rhtrlu_tist     s                   like(rbtist)
      *
     d rhsal1_kont     s                   like(rikont)
     d rhsal1_spes     s                   like(rispes)
     d rhsal1_avde     s                   like(riavde)
     d rhsal1_raar     s                   like(riraar)
     d rhsal1_type     s                   like(ritype)
      *
     d rhovlr_kont     s                   like(rhkont)
     d rhovlr_spes     s                   like(rhspes)
     d rhovlr_avde     s                   like(rhavde)
      *
     d raa4l1_4aar     s                   like(ra4aar)
     d ra05lr_ekod     s                   like(raekod)
     d rhtrl6_raar     s                   like(rbraar)
      *
     d w_edat          s               d   datfmt(*iso)
      *
     d                 ds
     d w_dato                         6  0
     d  w_dag                         2  0 overlay(w_dato)
     d  w_mnd                         2  0 overlay(w_dato:3)
     d  w_aar                         2  0 overlay(w_dato:5)
      *
     d w_udat          s              6  0
      *
      * Local data area
      *
     d                uds
     d rhpkda                300    305  0
     d rhprÅr                         4  0
     d rhpper                         2  0
     d rhpmva                         1  0
     d rhpmv0                         1
     d rhptst                         1
     d rhpost                         1
     d rhpsps                         1
      *
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d g_mvak          s                   like(rbmvak)
     d wpavde          s                   like(rbavde)
     d wpfirm          s                   like(rbfirm)
     d wpkont          s                   like(rbkont)
     d wpspes          s                   like(rbspes)
     d w_firm          s                   like(l_firm)
     d w_stat          s              1
     d w_akod          s              2  0
     d w_atxt          s             20
      *
     d amva            s              1
     d a01txt          s             20
     d brto            s             13  2
     d mvag            s             13  2
     d iavg            s             11  2
     d imva            s             11  2
      *
     d recnr1          s              5  0
     d tsal            s             13  2
     d umva            s             11  2
      *
     d oms01g          s             11  0
     d oms02g          s             11  0
     d oms03g          s             11  0
     d oms04g          s             11  0
     d oms04a          s             11  0
     d oms05g          s             11  0
     d oms05a          s             11  0
     d oms06g          s             11  0
     d oms06a          s             11  0
     d oms07g          s             11  0
     d oms07a          s             11  0
     d oms08a          s             11  0
     d oms09a          s             11  0
     d oms10a          s             11  0
     d oms11a          s             11  0
      *
     d wmva01g         s             11  2
     d wmva02g         s             11  2
     d wmva03g         s             11  2
     d wmva04g         s             11  2
     d wmva04a         s             11  2
     d wmva05g         s             11  2
     d wmva05a         s             11  2
     d wmva06g         s             11  2
     d wmva06a         s             11  2
     d wmva07g         s             11  2
     d wmva07a         s             11  2
     d wmva08a         s             11  2
     d wmva09a         s             11  2
     d wmva10a         s             11  2
     d wmva11a         s             11  2
      *
     d wptex1          s             30
     d wptex2          s             30
     d wsbel           s             13  2
     d wsfylk          s             11  2
     d wsimva          s             11  2
     d wsimvu          s             11  2
E5.40d wsksal          s             11  0
     d wsnett          s             13  2
     d wsumva          s             11  2
6.NU d ww4sgb          s              8  0
     d x               s              2  0
     d xmva            s                   like(raebmk)
      *
      /eject
      *
     irhsal1r
      * Saldobalanse
      *
     i              risa01                      sal(01)
     i              risa02                      sal(02)
     i              risa03                      sal(03)
     i              risa04                      sal(04)
     i              risa05                      sal(05)
     i              risa06                      sal(06)
     i              risa07                      sal(07)
     i              risa08                      sal(08)
     i              risa09                      sal(09)
     i              risa10                      sal(10)
     i              risa11                      sal(11)
     i              risa12                      sal(12)
     i              risa13                      sal(13)
     i              risa00                      sal(14)
      *
      /eject
     c     dummy         tag
      *
      * Sett starverdi
      *
     c                   eval      rhtrl6_raar = rhprÅr
     c     rhtrl6_key    setll     rhtrl6
     c     rhtrl6_key    reade     rhtrl6                                 60
      *
     c                   dow       *in60 = *off
      *
      * Sett mva-kode = 0 hvis blank
      *
     c                   if        rbmvak = ' '
     c                   eval      rbmvak = '0'
     c                   endif
      *
      * Brudd på MVA-kode
      *
     c                   if        rbmvak <> g_mvak
     c                   exsr      brudd
     c                   move      rbmvak        g_mvak
     c                   endif
      *
 E5.4 * Kun posteringer med denne mva-periode
 E5.4 * blir behandlet videre
      *
T5.40c                   if        rbmvak = '9' and
T5.40c                             rbautk = '2'
T5.40c                   goto      neste
T5.40c                   endif
      *
     c                   if        rbrper <= rhpper and
     c                             rbmvap  = rhpmva
     c                   exsr      beh_post
     c                   endif
      *
T5.40c     neste         tag
     c     rhtrl6_key    reade     rhtrl6                                 60
     c                   enddo
      *
      /space 3
      *
     c     slutt         tag
      *
     c                   exsr      mvatot
      *
     c                   seton                                        lr
      *
      /eject
      *
      * Subrutine for behandling av posteringer
      *
     c     beh_post      begsr
      *
      * Bilagsdato
      *
     c     *dmy          move      rbbdat        w_udat
      *
      * Nullstill beløpsfelt
      *
     c                   eval      brto = *zeros
     c                   eval      mvag = *zeros
     c                   eval      imva = *zeros
     c                   eval      umva = *zeros
      *
      * Beregn brutto-beløp
      *
     c                   eval      brto = rbmvag + rbmvab + rbinvb
      *
T     * Koder uten grunnlag, f.eks. 0 og 4
      *
T    c                   if        brto = *zero
T    c                   eval      brto = rbneto
T    c                   endif
      *
     c                   eval      bto(x) = bto(x) + brto
T    c                   eval      nto(x) = nto(x) + rbneto
      *
      /eject
      *
      * Beregn mva-grunnlag
      *
     c                   eval      mvag = mvag + rbmvag
     c                   eval      grl(x) = grl(x) + rbmvag
      *
      * Inngående MVA (Høy sats)
      *
     c                   if        xmva = 1 or
     c                             xmva = 12
     c                   eval      imva = imva + rbmvab
     c                   eval      imv(x) = imv(x) + rbmvab
     c                   eval      wmva08a = wmva08a + rbmvab
     c                   endif
      *
      * Inngående MVA (Utgår ????)
      *
     c                   if        xmva = 2
     c                   eval      imva = imva + rbmvab
     c                   eval      imv(x) = imv(x) + rbmvab
     c                   eval      wmva08a = wmva08a + rbmvab
     c                   endif
      *
      * Inngående MVA (Middels sats)
      *
     c                   if        xmva = 11
     c                   eval      imva = imva + rbmvab
     c                   eval      imv(x) = imv(x) + rbmvab
     c                   eval      wmva09a = wmva09a + rbmvab
     c                   endif
      *
      * Inngående MVA (Lav sats)
      *
     c                   if        xmva = 21
     c                   eval      imva = imva + rbmvab
     c                   eval      imv(x) = imv(x) + rbmvab
     c                   eval      wmva10a = wmva10a + rbmvab
     c                   endif
      *
      * Inngående avgift på tjenester kjøpt fra utlandet
      *
     c                   if        xmva = 12
     c                   eval      imu(x) = imu(x) + rbmvab
     c                   eval      wmva07a = wmva07a + rbmvab
     c                   eval      wmva07g = wmva07g + rbmvag
     c                   endif
      *
      * Utenfor MVA området
      *
     c                   if        xmva = 10
E5.50c                   eval      wmva01g = wmva01g - rbneto
     c                   endif
      *
      * Utgående MVA (Høy sats)
      *
     c                   if        xmva = 3
     c                   eval      umva = umva + rbmvab
     c                   eval      umv(x) = umv(x) + rbmvab
     c                   eval      wmva01g = wmva01g - rbmvag
     c                   eval      wmva02g = wmva02g - rbmvag
     c                   eval      wmva04g = wmva04g - rbmvag
     c                   eval      wmva04a = wmva04a - rbmvab
     c                   endif
      *
      * Utgående MVA (Middels sats)
      *
     c                   if        xmva = 13
     c                   eval      umva = umva + rbmvab
     c                   eval      umv(x) = umv(x) + rbmvab
     c                   eval      wmva01g = wmva01g - rbmvag
     c                   eval      wmva02g = wmva02g - rbmvag
     c                   eval      wmva05g = wmva05g - rbmvag
     c                   eval      wmva05a = wmva05a - rbmvab
     c                   endif
      *
      * Utgående MVA (Lav sats)
      *
     c                   if        xmva = 23
     c                   eval      umva = umva + rbmvab
     c                   eval      umv(x) = umv(x) + rbmvab
     c                   eval      wmva01g = wmva01g - rbmvag
     c                   eval      wmva02g = wmva02g - rbmvag
     c                   eval      wmva06g = wmva06g - rbmvag
     c                   eval      wmva06a = wmva06a - rbmvab
     c                   endif
      *
      * Avgiftsfritt
      *
     c                   if        xmva = 4
T    c                   eval      wmva01g = wmva01g - rbneto
T    c                   eval      wmva02g = wmva02g - rbneto
T    c                   eval      wmva03g = wmva03g + rbneto
     c                   endif
      *
      * Grunnlag utgående MVA  - høy sats
      *
     c                   if        xmva = 5
     c                   eval      wmva01g = wmva01g - rbmvag
     c                   eval      wmva02g = wmva02g - rbmvag
     c                   eval      wmva04g = wmva04g - rbmvag
     c                   endif
      *
      * Grunnlag utgående MVA  - middels sats
      *
     c                   if        xmva = 15
     c                   eval      wmva01g = wmva01g - rbmvag
     c                   eval      wmva02g = wmva02g - rbmvag
     c                   eval      wmva05g = wmva05g - rbmvag
     c                   endif
      *
      * Grunnlag utgående MVA  - lav sats
      *
     c                   if        xmva = 25
     c                   eval      wmva01g = wmva01g - rbmvag
     c                   eval      wmva02g = wmva02g - rbmvag
     c                   eval      wmva06g = wmva06g - rbmvag
     c                   endif
      /eject
      *
      * Direkte postering - høy sats
      *
     c                   if        xmva = 9
      *
      * Inngående MVA
      *
     c                   if        rbkont = radk02
     c                   eval      imva = imva + rbneto
     c                   eval      imv(x) = imv(x) + rbneto
     c                   eval      wmva08a = wmva08a + rbneto
     c                   endif
      *
      * Utgående MVA
      *
     c                   if        rbkont = radk01
     c                   eval      umva = umva + rbneto
     c                   eval      umv(x) = umv(x) + rbneto
     c                   eval      wmva04a = wmva04a - rbneto
     c                   endif
      *
     c                   endif
      *
      * Direkte postering - middels sats
      *
     c                   if        xmva = 19
      *
      * Inngående MVA
      *
     c                   if        rbkont = radk02
     c                   eval      imva = imva + rbneto
     c                   eval      imv(x) = imv(x) + rbneto
     c                   eval      wmva09a = wmva09a + rbneto
     c                   endif
      *
      * Utgående MVA
      *
     c                   if        rbkont = radk01
     c                   eval      umva = umva + rbneto
     c                   eval      umv(x) = umv(x) + rbneto
     c                   eval      wmva05a = wmva05a - rbneto
     c                   endif
      *
     c                   endif
      *
      * Direkte postering - lav sats
      *
     c                   if        xmva = 29
      *
      * Inngående MVA
      *
     c                   if        rbkont = radk02
     c                   eval      imva = imva + rbneto
     c                   eval      imv(x) = imv(x) + rbneto
     c                   eval      wmva10a = wmva10a + rbneto
     c                   endif
      *
      * Utgående MVA
      *
     c                   if        rbkont = radk01
     c                   eval      umva = umva + rbneto
     c                   eval      umv(x) = umv(x) + rbneto
     c                   eval      wmva06a = wmva06a - rbneto
     c                   endif
      *
     c                   endif
      /eject
      *
      * Skriv heading
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   eval      *in30 = *off
     c                   endif
      *
      * Skriv postering
      *
     c                   if        rhpmv0 = 'J'
     c                   write     d1det
     c                   else
     c                   if        rhpsps = 'N' and
     c                             xmva > *zero
     c                   write     d1det
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for behandling av brudd på mva-kode                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     brudd         begsr
      *
     c                   if        rbmvak = ' '
     c                   move      '0'           amva
     c                   else
     c                   move      rbmvak        amva
     c                   endif
      *
     c                   eval      x = x + 1
      *
     c                   move      amva          mk(x)
      *
      * Hent behandlingskode fra MVA-kode i koderegister
      *
     c                   move      amva          ra05lr_ekod
     c     ra05lr_key    chain     ra05lr                             61
      *
     c                   move      raebmk        xmva
     c                   eval      w_edat = raemda
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for utskrift av fordelingsskjema og mva-oppgave       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     mvatot        begsr
      *
     c                   write     t1hdr
      *
     c     1             do        98            x
      *
     c                   eval      wsbel = wsbel + bto(x)
     c                   eval      wsmvag = wsmvag + grl(x)
     c                   eval      wsimva = wsimva + imv(x)
     c                   eval      wsimvu = wsimvu + imu(x)
     c                   eval      wsumva = wsumva + umv(x)
     c                   eval      wsnett = wsnett + nto(x)
      *
     c                   if        nto(x) <> *zero or
     c                             grl(x) <> *zero or
     c                             bto(x) <> *zero or
     c                             imv(x) <> *zero or
V    c                             imu(x) <> *zero or
     c                             umv(x) <> *zero
     c                   eval      t1mk  = mk(x)
     c                   eval      t1bto = bto(x)
     c                   eval      t1grl = grl(x)
     c                   eval      t1imv = imv(x)
     c                   eval      t1umv = umv(x) + imu(x)
     c                   eval      t1nto = nto(x)
      *
     c                   write     t1det
     c                   endif
      *
     c                   enddo
      *
      /eject
      *
     c                   write     t1tot
      *
      * Hent saldo på fylkesskattesjefens konto
      *
     c                   move      l_firm        rifirm
     c                   move      radk04        rhsal1_kont
     c                   move      rads04        rhsal1_spes
     c                   move      rada04        rhsal1_avde
     c                   move      rhprÅr        rhsal1_raar
     c                   move      1             rhsal1_type
      *
     c     rhsal1_key    chain     rhsal1                             61
     c                   if        *in61 = *off
      *
      * Beregn saldo denne periode
      *
     c                   eval      tsal = sal(14)
      *
     c     1             do        rhpper        x
     c                   eval      tsal = tsal + sal(x)
     c                   enddo
      *
     c                   else
     c                   eval      tsal = *zero
     c                   endif
      *
      * Summer periodens beløp
      * til tidligere saldo på konto
      *
     c                   eval      wsfylk = wsumva + wsimva - wsimvu
      *
E5.40c                   eval(h)   wsksal = tsal - wsfylk
      *
      * Beløp avrundes til hele kroner
      *
     c                   eval(h)   oms01g = wmva01g
     c                   eval(h)   oms02g = wmva02g
      *
     c                   eval(h)   oms03g = 0 - wmva03g
      *
     c                   eval(h)   oms04a = wmva04a
     c                   eval(h)   oms04g = wmva04g
      *
     c                   eval(h)   oms05a = wmva05a
     c                   eval(h)   oms05g = wmva05g
      *
     c                   eval(h)   oms06a = wmva06a
     c                   eval(h)   oms06g = wmva06g
      *
     c                   eval(h)   oms07g = wmva07g
     c                   eval(h)   oms07a = wmva07a
      *
     c                   eval(h)   oms08a = wmva08a
      *
     c                   eval(h)   oms09a = wmva09a
      *
     c                   eval(h)   oms10a = wmva10a
      *
     c                   eval      oms11a = oms04a + oms05a + oms06a
     c                                    + oms07a - oms08a - oms10a
T    c                                    - oms09a
      *
     c                   if        oms11a > *zero
     c                   move      *on           *in80
     c                   else
     c                   move      *off          *in80
     c                   endif
      *
T5.40 * Korrigerer saldo fylkesskattesjefens konto
T5.40 * med avgift å betale/tilgode
      *
T5.40c                   eval      wsksal = wsksal + oms11a
      *
      * Hent Fylkesskattesjefens konto
      *
     c                   move      radk04        rhovlr_kont
     c                   move      rads04        rhovlr_spes
     c                   move      rada04        rhovlr_avde
      *
     c     rhovlr_key    chain     rhovlr                             61
      *
     c                   eval      t1tek1 = *blanks
      *
     c                   if        *in61  = *off
     c                   movel     rhtext        t1tek1
     c                   endif
      *
      * Skriv MVA-oppgave
      *
     c                   write     t1mva
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Definisjoner av arbeidsfelt
      *
     c                   eval      recnr1 = 1
     c                   eval      x = 0
      *
     c                   eval      wsbel = 0
     c                   eval      wsimva = 0
V    c                   eval      wsimvu = 0
     c                   eval      wsumva = 0
     c                   eval      wsnett = 0
     c                   eval      wmva01g = 0
     c                   eval      wmva02g = 0
     c                   eval      wmva03g = 0
     c                   eval      wmva04g = 0
     c                   eval      wmva04a = 0
     c                   eval      wmva05g = 0
     c                   eval      wmva05a = 0
     c                   eval      wmva06g = 0
     c                   eval      wmva07g = 0
     c                   eval      wmva07a = 0
     c                   eval      wmva08a = 0
     c                   eval      wmva09a = 0
     c                   eval      wmva10a = 0
     c                   eval      wmva11a = 0
     c                   eval      page = 0
      *
     c                   eval      mk = *blank
     c                   eval      rhpost = *blank
      *
      * Hent sist brukte bilagsnummer
      *
     c                   move      l_firm        w_firm
     c                   move      rhprÅr        raa4l1_4aar
     c     raa4l1_key    chain     raa4l1                             61
      *
     c                   if        *in61 = *off
     c                   eval      ww4sgb = ra4sgb + 1
     c                   eval      ra4sgb = ww4sgb
S5.40c*                  update    raa4l1r
     c                   endif
      *
      * Hent faste kontonr fra koderegister
      *
     c     ra04l1_key    chain     ra04l1                             61
      *
     c                   eval      a01txt = 'OVERFØRT'
      *
     c                   eval      x = 10
      *
      * Skriv heading
      *
     c                   write     a1hdr
      *
      * Key for posteringsregister - MVA
      *
     c     rhtrl6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhtrl6_raar
      *
      * Key for posteringsregister - oppdatering
      *
     c     rhtrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhtrlu_raar
     c                   kfld                    rhtrlu_kont
     c                   kfld                    rhtrlu_spes
     c                   kfld                    rhtrlu_avde
     c                   kfld                    rhtrlu_rper
     c                   kfld                    rhtrlu_bdat
     c                   kfld                    rhtrlu_tist
      *
      * Key for saldo-file
      *
     c     rhsal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhsal1_kont
     c                   kfld                    rhsal1_spes
     c                   kfld                    rhsal1_avde
     c                   kfld                    rhsal1_raar
     c                   kfld                    rhsal1_type
      *
      * Key for kontoplan
      *
     c     rhovlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovlr_kont
     c                   kfld                    rhovlr_spes
     c                   kfld                    rhovlr_avde
      *
      * Key for statuskode 4
      *
     c     raa4l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    raa4l1_4aar
      *
      * Key for faste konti
      *
     c     ra04l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for mva-kode
      *
     c     ra05lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra05lr_ekod
      *
     c                   endsr
      *
