     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RA101R
      * Beskrivelse . . : Bilagskoder, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       240202 KOB  Nytt program
      * V5.40 030805 KOB  Lagt på ny kode for overføring til factoring
      * V5.41 070905 KOB  Lagt på opplysningstype for arkiv
6.10  * V6.10 250909 NHO  Bilagsnummer serie/ debet og kredit søk ved
6.10  *                   bilagsregistrering
7.xx  * 7.00  010316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra01l1    if   e           k disk    rename(ra01pfr:ra01l1r)
     fra01l2    if   e           k disk    rename(ra01pfr:ra01l2r)
     fra01lr    if   e           k disk    rename(ra01pfr:ra01lrr)
     fra01lu    uf a e           k disk    rename(ra01pfr:ra01lur)
6.10 fanumlu    uf a e           k disk    rename(anumpfr:anumlur)
      *
     fra101d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d ra01l1_akod     s                   like(raakod)
     d ra01l2_atxt     s                   like(raatxt)
     d ra01lr_akod     s                   like(raakod)
     d ra01lu_akod     s                   like(raakod)
6.10 d p_fell          s              6
6.10 d anumlu_fell     s                   like(anfell)
6.10 d anumlu_type     s                   like(antype)
6.10 d anumlu_fast     s                   like(anfast)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(raafir)
      *
     d s_akod          s                   like(raakod)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Siden som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2akod <> 0 or
     c                             b2atxt <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ra01l2_atxt = b1atxt
     c     ra01l2_key    setll     ra01l2
     c                   other
     c                   eval      ra01l1_akod = b1akod
     c     ra01l1_key    setll     ra01l1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Bilagskode
      *
     c                   if        b2akod <> 0
     c                   eval      w_seqe = *blank
     c                   eval      ra01l1_akod = b2akod
     c     ra01l1_key    setll     ra01l1
     c                   goto      end_pos
     c                   endif
      *
      * Beskrivelse
      *
     c                   if        b2atxt <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      ra01l2_atxt = b2atxt
     c     ra01l2_key    setll     ra01l2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2akod = 0
     c                   eval      b2atxt = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1akod = b1akod
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1akod = b1akod
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1akod = b1akod
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1akod = b1akod
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1akod = b2akod
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1akod = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
     c***                if        c1akod = 0
     c***                eval      *in31  = *on
     c***                goto      c1taga
     c***                endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      ra01lr_akod = c1akod
     c     ra01lr_key    chain     ra01lr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1akod = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2akod = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      ra01lr_akod = c1akod
     c     ra01lr_key    chain     ra01lr
      *
     c                   if        %found
     c                   eval      c2atxt = raatxt
     c                   eval      c2akjØ = raakjØ
6.10 c                   eval      c2binu = rabinu
6.10 c                   eval      c2debe = radebe
6.10 c                   eval      c2kred = rakred
     c                   eval      c2arnk = raarnk
     c                   eval      c2aink = raaink
     c                   eval      c2afac = raafac
     c                   eval      c2asum = raasum
     c                   eval      c2aopt = raaopt
     c                   eval      c2eusr = raausr
     c     *dmy          move      raadat        c2edat
     c                   else
     c                   eval      c2atxt = *blank
     c                   eval      c2akjØ = *blank
6.10 c                   eval      c2binu = *blank
6.10 c                   eval      c2debe = *blank
6.10 c                   eval      c2kred = *blank
     c                   eval      c2arnk = *blank
     c                   eval      c2aink = *blank
     c                   eval      c2afac = *blank
     c                   eval      c2asum = *blank
     c                   eval      c2aopt = *blank
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1akod = k1akod
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
6.10  * Call program for nummerserier
6.10  *
6.10 c                   if        *inkd = *on and
6.10 c                             w_afld = 'C2BINU'
6.10 c                   call      'RA531R'
6.10 c                   parm                    w_firm
6.10 c                   parm                    p_fell
6.10  *
6.10 c                   if        p_fell <> *blanks
6.10 c                   move      p_fell        c2binu
6.10 c                   endif
6.10  *
6.10 c                   goto      c2taga
6.10 c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2atxt = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
6.10  * Sjekker om bilagsnummerserie finnes
6.10  *
6.10 c                   if        c2binu <>*blank
6.10 c                   eval      anumlu_fell = c2binu
6.10 c                   eval      anumlu_type = *blank
6.10 c                   eval      anumlu_fast = *blank
6.10 c     anumlu_key    chain     anumlur                            60
6.10 c                   if        *in60 = '1'
6.10 c                   eval      *in34 = *on
6.10 c                   goto      c2taga
6.10 c                   endif
6.10 c                   endif
6.10 c*
6.10 c                   if        c2binu <>*blank
6.10 c                   eval      anumlu_fell = c2binu
6.10 c                   eval      anumlu_type = *blank
6.10 c                   eval      anumlu_fast = *blank
6.10 c     anumlu_key    chain     anumlur                            60
6.10 c                   if        *in60  = *on
6.10 c                   add       1             ansist
6.10 c                   update    anumlur
6.10 c                   endif
6.10 c                   endif
      * Oppdatering
      *
     c                   clear                   ra01lur
      *
     c                   eval      ra01lu_akod = c1akod
     c     ra01lu_key    chain     ra01lur
      *
6.10 c                   eval      rabinu = c2binu
6.10 c                   eval      radebe = c2debe
6.10 c                   eval      rakred = c2kred
     c                   eval      raatxt = c2atxt
     c                   eval      raakjØ = c2akjØ
     c                   eval      raarnk = c2arnk
     c                   eval      raaink = c2aink
     c                   eval      raafac = c2afac
     c                   eval      raasum = c2asum
     c                   eval      raaopt = c2aopt
      *
     c                   time                    raadat
     c                   time                    raatim
     c                   eval      raausr = l_user
      *
     c                   if        %found
     c                   update    ra01lur
     c                   else
     c                   eval      raafir = w_firm
     c                   eval      raakod = c1akod
     c                   write     ra01lur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1atxt = raatxt
     c                   eval      b1akjØ = raakjØ
     c                   eval      b1arnk = raarnk
     c                   eval      b1aink = raaink
     c                   eval      b1asum = raasum
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      ra01lr_akod = d1akod
     c     ra01lr_key    chain     ra01lr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      ra01lu_akod = d1akod
     c     ra01lu_key    chain     ra01lur
      *
     c                   delete    ra01lur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_akod = k1akod
      *
      * Fyller kopierings-bildet
      *
     c                   eval      ra01lr_akod = k1akod
     c     ra01lr_key    chain     ra01lr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1akod = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      ra01lr_akod = k1akod
     c     ra01lr_key    chain     ra01lr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1akod = s_akod
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      ra01l2
     c                   other
     c                   read      ra01l1
     c                   endsl
      *
     c                   if        %eof or
     c                             raafir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ra01l2_atxt = raatxt
     c                   other
     c                   eval      ra01l1_akod = raakod
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1akod = raakod
     c                   eval      b1atxt = raatxt
     c                   eval      b1akjØ = raakjØ
     c                   eval      b1arnk = raarnk
     c                   eval      b1aink = raaink
     c                   eval      b1afac = raafac
     c                   eval      b1asum = raasum
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     ra01l2_key    setgt     ra01l2
     c                   readp     ra01l2
     c                   other
     c     ra01l1_key    setgt     ra01l1
     c                   readp     ra01l1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     ra01l2
     c                   other
     c                   readp     ra01l1
     c                   endsl
      *
     c                   if        %eof or
     c                             raafir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ra01l2_atxt = raatxt
     c                   other
     c                   eval      ra01l1_akod = raakod
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B' and
     c                             %eof(ra01l2)
     c     ra01l2_key    setll     ra01l2
     c                   other
     c     ra01l1_key    setll     ra01l1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på bilagskode
      *
     c     ra01l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra01l1_akod
      *
      * Key for posisjonering på beskrivelse
      *
     c     ra01l2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra01l2_atxt
      *
      * Key for les på bilagskode
      *
     c     ra01lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra01lr_akod
      *
      * Key for oppdatering av bilagskode
      *
     c     ra01lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra01lu_akod
      *
      * Key for nummerteller
      *
6.10 C     anumlu_key    klist
6.10 C                   kfld                    w_firm
6.10 C                   kfld                    anumlu_fell
6.10 C                   kfld                    anumlu_type
6.10 C                   kfld                    anumlu_fast
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      ra01l1_akod = 0
     c     ra01l1_key    setll     ra01l1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
