     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO761R
      * Beskrivelse . . : Oppdatering av lager-register utfra vare i ordre
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       260902 HKO  Nytt program
5.31  * R5.31 100104 ASK  Lagt inn endringer for lager på linjenivå.
      * 6.10  260609 BSA  Oppdatert med sporingsinformasjon
      * 6.10  220909 ASK  Nye parametre til VL001R
6.20  * 6.20  208013 bof  Teste på leveringstype = 0
6.nu  * 6.30  050614 bof  Gjennomgang mtp. nummerutvidelse
6.30  * 6.30  040615 bkv  Ordre økt fra 6 til 8
8.01  *PRG2   200622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvlaglu    uf   e           k disk    rename(vlagpfr:vlaglur)
     ffodtlv    if   e           k disk    rename(fodtpfr:fodtlvR)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1R)
     fvotylr    if   e           k disk    rename(votypfr:votylrr)
      *
6.10 D                UDS
6.10 d l_user                911    920
      *
      * Definering av parametre
      *
     d p_firm          s                   like(fofirm)
     d p_vare          s                   like(fdvare)
      *
      * Datastruktur - lageroppdatering
      *
     d                 ds
6.nu d hlrec                        128
     d  hlvare                       15    overlay(hlrec)
     d  hllage                        2  0 overlay(hlrec:16)
     d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
     d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
     d  hlotyp                        2    overlay(hlrec:36)
     d  hlltyp                        2    overlay(hlrec:38)
     d  hlanta                       11  3 overlay(hlrec:40)
     d  hlkopr                        9  2 overlay(hlrec:51)
     d  hlrefr                       20    overlay(hlrec:60)
     d  hlsyst                        1    overlay(hlrec:80)
     d  hlaltk                        2    overlay(hlrec:81)
     d  hlrest                        1    overlay(hlrec:83)
     d  hltype                        1    overlay(hlrec:84)
     d  hlenhe                        3    overlay(hlrec:85)
     d  hlinlr                        1    overlay(hlrec:88)
     d  hllety                        1    overlay(hlrec:89)
6.nu d  hlonum                        8  0 overlay(hlrec:90)
6.nu d  hlolin                        4  0 overlay(hlrec:98)
      *
      * Definering av variabler i nøkler
      *
     d vlaglu_vare     s                   like(vlvare)
     d vlaglu_lage     s                   like(vllage)
     d fodtlv_vare     s                   like(fdvare)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d votylr_otyp     s                   like(vaotyp)
      *
      * Definering av variable
      *
     d w_stat          s              1
      *
     d w_firm          s                   like(fofirm)
6.30 d w_numa          s              8
5.02 d w_lina          s              4
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer akkumulatorer i lager
      *
     c                   eval      vlaglu_vare = p_vare
     c     vlaglu_key    setll     vlaglu
     c     vlaglu_key    reade     vlaglur
     c                   dow       %eof = *off
      *
     c                   eval      vlpakk = 0
     c                   eval      vloord = 0
     c                   eval      vlores = 0
      *
6.10 c                   time                    vledat
6.10 c                   time                    vletim
6.10 c                   eval      vleusr = l_user
     c                   update    vlaglur
      *
     c     vlaglu_key    reade     vlaglur
     c                   enddo
      *
      * Leser alle forekomster av varen i ordre og kaller program
      * for oppdatering av akkumulatorer i lager
      *
     c                   eval      fodtlv_vare = p_vare
     c     fodtlv_key    setll     fodtlv
     c     fodtlv_key    reade     fodtlv
     c                   dow       %eof = *off
      *
6.20 c                   if        fdlety = 0
      *
     c                   eval      fohel1_numm = fdnumm
     c                   eval      fohel1_suff = fdsuff
     c     fohel1_key    chain     fohel1
     c                   if        %found
      *
     c                   eval      votylr_otyp = footyp
     c     votylr_key    chain     votylr
     c                   if        vaoakk < 2
     c                   eval      hltype = 'R'
     c                   eval      hlinlr = *blank
      *
     c                   eval      hlvare = fdvare
5.31 c                   eval      hllage = fdlage
     c                   eval      hlenhe = fdenh1
     c                   eval      hldato = *loval
     c                   eval      hltime = *loval
     c                   eval      hlotyp = footyp
     c                   eval      hlltyp = fdltyp
      *
     c                   eval      hlanta = fdanta
     c                   eval      hlkopr = fdkopr
      *
     c                   eval      hlrefr = *blank
     c                   eval      hlsyst = 'F'
     c                   eval      hlaltk = *blank
     c                   eval      hlrest = *blank
     c                   move      fdnumm        w_numa
     c                   move      fdline        w_lina
     c                   eval      hlrefr = 'O:' + w_numa +
     c                                      ' L:' + w_lina
     c                   eval      hlonum = fdnumm
     c                   eval      hlolin = fdline
3.12 c                   move      fdlety        hllety
      *
      * Kaller lageroppdaterings-program
      *
     c                   eval      w_stat = *blank
      *
     c                   call      'VL001R'
     c                   parm                    w_stat
     c                   parm                    hlrec
      *
     c                   endif
     c                   endif
      *
6.20 c                   endif
      *
     c     fodtlv_key    reade     fodtlv
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       TAG
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
      *
      * Key for oppdatering av lager
      *
     c     vlaglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglu_vare
     c                   kfld                    vlaglu_lage
      *
     c     vlaglu_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglu_vare
      *
      * Key for les på ordre-linje
      *
     c     fodtlv_key    KLIST
     c                   KFLD                    w_firm
     c                   KFLD                    fodtlv_vare
      *
      * Key for oppslag på ordre-hode
      *
     c     fohel1_key    KLIST
     c                   KFLD                    w_firm
     c                   KFLD                    fohel1_numm
     c                   KFLD                    fohel1_suff
      *
      *  Key for les av ordretype
      *
     c     votylr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votylr_otyp
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
