      /TITLE  GL102R Utskrift av forslag til utbetaling
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * System  . . . . : ASOKON                                        *
      * Program . . . . : GL102R                                        *
      * Beskrivelse . . : Utskrift av forslag til utbetaling            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign *
      * --------- ------  ----------------------------------------------*
      *           211094  Nytt program oktober-1994                RH   *
      *                                                                 *
      * 30049     300499  Dersom SWIFTADRESSE er utfylt skal       RH   *
      *                   det ikke være noe krav at også           RH   *
      *                   adresse 1, 2 og 3 må være utfylt.        RH   *
      *                                                                 *
      * 12059     120599  Tatt bort test mot postnummer dersom     RH   *
      *                   leverandør ikke er norsk.                RH   *
      *                                                                 *
      * 12059     120599  Enten SWIFT eller kontonummer må være    RH   *
      *                   utfylt, samt kun adresselinje 1.         RH   *
      *                                                                 *
T5.00 * V5.00     091002  Kaller RS716R i stedet for RS116R        HFL  *
T5.00 * V5.00     151002  Endring i datofelt og key-felt.          HFL  *
T5.00 * V5.20     081003  Endring i datofelt i raport og program   KOB  *
T5.00 * V5.20     081003  Programmet er renovert                   KOB  *
6.30  *  6.30     150814  Innført mulighet for arkivering og PDF   Bsa  *
6.31  * R6.30 180816 bsa  Rokkert på rekkefølge av close printerfile
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KC = F3  = Exit                                           *
      *       KE = F5  = Forny                                          *
      *       KL = F12 = Annuller                                       *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Overflow i rapport.                                  *
      *    31-50 = Brukes til meldinger i rapport.                      *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * D1DET  - Detail  D1 på rapport
      * T1TOTA - Total   pr. leverandør/forfallsdato.
      * T2TOTA - Meldinger
      * T2GIRO - Meldinger
      * T1TOTC - Meldinger
      * T1TOTD - Meldinger
      * T2LEV  - Feilmelding på leverandør.
      * T2TOT  - Total   pr. leverandør.
      * T3TOT  - Total   på rapport
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frlbapf    if   e           k disk    rename(rlbapfr:rlvapfr)
E5.00frltrl2    if   e           k disk    rename(rltrpfr:rltrl2r)
     fgubfl1    if   e           k disk    rename(gabfpfr:gabfpf1)
     fgubfl4    if   e           k disk    rename(gabfpfr:gabfpf4)
     fgfaspf    if   e           k disk
     fgl102p    o    e             printer
6.30 f                                     usropn
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Kommentarer til program.                                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Datastruktur for forfallsdato på forslagfile (ÅMD)
      *
     d                 ds
     d gaforf                         8  0
     d  gaforÅ                        4  0 overlay(gaforf)
     d  gaform                        2  0 overlay(gaforf:5)
     d  gaford                        2  0 overlay(gaforf:7)
      *
      * Datastruktur for å hente ut firmanummer og forslagsnummer
      * i fra parameterliste:
      *
     d                 ds
     d dslist                         7
     d  wpfirm                        3  0 overlay(dslist)
     d  wpbfnr                        3  0 overlay(dslist:4)
      *
     d rlevl1_levr     s                   like(rllevr)
     d rlbapf_levr     s                   like(rllevr)
     d rltrl2_levr     s                   like(rolevr)
E5.00d rltrl2_bdat     s                   like(robdat)
E5.00d rltrl2_tist     s                   like(rotist)
      *
      * Henter firmanavn.
      *
     d                uds
6.30 d l_poff                584    584
6.30 d l_utqm                585    594
6.30 d l_bach                595    635
6.30 d l_arch                636    636  0
6.30 d l_skje                637    637  0
6.30 d l_land                638    638  0
6.30 d l_exlp                639    639  0
6.30 d l_mail                640    640  0
     d l_ruti                800    809
6.30 d l_outq                810    819
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d gabrud          s             30
T5.00d p_stat          s              1
T5.00d p_pkod          s                   like(gavalk)
T5.00d p_tx20          s             20
     d t3kons          s             11  2
     d wovrfl          s              1  0
     d wpbrud          s             30
     d wpfor1          s              8  0
     d wpgiro          s             35
     d wpkids          s                   like(gakids)
     d wpkont          s             11  0
     d wplevr          s                   like(galevr)
     d wplev1          s                   like(galevr)
     d wplist          s              6
     d wpnavn          s                   like(ganavn)
     d wpnav1          s                   like(ganavn)
     d wpparm          s              1
     d wprabb          s             11  2
     d wpvalk          s                   like(gavalk)
T5.00d w_fdat          s                   like(gafdat)
      *
6.30 d p_btyp          s            100
6.30 d w_year          s              4  0
      *
6.30 d splfname2       s             10a
6.30 d jobname2        s             10a
6.30 d username2       s             10a
6.30 d jobnbr2         s              6a
6.30 d splfnbr2        s             10i 0
6.30 d splfname3       s             10a
6.30 d jobname3        s             10a
6.30 d username3       s             10a
6.30 d jobnbr3         s              6a
6.30 d splfnbr3        s             10i 0
6.30 d p_tagg0         s             20
6.30 d p_tagg0val      s             50
6.30 d p_tagg1         s             20
6.30 d p_tagg1val      s             50
6.30 d p_tagg2         s             20
6.30 d p_tagg2val      s             50
6.30 d p_tagg3         s             20
6.30 d p_tagg3val      s             50
6.30 d p_tagg4         s             20
6.30 d p_tagg4val      s             50
6.30 d p_tagg5         s             20
6.30 d p_tagg5val      s             50
6.30 d p_tagg6         s             20
6.30 d p_tagg6val      s             50
6.30 d p_tagg7         s             20
6.30 d p_tagg7val      s             50
6.30 d p_tagg8         s             20
6.30 d p_tagg8val      s             50
6.30 d p_tagg9         s             20
6.30 d p_tagg9val      s             50
6.30 d p_refn          s             50
6.30 d p_dspl          s            100
      *
6.30 d qsprilsp        pr                  extpgm('QSPRILSP')
6.30 d rcvvar                     32767a   options(*varsize)
6.30 d rcvvarlen                     10i 0 const
6.30 d format                         8a   const
6.30 d errorcode                  32767a   options(*varsize)
      *
6.30 d sprl0100        ds
6.30 d  bytesrtn                     10i 0
6.30 d  bytesavl                     10i 0
6.30 d  splfname                     10a
6.30 d  jobname                      10a
6.30 d  username                     10a
6.30 d  jobnbr                        6a
6.30 d  splfnbr                      10i 0
6.30 d  systemname                    8a
6.30 d  createdate                    7a
6.30 d                                1a
6.30 d  createtime                    6a
      *
6.30 d errorcode       ds
6.30 d  bytesprov                    10i 0 inz(0)
6.30 d  byteavail                    10i 0 inz(0)
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Utskrift av betalingsforslag ("L"=Lønnsforslag):
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c                   if        wpparm <> 'L'
     c                   exsr      xrappo
     c                   else
     c                   exsr      xrappl
     c                   endif
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Generer xml for videre utskrift
6.30 c                   if        l_poff = '1'
6.30 c                   exsr      spoolnbr
6.31 c                   close     gl102p
6.31 c                   exsr      xml_create
6.30 c                   exsr      pdf_preview
6.30  * Avslutt jobbiden
6.30 c                   call      'AB710R'
6.30 c                   parm                    l_bach
6.30 c                   endif
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Skriver utbetalingsforslag.                                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xrappo        begsr
      *
     c                   eval      a1bfnr = wpbfnr
      *
      * Skriver ut heading på rapport.
      *
     c                   write     a1hdr
      *
      * WPKIDS og WPVALK vil inneholde verdi i fra foregående les post,
      * men den første initielle verdien vil være henholdsvis "I" og
      * "INI" (INItiell verdi).
      *
     c                   movel     'I'           wpkids
     c                   movel     'INI'         wpvalk
      *
      * Initierer felt som vil inneholde informasjon om leverandørnr
      * og forfallsdato på sist LESTE post.
      *
     c                   eval      wpnav1 = *blanks
     c                   eval      wplev1 = 0
     c                   eval      wpfor1 = 0
      *
      * GUBFL1 velges v/sortering på lev.nr.
      * GUBFL4 velges v/sortering på lev.navn.
      *
     c                   if        gefsbf = 'N'
     c     gubfl1_key    setll     gubfl1
     c     gubfl1_key    reade     gubfl1                                 60
T5.00c                   move      gafdat        gaforf
      *
     c                   else
      *
     c     gubfl4_key    setll     gubfl4
     c     gubfl4_key    reade     gubfl4                                 60
T5.00c                   move      gafdat        gaforf
     c                   endif
      *
     c     *in60         cabeq     *on           xendra
      *
      * Finner leverandørinformasjon:
      *
Les1 c                   exsr      xlever
      *
      * Lese alle poster fra forslagsfile:
      *
     c                   if        gefsbf = 'N'
     c                   exsr      xlesnr
     c                   else
     c                   exsr      xlesna
     c                   endif
      *
      * Skrive totalsum før det avsluttes.
      *
     c                   if        *in60 = *on
      *
      * Test om det er meldinger som skal skrives ut
      * ved brudd på Betalingsdato/Valutakode/KID-status.
      *
     c                   exsr      xt1brk
      *
     c                   write     t1tota                               30
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
      *
      * Skriver ut ev. meldinger ved brudd på betalingsdato
      *
     c                   exsr      xerrt1
      *
      * Test om det er meldinger som skal skrives ut
      * ved brudd på Leverandør.
      *
     c                   exsr      xt2brk
      *
      *
      * Skriver ut ev. meldinger ved brudd på leverandør.
      *
     c                   exsr      xerrt2
      *
     c                   write     t3tot                                30
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
     c                   endif
      *
     c     xendra        tag
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Skriver utbetalingsforslag - Lønnstransaksjoner:                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xrappl        begsr
      *
     c                   eval      a1bfnr = wpbfnr
      *
      * Skriver ut heading på rapport.
      *
     c                   write     b1hdr
      *
     c     gubfl1_key    setll     gubfl1
     c     gubfl1_key    reade     gubfl1                                 60
      *
     c                   dow       *in60 = *off
      *
      * Overfører verdier:
      *
     c                   eval      e1lØnr = galevr
     c                   movel     ganavn        e1navn
T5.00c                   move      gafdat        gaforf
      *
      * De 6 siste posisjonene i navnefeltet inneholder leverandør-
      * nummeret, og skal således IKKE skrives ut.  Feltet brukes i
      * forbindelse med ALFA-sortering på leverandørnavn, for å
      * skille de leverandører som opererer under forskjellig
      * leverandørnummer.
      *
     c                   move      '      '      e1navn
     c                   eval      e1giro = gabgir
     c                   eval      e1belØ = gabelØ
     c                   move      gaforf        e1fdat
     c                   eval      e1bfli = gabfli
      * Totalt:
     c                   eval      e1lØnn = e1lØnn + gabelØ
      *
      * Skriver ut detaljlinje:
      *
     c                   write     e1det                                30
      *
     c                   if        *in30 = *on
     c                   write     b1hdr
     c                   endif
      *
     c     gubfl1_key    reade     gubfl1                                 60
      *
     c                   if        *in60 = *on
T5.00c                   move      gafdat        gaforf
      *
     c                   write     e1tota                               30
     c                   if        *in30 = *on
     c                   write     b1hdr
     c                   endif
      *
     c                   write     e1totb                               30
     c                   if        *in30 = *on
     c                   write     b1hdr
     c                   endif
      *
     c                   write     e1totc                               30
     c                   if        *in30 = *on
     c                   write     b1hdr
     c                   endif
      *
     c                   endif
      *
     c                   enddo
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for å behandle file GUBFL1 - sortert på lev nr:
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     xlesnr        begsr
      *
     c                   dow       *in60 = *off
      *
      * Tester om det er ny Leverandør ELLER ny Forfallsdato ELLER
      * ny Valutakode ELLER ny KID-status.  I såfall må det skrives
      * ut totallinje for forfallsdato.
      *
     c                   if        wpfor1 <> gaforf and
     c                             wpfor1 <> 0 or
     c                             wplev1 <> galevr and
     c                             wplev1 <> 0 or
     c                             wpvalk <> gavalk and
     c                             wpvalk <> 'INI'or
     c                             wpkids <> gakids and
     c                             wpkids <> 'I'
      *
      * Test om det er meldinger som skal skrives ut
      * ved brudd på Betalingsdato/Valutakode/KID-status.
      *
     c                   exsr      xt1brk
      *
      * Skriv totale for brudd på Beta.dato/Valuta/-KID-status
      *
     c                   write     t1tota                               30
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
      *
      * Skriver ut ev. meldinger ved brudd på betalingsdato
      *
     c                   exsr      xerrt1
      *
      * Skriver en "dummy" linje for å gi mer luft i rapporten
      * Linjen blir alltid skrevet ut før ny detaillinje dersom
      * det har vært brudd på betalingsdato, valutakode eller
      * KID-status.  Linjen skrives IKKE ut ved brudd på ny
      * leverandør.
      *
     c                   if        wplev1 = galevr
     c                   write     d1luft                               30
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Tester om det er ny leverandør.  I såfall må det skrives
      * ut ny "header" for leverandør.
      *
     c                   if        wplev1 <> galevr
     c                             and wplev1 <> 0
      *
      * Test om det er meldinger som skal skrives ut
      * ved brudd på leverandør.
      *
     c                   exsr      xt2brk
      *
      * Skriver ut ev. meldinger ved brudd på leverandør.
      *
     c                   exsr      xerrt2
      *
      * Nullstiller indikatorer.
      *
     c                   exsr      xnulin
      *
     c                   eval      t2belØ = 0
      *
      * Henter leverandørinformasjon
      *
Les2 c                   exsr      xlever
      *
     c                   endif
      *
      * Legger over sist leste leverandørnummer og forfallsdato
      * til hjelpefelter.  Det er allerede lest inn ny post i
      * fra file GUBFL1, og det må derfor tas vare på leverandør
      * og forfallsdato.
      *
     c                   eval      wplev1 = galevr
     c                   eval      wpfor1 = gaforf
     c                   movel     gakids        wpkids
     c                   movel     gavalk        wpvalk
      *
      * Overføre data i fra file til prt.felter
      *
     c                   eval      d1levr = galevr
     c                   movel     rlnavn        d1navn
      *
T5.00 * Bilagsdato
      *
T5.00c     *dmy          move      gabdat        d1bdat
      *
T5.00 * Forfallsdato
      *
T5.00c     *dmy          move      gafdat        d1fdat
T5.00c     *dmy          move      gafdat        t1fdat
T5.00c                   move      gafdat        gaforf
      *
T5.00 * Rabattdato 1
      *
T5.00c                   if        garda1 = *loval
T5.00c                   move      *zeros        d1rad1
T5.00c                   else
T5.00c     *dmy          move      garda1        d1rad1
T5.00c                   endif
      *
T5.00 * Rabattdato 2
      *
T5.00c                   if        garda2 = *loval
T5.00c                   move      *zeros        d1rad2
T5.00c                   else
T5.00c     *dmy          move      garda2        d1rad2
T5.00c                   endif
      *
T5.00 * Rabattdato 3
      *
T5.00c                   if        garda3 = *loval
T5.00c                   move      *zeros        d1rad3
T5.00c                   else
T5.00c     *dmy          move      garda3        d1rad3
T5.00c                   endif
      *
     c                   movel     gavalk        t1valk
     c                   movel     gatext        d1text
     c                   eval      d1biln = gabiln
     c                   movel     gakids        d1kids
     c                   eval      d1belØ = gabelØ
      *
     c                   eval      d1rbn1 = garbn1
     c                   eval      d1rbn2 = garbn2
     c                   eval      d1rbn3 = garbn3
     c                   eval      d1betb = gabetb
     c                   eval      d1bfli = gabfli
      *
      * Legger "/" ut i hjelpefelt, for å skille mellom rabattdato
      * og rabattbeløp:
      *
     c                   eval      d1rab1 = *blank
     c                   eval      d1rab2 = *blank
     c                   eval      d1rab3 = *blank
      *
     c                   if        d1rbn1 <> 0
     c                   movel     '/'           d1rab1
     c                   endif
      *
     c                   if        d1rbn2 <> 0
     c                   movel     '/'           d1rab2
     c                   endif
      *
     c                   if        d1rbn3 <> 0
     c                   movel     '/'           d1rab3
     c                   endif
      *
      * Beregn trukket rabatt.
      *
     c                   eval      t1rabb = t1rabb + gaopbn
     c                   eval      t1rabb = t1rabb - gabelØ
      *
      * Skriv post.
      *
     c                   if        gabelØ < 0
     c                   eval      t3kons = t3kons - gabelØ
     c                   else
     c                   eval      t3kons = t3kons + gabelØ
     c                   endif
      *
      * Sum pr. leverandør/forfall
      *
     c                   eval      t1belØ = t1belØ + gabelØ
     c                   eval      t1valb = t1valb + gavalb
      *
      * Sum pr. leverandør
      *
     c                   eval      t2belØ = t2belØ + gabelØ
      *
      * Sum totalt
      *
     c                   eval      t3belØ = t3belØ + gabelØ
      *
      * Råsum
      *
     c                   if        gabelØ < 0
     c                   eval      t4belØ = t4belØ - gabelØ
     c                   else
     c                   eval      t4belØ = t4belØ + gabelØ
     c                   endif
      *
      * Fjerner rabatt på fakturabeløpet kun i forbindelse med
      * utskrift på rapporten.
      *
     c                   eval      wprabb = gaopbn - gabelØ
     c                   eval      d1belØ = d1belØ + wprabb
      *
      * Kode "X" i feltet D1OVER dersom post er overført tidligere:
      *
     c                   eval      d1over = *blank
      *
     c                   eval      rltrl2_levr = galevr
E5.00c                   eval      rltrl2_bdat = gabdat
E5.00c                   eval      rltrl2_tist = gatist
E5.00c     rltrl2_key    chain     rltrl2                             61
      *
     c                   if        *in61 = *off and
     c                             robetk = '1'
     c                   movel     'X'           d1over
     c                   endif
      *
     c                   write     d1det                                30
      * Overflow ?
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
      *
     c     gubfl1_key    reade     gubfl1                                 60
T5.00c                   move      gafdat        gaforf
      *
     c                   enddo
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for å behandle file GUBFL4 - sortert på lev navn:
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     xlesna        begsr
     c                   dow       *in60 = *off
      *
      * Tester om det er ny Leverandør ELLER ny Forfallsdato ELLER
      * ny Valutakode ELLER ny KID-status.  I såfall må det skrives
      * ut totallinje for forfallsdato.
      *
     c                   if        wpfor1 <> gaforf and
     c                             wpfor1 <> 0 or
     c                             wpnav1 <> ganavn and
     c                             wpnav1 <> *blanks or
     c                             wpvalk <> gavalk and
     c                             wpvalk <> 'INI' or
     c                             wpkids <> gakids and
     c                             wpkids <> 'I'
      *
      * Test om det er meldinger som skal skrives ut
      * ved brudd på Betalingsdato/Valutakode/KID-status.
      *
     c                   exsr      xt1brk
      *
      * Skriv totale for brudd på Beta.dato/Valuta/-KID-status
      *
     c                   write     t1tota                               30
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
      *
      * Skriver ut ev. meldinger ved brudd på betalingsdato
      *
     c                   exsr      xerrt1
      *
      * Skriver en "dummy" linje for å gi mer luft i rapporten
      * Linjen blir alltid skrevet ut før ny detaillinje dersom
      * det har vært brudd på betalingsdato, valutakode eller
      * KID-status.  Linjen skrives IKKE ut ved brudd på ny
      * leverandør.
      *
     c                   if        wpnav1 = ganavn
     c                   write     d1luft                               30
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Tester om det er ny leverandør.  I såfall må det skrives
      * ut ny "header" for leverandør.
      *
     c                   if        wpnav1 <> ganavn
     c                             and wpnav1 <> *blanks
      *
      * Test om det er meldinger som skal skrives ut
      * ved brudd på leverandør.
      *
     c                   exsr      xt2brk
      *
      * Skriver ut ev. meldinger ved brudd på leverandør.
      *
     c                   exsr      xerrt2
      *
      * Nullstiller indikatorer.
      *
     c                   exsr      xnulin
      *
     c                   eval      t2belØ = 0
      * Henter leverandørinformasjon
Les3 c                   exsr      xlever
      *
     c                   endif
      *
      * Legger over sist leste leverandørnummer og forfallsdato
      * til hjelpefelter.  Det er allerede lest inn ny post i
      * fra file GUBFL1, og det må derfor tas vare på leverandør
      * og forfallsdato.
      *
     c                   movel     ganavn        wpnav1
     c                   eval      wplev1 = galevr
     c                   eval      wpfor1 = gaforf
     c                   movel     gakids        wpkids
     c                   movel     gavalk        wpvalk
      *
      * Overføre data i fra file til prt.felter
      *
     c                   eval      d1levr = galevr
     c                   movel     rlnavn        d1navn
T5.00 *
T5.00 * Bilagsdato
T5.00 *
T5.00c     *dmy          move      gabdat        d1bdat
T5.00 *
T5.00 * Forfallsdato
T5.00 *
T5.00c     *dmy          move      gafdat        d1fdat
T5.00c     *dmy          move      gafdat        t1fdat
      *
T5.00 * Rabattdato 1
      *
T5.00c                   if        garda1 = *loval
T5.00c                   move      *zeros        d1rad1
T5.00c                   else
T5.00c     *dmy          move      garda1        d1rad1
T5.00c                   endif
      *
T5.00 * Rabattdato 2
      *
T5.00c                   if        garda2 = *loval
T5.00c                   move      *zeros        d1rad2
T5.00c                   else
T5.00c     *dmy          move      garda2        d1rad2
T5.00c                   endif
      *
T5.00 * Rabattdato 3
      *
T5.00c                   if        garda3 = *loval
T5.00c                   move      *zeros        d1rad3
T5.00c                   else
T5.00c     *dmy          move      garda3        d1rad3
T5.00c                   endif
T5.00 *
     c                   movel     gavalk        t1valk
     c                   movel     gatext        d1text
     c                   eval      d1biln = gabiln
     c                   movel     gakids        d1kids
     c                   eval      d1belØ = gabelØ
     c                   eval      d1rbn1 = garbn1
     c                   eval      d1rbn2 = garbn2
     c                   eval      d1rbn3 = garbn3
     c                   eval      d1betb = gabetb
     c                   eval      d1bfli = gabfli
      *
      * Legger "/" ut i hjelpefelt, for å skille mellom rabattdato
      * og rabattbeløp:
      *
     c                   eval      d1rab1 = *blank
     c                   eval      d1rab2 = *blank
     c                   eval      d1rab3 = *blank
      *
     c                   if        d1rbn1 <> 0
     c                   movel     '/'           d1rab1
     c                   endif
      *
     c                   if        d1rbn2 <> 0
     c                   movel     '/'           d1rab2
     c                   endif
      *
     c                   if        d1rbn3 <> 0
     c                   movel     '/'           d1rab3
     c                   endif
      *
      * Beregn trukket rabatt.
      *
     c                   eval      t1rabb = t1rabb + gaopbn
     c                   eval      t1rabb = t1rabb - gabelØ
      *
      * Skriv post.
      *
     c                   if        gabelØ < 0
     c                   eval      t3kons = t3kons - gabelØ
     c                   else
     c                   eval      t3kons = t3kons + gabelØ
     c                   endif
      *
      * sum pr. leverandør/forfall
      *
     c                   eval      t1belØ = t1belØ + gabelØ
     c                   eval      t1valb = t1valb + gavalb
      *
      * sum pr. leverandør
      *
     c                   eval      t2belØ = t2belØ + gabelØ
      *
      * sum totalt
      *
     c                   eval      t3belØ = t3belØ + gabelØ
      *
      * råsum
      *
     c                   if        gabelØ < 0
     c                   eval      t4belØ = t4belØ - gabelØ
     c                   else
     c                   eval      t4belØ = t4belØ + gabelØ
     c                   endif
      *
      * Fjerner rabatt på fakturabeløpet kun i forbindelse med
      * utskrift på rapporten.
      *
     c                   eval      wprabb = gaopbn - gabelØ
     c                   eval      d1belØ = d1belØ + wprabb
      *
      * Kode "X" i feltet D1OVER dersom post er overført tidligere:
      *
     c                   eval      d1over = *blank
      *
     c                   eval      rltrl2_levr = galevr
E5.00c                   eval      rltrl2_bdat = gabdat
E5.00c                   eval      rltrl2_tist = gatist
E5.00c     rltrl2_key    chain     rltrl2                             61
      *
     c                   if        *in61 = *off
     c                             and robetk = '1'
     c                   movel     'X'           d1over
     c                   endif
      *
     c                   write     d1det                                30
      * Overflow ?
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
      *
     c     gubfl4_key    reade     gubfl4                                 60
T5.00c                   move      gafdat        gaforf
      *
     c                   enddo
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for nullstilling av indikatorer som brukes v/melding
      * på rapport.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xnulin        begsr
     c                   move      *off          *in31
     c                   move      *off          *in32
     c                   move      *off          *in33
     c                   move      *off          *in34
     c                   move      *off          *in35
     c                   move      *off          *in36
     c                   move      *off          *in37
     c                   move      *off          *in38
     c                   move      *off          *in40
     c                   move      *off          *in41
     c                   move      *off          *in42
     c                   move      *off          *in43
     c                   move      *off          *in44
     c                   move      *off          *in45
     c                   move      *off          *in46
     c                   move      *off          *in47
     c                   move      *off          *in48
     c                   move      *off          *in50
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine som tester på mangelfull informasjon, og skriver
      * dette ut ved brudd på betalingsdato.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xt1brk        begsr
      *
      * Nullstiller indikatorer som blir brukt ved feilmeldinger
      *
     c                   move      *off          *in31
     c                   move      *off          *in32
     c                   move      *off          *in33
     c                   move      *off          *in34
     c                   move      *off          *in35
      *
      * Rabatt?
      *
     c                   if        t1rabb <> 0
     c                   write     t1rab                                30
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
      *
     c                   eval      t1rabb = 0
     c                   endif
      *
      * *IN31 forteller at postene er MED UTFYLT KID-FELT.
      *
     c                   if        wpkids = '*'
     c                   move      *on           *in31
     c                   endif
      *
      * *IN32 forteller at postene er i fremmed valuta.
      *
     c                   if        wpvalk <> *blanks
     c                             and wpvalk <> 'NOK'
     c                   move      *on           *in32
     c                   move      *on           *in33
     c                   endif
      *
      * *IN33 forteller "Til utbetaling utland".
      *
     c                   if        rlland <> *blanks and
     c                             rlland <> 'NO'
     c                   move      *on           *in33
     c                   endif
      *
      * *IN35 forteller at valutakode er ukjent.
      *
     c                   if        wpvalk <> *blanks and
     c                             wpvalk <> 'NOK'
T5.00c                   move      t1valk        p_pkod
      *
E5.00c                   call      'RS716R'                                      valuta-
T5.00c                   parm                    p_stat
T5.00c                   parm                    p_pkod
T5.00c                   parm                    p_tx20
      *
T5.00c                   if        p_stat = 'N'                                 Kode
     c                   move      *on           *in35                           finnes
     c                   else                                                     ikke
     c                   move      *off          *in35
     c                   endif
      *
     c                   endif
      *
      * *IN40 forteller at det finnes ikke post for utenlandsk
      * bankinformasjon.
      * *IN41 forteller at SWIFT er blank.  Fordyrer.
      * *IN42 forteller at bankadresse MÅ utfylles v/fremmed
      * valuta/landkode.
      *
     c                   if        rlland <> *blanks and
     c                             rlland <> 'NO' or
RHO  c                             wpvalk <> *blanks and
RHO  c                             wpvalk <> 'NOK'
     c                   eval      rvhswa = *blanks
     c                   eval      rvhba1 = *blanks
     c                   eval      rvhba2 = *blanks
     c                   eval      rvhba3 = *blanks
     c                   eval      rvhba4 = *blanks
      *
     c                   eval      rlbapf_levr = rllevr
     c     rlbapf_key    chain     rlbapf                             40
      *
     c                   movel     rvhbko        wpgiro
      *
      * Bankforbindelse (SWIFT)
      *
     c                   if        rvhswa = *blanks and
12059c                             rvhbko = *blanks
     c                   move      *on           *in41
     c                   endif
      *
      * Bankadresse må utfylles dersom SWIFT er blankt:
      *
30049c                   if        rvhswa = *blanks
      *
      * Bankadresse
      *
     c                   if        rvhba1 = *blanks
     c                   move      *on           *in42
     c                   endif
      *
30049c                   endif
      *
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine som tester på mangelfull informasjon, og skriver
      * dette ut ved brudd på leverandør.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xt2brk        begsr
      *
      * *IN36 forteller at ved betalingsmåte Bank eller Post
      * OG Norge/NOK, så må gironummer være utfylt.
      *
     c                   if        rlbetm <> 0 and
     c                             rlbgir = 0
      *
     c                   if        rlland = *blanks or
     c                             rlland = 'NO' or
     c                             wpvalk = 'NOK' or
     c                             wpvalk = '   '
     c                   move      *on           *in36
     c                   endif
     c                   endif
      *
      * *IN36 forteller at gironummer pg betalingsmåte bør utfylles:
      *
     c                   if        rlbetm = 0 and
     c                             rlbgir = 0
      *
     c                   if        rlland = *blanks or
     c                             rlland = 'NO' or
     c                             wpvalk = 'NOK' or
     c                             wpvalk = '   '
     c                   move      *on           *in36
     c                   endif
      *
     c                   endif
      *
      * *IN37 forteller at Bankgironummer BØR utfylles.  Det er
      * samtidig test på at det er "LOKAL" landkode/valuta.
      *
     c                   if        rlbetm = 1 or
     c                             rlbetm = 3 or
     c                             rlbetm = 5 or
     c                             rlbetm = 7 or
     c                             rlbetm = 9
      *
     c                   if        rlland = *blanks or
     c                             rlland = 'NO' and
     c                             wpvalk = 'NOK' or
     c                             wpvalk = *blanks
     c                   movel     rlbgir        wpkont
      *
     c                   if        rlbgir = 0
     c                   move      *on           *in37
     c                   endif
      *
     c                   endif
     c                   endif
      *
      * *IN38 forteller at Postgironummer BØR utfylles.  Det er
      * samtidig test på at det er "LOKAL" landkode/valuta.
      *
     c                   if        rlbetm = 2 or
     c                             rlbetm = 4 or
     c                             rlbetm = 6 or
     c                             rlbetm = 8
      *
     c                   if        rlland = *blanks or
     c                             rlland = 'NO' and
     c                             wpvalk = 'NOK' or
     c                             wpvalk = *blanks
     c                   endif
     c                   endif
      *
      * *IN40 forteller at det finnes ikke post for utenlandsk
      * bankinformasjon.
      * *IN41 forteller at SWIFT er blank.  Fordyrer.
      * *IN42 forteller at bankadresse MÅ utfylles v/fremmed
      * valuta/landkode.
      *
     c                   if        rlland <> *blanks and
     c                             rlland <> 'NO' or
RHO  c                             wpvalk <> *blanks and
RHO  c                             wpvalk <> 'NOK'
     c                   eval      rvhswa = *blanks
     c                   eval      rvhba1 = *blanks
     c                   eval      rvhba2 = *blanks
     c                   eval      rvhba3 = *blanks
     c                   eval      rvhba4 = *blanks
      *
     c                   eval      rlbapf_levr = rllevr
     c     rlbapf_key    chain     rlbapf                             40
      *
     c                   movel     rvhbko        wpgiro
      *
      * Bankforbindelse (SWIFT)
      *
     c                   if        rvhswa = *blanks and
12059c                             rvhbko = *blanks
     c                   move      *on           *in41
     c                   endif
      *
      * Bankadresse må utfylles dersom SWIFT er blankt:
      *
30049c                   if        rvhswa = *blanks
      * Bankadresse
     c                   if        rvhba1 = *blanks
     c                   move      *on           *in42
     c                   endif
      *
30049c                   endif
      *
     c                   endif
      *
      * *IN47 forteller at postnummer ikke er utfylt.
      *
12059c                   if        rlland = 'NO' or
12059c                             rlland = *blanks
     c                   if        rlponr = 0
     c                   move      *on           *in47
     c                   endif
12059c                   endif
      *
      * *IN48 forteller at det ikke er samsvar mellom kontonummer
      * og betalingsmåte:
      *
      * TEST TATT BORT 10/12-95 (RH). P.G.A. AT LISTE BLIR "BLÅST OPP"
      * UNØDVENDIG.  INGEN LEV. HAR KODEN FOR UTBET.MÅTE UTFYLT!
      *
     c                   if        rlbetm = 0
     c                   if        wpvalk = 'NOK' or
     c                             wpvalk = *blanks
     c                   if        rlbgir <> 0
     c                   if        rlbgir <> 0
     c                   movel     rlbgir        wpkont
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for å hente opp leverandør-/factoringinformasjon      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xlever        begsr
      *
     c                   eval      d1levr = galevr
     c                   eval      d1sald = 0
     c                   eval      rlbgir = 0
     c                   eval      wpkont = 0
     c                   eval      wpgiro = *blanks
     c                   eval      d1navn = *blanks
     c                   eval      rlnavn = *blanks
     c                   eval      rlgate = *blanks
     c                   eval      rladr2 = *blanks
     c                   eval      rlsted = *blanks
      *
     c                   eval      rlevl1_levr = galevr
     c     rlevl1_key    chain     rlevl1                             61
      *
      * Navn er ikke utfylt.
      *
     c                   if        rlnavn = *blanks
     c                   move      *on           *in43
     c                   endif
      *
      * Blank i gate, adresse2 og poststed.
      *
     c                   if        rlgate = *blanks
     c                             and rladr2 = *blanks
     c                             and rlsted = *blanks
     c                   move      *on           *in44
     c                   endif
      *
     c                   if        *in61 = *off
     c                   movel     rlnavn        d1navn
     c                   eval      d1sald = rlsald
     c                   eval      d1fanr = rlfact
     c                   eval      d1fana = *blanks
      *
      * Henter factoringinformasjon.
      *
     c                   if        rlfact <> 0
     c                   eval      rlbgir = 0
     c                   eval      wpkont = 0
     c                   eval      wpgiro = *blanks
     c                   eval      rlnavn = *blanks
     c                   eval      rlgate = *blanks
     c                   eval      rladr2 = *blanks
     c                   eval      rlsted = *blanks
      *
     c                   eval      rlevl1_levr = rlfact
     c     rlevl1_key    chain     rlevl1                             61
      *
      * Navn er ikke utfylt.
      *
     c                   if        rlnavn = *blanks
     c                   move      *on           *in45
     c                   endif
      *
      * Blank i gate, adresse2 og poststed.
      *
     c                   if        rlgate = *blanks and
     c                             rladr2 = *blanks and
     c                             rlsted = *blanks
     c                   move      *on           *in46
     c                   endif
      *
     c                   if        *in61 = *off
     c                   movel     rlnavn        d1fana
     c                   endif
      *
     c                   endif
      *
     c                   endif
      *
     c                   write     d1hed1                               30
     c                   write     d1hed2                               30
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for å skrive feilmelding ved brudd på leverandør      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xerrt2        begsr
      *
      * Skriver ut kontonummer.
      *
     c                   write     t2tota                               30
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
      *
      * Skifter fortegn i D1SALD:
      *
     c                   eval      d1sald = d1sald * -1
      *
      * Skriver ut dummylinje:
      *
     c                   if        *in36 = *on or
     c                             t2belØ > d1sald or
     c                             *in37 = *on or
     c                             *in38 = *on or
     c                             *in40 = *on or
     c                             *in41 = *on or
     c                             *in42 = *on or
     c                             *in43 = *on or
     c                             *in44 = *on or
     c                             *in45 = *on or
     c                             *in46 = *on or
     c                             *in47 = *on
      *
     c                   write     d1luft                               30
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
     c                   endif
      *
      * Skriver melding om manglende gironr:
     c                   if        *in36 = *on or
     c                             *in48 = *on
     c                   write     t2giro                               30
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
     c                   endif
      *
      * Overskridelse av saldo.
      *
     c                   if        t2belØ > d1sald
     c                   write     t2over                               30
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
     c                   endif
      * Feil vedr. Bank-/Postgironummer
     c                   if        *in37 = *on or
     c                             *in38 = *on
     c                   write     t2bapo                               30
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
     c                   endif
      *
      * Feil vedr. navn på leverandør.
      *
     c                   if        rlfact = *zeros and
     c                             *in43 = *on
     c                   write     t2nav                                30
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
     c                   endif
      *
      * Feil vedr. adr. på leverandør.
      *
     c                   if        rlfact = *zeros and
     c                             *in44 = *on
     c                   write     t2adr                                30
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
     c                   endif
      *
      * Feil vedr. navn og adresse på factoringselskap
      *
     c                   if        rlfact <> *zeros
     c                   if        *in45 = *on or
     c                             *in46 = *on
     c                   write     t2fac                                30
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
     c                   endif
     c                   endif
      *
      * Feil vedr. utenlandsinfo på leverandør.
      *
     c                   if        *in40 = *on or
     c                             *in41 = *on or
     c                             *in42 = *on
     c                   write     t2utl                                30
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
     c                   endif
      *
      * Feil vedr. postnummer på leverandør.
      *
     c                   if        *in47 = *on
     c                   write     t2fel                                30
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
     c                   endif
      *
     c                   write     t2tot                                30
      * Sum leverandør.
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for å skrive feilmelding ved brudd på betalingsdato   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xerrt1        begsr
      *
      * Test mot leverandørinformasjon:
      * KID-felt må være utfylt dersom postgiro er utfylt med
      * 7 posisjoner, og når feltet brukes.
      * Testen utføres dersom betalingsmåte "forteller" at de er post-
      * giro som skal brukes, ELLER at KUN når postgironummer er utfylt
      * *IN31 er *OFF dersom KID ikke er utfylt.
      *
     c                   if        rlbetm = 2
     c                             or rlbetm = 4
     c                             or rlbetm = 6
     c                             or rlbetm = 8
     c                   endif
      *
     c                   if        rlbetm = 0
     c                             and rlbgir = 0
     c                   endif
      *
      * Ugyldig valutatype.
      *
     c                   if        *in35 = *on
     c                   write     t1valu                               30
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
     c                   endif
      *
      * Negativt beløp.
      *
     c                   if        t1belØ < 0
     c                   write     t1totc                               30
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
     c                   endif
      *
      * *IN50 settes "PÅ" dersom det er utbetalinger i fremmed
      * valuta.  Den brukes til å skrive ut kontonummer utland
      * på rapporten.
      *
     c                   if        t1valb <> 0
     c                   move      *on           *in50
     c                   endif
      *
     c                   eval      t1belØ = 0
     c                   eval      t1valb = 0
      *
     c                   endsr
      *
      /eject
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30  * Finner spool som er generert av jobben
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30 c     spoolnbr      begsr
6.30  /Free
6.30
6.30           QSPRILSP( SPRL0100
6.30                   : %size(SPRL0100)
6.30                   : 'SPRL0100'
6.30                   : errorcode );
6.30  /End-free
6.30  *
6.30 c                   endsr
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30  * Kaller eget program for generering av xml fil. Eget pgm         *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30 c     xml_create    begsr
6.30  *
6.30 c                   eval      splfname2 = *blanks
6.30 c                   eval      splfname3 = *blanks
6.30 c                   eval      jobname2 = *blanks
6.30 c                   eval      jobname3 = *blanks
6.30 c                   eval      username2 = *blanks
6.30 c                   eval      username3 = *blanks
6.30 c                   eval      jobnbr2 = *blanks
6.30 c                   eval      jobnbr3 = *blanks
6.30 c                   eval      splfnbr2 = *zeros
6.30 c                   eval      splfnbr3 = *zeros
6.30 c                   eval      p_btyp = 'UTBETALINGSFORSLAG'
6.30 c                   eval      p_dspl = 'Utbetalingsforslag '+
6.30 c                                      %char(a1bfnr)
6.30 c                   eval      p_refn = %char(w_year)+%char(a1bfnr)
6.30 c                   eval      p_tagg0 = *blanks
6.30 c                   eval      p_tagg0val = *blanks
6.30 c                   eval      p_tagg1 = *blanks
6.30 c                   eval      p_tagg1val = *blanks
6.30 c                   eval      p_tagg2 = *blanks
6.30 c                   eval      p_tagg2val = *blanks
6.30 c                   eval      p_tagg3 = *blanks
6.30 c                   eval      p_tagg3val = *blanks
6.30 c                   eval      p_tagg4 = *blanks
6.30 c                   eval      p_tagg4val = *blanks
6.30 c                   eval      p_tagg5 = *blanks
6.30 c                   eval      p_tagg5val = *blanks
6.30 c                   eval      p_tagg6 = *blanks
6.30 c                   eval      p_tagg6val = *blanks
6.30 c                   eval      p_tagg7 = *blanks
6.30 c                   eval      p_tagg7val = *blanks
6.30 c                   eval      p_tagg8 = *blanks
6.30 c                   eval      p_tagg8val = *blanks
6.30 c                   eval      p_tagg9 = *blanks
6.30 c                   eval      p_tagg9val = *blanks
      *
6.30 c                   call      'AP627R'
6.30 c                   PARM                    splfname
6.30 c                   PARM                    jobname
6.30 c                   PARM                    username
6.30 c                   PARM                    jobnbr
6.30 c                   PARM                    splfnbr
6.30 c                   PARM                    splfname2
6.30 c                   PARM                    jobname2
6.30 c                   PARM                    username2
6.30 c                   PARM                    jobnbr2
6.30 c                   PARM                    splfnbr2
6.30 c                   PARM                    splfname3
6.30 c                   PARM                    jobname3
6.30 c                   PARM                    username3
6.30 c                   PARM                    jobnbr3
6.30 c                   PARM                    splfnbr3
6.30 c                   parm                    p_tagg0
6.30 c                   parm                    p_tagg0val
6.30 c                   parm                    p_tagg1
6.30 c                   parm                    p_tagg1val
6.30 c                   parm                    p_tagg2
6.30 c                   parm                    p_tagg2val
6.30 c                   parm                    p_tagg3
6.30 c                   parm                    p_tagg3val
6.30 c                   parm                    p_tagg4
6.30 c                   parm                    p_tagg4val
6.30 c                   parm                    p_tagg5
6.30 c                   parm                    p_tagg5val
6.30 c                   parm                    p_tagg6
6.30 c                   parm                    p_tagg6val
6.30 c                   parm                    p_tagg7
6.30 c                   parm                    p_tagg7val
6.30 c                   parm                    p_tagg8
6.30 c                   parm                    p_tagg8val
6.30 c                   parm                    p_tagg9
6.30 c                   parm                    p_tagg9val
6.30 c                   parm                    l_bach
6.30 c                   parm                    p_btyp
6.30 c                   parm                    p_refn
6.30 c                   parm                    p_dspl
6.30  *
6.30 c                   endsr
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30  * Subrutiner for XML/jasper integrasjon - Skjermvisning           *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30 c     pdf_preview   begsr
6.30 c                   if        l_skje = 1
6.30  * Vi kaller program for launch av PDF i browser
6.30 c                   call      'AP621R'
6.30 c                   parm                    l_bach
6.30  *
6.30 c                   endif
6.30  *
6.30 c                   endsr
6.30  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Variable som brukes til "brudd-test".
      *
     c                   eval      wpbrud = *blanks
     c                   eval      gabrud = *blanks
      *
      * Variable som brukes i forbindelse med rapport.
      *
     c                   eval      wpgiro = *blanks
     c                   eval      wpkont = 0
     c                   eval      wprabb = *zero
     c                   eval      wpfor1 = *zero
     c                   move      'I'           wpkids
     c                   move      'INI'         wpvalk
     c                   eval      wovrfl = *zero
     c                   eval      t1belØ = *zero
     c                   eval      t1valb = *zero
     c                   eval      t2belØ = *zero
     c                   eval      t3belØ = *zero
     c                   eval      t4belØ = *zero
     c                   eval      t3kons = *zero
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    wplist
     c                   parm                    wpparm
      *
      * "Pakker ut" parameterlisten:
      *
     c                   movel     wplist        dslist
      *
      * Legger over firmanavn i fra datastruktur til rapportvaribel
      *
     c                   movel     l_fnav        a1fnav
      *
      * Key for rlevl1
      *
     c     rlevl1_key    klist
     c                   kfld                    wpfirm
     c                   kfld                    rlevl1_levr
      *
      * Key for file "Utbetalingsforslag" GUBFL4.
      *
     c     gubfl4_key    klist
     c                   kfld                    wpfirm
     c                   kfld                    wpbfnr
      *
      * Key for file "Utbetalingsforslag" GUBFL1, og brukes for
      * å lese kun Firma/Forslagsnummer.
      *
     c     gubfl1_key    klist
     c                   kfld                    wpfirm
     c                   kfld                    wpbfnr
      *
      * Key for RLBAPF
      *
     c     rlbapf_key    klist
     c                   kfld                    wpfirm
     c                   kfld                    rlbapf_levr
      *
E5.00 * Key for oppslag mot RLTRL2:
      *
     c     rltrl2_key    klist
     c                   kfld                    wpfirm
     c                   kfld                    rltrl2_levr
E5.00c                   kfld                    rltrl2_bdat
E5.00c                   kfld                    rltrl2_tist
      *
      * Henter faste opplysninger.
      *
     c     wpfirm        chain     gfaspf                             60
      *
6.30 c                   open      gl102p
6.30 c                   eval      w_year = *year
      *
     c                   endsr
