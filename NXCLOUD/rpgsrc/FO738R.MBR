     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO738R
      * Beskrivelse . . : Endring av leveringstype på ordrelinjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       271005 AMD  Nytt program
      * 6.10  180609 BSA  Oppdatert med sporingsinformasjon
6.nu  * R6.30 030614 bsa  Programmet gjennomgått mtp nummerutvidelser.
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffodtlu    uf   e           k disk    rename(fodtpfr:fodtlur)
     flodtlu    uf   e           k disk    rename(lodtpfr:lodtlur)
      *
      * Definering av parametere
      *
     d p_firm          s                   like(fofirm)
6.nu d p_numm          s                   like(fonumm)
     d p_suff          s                   like(fosuff)
     d p_flty          s                   like(folety)
     d p_valg          s              1
      *
      * Definering av variable i nøkler
      *
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d fodtlu_numm     s                   like(fdnumm)
     d fodtlu_suff     s                   like(fdsuff)
     d fodtlu_line     s                   like(fdline)
     d lodtlu_numm     s                   like(ldnumm)
     d lodtlu_suff     s                   like(ldsuff)
     d lodtlu_line     s                   like(ldline)
      *
6.10  * Definering av LDA
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av variable
      *
     d w_firm          s                   like(fofirm)
     d w_tlty          s                   like(folety)
     d w_updt          s              1  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Lesing ordrehode    ORDRE-HODE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     fohel1_key    chain     fohel1r
      *
     c                   eval      w_tlty = folety
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Lesing av ordre     ORDRE-LINJE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     fodtl1_key    setll     fodtl1
     c     fodtl1_key    reade     fodtl1
      *
     c                   dow       not %eof
      *
      * Finn ut om linjer skal oppdateres
      *
     c                   eval      w_updt = 0
     c                   if        p_valg <> '1'
     c                   if        p_flty = fdlety
     c                   eval      w_updt = 1
     c                   endif
     c                   endif
      *
     c                   if        p_valg =  '1'
     c                   eval      w_updt = 1
     c                   endif
      *
     c                   if        w_updt = 1
      *
      * Oppdaterer linje med riktig leveringstype
      *
     c                   eval      fodtlu_numm = fdnumm
     c                   eval      fodtlu_suff = fdsuff
     c                   eval      fodtlu_line = fdline
      *
     c     fodtlu_key    chain     fodtlur
     c                   if        %found
     c                   eval      fdlety = w_tlty
6.10 c                   time                    fdedat
6.10 c                   time                    fdetim
6.10 c                   eval      fdeusr = l_user
     c                   update    fodtlur
     c                   endif
      *
      * Oppdaterer bestillingslinje dersom den finnes
      *
     c                   eval      lodtlu_numm = fdbenr
     c                   eval      lodtlu_suff = fdbsuf
     c                   eval      lodtlu_line = fdblin
      *
     c                   if        fdbenr <> 0
     c     lodtlu_key    chain     lodtlur
     c                   if        %found
     c                   eval      ldlety = w_tlty
6.10 c                   time                    ldedat
6.10 c                   time                    ldetim
6.10 c                   eval      ldeusr = l_user
     c                   update    lodtlur
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c     fodtl1_key    reade     fodtl1
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for file : ORDRE-HODE-REGISTER
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for file : ORDRE-LINJE-REGISTER
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
      *
      * Key for file : ORDRE-LINJE-REGISTER UPDATE
      *
     c     fodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlu_numm
     c                   kfld                    fodtlu_suff
     c                   kfld                    fodtlu_line
      *
      * Key for file : BESTILLINGS-LINJE-REGISTER UPDATE
      *
     c     lodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlu_numm
     c                   kfld                    lodtlu_suff
     c                   kfld                    lodtlu_line
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_firm
6.nu c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_flty
     c                   parm                    p_valg
      *
      * Legger inn firmanummer i nøkklene
      *
     c                   eval      w_firm = p_firm
      *
      * Legger inn ordrenummer og suffix inn i nøkklene
      *
     c                   eval      fohel1_numm = p_numm
     c                   eval      fohel1_suff = p_suff
      *
     c                   eval      fodtl1_numm = p_numm
     c                   eval      fodtl1_suff = p_suff
      *
     c                   endsr
