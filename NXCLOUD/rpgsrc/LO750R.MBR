     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LO750R
      *  Beskrivelse . . : Leverandørbestilling i WEBTRADE - format
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  -------------------------------------------------
      *
6.11  * 6.10  130709 bof  Utvidet eannr til 14 og brukt std. oppslag
6.12  * 6.10  090713 bsa  GTIN problematikk. Må avstemmes med formatet i
6.12  *                   "andre enden". Nullstilles for øyeblikket.
6.nu  * 6.30  120614 bof  Usikkert om dette er i bruk, ikke foretatt endring
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     flldilr    if   e           k disk    rename(lldipfr:lldilrr)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     flwjepf    o    e           k disk
      *
      *  Definering av datastrukturer for WEBTRADE
      *
      * File-interface
     d                 ds
     d d_frec                       950
     d  d_ffid                        4    overlay(d_frec)     inz('FILE')
     d  d_fd01                        2    overlay(d_frec:5)   inz('&&')
     d  d_fide                        4    overlay(d_frec:7)   inz('WT31')
     d  d_fd02                        2    overlay(d_frec:11)  inz('&&')
     d  d_fver                        3  0 overlay(d_frec:13)  inz(001)
     d  d_fd03                        2    overlay(d_frec:16)  inz('&&')
     d  d_ftyp                        5    overlay(d_frec:18)  inz('ORDER')
     d  d_fd04                        2    overlay(d_frec:23)  inz('&&')
     d  d_fact                        3    overlay(d_frec:25)  inz('NEW')
      *
      *  Heading-record
     d                 ds
     d d_hrec                       950
     d  d_hfid                        4    overlay(d_hrec)     inz('HEAD')
     d  d_hd01                        2    overlay(d_hrec:5)   inz('&&')
     d  d_hnam                       30    overlay(d_hrec:7)
     d  d_hd02                        2    overlay(d_hrec:37)  inz('&&')
     d  d_had1                       36    overlay(d_hrec:39)
     d  d_hd03                        2    overlay(d_hrec:75)  inz('&&')
     d  d_had2                       36    overlay(d_hrec:77)
     d  d_hd04                        2    overlay(d_hrec:113) inz('&&')
     d  d_had3                       36    overlay(d_hrec:115)
     d  d_hd05                        2    overlay(d_hrec:151) inz('&&')
     d  d_hcty                       36    overlay(d_hrec:153)
     d  d_hd06                        2    overlay(d_hrec:189) inz('&&')
     d  d_hstc                        2    overlay(d_hrec:191)
     d  d_hd07                        2    overlay(d_hrec:193) inz('&&')
     d  d_hpon                       10    overlay(d_hrec:195)
     d  d_hd08                        2    overlay(d_hrec:205) inz('&&')
     d  d_hcry                       36    overlay(d_hrec:207)
     d  d_hd09                        2    overlay(d_hrec:243) inz('&&')
     d  d_hphn                       16    overlay(d_hrec:245)
     d  d_hd10                        2    overlay(d_hrec:261) inz('&&')
     d  d_hph2                       16    overlay(d_hrec:263)
     d  d_hd11                        2    overlay(d_hrec:279) inz('&&')
     d  d_hfan                       16    overlay(d_hrec:281)
     d  d_hd12                        2    overlay(d_hrec:297) inz('&&')
     d  d_hema                       36    overlay(d_hrec:299)
     d  d_hd13                        2    overlay(d_hrec:335) inz('&&')
     d  d_hfnm                       30    overlay(d_hrec:337)
     d  d_hd14                        2    overlay(d_hrec:367) inz('&&')
     d  d_hlnm                       30    overlay(d_hrec:369)
     d  d_hd15                        2    overlay(d_hrec:399) inz('&&')
     d  d_hprm                       10    overlay(d_hrec:401)
     d  d_hd16                        2    overlay(d_hrec:411) inz('&&')
     d  d_hddt                        8  0 overlay(d_hrec:413)
     d  d_hd17                        2    overlay(d_hrec:421) inz('&&')
     d  d_hoty                        4    overlay(d_hrec:423)
     d  d_hd18                        2    overlay(d_hrec:427) inz('&&')
     d  d_hbun                       20    overlay(d_hrec:429)
     d  d_hd19                        2    overlay(d_hrec:449) inz('&&')
     d  d_hyrf                       36    overlay(d_hrec:451)
     d  d_hd20                        2    overlay(d_hrec:487) inz('&&')
     d  d_hcm1                       60    overlay(d_hrec:489)
     d  d_hd21                        2    overlay(d_hrec:549) inz('&&')
     d  d_hcm2                       60    overlay(d_hrec:551)
     d  d_hd22                        2    overlay(d_hrec:611) inz('&&')
     d  d_hcm3                       60    overlay(d_hrec:613)
     d  d_hd23                        2    overlay(d_hrec:673) inz('&&')
     d  d_hxs1                        1    overlay(d_hrec:675)
     d  d_hd24                        2    overlay(d_hrec:676) inz('&&')
     d  d_hxs2                        1    overlay(d_hrec:678)
     d  d_hd25                        2    overlay(d_hrec:679) inz('&&')
     d  d_hxs3                        1    overlay(d_hrec:681)
     d  d_hd26                        2    overlay(d_hrec:682) inz('&&')
     d  d_hxm1                       10    overlay(d_hrec:684)
     d  d_hd27                        2    overlay(d_hrec:694) inz('&&')
     d  d_hxm2                       10    overlay(d_hrec:696)
     d  d_hd28                        2    overlay(d_hrec:706) inz('&&')
     d  d_hxm3                       10    overlay(d_hrec:708)
     d  d_hd29                        2    overlay(d_hrec:718) inz('&&')
     d  d_hxl1                       30    overlay(d_hrec:720)
     d  d_hd30                        2    overlay(d_hrec:750) inz('&&')
     d  d_hxl2                       30    overlay(d_hrec:752)
     d  d_hd31                        2    overlay(d_hrec:782) inz('&&')
     d  d_hxl3                       30    overlay(d_hrec:784)
     d  d_hd32                        2    overlay(d_hrec:814) inz('&&')
     d  d_hxn1                       15  4 overlay(d_hrec:816)
     d  d_hd33                        2    overlay(d_hrec:831) inz('&&')
     d  d_hxn2                       15  4 overlay(d_hrec:833)
     d  d_hd34                        2    overlay(d_hrec:848) inz('&&')
     d  d_hxn3                       15  4 overlay(d_hrec:850)
     d  d_hd35                        2    overlay(d_hrec:865) inz('&&')
     d  d_hpu1                       16    overlay(d_hrec:867)
     d  d_hd36                        2    overlay(d_hrec:883) inz('&&')
     d  d_hpu2                       16    overlay(d_hrec:885)
     d  d_hd37                        2    overlay(d_hrec:901) inz('&&')
     d  d_hpu3                       16    overlay(d_hrec:903)
      *
      * Detail-record
     d                 ds
     d d_drec                       950
     d  d_dfid                        4    overlay(d_drec)     inz('LINE')
     d  d_dd01                        2    overlay(d_drec:5)   inz('&&')
     d  d_ditn                       20    overlay(d_drec:7)
     d  d_dd02                        2    overlay(d_drec:27)  inz('&&')
     d  d_dnor                        8  3 overlay(d_drec:29)
     d  d_dd03                        2    overlay(d_drec:37)  inz('&&')
     d  d_dunt                        3    overlay(d_drec:39)
     d  d_dd04                        2    overlay(d_drec:42)  inz('&&')
     d  d_dnob                        8  0 overlay(d_drec:44)
     d  d_dd05                        2    overlay(d_drec:52)  inz('&&')
     d  d_dean                       13  0 overlay(d_drec:54)
     d  d_dd06                        2    overlay(d_drec:67)  inz('&&')
     d  d_dcm1                       60    overlay(d_drec:69)
     d  d_dd07                        2    overlay(d_drec:148) inz('&&')
     d  d_dcm2                       60    overlay(d_drec:150)
     d  d_dd08                        2    overlay(d_drec:210) inz('&&')
     d  d_dcm3                       60    overlay(d_drec:212)
     d  d_dd09                        2    overlay(d_drec:272) inz('&&')
     d  d_dddt                        8  0 overlay(d_drec:274)
     d  d_dd10                        2    overlay(d_drec:282) inz('&&')
     d  d_dxs1                        1    overlay(d_drec:284)
     d  d_dd11                        2    overlay(d_drec:285) inz('&&')
     d  d_dxs2                        1    overlay(d_drec:287)
     d  d_dd12                        2    overlay(d_drec:288) inz('&&')
     d  d_dxs3                        1    overlay(d_drec:290)
     d  d_dd13                        2    overlay(d_drec:291) inz('&&')
     d  d_dxm1                       10    overlay(d_drec:293)
     d  d_dd14                        2    overlay(d_drec:303) inz('&&')
     d  d_dxm2                       10    overlay(d_drec:305)
     d  d_dd15                        2    overlay(d_drec:315) inz('&&')
     d  d_dxm3                       10    overlay(d_drec:317)
     d  d_dd16                        2    overlay(d_drec:327) inz('&&')
     d  d_dxl1                       30    overlay(d_drec:329)
     d  d_dd17                        2    overlay(d_drec:359) inz('&&')
     d  d_dxl2                       30    overlay(d_drec:361)
     d  d_dd18                        2    overlay(d_drec:391) inz('&&')
     d  d_dxl3                       30    overlay(d_drec:393)
     d  d_dd19                        2    overlay(d_drec:423) inz('&&')
     d  d_dxn1                       15  4 overlay(d_drec:425)
     d  d_dd20                        2    overlay(d_drec:440) inz('&&')
     d  d_dxn2                       15  4 overlay(d_drec:442)
     d  d_dd21                        2    overlay(d_drec:457) inz('&&')
     d  d_dxn3                       15  4 overlay(d_drec:459)
     d  d_dd22                        2    overlay(d_drec:474) inz('&&')
     d  d_dpu1                       16    overlay(d_drec:476)
     d  d_dd23                        2    overlay(d_drec:492) inz('&&')
     d  d_dpu2                       16    overlay(d_drec:494)
     d  d_dd24                        2    overlay(d_drec:510) inz('&&')
     d  d_dpu3                       16    overlay(d_drec:512)
      *
      * Definering av LDA
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d lodtl1_line     s                   like(ldline)
     d lldilr_ldor     s                   like(lpldor)
     d lldilr_lage     s                   like(lplage)
     d vvarl1_vare     s                   like(vvvare)
      *
      *  Definering av variable i parameter
      *
     d p_numm          s                   like(lonumm)
     d p_suff          s                   like(losuff)
     d p_otyp          s              4
     d p_akod          s              2  0
     d p_aktk          s              2
     d p_luke          s              4  0
      *
      * Definering av øvrige variable
      *
     d w_firm          s                   like(lofirm)
     d w_dalf          s              8
     d w_balf          s              6
     d w_salf          s              2
     d w_wrec          s            950
      *
     d w_hddt          s              8
     d w_hxn1          s             15
     d w_hxn2          s             15
     d w_hxn3          s             15
      *
     d w_dnor          s              8
     d w_dnob          s              8
     d w_dean          s             13
     d w_dddt          s              8
     d w_dxn1          s             15
     d w_dxn2          s             15
     d w_dxn3          s             15
6.11 d w_east          s              1
6.11 d w_eann          s             14  0
      *
     d b_head          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Utlegging av file-interface og heading
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      webrec = d_frec
     c                   write     lwjepfr
      *
     c     lohel1_key    chain     lohel1r
     c                   if        not %found
     c                   goto      end_pgm
     c                   endif
      *
     c                   eval      lldilr_ldor = loldor
     c                   move      lolage        lldilr_lage
     c     lldilr_key    chain     lldilr
     c                   if        not %found
     c                   eval      lldilr_lage = *blank
     c     lldilr_key    chain     lldilr
     c                   if        not %found
     c                   goto      end_pgm
     c                   endif
     c                   endif
      *
     c                   eval      d_hnam = *blank
     c                   eval      d_had1 = *blank
     c                   eval      d_had2 = *blank
     c                   eval      d_hcty = *blank
     c                   eval      d_hpon = *blank
     c                   eval      d_hphn = *blank
     c                   eval      d_hfan = *blank
      *
     c     *iso0         move      lobdat        w_dalf
     c                   move      w_dalf        d_hddt
     c                   eval      d_hbun = lplknr
     c                   move      lonumm        w_balf
     c                   move      losuff        w_salf
     c                   eval      d_hyrf = w_balf + '/' + w_salf
     c                   eval      d_hoty = p_otyp
     c                   movel     p_aktk        d_hxm2
     c                   movel     p_luke        d_hxm3
      *
     c                   if        p_akod <> 0
     c                   movel     p_akod        d_hxm1
     c                   else
     c                   movel     lolage        d_hxm1
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Utlegging av detalj-linjer, utelat tekstlinjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lodtl1_key    setll     lodtl1r
     c     lodtl1_key    reade     lodtl1r
      *
     c                   dow       not %eof
      *
     c                   if        ldltyp <> laatty
      *
     c                   if        b_head = *off
     c                   if        ldlety = 1
     c                   exsr      dir_lev
     c                   endif
     c                   exsr      trim_hrec
     c                   eval      webrec = w_wrec
     c                   write     lwjepfr
     c                   eval      b_head = *on
     c                   endif
      *
     c                   eval      vvarl1_vare = ldvare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   eval      vvlvar = *blank
     c                   endif
6.11  *
6.11  * Kaller program for henting av ean-nr
6.11  *
6.11 c                   call      'VE711R'
6.11 c                   parm                    w_firm
6.11 c                   parm                    ldvare
6.11 c                   parm                    ldenh1
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_east
      *
     c                   if        ldlvar <> *blank
     c                   eval      d_ditn = ldlvar
     c                   else
     c                   eval      d_ditn = vvlvar
     c                   endif
     c                   eval      d_dnob = ldnobb
6.12 c                   if        w_eann < 10000000000000
     c                   eval      d_dean = w_eann
6.12 c                   else
6.12 c                   eval      d_dean = 0
6.12 c                   endif
     c                   eval      d_dnor = ldanta
     c                   eval      d_dunt = ldenh1
     c                   eval      d_dcm1 = ldtek1
     c                   eval      d_dcm2 = ldtek2
     c                   exsr      trim_drec
     c                   eval      webrec = w_wrec
     c                   write     lwjepfr
     c                   endif
      *
     c     lodtl1_key    reade     lodtl1r
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for adressering ved direktelevering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dir_lev       begsr
      *
     c                   eval      d_hnam = lonavn
     c                   eval      d_had1 = loadr1
     c                   eval      d_had2 = loadr2
     c                   eval      d_hcty = losted
     c                   eval      d_hpon = %subst(losted:1:4)
     c                   eval      d_hphn = aaftlf
     c                   eval      d_hfan = aaffax
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for trimming av heading record felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     trim_hrec     begsr
      *
     c                   move      d_hddt        w_hddt
     c                   move      d_hxn1        w_hxn1
     c                   move      d_hxn2        w_hxn2
     c                   move      d_hxn3        w_hxn3
      *
     c                   eval      w_wrec = d_hfid + d_hd01 +
     c                                      %trim(d_hnam) + d_hd02 +
     c                                      %trim(d_had1) + d_hd03 +
     c                                      %trim(d_had2) + d_hd04 +
     c                                      %trim(d_had3) + d_hd05 +
     c                                      %trim(d_hcty) + d_hd06 +
     c                                      %trim(d_hstc) + d_hd07 +
     c                                      %trim(d_hpon) + d_hd08 +
     c                                      %trim(d_hcry) + d_hd09 +
     c                                      %trim(d_hphn) + d_hd10 +
     c                                      %trim(d_hph2) + d_hd11 +
     c                                      %trim(d_hfan) + d_hd12 +
     c                                      %trim(d_hema) + d_hd13 +
     c                                      %trim(d_hfnm) + d_hd14 +
     c                                      %trim(d_hlnm) + d_hd15 +
     c                                      %trim(d_hprm) + d_hd16 +
     c                                      %trim(w_hddt) + d_hd17 +
     c                                      %trim(d_hoty) + d_hd18 +
     c                                      %trim(d_hbun) + d_hd19 +
     c                                      %trim(d_hyrf) + d_hd20 +
     c                                      %trim(d_hcm1) + d_hd21 +
     c                                      %trim(d_hcm2) + d_hd22 +
     c                                      %trim(d_hcm3) + d_hd23 +
     c                                      %trim(d_hxs1) + d_hd24 +
     c                                      %trim(d_hxs2) + d_hd25 +
     c                                      %trim(d_hxs3) + d_hd26 +
     c                                      %trim(d_hxm1) + d_hd27 +
     c                                      %trim(d_hxm2) + d_hd28 +
     c                                      %trim(d_hxm3) + d_hd29 +
     c                                      %trim(d_hxl1) + d_hd30 +
     c                                      %trim(d_hxl2) + d_hd31 +
     c                                      %trim(d_hxl3) + d_hd32 +
     c                                      %trim(w_hxn1) + d_hd33 +
     c                                      %trim(w_hxn2) + d_hd34 +
     c                                      %trim(w_hxn3) + d_hd35 +
     c                                      %trim(d_hpu1) + d_hd36 +
     c                                      %trim(d_hpu2) + d_hd37 +
     c                                      %trim(d_hpu3)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for trimming av detail record felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     trim_drec     begsr
      *
     c                   move      d_dnor        w_dnor
     c                   move      d_dnob        w_dnob
     c                   move      d_dean        w_dean
     c                   move      d_dddt        w_dddt
     c                   move      d_dxn1        w_dxn1
     c                   move      d_dxn2        w_dxn2
     c                   move      d_dxn3        w_dxn3
      *
     c                   eval      w_wrec = d_dfid + d_dd01 +
     c                                      %trim(d_ditn) + d_dd02 +
     c                                      %trim(w_dnor) + d_dd03 +
     c                                      %trim(d_dunt) + d_dd04 +
     c                                      %trim(w_dnob) + d_dd05 +
     c                                      %trim(w_dean) + d_dd06 +
     c                                      %trim(d_dcm1) + d_dd07 +
     c                                      %trim(d_dcm2) + d_dd08 +
     c                                      %trim(d_dcm3) + d_dd09 +
     c                                      %trim(w_dddt) + d_dd10 +
     c                                      %trim(d_dxs1) + d_dd11 +
     c                                      %trim(d_dxs2) + d_dd12 +
     c                                      %trim(d_dxs3) + d_dd13 +
     c                                      %trim(d_dxm1) + d_dd14 +
     c                                      %trim(d_dxm2) + d_dd15 +
     c                                      %trim(d_dxm3) + d_dd16 +
     c                                      %trim(d_dxl1) + d_dd17 +
     c                                      %trim(d_dxl2) + d_dd18 +
     c                                      %trim(d_dxl3) + d_dd19 +
     c                                      %trim(w_dxn1) + d_dd20 +
     c                                      %trim(w_dxn2) + d_dd21 +
     c                                      %trim(w_dxn3) + d_dd22 +
     c                                      %trim(d_dpu1) + d_dd23 +
     c                                      %trim(d_dpu2) + d_dd24 +
     c                                      %trim(d_dpu3)
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for file les FIRMA-OPPLYSNINGER
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for file les LAGER-KODE
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for les av BESTILLING-HODE
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      * Key for oppslag på leverandør distribusjon
      *
     c     lldilr_key    klist
     C                   kfld                    w_firm
     c                   kfld                    lldilr_ldor
     c                   kfld                    lldilr_lage
      *
      *  Key for les av BESTILLING-LINJE
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *entry        plist
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_otyp
     c                   parm                    p_akod
     c                   parm                    p_aktk
     c                   parm                    p_luke
      *
      *  Legger inn verdier i nøklene
      *
     c                   eval      w_firm = l_firm
     c                   eval      lohel1_numm = p_numm
     c                   eval      lohel1_suff = p_suff
     c                   eval      lodtl1_numm = p_numm
     c                   eval      lodtl1_suff = p_suff
      *
      *  Setter startverdier i datastrukturer
      *
     c                   eval      d_hddt = 0
     c                   eval      d_hxn1 = 0
     c                   eval      d_hxn2 = 0
     c                   eval      d_hxn3 = 0
     c                   eval      d_dnor = 0
     c                   eval      d_dddt = 0
     c                   eval      d_dxn1 = 0
     c                   eval      d_dxn2 = 0
     c                   eval      d_dxn3 = 0
      *
      *  Hent opplysninger fra FIRMA og LAGER-KODE
      *
     c     lstsl1_key    chain     lstsl1r                            90
      *
     c     afirl1_key    chain     afirl1r                            90
      *
     c                   endsr
