     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LL506R
      * Beskrivelse . . : Vise lagerinformasjon, disponibelt pr. dato
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
6.nu  * R6.30 110614 bof  Endringer i forb. med nummerutvidelse
6.30  * R6.30 170616 nho  Flytter ut lev.dato fra linje hvis <> 0
7.00  * K00   060520 bsa  Takler brede skjermbilder
7.01  *       230819 sst  Utelate leveringstype = 9
8.01  * 800   211124 ask lagt inn mulighet for å vise
8.01  * 800   211124 ask lagt inn mulighet for å vise
8.01  *                  disponibel beholdning med bekr. NXS-20463
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = avslutt
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     ffodtlv    if   e           k disk    rename(fodtpfr:fodtlvr)
     ffohelr    if   e           k disk    rename(fohepfr:fohelrr)
     flodtlv    if   e           k disk    rename(lodtpfr:lodtlvr)
     flohelr    if   e           k disk    rename(lohepfr:lohelrr)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fll506d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      *  Definering av datastrukturer
      *
      * Transaksjons-array
     d                 ds
6.nu d tr_info                       82    dim(500)
6.nu d  tr_numm                       8  0 overlay(tr_info:1)
6.nu d  tr_suff                       2  0 overlay(tr_info:9)
6.nu d  tr_type                       2    overlay(tr_info:11)
6.nu d  tr_navn                      30    overlay(tr_info:13)
6.nu d  tr_dato                        d   datfmt(*iso)
6.nu d                                     overlay(tr_info:43)
8.01 d  tr_datb                        d   datfmt(*iso)
8.01 d                                     overlay(tr_info:53)
6.nu d  tr_anta                       9  2 overlay(tr_info:63)
8.01 d  tr_dats                        d   datfmt(*iso)
8.01 d                                     overlay(tr_info:72)
8.01 d  tr_syst                       1    overlay(tr_info:82)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
7.01 d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d fodtlv_vare     s                   like(fdvare)
     d fohelr_numm     s                   like(fonumm)
     d fohelr_suff     s                   like(fosuff)
     d lodtlv_vare     s                   like(ldvare)
     d lohelr_numm     s                   like(lonumm)
     d lohelr_suff     s                   like(losuff)
     d vvarl1_vare     s                   like(vvvare)
     d vlagl1_lage     s                   like(vllage)
     d vlagl1_vare     s                   like(vlvare)
     d votyl1_otyp     s                   like(vaotyp)
     d vltyl1_ltyp     s                   like(valtyp)
     d vvenl1_vare     s                   like(vevare)
      *
      * Definering av variable
      *
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
     d ind             s              3  0
      *
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(vlfirm)
     d w_numm          s                   like(lonumm)
     d w_vare          s                   like(vlvare)
     d w_lage          s                   like(lolage)
     d w_latx          s             20
     d w_adrs          s             30
     d w_ponr          s              4  0
     d w_pste          s             30
     d w_avde          s              4  0
     d w_anta          s              9  2
     d w_disp          s              9  2
     d w_enh1          s                   like(ldenh1)
     d w_omrl          s                   like(veomre)
     d w_omrs          s                   like(veomre)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
7.01 d u_s701          s              1    inz(*off)                            Nulle fakturatype
8.01 d u_s801          s              1    inz(*off)
7.01  *
7.01  * Definering av eksterne prg
7.02  *
7.02   dcl-s co402_verdi1  char(1);
7.02   dcl-s co402_verdi2  char(2048);
7.02   DCL-PR CO402R EXTPGM('CO402R');
7.02     p_filgrp            char(3)     const;
7.02     p_firm              packed(3:0) const;
7.02     p_lager             packed(2:0) const;
7.02     p_nokkel            char(50)    const;
7.02     p_verdi1            char(1);
7.02     p_verdi2            char(2048);
7.02   END-PR;
7.02  *
      *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * w_virn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * B4WIN  - B4 Vindu for slette (Delete)
      * B5WIN  - B5 Vindu for masseutskrift
      * B6WIN  - B6 Vindu for masseoverførsel til bestilling
      * B7WIN  - B7 Vindu for vedlikehold av forslagshode
      * B8WIN  - B8 Vindu for overføring til bestilling
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_pgm
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   endsl
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   exsr      lag_head
     c                   eval      ind = 0
     c                   exsr      finn_ordre
     c                   exsr      finn_bestill
     c                   exsr      sort_tab
     c                   eval      ind = 1
     c                   eval      w_spge = w_spge + c_sfil
      *
     c                   dou       ind > 500
      *
     c                   if        tr_dato(ind) > *loval
     c                   eval      w_srrn = w_srrn + 1
     c                   eval      b1numm = tr_numm(ind)
     c                   eval      b1suff = tr_suff(ind)
     c                   eval      b1otyp = tr_type(ind)
     c                   eval      b1navn = tr_navn(ind)
     c                   eval      b1anta = tr_anta(ind)
8.01  *
8.01 c                   if        u_s801 = *on
8.01 c                   if        tr_datb(ind) > *loval
8.01 c     *dmy          move      tr_datb(ind)  b1dato
8.01 c     *dmy          move      tr_datb(ind)  b1datb
8.01 c                   eval      w_disp = w_disp + tr_anta(ind)
8.01 c                   eval      b1antc = w_disp
8.01 c                   else
8.01 c     *dmy          move      tr_dato(ind)  b1dato
8.01 c                   eval      b1datb = 0
8.01 c                   if        tr_syst(ind) = 'B'
8.01 c                   eval      b1antc = w_disp
8.01 c                   else
8.01 c                   eval      w_disp = w_disp + tr_anta(ind)
8.01 c                   eval      b1antc = w_disp
8.01 c                   endif
8.01 c                   endif
8.01 c                   eval      *in30 = *on
8.01 c                   else
8.01 c     *dmy          move      tr_dato(ind)  b1dato
8.01 c                   eval      *in30 = *off
     c                   eval      w_disp = w_disp + tr_anta(ind)
     c                   eval      b1antc = w_disp
8.01 c                   endif
      *
     c                   if        b1antc < 0
     c                   eval      *in41 = *on
     c                   else
     c                   eval      *in41 = *off
     c                   endif
      *
     c                   write     b1sfl
     c                   endif
      *
     c                   eval      ind = ind + 1
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   eval      *in15 = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for bygging av heading
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lag_head      begsr
      *
      * Nullstiller tabell
      *
     c                   movea     *blank        tr_info
     c                   eval      ind = 1
      *
     c                   dou       ind > 500
     c                   eval      tr_numm(ind) = 0
     c                   eval      tr_suff(ind) = 0
     c                   eval      tr_dato(ind) = *loval
8.01 c                   eval      tr_datb(ind) = *loval
8.01 c                   eval      tr_dats(ind) = *loval
     c                   eval      tr_anta(ind) = 0
     c                   eval      tr_type(ind) = *blank
     c                   eval      tr_navn(ind) = *blank
8.01 c                   eval      tr_syst(ind) = *blank
     c                   eval      ind = ind + 1
     c                   enddo
      *
      * Hent vare informasjon
      *
     c                   movel     b2vare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1r                            90
      *
     c                   movel     vvtek1        b2tek1
     c                   eval      b2enhl = vvenhl
      *
      * Hent lager informasjon
      *
     c                   exsr      sjekk_lager
      *
     c                   movel     b2vare        vlagl1_vare
     c                   eval      vlagl1_lage = b2lage
     c     vlagl1_key    chain     vlagl1r                            90
      *
     c                   eval      w_srrn = 1
     c                   eval      b1numm = 0
     c                   eval      b1suff = 0
     c                   eval      b1otyp = *blank
     c                   eval      b1navn = 'Beholdning'
     c                   eval      b1dato = 0
     c                   eval      b1anta = 0
     c                   eval      b1antc = vlbehl
     c                   eval      w_disp = vlbehl
     c                   write     b1sfl
      *
     c                   if        vlpakk <> 0
     c                   eval      w_srrn = w_srrn + 1
     c                   eval      b1numm = 0
     c                   eval      b1suff = 0
     c                   eval      b1otyp = *blank
     c                   eval      b1navn = 'For utlevering'
     c                   eval      b1dato = 0
     c                   eval      b1anta = vlpakk * -1
     c                   eval      b1antc = vlbehl - vlpakk
     c                   eval      w_disp = w_disp - vlpakk
     c                   write     b1sfl
     c                   endif
      *
     c     end_head      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for lesing av ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_ordre    begsr
      *
      *  Legger inn startverdi for lesingen
      *
     c                   eval      *in90 = *off
     c                   movel     b2vare        fodtlv_vare
     c     fodtlv_key    setll     fodtlv
     c     fodtlv_key    reade     fodtlvr                                90
      *
     c                   dow       *in90 = *off
      *
      * Sjekk linjetype
      *
     c                   eval      vltyl1_ltyp = fdltyp
     c     vltyl1_key    chain     vltyl1                             91
     c                   if        vallag <> 1
     c                   goto      les_ordre
     c                   endif
      *
      * Sjekk leveringstype
      *
7.01 c                   if        u_s701 = *on
7.01 c                   if        fdlety = 1
7.01 c                             or fdlety = 9
7.01 c                   goto      les_ordre
7.01 c                   endif
7.01 c                   else
     c                   if        fdlety = 1
     c                   goto      les_ordre
     c                   endif
7.01 c                   endif
      *
      *  Hent ordrehode informasjon
      *
     c                   eval      fohelr_numm = fdnumm
     c                   eval      fohelr_suff = fdsuff
     c     fohelr_key    chain     fohelr                             92
     c                   if        *in92 = *on
     c                   goto      les_ordre
     c                   endif
      *
      * Sjekk riktig ordretype og lageroppdateringskode
      *
     c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1                             93
     c                   if        vaosys <> 0 or
     c                             vaoakk <> 1 or
     c                             vaolag <> 1
     c                   goto      les_ordre
     c                   endif
      *
      * Sjekk riktig lager
      *
     c                   if        fdlage <> b2lage
     c                   goto      les_ordre
     c                   endif
      *
      * Sjekk hvilken ordredato som er registrert
      *
     c                   eval      ind = ind + 1
     c                   if        foldat <> *loval
     c                   eval      tr_dato(ind) = foldat
8.01 c                   eval      tr_dats(ind) = foldat
     c                   else
     c                   eval      tr_dato(ind) = fobdat
8.01 c                   eval      tr_dats(ind) = fobdat
     c                   endif
      *
6.30 c                   if        fdldat <> *loval
6.30 c                   eval      tr_dato(ind) = fdldat
8.01 c                   eval      tr_dats(ind) = fdldat
6.30 c                   endif
6.30  *
8.01 c                   eval      tr_datb(ind) = tr_dato(ind)
     c                   eval      tr_numm(ind) = fonumm
     c                   eval      tr_suff(ind) = fosuff
     c                   eval      tr_type(ind) = footyp
     c                   eval      tr_navn(ind) = fokuna
8.01 c                   eval      tr_syst(ind) = 'O'
      *
     c                   if        fdenh1 <> vvenhl
     c                   eval      w_anta = fdanta
     c                   eval      w_enh1 = fdenh1
     c                   exsr      omr_antall
     c                   else
     c                   eval      w_anta = fdanta
     c                   endif
      *
     c                   if        vaokre = '-'
     c                   eval      w_anta = w_anta * -1
     c                   endif
      *
     c                   if        valkre = '-'
     c                   eval      w_anta = w_anta * -1
     c                   endif
      *
     c                   eval      tr_anta(ind) = w_anta
     c                   eval      tr_anta(ind) = tr_anta(ind) * -1
      *
     c     les_ordre     tag
     c     fodtlv_key    reade     fodtlvr                                90
     c                   enddo
      *
     c     end_ordre     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for lesing av bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_bestill  begsr
      *
      *  Legger inn startverdi for lesingen
      *
     c                   movel     b2vare        lodtlv_vare
     c     lodtlv_key    setll     lodtlv
     c     lodtlv_key    reade     lodtlvr                                90
      *
     c                   dow       *in90 = *off
      *
      * Sjekk linjetype
      *
     c                   eval      vltyl1_ltyp = ldltyp
     c     vltyl1_key    chain     vltyl1                             91
     c                   if        vallag <> 1
     c                   goto      les_best
     c                   endif
      *
      * Sjekk leveringstype
      *
7.01 c                   if        u_s701 = *on
7.01 c                   if        ldlety = 1
7.01 c                             or ldlety = 9
7.01 c                   goto      les_best
7.01 c                   endif
7.01 c                   else
     c                   if        ldlety = 1
     c                   goto      les_best
     c                   endif
7.01 c                   endif
      *
      *  Hent bestillingshode informasjon
      *
     c                   eval      lohelr_numm = ldnumm
     c                   eval      lohelr_suff = ldsuff
     c     lohelr_key    chain     lohelr                             92
     c                   if        *in92 = *on
     c                   goto      les_best
     c                   endif
      *
      * Sjekk riktig ordretype og lageroppdateringskode
      *
     c                   eval      votyl1_otyp = lootyp
     c     votyl1_key    chain     votyl1                             93
     c                   if        vaosys <> 1 or
     c                             vaoakk <> 1 or
     c                             vaolag <> 1
     c                   goto      les_best
     c                   endif
      *
      * Sjekk riktig lager
      *
     c                   if        lolage <> b2lage
     c                   goto      les_best
     c                   endif
      *
      * Sjekk hvilken leveringsdato som er registrert
      *
     c                   eval      ind = ind + 1
8.01  *
8.01 c                   if        u_s801 = *on
     c                   if        lomoda <> *loval
     c                   if        ldldat <> *loval
     c                   eval      tr_dato(ind) = ldldat
8.01 c                   eval      tr_datb(ind) = ldldat
8.01 c                   eval      tr_dats(ind) = ldldat
     c                   else
8.01 c                   eval      tr_dato(ind) = lomoda
8.01 c                   eval      tr_datb(ind) = lomoda
8.01 c                   eval      tr_dats(ind) = lomoda
8.01 c                   endif
     c                   else
     c                   if        loldat <> *loval
     c                   eval      tr_dato(ind) = loldat
8.01 c                   eval      tr_datb(ind) = *loval
8.01 c                   eval      tr_dats(ind) = loldat + %days(365)
     c                   else
     c                   eval      tr_dato(ind) = lobdat
8.01 c                   eval      tr_datb(ind) = *loval
8.01 c                   eval      tr_dats(ind) = lobdat + %days(365)
     c                   endif
     c                   endif
8.01 c                   else
     c                   if        ldldat <> *loval
     c                   eval      tr_dato(ind) = ldldat
8.01 c                   eval      tr_dats(ind) = ldldat
     c                   else
     c                   if        lomoda <> *loval
     c                   eval      tr_dato(ind) = lomoda
8.01 c                   eval      tr_dats(ind) = lomoda
     c                   else
     c                   if        loldat <> *loval
     c                   eval      tr_dato(ind) = loldat
8.01 c                   eval      tr_dats(ind) = loldat
     c                   else
     c                   eval      tr_dato(ind) = lobdat
8.01 c                   eval      tr_dats(ind) = lobdat
     c                   endif
     c                   endif
     c                   endif
8.01 c                   endif
      *
     c                   eval      tr_numm(ind) = lonumm
     c                   eval      tr_suff(ind) = losuff
     c                   eval      tr_type(ind) = ldotyp
     c                   eval      tr_navn(ind) = lolena
8.01 c                   eval      tr_syst(ind) = 'B'
      *
     c                   if        ldenh1 <> vvenhl
     c                   eval      w_anta = ldanta
     c                   eval      w_enh1 = ldenh1
     c                   exsr      omr_antall
     c                   else
     c                   eval      w_anta = ldanta
     c                   endif
      *
     c                   if        vaokre = '-'
     c                   eval      w_anta = w_anta * -1
     c                   endif
      *
     c                   if        valkre = '-'
     c                   eval      w_anta = w_anta * -1
     c                   endif
      *
     c                   eval      tr_anta(ind) = w_anta
      *
     c     les_best      tag
     c     lodtlv_key    reade     lodtlvr                                90
     c                   enddo
      *
     c     end_bestill   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sortering av tabell
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sort_tab      begsr
      *
     c                   sorta     tr_dats
      *
     c     end_sort      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kontroll av lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_lager   begsr
      *
     c                   eval      b_feil = *off
     c                   call      'FÅ720R'
     c                   parm                    w_firm
     c                   parm                    b2lage
     c                   parm                    b2ltek
     c                   parm                    w_adrs
     c                   parm                    w_ponr
     c                   parm                    w_pste
     c                   parm                    w_avde
     c                   parm                    b_feil
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for omregning til lagerenhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     omr_antall    begsr
      *
      * Finner omregningsfaktor volum for lagerenhet og for opptalt enhet
      * Antall regnes om etter forholdet mellom disse
      *
     c                   movel     b2vare        vvenl1_vare
     c     vvenl1_key    setll     vvenl1
     c     vvenl1_key    reade     vvenl1                                 91
     c                   dow       *in91 = *off
     c                   if        w_enh1 = veenhe
     c                   eval      w_omrl = veomre
     c                   endif
     c                   if        vvenhl = veenhe
     c                   eval      w_omrs = veomre
     c                   endif
     c     vvenl1_key    reade     vvenl1                                 91
     c                   enddo
      *
     c                   if        w_omrs = 0
     c                   eval      w_omrs = 1
     c                   endif
      *
     c                   if        w_omrl <> 0
     c                   eval      w_anta = w_anta * w_omrl / w_omrs
     c                   else
     c                   eval      w_anta = 0
     c                   endif
      *
     c     end_omr       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av ORDRE-LINJE
      *
     c     fodtlv_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlv_vare
      *
      *  Key les av ORDRE-HODE
      *
     c     fohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelr_numm
     c                   kfld                    fohelr_suff
      *
      *  Key for les av BESTILLINGS-LINJE
      *
     c     lodtlv_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlv_vare
      *
      *  Key les av BESTILLINGS-HODE
      *
     c     lohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelr_numm
     c                   kfld                    lohelr_suff
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *  Key for les av LAGER
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
      *
      *  Key for les av ORDRE-TYPE
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      *  Key for les av LINJETYPE
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
      * Key for les av vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    w_vare
     c                   parm                    w_lage
      *
      * Setter inn startverdier
      *
     c                   eval      w_firm = l_firm
     c                   eval      b2vare = w_vare
     c                   eval      b2lage = w_lage
      *
7.01  *
7.01  * Sjekk mot leveringstype 9
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'LL506_701':
7.01   co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_s701 = *on;
7.01   endif;
7.01  *
8.01  *
8.01  * Endring 8.03
8.01  *
8.01   CallP CO402R(l_filg:w_firm:0:'BESTILLING_DISP':
8.01                    co402_verdi1:co402_verdi2);
8.01   if co402_verdi1 = '1';
8.01     u_s801 = *on;
8.01   endif;
     c                   endsr
      *
