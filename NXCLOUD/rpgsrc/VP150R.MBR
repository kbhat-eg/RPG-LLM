     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VP150R
      * Beskrivelse . . : Vare-pris, prisjustering - parameter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       010699 HKO  Nytt program
      *       210200 HKO  Lagt inn valg for utelating av varer i.h.t.
      *                   priskode
      *       230304 GRO  Lagt til leveringstype som parameter
5.70  *       051108 bof  Utvidet med prisgruppe
7.xx  * 7.00  290916 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
5.70 fra30l1    if   e           k disk    rename(ra30pfr:ra30l1r)
      *
     fvp150d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable i nøkler
      *
     d rlevl1_levr     s                   like(rllevr)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
5.70 d ra30l1_dkod     s                   like(redkod)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
     d w_udat          s               d   datfmt(*iso)
     d w_dato          s               d   datfmt(*iso)
     d w_varf          s             15
     d w_vart          s             15
     d w_tek1          s             35
     d w_prgr          s                   like(redkod)
      *
     d w_firm          s                   like(rlfirm)
      *
      * Definering av konstanter
      *
     d digits          c                   ' 0123456789'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer skjermbildet
      *
     c                   time                    w_udat
     c     *dmy          move      w_udat        b1dato
      *
      * Behandler bildet
      *
     c     b1taga        tag
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
      * Kaller oppdateringsprogram
      *
     c     *dmy          move      b1dato        w_dato
      *
     c                   if        b1varf <> 0
     c                   movel     b1varf        w_varf
     c                   endif
      *
     c                   if        b1vart <> 0
     c                   movel     b1vart        w_vart
     c                   endif
      *
     c                   call      'VP151R'
     c                   parm                    w_firm
     c                   parm                    b1ldor
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    b1ugrp
     c                   parm                    w_varf
     c                   parm                    w_vart
     c                   parm                    w_dato
     c                   parm                    b1spro
     c                   parm                    b1kpro
     c                   parm                    b1ipro
     c                   parm                    b1bpro
     c                   parm                    b1dekn
     c                   parm                    b1nypr
     c                   parm                    b1orpv
     c                   parm                    b1ntpv
     c                   parm                    b1bupv
     c                   parm                    b1lety
5.70 c                   parm                    b1prgr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørringer (FD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Leverandør
      *
     c                   if        w_afld = 'B1LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    b1ldor
     c                   parm                    b1navn
     c                   goto      end_spØrr
     c                   endif
      *
      * Overgruppe
      *
     c                   if        w_afld = 'B1OGRP'
     c                   eval      b1hgrp = 0
     c                   eval      b1ugrp = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1utxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_afld = 'B1HGRP'
     c                   eval      b1ugrp = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    b1utxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_afld = 'B1UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    b1ugrp
     c                   parm                    b1utxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Varenummer
      *
     c                   if        w_afld = 'B1VARF'
     c                   movel     b1varf        w_varf
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_varf
     c                   parm                    w_tek1
     c                   movel     w_varf        b1varf
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'B1VART'
     c                   movel     b1vart        w_vart
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vart
     c                   parm                    w_tek1
     c                   movel     w_vart        b1vart
     c                   goto      end_spØrr
     c                   endif
      *
5.70 c                   if        w_afld = 'B1PRGR'
5.70 c                   movel     b1prgr        w_prgr
5.70 c                   call      'RA530R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    w_prgr
5.70 c                   parm                    w_tek1
5.70 c                   movel     w_prgr        b1prgr
5.70 c                   goto      end_spØrr
5.70 c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
      * Initierer variable
      *
     c                   eval      b1navn = *blank
     c                   eval      b1utxt = *blank
      *
      * Utplukk
      *
     c                   if        b1ldor = 0 and
     c                             b1ogrp = 0 and
     c                             b1hgrp = 0 and
     c                             b1ugrp = 0 and
     c                             b1varf = 0 and
     c                             b1vart = 0
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Leverandør
      *
     c                   if        b1ldor <> 0
     c                   eval      rlevl1_levr = b1ldor
     c     rlevl1_key    chain     rlevl1                             90
     c                   if        *in90  = *on
     c                   eval      *in32  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1navn = rlnavn
     c                   endif
      *
      * Overgruppe
      *
     c                   if        b1ogrp <> 0
     c                   eval      vogrl1_oogr = b1ogrp
     c     vogrl1_key    chain     vogrl1                             90
     c                   if        *in90  = *on
     c                   eval      *in33  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1utxt = vgotxt
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        b1hgrp <> 0
     c                   eval      vhgrl1_hogr = b1ogrp
     c                   eval      vhgrl1_hhgr = b1hgrp
     c     vhgrl1_key    chain     vhgrl1                             90
     c                   if        *in90  = *on
     c                   eval      *in34  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1utxt = vghtxt
     c                   endif
      *
      * Undergruppe
      *
     c                   if        b1ugrp <> 0
     c                   eval      vugrl1_uogr = b1ogrp
     c                   eval      vugrl1_uhgr = b1hgrp
     c                   eval      vugrl1_uugr = b1ugrp
     c     vugrl1_key    chain     vugrl1                             90
     c                   if        *in90  = *on
     c                   eval      *in35  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1utxt = vgutxt
     c                   endif
      *
      * Varenummer
      *
     c                   if        b1varf <> 0 and
     c                             b1vart = 0
     c                   eval      b1vart = b1varf
     c                   endif
      *
     c                   if        b1varf > b1vart
     c                   eval      *in36  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Dato
      *
     c                   if        b1dato = 0
     c     *dmy          move      w_udat        b1dato
     c                   endif
      *
     c     *dmy          test(d)                 b1dato                 37
     c                   if        *in37  = *on                                          .....
     c                   eval      b_feil = *on
     c                   goto      end_sjekk                                                 .
     c                   endif                                                           .....
      *
     c     *dmy          move      b1dato        w_dato
     c                   if        w_dato < w_udat
     c                   eval      *in38  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk                                                 .
     c                   endif
      *
      * Justering
      *
     c                   if        b1spro = 0 and
     c                             b1kpro = 0 and
     c                             b1ipro = 0 and
     c                             b1bpro = 0 and
     c                             b1dekn = 0
     c                   eval      *in39  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   if        b1spro <> 0 and
     c                             b1dekn <> 0
     c                   eval      *in40  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Utelat oppdatering på priskode
      *
     c                   if        b1orpv = 1 and
     c                             b1ntpv = 1 and
     c                             b1bupv = 1
     c                   eval      *in41  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Sjekk leveringstype (blank eller numerisk)
      *
     c                   if        %check(digits : b1lety) > 0
     c                   eval      *in42  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
5.70  *
5.70  * Prisgruppe
5.70  *
5.70 c                   if        b1prgr <> *blank
5.70 c                   eval      ra30l1_dkod = b1prgr
5.70 c     ra30l1_key    chain     ra30l1                             90
5.70 c                   if        *in90  = *on
5.70 c                   eval      *in43  = *on
5.70 c                   eval      b_feil = *on
5.70 c                   goto      end_sjekk
5.70 c                   endif
5.70 c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
5.70  *
5.70  * Key for oppslag på prisgrupee
5.70  *
5.70 c     ra30l1_key    klist
5.70 c                   kfld                    w_firm
5.70 c                   kfld                    ra30l1_dkod
      *
      *
      * Henter firmanummer fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
