     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LH102R
      * Beskrivelse . . : Bestillingsforslag, vedlikehold detaljlinjer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       210300 ASK  Nytt program
      *       040604 bø   Tilpasset mot nye bestillingsrutiner
      *       040604 bø   Lagt inn ny std. ellers opprinnelig LH102R
      *       010904 bø   Justert parameter mot status best.forslag
      *       010205 bø   Tillater også endre status 1 til 0
      *       080305 bø   Test på om lev.på vare er lik den på heading.
      *       110305 bø   Justert status vedr. om vare på bestilling.
      *       150305 bø   Parameter for styre om status brukes
      *                   Midlertidig hardkodet, legge kode på kodereg. v.540
      *       080805 HKO  Henter vekt fra vare-enhet
      *       240805 bø   Kode for å styre status på plass
      *       090307 bø   Skift-tast for å vise inkjøpsenhet og antall
      *       150307 bø   Tar aldri best.pkt,min,mm. fra varereg. kun lager
      *       040607 bø   Hvis vare er sperret fra prognoseoppdatering, gis M
      *       010409 ASK  Lagt inn firmakode ved manuell opprett av linje
5.61  *       200409 bof  Justert hent av sperret fra prognoseoppdatering.
5.70  * PRGR  021208 bof  Utvidet med prisgruppe
      * 6.10  170609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  040909 bof  Utvidet med Kjedesortiment
6.12  * 6.10  140909 bof  Utvidet med pakningsklassen
6.13  * 6.10  021009 bof  Sjekker på om enhet er bestillbar pr. prisgiver
6.14  *       300910 bof  valg 8 og 9 endret valg av felter for kall
6.15  * 6.10  071110 bof  Sjekker at vare ikke er skaffevare
6.20  * 6.20  021110 bof  Utvidet med utg.dato pr. lager
6.21  * 6.20 101110 bof  Utvidet med leverandør på lager
6.22  * 6.20 070211 nho  Byttet ut fil mpspxx med vlagxx
6.23  * 6.20  151210 bof  Utvidet med tekstlinjer, sorterer på linjenr
6.23  *                   hvis det er kundeordre.
6.24  * 6.20  161210 bof  nytt valg - gå til innkjøpsplanlegging valg 6
6.25  * 6.20  161210 bof  Viser bpkt/bmen eller min/max ut fra hva parameter
6.26  * 6.20  190511 bø   Utvidet vl710r med div. info
6.27  * 6.20  120112 bsa  Utvidet vp750r med kampanje
6.28  * 6.20  100412 nho  Endret antall linjer på siden
6.29  * 6.20  250113 bof  Lagt til en ny status
6.2a  * 6.20  140313 bof  tatt en clear før les og write
6.2b  * 6.20  210313 bof  rettet opp "grove" feil, vedr. felt vedr. skriv
6.2c  * 6.20  151113 bsa  Bruker innkjøpspris og ikke kostpris ved ut-
6.2c  *                   legg av verdi på bestillingsforslag.
6.30  * 6.30  060214 bsa  Forklarer status på erstatningsvare. Behandler
6.30  *                   kode E.
6.nu  * 6.30  260614 bof  Sjekket mht. nummerutvidelse
6.31  * 6.30  280415 bof  Rettelse vedr subfile og erstatningsvare-status
6.32  * 6.30  280415 bof  Rettelse vedr. innkjøpspris fra kampanje
6.33  * 6.30  290515 bsa  Erstatningsvare gjelder kun for de med BM_MODUL.
6.34  * 6.30  231015 bof  Hvis lagerenhet, så ikke test på om enhet er innk.enhet
6.34x * 6.30  050116 bof  omskriving av noen reade
6.35  * 6.30  050116 bof  Henter linjebeløp på nytt ut fra gjeldende enhet.
6.36  * 6.30  160216 bof  Justering på vise status erstatningsvare
6.37  * 6.30  270416 nho  Lager må være med inn best. sjekkes
      *                   Byttet ut LODTLV med LODTLX
6.38  * 6.30  130916 nho  Viser full varetekst dersom 5 tastes foran   het.
      *                   tastes foran varelinja
6.39  * 6.30  021116 bsa  Viser status for erstatningsvare uavhengig av
6.39  *                   tilgang til modul, siden dette gir jacada trøbbel
6.39  *                   Reverserer endring 6.36
6.3a  * 6.30  170918 bkv  Utvidet med trelast sortiment
6.3b  * 6.30  270519 bof  Henter innkj.pris fra evr kampanje.  (BM tilp.)
7.xx  * 7.00  170316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  110919 bkv  Henter vekt ved endring av enhet
7.02  * 7.00  160120 bkv  Rettelse på 6.3b. Fjerner setting av IN50
7.03  * K00   270420 bsa  Utvidet til bredt skjermbilde og lagt til felter
7.04  * K00   100221 ask  Håndterer desimaler på vektvisning endringsbilde (B3)
      *
8.01  * K000  180222 bsa  Utvider kall til pris med lager fra bestillingsforslaget
8.01  * K000              Ønsker ikke bruke lager fra pålogget bruker, da det kan
8.01  * K000              gi feil leverandør ved overstyring på lager.
8.02  * 800   080621 ask  Lagt inn fjerning av statuskode 6 om bestilling er til ordre
8.02  *                   Styrt av switch 'BESTF_STATUS6'
8.03  *PRG2   150622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flfhelr    if   e           k disk    rename(lfhepfr:lfhelrr)
     flfhelu    uf   e           k disk    rename(lfhepfr:lfhelur)
6.23 flfdtlr    if   e           k disk    rename(lfdtpfr:lfdtlrr)
     flfdtl1    if   e           k disk    rename(lfdtpfr:lfdtl1r)
     flfdtlu    uf a e           k disk    rename(lfdtpfr:lfdtlur)
     f*fdtl2    if   e           k disk    rename(lfdtpfr:lfdtl2r)
     flfdtl4    if   e           k disk    rename(lfdtpfr:lfdtl4r)
6.37 f*lodtlv    if   e           k disk    rename(lodtpfr:lodtlvr)
6.37 flodtlx    if   e           k disk    rename(lodtpfr:lodtlxr)
     flohelr    if   e           k disk    rename(lohepfr:lohelrr)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     fvotylr    if   e           k disk    rename(votypfr:votylrr)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvvenlr    if   e           k disk    rename(vvenpfr:vvenlrr)
6.13 fvveplr    if   e           k disk    rename(vveppfr:vveplrr)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
6.25 fmppalr    if   e           k disk    rename(mppapfr:mppalrr)
6.30 fvverl1    if   e           k disk    rename(vverpfr:vverl1r)
6.3a fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
7.03 fjvpkl2    if   e           k disk    rename(jvpkpfr:jvpkl2r)
7.03 fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
7.03 fvvasl1    if   e           k disk    rename(vvaspfr:vvasl1r)
      *
     flh102d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
6.3b d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d lfhelr_numm     s                   like(lhnumm)
     d lfhelu_numm     s                   like(lhnumm)
     d rlevl1_levr     s                   like(rllevr)
6.23 d lfdtlr_numm     s                   like(lfnumm)
6.23 d lfdtlr_line     s                   like(lfline)
     d lfdtl1_numm     s                   like(lfnumm)
     d lfdtl1_line     s                   like(lfline)
     d*lfdtl2_numm     s                   like(lfnumm)
     d lfdtlu_numm     s                   like(lfnumm)
     d lfdtlu_line     s                   like(lfline)
     d lfdtl4_numm     s                   like(lfnumm)
     d lfdtlx_numm     s                   like(lfnumm)
     d lfdtl4_vare     s                   like(lfvare)
     d vvarl1_vare     s                   like(vvvare)
     d vlagl1_vare     s                   like(vlvare)
     d vlagl1_lage     s                   like(vllage)
     d vvenlr_vare     s                   like(vevare)
     d vvenlr_enhe     s                   like(veenhe)
6.13 d vveplr_pvar     s                   like(vepvar)
6.13 d vveplr_penh     s                   like(vepenh)
6.13 d vveplr_pldo     s                   like(vepldo)
     d vvenlx_vare     s                   like(vevare)
6.37 d*lodtlv_vare     s                   like(ldvare)
6.37 d lodtlx_lage     s                   like(lhlage)
6.37 d lodtlx_vare     s                   like(ldvare)
     d lohelr_numm     s                   like(lonumm)
     d lohelr_suff     s                   like(losuff)
     d votylr_otyp     s                   like(vaotyp)
6.22 d mppalr_ldor     s                   like(mpldor)
6.22 d mppalr_lnum     s                   like(mplnum)
6.30 d vverl1_envr     s                   like(vvenvr)
7.03 d jvpkl2_vare     s                   like(jwvare)
7.03 d jvpkl2_ldor     s                   like(jwldor)
7.03 d jvpkl2_enhe     s                   like(jwenhe)
7.03 d vvenl1_vare     s                   like(vevare)
7.03 d vvenl1_enhe     s                   like(veenhe)
7.03 d vvasl1_vare     s                   like(vvvare)
      *
      *
      * Definering av variabler i parametre
      *
     d p_numm          s                   like(lfnumm)
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_sts0          s              1    inz(*off)
     d b_sts1          s              1    inz(*off)
     d b_sts2          s              1    inz(*off)
     d b_sts3          s              1    inz(*off)
     d b_sts4          s              1    inz(*off)
     d b_sts5          s              1    inz(*off)
     d b_sts6          s              1    inz(*off)
     d b_f09           s              1    inz(*off)
6.11 d b_ksor          s              1    inz(*off)
6.20 d b_inlr          s              1    inz(*off)
6.23 d b_kord          s              1    inz(*off)
6.30 d b_repl          s              1    inz(*off)
6.33 d b_bmpp          s              1    inz(*off)
      *
     d w_firm          s                   like(lffirm)
     d w_numm          s                   like(lfnumm)
     d w_line          s                   like(lfline)
     d w_slin          s                   like(lfline)
     d w_vare          s                   like(lfvare)
     d w_lage          s                   like(lflage)
     d w_ldor          s                   like(lhldor)
6.3a d w_lv_nobb       s                   like(vvlnob)
6.3a d w_lv_modn       s                   like(vvlmod)
6.3a d w_lv_lldo       s                   like(vvlnle)
6.3a d w_lv_llva       s                   like(vvllva)
     d w_enh1          s                   like(lfenh1)
     d w_bmen          s                   like(vlbmen)
     d w_bmin          s                   like(vlbmin)
6.25 d w_maks          s                   like(vlmaks)
6.25 d w_mini          s                   like(vlmini)
7.03 d w_bpun          s                   like(vlbpun)
     d w_letx          s             30
     d w_vtxt          s             35
     d w_etxt          s             35
     d w_lety          s              1  0
     d w_kpny          s              9  2
     d w_ipny          s              9  2
6.20 d w_ddat          s               d   datfmt(*iso)
6.20 d w_udat          s               d   datfmt(*iso)
     d w_sum           s             11  2
     d w_112           s             11  2
     d w_stat          s                   like(lfstat)
     d w_anta          s                   like(lfanta)
     d w_tbel          s                   like(lhtbel)
     d w_tvkt          s             11  3
     d w_omrl          s                   like(veomre)
     d w_omri          s                   like(veomre)
5.61 d w_manu          s              1
5.70 d w_avde          s              4  0
8.03 d w_prgr          s              2
6.15 d w_vtyp          s                   like(vlvtyp)
6.27 d w_kamp          s              5
6.30 d w_enhe          s                   like(lfenh1)
6.32 d w_vvar          s                   like(lfvare)
6.32 d w_vldo          s                   like(lhldor)
6.32 d w_vkai          s              9  2
6.33 d w_disp          s              1
6.33 d w_msts          s              1
6.33 d w_modu          s             10
7.03 d w_vekt          s              9  3
7.03 d w_volu          s              9  3
7.03 d w_avvi          s              1
7.03 d w_utda          s                   like(vvudat)
      *
6.3b d u_63d           s              1    inz(*off)
8.02 d u_802           s              1    inz(*off)
6.3b  * Definering av eksterne prg
6.3b  *
6.3b   dcl-s co402_verdi1  char(1);
6.3b   dcl-s co402_verdi2  char(2048);
6.3b   DCL-PR CO402R EXTPGM('CO402R');
6.3b     p_filgrp            char(3)     const;
6.3b     p_firm              packed(3:0) const;
6.3b     p_lager             packed(2:0) const;
6.3b     p_nokkel            char(50)    const;
6.3b     p_verdi1            char(1);
6.3b     p_verdi2            char(2048);
6.3b   END-PR;
      *
      * Definering av konstanter
      *
7.03 d c_sfil          s              2  0 inz(16)
6.33 d c_modu          c                   'BM_MODUL  '
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inka
     c                   exsr      xh1win
     c                   exsr      forny
     c                   goto      b2taga
     c                   when      *inkc or *inkl
     c                   goto      end_pgm
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkd
     c                   exsr      sp_input
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xb6win
     c                   exsr      finn_sum
     c                   eval      b2tbel = w_sum
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inki
     c                   if        b_f09 = *off
     c                   eval      b_f09 = *on
     c                   else
     c                   eval      w_fcrn = 0
     c                   eval      b1vare = *blank
     c                   eval      b_f09 = *off
     c                   endif
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkj
     c                   exsr      xb10win
     c                   exsr      forny
     c                   goto      b2tagb
7.03 c*                  when      *inkk
7.03 c*                  if        *in51 = *off
7.03 c*                  eval      *in51 = *on
7.03 c*                  else
7.03 c*                  eval      *in51 = *off
7.03 c*                  endif
7.03 c*                  exsr      forny
7.03 c*                  goto      b2taga
     c                   when      *inkm
     c                   exsr      xh2win
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2vare <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     end_pgm       tag
      *
     c                   eval      w_numm = p_numm
     c                   eval      w_tbel = 0
     c                   eval      w_tvkt = 0
     c                   eval      w_stat = *blank
     c                   call      'LH714R '
     c                   parm                    w_numm
     c                   parm                    w_stat
     c                   parm                    w_tbel
     c                   parm                    w_tvkt
      *
     c                   eval      lfhelu_numm = p_numm
     c     lfhelu_key    chain     lfhelu
     c                   if        %found(lfhelu)
     c                   eval      lhtbel = w_tbel
     c                   eval      lhvekt = w_tvkt
     c                   eval      lhstat = w_stat
6.10 c                   time                    lhedat
6.10 c                   time                    lhetim
6.10 c                   eval      lheusr = l_user
     c                   update    lfhelur
     c                   endif
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
      *
6.23 c                   if        b_kord = *on
6.23 c                   eval      lfdtl1_line = b1line
6.23 c     lfdtl1_key    setll     lfdtl1
6.23 c                   else
     c                   eval      lfdtl4_vare = b1vare
     c     lfdtl4_key    setll     lfdtl4
6.23 c                   endif
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      *
      * Posisjoner startverdi på leverandør
      *
      * Posisjoner startverdi på vare
      *
     c                   if        b2vare <> *blank
     c                   eval      w_seqe = 'V'
     c                   eval      lfdtl4_vare = b2vare
     c     lfdtl4_key    setll     lfdtl4
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c*                  eval      b2ldor = 0
     c                   eval      b2vare = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre post
      *
     c                   if        b1valg = 2
6.23 C                   if        b1ltyp = 'TX'
6.23 c                   exsr      xb11win
6.23 c                   else
     c                   exsr      xb3win
     c                   exsr      finn_sum
     c                   eval      b2tbel = w_sum
6.24 c                   endif
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slette post
      *
     c                   if        b1valg = 4
     c                   eval      b4vare = b1vare
     c                   eval      b4tek1 = b1tek1
     c                   eval      lfdtlu_line = b1line
     c                   exsr      xb4win
     c                   exsr      finn_sum
     c                   eval      b2tbel = w_sum
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Sette status lik 0
      *
     c                   if        b1valg = 5
     c                   eval      b7vare = b1vare
6.38 c**                 eval      b7tek1 = b1tek1
6.38 c                   eval      b7tek1 = xxtek1
     c                   eval      b7stat = b1stat
     c                   eval      lfdtlu_line = b1line
     c                   exsr      xb7win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
6.24  *
6.24  * Vise innkjøpsplanlegging
6.24  *
6.24 c                   if        b1valg = 6
6.24 c                   movel     b1vare        w_vare
6.24 c                   exsr      xc6bld
6.24 c                   eval      b1valg = 0
6.24 c                   update    b1sfl
6.24 c                   iter
6.24 c                   endif
      *
      * Vise vareinfo
      *
     c                   if        b1valg = 7
     c                   movel     b1vare        w_vare
     c                   exsr      xc7bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise lagerspørring
      *
     c                   if        b1valg = 9
6.14 c                   movel     b1vare        w_vare
     c                   exsr      xc8bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise lagervedlikehold
      *
     c                   if        b1valg = 8
6.14 c                   movel     b1vare        w_vare
     c                   eval      w_line = b1line
     c                   exsr      xc9bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
6.24  *
6.24  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.24  * Subrutine for å kalle opp program for å vise innkjøpsplanlegging
6.24  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.24  *
6.24 c     xc6bld        begsr
6.24  *
6.24 c                   call      'LL700R'
6.24 c                   parm                    w_vare
6.24 c                   parm                    w_lage
6.24  *
6.24 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å kalle opp program for å vise vareinfo
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc7bld        begsr
      *
     c                   call      'VV510R'
     c                   parm                    w_firm
     c                   parm                    w_vare
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å kalle opp program for lagerspørring.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc8bld        begsr
      *
      * Les forslagslinje for informasjon
      *
6.14 c*                  eval      lfdtl1_line = w_line
6.14 c*    lfdtl1_key    chain     lfdtl1                             90
6.14 c*                  if        *in90 = *on
6.14 c*                  leavesr
6.14 c*                  endif
      *
     c                   call      'LL502R'
     c                   parm                    w_firm
6.14 c                   parm                    w_vare
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å kalle opp program for lagervedlikehold
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc9bld        begsr
      *
      * Les forslagslinje for informasjon
      *
6.23 c                   eval      lfdtlr_line = w_line
6.23 c     lfdtlr_key    chain     lfdtlr                             90
     c                   if        *in90 = *on
     c                   leavesr
     c                   endif
      *
     c                   call      'LL101R'
6.14 c                   parm                    w_vare
6.14 c                   parm                    w_lage
      *
      * sjekker status itilfelle verdier er endret på lager
      *
     c                   if        lfstat <> '0'
     c                   eval      w_anta = lfanta
     c                   exsr      finn_varelag
     c                   exsr      sjk_stat
     c                   eval      lfdtlu_numm = w_numm
     c                   eval      lfdtlu_line = w_line
     c     lfdtlu_key    chain     lfdtlur
     c                   if        %found(lfdtlu)
     c                   if        laabst = 1
     c                   eval      lfstat = w_stat
     c                   else
     c                   eval      lfstat = '0'
     c                   endif
6.10 c                   time                    lfedat
6.10 c                   time                    lfetim
6.10 c                   eval      lfeusr = l_user
     c                   update    lfdtlur
     c                   endif
     c                   endif
     c                   eval      b_forn = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av endringsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb3win        begsr
6.30  * Sjekk om det er erstatningsvare
6.33 c                   eval      b3info = *blanks
6.33 c                   if        b_bmpp = *on
6.30 c                   exsr      sjk_erst
6.33 c                   endif
      *
     c                   eval      b3vare = b1vare
     c                   eval      b3tek1 = b1tek1
     c                   eval      b3anta = b1anta
     c                   eval      b3enh1 = b1enh1
6.25 c                   eval      b3mini = b1mini
     c                   eval      b3bmen = b1bmen
     c                   eval      b3udat = b1udat
     c                   eval      b3bmin = b1bmin
6.25 c                   eval      b3maks = b1maks
     c                   eval      b3stat = b1stat
7.04 c                   eval      b3vekt = b1vekd
     c                   eval      b3lbel = b1lbel
     c                   eval      lfdtlu_line = b1line
6.23 c                   eval      lfdtlr_line = b1line
6.23 c     lfdtlr_key    chain     lfdtlrr                            90
      *
     c     b3win_ut      tag
     c                   exfmt     b3win
      *
     c                   if        *inkc = *on or
     c                             *inkl = *on
     c                   leavesr
     c                   endif
      *
     c                   if        *inkd = *on
     c                   exsr      sp_input
     c                   goto      b3win_ut
     c                   endif
      *
     c                   movel     b3vare        w_vare
     c                   movel     b3enh1        w_enh1
     c                   exsr      les_vare_enh
     c                   if        b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      b3win_ut
     c                   endif
      *
6.20  *   Hent utg.dato fra subprogram
6.20 c                   call      'VL720R'
6.20 c                   parm                    w_firm
6.20 c                   parm                    vvvare
6.20 c                   parm                    w_lage
6.20 c                   parm                    w_udat
6.20 c                   parm                    b_inlr
6.20  *
6.20 c                   if        w_udat <> *loval and
6.20 c                             w_udat <= w_ddat
     c                   eval      *in33 = *on
     c                   goto      b3win_ut
     c                   endif
      *
     c                   eval      w_anta = b3anta
     c                   exsr      finn_varelag
     c                   exsr      sjk_stat
6.35 c                   exsr      finn_lbel
      *
      *  Endrer valgt linje i forslag
      *
     c     lfdtlu_key    chain     lfdtlur                            90
     c                   if        *in90 = *off
     c                   eval      lfanta = b3anta
     c                   eval      lfenh1 = b3enh1
7.01 c                   eval      lfvekt = b3vekt
6.35 c                   eval      lflbel = w_ipny
6.35 c                   if        lflbel = 0
6.35 c                   eval      lflbel = w_kpny
6.35 c                   endif
     c                   if        laabst = 1
     c                   eval      lfstat = w_stat
     c                   else
     c                   eval      lfstat = '0'
     c                   endif
6.10 c                   time                    lfedat
6.10 c                   time                    lfetim
6.10 c                   eval      lfeusr = l_user
     c                   update    lfdtlur
     c                   eval      b_forn = *on
     c                   endif
      *
     c                   endsr
      *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  * Subrutine for å sjekke erstatningsvare
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  *
6.30 c     sjk_erst      begsr
6.30  *
6.30 c                   eval      b_repl = *off
6.30 c                   eval      vverl1_envr = b1vare
6.30 c     vverl1_key    chain     vverl1
6.30 c                   if        %found
6.30 c                   if        vvantb < 3 and
6.30 c                             w_ddat >= vvfidt and
6.30 c                             w_ddat <= vvsidt
6.30 c                   eval      b3info = '(Erstatningsvare)'
6.30 c                   eval      b_repl = *on
6.30 c                   endif
6.30 c                   endif
6.30  *
6.30 c                   endsr
6.30  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb4win        begsr
      *
     c                   exfmt     b4win
      *
     c                   if        *inkc = *on or
     c                             *inkl = *on
     c                   leavesr
     c                   endif
      *
      *  Sletter valgt linje i forslag
      *
     c     lfdtlu_key    chain     lfdtlur                            90
     c                   if        *in90 = *off
     c                   delete    lfdtlur
     c                   eval      b_forn = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av registreringsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb6win        begsr
      *
     c                   eval      *in33 = *off
     c                   eval      lflage = w_lage
     c                   eval      lfnumm = p_numm
     c                   eval      b6vare = *blank
     c                   eval      b6tek1 = *blank
     c                   eval      b6tek2 = *blank
     c                   eval      b6anta = 0
     c                   eval      b6enh1 = *blank
     c                   eval      b6bmen = 0
     c*                  eval      b6ldor = 0
     c*                  eval      b6ltek = *blank
      *
     c     b6win_ut      tag
     c                   exfmt     b6win
      *
     c                   if        *inkc = *on or
     c                             *inkl = *on
     c                   goto      end_b6win
     c                   endif
      *
     c                   if        *inkd = *on
     c                   exsr      sp_input
     c                   goto      b6win_ut
     c                   endif
6.15  *
6.15 c                   movel     b6vare        w_vare
6.15  *
6.15  * sjekker om vare er lagervare
6.15  *
6.15 c                   call      'VL710R'
6.15 c                   parm                    w_firm
6.15 c                   parm                    w_vare
6.15 c                   parm                    w_lage
6.15 c                   parm                    w_vtyp
6.26 c                   parm                    w_udat
6.26 c                   parm                    w_ldor
6.26 c                   parm                    b_inlr
6.3a c                   parm                    w_lv_nobb
6.3a c                   parm                    w_lv_modn
6.3a c                   parm                    w_lv_lldo
6.3a c                   parm                    w_lv_llva
6.15 c                   if        w_vtyp <> 'L'
6.15 c                   eval      *in35 = *on
6.15 c                   goto      b6win_ut
6.15 c                   endif
      *
     c                   eval      w_enh1 = b6enh1
     c                   eval      lfvare = w_vare
     c                   eval      lflage = w_lage
     c                   if        b6anta = 0
     c                   exsr      reg_bilde
     c                   goto      b6win_ut
     c                   endif
      *
     c                   exsr      les_vare_enh
     c                   if        b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      b6win_ut
     c                   endif
      *
     c*                  eval      w_ldor = vvldor
     c*                  exsr      les_ldor
     c*                  if        b_feil = *on
     c*                  eval      *in32 = *on
     c*                  goto      b6win_ut
     c*                  endif
      *
6.20  *
6.20 c                   if        w_udat <> *loval and
6.20 c                             w_udat <= w_ddat
     c                   eval      *in34 = *on
     c                   goto      b6win_ut
     c                   endif
      *
     c                   eval      w_anta = b6anta
      *
     c                   exsr      sjk_stat
      *
      * sjekker status kode 8
      *
6.21 c                   if        w_ldor <> lhldor
     c                   eval      w_stat = '8'
     c                   endif
      *
      *  Legger inn ny linje i forslag
      *
6.2a c                   clear                   lfdtlur
     c                   eval      lfdtlu_line = w_slin + 2
     c     lfdtlu_key    chain     lfdtlur                            90
     c                   if        *in90 = *on
     c                   eval      lffirm = w_firm
6.2b c                   eval      lfnumm = lfdtlu_numm
     c                   eval      w_slin = w_slin + 2
     c                   eval      lfline = w_slin
6.2b c                   eval      lfvare = b6vare
6.2b c                   eval      lflage = lhlage
     c                   eval      lfanta = b6anta
     c                   eval      lfenh1 = b6enh1
6.21 c                   eval      lfvlev = w_ldor
     c                   eval      lfldor = lhldor
     c                   eval      lftek1 = vvtek1
     c                   eval      lftek2 = vvtek2
     c                   eval      lfogrp = vvogrp
     c                   eval      lfhgrp = vvhgrp
     c                   eval      lfugrp = vvugrp
6.3a c                   if        w_lv_nobb > 0
6.3a c                   eval      lflvar = w_lv_llva
6.3a c                   else
     c                   eval      lflvar = vvlvar
6.3a c                   endif
     c                   if        laabst = 1
     c                   eval      lfstat = w_stat
     c                   else
     c                   eval      lfstat = '0'
     c                   endif
6.32  * sjekker om kamp.pris finnes
6.32 c                   exsr      sjekk_kamp
6.32  *
     c                   eval      lfvekt = vevekt
6.2c  * Bruk innkjøpspris, hvis ikke finnes bruk kost
6.2c c**                 eval      lflbel = w_kpny
6.2c c                   eval      lflbel = w_ipny
6.2c c                   if        lflbel = 0
6.2c c                   eval      lflbel = w_kpny
6.2c c                   endif
6.2c  *
6.10 c                   time                    lfodat
6.10 c                   time                    lfotim
6.10 c                   eval      lfousr = l_user
6.10 c                   time                    lfedat
6.10 c                   time                    lfetim
6.10 c                   eval      lfeusr = l_user
     c                   write     lfdtlur
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_b6win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb7win        begsr
      *
     c                   exfmt     b7win
      *
     c                   if        *inkc = *on or
     c                             *inkl = *on
     c                   goto      end_b7win
     c                   endif
      *
      *  Sletter valgt linje i forslag
      *
     c     lfdtlu_key    chain     lfdtlur                            90
     c                   if        *in90 = *off
     c                   eval      lfstat = '0'
6.10 c                   time                    lfedat
6.10 c                   time                    lfetim
6.10 c                   eval      lfeusr = l_user
     c                   update    lfdtlur
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_b7win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å endre status til 0 på alle med inntastet status
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb10win       begsr
      *
     c                   eval      b10stat = '0'
      *
     c     b10winut      tag
     c                   exfmt     b10win
      *
     c                   if        *inkc = *on or
     c                             *inkl = *on
     c                   goto      end_b10win
     c                   endif
      *
     c                   if        b10stat <> '1' and
     c                             b10stat <> '2' and
     c                             b10stat <> '3' and
     c                             b10stat <> '4' and
     c                             b10stat <> '5' and
     c                             b10stat <> '6' and
     c                             b10stat <> '7' and
     c                             b10stat <> '8' and
6.30 c                             b10stat <> '9' and
6.30 c                             b10stat <> 'E'
     c                   eval      *in31 = *on
     c                   goto      b10winut
     c                   endif
      *
      * setter status lik 0
      *
     c                   eval      lfdtlx_numm = p_numm
     c     lfdtlx_key    setll     lfdtlu
6.34xc     lfdtlx_key    reade     lfdtlu
6.34xc                   dow       not %eof(lfdtlu)
     c                   if        lfstat = b10stat
     c                   eval      lfstat = '0'
6.10 c                   time                    lfedat
6.10 c                   time                    lfetim
6.10 c                   eval      lfeusr = l_user
     c                   update    lfdtlur
     c                   endif
6.34xc     lfdtlx_key    reade     lfdtlu
6.34xc                   enddo
      *
     c     end_b10win    endsr
6.23  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.23  * Subrutine for behandling av endring av tekstlinjer
6.23  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.23  *
6.23 c     xb11win       begsr
6.23  *
6.23 c                   eval      lfdtlu_line = b1line
6.23 c                   eval      lfdtlr_line = b1line
6.23 c     lfdtlr_key    chain     lfdtlrr                            90
6.23 c                   eval      b11te1 = lftek1
6.23 c                   eval      b11te2 = lftek2
6.23  *
6.23 c     b11taga       tag
6.23 c                   exfmt     b11win
6.23  *
6.23 c                   if        *inkc = *on or
6.23 c                             *inkl = *on
6.23 c                   leavesr
6.23 c                   endif
6.23  *
6.23  *  Endrer valgt linje i forslag
6.23  *
6.23 c*    lfdtlu_key    chain     lfdtlur                            90
6.23 c*                  if        *in90 = *off
6.23 c*                  eval      lftek1 = b11te1
6.23 c*                  eval      lftek2 = b11te2
6.23 c*                  time                    lfedat
6.23 c*                  time                    lfetim
6.23 c*                  eval      lfeusr = l_user
6.23 c*                  update    lfdtlur
6.23 c*                  eval      b_forn = *on
6.23 c*                  endif
6.23  *
6.23 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * xh1win  Bilde for behandling av bla-alternativer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xh1win        begsr
      *
     c     h1taga        tag
      *
     c                   write     h1win
     c                   exfmt     h1win
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      end_h1win
     c                   endsl
      *
     c     end_h1win     endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * xh2win  Bilde for forklaring på statuskoder
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xh2win        begsr
      *
     c     h2taga        tag
      *
     c                   write     h2win
     c                   exfmt     h2win
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      end_h2win
     c                   endsl
      *
     c     end_h2win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
6.23 c                   select
6.23 c                   when      w_seqe = 'V'
     c                   read      lfdtl4                                 15
6.23 c                   other
6.23 c                   if        b_kord = *on
6.23 c                   read      lfdtl1                                 15
6.23 c                   else
6.23 c                   read      lfdtl4                                 15
6.23 c                   endif
6.23 c                   endsl
      *
     c                   if        %eof or
     c                             lffirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        lfnumm <> w_numm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
      * Sjekk om F9 = tastet (hvis på, så kun de med feilstatus 1 - 7')
      *
     c                   if        b_f09 = *on
     c                   if        lfstat = '0'
     c                   iter
     c                   endif
      *
     c                   else
      *
      * Uaktuell status ?
      *
     c                   if        h1utel = 0 and
     c                             h1stat <> *blank and
     c                             h1stat <> lfstat
     c                   iter
     c                   endif
      *
      * Utelate status ?
      *
     c                   if        h1utel = 1 and
     c                             h1stat <> *blank and
     c                             h1stat = lfstat
     c                   iter
     c                   endif
      *
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      lfdtl4_vare = lfvare
6.23 c                   eval      lfdtl1_line = lfline
      *
6.12 c                   eval      b1pkla = *blank
6.13 c                   eval      vveplr_pvar = lfvare
6.13 c                   eval      vveplr_penh = lfenh1
6.13 c                   eval      vveplr_pldo = w_ldor
6.13 c     vveplr_key    chain     vveplr
6.13 c                   if        %found(vveplr)
6.13 c                   eval      b1pkla = vepkla
6.13 c                   endif
      *
     c                   eval      b1valg = 0
     c                   eval      b1line = lfline
6.24 c                   eval      b1ltyp = lfltyp
     c                   movel     lfvare        b1vare
     c                   eval(h)   b1anta = lfanta
     c                   eval      b1enh1 = lfenh1
     c                   eval      b1bmen = 0
     c                   exsr      finn_varelag
     c                   eval      b1bmen = w_bmen
6.25 c                   eval      b1mini = w_mini
6.25 c                   eval      b1bmin = w_bmin
6.25 c                   eval      b1maks = w_maks
7.03 c                   eval      b1bpun = w_bpun
7.03 c*                  if        *in50 = *off
7.03 c*                  eval      b1bmex = w_bmen
7.03 c*                  else
7.03 c*                  eval      b1bmex = w_mini
7.03 c*                  endif
6.11 c                   if        b_ksor = *on
6.11 c                   movel     vvsort        b1sort
6.11 c                   else
6.11 c                   eval      b1sort = *blank
6.11 c                   endif
      * skifter mellom vanlig bilde og å vise innkj.mengde + i.enh.
7.03 c*                  if        *in51  = *off
7.03 c*                  if        *in50  = *off
7.03 c*                  eval      b1bmix = w_bmin
7.03 c*                  else
7.03 c*                  eval      b1bmix = w_maks
7.03 c*                  eval      b1maks = w_maks
7.03 c*                  endif
     c                   eval      b1tek1 = lftek1
6.38 c                   eval      xxtek1 = lftek1
7.03 c*                  else
     c                   eval      w_vare = lfvare
     c                   exsr      hent_inkenh
     c                   if        w_enh1 <> *blank and
     c                             w_enh1 <> lfenh1
7.03 c                   eval      b1enh2 = lfenh1
7.03 c*                  eval      b1tek1 = w_enh1 + ' ' +
7.03 c                   eval      b1tek1 = %subst(lftek1:1:19)
     c                   exsr      hent_omr
     c                   if        w_omrl <> 0
     c                   eval      b1bmin = b1anta * w_omrl / w_omri
     c                   else
     c                   eval      b1bmin = b1anta
     c                   endif
     c                   else
7.03 c*                  eval      b1bmin = b1anta
7.03 c                   eval      b1enh2 = lfenh1
7.03 c*                  eval      b1tek1 = lfenh1 + ' ' +
7.03 c                   eval      b1tek1 = %subst(lftek1:1:19)
     c                   endif
7.03 c*                  endif
6.30  * Sjekk erstatningsvare
6.31 c                   eval      b_repl = *off
6.33 c                   if        b_bmpp = *on
6.30 c                   if        lfstat = ' '
6.30 c**                           lfstat = '0'
6.30 c                   exsr      sjk_erst
6.30 c                   endif
6.36 c                   endif
     c                   eval      b1stat = lfstat
6.30 c                   if        b_repl = *on
6.30 c                   eval      b1stat = 'E'
6.30 c                   endif
6.33  *
     c                   eval      b1vekt = lfvekt
7.04 c                   eval      b1vekd = lfvekt
     c                   eval      b1lbel = lflbel
6.20  *   Hent utg.dato fra subprogram
6.20 c                   call      'VL720R'
6.20 c                   parm                    w_firm
6.20 c                   parm                    vvvare
6.20 c                   parm                    w_lage
6.20 c                   parm                    w_udat
6.20 c                   parm                    b_inlr
6.20  *
6.20 c                   if        w_udat <> *loval
6.20 c     *dmy          move      w_udat        b1udat
     c                   else
     c                   eval      b1udat = 0
     c                   endif
5.61 c                   eval      b1manu = w_manu
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
6.23 c                   select
6.23 c                   when      w_seqe = 'V'
     c     lfdtl4_key    setgt     lfdtl4
     c                   readp     lfdtl4
6.23 c                   other
6.23 c                   if        b_kord = *on
6.23 c     lfdtl1_key    setgt     lfdtl1
6.23 c                   readp     lfdtl1
6.23 c                   else
6.23 c     lfdtl4_key    setgt     lfdtl4
6.23 c                   readp     lfdtl4
6.23 c                   endif
6.23 c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
6.23 c                   select
6.23 c                   when      w_seqe = 'V'
     c                   readp     lfdtl4
6.23 c                   other
6.23 c                   if        b_kord = *on
6.23 c                   readp     lfdtl1
6.23 c                   else
6.23 c                   readp     lfdtl4
6.23 c                   endif
6.23 c                   endsl
      *
     c                   if        %eof or
     c                             lffirm <> w_firm or
     c                             lfnumm <> w_numm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
6.23 c                   select
6.23 c                   when      w_seqe = 'V'
     c                   eval      lfdtl4_vare = lfvare
6.23 c                   other
6.23 c                   if        b_kord = *on
6.23 c                   eval      lfdtl1_line = lfline
6.23 c                   else
6.23 c                   eval      lfdtl4_vare = lfvare
6.23 c                   endif
6.23 c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
6.23 c                   select
6.23 c                   when      w_seqe = 'V'
     c     lfdtl4_key    setll     lfdtl4
6.23 c                   other
6.23 c                   if        b_kord = *on
6.23 c     lfdtl1_key    setll     lfdtl1
6.23 c                   else
6.23 c     lfdtl4_key    setll     lfdtl4
6.23 c                   endif
6.23 c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for spørring på inputfelter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp_input      begsr
      *
      *  Spørring på leverandør
      *
     c*                  if        w_actf = 'B2LDOR'
     c*                  call      'RL500R'
     c*                  parm                    w_firm
     c*                  parm                    b2ldor
     c*                  parm                    w_letx
     c*                  goto      end_sp
     c*                  endif
      *
     c*                  if        w_actf = 'B3LDOR'
     c*                  call      'RL500R'
     c*                  parm                    w_firm
     c*                  parm                    b3ldor
     c*                  parm                    w_letx
     c*                  eval      b3ltek = w_letx
     c*                  goto      end_sp
     c*                  endif
      *
     c*                  if        w_actf = 'B6LDOR'
     c*                  call      'RL500R'
     c*                  parm                    w_firm
     c*                  parm                    b6ldor
     c*                  parm                    w_letx
     c*                  eval      b6ltek = w_letx
     c*                  goto      end_sp
     c*                  endif
      *
      *  Spørring i vareregister
      *
     c                   if        w_actf = 'B2VARE'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   movel     w_vare        b2vare
     c                   goto      end_sp
     c                   endif
      *
     c                   if        w_actf = 'B6VARE'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   eval      b6vare = w_vare
     c                   eval      lfvare = w_vare
     c                   exsr      reg_bilde
     c                   goto      end_sp
     c                   endif
      *
      *  Spørring på innkjøpsenhet
      *
     c                   if        w_actf = 'B3ENH1'
     c                   movel     b3vare        w_vare
     c                   call      'VE550R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    b3enh1
     c                   parm                    w_etxt
     c                   endif
      *
     c                   if        w_actf = 'B6ENH1'
     c                   movel     b6vare        w_vare
     c                   call      'VE550R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    b6enh1
     c                   parm                    w_etxt
     c                   endif
      *
     c     end_sp        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utfylling av registreringsbilde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     reg_bilde     begsr
      *
     c                   eval      *in33 = *on
     c                   exsr      finn_varelag
     c                   eval      b6tek1 = vvtek1
     c                   eval      b6tek2 = vvtek2
     c                   eval      b6bmen = w_bmen
     c                   eval      b6bmin = w_bmin
      *
6.20  *   Hent utg.dato fra subprogram
6.20 c                   call      'VL720R'
6.20 c                   parm                    w_firm
6.20 c                   parm                    vvvare
6.20 c                   parm                    w_lage
6.20 c                   parm                    w_udat
6.20 c                   parm                    b_inlr
6.20  *
6.20 c                   if        w_udat <> *loval
6.20 c     *dmy          move      w_udat        b6udat
     c                   else
     c                   eval      b1udat = 0
     c                   endif
      *
     c                   if        b6enh1 = *blank
     c                   exsr      hent_inkenh
     c                   eval      b6enh1 = w_enh1
     c                   endif
      *
     c*                  exsr      les_ldor
     c*                  eval      b6ldor = w_ldor
     c*                  eval      b6ltek = w_letx
      *
     c     end_reg       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for lesing av leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_ldor      begsr
      *
     c                   eval      b_feil = *off
     c                   eval      rlevl1_levr = w_ldor
     c     rlevl1_key    chain     rlevl1                             91
     c                   if        *in91 = *off
     c                   eval      w_letx = rlnavn
     c                   else
     c                   eval      w_letx = *blank
     c                   eval      b_feil = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for lesing av vare/enhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_vare_enh  begsr
      *
7.01  * Finner vekten på valgt enhet
7.01 c                   eval      vvenlr_vare = w_vare
7.01 c                   eval      vvenlr_enhe = w_enh1
7.01 c     vvenlr_key    chain     vvenlr
7.01 c                   if        %found
7.01 c                   eval      b3vekt = vevekt
7.01 c                   endif
      *
     c                   eval      vvarl1_vare = w_vare
     c     vvarl1_key    chain     vvarl1
     c                   eval      b_feil = *off
6.33 c                   if        vvenhl <> w_enh1
      *
6.13  * sjekker om det er en gyldig best.enhet på prisgiver
6.13  * hvis funnet, men best.bar = 0 gir feil
6.13 c                   eval      vveplr_pvar = w_vare
6.13 c                   eval      vveplr_penh = w_enh1
6.13 c                   eval      vveplr_pldo = w_ldor
6.13 c     vveplr_key    chain     vveplr
6.13 c                   if        %found(vveplr) and
6.13 c                             vepbes = 1
6.13 c                   leavesr
6.13 c                   endif
      *
     c                   eval      vvenlr_vare = w_vare
     c                   eval      vvenlr_enhe = w_enh1
     c     vvenlr_key    chain     vvenlr
     c                   if        not %found or
     c                             veinen = 0
     c                   eval      b_feil = *on
     c                   endif
      *
6.33 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av vare/lager informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_varelag  begsr
      *
     c                   eval      w_bmen = 0
     c                   eval      w_bmin = 0
6.25 c                   eval      w_mini = 0
6.25 c                   eval      w_maks = 0
7.03 c                   eval      w_bpun = 0
     c                   eval      vvarl1_vare = lfvare
     c                   eval      vlagl1_vare = lfvare
     c                   eval      vlagl1_lage = lflage
      *
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
     c     vlagl1_key    chain     vlagl1                             93
     c                   if        *in93 = *on
     c                   leavesr
     c                   endif
      *
5.61 c                   if        %found(vlagl1) and
6.22 c                             vlstda <> *loval and
6.22 c                             vlstda > w_ddat
5.61 c                   eval      w_manu = 'M'
5.61 c                   else
5.61 c                   eval      w_manu = *blank
5.61 c                   endif
      *
      * Verdier fra lager eller vare?
      * fom. ver. 5.50 tas verdier kun fra lager
      *
     c                   eval      w_bmen = vlbmen
     c                   eval      w_bmin = vlbmin
6.25 c                   eval      w_mini = vlmini
6.25 c                   eval      w_maks = vlmaks
7.03 c                   eval      w_bpun = vlbpun
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter innkjøpsenhet og priser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_inkenh   begsr
      *
     c                   eval      w_lety = 0
6.27 c                   eval      w_kamp = lokamp
      *
     c                   call      'VP750R'
     c                   parm                    w_firm
     c                   parm                    w_vare
5.70 c                   parm                    w_prgr
     c                   parm                    w_lety
     c                   parm                    lhbdat
     c                   parm                    w_enh1
     c                   parm                    w_kpny
     c                   parm                    w_ipny
6.27 c                   parm                    w_kamp
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter innkjøpsenhet og priser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_sum      begsr
      *
     c                   eval      w_sum = 0
6.23 c                   eval      lfdtlx_numm = p_numm
6.23 c     lfdtlx_key    setll     lfdtlr
6.23 c     lfdtlx_key    reade     lfdtlr
6.23 c                   dow       not %eof
6.23 c                   eval      w_sum = w_sum + (lfanta * lflbel)
6.23 c     lfdtlx_key    reade     lfdtlr
     c                   enddo
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekker mulige statuskoder
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_stat      begsr
      *
      * Her sjekkes det for status 4 - 6.
      *
     c                   eval      w_stat = '0'
     c                   if        w_bmen <> 0 and
     c                             w_anta <> w_bmen
     c                   eval      w_stat = '4'
     c                   endif
      *
     c                   if        w_bmin <> 0
     c                   eval      w_112  = w_anta / w_bmin
     c                   move      w_112         w_20              2 0
     c                   if        w_20 > 0
     c                   eval      w_stat = '5'
     c                   endif
     c                   endif
      *
      * sjekk mot bestilling /restleveranse
     c                   exsr      sjk_best
      *
      * sjekk om det er en cross-docking vare
      *
     c                   if        vvcrdo = 1
     c                   eval      w_stat = '7'
     c                   endif
6.11  *
6.11  * hvis man kjører kjede-sortiment, så sjekkes det om vare har sort.
6.11  *
6.11 c                   if        w_stat = *blank and
6.11 c                             b_ksor = *on    and
6.11 c                             vvsort = *blank
6.11 c                   eval      w_stat = '9'
6.11 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekk av bestilling.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_best      begsr
6.34xc*6.37              eval      lodtlv_vare = vvvare
6.34xc*6.37lodtlv_key    setll     lodtlv
6.34xc*6.37lodtlv_key    reade     lodtlv
6.34xc*6.37              dow       not %eof(lodtlv)
6.37 c                   eval      lodtlx_lage = lhlage
6.37 c                   eval      lodtlx_vare = vvvare
6.37 c     lodtlx_key    setll     lodtlx
6.37 c     lodtlx_key    reade     lodtlx
6.37 c                   dow       not %eof(lodtlx)
6.37 c*                  if        ldanta > 0
6.37 c                   if        ldanta > 0 and
6.37 c                             ldlage = lhlage
      *
     c                   eval      lohelr_numm = ldnumm
     c                   eval      lohelr_suff = ldsuff
     c     lohelr_key    chain     lohelr
     c                   if        %found(lohelr)
     c                   eval      votylr_otyp = lootyp
     c     votylr_key    chain     votylr
     c                   if        %found(votylr) and
     c                             vaolag = 1  and
     c                             vaoakk = 1
     c                   eval      w_stat = '6'
     c                   eval      b_sts6 = *on
8.02 c                   goto      sjk_u801
     c                   goto      end_sjk
     c                   endif
     c                   endif
     c                   endif
6.34xc*6.37lodtlv_key    reade     lodtlv
6.37 c     lodtlx_key    reade     lodtlx
6.34xc                   enddo
8.02  *
8.02  * Sjekk om switch for overstyring av kode 6 er satt
8.02  *
8.02 c     sjk_u801      tag
8.02 c                   if        u_802 = *on and
8.02 c                             ldornu <> 0
8.02 c                   eval      w_stat = ' '
8.02 c                   eval      b_sts6 = *off
8.02 c                   endif
      *
     c     end_sjk       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne omregningsfaktor
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_omr      begsr
      *
     c                   eval      vvenlx_vare = lfvare
     c     vvenlx_key    setll     vvenlr
     c     vvenlx_key    reade     vvenlr                                 91
     c                   dow       *in91 = *off
     c                   if        lfenh1 = veenhe
     c                   eval      w_omrl = veomre
     c                   endif
     c                   if        w_enh1 = veenhe
     c                   eval      w_omri = veomre
     c                   endif
     c     vvenlx_key    reade     vvenlr                                 91
     c                   enddo
      *
     c                   if        w_omri = 0
     c                   eval      w_omri = 1
     c                   endif
      *
     c                   endsr
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *  Subrutine for å finne evt. kampanjepris fra VA på valgt vare
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     sjekk_kamp    begsr
6.32  *
6.32 c                   eval      w_vkai = 0
6.32 c                   eval      w_vvar = lfvare
6.32 c                   eval      w_vldo = lhldor
6.32 c                   eval      w_enhe = *blank
6.3b c                   if        u_63d = *on
6.3b c                   eval      w_enhe = lfenh1
6.3b c                   eval      b_inlr = *on
6.3b c                   call      'VT592R  '
6.3b c                   parm                    w_firm
6.3b c                   parm                    w_vvar
6.3b c                   parm                    w_enhe
6.3b c                   parm                    w_vkai
6.3b c                   parm                    b_inlr
6.3b c                   eval      b_inlr = *off
6.3b c                   else
6.32 c                   call      'JM592R  '
6.32 c                   parm                    w_firm
6.32 c                   parm                    w_vvar
6.32 c                   parm                    w_vldo
6.32 c                   parm                    w_vkai
6.32 c                   parm                    w_enhe
6-3b c                   endif
6.32 c                   if        lfenh1 <> w_enhe
6.32 c                   eval      w_vkai = 0
6.32 c                   endif
6.32 c                   if        w_vkai <> 0
6.32 c                   eval      w_ipny = w_vkai
6.32 c                   endif
6.32  *
6.32 c                   endsr
6.35  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.35  *  Subrutine for å finne linjebeløp hvis enhet er endret
6.35  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.35  *
6.35 c     finn_lbel     begsr
6.35  *
6.35 c                   eval      w_kamp = lokamp
6.35  *
8.01 c**                 call      'VP753R'
8.01 c                   call      'VP754R'
6.35 c                   parm                    w_firm
6.35 c                   parm                    w_vare
6.35 c                   parm                    w_prgr
6.35 c                   parm                    w_enh1
6.35 c                   parm                    w_lety
6.35 c                   parm                    lhbdat
6.35 c                   parm                    w_kpny
6.35 c                   parm                    w_ipny
6.35 c                   parm                    w_kamp
8.01 c                   parm                    w_lage
6.35  *
6.35 c                   endsr
6.35  *
      *
7.03  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.03  * Subrutine for å behandle kalkulering av vekt og volum
7.03  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.03  *
7.03 c     cal_line      begsr
7.03  *
7.03 c**                 eval      b1vekt = 0
7.03 c                   eval      b1volu = 0
7.03  *
7.03 c                   if        lfvare = *blank
7.03 c                   goto      end_cal
7.03 c                   endif
7.03  *
7.03  * Behandler ikke tjenester og diversevarer
7.03  *
7.03 c                   eval      vvarl1_vare = lfvare
7.03 c     vvarl1_key    chain     vvarl1
7.03 c                   if        not %found
7.03 c     vvarl1_key    chain     vvasl1
7.03 c                   endif
7.03  *
7.03  * Kaller program for henting av varetype
7.03  *
7.03 c                   call      'VL710R'
7.03 c                   parm                    w_firm
7.03 c                   parm                    lfvare
7.03 c                   parm                    lflage
7.03 c                   parm                    w_vtyp
7.03 c                   parm                    w_utda
7.03 c                   parm                    lfldor
7.03 c                   parm                    b_inlr
7.03 c                   parm                    w_lv_nobb
7.03 c                   parm                    w_lv_modn
7.03 c                   parm                    w_lv_lldo
7.03 c                   parm                    w_lv_llva
7.03  *
7.03 c                   if        w_vtyp = 'T' or
7.03 c                             w_vtyp = 'D'
7.03 c                   goto      end_cal
7.03 c                   endif
7.03  *
7.03  * Finner vekt og volum
7.03  *
7.03 c                   eval      vvenl1_vare = lfvare
7.03 c                   eval      vvenl1_enhe = lfenh1
7.03 c     vvenl1_key    chain     vvenl1
7.03 c                   if        not %found or
7.03 c                             vevekt = 0 or
7.03 c                             vevolu = 0
7.03 c                   eval      jvpkl2_vare = lfvare
7.03 c                   eval      jvpkl2_ldor = lfldor
7.03 c                   eval      jvpkl2_enhe = lfenh1
7.03 c     jvpkl2_key    chain     jvpkl2
7.03 c                   if        not %found
7.03 c                   eval      jvpkl2_vare = lfvare
7.03 c                   eval      jvpkl2_ldor = vvnlev
7.03 c                   eval      jvpkl2_enhe = lfenh1
7.03 c     jvpkl2_key    chain     jvpkl2
7.03 c                   endif
7.03 c                   endif
7.03  *
7.03 c                   if        vevekt <> 0
7.03 c                   eval      w_vekt = vevekt
7.03 c                   else
7.03 c                   eval      w_vekt = jwvekt / 1000
7.03 c                   endif
7.03  *
7.03 c                   if        vevolu <> 0
7.03 c                   eval      w_volu = vevolu
7.03 c                   else
7.03 c                   eval      w_volu = jwvolu
7.03 c                   endif
7.03  *
7.03  * Summerer vekt og volum
7.03  *
7.03 c**                 eval      b1vekt = (w_vekt * ldanta)
7.03 c                   eval      b1volu = (w_volu * ldanta)
7.03  *
7.03 c     end_cal       endsr
7.03  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *
      *  Key for les av FORSLAG-HODE
      *
     c     lfhelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lfhelr_numm
      *
      *  Key for oppdatering av FORSLAG-HODE
      *
     c     lfhelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lfhelu_numm
      *
      *  Key for les av FORSLAGS-LINJE
      *
6.23 c     lfdtlr_key    klist
6.23 c                   kfld                    w_firm
6.23 c                   kfld                    lfdtlr_numm
6.23 c                   kfld                    lfdtlr_line
6.23  *
6.23  *  Key for les av FORSLAGS-LINJE
6.23  *
6.23 c     lfdtl1_key    klist
6.23 c                   kfld                    w_firm
6.23 c                   kfld                    lfdtl1_numm
6.23 c                   kfld                    lfdtl1_line
      *
      *  Key for les av FORSLAGS-LINJE
      *
     c     lfdtlx_key    klist
     c                   kfld                    w_firm
6.23 c                   kfld                    lfdtlr_numm
      *
      *  Key for oppdatering av FORSLAGS-LINJE
      *
     c     lfdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lfdtlu_numm
     c                   kfld                    lfdtlu_line
      *
      *  Key for les av FORSLAGS-LINJE - leverandørsortert
      *
     c*    lfdtl2_key    klist
     c*                  kfld                    w_firm
     c*                  kfld                    lfdtl2_numm
     c*                  kfld                    lfdtl2_ldor
      *
      *  Key for les av FORSLAGS-LINJE - varesortert
      *
     c     lfdtl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lfdtl4_numm
     c                   kfld                    lfdtl4_vare
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *  Key for les av LAGER
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
      *
      *  Key for les av VARE/ENHET
      *
     c     vvenlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenlr_vare
     c                   kfld                    vvenlr_enhe
6.13  *
6.13  *  Key for les av VARE/ENHET
6.13  *
6.13 c     vveplr_key    klist
6.13 c                   kfld                    w_firm
6.13 c                   kfld                    vveplr_pvar
6.13 c                   kfld                    vveplr_penh
6.13 c                   kfld                    vveplr_pldo
      *
      *  Key for les av VARE/ENHET
      *
     c     vvenlx_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenlx_vare
      *
      * Key for les av leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      *  Key for les av bestillinger
      *
6.37 c*    lodtlv_key    klist
6.37 c     lodtlx_key    klist
     c                   kfld                    w_firm
6.37 c*                  kfld                    lodtlv_vare
6.37 c                   kfld                    lodtlx_lage
6.37 c                   kfld                    lodtlx_vare
      *
      *  Key for les av bestilling hode
      *
     c     lohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelr_numm
     c                   kfld                    lohelr_suff
      *
      *  Key for les av ordretype
      *
     c     votylr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votylr_otyp
      *
      *  Key for les av koderegister
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
6.25  *
6.25  *  Key for les av best.forslag parameter
6.25  *
6.25 c     mppalr_key    klist
6.25 c                   kfld                    w_firm
6.25 c                   kfld                    mppalr_ldor
6.25 c                   kfld                    mppalr_lnum
6.30  *
6.30  *  Key for les av erstatningsvare
6.30  *
6.30 c     vverl1_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    vverl1_envr
7.03  *
7.03  * Key for les av enhet
7.03  *
7.03 c     vvenl1_key    klist
7.03 c                   kfld                    w_firm
7.03 c                   kfld                    vvenl1_vare
7.03 c                   kfld                    vvenl1_enhe
7.03  *
7.03  * Key for oppslag på vare-pakning (VA)
7.03  *
7.03 c     jvpkl2_key    klist
7.03 c                   kfld                    jvpkl2_vare
7.03 c                   kfld                    jvpkl2_ldor
7.03 c                   kfld                    jvpkl2_enhe
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_numm
      *
      * Legger inn startverdier
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_numm = p_numm
6.20 c                   time                    w_ddat
      *
      * Les forslags hode
      *
     c                   eval      lfhelr_numm = w_numm
     c     lfhelr_key    chain     lfhelr                             90
     c                   eval      w_lage = lhlage
     c                   eval      w_ldor = lhldor
     c                   eval      b2ldor = lhldor
     c                   eval      b2numm = lhnumm
     c                   eval      b2tbel = lhtbel
     c                   eval      w_ldor = lhldor
     c                   exsr      les_ldor
     c                   movel     w_letx        b2lnav
6.23 c                   if        lhornr > 0
6.23 c                   eval      b_kord = *on
6.23 c                   endif
6.25  * slår opp mot parameter og henter "valg av varer"
6.25  * dette for å avgjøre om max/min skal vises eller
6.25  * best.m. / best.pkt.
6.25 c                   eval      *in50  = *off
6.25 c                   eval      mppalr_ldor = lhldor
6.25 c                   eval      mppalr_lnum = lhlnum
6.25 c     mppalr_key    chain     mppalr
6.25 c                   if        %found(mppalr)
6.25 c                   if        mpvval = 1
6.25 c                   eval      *in50  = *on
6.25 c                   endif
6.25 c                   endif
      *
      * Finn høyeste forslags-linjenummer
      *
6.23 c                   eval      lfdtlr_numm = w_numm
6.23 c                   eval      lfdtlr_line = 9999
6.23 c     lfdtlr_key    setll     lfdtlr
6.23 c                   readp     lfdtlr                                 99
     c                   if        *in99 = *on or
     c                             lfnumm <> w_numm
     c                   eval      w_slin = 0
     c                   else
     c                   eval      w_slin = lfline
     c                   endif
      *
      * Avgjør om feilstatus skal brukes
      *
     c     lstsl1_key    chain     lstsl1
5.70  *
5.70  * Hent prisgruppe
5.70  *
5.70 c                   eval      w_avde = 0
5.70 c                   call      'VL712R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    w_lage
5.70 c                   parm                    w_avde
5.70 c                   parm                    w_prgr
5.70  *
6.11  * sjekker om kjedesortiment brukes
6.11  *
6.11 c                   if        laakso <> *blank
6.11 c                   eval      b_ksor = *on
6.11 c                   endif
6.33  * Sjekk om du har tilgang til BM modulen.
6.33 c                   eval      b_bmpp = *on
6.36 c                   eval      *in55  = *on
6.33 c                   eval      w_disp = '0'
6.33 c                   eval      w_msts = '0'
6.33 c                   eval      w_modu = c_modu
6.33 c                   call      'AX700R'
6.33 c                   parm                    w_modu
6.33 c                   parm                    w_disp
6.33 c                   parm                    w_msts
6.33  *
6.33 c                   if        w_msts = '1'
6.33 c                   eval      b_bmpp = *off
6.39 c**                 eval      *in55  = *off
6.33 c                   endif
6.3b  *
6.3b  * Endring 6.3d
6.3b  *
6.3b   CallP CO402R(l_filg:w_firm:0:'TILB_INNKJ_PERIOD':
6.3b                    co402_verdi1:co402_verdi2);
6.3b   if co402_verdi1 = '1';
6.3b     u_63d = *on;
7.02   //  *in50 = *on;
6.3b   endif;
8.02  *
8.02  * Endring 8.02
8.02  *
8.02   CallP CO402R(l_filg:w_firm:0:'BESTF_STATUS6':
8.02                    co402_verdi1:co402_verdi2);
8.02   if co402_verdi1 = '1';
8.02     u_802 = *on;
8.02   endif;
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      lfdtlu_numm = w_numm
6.23 c                   eval      lfdtlr_numm = w_numm
     c                   eval      lfdtl1_numm = w_numm
     c*                  eval      lfdtl2_numm = w_numm
     c*                  eval      lfdtl2_ldor = 0
     c                   eval      lfdtl4_numm = w_numm
     c                   eval      lfdtl4_vare = *blank
      *
     c     lfdtl4_key    setll     lfdtl4
6.23 c     lfdtl1_key    setll     lfdtl1
      *
     c                   eval      w_seqe = ' '
     c                   exsr      crt_subfile
      *
     c                   endsr
