     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VA720R
      * Beskrivelse . . : Henter Konto - Avd - Spes
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
3.11  * R3.11 150900 KOB  Lagt ut kontohenvisning i parameter
4.01  * R4.01 190302 AMD  Endring fra spes til kostnadsbærer
5.20  * R5.20 171003 AMD  Utvidet med mulighet for å kunne kontere på
      *                   leveringstype
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvkhvl1    if   e           k disk    rename(vkhvpfr:vkhvl1r)
      *
      * Definering av parametere
      *
     d p_stat          s              1
5.20 d p_ltyp          s              1  0
     d p_otyp          s              2
     d p_ogrp          s              2  0
     d p_hgrp          s              2  0
     d p_ugrp          s              3  0
     d p_vare          s             15
5.20 d p_hele          s            128
      *
      * Definering av variable i nøkler
      *
     d w_firm          s                   like(faafir)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d vvarl1_vare     s                   like(vvvare)
     d vkhvl1_khev     s                   like(vvkhev)
5.20 d vkhvl1_klty     s                   like(vaklty)
     d vkhvl1_koty     s                   like(vakoty)
      *
      * Definering av datastruktur
      *
     d                 ds
5.20 d d_hele                  1    128
     d d_kav1                  1      4  0
     d d_kko1                  5     10  0
4.01 d d_ksp1                 11     14  0
4.01 d d_kav2                 15     18  0
4.01 d d_kko2                 19     24  0
4.01 d d_ksp2                 25     28  0
4.01 d d_kav3                 29     32  0
4.01 d d_kko3                 33     38  0
4.01 d d_ksp3                 39     42  0
4.01 d d_kav4                 43     46  0
4.01 d d_kko4                 47     52  0
4.01 d d_ksp4                 53     56  0
4.01 d d_kav5                 57     60  0
4.01 d d_kko5                 61     66  0
4.01 d d_ksp5                 67     70  0
4.01 d d_kav6                 71     74  0
4.01 d d_kko6                 75     80  0
4.01 d d_ksp6                 81     84  0
4.01 d d_kav7                 85     88  0
4.01 d d_kko7                 89     94  0
4.01 d d_ksp7                 95     98  0
4.01 d d_khev                 99    102
5.20 d d_nivo                103    103
      *
      * Definering av lda
      *
     d                uds
     d  l_firm               944    946  0
      *
      *  Kontohenvisning kan legges inn på inntil 5 nivå :
      *
      *    1) Statusnivå felles for hele firma
      *    2) Overgruppe
      *    3) Hovedgruppe
      *    4) Undergruppe
      *    5) Varenummer
      *
      *  Oppslag mot tabeller utføres i denne rekkefølge:
      *
      *       - Vareregister
      *       - Undergruppe
      *       - Hovedgrupp
      *       - Overgruppe
      *       - Status
      *
      *  Dersom post finnes og kontohenvisnings kode er utfylt, chaines
      *  kontohenvisningsregister.  Alle konti herfra flyttes over i en
      *  datastruktur som sendes tilbake til programmet som utførte 'call'
      *  'w_nivo' er et hjelpefelt som styrer hvilket nivå som skal
      *  benyttes. Verdien kan være 1 2 3 4 eller 5, som forklart over.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Henter opplysninger om varen                                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Slå opp mot vareregister
      *
     c     p_vare        ifne      *blank
     c                   move      p_vare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1r                            80
     c     *in80         ifeq      *off
     c     vvkhev        ifne      *blank
     c                   move      vvkhev        w_khv5
     c                   move      5             w_nivo
     c                   goto      xtag01
     c                   endif
     c                   endif
     c                   endif
      *
      *  Slå opp mot undergrupperegister
      *
     c     p_ugrp        ifne      *zero
     c                   move      p_ogrp        vugrl1_uogr
     c                   move      p_hgrp        vugrl1_uhgr
     c                   move      p_ugrp        vugrl1_uugr
     c     vugrl1_key    chain     vugrl1r                            80
     c     *in80         ifeq      *off
     c     vgukhv        ifne      *blank
     c                   move      vgukhv        w_khv4
     c                   move      4             w_nivo
     c                   goto      xtag01
     c                   endif
     c                   endif
     c                   endif
      *
      *  Slå opp mot hovedgrupperegister
      *
     c     p_hgrp        ifne      *zero
     c                   move      p_ogrp        vhgrl1_hogr
     c                   move      p_hgrp        vhgrl1_hhgr
     c     vhgrl1_key    chain     vhgrl1r                            80
     c     *in80         ifeq      *off
     c     vghkhv        ifne      *blank
     c                   move      vghkhv        w_khv3
     c                   move      3             w_nivo
     c                   goto      xtag01
     c                   endif
     c                   endif
     c                   endif
      *
      *  Slå opp mot overgrupperegister
      *
     c     p_ogrp        ifne      *zero
     c                   move      p_ogrp        vogrl1_oogr
     c     vogrl1_key    chain     vogrl1r                            80
     c     *in80         ifeq      *off
     c     vgokhv        ifne      *blank
     c                   move      vgokhv        w_khv2
     c                   move      2             w_nivo
     c                   goto      xtag01
     c                   endif
     c                   endif
     c                   endif
      *
      *  Slå opp mot statusregister
      *
     c     fstsl1_key    chain     fstsl1r                            80
     c     *in80         ifeq      *off
     c     faakhv        ifne      *blank
     c                   move      faakhv        w_khv1
     c                   move      1             w_nivo
     c                   goto      xtag01
     c                   endif
     c                   endif
      *
      *  Slå opp mot kontohenvisningsregister
      *
     c     xtag01        tag
      *
     c     w_nivo        ifeq      5
     c                   move      w_khv5        vkhvl1_khev
     c                   endif
      *
     c     w_nivo        ifeq      4
     c                   move      w_khv4        vkhvl1_khev
     c                   endif
      *
     c     w_nivo        ifeq      3
     c                   move      w_khv3        vkhvl1_khev
     c                   endif
      *
     c     w_nivo        ifeq      2
     c                   move      w_khv2        vkhvl1_khev
     c                   endif
      *
     c     w_nivo        ifeq      1
     c                   move      w_khv1        vkhvl1_khev
     c                   endif
      *
      * Oppslag mot kontohenvisningsregister skjer etter følgende prinsipp:
      * -------------------------------------------------------------------
      * 1) Kontohenvisning + leveringstype + ordretype
      * 2) Kontohenvisning + leveringstype + blank i ordretype
      * 3) Kontohenvisning + blank levtype + ordretype
      * 4) Kontohenvisning + blank levtype + blank i ordretype
      * 5) Kontohenvisning + leveringstype + ordretype         (Fra Status)
      * 6) Kontohenvisning + leveringstype + blank i ordretype (Fra Status)
      * 7) Kontohenvisning + blank levtype + ordretype         (Fra Status)
      * 8) Kontohenvisning + blank levtype + blank i ordretype (Fra Status)
      *
5.20  *    Etterfølgende logikk er omskrevet og endringer er ikke
5.20  *    anmerket på linjenivå.
      *
      *    Kontohenvisning + leveringstype + ordretype
      *
     c                   eval      vkhvl1_klty = p_ltyp
     c                   eval      vkhvl1_koty = p_otyp
     c     vkhvl1_key    chain     vkhvl1r
     c                   if        %found
     c                   goto      xtagok
     c                   endif
      *
      *    Kontohenvisning + leveringstype + blank ordretype
      *
     c                   eval      vkhvl1_klty = p_ltyp
     c                   eval      vkhvl1_koty = *blank
     c     vkhvl1_key    chain     vkhvl1r
     c                   if        %found
     c                   goto      xtagok
     c                   endif
      *
      *    Kontohenvisning + blank levtype + ordretype
      *
     c                   eval      vkhvl1_klty = 0
     c                   eval      vkhvl1_koty = p_otyp
     c     vkhvl1_key    chain     vkhvl1r
     c                   if        %found
     c                   goto      xtagok
     c                   endif
      *
      *    Kontohenvisning + blank levtype + blank ordretype
      *
     c                   eval      vkhvl1_klty = 0
     c                   eval      vkhvl1_koty = *blank
     c     vkhvl1_key    chain     vkhvl1r
     c                   if        %found
     c                   goto      xtagok
     c                   endif
      *
      *    Kontohenvisning + leveringstype + ordretype         (Fra Status)
      *
     c                   eval      vkhvl1_khev = w_khv1
     c                   eval      vkhvl1_klty = p_ltyp
     c                   eval      vkhvl1_koty = p_otyp
     c     vkhvl1_key    chain     vkhvl1r
     c                   if        %found
     c                   goto      xtagok
     c                   endif
      *
      *    Kontohenvisning + leveringstype + blank ordretype   (Fra Status)
      *
     c                   eval      vkhvl1_khev = w_khv1
     c                   eval      vkhvl1_klty = p_ltyp
     c                   eval      vkhvl1_koty = *blank
     c     vkhvl1_key    chain     vkhvl1r
     c                   if        %found
     c                   goto      xtagok
     c                   endif
      *
      *    Kontohenvisning + blank levtype + ordretype         (Fra Status)
      *
     c                   eval      vkhvl1_khev = w_khv1
     c                   eval      vkhvl1_klty = 0
     c                   eval      vkhvl1_koty = p_otyp
     c     vkhvl1_key    chain     vkhvl1r
     c                   if        %found
     c                   goto      xtagok
     c                   endif
      *
      *    Kontohenvisning + blank levtype + blank ordretype   (Fra Status)
      *
     c                   eval      vkhvl1_khev = w_khv1
     c                   eval      vkhvl1_klty = 0
     c                   eval      vkhvl1_koty = *blank
     c     vkhvl1_key    chain     vkhvl1r
     c                   if        %found
     c                   goto      xtagok
     c                   endif
      *
      * Dersom ikke funnet nå heller
      * settes statuskode lik 9 og
      * programmet avsluttes
      *
     c     *in80         ifeq      *on
     c                   move      '9'           p_stat
     c                   goto      xslutt
     c                   endif
      *
      *  Kontohenvisning er funnet
      *  oppdater datastruktur
      *
     c     xtagok        tag
     c                   move      vakav1        d_kav1
     c                   move      vakko1        d_kko1
     c                   move      vaksp1        d_ksp1
     c                   move      vakav2        d_kav2
     c                   move      vakko2        d_kko2
     c                   move      vaksp2        d_ksp2
     c                   move      vakav3        d_kav3
     c                   move      vakko3        d_kko3
     c                   move      vaksp3        d_ksp3
     c                   move      vakav4        d_kav4
     c                   move      vakko4        d_kko4
     c                   move      vaksp4        d_ksp4
     c                   move      vakav5        d_kav5
     c                   move      vakko5        d_kko5
     c                   move      vaksp5        d_ksp5
     c                   move      vakav6        d_kav6
     c                   move      vakko6        d_kko6
     c                   move      vaksp6        d_ksp6
     c                   move      vakav7        d_kav7
     c                   move      vakko7        d_kko7
     c                   move      vaksp7        d_ksp7
3.11 c                   move      vkhvl1_khev   d_khev
5.20 c                   move      w_nivo        d_nivo
     c                   move      d_hele        p_hele
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xslutt        tag
      *
     c                   seton                                        lr
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     c                   move      *zero         w_nivo            1 0
     c                   move      *blank        w_khv1            4
     c                   move      *blank        w_khv2            4
     c                   move      *blank        w_khv3            4
     c                   move      *blank        w_khv4            4
     c                   move      *blank        w_khv5            4
      *
      *  Key for file : OFAK-KODE-REGISTER
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for file : OVER-GRUPPE-REGISTER
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      *  Key for file : HOVED-GRUPPE-REGISTER
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      *  Key for file : UNDER-GRUPPE-REGISTER
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      *  Key for file : VARE-REGISTER
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *  Key for file : KONTO-HENVISNINGS-REGISTER
      *
     c     vkhvl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vkhvl1_khev
5.20 c                   kfld                    vkhvl1_klty
     c                   kfld                    vkhvl1_koty
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     *entry        plist
     c                   parm                    p_stat
5.20 c                   parm                    p_ltyp
     c                   parm                    p_otyp
     c                   parm                    p_ogrp
     c                   parm                    p_hgrp
     c                   parm                    p_ugrp
     c                   parm                    p_vare
     c                   parm                    p_hele
      *
      *  Legger inn firmanummer i nøkklene
      *
     c                   move      l_firm        w_firm
      *
      *  Legg inn nuller
      *  i datastruktur
      *
     c                   move      *zero         d_kav1
     c                   move      *zero         d_kko1
     c                   move      *zero         d_ksp1
     c                   move      *zero         d_kav2
     c                   move      *zero         d_kko2
     c                   move      *zero         d_ksp2
     c                   move      *zero         d_kav3
     c                   move      *zero         d_kko3
     c                   move      *zero         d_ksp3
     c                   move      *zero         d_kav4
     c                   move      *zero         d_kko4
     c                   move      *zero         d_ksp4
     c                   move      *zero         d_kav5
     c                   move      *zero         d_kko5
     c                   move      *zero         d_ksp5
     c                   move      *zero         d_kav6
     c                   move      *zero         d_kko6
     c                   move      *zero         d_ksp6
     c                   move      *zero         d_kav7
     c                   move      *zero         d_kko7
     c                   move      *zero         d_ksp7
3.11 c                   move      *blank        d_khev
      *
     csr                 endsr
