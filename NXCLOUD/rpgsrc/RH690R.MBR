     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RH690R                                        *
      * Beskrivelse . . : Hovedbok - saldooverføring - parameter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
T5.30 * V5.30     161104  Lagt inn mulighet for regnskapsår > *year HFL *
      *                                                                 *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *       KC = F3  = Exit                                           *
      *       KE = F5  = Forny                                          *
      *       KL = F12 = Annuller                                       *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       10 = Roll                                                 *
      *       12 = Sfldsp                                               *
      *       13 = Sfldspctl                                            *
      *       14 = Sflclr                                               *
      *       15 = Sflend                                               *
      *       16 = Readc 16 on, no more records                         *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     fra19l1    if   e           k disk    rename(ra19pfr:ra19l1r)
     frh690d    cf   e             workstn
     f                                     sfile(b1wsf:srrn01)
      *
     d                uds
     d rhpkda                300    305  0
     d rhprÅr                         4  0
     d rhpovr                         1
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d sfrn01          s                   like(srrn01)
     d spge01          s              3  0
     d srrn01          s              4  0
     d ssrn01          s                   like(srrn01)
     d wactli          s              2  0
     d wactpo          s              3  0
     d wpdat1          s              6  0
     d p_in03          s              1
     d wpokok          s              1
     d wwaar           s              2  0
     d w_firm          s                   like(l_firm)
     d wwskod          s                   like(raskod)
     d wwstxt          s                   like(rastxt)
      *
T5.30d var             s             40    dim(2) ctdata perrcd(1)               Varsel m/svarkrav
T5.30d wsavli          s              2  0
T5.30d wsavpo          s              3  0
      *
     c     dummy         tag
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av felter                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      *     nn - er 01 for subfile 01,
      *             02 for subfile 02,
      *             03 for subfile 03, o.s.v.
      * SRRNnn - Relativt recordnummer i subfile
      * SPGEnn - Sidestørrelse i subfile (page)
      * SFRNnn - Første recordnummer på siden
      * SSRNnn - Siste  recordnummer på siden
      * SVRNnn - Vis den side som inneholder denne record.
      *
      * Forklaring på spesielle felter
      * som benyttes ved windows.
      * - - - - - - - - - - - - - - - - -
      *
      * WACTLI - Actual line, angir startlinje for vindu.
      * WACTPO - Actual posision, angir startposisjon for vindu.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * B1WFL  - B1 Subfile - vindu for valg av post
      * B2WCT  - B2 Subfile - vindu control record
      * B2CMD  - B2 Vindu for CMD-taster
      * ASUME  -    Record for å styre at vindu legges over
      *             eksisterende data.
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * B2WCT Behandle åpningsbildet med subfile                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      xdsp01                                       DSP SFL
      *
      * Avslutt program
      *
     c                   if        *inkc = *on                                           .....
     c                             or *inkl = *on                                            .
     c                   move      '1'           p_in03                                      .
     c                   goto      xslutt                                                    .
     c                   endif                                                           .....
      *
      * Forny
      *
     c                   if        *inke = *on                                           .....
      *                                                                 .
     c                   if        sfrn01 <> *zero                                           .
     c     sfrn01        chain     b1wsf                              60                     .
     c                   endif                                                               .
      *                                                                 .
     c                   eval      wwskod = *zero                                            .
      *                                                                 .
     c     ra19l1_key    setll     ra19l1                                                    .
      *                                                                 .
     c                   exsr      xclr01                                       TØM SFL      .
     c                   exsr      xcrt01                                       CRT SFL      .
     c                   goto      b2tagb                                                    .
     c                   endif                                                           .....
      *
      * Vedlikehold av kontoklasse
      *
     c                   if        *inkd = *on                                           .....
     c                   call      'RA519R'                                                  .
     c                   parm                    w_firm                                      .
     c                   parm                    wwskod                                      .
     c                   parm                    wwstxt                                      .
      *                                                                 .
     c                   eval      wwskod = *zero                                            .
      *                                                                 .
     c     ra19l1_key    setll     ra19l1                                                    .
      *                                                                 .
     c                   exsr      xclr01                                       TØM SFL      .
     c                   exsr      xcrt01                                       CRT SFL      .
     c                   goto      b2tagb                                                    .
     c                   endif                                                           .....
      *
      * Roll
      *
     c                   if        *in10 = *on                                           .....
     c                   exsr      xcrt01                                       CRT SFL      .
     c                   goto      b2tagb                                                    .
     c                   endif                                                           .....
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Behandling av parameterbilde                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c                   exsr      xc2win
      *
     c                   goto      xslutt
      *
      /space 3
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * C2WIN Parameter for saldooverføring                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xc2win        begsr
      *
     c     c2taga        tag
      *
      * Bare data
      *
     c     c2tagb        tag
      *
     c                   eval      wactli = 7
     c                   eval      wactpo = 10
     c                   exfmt     c2win
      *
      * Gå ut
      *
     c                   if        *inkc = *on                                  .....
     c                   move      '1'           p_in03                             .
     c                   goto      xslutt                                           .
     c                   endif                                                  .....
      *
      * Sjekke om kjøredato er gyldig
      *
     c                   if        c2pkda = *zero                                        .....
     c                   move      udate         c2pkda                                      .
     c                   endif                                                           .....
      *
     c                   move      c2pkda        wwaar
      *
     c     *dmy          test(d)                 c2pkda                 33
     c                   if        *in33 = *on                                           .....
     c                   goto      c2taga                                                    .
     c                   endif                                                           .....
      *
      *  Kontroll av regnskapsår
T5.30 *  Varsel ved regnskapsår større enn *year
      *
     c                   if        c2prÅr > *year                                        .....
T5.30c                   movel     var(1)        m1mld1
T5.30c                   movel     var(2)        m1mld2
T5.30c                   move      'N'           m1svar
T5.30c                   exsr      xm1win
T5.30c                   if        m1svar = 'N'
     c                   eval      *in34 = *on                                               .
     c                   goto      c2taga                                                    .
T5.30c                   endif
     c                   endif                                                           .....
      *
      * Skriv parameter til LDA
      *
     c                   move      c2pkda        rhpkda
     c                   move      c2prÅr        rhprÅr
     c                   move      c2povr        rhpovr
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Tøm subfile                                                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xclr01        begsr
      *
     c                   move      *on           *in14
     c                   write     b2wct
     c                   move      *off          *in14
      *
     c                   move      *off          *in15
     c                   eval      srrn01 = 0
     c                   eval      sfrn01 = 0
     c                   eval      ssrn01 = 0
     c                   eval      spge01 = 0
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Fyll opp subfile                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xcrt01        begsr
      *
     c                   eval      spge01 = spge01 + 13                         SFL END
     c                   eval      srrn01 = ssrn01
      *
     c                   dou       srrn01 = spge01                                       .....
     c                   read      ra19l1                                 15                 .
     c     *in15         cabeq     *on           xend01                         EOF          .
      *                                                                 .
     c                   if        rasfir <> w_firm                                          .
     c                   move      *on           *in15                                       .
     c                   endif                                                               .
      *                                                                 .
     c     *in15         cabeq     *on           xend01                         NYTT FIRMA   .
      *                                                                 .
     c                   if        rassdk = *blank                                     ..... .
     c                   goto      xenddo                                                  . .
     c                   endif                                                         ..... .
      *                                                                 .
     c                   eval      srrn01 = srrn01 + 1                                       .
     c                   move      raskod        b1skod                                      .
     c                   move      rastxt        b1stxt                                      .
     c                   move      rassdk        b1ssdk                                      .
     c                   write     b1wsf                                                     .
      *                                                                 .
     c     xenddo        tag                                                                 .
      *                                                                 .
     c                   enddo                                                           .....
      *
     c     xend01        tag
      *
     c                   if        srrn01 > ssrn01                                       .....
     c                   eval      sfrn01 = ssrn01 + 1                                       .
     c                   endif                                                           .....
      *
     c                   eval      ssrn01 = srrn01
     c                   eval      svrn01 = sfrn01
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Display subfile                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xdsp01        begsr
      *
     c                   if        srrn01 <> 0                                           .....
     c                   move      *on           *in12                                       .
     c                   endif                                                           .....
      *
     c                   move      *on           *in13
     c                   exfmt     b2wct
     c                   move      *off          *in12
     c                   move      *off          *in13
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
T5.30 * M1WIN Behandle bilde for melding med svar                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
T5.30c     xm1win        begsr
      *
T5.30c                   eval      wsavli = 6
T5.30c                   eval      wsavpo = 22
T5.30c                   eval      w_arow = wsavli
T5.30c                   eval      w_acol = wsavpo
      *
T5.30c                   exfmt     m1win
      *
T5.30c                   if        *inkc = *on
T5.30c                   move      'N'           m1svar
T5.30c                   endif
      *
T5.30c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   eval      spge01 = 0
     c                   eval      srrn01 = 0
     c                   eval      wactli = 0
     c                   eval      wactpo = 0
      *
     c                   eval      wpdat1 = 0
     c                   eval      wpokok = *blank
      *
     c                   eval      p_in03 = ' '
      *
      *
      *
      * Legge inn firmanummer og navn fra parameter
      *
     c                   move      l_firm        w_firm
      *
      * Sett inn std.verdier i
      * parameterbilde
      *
     c                   movel     0101          c2pkda
     c                   move      uyear         c2pkda
     c                   move      *year         c2prÅr
     c                   move      'M'           c2povr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_in03
      *
      * Key for kontoklasse
      *
     c     ra19l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    wwskod
      *
     c     ra19l1_key    setll     ra19l1
      *
     c                   exsr      xcrt01
      *
     c                   endsr
      *
**
Valgt periode er neste år.
Bekreft dersom dette er riktig.
