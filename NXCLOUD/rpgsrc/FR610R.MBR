     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FR610R
      * Beskrivelse . . : Rabattmatrise, kundebrev - parameter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       110599 HKO  Nytt program
      *       130504 HKO  Lagt inn valg av sortiment
5.70  * PRGR  221008 bof  Utvidet med prisgruppe                       l
6.20  *       121110 bof  Utvidet med lager på varesortiment
7.01  *       290618 bsa  Henter prisgruppe fra brukers lager.
8.01  *PRG2   100622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra06pf    if   e           k disk    rename(ra06pfr:ra06pfr)
5.70 fra30l1    if   e           k disk    rename(ra30pfr:ra30l1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fvshel1    if   e           k disk    rename(vshepfr:vshel1r)
      *
     ffr610d    cf   e             workstn
      *
      * Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Datastruktur for parameterliste -ut
      *
     d                 ds
     d d_list                       256
     d  d_firm                        3  0 overlay(d_list)
     d  d_rkat                        3  0 overlay(d_list:4)
     d  d_kund                        6  0 overlay(d_list:7)
     d  d_kpro                        6  0 overlay(d_list:13)
     d  d_vsor                       10    overlay(d_list:19)
     d  d_rdat                       10d   overlay(d_list:29) datfmt(*iso)
     d  d_txt1                       60    overlay(d_list:39)
     d  d_txt2                       60    overlay(d_list:99)
     d  d_txt3                       60    overlay(d_list:159)
     d  d_valg                        1  0 overlay(d_list:219)
8.01 d  d_prgr                        2    overlay(d_list:220)
     d  d_dumm                        1    overlay(d_list:256) inz('*')
      *
      * Definering av variable i nøkler
      *
     d ra06pf_fkod     s                   like(rafkod)
5.70 d ra30l1_dkod     s                   like(redkod)
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d vshel1_vsor     s                   like(vcvsor)
6.20 d vshel1_lage     s                   like(vclage)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_udat          s               d   datfmt(*iso)
     d w_stmp          s               z
     d w_stmp_alf      s             26
     d w_memb          s             10
6.20 d w_lage          s                   like(vclage)
8.01 d w_prgr          s              2
      *
     d w_firm          s                   like(flfirm)
     d w_besk          s                   like(vcbesk)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandling av skjermbilde
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * F3=Avslutt
      *
     c                   if        *inkc or *inkl
     c                   goto      avslutt
     c                   endif
      *
      * F4=Spørring
      *
     c                   if        *inkd = *on
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endif
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
      * Kaller utskriftsprogram
      *
     c                   exsr      flytt_par
      *
     c                   call      'FR610C'
     c                   parm                    d_list
     c                   parm                    w_memb
      *
     c                   goto      b1taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørringer (FD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
5.70  *
5.70  * Prisgruppe
5.70  *
5.70 c                   if        w_afld = 'B1PRGR'
5.70  *
5.70 c                   call      'RA530R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    b1prgr
5.70 c                   parm                    b1ptxt
5.70  *
5.70 c                   leavesr
5.70 c                   endif
      *
      * Rabattkategori
      *
     c                   if        w_afld = 'B1RKAT'
      *
     c                   call      'RA506R'
     c                   parm                    w_firm
     c                   parm                    b1rkat
     c                   parm                    b1rtxt
      *
     c                   leavesr
     c                   endif
      *
      * Kunde
      *
     c                   if        w_afld = 'B1KUND'
      *
     c                   eval      b1kpro = 0
     c                   eval      b1pnav = *blank
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    b1kund
     c                   parm                    b1knav
      *
     c                   leavesr
     c                   endif
      *
      * Kundeprosjekt
      *
     c                   if        w_afld = 'B1KPRO'
      *
     c                   call      'FL500R'
     c                   parm                    w_firm
     c                   parm                    b1kund
     c                   parm                    b1kpro
     c                   parm                    b1knav
     c                   parm                    b1pnav
      *
     c                   leavesr
     c                   endif
      *
      * Varesortiment
      *
     c                   if        w_afld = 'B1VSOR'
      *
     c                   call      'VC500R'
     c                   parm                    w_firm
     c                   parm                    b1vsor
6.20 c                   parm                    w_lage
     c                   parm                    w_besk
6.20 c                   eval      w_lage = 0
      *
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
5.70 c                   eval      b1ptxt = *blank
     c                   eval      b1rtxt = *blank
     c                   eval      b1knav = *blank
     c                   eval      b1pnav = *blank
5.70  *
5.70  * Prisgruppe
5.70  *
5.70 c                   if        b1prgr <> *blank
5.70 c                   eval      ra30l1_dkod = b1prgr
5.70 c     ra30l1_key    chain     ra30l1                             33
5.70 c                   if        *in40 = *on
5.70 c                   eval      b_feil = *on
5.70 c                   leavesr
5.70 c                   endif
5.70 c                   eval      b1ptxt = redtxt
5.70 c                   endif
      *
      * Rabattkategori / kunde
      *
     c                   if        b1rkat = 0 and
     c                             b1kund = 0
     c                   eval      *in31 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b1rkat <> 0 and
     c                             b1kund <> 0
     c                   eval      *in32 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Rabattkategori
      *
     c                   if        b1rkat <> 0
     c                   eval      ra06pf_fkod = b1rkat
     c     ra06pf_key    chain     ra06pf                             33
     c                   if        *in33 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1rtxt = raftxt
     c                   endif
      *
      * Kunde / kundeprosjekt
      *
     c                   if        b1kund <> 0
     c                   eval      rkunl1_kund = b1kund
     c     rkunl1_key    chain     rkunl1                             34
     c                   if        *in34 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1knav = rknavn
     c                   endif
      *
     c                   if        b1kund = 0 and
     c                             b1kpro <> 0
     c                   eval      *in35 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b1kpro <> 0
     c                   eval      fkprl1_kund = b1kund
     c                   eval      fkprl1_kpro = b1kpro
     c     fkprl1_key    chain     fkprl1                             36
     c                   if        *in36 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1pnav = flnavn
     c                   endif
      *
      * Varesortiment
      *
     c                   if        b1vsor <> *blank
     c                   eval      vshel1_vsor = b1vsor
6.20 c                   eval      vshel1_lage = w_lage
     c     vshel1_key    chain     vshel1                             37
     c                   if        *in37 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Rabatt-dato
      *
     c                   if        b1rdat = 0
     c     *dmy          move      w_udat        b1rdat
     c                   endif
      *
     c     *dmy          test(d)                 b1rdat                 38
     c                   if        *in38 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Valg
      *
     c                   if        b1valg <> 0 and
     c                             b1valg <> 1
     c                   eval      *in39 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for flytting av felter til DS-parametre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     flytt_par     begsr
      *
     c                   eval      d_firm = w_firm
      *
5.70 c                   eval      d_prgr = b1prgr
     c                   eval      d_rkat = b1rkat
     c                   eval      d_kund = b1kund
     c                   eval      d_kpro = b1kpro
     c                   eval      d_vsor = b1vsor
     c                   eval      d_txt1 = b1txt1
     c                   eval      d_txt2 = b1txt2
     c                   eval      d_txt3 = b1txt3
     c                   eval      d_valg = b1valg
      *
     c     *dmy          move      b1rdat        d_rdat
      *
      * Dersom kundeprosjekt er valgt, skal kundebrev skrives ut
      * uavhengig av om prosjektet har egne priser/rabatter eller ikke
      *
     c                   if        d_kpro <> 0
     c                   eval      d_valg = 1
     c                   endif
      *
      * Lager membernavn sammensatt av 'W' + timestamp
      *
     c                   time                    w_stmp
      *
     c                   move      w_stmp        w_stmp_alf
     c                   eval      w_memb = 'W' +
     c                                      %subst(w_stmp_alf:12:2) +
     c                                      %subst(w_stmp_alf:15:2) +
     c                                      %subst(w_stmp_alf:18:2) +
     c                                      %subst(w_stmp_alf:21:3)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på rabattkategori
      *
     c     ra06pf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra06pf_fkod
      *
      * Key for oppslag på prisgruppe
      *
     c     ra30l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra30l1_dkod
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag på varesortiment-hode
      *
     c     vshel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vshel1_vsor
6.20 c                   kfld                    vshel1_lage
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      * her kan man ikke bruker sortiment med lager - enn så lenge
6.20 c                   eval      w_lage = 0
      *
      * Initiering av skjermfelter
      *
     c                   time                    w_udat                                      .
     c     *dmy          move      w_udat        b1rdat
7.01  *
7.01  * henter prisgruppe
7.01  *
7.01 c                   call      'VL711R   '
7.01 c                   parm                    w_prgr
7.01  *
7.01 c                   eval      b1prgr = w_prgr
      *
     c                   endsr
