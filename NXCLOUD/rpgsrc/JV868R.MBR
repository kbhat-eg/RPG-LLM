     h option(*nodebugio) datedit(*dmy)

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JV868R
      * Beskrivelse . . : Oppdater fra NOBB, deltager
      *
      *                   Oppdaterer deltager NOBB (XML-format)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       040414 BKV
6.20  * 6.20  040814 Bsa  Sjekker om gyldig dato
6.31  * 6.30  040814 Bsa  Lang bindestrek konv til kort.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjlevlu    uf a e           k disk    rename(jlevpfr:jlevlur)
     f                                     infsr(FileErr)
     fjstsl1    if   e           k disk    rename(jstspfr:jstsl1r)
     f                                     infsr(FileErr)
     fjlkplu    uf   e           k disk    rename(jlkppfr:jlkplur)
     fjlgslu    uf   e           k disk    rename(jlgspfr:jlgslur)
      *
      * Definering av variable i nøkler
      *
     d jlevlu_ldor     s                   like(jlldor)

     d jlkplu_pldo     s                   like(jkpldo)
     d jlgslu_gldo     s                   like(jlgldo)
      * Brukes som parametre til jobblogging
     d p_jbid          s             41
     d p_jsts          s              2  0
     d p_jmld          s            200
6.20 d w_dato          s               d   datfmt(*iso)
      *
      * Definering av Local Data Area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
     d w_firm          s              3  0
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_stat          s              1
     d p_orec          s            100
     d p_hrec          s            100
     d p_urec          s            100
     d p_drec          s            600
     d p_job_log       s             50
     d p_inlr          s              1
      *
      *  Dataelementer leverandør
      *
     d                 ds
     d d_drec                       600
     d  d_created                    19    overlay(d_drec:1)
     d  d_updated                    19    overlay(d_drec:20)
     d  d_deleted                    19    overlay(d_drec:40)
     d  d_dtnr                        6    overlay(d_drec:41)
     d    d_dtnr_num                  6  0 overlay(d_dtnr:1)
     d  d_dtyp                        1    overlay(d_drec:47)
     d    d_dtyp_num                  1  0 overlay(d_dtyp:1)
     d  d_navn                       35    overlay(d_drec:48)
     d  d_adr1                       35    overlay(d_drec:83)
     d  d_adr2                       35    overlay(d_drec:118)
     d  d_ponr                       15    overlay(d_drec:153)
     d  d_pstd                       35    overlay(d_drec:168)
     d  d_land                       20    overlay(d_drec:193)
     d  d_mail                       50    overlay(d_drec:213)
     d  d_urla                       50    overlay(d_drec:263)
     d  d_tlfn                       15    overlay(d_drec:313)
     d  d_faxn                       15    overlay(d_drec:328)
     d  d_orgn                        9    overlay(d_drec:343)
     d   d_orgn_num                   9  0 overlay(d_orgn:1)
     d  d_valu                        3    overlay(d_drec:352)
     d  d_kper                       35    overlay(d_drec:355)
     d  d_gron                        1    overlay(d_drec:390)
     d   d_gron_num                   1  0 overlay(d_gron:1)
     d  d_mmrk                       10    overlay(d_drec:391)
     d  d_bgir                       11    overlay(d_drec:401)
     d   d_bgir_num                  11  0 overlay(d_bgir:1)
     d  d_pgir                       11    overlay(d_drec:412)
     d   d_pgir_num                  11  0 overlay(d_pgir:1)
     d  d_faxo                       15    overlay(d_drec:423)
     d  d_crea                       10    overlay(d_drec:438)
     d   d_crea_iso                    d   overlay(d_crea:1) datfmt(*iso)
     d  d_upda                       10    overlay(d_drec:448)
     d   d_upda_iso                    d   overlay(d_upda:1) datfmt(*iso)
     d  d_dele                       10    overlay(d_drec:458)
     d   d_dele_iso                    d   overlay(d_dele:1) datfmt(*iso)
      *
     d d_job_log                     50
     d  w_alevt                       5    overlay(d_job_log:2)
     d  w_alev                        5  0 overlay(w_alevt:1)
      * Definering av variable
      *
     d b_inlr          s              1
     d b_vfel          s              1    inz(*off)
     d b_slett         s              1    inz(*off)
      *
      * Datastruktur for informasjon ved exceptions
      *
     d PgmError       sds
     d  proc_name        *proc
     d  pgm_status       *STATUS
     d  prv_status            16     20s 0
     d  line_numm             21     28
     d  routine          *ROUTINE
     d  parms            *PARMS
     d  excp_type             40     42
     d  excp_numm             43     46
     d  pgm_lib               81     90
     d  excp_data             91    170
     d  excp_id              171    174
     d  date                 191    198
     d  year                 199    200s 0
     d  last_file            201    208
     d  file_info            209    243
     d  job_name             244    253
     d  job_user             254    263
     d  job_numb             264    269s 0

      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

     c                   eval      d_job_log = p_job_log
     c                   if        p_inlr = *on
     c                   eval      d_drec = p_drec
      *
     c                   exsr      skr_delt
      *
     c                   endif

      * retunerer jobb status
     c                   eval      p_job_log = d_job_log

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     avslutt       tag

     c                   if        p_inlr <> *on
     c                   eval      *inlr = *on
     c                   endif
     c                   return

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Error rutine for file errors
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     FileErr       begsr
      *
     c                   eval      p_stat = '1'
     c                   eval      p_jsts = 50
     c                   eval      p_jmld = 'Fil ' + %trim(last_file) +
     c                             ' feil. Info=' + %trim(excp_data) +
     c                             '. Job=' + %trim(job_name) + '/' +
     c                             %trim(job_user) + '/' +
     c                             %char(job_numb)
      *
     c                   call      'AB705R'
     c                   parm                    p_jbid
     c                   parm                    p_jsts
     c                   parm                    p_jmld
      *
     c                   goto      avslutt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer deltaker/levr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_delt      begsr
      *
      *
      * sjekker om utmeldt
     c                   if        d_dtyp_num = 5
     c                   exsr      slett_delt
     c                   leavesr
     c                   endif
      *
     c                   clear                   jlevlur
      *
     c                   eval      jlevlu_ldor = d_dtnr_num
     c     jlevlu_key    chain     jlevlur
      *
     c*                  if        d_stat = '4'
     c*                  if        %found
     c*                  delete    jlevlur
     c*                  endif
     c*                  leavesr
     c*                  endif
      *
     c                   eval      jlnavn = %trim(d_navn)
6.31 c     x'3F':'-'     xlate     jlnavn        jlnavn
     c                   eval      jladr1 = %trim(d_adr1)
      *                  eval      jladr2 = %trim(d_adr2)
     c                   eval      jlponr = %trim(d_ponr)
     c                   eval      jlsted = %trim(d_pstd)
     c                   eval      jltlfn = %trim(d_tlfn)
     c                   eval      jlmobn = *blank
     c                   eval      jlfaxn = %trim(d_faxn)
     c                   eval      jlfaxo = %trim(d_faxo)
     c                   eval      jlmail = %trim(d_mail)
     c                   eval      jlurla = %trim(d_urla)
     c                   eval      jlkprs = %trim(d_kper)
      *                  eval      jlktlf = %trim(d_btlf)
     c                   eval      jlkmob = *blank
     c*                  eval      jlkfax = %trim(d_bfax)
     c*                  eval      jlkmai = %trim(d_bmai)
     c                   eval      jlorgn = d_orgn_num
     c*                  eval      jleanl = d_eanl_num
     c*                  eval      jlvalu = %trim(d_valu)
     c                   eval      jlland = %trim(d_land)
     c                   eval      jldtyp = d_dtyp_num
     c                   eval      jlgrpk = d_gron_num
     c                   eval      jlmmrk = d_mmrk
     c                   eval      jlbgir = d_bgir_num
     c                   eval      jlpgir = d_pgir_num
6.20  * Sjekker gyldig dato
6.20  /Free
6.20           test(de) *iso d_crea;
6.20           if %error;
6.20           d_crea_iso = w_dato;
6.20           endif;
6.20
6.20           test(de) *iso d_upda;
6.20           if %error;
6.20           d_upda_iso = w_dato;
6.20           endif;
6.20  /End-Free
     c                   eval      jlnoda = d_crea_iso
     c                   eval      jlneda = d_upda_iso
     c                   eval      jlnsda = d_dele_iso
     c                   eval      jleusr = l_user
      *
     c                   if        jlfaxo <> *blank and
     c                             jlfaxn = *blank
     c                   eval      jlfaxn = jlfaxo
     c                   endif
      *
     c                   time                    jledat
     c                   time                    jletim
      *
     c                   if        %found
     c                   update    jlevlur
     c                   else
     c                   eval      w_alev = w_alev + 1
     c                   eval      jlldor = d_dtnr_num
     c                   time                    jlodat
     c                   write     jlevlur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hvis mulig så slettes deltaker
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slett_delt    begsr
      *
     c                   eval      b_slett = *off
     c                   eval      jlevlu_ldor = d_dtnr_num
     c     jlevlu_key    chain     jlevlur
     c                   if        %found(jlevlu) and
     c                             jlenum = 0
      *
     c                   delete    jlevlur
     c                   eval      b_slett = *on
      * sletter deltakers gln-data
     c                   eval      jlgslu_gldo = d_dtnr_num
     c     jlgslu_key    setll     jlgslur
     c     jlgslu_key    reade     jlgslur
     c                   dow       not %eof(jlgslu)
     c                   delete    jlgslur
     c     jlgslu_key    reade     jlgslur
     c                   enddo
      * sletter deltakers kontaktpersoner
     c                   eval      jlkplu_pldo = d_dtnr_num
     c     jlkplu_key    setll     jlkplur
     c     jlkplu_key    reade     jlkplur
     c                   dow       not %eof(jlkplu)
     c                   delete    jlkplur
     c     jlkplu_key    reade     jlkplur
     c                   enddo
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_drec
     c                   parm                    p_job_log
     c                   parm                    p_inlr
      *
      * Key for oppdatering av leverandør
      *
     c     jlevlu_key    klist
     c                   kfld                    jlevlu_ldor

      *
      * Key for oppslag på status
      *
     c     jstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for rydding av deltaker kontaktpersoner
      *
     c     jlkplu_key    klist
     c                   kfld                    jlkplu_pldo
      *
      * Key for rydding av deltaker gs1
      *
     c     jlgslu_key    klist
     c                   kfld                    jlgslu_gldo
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = l_firm
6.20 c                   time                    w_dato
      *
     c     jstsl1_key    chain     jstsl1
      *
     c                   endsr

