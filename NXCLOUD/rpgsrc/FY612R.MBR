     H/TITLE  Utskrift av Ordre-slettet-liste
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FY612R
      * Beskrivelse . . : Utskrift av ordre-slettet-liste
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * 6.10  220609 BSA  Oppdatert med sporingsinformasjon
6.nu  * 6.30  170614 bof  Endring ihht. nummerutvidelse
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     Frkunl1    IF   E           K DISK
     F                                     RENAME(RKUNPFR:rkunl1r)
      *
     FFSTSL1    IF   E           K DISK
     F                                     RENAME(FSTSPFR:FSTSL1R)
     FFDELL1    IF   E           K DISK
     F                                     RENAME(FDELPFR:FDELL1R)
     FFDELLU    UF   E           K DISK
     F                                     RENAME(FDELPFR:FDELLUR)
     FFY612P    O    E             PRINTER
      *
      *
     D                 DS
      *                                                      Parameter-inn
     D  LDPREC                 1     64
     D  LDVAL1                 1      1  0
     D  LDOTY1                 2      3
     D  LDOTY2                 4      5
     D  LDOTY3                 6      7
     D  LDOTY4                 8      9
     D  LDOTY5                10     11
     D  LDOTY6                12     13
      *
      * Hente inn firma fra local
      *
     D                UDS
6.10 d l_user                911    920
     D  DSFIRM               944    946  0
     D  l_fnav               951    980
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av formater                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede formater
      * - - - - - - - - - -
      *
      * A1HDR  - Heading A1 på rapport
      * D1DET  - Detail  D1 på rapport
      * T1TOT  - Total   T1 på rapport
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A1HDR Behandle heading A1                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *  Henter neste ledige SLETTE-LISTE-nummer fra Nummer-register
      *
     C     LDVAL1        IFEQ      1
     C                   MOVE      *ZERO         WSNUMM
     C                   MOVE      '0'           WSKODE
     C                   EXSR      HNTNUM
     C                   MOVE      WSNUMM        D1LIST
     C                   ENDIF
      *
      * Skrive heading
      *
     C                   WRITE     A1HDR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * D1DET Behandle file                                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Lese file
      *
     C     DELKEY        SETLL     FDELL1
     C                   READ      FDELL1                                 15
      *
     C     *IN15         DOWEQ     '0'
     C     FYPRTK        IFNE      1
      *
      * Er ingen ordretyper oppgitt skal alt være med
      *
     C     LDOTY1        IFEQ      *BLANK
     C     LDOTY2        IFEQ      *BLANK
     C     LDOTY3        IFEQ      *BLANK
     C     LDOTY4        IFEQ      *BLANK
     C     LDOTY5        IFEQ      *BLANK
     C     LDOTY6        IFEQ      *BLANK
     C                   GOTO      XTAG01
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *
      * Sjekk om ordretype skal være med
      *
     C     FYOTYP        IFNE      LDOTY1
     C     FYOTYP        IFNE      LDOTY2
     C     FYOTYP        IFNE      LDOTY3
     C     FYOTYP        IFNE      LDOTY4
     C     FYOTYP        IFNE      LDOTY5
     C     FYOTYP        IFNE      LDOTY6
     C                   GOTO      XTAG02
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *
     C     XTAG01        TAG
      *
      * Hent opplysninger fra KUNDE-REGISTER
      *
     C                   MOVE      FYKUND        KUNNUM
     C     KUNKEY        CHAIN     rkunl1r                            64
     C                   MOVE      RKNAVN        D1NAVN
      *
      * Oppdaterer ORDRE-SLETTET-REGISTER, Posten er tatt med på liste
      *
     C     LDVAL1        IFEQ      1
     C                   MOVE      FYFIRM        DELFIR
     C                   MOVE      FYAARR        DELAAR
     C                   MOVE      FYNUMM        DELNUM
     C                   MOVE      FYSUFF        DELSUF
     C     DELKEY        CHAIN     FDELLUR                            92
     C                   Z-ADD     1             FYPRTK
6.10 c                   time                    fyedat
6.10 c                   time                    fyetim
6.10 c                   eval      fyeusr = l_user
     C                   UPDATE    FDELLUR
     C                   ENDIF
      *
     c                   eval      d1odat = 0
     c                   eval      d1sdat = 0
     c                   eval      d1time = 0
      *
      * Snu ordre-dato
      *
     c                   if        fybdat <> *loval
     c     *dmy          move      fybdat        d1odat
     c                   endif
      *
      * Snu slettet-dato
      *
     c                   if        fydate <> *loval
     c     *dmy          move      fydate        d1sdat
     c                   endif
      *
      * Snu slettet tidspunkt
      *
     c                   if        fytime <> *loval
     c     *hms          move      fytime        d1time
     c                   endif
      *
      * Behandle record
      *
     C     FYTOTS        SUB       FYTOTR        D1SALG
     C                   ADD       D1SALG        T1SALG
     C                   ADD       FYTOTK        T1TOTK
     C                   ADD       1             T1POST
     C                   WRITE     D1DET                                30
      *
      * Er det overflow ?
      *
     C     *IN30         IFEQ      *ON
     C                   WRITE     A1HDR
     C                   ENDIF
      *
     C                   ENDIF
     C     XTAG02        TAG
     C                   READ      FDELL1                                 15
     C                   ENDDO
      *
     C     XEND01        TAG
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * T1TOT Behandle total T1                                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      * Skrive total
      *
     C                   WRITE     T1TOT                                30
     C                   GOTO      XSLUTT
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XSLUTT        TAG
     C                   SETON                                        LR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Henter/Oppdatering av NUMMER-REGISTER                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     HNTNUM        BEGSR
      *
     C                   MOVE      FAADEL        WSFELL
     C                   MOVE      *BLANK        WSTYPE
     C                   MOVE      *BLANK        WSFAST
      *
      *  Kaller opp subprogram for henting av nummer fra NUMMER-REGISTER
      *
     C                   CALL      'AS100R'
     C                   PARM                    WSKODE
     C                   PARM                    STSFIR
     C                   PARM                    WSFELL
     C                   PARM                    WSTYPE
     C                   PARM                    WSFAST
     C                   PARM                    WSNUMM
      *
     C                   ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     *INZSR        BEGSR
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     C                   MOVE      *BLANK        WSKODE            1
     C                   MOVE      *BLANK        WSFELL            6
     C                   MOVE      *BLANK        WSTYPE            2
     C                   MOVE      *BLANK        WSFAST           10
6.nu C                   MOVE      *ZERO         WSNUMM            8 0
      *
      *  Redefinering av felter
      *
      *
      *  Redefinering av nøkler
      *
     C     *LIKE         DEFINE    FAAFIR        STSFIR
      *
     C     *LIKE         DEFINE    RKFIRM        KUNFIR
     C     *LIKE         DEFINE    RKKUND        KUNNUM
      *
     C     *LIKE         DEFINE    FYFIRM        DELFIR
     C     *LIKE         DEFINE    FYAARR        DELAAR
     C     *LIKE         DEFINE    FYNUMM        DELNUM
     C     *LIKE         DEFINE    FYSUFF        DELSUF
      *
      * Key for kunderegister
      *
     C     KUNKEY        KLIST
     C                   KFLD                    KUNFIR
     C                   KFLD                    KUNNUM
      *
      * Key for statusregister
      *
     C     STSKEY        KLIST
     C                   KFLD                    STSFIR
      *
      * Key for file kasse/skjermregister
      *
     C     DELKEY        KLIST
     C                   KFLD                    DELFIR
     C                   KFLD                    DELAAR
     C                   KFLD                    DELNUM
     C                   KFLD                    DELSUF
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    WWPARM           64
      *
     C                   MOVE      WWPARM        LDPREC
      *
     C                   MOVEL     l_fnav        A1FNAV
      * Legge inn key
      *
     C                   MOVE      DSFIRM        A1FIRM
     C                   MOVE      DSFIRM        KUNFIR
     C                   MOVE      DSFIRM        STSFIR
     C                   MOVE      DSFIRM        DELFIR
      *
      * Hent opplysninger fra OFAK-STATUS-KODER
      *
     C     STSKEY        CHAIN     FSTSL1R                            69
      *
     C                   ENDSR
