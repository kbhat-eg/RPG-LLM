     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : fo561R
      * Beskrivelse . . : Liste/ hull i nummerserie
      *                   Først testes SOHEPF, så FOHEPF, sFDELPF
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       240811 bof  Nytt program
6.nu  * R6.30 040614 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.3q  * R6.30 260515 bof  datoformat vedr. sql
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsohel1    if   e           k disk    rename(sohepfr:sohel1r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffdell3    if   e           k disk    rename(fdelpfr:fdell3r)
      *
     ffo561p    o    e             printer
      *
      * Definering av parametre
     d p_aarr          s                   like(soaarr)
     d p_fnum          s                   like(sonumm)
     d p_tnum          s                   like(sonumm)
      *
     d w_firm          s                   like(sofirm)
     d w_aarr          s                   like(soaarr)
     d w_fnum          s                   like(sonumm)
     d w_tnum          s                   like(sonumm)
     d w_tell          s                   like(sonumm)
     d b_funn          s              1    inz(*off)
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      *
      * Definering av variabler i nøkler
      *
     d fohel1_numm     s                   like(fonumm)
     d fdell3_numm     s                   like(fynumm)
      *
      *
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO
6.3q C/End-Exec
      *
     c                   exsr      skriv_hdr
      *
      * Les poster
     c                   exsr      les
      * Avslutt program
      *
     c     avslutt       tag
      *
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Leser post
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les           begsr
      *
     c                   exsr      skriv_hdr
      *
     c                   eval      w_tell = w_fnum
      *
     c                   dow       w_tell < w_tnum
     c                   exsr      les_hist
     c                   if        b_funn = *off
     c                   exsr      les_ordre
     c                   if        b_funn = *off
     c                   exsr      les_slet
     c                   endif
     c                   endif
      *
     c                   if        b_funn = *off
     c                   exsr      skriv_mangel
     c                   endif
      *
     c                   eval      w_tell = w_tell + 1
      *
     c                   enddo
      *
     c                   endsr
      *
      *-----------------------------------------------------------------------
      * Subrutine som leser historisk fakturareg på bilagsnr
      *-----------------------------------------------------------------------
      *
     c     les_hist      begsr
      *
     c/exec sql
     c+                  declare sok_h cursor for
     c+                  select sofirm,
     c+                         sonumm,
     c+                         soaarr
     c+                  from   sohepf
     c+                    where sobinr = :w_tell  and
     c+                          sofirm = :w_firm and
     c+                          soaarr > :w_aarr
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_h
     c/end-exec
     c/exec sql
     c+                  open sok_h
     c/end-exec
      *
     c/exec sql
     c+                  fetch next
     c+                  from  sok_h
     c+                  into  :sofirm,
     c+                        :sonumm,
     c+                        :soaarr
     c/end-exec
      *
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   if        *in15 = *on
     c                   leavesr
     c                   endif
      *
      *
     c                   if        sofirm <> w_firm
     c                   eval      *in15 = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      b_funn = *on
      *
     c                   endsr
      *
      *-----------------------------------------------------------------------
      * Subrutine som leser ordreregister
      *-----------------------------------------------------------------------
      *
     c     les_ordre     begsr
      *
     c                   eval      fohel1_numm = w_tell
     c     fohel1_key    chain     fohel1
     c                   if        %found(fohel1)
     c                   eval      b_funn = *on
     c                   endif
      *
     c                   endsr
      *
      *-----------------------------------------------------------------------
      * Subrutine som leser ordre som er slettet
      *-----------------------------------------------------------------------
      *
     c     les_slet      begsr
      *
     c                   eval      fdell3_numm = w_tell
     c     fdell3_key    setll     fdell3
     c     fdell3_key    reade     fdell3
     c                   dow       not %eof(fdell3)
     c                   if        fyaarr >= w_aarr
     c                   eval      b_funn = *on
     c                   leavesr
     c                   endif
      *
     c     fdell3_key    reade     fdell3
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å skrive detaljelinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_mangel  begsr
      *
     c                   eval(h)   d1numm = w_tell
      *
     c                   write     d1det                                30
     c                   if        *in30 = *on
     c                   exsr      skriv_hdr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  OVFLOW    Behandling av overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_hdr     begsr
      *
      *  Skriver heading
      *
     c                   write     a1hdr                                30
      *
     c                   endsr
      *-----------------------------------------------------------------------
      * Subrutine for initiering
      *-----------------------------------------------------------------------
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_aarr
     c                   parm                    p_fnum
     c                   parm                    p_tnum
      *
      *
      * Key for oppslag på slettet ordre
      *
     c     fdell3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fdell3_numm
      *
      * Key for oppslag på ordre-hode-register
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
      *
     c                   eval                    w_aarr = p_aarr
     c                   eval                    w_fnum = p_fnum
     c                   eval                    w_tnum = p_tnum
     c                   eval                    w_firm = l_firm
     c                   eval                    a1aarr = p_aarr
     c                   eval                    a1fnum = p_fnum
     c                   eval                    a1tnum = p_tnum
      *
     c                   endsr
