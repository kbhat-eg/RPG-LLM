     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FX709R
      * Beskrivelse . . : Konv550: Henter momskode på ordre- og fakturalinje
      *
      * Spesialversjon. : Oppdaterer bare ordrelinjer
      * Tilpasset for . : Kunder med feil moms på ordrelinje
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       291004 HKO  Nytt program
      *       260907 AMD  Oppdaterer bare ordrelinjer (fixe-program
      *                   når tilbud eller ordre kommer med feil mva.)
      * 6.10  240609 BSA  Oppdatert med sporingsinformasjon
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffodtlu    uf   e           k disk    rename(fodtpfr:fodtlur)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvasl1    if   e           k disk    rename(vvaspfr:vvasl1r)
      *
6.10  * Definering av local data area
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_firm     s                   like(vvfirm)
     d vvarl1_vare     s                   like(vvvare)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Ordre-linje
      *
     c                   read      fodtlur
     c                   dow       not %eof
      *
     c                   if        fdvare <> *blank and
     c                             fdomva = *blank
      *
     c                   eval      fdomva = '3'
      *
     c                   eval      vvarl1_firm = fdfirm
     c                   eval      vvarl1_vare = fdvare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c     vvarl1_key    chain     vvasl1
     c                   endif
      *
     c                   if        %found and
     c                             vvkmva <> 0
     c                   if        vvkmva = 1
     c                   eval      fdomva = '4'
     c                   else
     c                   eval      fdomva = 'H'
     c                   endif
     c                   endif
      *
6.10 c                   time                    fdedat
6.10 c                   time                    fdetim
6.10 c                   eval      fdeusr = l_user
     c                   update    fodtlur
     c                   endif
      *
     c                   read      fodtlur
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    vvarl1_firm
     c                   kfld                    vvarl1_vare
      *
     c                   endsr
