     H DFTACTGRP(*NO)

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JV867R_XML
      * Beskrivelse . . : Leser NOBB deltager   XML 8.0. Legger data i deltager variable.
      *                   Kaller standard rpg program, som gjør sjekk av data
      *                   og oppdatere basen
      *
      *                   Dette programmet er skrevet i free format RPG
      *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       040414 BKV  Nytt program
6.30  * R6.30 300614 BKV  Korrigering av parameter def
6.31  * R6.30 130814 BKV  Angi xpath i basen
6.32  * R6.30 061014 BKV  Ok hvis ingen moduler, dvs retur dagens dato
6.33  * R6.30 120918 BKV  Leser kontaktpersoner
8.01  *       291223 bof  Takle Foretagsnr på NO-xxxxxxx  format
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.31 fafpslr    if   e           k disk    rename(afpspfr:afpslrr)

      * Definering av Local Data Area
      *
6.31 d                uds
6.31 d l_user                911    920
6.31 d l_firm                944    946  0
6.31 d l_filg                931    933
      *
      * Definering av variabler i nøkler
      *
6.31 d afpslr_ufil     s                   like(l_filg)
6.31 d afpslr_uide     s                   like(afuide)

     d p_stat          s              1
      * Brukes som parametre til jobblogging
     d p_jnav          s             15
     d p_jbid          s             41
     d p_jtxt          s             35
     d p_jsts          s              2  0
     d p_jmld          s            200

     d p_sistDato      s             10a
     d p_sistDelNr     s             20a
6.32 d w_lest          s              1  0 Inz(0)

      * Definering av tabell med meldinger
     d a_meld          s            100    dim(1) ctdata perrcd(1)
      *
      * const
      *
6.33 d c_ant_kon       c                   10

      *
      * START NOBB 8.0 XML definisjon
      *
      * Path til deltager  taggen
     D deltager_path...
     D                 s             66a   Inz(
     d                                     'PartnerDeltagerResult/+
     d                                     DeltagerListe/+
     d                                     Deltager')

      * taggen <b:Deltager>
     d Deltager_t      ds                  Qualified
     d   status                            likeds(status_t)
     d   DeltagerNr...
     d                                6a   varying
     d   Segment...
     d                               15a   varying
     d   DeltagerType...
     d                               20a   varying
     d   Abonnement...
     d                               20a   varying
     d   MarkedsNavn...
     d                              255a   varying
     d   JuridiskNavn...
     d                              255a   varying
     d   Adresse...
     d                                     likeds(Adresse_t) Dim(5)
     D   countAdresse...
     D                                5i 0
     d   ForetagsNr...
     d                               20a   varying
     d   EierGLNNr...
     d                               13a   varying
     d   FirmaEpost...
     d                              100a   varying
     d   URL...
     d                              100a   varying
     d   Bransje...
     d                               50a   varying
     d   Kjede...
     d                              100a   varying
6.33 d   KontaktPersoner...
6.33 d                                     likeds(KontaktPersoner_t) Dim(20)
6.33 D   countKontaktPersoner...
     D                                5i 0
     d   GrontPunkt...
     d                                5a   varying
     d   Miljomerke...
     d                                     likeds(Miljomerke_t) Dim(5)
     D   countMiljomerke...
     D                                5i 0
     d   BankGiro...
     d                               20a   varying
     d   PostGiro...
     d                               20a   varying
     d*   Produsent...
     d*                                     likeds(Produsent_t) Dim(5)
     D*   countProdusent...
     D*                                5i 0
     d*   Prisgiver...
     d*                                     likeds(Prisgiver_t) Dim(5)
     D*   countPrisgiver...
     D*                                5i 0

      * taggen <b:Adresse>
     d Adresse_t...
     d                 ds                  Qualified
     d                                     based(Template)
     d   Beskrivelse...
     d                              100a   varying
     d   Adresse...
     d                                     likeds(Adresse_t1) Dim(5)
     D   countAdresse...
     D                                5i 0
     d   PostNr...
     d                               10a   varying
     d   PostSted...
     d                               50a   varying
     d   Land...
     d                               50a   varying
     d   Telefon...
     d                               20a   varying
     d   Telefax...
     d                               20a   varying
     d   TelefaxOrdre...
     d                               20a   varying
     d*   GLNLokasjon...
     d*                                     likeds(GLNLokasjon_t) Dim(5)
     D*   countGLNLokasjon...
     D*                                5i 0
     d   AdresseType...
     d                               20a   varying
     d   status                            likeds(status_t)

      * taggen <b:Adresse>
     d Adresse_t1...
     d                 ds                  Qualified
     d                                     based(Template)
     D   text                       100a   varying

      * taggen <b:KontaktPersoner>
6.33 d KontaktPersoner_t...
6.33 d                 ds                  Qualified
6.33 d                                     based(Template)
6.33 d   Navn...
6.33 d                              100a   varying
6.33 d   Tittel...
6.33 d                               50a   varying
6.33 d   Telefon...
6.33 d                               20a   varying
6.33 d   Telefax...
6.33 d                               20a   varying
6.33 d   Mobil...
6.33 d                               20a   varying
6.33 d   Epost...
6.33 d                               50a   varying
6.33 d   Rolle...
6.33 d                               20a   varying
     d   status                            likeds(status_t)

      * taggen <b:Miljomerke>
     d Miljomerke_t...
     d                 ds                  Qualified
     d                                     based(Template)
     d   MiljoMerke...
     d                               20a   varying

      ** taggen <b:Produsent>
     d* Produsent_t...
     d*                 ds                  Qualified
     d*                                     based(Template)
     d*   Navn...
     d*                              100a   varying
     d*   ProdusentNr...
     d*                                5a   varying
     d*   Valuta...
     d*                               20a   varying

      * taggen <b:Prisgiver>
     d* Prisgiver_t...
     d*                 ds                  Qualified
     d*                                     based(Template)
     d*   Prisgiver...
     d*                               10a   varying


      * taggen <b:GLNLokasjon>
     d* GLNLokasjon_t...
     d*                 ds                  Qualified
     d*                                     based(Template)
     d*   Nummer...
     d*                               50a   varying
     d*   LokasjonAdresse...
     d*                               50a   varying
     d*   Beskrivelse...
     d*                               50a   varying

      * taggen: <b:status>
     d status_t        ds                  template
     d   created                     25a
     d   updated                     25a

      *
      * stopp nobb 8.0 xml definisjon
      *

6.33  *
6.33  *  Dataelementer kontaktperson
6.33  *
6.33 d d_konrec        ds           200
6.33 d  d_jkpldo                      6    overlay(d_konrec:1)
6.33 d   d_jkpldo_nym                 6  0 overlay(d_jkpldo:1)
6.33 d  d_jkptel                      6    overlay(d_konrec:7)
6.33 d   d_jkptel_num                 6  0 overlay(d_jkptel:1)
6.33 d  d_jkpnav                     35    overlay(d_konrec:13)
6.33 d  d_jkpttl                     35    overlay(d_konrec:48)
6.33 d  d_jkptlf                     15    overlay(d_konrec:83)
6.33 d  d_jkpmob                     15    overlay(d_konrec:98)
6.33 d  d_jkpfax                     15    overlay(d_konrec:113)
6.33 d  d_jkpmai                     50    overlay(d_konrec:128)
6.33 d  d_jkprol                     15    overlay(d_konrec:178)

      *
      *  Dataelementer leverandør
      *
     d                 ds
     d d_drec                       600
     d  d_created                    19    overlay(d_drec:1)
     d  d_updated                    19    overlay(d_drec:20)
     d  d_deleted                    19    overlay(d_drec:40)
     d  d_dtnr                        6    overlay(d_drec:41)
     d    d_dtnr_num                  6  0 overlay(d_dtnr:1)
     d  d_dtyp                        1    overlay(d_drec:47)
     d    d_dtyp_num                  1  0 overlay(d_dtyp:1)
     d  d_navn                       35    overlay(d_drec:48)
     d  d_adr1                       35    overlay(d_drec:83)
     d  d_adr2                       35    overlay(d_drec:118)
     d  d_ponr                       15    overlay(d_drec:153)
     d  d_pstd                       35    overlay(d_drec:168)
     d  d_land                       20    overlay(d_drec:193)
     d  d_mail                       50    overlay(d_drec:213)
     d  d_urla                       50    overlay(d_drec:263)
     d  d_tlfn                       15    overlay(d_drec:313)
     d  d_faxn                       15    overlay(d_drec:328)
     d  d_orgn                        9    overlay(d_drec:343)
     d   d_orgn_num                   9  0 overlay(d_orgn:1)
     d  d_valu                        3    overlay(d_drec:352)
     d  d_kper                       35    overlay(d_drec:355)
     d  d_gron                        1    overlay(d_drec:390)
     d   d_gron_num                   1  0 overlay(d_gron:1)
     d  d_mmrk                       10    overlay(d_drec:391)
     d  d_bgir                       11    overlay(d_drec:401)
     d   d_bgir_num                  11  0 overlay(d_bgir:1)
     d  d_pgir                       11    overlay(d_drec:412)
     d   d_pgir_num                  11  0 overlay(d_pgir:1)
     d  d_faxo                       15    overlay(d_drec:423)
     d  d_crea                       10    overlay(d_drec:438)
     d   d_crea_iso                    d   overlay(d_crea:1) datfmt(*iso)
     d  d_upda                       10    overlay(d_drec:448)
     d   d_upda_iso                    d   overlay(d_upda:1) datfmt(*iso)
     d  d_dele                       10    overlay(d_drec:458)
     d   d_dele_iso                    d   overlay(d_dele:1) datfmt(*iso)

     d d_job_log                     50
     d  w_alevt                       5    overlay(d_job_log:2)
     d  w_alev                        5  0 overlay(w_alevt:1)

     d b_inlr          s              1

      * info og commarea må være likt def. de brukes ikke i programmet, men må
      * være definert
     d info            s             10a
     d options         s            200a   varying

      * prototype for externt program sjekker deltager og lagrer i basen
      *  overfører vara datastruktur som parameter
     d P_BehDelt       pr                  extpgm('JV868R')
     d  d_drec                      600
     d  d_job_log                    50
     d  b_inlr                        1

6.33 d P_BehKonp       pr                  extpgm('JV876R')
6.33 d  d_konrec                    200
6.33 d  d_job_log                    50
6.33 d  b_inlr                        1
6.33 d
     d p_ab705r        pr                  extpgm('AB705R')
     d  p_jbid                       41
     d  p_jsts                        2  0
     d  p_jmld                      200

     d p_ab710r        pr                  extpgm('AB710R')
     d  p_jnav                       15
     d  p_jbid                       41


     d p_ab700r        pr                  extpgm('AB700R')
     d  p_jnav                       15
     d  p_jbid                       41
     d  p_jtxt                       35

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
       // ------------------------------------- prototypes
     d JV868R_XML      pr                  extpgm('JV868R_XML')
     d   filnavn                    255a   options(*varsize)
6.30 d   lengde                       3p 0 const
     d   sistedato                   10a
       // ----------------------- main procedure interface
     d JV868R_XML      pi
     d   filnavn                    255    options(*varsize)
6.30 d   lengde                       3p 0 const
     d   sistedato                   10a

     d   navnet        s            255a   varying
6.30 d   len           s              3i 0

      *
6.31  *
6.31  * Key for propertiesfil
6.31  *
6.31 c     afpslr_key    klist
6.31 c                   kfld                    afpslr_ufil
6.31 c                   kfld                    afpslr_uide

      /free
6.32   w_lest = 0;

       len = lengde;
       navnet = %trim(%subst(filnavn:1:len));

       // vi starter jobblogging
       p_jnav = 'DELTAGERXML';
       p_jtxt = *blank;
       callp p_ab700r (p_jnav : p_jbid : p_jtxt);

       w_alev = *zeros;

6.31   //  Hent inn NOBB 80 XML path som skal brukes
6.31   afpslr_ufil = l_filg;
6.31   afpslr_uide = 'NOBB80DEL';
6.31   chain afpslr_key afpslrr;
6.31   if %found;
6.31     deltager_path = *blanks;
6.31     deltager_path = %trim(afudss);
6.31   else;
6.31     p_stat = '1';
6.31   endif;

6.31   if (p_stat <> '1');

         b_inlr = *on;
         options = 'doc=file +
                    case=any +
                    countprefix=count +
                    datasubf=text +
                    ns=remove +
                    path='+deltager_path+' '+
                   'allowmissing=yes +
                    allowextra=yes';

          // Bruker RPG xml-info til å lese xml filen.
          // Behandling av innleste vgrupper gjøres av LesVgrupp
          xml-into %handler(LesDeltag : info) %xml(navnet: options);

         b_inlr = *off;
         CallP P_BehDelt (d_drec : d_job_log : b_inlr);
6.33     CallP P_BehKonp(d_konrec: d_job_log : b_inlr);
6.31   endif;

       sisteDato = p_sistDato;
6.32   // hvis w_lest er 0, så var det ingen elementer i XML, dvs tom liste, men det er ok
6.32   if (sisteDato = '' and w_lest = 0);
6.32     sisteDato = %char(%date(): *iso);
6.32   endif;

       // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
       // Avslutt program
       // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
       // Vi oppsummerer

       if (p_stat = '1');
         p_jsts = 50;
         p_jmld = 'Alvorlig feil oppstod i JV867R_XML';

         CallP P_AB705R (p_jbid : p_jsts : p_jmld);

       else;

         p_jsts = 10;
         p_jmld = *blanks;
         p_jmld = %trim(a_meld(1)) + ' ' + %Char(w_alev);
         CallP P_AB705R (p_jbid : p_jsts : p_jmld);

       endif;

       CallP P_AB710R (p_jnav : p_jbid );

       *inlr = *on;

       /end-free

      *
      * Denne proseduren mottar 5 og 5 Grupper fra XML filen
      *
      *
     P LesDeltag       B
     D LesDeltag       pi            10i 0
     D   commArea                    10a
     D   Deltager                          likeds(Deltager_t)
     D                                     dim(5) const
     D   count                       10u 0 value

     D t               s             10u 0
     D d               s              5i 0
     D e               s              5i 0
     D
      /free

         for t = 1 to count;

           BehDeltg(Deltager(t));
6.32       w_lest = 1;


         endfor;
         return 0;

      /end-free
     P LesDeltag       E



      *
      * Behandler 1 overgruppe. Overføre overgruppe data til data struktur
      * og kaller programmet P_BehDelt
     P BehDeltg        B
     D BehDeltg        pi            10i 0
     D   Deltg                             likeds(Deltager_t) const
     D
6.33 D t               s              5i 0
     D Numbers         s             10    Inz('0123456789')
     D Non_Num_Posn    s              2  0 Inz(*Zero)
     c
     c
      /free

       DeltInit();
       d_dtnr_num   = %Int(Deltg.DeltagerNr) ;
       select;
       when %Trim(Deltg.DeltagerType)  = 'Vareeier';
       d_dtyp_num   = 0 ;
       when %Trim(Deltg.DeltagerType)  = 'BrukerMedPris';
       d_dtyp_num   = 3 ;
       when %Trim(Deltg.DeltagerType)  = 'BrukerUtenPris';
       d_dtyp_num   = 4 ;
       when %Trim(Deltg.DeltagerType)  = 'Utmeldt';
       d_dtyp_num   = 5 ;
       other;
       d_dtyp_num   = 0 ;
       endsl;
       d_navn       = Deltg.MarkedsNavn ;
       d_adr1       = Deltg.Adresse(1).Adresse(1).text ;
       d_adr2       = Deltg.Adresse(1).Adresse(2).text ;
       d_ponr       = Deltg.Adresse(1).PostNr ;
       d_pstd       = Deltg.Adresse(1).PostSted ;
       d_land       = Deltg.Adresse(1).Land ;
       d_mail       = Deltg.FirmaEpost ;
       d_urla       = Deltg.URL ;
       d_tlfn       = Deltg.Adresse(1).Telefon ;
       d_faxn       = Deltg.Adresse(1).Telefax ;
       d_faxo       = Deltg.Adresse(1).TelefaxOrdre;
8.01   if (%Trim(Deltg.ForetagsNr)) <> ''
8.01   and (%len(%Trim(Deltg.ForetagsNr))=12);
8.01   if %subst(%trim(Deltg.ForetagsNr):1:3) = 'NO-';
8.01     Non_Num_Posn = %check(Numbers:%subst(Deltg.ForetagsNr:4:9));
8.01     If Non_Num_Posn = 0;
8.01       d_orgn_num  = %Int(%subst(Deltg.ForetagsNr:4:9)) ;
8.01     endif;
8.01     endif;
8.01   else;
       if (%Trim(Deltg.ForetagsNr)) <> '' and (%len(%Trim(Deltg.ForetagsNr))=9);
         Non_Num_Posn = %check(Numbers:%Trim(Deltg.ForetagsNr));
         If Non_Num_Posn = 0;
           d_orgn_num  = %Int(%Trim(Deltg.ForetagsNr)) ;
         endif;
       endif;
8.01   ENDIF;
       //d_valu       = Deltg. ;
       //d_kper       = Deltg. ;
       if (%Trim(Deltg.GrontPunkt) = 'true');
         d_gron_num  = 1 ;
       else;
         d_gron_num  = 0 ;
       endif;

       d_mmrk       = Deltg.Miljomerke(1).MiljoMerke ;
       if (%Trim(Deltg.BankGiro) <> '');
         d_bgir_num  = %Int(Deltg.BankGiro) ;
       endif;
       if (%Trim(Deltg.PostGiro) <> '');
        d_pgir_num  = %Int(Deltg.PostGiro) ;
       endif;
       //d_faxo       = Deltg. ;
       d_crea       = Deltg.status.created ;
       d_upda       = Deltg.status.updated ;

       CallP P_BehDelt (d_drec : d_job_log : b_inlr);

       p_sistDato = d_upda;
       p_sistDelNr = Deltg.DeltagerNr;

6.33   for t = 1 to Deltg.countKontaktPersoner;
6.33     BehKontap(Deltg.Kontaktpersoner(t) : t);
6.33   endfor;

       return 0;

      /end-free

     P BehDeltg        E


6.33  *
6.33  * Behandler Kontaktpersoner
6.33 P BehKontap       B
6.33 D BehKontap       pi            10i 0
6.33 D   KontaktPersoner...
6.33 D                                     likeds(KontaktPersoner_t) const
6.33 D t                              5i 0
6.33  /free
6.33
6.33   d_jkpldo_nym = d_dtnr_num;
6.33   d_jkptel_num = t;
6.33   d_jkpnav = KontaktPersoner.Navn;
6.33   d_jkpttl = KontaktPersoner.Tittel;
6.33   d_jkptlf = KontaktPersoner.Telefon;
6.33   d_jkpmob = KontaktPersoner.Mobil;
6.33   d_jkpfax = KontaktPersoner.Telefax;
6.33   d_jkpmai = KontaktPersoner.Epost;
6.33   d_jkprol = KontaktPersoner.Rolle;
6.33
6.33   CallP P_BehKonp(d_konrec: d_job_log : b_inlr);
6.33
6.33   return 0;
6.33
6.33  /end-free
6.33
6.33 P BehKontap       E


      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av deltager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     P DeltInit        B
     D
     D t_teller        s              5i 0
      /free

       d_drec = *blank;
       d_dtnr_num = *zero;
       d_dtyp_num = *zero;
       d_orgn_num = *zero;
       d_bgir_num = *zero;
       d_pgir_num = *zero;
       d_crea_iso = *loval;
       d_upda_iso = *loval;
       d_dele_iso = *loval;

      /end-free
     P DeltInit        E

**
Antall nye leverandører er:
