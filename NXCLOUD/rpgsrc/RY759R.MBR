     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : RY759R
      * Beskrivelse . . : Leser LALUND-fil, og oppdaterer påslag på
      *                   varenummernivå
      *
      *                   Forutsetter at påslag er lagt midlertidig i vare-
      *                   pris-filen.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       101000 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjrvapf    if   e           k disk
      *
     fjvprl2    if   e           k disk    rename(jvprpfr:jvprl2r)
     fjvdtl1    if   e           k disk    rename(jvdtpfr:jvdtl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjpfalu    uf a e           k disk    rename(jpfapfr:jpfalur)
      *
      * Definering av LDA
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d jvprl2_ldor     s                   like(jxldor)
     d jvprl2_lvar     s                   like(jxlvar)
     d jvdtl1_dvar     s                   like(jvdvar)
     d jvarl1_vare     s                   like(jvvare)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_lvar          s             15
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser LALUND-fil
      *
     c                   read      jrvapf                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      w_lvar = %subst(jrvrad:1:15)
      *
     c                   exsr      oppdater
      *
     c                   read      jrvapf                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av varerecord
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
     c                   eval      jvprl2_ldor = 900000
     c                   eval      jvprl2_lvar = w_lvar
     c     jvprl2_key    chain     jvprl2                             91
     c                   if        *in91 = *on
     c                   leavesr
     c                   endif
      *
      * Oppretter påslag på frigitte varer
      *
     c                   eval      jvdtl1_dvar = jxvare
     c     jvdtl1_key    chain     jvdtl1                             91
     c                   if        *in91 = *on or
     c                             jvdhko = 1
     c                   leavesr
     c                   endif
      *
     c                   eval      jvarl1_vare = jxvare
     c     jvarl1_key    chain     jvarl1                             91
     c                   if        *in91 = *on
     c                   leavesr
     c                   endif
      *
     c                   clear                   jpfalur
      *
     c                   eval      jcfirm = w_firm
     c                   eval      jcogrp = jvogrp
     c                   eval      jchgrp = jvhgrp
     c                   eval      jcugrp = jvugrp
     c                   eval      jcldor = jvldor
     c                   eval      jcmodn = jvmodn
     c                   eval      jcvare = jvvare
     c                   eval      jckofa = 1
     c                   eval(h)   jcsafa = jxpris
      *
     c                   time                    jcodat
     c                   time                    jcedat
     c                   time                    jcetim
      *
     c                   write     jpfalur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på vare-pris
      *
     c     jvprl2_key    klist
     c                   kfld                    jvprl2_ldor
     c                   kfld                    jvprl2_lvar
      *
      * Key for oppslag pa vare-detalj
      *
     c     jvdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jvdtl1_dvar
      *
      * Key for oppslag pa vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
