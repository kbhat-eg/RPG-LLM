     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : EK141R
      * Beskrivelse . . : Kunde bonus, beregner og oppdaterer bonus - alle
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       221013 bsa  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fekbti1    if   e           k disk    rename(ekbtst:ekbti1r)
     febkrl1    if   e           k disk    rename(ebkrpfr:ebkrl1r)
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d ekbti1_tbku     s                   like(ektbku)
     d ekbti1_tbot     s                   like(ektbot)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_paar          s              4
     d w_pfir          s              3
     d w_tbot          s              2
      *
     d w_udat          s               d   datfmt(*iso)
     d w_udat_8        s              8
      *
     d w_firm_alf      s              3
     d w_bkun_alf      s              6
     d w_pfra          s              4
     d w_ptil          s              4
     d w_inlr          s              1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Legger inn parametre
      *
     c                   eval      w_firm_alf = w_pfir
     c                   eval      w_inlr = ' '
     c                   eval      ekbti1_tbot = w_tbot
      *
      * Kaller kalkulasjonsprogram for alle kunder det er knyttet
      * bonusavtale i mot
      *
     c     rkunl1_key    setll     rkunl1
     c     rkunl1_key    reade     rkunl1
     c                   dow       not %eof
      *
     c                   eval      ekbti1_tbku = rkkund
     c     ekbti1_key    chain     ekbti1
     c                   if        %found
     c                   movel     ektbku        w_bkun_alf
     c                   call      'EK142R'
     c                   parm                    w_firm_alf
     c                   parm                    w_bkun_alf
     c                   parm                    w_pfra
     c                   parm                    w_ptil
     c                   parm                    w_tbot
     c                   parm                    w_inlr
     c                   endif
      *
     c     rkunl1_key    reade     rkunl1
     c                   enddo
      *
      * Et siste kall for å stenge åpne filer i kalkulasjonsprogram
      *
     c                   eval      w_inlr = '1'
     c                   eval      w_bkun_alf = *blank
     c                   call      'EK142R'
     c                   parm                    w_firm_alf
     c                   parm                    w_bkun_alf
     c                   parm                    w_pfra
     c                   parm                    w_ptil
     c                   parm                    w_tbot
     c                   parm                    w_inlr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les på kunde
      *
     c     ebkrl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på kundebonus
      *
     c     ekbti1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ekbti1_tbku
     c                   kfld                    ekbti1_tbot
      *
     c                   eval      w_firm = l_firm
      *
      * Hent inn parametere
      *
     c     ebkrl1_key    chain     ebkrl1
     c                   eval      w_tbot = ebkbot
     c                   move      ebkpfr        w_pfra
     c                   move      ebkpti        w_ptil
      *
     c                   endsr
