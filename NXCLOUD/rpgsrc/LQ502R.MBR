      /TITLE  ASLAGR Lagerbeholdning
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LQ502R
      * Beskrivelse . . : Vise lagerbeholdning alle lager
      *                   uavhengig av filgruppe.
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       100401 ASK  Nytt program
      * 800   210422 ask  Programmet er helt omskrevet for å bli uavhengig av
      *                   kundetilpasninger.
      *                   Switchstyres med "LAGER_FILGRUPPER"
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = avslutt
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flq502d    cf   e             workstn sfile(d1sfl:w_srrn)
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable i parametre
      *
     d p_vare          s             15
     d p_tell          s              2  0
     d p_fi01          s              3
     d p_fi02          s              3
     d p_fi03          s              3
     d p_fi04          s              3
     d p_fi05          s              3
     d p_fi06          s              3
     d p_fi07          s              3
     d p_fi08          s              3
     d p_fi09          s              3
     d p_fi10          s              3
      *
      * Definering av variable
      *
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
     d w_firm          s              3  0
     d w_lage          s              2  0
     d w_behl          s             11  3
     d w_pakk          s             11  3
     d w_oord          s             11  3
     d w_bbes          s             11  3
     d w_figr          s              3
     d w_ltek          s             20
     d sqllager        s           4096
     d sqltekst        s           4096
      *
     d b_forn          s              1    inz(*off)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * w_virn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * D1SFL  - D1 Subfile - record
      * D2CTL  - D2 Subfile - control record
      * D2CMD  - D2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * D2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c/Exec SQL
     c+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR,
     c+             commit=*none
     c/End-Exec
      * Send Alt
      *
     c     d2taga        tag
     c                   write     d2cmd
      *
      * Bare data
      *
     c     d2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_pgm
     c                   when      *inke
     c                   exsr      forny
     c                   goto      d2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      d2tagb
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      d2taga
      *
      * Avslutt program
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     d1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      w_virn = w_srrn
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     d2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
      *
     c                   exsr      les_01
     c                   exsr      les_02
     c                   exsr      les_03
     c                   exsr      les_04
     c                   exsr      les_05
     c                   exsr      les_06
     c                   exsr      les_07
     c                   exsr      les_08
     c                   exsr      les_09
     c                   exsr      les_10
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     d2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     d2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å lese lager på filgruppe 01
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_01        begsr
      *
     c                   if        p_tell < 1
     c                   goto      end_01
     c                   endif
      *
     c                   eval      w_figr = p_fi01
     c                   exsr      les_lager
      *
     c     end_01        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å lese lager på filgruppe 02
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_02        begsr
      *
     c                   if        p_tell < 2
     c                   goto      end_02
     c                   endif
      *
     c                   eval      w_figr = p_fi02
     c                   exsr      les_lager
      *
     c     end_02        endsr

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å lese lager på filgruppe 03
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_03        begsr
      *
     c                   if        p_tell < 3
     c                   goto      end_03
     c                   endif
      *
     c                   eval      w_figr = p_fi03
     c                   exsr      les_lager
      *
     c     end_03        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å lese lager på filgruppe 04
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_04        begsr
      *
     c                   if        p_tell < 4
     c                   goto      end_04
     c                   endif
      *
     c                   eval      w_figr = p_fi04
     c                   exsr      les_lager
      *
     c     end_04        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å lese lager på filgruppe 05
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_05        begsr
      *
     c                   if        p_tell < 5
     c                   goto      end_05
     c                   endif
      *
     c                   eval      w_figr = p_fi05
     c                   exsr      les_lager
      *
     c     end_05        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å lese lager på filgruppe 06
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_06        begsr
      *
     c                   if        p_tell < 6
     c                   goto      end_06
     c                   endif
      *
     c                   eval      w_figr = p_fi06
     c                   exsr      les_lager
      *
     c     end_06        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å lese lager på filgruppe 07
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_07        begsr
      *
     c                   if        p_tell < 7
     c                   goto      end_07
     c                   endif
      *
     c                   eval      w_figr = p_fi07
     c                   exsr      les_lager
      *
     c     end_07        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å lese lager på filgruppe 08
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_08        begsr
      *
     c                   if        p_tell < 8
     c                   goto      end_08
     c                   endif
      *
     c                   eval      w_figr = p_fi08
     c                   exsr      les_lager
      *
     c     end_08        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å lese lager på filgruppe 09
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_09        begsr
      *
     c                   if        p_tell < 9
     c                   goto      end_09
     c                   endif
      *
     c                   eval      w_figr = p_fi09
     c                   exsr      les_lager
      *
     c     end_09        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å lese lager på filgruppe 10
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_10        begsr
      *
     c                   if        p_tell < 10
     c                   goto      end_10
     c                   endif
      *
     c                   eval      w_figr = p_fi10
     c                   exsr      les_lager
      *
     c     end_10        endsr
      //
      // Subrutine for å lese lager på filgruppen
      //

           begsr les_lager;

            sqllager = 'select vllage, vlbehl, vlpakk, vloord, vlbbes +
                        from adb' + w_figr + '/vlagpf ' +
                        'where vlfirm = ? and vlvare = ?';

            exec sql
             prepare sql_stmt1 from :sqllager;

            exec sql
             declare stmt1 cursor for sql_stmt1;

            exec sql
             open stmt1 using :w_firm, :p_vare;

           dou       w_srrn = w_spge;
            exec sql
             fetch next from stmt1
               into :w_lage, :w_behl, :w_pakk, :w_oord, :w_bbes;
             if sqlcod = 100 or sqlstt = '02000';
              leave;
             endif;

            exsr hent_besk;

      // Fyller opp subfile

           w_srrn = w_srrn + 1;
           d1filg = w_figr;
           d1lage = w_lage;
           d1ltek = w_ltek;
           d1behl = w_behl;
           d1pakk = w_pakk;
           d1oord = w_oord;
           d1bbes = w_bbes;
           d1disp = w_behl + w_bbes - w_pakk - w_oord;
           if d1disp <= 0;
             *in41 = *on;
            else;
             *in41 = *off;
           endif;
           write d1sfl;
           enddo;

            exec sql
             close stmt1;

           endsr;

      //
      // Subrutine for å hente beskrivelse av lager
      //

           begsr hent_besk;

            sqltekst = 'select rajtxt from +
                        adb' + w_figr + '/ra10pf ' +
                        'where rajfir = ? and rajkod = ?';

            exec sql
             prepare sql_stmt2 from :sqltekst;

            exec sql
             declare stmt2 cursor for sql_stmt2;

            exec sql
             open stmt2 using :w_firm, :w_lage;

            exec sql
             fetch stmt2
               into :w_ltek;

            exec sql
             close stmt2;

           endsr;

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_vare
     c                   parm                    p_tell
     c                   parm                    p_fi01
     c                   parm                    p_fi02
     c                   parm                    p_fi03
     c                   parm                    p_fi04
     c                   parm                    p_fi05
     c                   parm                    p_fi06
     c                   parm                    p_fi07
     c                   parm                    p_fi08
     c                   parm                    p_fi09
     c                   parm                    p_fi10
      *
      *  Legger inn startverdier
      *
     c                   eval      w_firm = l_firm
      *
      *  Finn varetekst og legg ut i bilde
      *
            d2vare = p_vare;
            exec sql
              select vvtek1, vvenhl
                into :d2tek1, :d2lenh
                from vvarpf
                where vvfirm = :w_firm and vvvare = :p_vare;
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
