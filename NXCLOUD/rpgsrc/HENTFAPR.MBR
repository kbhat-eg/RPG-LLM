     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Følgende kolonner kan/skal forekomme i regnearket
      *    Kontonummer
      *    Kontonavn
      *    År               Ikke utfyllt
      *    Periode          Ikke utfyllt
      *    Bevegelser
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  *  2017_08_02 sst Lagt inn avdeling i regnearket og her
6.31  *  2017_08_01 sst Legger alltid på lager 99 dersom = 0
6.32  *  2017_08_16 sst Høyrejust varenr innenfor 8 siffer.
6.33  *  2018_02_26 sst Skifte ordretype ved kreditnota
7.00  *  2020_05_11 sst HEnte opp avtaler m.m. dersom ikke i VVAR
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffapost    uf   f  512        disk    usropn
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
6.30 fra07l1    if   e           k disk    rename(ra07pfr:ra07l1r)
     fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvpgrl1    if   e           k disk    rename(vpgrpfr:vpgrl1r)
     fvmval1    if   e           k disk    rename(vmvapfr:vmval1r)
     ffohelu    uf a e           k disk    rename(fohepfr:fohelur)
     ffodtlu    uf a e           k disk    rename(fodtpfr:fodtlur)
      *
      * Definering av Local data area
      *
     d                uds
     d l_feil                  1      1
     d l_ws                  901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d fodtlu_firm     s                   like(fdfirm)
     d fodtlu_numm     s                   like(fdnumm)
     d fodtlu_suff     s                   like(fdsuff)
     d fodtlu_line     s                   like(fdline)
     d fohelu_firm     s                   like(fofirm)
     d fohelu_numm     s                   like(fonumm)
     d fohelu_suff     s                   like(fosuff)
     d rkunl1_firm     s                   like(rkfirm)
     d rkunl1_kund     s                   like(rkkund)
6.30 d ra07l1_firm     s                   like(ragfir)
6.30 d ra07l1_kode     s                   like(ragkod)
     d ra09l1_firm     s                   like(raifir)
     d ra09l1_kode     s                   like(raikod)
     d vvarl1_firm     s                   like(vvfirm)
     d vvarl1_vare     s                   like(vvvare)
     d vpgrl1_firm     s                   like(vpgfir)
     d vpgrl1_lage     s                   like(vpglag)
     d vmval1_firm     s                   like(vamfir)
     d vmval1_mkod     s                   like(vamkod)
      *
     d c_digi          s             15    inz('0123456789')
     d c_dig2          s             15    inz('0123456789 ,-')
      *
      * Definering av variable
      *
     d w_otyp          s              2      inz('VF')
6.33 d w_otypk         s              2      inz('VK')
     d w_faty          s              2  0   inz(21)
     d w_mvak          s              1      inz('3')
     d w_mvaf          s              1      inz('4')
      *
     d i               s              3  0
     d j               s              3  0
     d w_amdpdat       s              6  0
     d w_amdfdat       s              6  0
     d w_amdidat       s              6  0
     d w_belo          s             11  2
     d w_rbel          s             11  2
     d w_salg          s             11  2
      *
      * Definering av variable for timestamp
      *
     d w_tmst          s               z
     d w_tmst_20       s             20
     d w_line          s              6  0
     d w_line_alf      s              6
     d w_datx          s              6
     d w_dat           s              6  0
     d w_bdat          s              6  0
     d w_fdat          s              6  0
     d w_iso           s               d   datfmt(*iso)
     d w_isu           s               d   datfmt(*iso)
      *
     d w_kode          s              1
     d w_firm          s                     like(fofirm)
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
7.00 d w_numm          s                   like(fonumm)
7.00 d w_suff          s                   like(fosuff)
7.00 d w_anta          s                   like(fdanta)
     d w_stat          s             10
     d w_feil          s             15
     d w_feilMf        s              1    inz(' ')
     d w_tran          s            256
      *
     d w_6             s              6
     d d_firm          s              3
     d d_lnav          s             30
     d d_kund          s              6
     d d_aar           s              4
     d d_per           s              2
     d d_lage          s              4
     d d_selg          s              4
     d d_vare          s             15
     d d_pdat          s              8
     d d_fdat          s              8
     d d_idat          s              8
     d d_bild          s             10
     d d_forf          s             10
     d d_biln          s              8
     d d_kond          s              6
6.30 d d_avde          s              4
     d d_sped          s              4
     d d_mvad          s              1
     d d_konc          s              6
     d d_avdc          s              4
     d d_spec          s              4
     d d_mvac          s              1
     d d_jour          s              6
     d d_betb          s              2
     d d_belo          s             15
     d d_rbel          s             15
     d d_antp          s              5
     d d_ikod          s              1
     d d_valk          s              3
     d d_fakn          s             15
     d d_user          s             10
     d d_text          s             30
      *
     d s_kund          s              6
     d s_aar           s              4
     d s_per           s              2
7.00  *
7.00 d p_ltyp          s                   like(fdltyp)
7.00 d p_lety          s                   like(fdlety)
7.00 d p_kode          s              1
      *
     Ifapost    ns  01
     I                                  1  256  fatran
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   open      fapost
     c                   eval      w_stat = 'Kontroll'
     c                   eval      l_feil = *blank
      *
     c     start         tag
     c                   read      fapost
     c                   if        %eof(fapost)
     c                   if        w_stat = 'Oppdat'
     c                   exsr      getord
     c                   close     fapost
     c                   goto      end
     c                   else
     c                   if        w_feilMf = *blank
     c                   close     fapost
     c                   open      fapost
     c                   eval      w_stat = 'Oppdat'
     c                   eval      w_line = 09
     c                   eval      s_kund = *blank
     c                   eval      w_salg = *zero
     c                   goto      start
     c                   else
     c                   eval      l_feil = 'J'
     c                   goto      end
     c                   endif
     c                   endif
     c                   endif
      *
     c                   eval      w_tran = fatran
      *
      * Periode kommer på egen linje først
     c                   if        %subst(fatran:1:8) = 'Periode:'
     c                   eval      d_aar = %subst(fatran:10:4)
     c                   eval      d_per = %subst(fatran:15:2)
     c                   if        %check(c_digi : d_aar) > 0
     c                             or %check(c_digi : d_per) > 0
     c                   eval      w_feil = 'Feil Periode'
     c                   goto      feil
     c                   endif
     c                   goto      start
     c                   endif
      *
     c                   if           (%subst(fatran:1:1) < '0'
     c                             or  %subst(fatran:1:1) > '9')
     c                   goto      start
     c                   endif
     c                   eval      i = %scan('            ':fatran)
     c                   eval      %subst(fatran:i:1) = ';'
      *
     c                   eval      d_kund = *blank
     c                   eval      d_vare = *blank
     c                   eval      d_belo = *blank
     c                   eval      d_selg = *blank
     c                   eval      d_lage = *blank
     c                   eval      d_text = *blank
      * Æ,Ø,Å
     c                   eval      fatran= %xlate(x'77':'Ø':fatran)
     c                   eval      fatran= %xlate(x'2A':'Å':fatran)
     c                   eval      fatran= %xlate(x'46':'Æ':fatran)
     c                   eval      fatran= %xlate(x'EF':'å':fatran)
     c                   eval      fatran= %xlate(x'90':'ø':fatran)
     c                   eval      fatran= %xlate(x'A0':'æ':fatran)
     c                   eval      fatran= %xlate(x'14':'Ø':fatran)
      *
      *  Kunde
     c                   eval      i = %scan(';':fatran)
     c                   eval      d_kund = %trim(%subst(fatran:1:i-1))
     c                   eval      fatran = %subst(fatran:i+1:256-i)
     c                   dow       %subst(d_kund:6:1) = ' ' and
     c                             d_kund <> *blank
     c                   eval      %subst(d_kund:2:5) = %subst(d_kund:1:5)
     c                   eval      %subst(d_kund:1:1) = '0'
     c                   enddo
     c                   if        %check(c_digi : d_kund) > 0
     c                   eval      w_feil = 'Feil Kundenr'
     c                   goto      feil
     c                   endif
     c                   move      d_kund        rkunl1_kund
     c     rkunl1_key    chain     rkunl1
     c                   if        not %found(rkunl1)
     c                   eval      w_feil = 'Ukjent Kunde'
     c                   goto      feil
     c                   endif
      *
      *  Vare
     c                   eval      i = %scan(';':fatran)
     c                   eval      d_vare = %trim(%subst(fatran:1:i-1))
     c                   eval      fatran = %subst(fatran:i+1:256-i)
6.32  *  Høyrejust av varenr innenfor 8 siffer etter møte m/mgr
6.32 c                   dow       d_vare <> *blank
6.32 c                             and %subst(d_vare:8:1) = *blank
6.32 c                   eval      %subst(d_vare:2:7) =
6.32 c                             %subst(d_vare:1:7)
6.32 c                   eval      %subst(d_vare:1:1) = '0'
6.32 c                   enddo
      *
      *  Beløp
     c                   eval      i = %scan(';':fatran)
     c                   eval      d_belo = %trim(%subst(fatran:1:i-1))
     c                   eval      fatran = %subst(fatran:i+1:256-i)
     c                   if        %check(c_dig2:d_belo) > 0
     c                   eval      w_feil = 'Feil Beløp  '
     c                   goto      feil
     c                   endif
     c     1             do        14            j
     c                   if        %subst(d_belo:j:1) = ' '
     c                   eval      %subst(d_belo:j:15-j) =
     c                             %subst(d_belo:j+1:15-j)
     c                   endif
     c                   enddo
     c                   dow       %subst(d_belo:15:1) = ' ' and
     c                             d_belo <> *blank
     c                   eval      %subst(d_belo:2:14) = %subst(d_belo:1:14)
     c                   eval      %subst(d_belo:1:1) = '0'
     c                   enddo
      *   Har feltet desimalkomma
     c                   eval      j = %scan(',':d_belo)
     c                   if        j > 0
     c                   if        j = 14
     c                   eval      %subst(d_belo:1:15) =
     c                             %subst(d_belo:1:j-1)
     c                             + %subst(d_belo:15:1)
     c                             + '0'
     c                   endif
     c                   if        j = 13
     c                   eval      %subst(d_belo:1:15) =
     c                             %subst(d_belo:1:j-1)
     c                             + %subst(d_belo:14:2)
     c                   endif
     c                   if        j = 12
     c                   eval      %subst(d_belo:1:15) =
     c                             %subst(d_belo:1:j-1)
     c                             + %subst(d_belo:13:2)
     c                   endif
     c                   eval      %subst(d_belo:1:1) = *blank
     c                   else
     c                   eval      %subst(d_belo:1:13) =
     c                             %subst(d_belo:3:13)
     c                   eval      %subst(d_belo:14:2) = '00'
     c                   endif
     c                   eval      j = 15
     c                   dow       %subst(d_belo:1:j) <> *blank
     c                   if        %subst(d_belo:j:1) = ' '
     c                   eval      %subst(d_belo:2:j-1) =
     c                             %subst(d_belo:1:j-1)
     c                   endif
     c                   eval      j = j - 1
     c                   enddo
     c                   eval      j = %scan('-':d_belo)
     c                   if        j > 1
     c                   eval      %subst(d_belo:j:1) = *blank
     c                   move      d_belo        w_belo
     c                   eval      w_belo = w_belo * -1
     c                   else
     c                   move      d_belo        w_belo
     c                   endif
      *
      *  Bilagsdato
     c                   eval      i = %scan(';':fatran)
     c                   eval      d_bild = %trim(%subst(fatran:1:i-1))
     c                   eval      fatran = %subst(fatran:i+1:256-i)
     c                   dow       %subst(d_bild:10:1) = ' ' and
     c                             d_bild <> *blank
     c                   eval      %subst(d_bild:2:9) = %subst(d_bild:1:9)
     c                   eval      %subst(d_bild:1:1) = '0'
     c                   enddo
      *
     c                   if            %subst(d_bild:3:1) = '.'
     c                             and %subst(d_bild:6:1) = '.'
     c                             and %subst(d_bild:9:2) <> ' '
     c                   eval      w_datx = %subst(d_bild:1:2)
     c                                    + %subst(d_bild:4:2)
     c                                    + %subst(d_bild:9:2)
     c                   move      w_datx        w_bdat
     c                   else
     c                   move      d_bild        w_bdat
     c                   endif
     c     *dmy          test (d)                w_bdat                 33
     c                   if        *in33 = *on
     c                   eval      w_feil = 'Feil bilagsdato'
     c                   goto      feil
     c                   endif
     c     *dmy          move      udate         w_isu
     c     *dmy          move      w_bdat        w_iso
     c                   if           %diff(w_iso:w_isu: *days) > 365
     c                             or %diff(w_iso:w_isu: *days) < -365
     c                   eval      w_feil = 'Feil bilagsdato'
     c                   goto      feil
     c                   endif
      *
      *  Forfallsdato
     c                   eval      i = %scan(';':fatran)
     c                   eval      d_forf = %trim(%subst(fatran:1:i-1))
     c                   eval      fatran = %subst(fatran:i+1:256-i)
     c                   dow       %subst(d_forf:10:1) = ' ' and
     c                             d_forf <> *blank
     c                   eval      %subst(d_forf:2:9) = %subst(d_forf:1:9)
     c                   eval      %subst(d_forf:1:1) = '0'
     c                   enddo
      *
     c                   if            %subst(d_forf:3:1) = '.'
     c                             and %subst(d_forf:6:1) = '.'
     c                             and %subst(d_forf:9:2) <> ' '
     c                   eval      w_datx = %subst(d_forf:1:2)
     c                                    + %subst(d_forf:4:2)
     c                                    + %subst(d_forf:9:2)
     c                   move      w_datx        w_fdat
     c                   else
     c                   move      d_forf        w_fdat
     c                   endif
     c     *dmy          test (d)                w_fdat                 33
     c                   if        *in33 = *on
     c                   eval      w_feil = 'Feil forf.dato '
     c                   goto      feil
     c                   endif
     c     *dmy          move      w_fdat        w_iso
     c                   if           %diff(w_iso:w_isu: *days) > 365
     c                             or %diff(w_iso:w_isu: *days) < -365
     c                   eval      w_feil = 'Feil forf.dato '
     c                   goto      feil
     c                   endif
      *
      *  Selger
     c                   eval      i = %scan(';':fatran)
     c                   eval      d_selg = %trim(%subst(fatran:1:i-1))
     c                   eval      fatran = %subst(fatran:i+1:256-i)
     c                   dow       %subst(d_selg:4:1) = ' ' and
     c                             d_selg <> *blank
     c                   eval      %subst(d_selg:2:3) = %subst(d_selg:1:3)
     c                   eval      %subst(d_selg:1:1) = '0'
     c                   enddo
      *
     c                   if        %check(c_digi : d_selg) > 0
     c                   eval      w_feil = 'Feil Selger'
     c                   goto      feil
     c                   endif
     c                   eval      ra09l1_firm = l_firm
     c                   move      d_selg        ra09l1_kode
     c     ra09l1_key    chain     ra09l1
     c                   if        not %found(ra09l1)
     c                   eval      w_feil = 'Ukjent Selger'
     c                   goto      feil
     c                   endif
      *
      *  Lager
     c                   eval      i = %scan(';':fatran)
     c                   eval      d_lage = %trim(%subst(fatran:1:i-1))
     c                   eval      fatran = %subst(fatran:i+1:256-i)
     c                   dow       %subst(d_lage:4:1) = ' ' and
     c                             d_lage <> *blank
     c                   eval      %subst(d_lage:2:3) = %subst(d_lage:1:3)
     c                   eval      %subst(d_lage:1:1) = '0'
     c                   enddo
6.30  *
6.30  *  Avdeling
6.30 c                   eval      i = %scan(';':fatran)
6.30 c                   eval      d_avde = %trim(%subst(fatran:1:i-1))
6.30 c                   eval      fatran = %subst(fatran:i+1:256-i)
6.30 c                   dow       %subst(d_avde:4:1) = ' ' and
6.30 c                             d_avde <> *blank
6.30 c                   eval      %subst(d_avde:2:3) = %subst(d_avde:1:3)
6.30 c                   eval      %subst(d_avde:1:1) = '0'
6.30 c                   enddo
6.30  *
6.30 c                   if        d_avde <> *blank
6.30  *
6.30 c                   if        %check(c_digi : d_avde) > 0
6.30 c                   eval      w_feil = 'Feil Avdeling'
6.30 c                   goto      feil
6.30 c                   endif
6.30 c                   eval      ra07l1_firm = l_firm
6.30 c                   move      d_avde        ra07l1_kode
6.30 c     ra07l1_key    chain     ra07l1
6.30 c                   if        not %found(ra07l1)
6.30 c                   eval      w_feil = 'Ukjent Avdeling'
6.30 c                   goto      feil
6.30 c                   endif
6.30  *
6.30 c                   endif
      *
      *  Tekst
     c                   eval      i = %scan(';':fatran)
     c                   eval      d_text = %trim(%subst(fatran:1:i-1))
     c                   eval      fatran = %subst(fatran:i+1:256-i)
      *
     c     Ikke_Fler     tag
      *
     c                   eval      fofirm = l_firm
      *  Ev. bygge ny ordre
     c                   if            d_kund = s_kund
     c                   else
     c                   if        w_stat = 'Oppdat'
     c                   exsr      getord
     c                   eval      w_line = 09
     c                   endif
     c                   endif
      *   Bygge detaljlinje
     c                   clear                   fodtlur
7.00 c                   eval      w_line = w_line + 10
     c                   eval      fdnumm = w_numm
     c                   eval      fdsuff = 00
     c                   eval      fdline = w_line
     c                   eval      fdotyp = w_otyp
     c                   eval      fdvare = d_vare
     c                   eval      fdenh1 = 'STK'
     c                   move      d_lage        fdlage
6.31 c                   if        fdlage = 0
6.31 c                   eval      fdlage = 99
6.31 c                   endif
     c                   move      d_kund        fdkund
     c                   movel     d_text        fdtek1
     c                   eval      fdanta = 1
     c                   eval      fdsapr = w_belo
     c                   eval      fdsums = w_belo
     c                   eval      w_salg = w_salg + w_belo
      *   Hent vare
     c                   eval      vvarl1_firm = l_firm
     c                   eval      vvarl1_vare = d_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found(vvarl1)
     c                   eval      vmval1_firm = l_firm
     c                   eval      vmval1_mkod = vvkmva
     c     vmval1_key    chain     vmval1
     c                   if        %found(vmval1)
     c                   eval      fdomva = vamkof
     c                   else
     c                   eval      fdomva = w_mvak
     c                   endif
     c                   eval      fdogrp = vvogrp
     c                   eval      fdhgrp = vvhgrp
     c                   eval      fdugrp = vvugrp
6.30 c                   if        fdtek1 = *blank
6.30 c                   eval      fdtek1 = vvtek1
6.30 c                   endif
     c                   else
7.00  *  Forsøk å finne avtale fra skaffevare logikk
7.00  *
7.00 c*                  move      '->'          b1tek1
7.00  *
7.00 c**                 eval      w_anta = b1anta
7.00  *
7.00 c                   call      'FD120R'
7.00 c                   parm                    p_kode
7.00 c                   parm                    w_firm
7.00 c                   parm                    w_numm
7.00 c                   parm                    w_suff
7.00 c                   parm                    w_line
7.00 c                   parm                    p_ltyp
7.00 c                   parm                    p_lety
7.00 c                   parm                    d_vare
7.00 c                   parm                    w_anta
7.00  *   Hva gjøres nå?
7.00  *   Er linje skrevet?
7.00  *
     c                   eval      w_feil = 'Ukjent vare'
     c                   goto      feil
     c                   eval      fdogrp = 99
     c                   eval      fdhgrp = 99
     c                   eval      fdugrp = 999
     c                   endif
      *   Ev. overstyre momskode dersom kunde er avgiftsfri
     c                   if        rkmkod = 1
     c                   eval      fdomva = w_mvaf
     c                   endif
      *
     c                   eval      vpgrl1_firm = l_firm
     c                   move      d_lage        vpgrl1_lage
     c     vpgrl1_key    chain     vpgrl1
     c                   if        %found(vpgrl1)
     c                   eval      fdprgr = vpgprg
     c                   else
     c                   eval      fdprgr = *blank
     c                   endif
     c                   time                    fdedat
     c                   time                    fdetim
     c                   movel     l_user        fdeusr
     c                   time                    fdodat
     c                   time                    fdotim
     c                   movel     l_user        fdoous
      *
     c                   if        w_stat = 'Oppdat'
     c                   write     fodtlur
     c                   endif
      *
     c                   goto      start
      *
     c     feil          tag
     c                   eval      fatran = %trim(w_tran) + ';' +
     c                                      %trim(w_feil) + ';'
     c                   eval      w_feilmf = 'J'
     c                   except    fauppd
     c                   goto      start
      *
     c     end           tag
     c                   eval      *inlr = *on
     c                   return
      *
      *  Ev. oppdaterer forrige bunt og henter nytt nummer
      *
     c     getord        begsr
      *  Ev. oppdaterer forrige ordre
     c                   if        s_kund <> *blank
     c                   eval      fohelu_firm = l_firm
     c                   eval      fohelu_numm = w_numm
     c                   eval      fohelu_suff = 00
     c     fohelu_key    chain     fohelu
     c                   if        %found(fohelu)
     c                   eval      fotots = w_salg
6.33 c                   if        w_salg < 0
6.33 c                   eval      footyp = w_otypk
6.33 c                   eval      fotots = fotots * -1
6.33 c                   exsr      faktkred
6.33 c                   endif
     c                   update    fohelur
     c                   eval      w_salg = *zero
     c                   endif
     c                   endif
      *   Hent nytt buntnummer
     c                   if        not %eof(fapost)
      *
      * Kaller opp subprogram for henting av nummer fra NUMMER-REGISTER
      *
     c                   eval      w_kode = '0'
     c                   eval      w_firm = l_firm
     c                   eval      w_fell = 'ORDRE'
     c                   eval      w_type = w_otyp
     c                   eval      w_fast = l_ws
     c                   eval      w_numm = *zero
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
     c                   eval      fofirm = l_firm
     c                   eval      fonumm = w_numm
     c                   eval      fosuff = 00
     c                   move      d_kund        fokund
     c                   move      d_kund        s_kund
     c                   eval      footyp = w_otyp
     c                   move      d_lage        folage
6.31 c                   if        folage = 0
6.31 c                   eval      folage = 99
6.31 c                   endif
     c     *dmy          move      w_bdat        fobdat
     c     *dmy          move      w_bdat        foldat
     c     *dmy          move      w_fdat        foffda
     c                   eval      rkunl1_firm = l_firm
     c                   move      d_kund        rkunl1_kund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found(rkunl1)
     c                   eval      fobetb = rkbetb
     c                   else
     c                   eval      fobetb = *zero
     c                   endif
     c                   move      d_selg        foselg
     c                   eval      fodist = rkdist
     c                   eval      folmat = 1
     c                   eval      forkat = rkrabk
     c                   move      d_kund        fokund
     c                   eval      fokuna = rknavn
     c                   eval      fonavn = rknavn
     c                   eval      foadr1 = rkgate
     c                   eval      foadr2 = rkadr2
     c                   eval      fosted = rksted
     c                   eval      foavde = rkavde
6.30 c                   if        d_avde <> *blank
6.30 c                   move      d_avde        foavde
6.30 c                   endif
     c                   eval      ra09l1_firm = l_firm
     c                   move      d_selg        ra09l1_kode
     c     ra09l1_key    chain     ra09l1
     c                   if        %found(ra09l1)
     c                   movel     raitxt        fovref
     c                   endif
     c                   eval      fokgfa = rkfagb
     c                   eval      fodref = rkpers
     c*                  eval      fofaty = w_faty
     c                   eval      fofaty = rkfaty
     c                   eval      fowsid = l_ws
     c                   time                    fodate
     c                   time                    fotime
     c                   time                    foedat
     c                   time                    foetim
     c                   eval      fooous = l_user
     c                   eval      foeusr = l_user
     c                   write     fohelur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for endring av faktura til kreditnota
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     faktkred      begsr
     c                   eval      fodtlu_firm = fofirm
     c                   eval      fodtlu_numm = fonumm
     c                   eval      fodtlu_suff = fosuff
     c                   eval      fodtlu_line = *zero
     c     fodtlu_key    setll     fodtlu
     c                   read      fodtlu
     c                   dow       fdnumm = fonumm
     c                             and fdsuff = fosuff
     c                   eval      fdsums = fdsums * -1
     c                   eval      fdsapr = fdsapr * -1
     c                   eval      fdotyp = w_otypk
     c                   update    fodtlur
     c                   read      fodtlu
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på bunts
      *
     c     fohelu_key    klist
     c                   kfld                    fohelu_firm
     c                   kfld                    fohelu_numm
     c                   kfld                    fohelu_suff
      *
      * Key for posisjonering på trans
      *
     c     fodtlu_key    klist
     c                   kfld                    fodtlu_firm
     c                   kfld                    fodtlu_numm
     c                   kfld                    fodtlu_suff
     c                   kfld                    fodtlu_line
      *
      * Key for posisjonering på KUNDE
      *
     c     rkunl1_key    klist
     c                   kfld                    rkunl1_firm
     c                   kfld                    rkunl1_kund
6.30  *
6.30  * Key for posisjonering på Avdeling
6.30  *
6.30 c     ra07l1_key    klist
6.30 c                   kfld                    ra07l1_firm
6.30 c                   kfld                    ra07l1_kode
      *
      * Key for posisjonering på Selger
      *
     c     ra09l1_key    klist
     c                   kfld                    ra09l1_firm
     c                   kfld                    ra09l1_kode
      *
      * Key for posisjonering på Vare
      *
     c     vvarl1_key    klist
     c                   kfld                    vvarl1_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for posisjonering på Prisgruppe-knytning
      *
     c     vpgrl1_key    klist
     c                   kfld                    vpgrl1_firm
     c                   kfld                    vpgrl1_lage
      *
      * Key for posisjonering på Momskodeknytning
      *
     c     vmval1_key    klist
     c                   kfld                    vmval1_firm
     c                   kfld                    vmval1_mkod
      *
      *
     c                   endsr
      *
     oFaPost    e            FaUppd
     o                       fatran
