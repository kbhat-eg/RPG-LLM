     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LI652R
      * Beskrivelse . . : EDI, Kvalitetskontroll - kontroll og utskrift
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *                   Nytt program
      *       040805 HKO  Endret test mot innkjøpenhet
5.70  * PRGR  151208 bof  Utvidet med prisgruppe /varetype
6.10  *       201009 bof  Sjekker mot bestilbar enhet pr. leverandør
6.11  *       010210 bof  standard kall for å hente gtin-nr
6.13  *       230112 bsa  Endret kontroll mot GTIN nr. Må finnes på
6.13  *                   minst en innkjøpsenhet.
6.20  *       140612 bsa  Utvidet parameterliste til VL710R
6.21  *       300614 bsa  Sjekker ikke på utgåtte varer i EVR
6.30  *       290314 bof  Ny kode for forsendelsesmåte bestilling
6.31  *       120416 nho  Må test på nye koder
6.32  * 6.30  170918 bkv  Utvidet med trelast sortiment
8.01  *PRG2   150622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     flldil1    if   e           k disk    rename(lldipfr:lldil1r)
     fvvarl5    if   e           k disk    rename(vvarpfr:vvarl5r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
6.12 fvvepl1    if   e           k disk    rename(vveppfr:vvepl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
6.32 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
     fli652p    o    e             printer
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
5.70 d l_lage               1001   1002  0
      *
      * Parameter-inn
      *
     d                 ds
     d d_parm                        15
     d  d_firm                        3  0 overlay(d_parm)
     d  d_ldor                        6  0 overlay(d_parm:4)
     d  d_val1                        1  0 overlay(d_parm:10)
     d  d_val2                        1  0 overlay(d_parm:11)
     d  d_val3                        1  0 overlay(d_parm:12)
      *
      * Definering av variabler i nøkler
      *
     d rlevl1_levr     s                   like(rllevr)
     d lldil1_ldor     s                   like(lpldor)
     d vvarl5_ldor     s                   like(vvldor)
     d vvenl1_vare     s                   like(vevare)
6.12 d vvepl1_pvar     s                   like(vepvar)
6.12 d vvepl1_penh     s                   like(vepenh)
6.12 d vvepl1_pldo     s                   like(vepldo)
     d jvarl1_vare     s                   like(jvvare)
      *
      * Definering av variable
      *
     d w_firm          s                   like(rlfirm)
     d w_parm          s             15
     d w_kode          s              2  0
     d w_ktx1          s             20
     d w_ktx2          s             20
     d w_ktx3          s             20
     d w_stat          s              1
     d w_stex          s              3
5.70 d w_lage          s              2  0
5.70 d w_vtyp          s                   like(vvvtyp)
6.13 d w_ldor          s                   like(vvldor)
6.11 d w_eann          s             14  0
6.11 d w_east          s              1
6.20 d w_udat          s               d   datfmt(*iso)
6.20 d w_lenr          s                   like(vvldor)
6.20 d b_inlr          s              1    inz(*off)
6.32 d w_lv_nobb       s                   like(vvlnob)
6.32 d w_lv_modn       s                   like(vvlmod)
6.32 d w_lv_lldo       s                   like(vvlnle)
6.32 d w_lv_llva       s                   like(vvllva)
      *
     d b_feil          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram: Styrerutine for valgte sjekkpunkter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Les valgt leverandør
      *
     c     rlevl1_key    chain     rlevl1
     c                   if        not %found
     c                   goto      end_pgm
     c                   endif
      *
      *  Sjekke leverandørinformasjon?
      *
     c                   if        d_val1 = 1 or
     c                             d_val2 = 1
     c                   exsr      skriv_head
      *
     c                   if        rleanl <> 0
     c                   eval      d1eanl = rleanl
     c                   eval      *in40 = *off
     c                   else
     c                   eval      d1eanl = 0
     c                   eval      *in40 = *on
     c                   endif
      *
     c                   write     d1lev                                30
     c                   endif
      *
      *  Sjekke betingelser?
      *
     c                   if        d_val1 = 1
     c                   exsr      sjekk_bet
     c                   endif
      *
      *  Sjekke distribusjon?
      *
     c                   if        d_val2 = 1
     c                   exsr      sjekk_distr
     c                   endif
      *
      *  Sjekke vareinformasjon?
      *
     c                   if        d_val3 = 1
     c                   exsr      sjekk_varer
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for sjekk av betingelser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_bet     begsr
      *
      * Forsendelsesmåte
      *
     c                   eval      *in41 = *off
     c                   eval      w_kode = rlform
      *
6.30 c                   call      'RS731R'
     c                   parm                    w_stat
     c                   parm                    w_kode
     c                   parm                    w_ktx1
      *
     c                   if        w_stat <> 'N'
     c                   eval      d1beko = w_kode
     c                   else
     c                   eval      d1beko = 0
     c                   endif
     c                   eval      d1betx = w_ktx1
      *
6.31 c**                 if        w_kode <> 1 and
6.31 c**                           w_kode <> 2 and
6.31 c**                           w_kode <> 3
6.31 c                   if        w_kode <> 4 and
6.31 c                             w_kode <> 6
     c                   eval      *in41 = *on
     c                   endif
      *
      * Leveringsbetingelse
      *
     c                   eval      *in42 = *off
     c                   eval      w_kode = rllbet
      *
     c                   call      'RS718R'
     c                   parm                    w_stat
     c                   parm                    w_kode
     c                   parm                    w_ktx1
     c                   parm                    w_ktx2
     c                   parm                    w_ktx3
      *
     c                   if        w_stat <> 'N'
     c                   eval      d1leko = w_kode
     c                   else
     c                   eval      d1leko = 0
     c                   endif
     c                   eval      d1letx = w_ktx1
      *
     c                   eval      w_stex = %subst(w_ktx1:1:3)
     c                   if        w_stex <> 'EXW' and
     c                             w_stex <> 'FCA' and
     c                             w_stex <> 'FAS' and
     c                             w_stex <> 'FOB' and
     c                             w_stex <> 'CFR' and
     c                             w_stex <> 'CIF' and
     c                             w_stex <> 'CPT' and
     c                             w_stex <> 'CIP' and
     c                             w_stex <> 'DES' and
     c                             w_stex <> 'DEQ' and
     c                             w_stex <> 'DDU' and
     c                             w_stex <> 'DDP' and
     c                             w_stex <> 'DAF'
     c                   eval      *in42 = *on
     c                   endif
      *
     c                   write     d1bet                                30
      *
     c     end_bet       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for sjekk av distribusjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_distr   begsr
      *
     c                   eval      *in43 = *off
     c                   write     a1for                                30
     c     lldil1_key    setll     lldil1
     c     lldil1_key    reade     lldil1
      *
     c                   dou       %eof(lldil1)
     c                   eval      d1lage = lplage
     c                   eval      d1form = lpform
     c                   eval      d1type = lptype
     c                   eval      d1lknr = %subst(lplknr:1:13)
      *
     c                   write     d1for                                30
      *
     c                   if        lpform <> 'EDI       '
     c                   eval      *in43 = *on
     c                   endif
      *
     c                   if        lptype <> 'LU0       ' and
     c                             lptype <> 'FTP       '
     c                   eval      *in43 = *on
     c                   endif
     c     lldil1_key    reade     lldil1
     c                   enddo
      *
     c                   write     t1for                                30
      *
     c     end_distr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for sjekk av vareinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_varer   begsr
      *
     c                   exsr      skriv_head
     c                   write     a1var                                30
      *
     c     vvarl5_key    setll     vvarl5
     c     vvarl5_key    reade     vvarl5
      *
     c                   dow       %eof = *off
6.21  * Sjekk om utgårdato er satt på varen, da skal den ikke behandles
6.21 c                   if        vvudat <> *loval
6.21 c                   goto      ny_vare
6.21 c                   endif
6.21  *
     c                   eval      d1vare = vvvare
     c                   eval      d1tek1 = vvtek1
     c                   eval      *in45 = *off
     c                   eval      *in46 = *off
     c                   eval      *in47 = *off
     c                   eval      *in48 = *off
     c                   eval      *in49 = *off
     c                   eval      *in50 = *off
6.13 c                   eval      *in51 = *off
      *
      * Sjekk EAN-nummer - flyttet til sjekk av innkjøpsenheter
      *
6.13 c**                 exsr      sjekk_ean
6.13 c**                 if        b_feil = *on
6.13 c**                 eval      *in46 = *on
6.13 c**                 endif
      *
      * Sjekk om diversevare
      *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    w_lage
5.70 c                   parm                    w_vtyp
6.20 c                   parm                    w_udat
6.20 c                   parm                    w_lenr
6.20 c                   parm                    b_inlr
6.32 c                   parm                    w_lv_nobb
6.32 c                   parm                    w_lv_modn
6.32 c                   parm                    w_lv_lldo
6.32 c                   parm                    w_lv_llva
      *
      * Sjekk Nobb-nummer
      *
6.32 c                   if        w_lv_nobb > 0
6.32 c                   else
     c                   if        vvnobb = 0
     c                   eval      *in45 = *on
     c                   endif
6.32 c                   endif
      *
5.70 c                   if        w_vtyp = 'D'
     c                   eval      *in47 = *on
     c                   endif
      *
      * Sjekk matching mot LVR
      *
     c                   exsr      sjekk_match
     c                   if        b_feil = *on
     c                   eval      *in48 = *on
     c                   endif
      *
      * Sjekk innkjøpsenheter
      *
     c                   if        *in48 = *off
     c                   exsr      sjekk_enh
     c                   if        b_feil = *on
     c                   eval      *in49 = *on
     c                   endif
     c                   endif
      *
6.13  * Hvis innnkjøpsenheter er funnet, sjekk at det finnes GTIN
6.13  *
6.13 c                   if        *in48 = *off and
6.13 c                             *in49 = *off
6.13 c                   exsr      sjekk_ean
6.13 c                   endif
      *
      * Sjekk om overstyrt varetekst
      *
     c                   if        *in48 = *off
     c                   exsr      sjekk_tekst
     c                   if        b_feil = *on
     c                   eval      *in50 = *on
     c                   endif
     c                   endif
      *
     c                   if        *in45 = *on or
     c                             *in46 = *on or
     c                             *in47 = *on or
     c                             *in48 = *on or
     c                             *in49 = *on or
6.13 c                             *in50 = *on or
6.13 c                             *in51 = *on
     c                   write     d1var                                30
     c                   if        *in30 = *on
     c                   exsr      skriv_head
     c                   write     a1var
     c                   endif
     c                   endif
      *
     c                   if        *in50 = *on
     c                   write     d1ftek                               30
     c                   if        *in30 = *on
     c                   exsr      skriv_head
     c                   write     a1var
     c                   endif
     c                   endif
      *
     c                   if        *in49 = *on
     c                   write     d1fenh                               30
     c                   if        *in30 = *on
     c                   exsr      skriv_head
     c                   write     a1var
     c                   endif
     c                   endif
6.21  *
6.21 c     ny_vare       tag
      *
     c     vvarl5_key    reade     vvarl5
     c                   enddo
      *
     c     end_varer     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke EAN-nummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_ean     begsr
6.13  *
6.13  * Vi leser alle innkjøpsenheter, og sjekker at det finnes GTIN på minst 1
6.13  *
6.13 c                   eval      b_feil = *on
6.13 c                   eval      *IN46 = *on
6.13 c                   eval      d1nenh = *blank
6.13 c                   eval      vvenl1_vare = vvvare
6.13 c     vvenl1_key    setll     vvenl1
6.13 c     vvenl1_key    reade     vvenl1
6.13 c                   dow       %eof = *off
6.13 c                   if        veinen > 0
6.13 c                   eval      w_eann = 0
6.13 c                   eval      w_east = *blank
6.13 c                   eval      w_ldor = d_ldor
6.13  *
6.11 c**6.13             call      'VE712R'
6.13 c                   call      'VE719R'
6.13 c                   parm                    w_firm
6.13 c                   parm                    w_ldor
6.13 c                   parm                    vvvare
6.13 c                   parm                    veenhe
6.13 c                   parm                    w_eann
6.13 c                   parm                    w_east
6.13 c                   if        w_east = '1'
6.13 c                   eval      b_feil = *off
6.13 c                   eval      *IN46  = *off
6.13 c                   eval      *IN51  = *off
6.13 c                   endif
6.13 c                   if        w_east = '2' and
6.13 c                             b_feil = *on
6.13 c                   eval      *IN46  = *off
6.13 c                   eval      *IN51  = *on
6.13 c                   endif
6.13 c                   endif
6.13 c     vvenl1_key    reade     vvenl1
6.13 c                   enddo
6.13  *
6.13 c     end_ean       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke match mot LVR
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_match   begsr
      *
     c                   eval      b_feil = *off
     c                   eval      jvarl1_vare = vvvare
     c     jvarl1_key    chain     jvarl1
     c                   if        not %found
     c                   eval      b_feil = *on
     c                   endif
      *
     c     end_match     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke innkjøpsenheter mot LVR
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_enh     begsr
      *
6.13 c**                 eval      b_feil = *off
6.13 c                   eval      b_feil = *on
     c                   eval      d1nenh = *blank
     c                   eval      vvenl1_vare = vvvare
     c     vvenl1_key    setll     vvenl1
     c     vvenl1_key    reade     vvenl1
     c                   dow       %eof = *off
     c                   if        veinen > 0
6.13 c*    vvepl1_key    chain     vvepl1
6.13 c*                  if        not %found
6.13 c*                  eval      d1nenh = veenhe
6.13 c*                  eval      b_feil = *on
6.13 c                   eval      b_feil = *off
     c                   goto      end_enh
6.13 c*                  endif
     c                   endif
     c     vvenl1_key    reade     vvenl1
     c                   enddo
      *
      *
     c     end_enh       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke overstyrt varetekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_tekst   begsr
      *
     c                   eval      b_feil = *off
     c                   eval      d1ntek = *blank
     c                   if        vvtek1 <> jvtek1
     c                   eval      d1ntek = jvtek1
     c                   eval      b_feil = *on
     c                   endif
      *
     c     end_tekst     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for utskrift av heading på liste.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_head    begsr
      *
     c                   eval      a1fnav = l_fnav
     c                   eval      a1levr = rllevr
     c                   eval      a1navn = rlnavn
      *
     c                   write     a1hdr                                30
      *
     c     end_head      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    w_parm
     c                   eval      d_parm = w_parm
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for les av leverandør distribusjon
      *
     c     lldil1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lldil1_ldor
      *
      * Key for oppslag på vareregister
      *
     c     vvarl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl5_ldor
      *
      * Key for les av vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
      *
      * Key for oppslag på vare enhet pr. leverandør
      *
     c     vvepl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvepl1_pvar
     c                   kfld                    vvepl1_penh
     c                   kfld                    vvepl1_pldo
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      *  Legger inn startverdier
      *
     c                   eval      w_firm = d_firm
     c                   eval      rlevl1_levr = d_ldor
     c                   eval      lldil1_ldor = d_ldor
     c                   eval      vvarl5_ldor = d_ldor
5.70 c                   eval      w_lage = l_lage
      *
     c                   endsr
