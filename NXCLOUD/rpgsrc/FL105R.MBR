     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FL105R
      * Beskrivelse . . : Kundeprosjekt, vedlikehold av detaljer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Fign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       121005 HKO  Nytt program
      *                   Kopi av C2BLD i FL100R
      *       131005 HKO  Lagt inn test på duplikat kundeprosjektnavn
5.60  *       080108 bof  Lagt inn kjørerute og nr i vedl. bildet
5.61  *       270509 AMD  Korrigering av feil i parameterliste ved
5.61  *                   spørring på prosjektnummer (Regnskapsprosj.)
6.10  * 6.10  230609 BSA  Oppdatert med sporingsinformasjon
6.20  * 6.20  270111 sst  Oppslag mot nytt prosjekt
6.21  * 6.20  150112 bof  Utvidet med merke for Byggdok i ekstra tabell
6.22  * 6.20  220113 bof  Hvis byggdok velges, så krav om ekstern k.prosj.
6.23  * 6.20  220113 bof  Hvis 'BYGGDOK' på rolle kontaktperson, kryss av byggdok
6.24  * 6.20  130313 amd  Lagt inn litt mer kontroll på prosjektdato
6.24  *                   fra og til
6.25  * 6.20  180913 bkv  Kun kontroll på fra prosjektdato hvis ny
      *                   Tillater 30 dager tilbake i tid på fradato
6.nu  *       160614 bof  Endring vedr. nummerutvidelse
6.30  *       160914 bkv  Ny Flere koder
6.31  *       201014 nho  Nytt felt cobuilder + nye feilmeldinger
6.31  *       201014 nho  Nytt felt cobuilder + nye feilmeldinger
6.32  *       221014 bkv  Mer om cobuilder
6.33  *       241014 bkv  Felt for å ikke kopiere endring fra kunde
6.34  *       241014 bkv  Kopierer endring til ordre
6.35  * 6.20  180316 bof  Validering på felter ihht. AVALPF
6.36  * 6.20  210316 bof  Absolutt krav om adr1 og postnr hvis cobuilder
6.37  * 6.30  260516 bof  Feil-retting små/store bokstaver ROLLE
6.38  * 6.30  290616 bof  Rettelse vedr. test på byggdok / cobuilder
7.01  *       170220 bof  Egen serie hvis kundenr. 110000
7.02  *       300517 sst  Nye  felt for konto svinn
7.03  *       290917 sst  Nye  felt for underprosjekt og fakturakunde
7.04  *       240918 bsa  Flyttet felt for fakturatype og prisdato.
7.05  *       150119 bsa  Logger endringer i fakturatype og prisdato.
7.06  *       120319 BKV  rettet feil i if
7.07  *       210819 sst  Prosjektregnskap Fase 2,  August 2019
7.08  *       230420 sst  Justert denne versjon i forhold til I06
7.09  * K000  210121 bsa  Rettet parameterliste til RP601
7.10  *       150517 sst  Nytt felt for konto utgiftsbilag
      *
8.01  *K000   090221 bsa  Slår opp mot rest API for å hente inn gyldige
8.01  *K000               profiler.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     ffkprl4    if   e           k disk    rename(fkprpfr:fkprl4r)
     ffkprlu    uf a e           k disk    rename(fkprpfr:fkprlur)
7.10 ffkp2l1    if   e           k disk    rename(fkp2pfr:fkp2l1r)
7.10 ffkp2lu    uf a e           k disk    rename(fkp2pfr:fkp2lur)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
     fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
     fra25lr    if   e           k disk    rename(ra25pfr:ra25lrr)
     fra06pf    if   e           k disk    rename(ra06pfr:ra06pfr)
     fraa1pf    if   e           k disk    rename(raa1pfr:raa1pfr)
     faposl1    if   e           k disk    rename(apospfr:aposl1r)
     ffmftl1    if   e           k disk    rename(fmftpfr:fmftl1r)
6.23 frukpl6    if   e           k disk    rename(rukppfr:rukpr1r)
6.32 fafpslr    if   e           k disk    rename(afpspfr:afpslrr)
6.32 famodl1    if   e           k disk    rename(amodpfr:amodl1r)
6.34 ffohelk    if   e           k disk    rename(fohepfr:fohelkr)
6.34 ffohelu    uf a e           k disk    rename(fohepfr:fohelur)
6.35 favall1    if   e           k disk    rename(avalpfr:avall1r)
7.05 fsloglu    uf a e           k disk    rename(slogpfr:sloglur)
8.01 ffkp3iu    uf a e           k disk    rename(fkp3st:fkp3iur)
8.01 ffkp3i1    if   e           k disk    rename(fkp3st:fkp3i1r)
8.01 fpromst    if   e           k disk    rename(promst:prompfr)
      *
     ffl105d    cf   e             workstn
      *
      * Definering av parametre
      *
6.30 d p_stat          s              1
     d p_firm          s                   like(flfirm)
     d p_kund          s                   like(flkund)
     d p_kpro          s                   like(flkpro)
     d p_navn          s                   like(flnavn)
     d p_adr1          s                   like(fladr1)
     d p_adr2          s                   like(fladr2)
     d p_kund_kopi     s                   like(flkund)
     d p_kpro_kopi     s                   like(flkpro)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_filg                931    933
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d fkprl4_kund     s                   like(flkund)
     d fkprl4_navn     s                   like(flnavn)
     d fkprlu_kund     s                   like(flkund)
     d fkprlu_kpro     s                   like(flkpro)
     d rkunl1_kund     s                   like(rkkund)
     d fusrl1_user     s                   like(fbuser)
     d ra09l1_ikod     s                   like(raikod)
     d ra06pf_fkod     s                   like(rafkod)
     d ra25lr_yrut     s                   like(rayrut)
     d aposl1_ponr     s                   like(apponr)
     d fmftl1_faty     s                   like(flfaty)
6.23 d rukpl6_kund     s                   like(rukund)
6.30 d rukpl6_kpnr     s                   like(rukpnr)
6.32 d afpslr_ufil     s                   like(l_filg)
6.32 d afpslr_uide     s                   like(afuide)
6.32 d amodl1_modu     s                   like(axmodu)
6.34 d fohelk_kund     s                   like(fokund)
6.34 d fohelu_numm     s                   like(fonumm)
6.34 d fohelu_suff     s                   like(fosuff)
6.35 d avall1_apgm     s                   like(avapgm)
7.05 d sloglu_regi     s                   like(elregi)
7.05 d sloglu_fore     s                   like(elfore)
7.05 d sloglu_fort     s                   like(elfort)
7.05 d sloglu_felt     s                   like(elfelt)
7.05 d sloglu_rdat     s                   like(elrdat)
7.05 d sloglu_rtim     s                   like(elrtim)
8.01 d fkp3iu_kund     s                   like(f3kund)
8.01 d fkp3iu_kpro     s                   like(f3kpro)
8.01 d fkp3i1_kund     s                   like(f3kund)
8.01 d fkp3i1_kpro     s                   like(f3kpro)
      *
      * Definering av variable
      *
6.30 d a_meld          s             50    dim(1) ctdata perrcd(1)
      *
     d b_stor          s              1    inz(*off)
      *
     d w_udat          s               d   datfmt(*iso)
     d w_fdat          s               d   datfmt(*iso)
     d w_tdat          s               d   datfmt(*iso)
7.08 d w_akti          s              6
     d w_kont          s              5  0
     d w_ptx1          s             30
     d w_ptx2          s             30
     d w_fast          s             10
     d w_fell          s              6
     d w_kode          s              1
     d w_retk          s              1
     d w_snav          s             20
     d w_spes          s              4  0
7.10 d w_avde          s              4  0
     d w_type          s              2
6.20 d w_uprs          s              1
6.20 d w_upro          s                   like(c2proj)
6.nu d w_numm          s              8  0
6.30 d w_mld1          s             50
6.30 d w_mld2          s             50
6.30 d w_mld3          s             50
6.30 d w_svar          s              1
6.30 d w_in12          s              1  0
6.30 d w_kund          s                   like(flkund)
7.03 d w_kunp          s                   like(flkund)
6.30 d w_knvn          s                   like(flkprs)
6.30 d w_kpnr          s                   like(rukpnr)
6.30 d w_kema          s                   like(flmail)
6.32 d w_bdok          s              1
6.32 d w_cobu          s              1
6.34 d w_faty          s                   like(flfaty)
6.34 d w_mkod          s                   like(flmkod)
6.34 d w_ekgb          s                   like(flekgb)
6.34 d w_betb          s                   like(flbetb)
6.34 d w_neto          s                   like(flneto)
6.34 d w_fagb          s                   like(flfagb)
6.34 d w_endr          s              1    inz(*off)
6.34 d w_kpro          s                   like(flkpro)
7.01 d w_pos           s              2s 0
7.01 d w_pkund         s                   like(flkund)
7.01 d w_wfell         s              6
7.03 d w_txt1          s             30
7.03 d w_txt2          s             30
7.07 d w_lagret        s              1
7.07 d w_fdat2         s                   like(c2fdat)
7.07 d w_tdat2         s                   like(c2tdat)
8.01 d w_memb          s             10
8.01 d w_pmtx          s             50
8.01 d w_noit          s              7  0
8.01 d w_stat          s              1
8.01 d w_mld01         s             30
8.01 d w_mld02         s             30
8.01 d w_ctli          s              2  0
8.01 d w_ctpo          s              3  0
      *
     d w_firm          s                   like(flfirm)
      *
7.05 d s_faty          s                   like(flfaty)
7.05 d s_opda          s                   like(flopda)
6.35  *
6.35  * Firmastyrt validering av felt
6.35  *
6.35 d u_adr1          s              1    inz(*off)
6.35 d u_ponr          s              1    inz(*off)
      *
      * Eksternt program
7.01   dcl-s co402_verdi1  char(1);
7.01   dcl-s co402_verdi2  char(2048);
7.01   DCL-PR CO402R EXTPGM('CO402R');
7.01     p_filgrp            char(3)     const;
7.01     p_firm              packed(3:0) const;
7.01     p_lager             packed(2:0) const;
7.01     p_nokkel            char(50)    const;
7.01     p_verdi1            char(1);
7.01     p_verdi2            char(2048);
7.01   END-PR;
7.01 d u_701           s              1    inz(*off)
      *
      * Konstanter
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøå'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅ'
      *
8.01 C/Exec SQL
8.01 C+  Set Option DatFmt=*ISO , commit=*none
8.01 C+  , DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
8.01 C/End-Exec
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.32 c                   eval      w_bdok = *off
6.32 c                   eval      w_cobu = *off
6.32  * Sjekker om Byggdok skal kjøres forutsetter at modul er på plass
6.32 c                   eval      amodl1_modu = 'BYGGDOK'
6.32 c     amodl1_key    chain     amodl1
6.32 c                   if        %found(amodl1)
6.32  * Sjekk om BYGGDOK aktivering er på/av
6.32 c                   eval      afpslr_ufil = l_filg
6.32 c                   eval      afpslr_uide = 'BYGGDOK   '
6.32 c     afpslr_key    chain     afpslrr
6.32 c                   if        %found(afpslr)
6.32 c                   eval      w_bdok = *on
6.32 c                   endif
6.38 c                   endif
      *
6.38  * Sjekker om Byggdok skal kjøres forutsetter at modul er på plass
6.38 c                   eval      amodl1_modu = 'COBUILDER '
6.38 c     amodl1_key    chain     amodl1
6.38 c                   if        %found(amodl1)
6.32  * Sjekk om COBUILDER aktivering er på/av
6.32 c                   eval      afpslr_ufil = l_filg
6.32 c                   eval      afpslr_uide = 'COBUILDER '
6.32 c     afpslr_key    chain     afpslrr
6.32 c                   if        %found(afpslr)
6.32 c                   eval      w_cobu = *on
6.32 c                   endif
6.38 c                   endif
      *
6.30 c                   eval      fusrl1_user = l_user
6.30 c     fusrl1_key    chain     fusrl1
      *
     c                   eval      fkprl1_kund = p_kund
     c                   eval      fkprl1_kpro = p_kpro
     c     fkprl1_key    chain     fkprl1
7.10 c     fkprl1_key    chain     fkp2l1
      *
7.10 c                   if        %found(fkprl1)
      *
6.34 c                   eval      w_faty = flfaty
6.34 c                   eval      w_mkod = flmkod
6.34 c                   eval      w_ekgb = flekgb
6.34 c                   eval      w_betb = flbetb
6.34 c                   eval      w_neto = flneto
6.34 c                   eval      w_fagb = flfagb
      *
     c                   eval      c2fdat = 0
     c                   eval      c2tdat = 0
     c                   eval      c2selg = flselg
     c                   eval      c2navn = flnavn
     c                   eval      c2adr1 = fladr1
     c                   eval      c2adr2 = fladr2
     c                   eval      c2ponr = flponr
     c                   eval      c2epro = flepro
6.31 c                   eval      c2cobu = flcobu
     c                   eval      c2kprs = flkprs
     c                   eval      c2tlfa = fltlfa
     c                   eval      c2tlfp = fltlfp
     c                   eval      c2tlfm = fltlfm
     c                   eval      c2mail = flmail
     c                   eval      c2proj = flproj
7.03 c                   eval      c2upro = flupro
7.03 c                   eval      c2kunf = flkunf
     c                   eval      c2boku = flboku
     c                   eval      c2priv = flpriv
     c                   eval      c2orka = florka
     c                   eval      c2okun = flokun
     c                   eval      c2okpr = flokpr
     c                   eval      c2opda = 0
     c                   eval      c2eusr = fleusr
     c                   eval      c2faty = flfaty
7.05 c                   eval      s_faty = flfaty
     c                   eval      c2kjor = flkjor
     c                   eval      c2kjnr = flkjnr
6.21 c                   eval      c2bdok = flbdok
6.30 c                   eval      f2mkod = flmkod
6.30 c                   eval      f2ekgb = flekgb
6.30 c                   eval      f2prpa = flprpa
6.30 c                   eval      f2neto = flneto
6.30 c                   eval      f2fagb = flfagb
6.30 c                   eval      f2samf = flsamf
6.30 c                   eval      f2sekv = flsekv
6.33 c                   eval      f2fkun = flfkun
7.10 c                   eval      f2ktou = flktou
7.10 c                   if        %found(fkp2l1)
7.10 c                   if        flktou > *zero
7.10 c                   eval      p_stat = *blank
7.10 c                   eval      w_spes = *zero
7.10 c                   eval      w_avde = *zero
7.10 c                   call      'RS740R'
7.10 c                   parm                    p_stat
7.10 c                   parm                    f2ktou
7.10 c                   parm                    w_spes
7.10 c                   parm                    w_avde
7.10 c                   parm                    f2ktxt
7.10 c                   else
7.10 c                   eval      f2ktou = *zero
7.10 c                   eval      f2ktxt = *blank
7.10 c                   endif
7.10 c                   else
7.10 c                   eval      f2ktou = *zero
7.10 c                   eval      f2ktxt = *blank
7.10 c                   endif
7.02  *
7.02 c                   eval      f2ktsd = flktsd
7.02 c                   if        %found(fkp2l1)
7.02 c                   if        flktsd > *zero
7.02 c                   eval      p_stat = *blank
7.02 c                   eval      w_spes = *zero
7.02 c                   eval      w_avde = *zero
7.02 c                   call      'RS740R'
7.02 c                   parm                    p_stat
7.02 c                   parm                    f2ktsd
7.02 c                   parm                    w_spes
7.02 c                   parm                    w_avde
7.02 c                   parm                    f2txtd
7.02 c                   else
7.02 c                   eval      f2ktsd = *zero
7.02 c                   eval      f2txtd = *blank
7.02 c                   endif
7.02 c                   else
7.02 c                   eval      f2ktsd = *zero
7.02 c                   eval      f2txtd = *blank
7.02 c                   endif
7.02  *
7.02 c                   eval      f2ktsc = flktsc
7.02 c                   if        %found(fkp2l1)
7.02 c                   if        flktsc > *zero
7.02 c                   eval      p_stat = *blank
7.02 c                   eval      w_spes = *zero
7.02 c                   eval      w_avde = *zero
7.02 c                   call      'RS740R'
7.02 c                   parm                    p_stat
7.02 c                   parm                    f2ktsc
7.02 c                   parm                    w_spes
7.02 c                   parm                    w_avde
7.02 c                   parm                    f2txtc
7.02 c                   else
7.02 c                   eval      f2ktsc = *zero
7.02 c                   eval      f2txtc = *blank
7.02 c                   endif
7.02 c                   else
7.02 c                   eval      f2ktsc = *zero
7.02 c                   eval      f2txtc = *blank
7.02 c                   endif
      *
6.30 c                   eval      f2ousr = flousr
6.30 c     *dmy          move      flodat        f2odat
6.30 c                   if        f2betb = 0
6.30 c                   eval      f2betb = flbetb
6.30 c                   if        f2betb <> *zero
6.30 c                   call      'RS703R'
6.30 c                   parm                    p_stat
6.30 c                   parm                    f2betb
6.30 c                   parm                    f2ctxt
      *
6.30 c                   if        p_stat = 'N'
6.30 c                   eval      f2ctxt = *blank
6.30 c                   endif
6.30 c                   endif
6.30 c                   endif
7.10 c                   eval      f2ousr = flousr
8.01  *
8.01  * Hent inn verdier fra tilleggstabell
8.01  *
8.01 c                   eval      fkp3i1_kund = p_kund
8.01 c                   eval      fkp3i1_kpro = p_kpro
8.01 c     fkp3i1_key    chain     fkp3i1
8.01 c                   if        %found
8.01 c                   eval      f2pmtx = f3pmtx
8.01 c                   eval      f2noit = f3noit
8.01 c                   endif
      *
6.30 c     *dmy          move      flodat        f2odat
      *
     c                   if        flfdat <> *loval
     c     *dmy          move      flfdat        c2fdat
7.08 c     *dmy          move      flfdat        w_fdat2
     c                   endif
      *
     c                   if        fltdat <> *loval
     c     *dmy          move      fltdat        c2tdat
7.08 c     *dmy          move      fltdat        w_tdat2
     c                   endif
      *
7.05 c                   eval      s_opda = flopda
     c                   if        flopda <> *loval
     c     *dmy          move      flopda        c2opda
     c                   endif
      *
     c     *dmy          move      flodat        c2odat
     c     *dmy          move      fledat        c2edat
      *
     c                   else
      *
     c                   eval      c2fdat = 0
     c                   eval      c2tdat = 0
     c                   eval      c2selg = 0
     c                   eval      c2navn = p_navn
     c                   eval      c2adr1 = p_adr1
     c                   eval      c2adr2 = p_adr2
     c                   eval      c2ponr = 0
     c                   eval      c2sted = *blank
     c                   eval      c2epro = *blank
6.31 c                   eval      c2cobu = 0
6.21 c                   eval      c2bdok = 0
     c                   eval      c2kprs = *blank
     c                   eval      c2tlfa = *blank
     c                   eval      c2tlfp = *blank
     c                   eval      c2tlfm = *blank
     c                   eval      c2mail = *blank
     c                   eval      c2proj = *blank
     c                   eval      c2boku = 0
     c                   eval      c2priv = 0
     c                   eval      c2orka = 0
     c                   eval      c2okun = 0
     c                   eval      c2okpr = 0
     c                   eval      c2opda = 0
     c                   eval      c2odat = 0
     c                   eval      c2edat = 0
     c                   eval      c2kjor = 0
     c                   eval      c2kjnr = 0
     c                   eval      c2eusr = *blank
     c                   eval      rkunl1_kund = p_kund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      c2faty = rkfaty
6.30 c                   eval      f2mkod = rkmkod
6.30 c                   eval      f2ekgb = rkekgb
6.30 c                   eval      f2prpa = rkprpa
6.30 c                   eval      f2betb = rkbetb
6.30 c                   eval      f2neto = rkneto
6.30 c                   eval      f2fagb = rkfagb
6.30 c                   eval      f2samf = rksamf
6.30 c                   eval      f2sekv = rksekv
8.01  *
8.01 c                   eval      f2pmtx = *blanks
8.01 c                   eval      f2noit = 0
8.01  *
     c                   endif
      *
     c                   eval      fusrl1_user = l_user
     c     fusrl1_key    chain     fusrl1
     c                   if        %found
     c                   eval      ra09l1_ikod = fbselg
     c     ra09l1_key    chain     ra09l1
     c                   if        %found
     c                   eval      c2selg = raikod
     c                   eval      c2snav = raitxt
     c                   endif
     c                   endif
      *
6.23  * sjekker rolle på kontaktperson
6.23 c                   eval      rukpl6_kund = p_kund
6.23 c     rukpl6_key    setll     rukpl6
6.23 c     rukpl6_key    reade     rukpl6
6.23 c                   dow       not  %eof(rukpl6)
6.37 c     Lo:Up         xlate     rukrol        rukrol
6.23 c                   if        rukrol = 'BYGGDOK' and w_bdok = *on
6.23 c                   eval      c2bdok = 1
6.23 c                   endif
6.32 c                   if        rukrol = 'COBUILDER' and w_cobu = *on
6.32 c                   eval      c2cobu = 1
6.32 c                   endif
6.23 c     rukpl6_key    reade     rukpl6
6.23 c                   enddo
      *
     c                   endif
      *
     c                   if        p_kund_kopi<> 0
     c                   eval      p_kund = p_kund_kopi
     c                   eval      p_kpro = p_kpro_kopi
     c                   eval      c2kund = p_kund
     c                   eval      c2kpro = p_kpro
     c                   endif
      *
     c                   eval      c2knav = *blank
     c                   eval      c2snav = *blank
     c                   eval      c2sted = *blank
     c                   eval      c2bokn = *blank
     c                   eval      c2ortx = *blank
     c                   eval      c2okna = *blank
     c                   eval      c2opna = *blank
     c                   eval      c2ptxt = *blank
      *
      * Henter selgernavn
      *
     c                   if        c2selg <> 0
     c                   eval      ra09l1_ikod = c2selg
     c     ra09l1_key    chain     ra09l1
     c                   if        %found
     c                   eval      c2snav = raitxt
     c                   endif
     c                   endif
      *
      * Henter kundenavn
      *
     c                   eval      rkunl1_kund = p_kund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      c2knav = rknavn
     c                   endif
      *
      * Henter poststed
      *
     c                   if        c2ponr <> 0
     c                   eval      aposl1_ponr = c2ponr
     c     aposl1_key    chain     aposl1
     c                   if        %found
     c                   eval      c2sted = apsted
     c                   endif
     c                   endif
      *
      * Henter navn på bonuskunde
      *
     c                   if        c2boku <> 0
     c                   eval      rkunl1_kund = c2boku
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      c2bokn = rknavn
     c                   endif
     c                   endif
      *
      * Henter beskrivelse av overstyringer
      *
     c                   if        c2orka <> 0
     c                   eval      ra06pf_fkod = c2orka
     c     ra06pf_key    chain     ra06pf
     c                   if        %found
     c                   eval      c2ortx = raftxt
     c                   endif
     c                   endif
      *
     c                   if        c2okun <> 0
     c                   eval      rkunl1_kund = c2okun
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      c2okna = rknavn
     c                   endif
     c                   endif
      *
     c                   if        c2okpr <> 0
     c                   eval      fkprl1_kund = c2okun
     c                   eval      fkprl1_kpro = c2okpr
     c     fkprl1_key    chain     fkprl1
     c                   if        %found
     c                   eval      c2opna = flnavn
     c                   endif
     c                   endif
      *
      * Henter prosjektinfo fra regnskap
      *
     c                   exsr      sjekk_prosj
      *
      * Henter status for bokstavstørrelse
      *
     c     raa1pf_key    chain     raa1pf
     c                   if        %found and
     c                             ra1bkk = 0
     c                   eval      b_stor = *on
     c                   else
     c                   eval      b_stor = *off
     c                   endif
      *
      * Henter kjørerute beskrivelse
      *
     c                   eval      ra25lr_yrut = c2kjor
     c     ra25lr_key    chain     ra25lr
     c                   if        %found
     c                   eval      c2kjna = raytxt
     c                   endif
      *
      * Behandler bildet
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl
7.07 c                   if        w_lagret  <> 'J'
7.07 c                   eval      w_mld2 = 'Du ber om avslutning uten å lagre'
7.07 c                   eval      w_mld3 = 'Ønsker du å lagre, +
7.07 c                                       endringer svar J og 2 x Enter'
7.07 c                   eval      w_svar = 'N'
7.07 c                   eval      w_in12 = 0
7.07  *
7.07 c                   call      'AA007R'
7.07 c                   parm                    w_mld2
7.07 c                   parm                    w_mld3
7.07 c                   parm                    w_svar
7.07 c                   parm                    w_in12
7.07 c                   if        w_svar = 'J'
7.07 c                   goto      c2tagb
7.07 c                   endif
7.07 c                   endif
     c                   goto      avslutt
7.07 c                   endif
6.30  *
6.30  * Call program for Flere koder
6.30  *
6.30 c                   if        *inkf = *on
6.30 c                   exsr      xf2win
6.30 c                   goto      c2tagb
6.30 c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
      *
     c                   if        w_afld = 'C2SELG'
     c                   eval      w_snav = *blank
     c                   call      'RA509R'
     c                   parm                    w_firm
     c                   parm                    c2selg
     c                   parm                    w_snav
     c                   eval      c2snav = w_snav
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        w_afld = 'C2PONR'
     c                   call      'AP500R'
     c                   parm                    c2ponr
     c                   parm                    c2sted
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        w_afld = 'C2PROJ'
6.20 c*                  call      'RP500R'
6.20 c*                  parm                    w_firm
6.20 c*                  parm                    c2proj
6.20 c*                  parm                    w_akti
6.20 c*                  parm                    w_kont
6.20 c*                  parm                    w_spes
6.20 c*                  parm                    c2ptxt
7.08 c                   eval      w_txt1 = c2navn
6.20 c                   call      'RP600R'
6.20 c                   parm                    w_firm
6.20 c                   parm                    c2proj
6.20 c                   parm                    c2navn
6.20 c                   parm                    w_uprs
7.07 c                   parm                    c2fdat
7.07 c                   parm                    c2tdat
7.07 c                   parm                    c2knav
7.07 c                   parm                    c2selg
7.07 c                   parm                    c2kunf
7.07 c                   parm                    c2kpro
7.03 c                   if        w_uprs = 'J'
7.08 c                   if        w_txt1 = *blank
7.08 c                   eval      w_txt1 = c2navn
7.08 c                   endif
7.08 c                   eval      w_txt2 = c2adr1
7.08 c                   eval      w_kunp = c2kunf
7.03 c                   call      'RP601R'
7.03 c                   parm                    w_firm
7.03 c                   parm                    c2proj
7.03 c                   parm                    c2upro
7.09 c                   parm                    w_uprs
7.09 **                  parm                    w_akti
7.03 c                   parm                    w_txt1
7.03 c                   parm                    w_txt2
7.03 c                   parm                    w_kunp
7.07 c                   parm                    c2fdat
7.07 c                   parm                    c2tdat
7.07 c                   parm                    c2knav
7.07 c                   parm                    c2selg
7.07 c                   parm                    c2kunf
7.07 c                   parm                    c2kpro
7.03 c                   if         w_txt1 <> *blank
7.03 c                   eval       c2ptxt = %trim(c2ptxt) + '/' +
7.03 c                                       %trim(w_txt1)
7.03 c                   endif
7.03 c                   endif
7.08 c                   eval      c2navn = w_txt1
7.06 c                   goto      c2tagb
7.06 c                   endif
      *
7.03 c                   if        w_afld = 'C2UPRO'
7.08 c                   if        w_txt1 = *blank
7.08 c                   eval      w_txt1 = c2navn
7.08 c                   endif
7.08 c                   eval      w_txt2 = c2adr1
7.08 c                   eval      w_kunp = c2kunf
7.03 c                   call      'RP601R'
7.03 c                   parm                    w_firm
7.03 c                   parm                    c2proj
7.03 c                   parm                    c2upro
7.08 c                   parm                    w_akti
7.03 c                   parm                    w_txt1
7.03 c                   parm                    w_txt2
7.03 c                   parm                    w_kunp
7.08 c**??               parm                    w_uprs
7.07 c                   parm                    c2fdat
7.07 c                   parm                    c2tdat
7.07 c                   parm                    c2knav
7.07 c                   parm                    c2selg
7.07 c                   parm                    c2kunf
7.07 c                   parm                    c2kpro
7.03 c                   if         w_txt1 <> *blank
7.03 c                   eval       c2ptxt = %trim(c2ptxt) + '/' +
7.03 c                                       %trim(w_txt1)
7.03 c                   endif
7.08 c                   eval      c2navn = w_txt1
     c                   goto      c2tagb
     c                   endif
7.03  *
7.03 c                   if        w_afld = 'C2KUNF'
7.03 c                   call      'RK500R'
7.03 c                   parm                    w_firm
7.03 c                   parm                    c2kunf
7.03 c                   parm                    c2fktx
7.03 c                   goto      c2tagb
7.03 c                   endif
      *
     c                   if        w_afld = 'C2BOKU'
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    c2boku
     c                   parm                    c2bokn
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        w_afld = 'C2ORKA'
     c                   call      'RA506R'
     c                   parm                    w_firm
     c                   parm                    c2orka
     c                   parm                    c2ortx
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        w_afld = 'C2OKUN'
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    c2okun
     c                   parm                    c2okna
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        w_afld = 'C2OKPR'
     c                   call      'FL500R'
     c                   parm                    w_firm
     c                   parm                    c2okun
     c                   parm                    c2okpr
     c                   parm                    c2okna
     c                   parm                    c2opna
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        w_afld = 'C2KJOR'
     c                   call      'RA525R'
     c                   parm                    w_firm
     c                   parm                    c2kjor
     c                   parm                    c2kjna
     c                   goto      c2tagb
     c                   endif
      *
6.30 c                   if        w_afld = 'C2KPRS'
6.30 c                   eval      w_kund = c2kund
6.30 c                   eval      w_kpnr = 0
6.30 c                   call      'RK503R'
6.30 c                   parm                    w_firm
6.30 c                   parm                    w_kund
6.30 c                   parm                    w_knvn
6.30 c                   parm                    w_kpnr
6.30 c                   parm                    w_kema
6.30 c                   if        w_kpnr > 0
6.30 c                   eval      rukpl6_kund = w_kund
6.30 c                   eval      rukpl6_kpnr = w_kpnr
6.30 c     rukpl6_key2   chain     rukpl6
6.30 c                   if        %found
6.30 c                   eval      c2kprs = w_knvn
6.30 c                   eval      c2mail = w_kema
6.30 c                   eval      c2tlfa = ruktlf
6.30 c                   eval      c2tlfp = ruptlf
6.30 c                   eval      c2tlfm = rumtlf
6.30 c                   endif
6.30 c                   endif
6.30 c                   goto      c2tagb
6.30 c                   endif
      *
     c                   endif
      *
      * Validering
      *
     c                   if        c2fdat <> 0
     c     *dmy          test(d)                 c2fdat                 31
     c                   if        *in31 = *on
     c                   goto      c2tagb
     c                   endif
     c                   endif
      *
     c                   if        c2tdat <> 0
     c     *dmy          test(d)                 c2tdat                 32
     c                   if        *in32 = *on
     c                   goto      c2tagb
     c                   endif
     c                   endif
      *
     c                   if        c2tdat <> 0 and
     c                             c2fdat = 0
     c     *dmy          move      w_udat        c2fdat
     c                   endif
      *
     c                   if        c2fdat <> 0
     c     *dmy          move      c2fdat        w_fdat
     c                   if        c2tdat <> 0
     c     *dmy          move      c2tdat        w_tdat
     c                   else
     c                   eval      w_tdat = *loval
     c                   endif
     c                   if        w_fdat > w_tdat
     c                   eval      *in33 = *on
     c                   goto      c2tagb
     c                   endif
     c                   endif
6.24  *
6.24  * Sjekk om fradato er lavere
6.24  * enn dagens dato
6.24  *
6.24 c                   if        c2fdat <> 0
6.25 c                   if        w_fdat <  (w_udat - %days(30))
6.25 c                             and FLFDAT = *loval
6.24 c                   eval      *in49 = *on
6.24 c                   goto      c2tagb
6.24 c                   endif
6.24 c                   endif
      *
     c                   if        c2fdat = 0
     c                   eval      *in34 = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        c2selg = 0
     c                   eval      *in35 = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   eval      ra09l1_ikod = c2selg
     c     ra09l1_key    chain     ra09l1
     c                   if        not %found
     c                   eval      *in36 = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        c2navn = *blank
     c                   eval      *in37 = *on
     c                   goto      c2tagb
     c                   endif
6.35  *
6.35  * Sjekk,  Er det krav til adresse1
6.35  *
6.35 c                   if        u_adr1 = *on  and
6.35 c                             c2adr1 = *blank
6.35 c                   eval      *in60  = *on
6.35 c                   goto      c2tagb
6.35 c                   endif
6.35  *
6.35  * Sjekk,  Er det krav til postnr
6.35  *
6.35 c                   if        u_ponr = *on  and
6.35 c                             c2ponr = 0
6.35 c                   eval      *in61  = *on
6.35 c                   goto      c2tagb
6.35 c                   endif
      *
     c                   eval      fkprl4_kund = c2kund
     c                   eval      fkprl4_navn = c2navn
     c     fkprl4_key    setll     fkprl4
     c     fkprl4_key    reade     fkprl4
     c                   dow       not %eof
     c                   if        flkpro <> c2kpro
     c                   eval      *in50 = *on
     c                   goto      c2tagb
     c                   endif
     c     fkprl4_key    reade     fkprl4
     c                   enddo
      *
     c                   if        c2ponr <> 0
     c                   eval      aposl1_ponr = c2ponr
     c     aposl1_key    chain     aposl1
     c                   if        not %found
     c                   eval      *in38 = *on
     c                   goto      c2tagb
     c                   endif
     c                   eval      c2sted = apsted
     c                   endif
      *
     c                   if        c2boku <> 0
     c                   eval      rkunl1_kund = c2boku
     c     rkunl1_key    chain     rkunl1
     c                   if        not %found
     c                   eval      *in46 = *on
     c                   goto      c2tagb
     c                   endif
     c                   endif
      *
     c                   if        c2orka <> 0
     c                   eval      ra06pf_fkod = c2orka
     c     ra06pf_key    chain     ra06pf
     c                   if        not %found
     c                   eval      *in39 = *on
     c                   goto      c2tagb
     c                   endif
     c                   endif
      *
     c                   if        c2orka <> 0 and
     c                             c2okun <> 0
     c                   eval      *in40 = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        c2okun = p_kund and
     c                             c2okpr = p_kpro
     c                   eval      *in41 = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        c2okun <> 0
     c                   eval      rkunl1_kund = c2okun
     c     rkunl1_key    chain     rkunl1
     c                   if        not %found
     c                   eval      *in42 = *on
     c                   goto      c2tagb
     c                   endif
     c                   endif
      *
     c                   if        c2okpr <> 0
     c                   eval      fkprl1_kund = c2okun
     c                   eval      fkprl1_kpro = c2okpr
     c     fkprl1_key    chain     fkprl1
     c                   if        not %found
     c                   eval      *in43 = *on
     c                   goto      c2tagb
     c                   endif
     c                   endif
      *
7.04 c*                  if        c2opda <> 0
7.04 c*    *dmy          test(d)                 c2opda                 44
7.04 c*                  if        *in44 = *on
7.04 c*                  goto      c2tagb
7.04 c*                  endif
7.04 c*                  endif
      *
      * Sjekker prosjekt (regnskap)
      *
     c                   eval      c2ptxt =  *blank
     c                   if        c2proj <> *blank
     c                   if        c2proj <> flproj
     c                   exsr      sjekk_prosj
     c                   if        w_retk <> *blank
     c                   eval      *in45  =  *on
     c                   goto      c2tagb
     c                   endif
     c                   endif
     c                   endif
      *
      * Sjekker fakturatype
      *
7.04 c*                  if        c2faty <> 0
7.04 c*                  eval      fmftl1_faty = c2faty
7.04 c*    fmftl1_key    chain     fmftl1
7.04 c*                  if        not %found
7.04 c*                  eval      *in47 = *on
7.04 c*                  goto      c2tagb
7.04 c*                  endif
7.04 c*                  else
7.04 c*                  eval      *in47 = *on
7.04 c*                  goto      c2tagb
7.06 c*                  endif
      *
     c                   if        c2kjor <> 0
     c                   eval      ra25lr_yrut = c2kjor
     c     ra25lr_key    chain     ra25lr
     c                   if        not %found
     c                   eval      *in48 = *on
     c                   goto      c2tagb
     c                   endif
     c                   endif
6.32  *
6.32  * sjekker om Byggdok er satt opp utfylt når cobuilder er valgt
6.32  *
6.32 c                   if        c2bdok <> 0 and
6.32 c                             w_bdok = *off
6.32 c                   eval      *in53 = *on
6.32 c                   goto      c2tagb
6.32 c                   endif
6.22  *
6.22  * sjekker om Eksternt prosjekt er utfylt når byggdok er valgt
6.22  *
6.22 c                   if        c2bdok <> 0 and
6.22 c                             c2epro = *blank
6.22 c                   eval      *in51 = *on
6.22 c                   goto      c2tagb
6.22 c                   endif
6.32  *
6.32  * sjekker om Cobuilder er satt opp utfylt når cobuilder er valgt
6.32  *
6.32 c                   if        c2cobu <> 0 and
6.32 c                             w_cobu = *off
6.32 c                   eval      *in54 = *on
6.32 c                   goto      c2tagb
6.32 c                   endif
6.32  *
6.32  * sjekker om Eksternt prosjekt er utfylt når cobuilder er valgt
6.32  *
6.32 c                   if        c2cobu <> 0 and
6.32 c                             c2epro = *blank
6.32 c                   eval      *in52 = *on
6.32 c                   goto      c2tagb
6.32 c                   endif

6.31  *
6.31  * sjekker om adresse er utfylt
6.31  *
6.36 c                   if        c2cobu = 1
6.31 c                   if        c2adr1 = *blank
6.31 c                   eval      *in60 = *on
6.31 c                   eval      *in81 = *on
6.31 c                   goto      c2tagb
6.31 c                   else
6.31 c                   eval      *in60 = *off
6.31 c                   eval      *in81 = *off
6.31 c                   endif
6.31  * Sjekke om postnr er utfyllt
6.31  *
6.31 c                   if        c2ponr = *zero
6.31 c                   move      *on           *in61
6.31 c                   move      *on           *in81
6.31 c                   goto      c2tagb
6.31 c                   else
6.31 c                   eval      *in61 = *off
6.31 c                   eval      *in81 = *off
6.31 c                   endif
6.36 c                   endif

      *
      * Oppdaterer tabell
      *
     c                   clear                   fkprlur
7.10 c                   clear                   fkp2lur
      *
     c                   eval      fkprlu_kund = p_kund
     c                   eval      fkprlu_kpro = p_kpro
     c     fkprlu_key    chain     fkprlur
7.10 c     fkprlu_key    chain     fkp2lu
      *
     c                   if        b_stor = *on
     c     Lo:Up         xlate     c2navn        c2navn
     c     Lo:Up         xlate     c2adr1        c2adr1
     c     Lo:Up         xlate     c2adr2        c2adr2
     c     Lo:Up         xlate     c2kprs        c2kprs
     c                   endif
      *
      * automtisk koble prosjekt fra kunde, ved endring av felt
6.33 c*                  if        (flfaty <> c2faty or
6.33 c*                            flmkod <> f2mkod or
6.33 c*                            flekgb <> f2ekgb or
6.33 c*                            flprpa <> f2prpa or
6.33 c*                            flbetb <> f2betb or
6.33 c*                            flneto <> f2neto or
6.33 c*                            flfagb <> f2fagb or
6.33 c*                            flsamf <> f2samf or
6.33 c*                            flsekv <> f2sekv) and
6.33 c*                            flfkun = f2fkun
6.33 c*                  eval      f2fkun = 1
6.33 c*                  endif

     c                   eval      flfdat = *loval
     c                   eval      fltdat = *loval
     c                   eval      flselg = c2selg
     c                   eval      flnavn = c2navn
     c                   eval      fladr1 = c2adr1
     c                   eval      fladr2 = c2adr2
     c                   eval      flponr = c2ponr
     c                   eval      flepro = c2epro
6.21 c                   eval      flbdok = c2bdok
6.21 c                   eval      flcobu = c2cobu
     c                   eval      flkprs = c2kprs
     c                   eval      fltlfa = c2tlfa
     c                   eval      fltlfp = c2tlfp
     c                   eval      fltlfm = c2tlfm
     c                   eval      flmail = c2mail
     c                   eval      flproj = c2proj
7.03 c                   eval      flupro = c2upro
7.03 c                   eval      flkunf = c2kunf
     c                   eval      flboku = c2boku
     c                   eval      flpriv = c2priv
     c                   eval      florka = c2orka
     c                   eval      flokun = c2okun
     c                   eval      flokpr = c2okpr
     c                   eval      flopda = *loval
     c                   eval      fleusr = l_user
     c                   eval      flfaty = c2faty
     c                   eval      flkjor = c2kjor
     c                   eval      flkjnr = c2kjnr
6.30 c                   eval      flmkod = f2mkod
6.30 c                   eval      flekgb = f2ekgb
6.30 c                   eval      flprpa = f2prpa
6.30 c                   eval      flbetb = f2betb
6.30 c                   eval      flneto = f2neto
6.30 c                   eval      flfagb = f2fagb
6.30 c                   eval      flsamf = f2samf
6.30 c                   eval      flsekv = f2sekv
6.33 c                   eval      flfkun = f2fkun
7.10 c                   eval      flktou = f2ktou
7.10 c                   eval      flktsd = f2ktsd
7.10 c                   eval      flktsc = f2ktsc
      *
     c                   if        c2fdat <> 0
     c     *dmy          move      c2fdat        flfdat
     c                   endif
      *
     c                   if        c2tdat <> 0
     c     *dmy          move      c2tdat        fltdat
     c                   endif
      *
     c                   if        c2opda <> 0
     c     *dmy          move      c2opda        flopda
     c                   endif
      *
     c                   call      'RÅ950R'
     c                   parm                    fltlfa
     c                   call      'RÅ950R'
     c                   parm                    fltlfp
     c                   call      'RÅ950R'
     c                   parm                    fltlfm
      *
     c                   time                    fletim
     c                   time                    fledat
6.10 c                   eval      fleusr = l_user
      *
     c                   if        %found(fkprlu)
     c                   update    fkprlur
7.07 c                   eval      w_lagret  = 'J'
6.34 c                   eval      w_kpro = flkpro
6.34 c                   exsr      sjekk_ordre
7.05  * Sjekk fakturatype
7.05 c                   if        s_faty <> flfaty
7.05 c                   eval      sloglu_felt = 'FLFATY'
7.05 c                   time                    sloglu_rdat
7.05 c                   time                    sloglu_rtim
7.05 c     sloglu_key    chain     sloglu
7.05 c                   if        not %found
7.05 c                   eval      elfirm = w_firm
7.05 c                   eval      elregi = sloglu_regi
7.05 c                   eval      elfore = sloglu_fore
7.05 c                   eval      elfort = sloglu_fort
7.05 c                   eval      elfelt = 'FLFATY'
7.05 c                   eval      elrdat = sloglu_rdat
7.05 c                   eval      elrtim = sloglu_rtim
7.05 c                   eval      eluser = l_user
7.05 c                   time                    eldato
7.05 c                   movel     s_faty        elfrav
7.05 c                   movel     flfaty        eltilv
7.05 c                   eval      elrusr = l_user
7.05 c                   write     sloglur
7.05 c                   endif
7.05 c                   endif
7.05  * Sjekk overtyrt prisdato
7.05 c                   if        s_opda <> flopda
7.05 c                   eval      sloglu_felt = 'FLOPDA'
7.05 c                   time                    sloglu_rdat
7.05 c                   time                    sloglu_rtim
7.05 c     sloglu_key    chain     sloglu
7.05 c                   if        not %found
7.05 c                   eval      elfirm = w_firm
7.05 c                   eval      elregi = sloglu_regi
7.05 c                   eval      elfore = sloglu_fore
7.05 c                   eval      elfort = sloglu_fort
7.05 c                   eval      elfelt = 'FLOPDA'
7.05 c                   eval      elrdat = sloglu_rdat
7.05 c                   eval      elrtim = sloglu_rtim
7.05 c                   eval      eluser = l_user
7.05 c                   time                    eldato
7.05 c                   movel     s_opda        elfrav
7.05 c                   movel     flopda        eltilv
7.05 c                   eval      elrusr = l_user
7.05 c                   write     sloglur
7.05 c                   endif
7.05 c
7.05 c                   endif
7.05  *
     c                   else
      *
     c                   eval      w_kode = '0'
7.01 c                   if        ((u_701 = *on) and (p_kund =  w_pkund))
7.01 c                   eval      w_fell = w_wfell
7.01 c                   else
     c                   eval      w_fell = 'FKPROJ'
7.01 c                   endif
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
6.nu c                   parm                    w_numm
      *
6.nu c                   eval      p_kpro = w_numm
      *
     c                   eval      flfirm = w_firm
     c                   eval      flkund = p_kund
     c                   eval      flkpro = p_kpro
      *
     c                   time                    flodat
6.10 c                   time                    flotim
6.10 c                   eval      flousr = l_user
      *
     c                   write     fkprlur
7.07 c                   eval      w_lagret  = 'J'
      *
     c                   endif
7.03  *
7.03 c                   if        %found(fkp2lu)
7.03 c                   update    fkp2lur
7.03 c                   else
7.03 c                   eval      flfirm = w_firm
7.03 c                   eval      flkund = p_kund
7.03 c                   eval      flkpro = p_kpro
7.03 c                   write     fkp2lur
7.03 c                   endif
8.01  *
8.01  * Tilleggsregister med PM info
8.01  *
8.01 c                   clear                   fkp3iur
8.01  *
8.01 c     fkprlu_key    chain     fkp3iur
8.01  *
8.01 c                   eval      f3pmtx = f2pmtx
8.01 c                   eval      f3noit = f2noit
8.01 c                   time                    f3edat
8.01 c                   time                    f3etim
8.01 c                   eval      f3eusr = l_user
8.01  *
8.01 c                   if        not %found
8.01 c                   eval      f3firm = w_firm
8.01 c                   eval      f3kund = p_kund
8.01 c                   eval      f3kpro = p_kpro
8.01  *
8.01 c                   time                    f3odat
8.01 c                   time                    f3otim
8.01 c                   eval      f3ousr = l_user
8.01  *
8.01 c                   write     fkp3iur
8.01 c                   endif
      *
      * Oppdaterer søketekst
      *
     c                   call      'FL750R'
     c                   parm                    w_firm
     c                   parm                    p_kund
     c                   parm                    p_kpro
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      *
     c     xc2bld        begsr
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av prosjekt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_prosj   begsr
      *
      * Prosjekt
      *
     c                   eval      w_kont = 0
     c                   eval      w_spes = 0
      *
     c                   call      'RS880R'
     c                   parm                    w_retk
     c                   parm                    c2proj
     c                   parm                    w_upro
     c                   parm                    w_ptx1
     c                   parm                    w_ptx2
      *
     c                   eval      c2ptxt = *blank
     c                   if        w_retk = *blank
     c                   eval      c2ptxt = w_ptx1
7.03 c                   if        w_ptx2 <> *blank
7.03 c                   eval      c2ptxt = %trim(c2ptxt) + '/' +
7.03 c                                      %trim(w_ptx2)
7.03 c                   endif
     c                   endif
      *
     c                   endsr
      /eject
      *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30  * F2WIN Behandle bilde                                            *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30  *
6.30 c     xf2win        begsr
      *
6.30 c                   if        fbko17 = 0
6.30 c                   eval      w_mld1 = a_meld(1)
6.30 c                   call      'AA005R'
6.30 c                   parm                    w_mld1
6.30 c                   leavesr
6.30 c                   endif
6.30  *
6.30 c     f2tagb        tag
6.30 c                   exfmt     f2win
      *
     c                   if        *inkc or *inkl
     c                   goto      f2tag1
     c                   endif
6.30  *
6.30  *
6.30  * Spørring
6.30  *
6.30 c                   if        *inkd
6.30  *
6.30  * Call program for BETALINGSBETINGELSE
6.30  *
6.30 c                   if        w_afld = 'F2BETB'
6.30 c                   call      'RA503R'
6.30 c                   parm                    w_firm
6.30 c                   parm                    f2betb
6.30 c                   parm                    f2ctxt
6.30 c                   goto      f2tagb
6.30 c                   endif
7.10  *
7.10  * Call program for Kontonummer Utgift
7.10  *
7.10 c                   if        w_afld = 'F2KTOU'
7.10 c                   eval      w_spes = *zero
7.10 c                   eval      w_avde = *zero
7.10 c                   call      'RH500R'
7.10 c                   parm                    w_firm
7.10 c                   parm                    f2ktou
7.10 c                   parm                    w_spes
7.10 c                   parm                    w_avde
7.10 c                   parm                    f2ktxt
7.10 c                   goto      f2tagb
7.10 c                   endif
7.02  *
7.02  * Call program for Kontonummer Svinn Debet
7.02  *
7.02 c                   if        w_afld = 'F2KTSD'
7.02 c                   eval      w_spes = *zero
7.02 c                   eval      w_avde = *zero
7.02 c                   call      'RH500R'
7.02 c                   parm                    w_firm
7.02 c                   parm                    f2ktsd
7.02 c                   parm                    w_spes
7.02 c                   parm                    w_avde
7.02 c                   parm                    f2txtd
7.02 c                   goto      f2tagb
7.02 c                   endif
7.02  *
7.02  * Call program for Kontonummer Svinn Credit
7.02  *
7.02 c                   if        w_afld = 'F2KTSC'
7.02 c                   eval      w_spes = *zero
7.02 c                   eval      w_avde = *zero
7.02 c                   call      'RH500R'
7.02 c                   parm                    w_firm
7.02 c                   parm                    f2ktsc
7.02 c                   parm                    w_spes
7.02 c                   parm                    w_avde
7.02 c                   parm                    f2txtc
7.02 c                   goto      f2tagb
7.02 c                   endif
8.01  *
8.01  * Call program for profile manager
8.01  *
8.01 c                   if        w_afld = 'F2PMTX'
8.01 c                   exsr      sub_profilm
8.01 c                   goto      f2tagb
8.01 c                   endif
6.30  *
6.30 c                   endif

6.30  *
6.30  * Sjekke om betalingsbetingelse er gyldig
6.30  *
6.30 c                   if        f2betb <> *zero
6.30 c                   call      'RS703R'
6.30 c                   parm                    p_stat
6.30 c                   parm                    f2betb
6.30 c                   parm                    f2ctxt
6.30  *
6.30 c                   if        p_stat = 'N'
6.30 c                   move      *on           *in33
6.30 c                   goto      f2tagb
6.30 c                   endif
6.30 c                   endif
7.04  *
7.04  * Sjekker fakturatype
7.04  *
7.04 c                   if        c2faty <> 0
7.04 c                   eval      fmftl1_faty = c2faty
7.04 c     fmftl1_key    chain     fmftl1
7.04 c                   if        not %found
7.04 c                   eval      *in47 = *on
7.04 c                   goto      f2tagb
7.04 c                   endif
7.04 c                   else
7.04 c                   eval      *in47 = *on
7.04 c                   goto      f2tagb
7.04 c                   endif
7.04  *
7.04 c                   if        c2opda <> 0
7.04 c     *dmy          test(d)                 c2opda                 44
7.04 c                   if        *in44 = *on
7.04 c                   goto      f2tagb
7.04 c                   endif
7.04 c                   endif
7.10  *
7.10  * Sjekke om konto utgiftsbilag er gyldig
7.10  *
7.10 c                   if        f2ktou <> *zero
7.10 c                   eval      w_avde = *zero
7.10 c                   eval      w_spes = *zero
7.10 c                   call      'RS740R'
7.10 c                   parm                    p_stat
7.10 c                   parm                    f2ktou
7.10 c                   parm                    w_spes
7.10 c                   parm                    w_avde
7.10 c                   parm                    f2ktxt
7.10  *
7.10 c                   if        p_stat <> *blank
7.10 c                   move      *on           *in55
7.10 c                   goto      f2tagb
7.10 c                   endif
7.10 c                   else
7.10 c                   eval      f2ktxt = *blank
7.10 c                   endif
7.10  *
7.10  * Sjekke om konto svinn er gyldig
7.10  *
7.02 c                   if        f2ktsd <> *zero
7.02 c                   eval      w_avde = *zero
7.02 c                   eval      w_spes = *zero
7.02 c                   call      'RS740R'
7.02 c                   parm                    p_stat
7.02 c                   parm                    f2ktsd
7.02 c                   parm                    w_spes
7.02 c                   parm                    w_avde
7.02 c                   parm                    f2txtd
7.02  *
7.02 c                   if        p_stat <> *blank
7.02 c                   move      *on           *in56
7.02 c                   goto      f2tagb
7.02 c                   endif
7.02 c                   else
7.02 c                   eval      f2txtd = *blank
7.02 c                   endif
7.02  *
7.02 c                   if        f2ktsc <> *zero
7.02 c                   eval      w_avde = *zero
7.02 c                   eval      w_spes = *zero
7.02 c                   call      'RS740R'
7.02 c                   parm                    p_stat
7.02 c                   parm                    f2ktsc
7.02 c                   parm                    w_spes
7.02 c                   parm                    w_avde
7.02 c                   parm                    f2txtc
7.02  *
7.02 c                   if        p_stat <> *blank
7.02 c                   move      *on           *in57
7.02 c                   goto      f2tagb
7.02 c                   endif
7.02 c                   else
7.02 c                   eval      f2txtc = *blank
7.02 c                   endif
7.02  *
7.02  *  Ikke både utgiftskonto og svinn på samme kundeprosjekt
7.02 c                   if        f2ktou <> *zero
7.02 c                             and (f2ktsd <> *zero
7.02 c                               or f2ktsc <> *zero)
7.02 c*                  eval      f2txtd = 'Konto utgift og Svinn kan ikke'
7.02 c*                  eval      f2txtc = 'kombineres <<<<<<<<<<<<<<<<<<<'
7.02 c                   eval      *in58 = *on
7.02 c                   goto      f2tagb
7.02 c                   endif
7.02  *
7.02  *  Dersom Svinn Debet konto er utfylt kreves også svinn Credit
7.02 c                   if        (   f2ktsd <> *zero
7.02 c                             and f2ktsc =  *zero)
7.02 c                             or (f2ktsc <> *zero
7.02 c                             and f2ktsd =  *zero)
7.02 c*                  eval      f2txtd = 'Debet og Credit konto Svinn   '
7.02 c*                  eval      f2txtc = 'må fylles ut begge. <<<<<<<<<<'
7.02 c                   eval      *in59 = *on
7.02 c                   goto      f2tagb
7.02 c                   endif
      *
6.30  *
6.30  * Advarsel hvis Avgiftsfri er endret
6.30  *
6.30 c                   if        flmkod <> f2mkod
6.30 c                   eval      w_mld2 = 'Avgiftsfri kode er endret,'
6.30 c                   eval      w_mld3 = 'bekreft at dette ønskes . .     '
6.30 c                   eval      w_svar = 'N'
6.30 c                   eval      w_in12 = 0
6.30  *
6.30 c                   call      'AA007R'
6.30 c                   parm                    w_mld2
6.30 c                   parm                    w_mld3
6.30 c                   parm                    w_svar
6.30 c                   parm                    w_in12
6.30  *
6.30  * F12 er tatt
6.30  *
6.30 c                   if        w_in12 = 1
6.30 c                   goto      f2tagb
6.30 c                   endif
6.30  *
6.30 c                   if        w_svar = 'N'
6.30 c                   goto      f2tagb
6.30 c                   endif
6.30  *
6.30 c                   endif
7.10  *     Oppdatere fkp2lu
7.10 c     fkprl1_key    chain     fkp2lu
7.10 c*                  eval      flbdok = f2bdok
7.10 c                   eval      flktou = f2ktou
7.10 c                   eval      flktsd = f2ktsd
7.10 c                   eval      flktsc = f2ktsc
7.03 c                   eval      flupro = c2upro
7.03 c                   eval      flkunf = c2kunf
7.10 c                   if        %found(fkp2lu)
7.10 c                   update    fkp2lur
7.10 c                   else
7.10 c                   eval      flfirm = w_firm
7.10 c                   eval      flkund = p_kund
7.10 c                   eval      flkpro = p_kpro
7.10 c                   write     fkp2lur
7.10 c                   endif
6.30  *
6.30 c     f2tag1        tag
6.30  *
6.30 c                   endsr
6.30  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Sjekk om endring skal kopiers til ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
6.34 c     sjekk_ordre   begsr
      *
      *  sjekk om ett av feltene er endret
     c                   if        w_faty <> flfaty or
     c                             w_mkod <> flmkod or
     c                             w_betb <> flbetb or
     c                             w_ekgb <> flekgb or
     c                             w_fagb <> flfagb or
     c                             w_neto <> flneto
      *
     c                   eval      fohelk_kund = flkund
     c     fohelk_key    setll     fohelk
     c     fohelk_key    reade     fohelk
     c                   dow       not %eof
      * sjekk at ikke koblet mot kundeprosjekt
     c                   if        fokpro = w_kpro
      *
      * sjekk om et av feltene har gammel verdi
     c                   if        w_faty = fofaty or
     c                             w_mkod = fomkod or
     c                             w_ekgb = fokgek or
     c                             w_betb = fobetb or
     c                             w_neto = foneto or
     c                             w_fagb = fokgfa
      *
     c                   eval      fohelu_numm = fonumm
     c                   eval      fohelu_suff = fosuff
     c     fohelu_key    chain     fohelu
     c                   if        %found
     c                   eval      w_endr = *off
      * oppdater de feltene som har gammel verdi, og verdi er forskjellig fra kunde
     c                   if        fofaty = w_faty and fofaty <> flfaty
     c                   eval      fofaty = flfaty
     c                   eval      w_endr = *on
     c                   endif
     c                   if        fomkod = w_mkod and fomkod <> flmkod
     c                   eval      fomkod = flmkod
     c                   eval      w_endr = *on
     c                   endif
     c                   if        fokgek = w_ekgb and fokgek <> flekgb
     c                   eval      fokgek = flekgb
     c                   eval      w_endr = *on
     c                   endif
     c                   if        fobetb = w_betb and fobetb <> flbetb
     c                   eval      fobetb = flbetb
     c                   eval      w_endr = *on
     c                   endif
     c                   if        foneto = w_neto and foneto <> flneto
     c                   eval      foneto = flneto
     c                   eval      w_endr = *on
     c                   endif
     c                   if        fokgfa = w_fagb and fokgfa <> flfagb
     c                   eval      fokgfa = flfagb
     c                   eval      w_endr = *on
     c                   endif
      *
     c                   if        w_endr = *on
     c                   time                    foedat
     c                   time                    foetim
     c                   eval      foeusr = l_user
     c                   update    fohelur
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c     fohelk_key    reade     fohelk
     c                   enddo
      *
     c                   endif
      *
6.34 c                   endsr
      *
8.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
8.01  * Håndtering av verdier fra Profil Manager
8.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
8.01  *
8.01 c     sub_profilm   begsr
8.01  *
8.01  *  Kall til API for å hente info om Profile Manager
8.01  *
8.01  * Henter inn løpenummer teller
8.01  *
8.01 c                   eval      w_kode = '0'
8.01 c                   eval      w_fell = 'NXTKLI'
8.01 c                   eval      w_type = *blank
8.01 c                   eval      w_fast = *blank
8.01 c                   eval      w_numm = *zero
8.01  *
8.01 c                   call      'AS100R'
8.01 c                   parm                    w_kode
8.01 c                   parm                    p_firm
8.01 c                   parm                    w_fell
8.01 c                   parm                    w_type
8.01 c                   parm                    w_fast
8.01 c                   parm                    w_numm
8.01  *
8.01 c                   eval      w_memb = 'A' + %char(w_numm)
8.01 c                   eval      w_stat = '0'
8.01  * Slett for sikkerhetsskyld om det er noe som ligger her fra før
8.01 c/exec sql
8.01 c+ delete from promst
8.01 c+   where pmmemb = :w_memb
8.01 c/end-exec
8.01 c                   call      'AP050R'
8.01 c                   parm                    w_firm
8.01 c                   parm                    w_memb
8.01 c                   parm                    w_stat
8.01  *
8.01 c                   if        w_stat = '9'
8.01  *
8.01 c                   eval      w_ctli = 12
8.01 c                   eval      w_ctpo = 42
8.01 c                   eval      w_mld01 = 'Innhenting av PM info feilet'
8.01 c                   eval      w_mld02 = 'Prøv på nytt, eller kontakt EG'
8.01 c                   call      'AA006C'
8.01 c                   parm                    w_ctli
8.01 c                   parm                    w_ctpo
8.01 c                   parm                    w_mld01
8.01 c                   parm                    w_mld02
8.01  *
8.01 c                   elseif    w_stat = '8'
8.01  *
8.01 c                   eval      w_ctli = 12
8.01 c                   eval      w_ctpo = 42
8.01 c                   eval      w_mld01 = 'Teknisk feil ved retur       '
8.01 c                   eval      w_mld02 = 'Kontakt EG Support           '
8.01 c                   call      'AA006C'
8.01 c                   parm                    w_ctli
8.01 c                   parm                    w_ctpo
8.01 c                   parm                    w_mld01
8.01 c                   parm                    w_mld02
8.01  *
8.01 c                   elseif    w_stat = '7'
8.01  *
8.01 c                   eval      w_ctli = 12
8.01 c                   eval      w_ctpo = 42
8.01 c                   eval      w_mld01 = 'Returfila med PM info er tom '
8.01 c                   eval      w_mld02 = 'Kontakt EG Support           '
8.01 c                   call      'AA006C'
8.01 c                   parm                    w_ctli
8.01 c                   parm                    w_ctpo
8.01 c                   parm                    w_mld01
8.01 c                   parm                    w_mld02
8.01  * Da har vi fått noe i retur
8.01 c                   else
8.01  *
8.01  * Slett for sikkerhetsskyld om det er noe som ligger her fra før
8.01  *
8.01 c                   call      'PM500R'
8.01 c                   parm                    w_memb
8.01 c                   parm                    w_pmtx
8.01 c                   parm                    w_noit
8.01  *
8.01 c                   eval      f2noit = w_noit
8.01 c                   eval      f2pmtx = w_pmtx
8.01  *
8.01 c                   endif
8.01  *
8.01  * Rydd opp etter API kall og valg av verdier
8.01 c/exec sql
8.01 c+ delete from promst
8.01 c+   where pmmemb = :w_memb
8.01 c/end-exec
8.01  *
8.01 c                   endsr
8.01  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kund
     c                   parm                    p_kpro
     c                   parm                    p_navn
     c                   parm                    p_adr1
     c                   parm                    p_adr2
     c                   parm                    p_kund_kopi
     c                   parm                    p_kpro_kopi
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
     c     fkprl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl4_kund
     c                   kfld                    fkprl4_navn
      *
      * Key for oppdatering av kundeprosjekt
      *
     c     fkprlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprlu_kund
     c                   kfld                    fkprlu_kpro
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på bruker
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrl1_user
      *
      * Key for oppslag på selger
      *
     c     ra09l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra09l1_ikod
      *
      * Key for oppslag på rabattkategori
      *
     c     ra06pf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra06pf_fkod
      *
      * Key for oppslag på kjørerute
      *
     c     ra25lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra25lr_yrut
      *
      * Key for oppslag på status-register økonomi
      *
     c     raa1pf_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på poststed-register
      *
     c     aposl1_key    klist
     c                   kfld                    aposl1_ponr
      *
      *
      * Key for oppslag på fakturatype
      *
     c     fmftl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fmftl1_faty
6.23  *
6.23  * Key for oppslag på kontaktpersoner
6.23  *
6.23 c     rukpl6_key    klist
6.23 c                   kfld                    w_firm
6.23 c                   kfld                    rukpl6_kund
6.30  *
6.30  * Key for oppslag på kontaktpersoner
6.30  *
6.30 c     rukpl6_key2   klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    rukpl6_kund
6.30 c                   kfld                    rukpl6_kpnr
6.32  *
6.32  * Key for propertiesfil
6.32  *
6.32 c     afpslr_key    klist
6.32 c                   kfld                    afpslr_ufil
6.32 c                   kfld                    afpslr_uide
6.32  *
6.32  * Key for les av moduler Nexstep
6.32  *
6.32 c     amodl1_key    klist
6.32 c                   kfld                    amodl1_modu
6.34  *
6.34  * Key for oppslag på ordehode
6.34  *
6.34 c     fohelk_key    klist
6.34 c                   kfld                    w_firm
6.34 c                   kfld                    fohelk_kund
6.34  *
6.34  * Key for oppdatering av odre-hode
6.34  *
6.34 c     fohelu_key    klist
6.34 c                   kfld                    w_firm
6.34 c                   kfld                    fohelu_numm
6.34 c                   kfld                    fohelu_suff
6.35  *
6.35  * Key for file : Firmastyrt validering
6.35  *
6.35 c     avall1_key    klist
6.35 c                   kfld                    w_firm
6.35 c                   kfld                    avall1_apgm
7.05  *
7.05  * Key for oppdatering av endringslogg
7.05  *
7.05 c     sloglu_key    klist
7.05 c                   kfld                    w_firm
7.05 c                   kfld                    sloglu_regi
7.05 c                   kfld                    sloglu_fore
7.05 c                   kfld                    sloglu_fort
7.05 c                   kfld                    sloglu_felt
7.05 c                   kfld                    sloglu_rdat
7.05 c                   kfld                    sloglu_rtim
8.01  *
8.01  * Key for oppdatering av profile manager
8.01  *
8.01 c     fkp3iu_key    klist
8.01 c                   kfld                    w_firm
8.01 c                   kfld                    fkp3iu_kund
8.01 c                   kfld                    fkp3iu_kpro
8.01  *
8.01  * Key for oppdatering av profile manager
8.01  *
8.01 c     fkp3i1_key    klist
8.01 c                   kfld                    w_firm
8.01 c                   kfld                    fkp3i1_kund
8.01 c                   kfld                    fkp3i1_kpro
      *
      * Henter verdier fra parameter, og initierer skjermbilde
      *
     c                   eval      w_firm = p_firm
      *
     c                   eval      c2kund = p_kund
     c                   eval      c2kpro = p_kpro
      *
      * Henter dagens dato
      *
     c                   time                    w_udat
6.35  *
6.35  * Firmastyrt validering av utvalgte felter
6.35  *
6.35  *
6.35 c                   eval      avall1_apgm = 'FL105R'
6.35 c     avall1_key    setll     avall1
6.35 c     avall1_key    reade     avall1
6.35 c                   dow       not %eof(avall1)
6.35 c                   if        avaflt = 'FLADR1'  and
6.35 c                             avaval = 1
6.35 c                   eval      u_adr1 = *on
6.35 c                   endif
6.35 c                   if        avaflt = 'FLPONR'  and
6.35 c                             avaval = 1
6.35 c                   eval      u_ponr = *on
6.35 c                   endif
6.35 c     avall1_key    reade     avall1
6.35 c                   enddo
6.35  *
7.05 c                   eval      sloglu_regi = 'KPROS'
7.05 c                   movel     p_kund        sloglu_fore
7.05 c                   movel     p_kpro        sloglu_fort
7.07 c                   eval      w_lagret = 'N'
7.01  *
7.01  * Endring 7.01 - Usikker på hvorfor det ligger _702 i nøkkel ??
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'FL105R_702':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     w_pos = %scan('¤' : co402_verdi2);
7.01     if (w_pos > 0);
7.01       w_pkund = %DEC(%subst(co402_verdi2 : 1 : w_pos - 1) : 6 : 0);
7.01       co402_verdi2 =  %subst(co402_verdi2:w_pos+1:512-w_pos);
7.01       w_pos = %scan('¤' : co402_verdi2);
7.01       w_wfell  = %subst(co402_verdi2 : 1 : w_pos - 1);
7.01       if ((w_pkund > 0) and (w_wfell <> *blank));
7.01         u_701 = *on;
7.01       endif;
7.01     endif;
7.01   endif;
      *
     c                   endsr
**     M e l d i n g e r
Bruker har ikke lov til å endre koder             <---
