     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : NO150R
      * Beskrivelse . . : Ekstern ordre-hode, hent kundeordre
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       011205 HKO  Nytt program
      *       070506 HKO  Lagt inn oppretting av kundeprosjekt
      *                   Lagt inn innlesing av BA-kalk
5.60  *       130808 bof  Vise parameteret
7.xx  * 7.00  290116 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  070120 bsa  Tillater å hente EDI direkte fra eksternordre. Kodene
7.01  *                   for EDI/SMK og EHF henter fra EDI.
7.02  * 7.02  260220 hko  Lagt inn mottak av komplett prosjekt csv-filer fra
7.02  *                   SmartKalk (SKC)
7.03  * K000  250520 bsa  Endret program for innhenting av bestillinger.
7.04  * K000  201120 ask  Utvidet parameter liste til FA930R pga. 1881 søk
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fnkeol1    if   e           k disk    rename(nkeopfr:nkeol1r)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
      *
     fno150d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
5.10 d nkeol1_kode     s                   like(nkkode)
     d fusrl1_user     s                   like(fbuser)
5.10 d rkunl1_kund     s                   like(rkkund)
5.10 d fkprl1_kund     s                   like(flkund)
5.10 d fkprl1_kpro     s                   like(flkpro)
5.10 d vltpl1_lety     s                   like(vylety)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
5.60 d b_forst         s              1    inz(*off)
      *
     d w_retu          s              1
      *
     d w_firm          s                   like(nkfirm)
     d w_adr1          s                   like(rkgate)
     d w_adr2          s                   like(rkadr2)
     d w_sted          s                   like(rksted)
     d w_pad1          s                   like(fladr1)
     d w_pad2          s                   like(fladr2)
      *
     d w_kund_kopi     s                   like(flkund)
     d w_kpro_kopi     s                   like(flkpro)
      *
7.01 d w_type          s              4
7.01 d w_hent          s              1
7.01 d w_con1          c                   '*** EDI innhenting pågår *** '
7.01 d w_con2          c                   'Vent og prøv igjen senere.   '
7.01 d w_con3          c                   'Vent og prøv igjen senere.   '
7.01 d w_mld1          s             50
7.01 d w_mld2          s             50
7.01 d w_mld3          s             50
7.01 d w_len           s             15  5
7.01 d w_jsta          s              2  0
7.01 d w_onof          s              1  0
7.04 d w_tlfn          s             11
      *
7.01 d p_stat          s              1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     b1taga        tag
      *
     c                   exfmt     b1win
      *
     c                   if        *inkc or *inkl
     c                   goto      avslutt
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
      *
     c                   if        w_afld = 'B1KODE'
     c                   call      'NK510R'
     c                   parm                    w_firm
     c                   parm                    b1kode
     c                   parm                    b1besk
     c                   eval      nkeol1_kode = b1kode
     c     nkeol1_key    chain     nkeol1
     c                   if        %found
     c                   eval      b1mapp = nkmapp
     c                   eval      b1link = nklink
     c                   eval      b1file = nkfile
7.02 c                   eval      b_forst = *on
     c                   endif
     c                   goto      b1taga
     c                   endif
      *
     c                   if        w_afld = 'B1KUND'
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    b1kund
     c                   parm                    b1knav
     c                   goto      b1taga
     c                   endif
      *
     c                   if        w_afld = 'B1KPRO'
     c                   call      'FL500R'
     c                   parm                    w_firm
     c                   parm                    b1kund
     c                   parm                    b1kpro
     c                   parm                    b1knav
     c                   parm                    b1pnav
     c                   goto      b1taga
     c                   endif
      *
     c                   if        w_afld = 'B1LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    b1lety
     c                   parm                    b1lbes
     c                   goto      b1taga
     c                   endif
     c                   goto      b1taga
      *
     c                   endif
5.60  *
5.60  * viser parameter
5.60 c                   if        b_forst = *off
5.60 c                   eval      nkeol1_kode = b1kode
5.60 c     nkeol1_key    chain     nkeol1
5.60 c                   if        %found
5.60 c                   eval      b1mapp = nkmapp
5.60 c                   eval      b1link = nklink
5.60 c                   eval      b1file = nkfile
7.01 c                   eval      b1besk = nkbesk
5.60 c                   endif
5.60 c                   eval      b_forst = *on
5.60 c                   goto      b1taga
5.60 c                   endif
      *
      * F6=Opprett ny kunde
      *
     c                   if        *inkf = *on
      *
     c                   eval      fusrl1_user = l_user
     c     fusrl1_key    chain     fusrl1
     c                   if        not %found or
5.20 c                             fbko12 <> 1
     c                   eval      *in50 = *on
     c                   goto      b1taga
5.20 c                   endif
      *
     c                   exsr      oppr_kunde
     c                   goto      b1taga
      *
     c                   endif
      *
      * F7=Opprett kundeprosjekt
      *
     c                   if        *inkg = *on
     c                   exsr      oppr_kundepro
     c                   goto      b1taga
     c                   endif
      *
      * Validering av input
      *
     c                   eval      nkeol1_kode = b1kode
     c     nkeol1_key    chain     nkeol1
     c                   if        not %found
     c                   eval      *in31 = *on
     c                   goto      b1taga
     c                   endif
      *
      * Gjelder ikke hvis det er EDI som skal hentes.
      *
7.01 c                   if        b1kode <> 'EDI' and
7.01 c                             b1kode <> 'SMK' and
7.01 c                             b1kode <> 'EHF'
      *
     c                   if        b1kund <> 0
     c                   eval      rkunl1_kund = b1kund
     c     rkunl1_key    chain     rkunl1
     c                   if        not %found
     c                   eval      *in33 = *on
     c                   goto      b1taga
     c                   endif
     c                   endif
      *
     c                   if        b1kpro <> 0
     c                   eval      fkprl1_kund = b1kund
     c                   eval      fkprl1_kpro = b1kpro
     c     fkprl1_key    chain     fkprl1
     c                   if        not %found
     c                   eval      *in34 = *on
     c                   goto      b1taga
     c                   endif
     c                   endif
      *
     c                   eval      vltpl1_lety = b1lety
     c     vltpl1_key    chain     vltpl1
     c                   if        not %found
     c                   eval      *in35 = *on
     c                   goto      b1taga
     c                   endif
      *
     c                   if        b1mapp = *blank
     c                   eval      b1mapp = nkmapp
     c                   endif
      *
     c                   if        b1link = *blank
     c                   eval      b1link = nklink
     c                   endif
      *
     c                   if        b1file = *blank
     c                   eval      b1file = nkfile
     c                   endif
      *
     c                   if        b1mapp = *blank and
     c                             b1link = *blank
     c                   eval      *in37 = *on
     c                   goto      b1taga
     c                   endif
      *
     c                   if        b1file = *blank
     c                   eval      *in38 = *on
     c                   goto      b1taga
     c                   endif
7.01  *
7.01 c                   endif
      *
      * Kaller innlesingsprogram i.h.t. kode/system
      *
     c                   eval      b_feil = *off
      *
     c                   select
     c                   when      nkkode = 'NWD'
     c                   call      'NO750R'
     c                   parm                    w_firm
     c                   parm                    b1kund
     c                   parm                    b1kpro
     c                   parm                    b1lety
     c                   parm                    b1link
     c                   parm                    b1file
     c                   parm                    b_feil
     c                   when      nkkode = 'BAK'
     c                   call      'NO751R'
     c                   parm                    w_firm
     c                   parm                    b1kund
     c                   parm                    b1kpro
     c                   parm                    b1lety
     c                   parm                    b1link
     c                   parm                    b1file
     c                   parm                    b_feil
7.02 c                   when      nkkode = 'SKC'
7.02 c                   call      'NO752R'
7.02 c                   parm                    w_firm
7.02 c                   parm                    b1kund
7.02 c                   parm                    b1kpro
7.02 c                   parm                    b1lety
7.02 c                   parm                    b1mapp
7.02 c                   parm                    b1file
7.02 c                   parm                    b_feil
7.01  * EDI ordre
7.01 c                   when      nkkode = 'EDI'
7.01 c                   exsr      hent_meld
7.01  * Smartkalk via EDI
7.01 c                   when      nkkode = 'SMK'
7.01 c                   exsr      hent_meld
7.01  * EHF ordre via EDI
7.01 c                   when      nkkode = 'EHF'
7.01 c                   exsr      hent_meld
     c                   other
     c                   eval      *in49 = *on
     c                   goto      b1taga
     c                   endsl
      *
     c                   if        b_feil = *on
     c                   eval      *in49 = *on
     c                   goto      b1taga
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppretter ny kunde i kunderegisteret
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppr_kunde    begsr
5.20  *
      * Kaller program for oppretting av kunde
      *
     c                   call      'FA930R'
     c                   parm                    w_retu
     c                   parm                    w_firm
     c                   parm                    b1knav
     c                   parm                    w_adr1
     c                   parm                    w_adr2
     c                   parm                    w_sted
     c                   parm                    b1kund
7.04 c                   parm                    w_tlfn
      *
      * Henter navn på kunde
      *
     c                   if        b1kund <> 0
     c                   eval      rkunl1_kund = b1kund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      b1knav = rknavn
     c                   endif
     c                   endif
      *
     c                   eval      b1kpro = 0
     c                   eval      b1pnav = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppretter kundeprosjekt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppr_kundepro begsr
      *
      * Oppretter kundeprosjekt
      *
     c                   if        b1kund = 0
     c                   call      'FL104R'
     c                   parm                    w_firm
     c                   parm                    b1kund
     c                   parm                    b1kpro
     c                   else
     c                   call      'FL105R'
     c                   parm                    w_firm
     c                   parm                    b1kund
     c                   parm                    b1kpro
     c                   parm                    b1pnav
     c                   parm                    w_pad1
     c                   parm                    w_pad2
     c                   parm                    w_kund_kopi
     c                   parm                    w_kpro_kopi
     c                   endif
      *
      * Henter navn på kunde og kundeprosjekt
      *
     c                   if        b1kund <> 0
     c                   eval      rkunl1_kund = b1kund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      b1knav = rknavn
     c                   endif
     c                   endif
      *
     c                   if        b1kpro <> 0
     c                   eval      fkprl1_kund = b1kund
     c                   eval      fkprl1_kpro = b1kpro
     c     fkprl1_key    chain     fkprl1
     c                   if        %found
     c                   eval      b1pnav = flnavn
     c                   endif
     c                   endif
      *
     c                   endsr
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Subrutine for å hente nye meldinger
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01 c     hent_meld     begsr
7.01  *
7.01  * Sjekk om overføring allerede pågår.
7.01  *
7.01 c                   eval      w_jsta = 07
7.01 c                   eval      w_onof = 0
7.01  *
7.01 c                   call      'FA730R'
7.01 c                   parm                    w_jsta
7.01 c                   parm                    w_onof
7.01  *
7.01  * Overføring pågår eller finnes ikke.
7.01  *
7.01 c                   if        w_onof  = 1
7.01 c                   eval      w_mld1 = w_con1
7.01 c                   eval      w_mld2 = w_con2
7.01 c                   eval      w_mld3 = w_con3
7.01  *
7.01 c                   call      'AA031R'
7.01 c                   parm                    w_mld1
7.01 c                   parm                    w_mld2
7.01 c                   parm                    w_mld3
7.01  *
7.01 c                   goto      end_hent
7.01 c                   endif
7.01  *
7.01  * Oppdater jobb-register med aktiv-flagg
7.01  *
7.01 c                   eval      w_jsta = 07
7.01 c                   eval      w_onof = 1
7.01 c                   call      'FA720R'
7.01 c                   parm                    w_jsta
7.01 c                   parm                    w_onof
7.01  *
7.01 c                   eval      p_stat = *blanks
7.03 c**                 call      'LI700CO'
7.03 c                   call      'LI700CB'
7.01 c                   parm                    p_stat
7.01 c                   eval      w_type = 'ORDR'
7.01  * Flagger evnt feil
7.01 c                   if        p_stat = 'F'
7.01 c                   exfmt     b9win3
7.01 c                   endif
7.01  *
7.01  * Fjern aktiv-flagg fra jobb-register
7.01  *
7.01 c                   eval      w_jsta = 07
7.01 c                   eval      w_onof = 0
7.01 c                   call      'FA720R'
7.01 c                   parm                    w_jsta
7.01 c                   parm                    w_onof
7.01  *
7.01 c     end_hent      endsr
7.01  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-håndtering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      *in49 = *on
     c                   goto      b1taga
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på ekstern ordre-kode
      *
     c     nkeol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    nkeol1_kode
      *
      * Key for oppslag på ofak-bruker
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrl1_user
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag på leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
