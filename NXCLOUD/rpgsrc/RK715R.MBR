     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON
      * Program . . . . : RK715R
      * Beskrivelse . . : Overføring til FactoNor-format
      *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       040805 AMD  Nytt program
5.41  * R5.41 101106 AMD  Ny versjon av programmet da FactoNor er
5.41  *                   kjøpt opp av Glitnir
6.nu  * R6.30 140814 nho  Endring vedr utvidelse av felter                    *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fraa9l1    if   e           k disk    rename(raa9pfr:raa9l1r)
     frefal1    if   e           k disk    rename(refapfr:refal1r)
     frekul1    if   e           k disk    rename(rekupfr:rekul1r)
     fref2pf    uf a e             disk
     frek2pf    uf a e             disk
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable
      *
     d w_klie          s              3  0
     d w_kkus          s             24
     d w_ksif          s              1  0
      *
      * Definering av variabler i nøkler
      *
     d w_firm          s                   like(rnfirm)
      *
      * Datastruktur for fakturalinjer
      *
     d                 ds
     d d_frec_1                     256
     d  d_klie_1                      3  0 overlay(d_frec_1:1)
     d  d_kund_1                      6  0 overlay(d_frec_1:4)
     d  d_fakt_1                      6  0 overlay(d_frec_1:10)
     d  d_dato_1                      6  0 overlay(d_frec_1:16)
     d  d_forf_1                      6  0 overlay(d_frec_1:22)
     d  d_fil1_1                     12  0 overlay(d_frec_1:28)
     d  d_bilk_1                      2  0 overlay(d_frec_1:40)
     d  d_fbel_1                     11  2 overlay(d_frec_1:42)
     d  d_kidd_1                     16    overlay(d_frec_1:53)
     d  d_fref_1                      6  0 overlay(d_frec_1:69)
     d  d_fil2_1                     10  0 overlay(d_frec_1:75)
     d  d_land_1                      2    overlay(d_frec_1:85)
     d  d_valk_1                      3    overlay(d_frec_1:87)
     d  d_fil3_1                     39    overlay(d_frec_1:90)
      *
      * Datastruktur for kundetabell
      *
     d                 ds
5.41 d d_krec_2                     512
5.41 d  d_post_2                      1    overlay(d_krec_2:1)
     d  d_klie_2                      3  0 overlay(d_krec_2:2)
     d  d_kund_2                      6  0 overlay(d_krec_2:5)
     d  d_knav_2                     30    overlay(d_krec_2:11)
5.41 d  d_alfa_2                     10    overlay(d_krec_2:41)
     d  d_adr1_2                     30    overlay(d_krec_2:51)
     d  d_adr2_2                     30    overlay(d_krec_2:81)
     d  d_sted_2                     30    overlay(d_krec_2:111)
5.41 d  d_land_2                      2    overlay(d_krec_2:141)
5.41 d  d_bgir_2                     11  0 overlay(d_krec_2:143)
     d  d_tlff_2                     13    overlay(d_krec_2:154)
5.41 d  d_ffax_2                     13    overlay(d_krec_2:167)
5.41 d  d_mail_2                     30    overlay(d_krec_2:180)
5.41 d  d_kper_2                     30    overlay(d_krec_2:210)
5.41 d  d_ftak_2                     11  0 overlay(d_krec_2:240)
5.41 d  d_kvat_2                     13  0 overlay(d_krec_2:251)
5.41 d  d_fipe_2                      1    overlay(d_krec_2:264)
      *
      * Datastruktur for kid-feltet
      *
     d                 ds
     d d_kkid                        16
     d  d_kkus                       15    overlay(d_kkid)
     d   d_kkli                       3  0 overlay(d_kkus:1)
     d   d_kkun                       6  0 overlay(d_kkus:4)
     d   d_kbil                       6  0 overlay(d_kkus:10)
     d  d_kmod                        1  0 overlay(d_kkid:16)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Lese Kunde-transaksjoner
      *
     c                   read      refal1
     c                   dow       not %eof
      *
      * Oppdater faktura-tabell
      *
     c                   exsr      xopd_fakt
     c                   read      refal1
      *
     c                   enddo
      *
      * Lese Kunde-register
      *
     c                   read      rekul1
     c                   dow       not %eof
      *
      * Oppdater kunde-tabell
      *
     c                   exsr      xopd_kund
     c                   read      rekul1
      *
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatering av fil for faktura-poster
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xopd_fakt     begsr
      *
      * Fakturaposter
      *
     c                   eval      d_klie_1 = w_klie
     c                   eval      d_kund_1 = rnkund
     c                   eval      d_fakt_1 = rnbiln
     c                   eval      d_fil1_1 = 0
     c                   eval      d_fref_1 = 0
     c                   eval      d_fil2_1 = 0
     c                   eval      d_land_1 = '  '
     c                   eval      d_valk_1 = '   '
     c                   eval      d_fil3_1 = *blank
      *
      * Snu fortegn på beløp
      * dersom negativt
      *
     c                   if        rnbelØ   < 0
     c                   eval      d_fbel_1 = rnbelØ * -1
     c                   eval      d_bilk_1 = 22
     c                   else
     c                   eval      d_fbel_1 = rnbelØ
     c                   eval      d_bilk_1 = 21
     c                   endif
      *
      * Legge ut dato til 8 numerisk
      *
     c                   z-add     0             d_dato_1
     c                   if        rnbdat <> *loval
     c     *ymd          move      rnbdat        d_dato_1
     c                   endif
      *
     c                   z-add     0             d_forf_1
     c                   if        rnfdat <> *loval
     c     *ymd          move      rnfdat        d_forf_1
     c                   endif
      *
      * Legger ut felt for KID
      *
     c                   move      w_klie        d_kkli
     c                   move      rnkund        d_kkun
     c                   move      rnbiln        d_kbil
      *
     c                   move      d_kkus        w_kkus
     c                   eval      w_ksif = 0
      *
      * Beregning av sjekksiffer på kiddfelt (lang versjon)
      *
     c                   call      'AK720R'
     c                   parm                    w_kkus
     c                   parm                    w_ksif
      *
     c                   eval      d_kmod = w_ksif
      *
      * Legger inn kiddnummer
      *
     c                   eval      d_kidd_1 = d_kkid
      *
     c                   eval      refrec   = d_frec_1
      *
     c                   write     ref2pfr
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatering av fil for kunde-tabell
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xopd_kund     begsr
      *
      * Kunderegister
      *
5.41 c                   eval      d_post_2 = 'F'
     c                   eval      d_klie_2 = w_klie
     c                   eval      d_kund_2 = rkkund
     c                   eval      d_knav_2 = rknavn
     c                   eval      d_adr1_2 = rkgate
     c                   eval      d_adr2_2 = rkadr2
     c                   eval      d_sted_2 = rksted
5.41 c                   eval      d_land_2 = '  '
5.41 c                   eval      d_bgir_2 = rkbgir
     c                   eval      d_tlff_2 = rktlfn
5.41 c                   eval      d_ffax_2 = rktlfx
5.41 c                   eval      d_kper_2 = rkpers
     c                   eval      d_ftak_2 = rkftnr
5.41 c                   eval      d_kvat_2 = 0
5.41  *
5.41 c                   movel     rkalfa        d_alfa_2
5.41 c                   movel     rkemal        d_mail_2
5.41  *
5.41 c                   eval      d_ftak_2 = 0
5.41 c                   eval      d_fipe_2 = 'P'
5.41  *
5.41 c                   if        rkftnr <> 0
5.41 c                   eval      d_ftak_2 = rkftnr
5.41 c                   eval      d_fipe_2 = 'F'
5.41 c                   endif
5.41  *
5.41 c                   if        rkpenr <> 0
5.41 c                   eval      d_ftak_2 = rkpenr
5.41 c                   eval      d_fipe_2 = 'P'
5.41 c                   endif
      *
     c                   eval      rekrec   = d_krec_2
      *
     c                   write     rek2pfr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for statustabell factoring
      *
     c     raa9l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for kundetransaksjoner
      *
     c     refal1_key    klist
     c                   kfld                    w_firm
      *
      * Key for kunderegister
      *
     c     rekul1_key    klist
     c                   kfld                    w_firm
      *
      * Legge inn firmanummer
      *
     c                   eval      w_firm = l_firm
      *
      * Hent inn klientnummer
      *
     c     raa9l1_key    chain     raa9l1r
     c                   if        %found
     c                   move      ra9kli        w_klie
     c                   endif
      *
     c                   endsr
      *
