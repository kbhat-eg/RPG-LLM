     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FP100R
      * Beskrivelse . . : Spesialpriser, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       190199 HKO  Nytt program
      *       150503 HKO  Lagt inn kundekategori som nivå
      *       161103 HKO  Lagt inn filter
      *       180904 HKO  Lagt inn avtalenotat
5.70  * PRGR  131108 bof  Utvidet med prisgruppe
      * 6.10  250609 BSA  Oppdatert med sporingsinformasjon
6.20  * 6.20  010714 BSA  Justert vindu størrelsen pga JIT Gui i jacada
6.nu  * R6.30 030614 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.30  *       280617 NHO  Tekst må også hentes for skaffevare
6.31  *       061017 bof  Tester at salgs og kostpris blir for stor i subf
7.xx  * 7.00  240216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
8.01  *PRG2   100622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffprilr    if   e           k disk    rename(fpripfr:fprilrr)
     ffprilu    uf a e           k disk    rename(fpripfr:fprilur)
     ffpril1    if   e           k disk    rename(fpripfr:fpril1r)
     ffpril2    if   e           k disk    rename(fpripfr:fpril2r)
     ffpril3    if   e           k disk    rename(fpripfr:fpril3r)
     ffpril4    if   e           k disk    rename(fpripfr:fpril4r)
     ffpril5    if   e           k disk    rename(fpripfr:fpril5r)
     ffpril6    if   e           k disk    rename(fpripfr:fpril6r)
     ffpril7    if   e           k disk    rename(fpripfr:fpril7r)
     ffpril8    if   e           k disk    rename(fpripfr:fpril8r)
5.70 ffpril9    if   e           k disk    rename(fpripfr:fpril9r)
     fra06pf    if   e           k disk    rename(ra06pfr:ra06pfr)
5.70 fra30lr    if   e           k disk    rename(ra30pfr:ra30lrr)
     fra11pf    if   e           k disk    rename(ra11pfr:ra11pfr)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
6.30 fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fvenhl1    if   e           k disk    rename(venhpfr:venhl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
      *
     ffp100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variable i nøkler
      *
5.70 d fprilr_prgr     s                   like(fpprgr)
     d fprilr_rkat     s                   like(fprkat)
     d fprilr_kkat     s                   like(fpkkat)
     d fprilr_kund     s                   like(fpkund)
     d fprilr_kpro     s                   like(fpkpro)
     d fprilr_vare     s                   like(fpvare)
     d fprilr_enhe     s                   like(fpenhe)
     d fprilr_lety     s                   like(fplety)
5.70 d fprilu_prgr     s                   like(fpprgr)
     d fprilu_rkat     s                   like(fprkat)
     d fprilu_kkat     s                   like(fpkkat)
     d fprilu_kund     s                   like(fpkund)
     d fprilu_kpro     s                   like(fpkpro)
     d fprilu_vare     s                   like(fpvare)
     d fprilu_enhe     s                   like(fpenhe)
     d fprilu_lety     s                   like(fplety)
5.70 d fpril1_prgr     s                   like(fpprgr)
     d fpril1_rkat     s                   like(fprkat)
     d fpril1_kkat     s                   like(fpkkat)
     d fpril1_kund     s                   like(fpkund)
     d fpril1_kpro     s                   like(fpkpro)
     d fpril1_vare     s                   like(fpvare)
     d fpril1_enhe     s                   like(fpenhe)
     d fpril1_lety     s                   like(fplety)
     d fpril2_kkat     s                   like(fpkkat)
     d fpril3_kund     s                   like(fpkund)
     d fpril3_kpro     s                   like(fpkpro)
     d fpril4_kpro     s                   like(fpkpro)
     d fpril5_vare     s                   like(fpvare)
5.70 d fpril5_prgr     s                   like(fpprgr)
     d fpril5_enhe     s                   like(fpenhe)
     d fpril5_lety     s                   like(fplety)
     d fpril6_enhe     s                   like(fpenhe)
     d fpril6_lety     s                   like(fplety)
     d fpril7_lety     s                   like(fplety)
     d fpril8_numm     s                   like(fpnumm)
5.70 d fpril9_rkat     s                   like(fprkat)
     d ra06pf_fkod     s                   like(rafkod)
     d ra11pf_kkod     s                   like(rakkod)
5.70 d ra30lr_dkod     s                   like(redkod)
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d vvarl1_vare     s                   like(vvvare)
6.30 d jvarl1_vare     s                   like(jvvare)
     d venhl1_eenh     s                   like(vaeenh)
     d vltpl1_lety     s                   like(vylety)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_udat          s               d   datfmt(*iso)
     d w_inlr          s              1
      *
     d w_firm          s                   like(fpfirm)
5.70 d w_prgr          s                   like(fpprgr)
     d w_rkat          s                   like(fprkat)
     d w_kkat          s                   like(fpkkat)
     d w_rtxt          s                   like(raftxt)
5.70 d w_dtxt          s                   like(redtxt)
     d w_ktxt          s                   like(raktxt)
     d w_kund          s                   like(fpkund)
     d w_kpro          s                   like(fpkpro)
     d w_vare          s                   like(fpvare)
     d w_enhe          s                   like(fpenhe)
     d w_lety          s                   like(fplety)
     d w_fdat          s                   like(fpfdat)
     d w_tdat          s                   like(fptdat)
     d w_knav          s                   like(rknavn)
     d w_vtxt          s                   like(vvtek1)
     d w_etxt          s                   like(vaetxt)
     d w_ltxt          s                   like(vybesk)
     d w_pnav          s                   like(flnavn)
     d w_sapr          s                   like(fpsapr)
     d w_bupr          s                   like(fpsapr)
     d w_kopr          s                   like(fpkopr)
     d w_inpr          s                   like(fpkopr)
      *
5.70 d s_prgr          s                   like(fpprgr)
     d s_rkat          s                   like(fprkat)
     d s_kkat          s                   like(fpkkat)
     d s_kund          s                   like(fpkund)
     d s_kpro          s                   like(fpkpro)
     d s_vare          s                   like(fpvare)
     d s_enhe          s                   like(fpenhe)
     d s_lety          s                   like(fplety)
      *
      * Definering av konstanter
      *
7.xx d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meleingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Filter
      *
     c                   if        *inka
     c                   exsr      filter
     c                   goto      b2taga
     c                   endif
      *
      * Posisjonering
      *
5.70 c                   if        b2prgr <> *blank or
5.70 c                             b2rkat <> 0 or
     c                             b2kkat <> 0 or
     c                             b2kund <> 0 or
     c                             b2kpro <> 0 or
     c                             b2vare <> *blank or
     c                             b2enhe <> *blank or
     c                             b2lety <> 0 or
     c                             b2numm <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   eval      fpril2_kkat = b1kkat
     c     fpril2_key    setll     fpril2
     c                   when      w_seqe = 'K'
     c                   eval      fpril3_kund = b1kund
     c                   eval      fpril3_kpro = b1kpro
     c     fpril3_key    setll     fpril3
     c                   when      w_seqe = 'P'
     c                   eval      fpril4_kpro = b1kpro
     c     fpril4_key    setll     fpril4
     c                   when      w_seqe = 'V'
     c                   eval      fpril5_vare = b1vare
5.70 c                   eval      fpril5_prgr = b1prgr
     c                   eval      fpril5_enhe = b1enhe
     c                   eval      fpril5_lety = b1lety
     c     fpril5_key    setll     fpril5
     c                   when      w_seqe = 'E'
     c                   eval      fpril6_enhe = b1enhe
     c                   eval      fpril6_lety = b1lety
     c     fpril6_key    setll     fpril6
     c                   when      w_seqe = 'L'
     c                   eval      fpril7_lety = b1lety
     c     fpril7_key    setll     fpril7
     c                   when      w_seqe = 'O'
     c                   eval      fpril8_numm = b1numm
     c     fpril8_key    setll     fpril8
5.70 c                   when      w_seqe = 'R'
5.70 c                   eval      fpril9_rkat = b1rkat
5.70 c     fpril9_key    setll     fpril9
     c                   other
5.70 c                   eval      fpril1_prgr = b1prgr
     c                   eval      fpril1_rkat = b1rkat
     c                   eval      fpril1_kkat = b1kkat
     c                   eval      fpril1_kund = b1kund
     c                   eval      fpril1_kpro = b1kpro
     c                   eval      fpril1_vare = b1vare
     c                   eval      fpril1_enhe = b1enhe
     c                   eval      fpril1_lety = b1lety
     c     fpril1_key    setll     fpril1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på rabattkategori
      *
5.70 c                   if        b2prgr <> *blank
     c                   eval      w_seqe = *blank
5.70 c                   eval      fpril1_prgr = b2prgr
     c                   eval      fpril1_rkat = b2rkat
     c                   eval      fpril1_kkat = b2kkat
     c                   eval      fpril1_kund = b2kund
     c                   eval      fpril1_kpro = b2kpro
     c                   eval      fpril1_vare = b2vare
     c                   eval      fpril1_enhe = b2enhe
     c                   eval      fpril1_lety = b2lety
     c     fpril1_key    setll     fpril1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på kundekategori
      *
     c                   if        b2kkat <> 0
     c                   eval      w_seqe = 'A'
     c                   eval      fpril2_kkat = b2kkat
     c     fpril2_key    setll     fpril2
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på kunde
      *
     c                   if        b2kund <> 0
     c                   eval      w_seqe = 'K'
     c                   eval      fpril3_kund = b2kund
     c                   eval      fpril3_kpro = b2kpro
     c     fpril3_key    setll     fpril3
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på kundeprosjekt
      *
     c                   if        b2kpro <> 0
     c                   eval      w_seqe = 'P'
     c                   eval      fpril4_kpro = b2kpro
     c     fpril4_key    setll     fpril4
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på vare
      *
     c                   if        b2vare <> *blank
     c                   eval      w_seqe = 'V'
     c                   eval      fpril5_vare = b2vare
5.70 c                   eval      fpril5_prgr = b2prgr
     c                   eval      fpril5_enhe = b2enhe
     c                   eval      fpril5_lety = b2lety
     c     fpril5_key    setll     fpril5
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på enhet
      *
     c                   if        b2enhe <> *blank
     c                   eval      w_seqe = 'E'
     c                   eval      fpril6_enhe = b2enhe
     c                   eval      fpril6_lety = b2lety
     c     fpril6_key    setll     fpril6
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på leveringstype
      *
     c                   if        b2lety <> 0
     c                   eval      w_seqe = 'L'
     c                   eval      fpril7_lety = b2lety
     c     fpril7_key    setll     fpril7
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på ordrenummer
      *
     c                   if        b2numm <> 0
     c                   eval      w_seqe = 'O'
     c                   eval      fpril8_numm = b2numm
     c     fpril8_key    setll     fpril8
     c                   goto      end_pos
     c                   endif
5.70  *
5.70  * Posisjoner startverdi på rabatt-kategori
5.70  *
5.70 c                   if        b2rkat <> 0
5.70 c                   eval      w_seqe = 'R'
5.70 c                   eval      fpril9_rkat = b2rkat
5.70 c     fpril9_key    setll     fpril9
5.70 c                   goto      end_pos
5.70 c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
5.70 c                   eval      b2prgr = *blank
     c                   eval      b2rkat = 0
     c                   eval      b2kkat = 0
     c                   eval      b2kund = 0
     c                   eval      b2kpro = 0
     c                   eval      b2vare = *blank
     c                   eval      b2enhe = *blank
     c                   eval      b2lety = 0
     c                   eval      b2numm = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
5.70 c                   eval      c1prgr = b1prgr
     c                   eval      c1rkat = b1rkat
     c                   eval      c1kkat = b1kkat
     c                   eval      c1kund = b1kund
     c                   eval      c1kpro = b1kpro
     c                   eval      c1vare = b1vare
     c                   eval      c1enhe = b1enhe
     c                   eval      c1lety = b1lety
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
5.70 c                   eval      k1prgr = b1prgr
     c                   eval      k1rkat = b1rkat
     c                   eval      k1kkat = b1kkat
     c                   eval      k1kund = b1kund
     c                   eval      k1kpro = b1kpro
     c                   eval      k1vare = b1vare
     c                   eval      k1enhe = b1enhe
     c                   eval      k1lety = b1lety
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
5.70 c                   eval      d1prgr = b1prgr
     c                   eval      d1rkat = b1rkat
     c                   eval      d1kkat = b1kkat
     c                   eval      d1kund = b1kund
     c                   eval      d1kpro = b1kpro
     c                   eval      d1vare = b1vare
     c                   eval      d1enhe = b1enhe
     c                   eval      d1lety = b1lety
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
5.70 c                   eval      c1prgr = b1prgr
     c                   eval      c1rkat = b1rkat
     c                   eval      c1kkat = b1kkat
     c                   eval      c1kund = b1kund
     c                   eval      c1kpro = b1kpro
     c                   eval      c1vare = b1vare
     c                   eval      c1enhe = b1enhe
     c                   eval      c1lety = b1lety
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Avtalenotat
      *
     c                   if        b1valg = 7
     c                   eval      w_vare = b1vare
     c                   call      'FP150R'
     c                   parm                    w_firm
     c                   parm                    b1kund
     c                   parm                    b1kpro
     c                   parm                    w_vare
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
5.70 c                   eval      c1prgr = b2prgr
     c                   eval      c1rkat = b2rkat
     c                   eval      c1kkat = b2kkat
     c                   eval      c1kund = b2kund
     c                   eval      c1kpro = b2kpro
     c                   eval      c1enhe = b2enhe
     c                   eval      c1vare = b2vare
     c                   eval      c1lety = b2lety
      *
     c                   exsr      hent_info
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      c1taga
     c                   endsl
      *
5.70 c                   if        c1prgr = *blank and
5.70 c                             c1rkat = 0 and
     c                             c1kkat = 0 and
     c                             c1kund = 0 and
     c                             c1kpro = 0 and
     c                             c1enhe = *blank and
     c                             c1vare = *blank and
     c                             c1lety = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Sjekker input
      *
5.70 c                   eval      w_prgr = c1prgr
     c                   eval      w_rkat = c1rkat
     c                   eval      w_kkat = c1kkat
     c                   eval      w_kund = c1kund
     c                   eval      w_kpro = c1kpro
     c                   eval      w_vare = c1vare
     c                   eval      w_enhe = c1enhe
     c                   eval      w_lety = c1lety
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      c1taga
     c                   endif
      *
      * Sender opp informasjonsbildet dersom rad finnes fra før
      *
5.70 c                   eval      fprilr_prgr = c1prgr
     c                   eval      fprilr_rkat = c1rkat
     c                   eval      fprilr_kkat = c1kkat
     c                   eval      fprilr_kund = c1kund
     c                   eval      fprilr_kpro = c1kpro
     c                   eval      fprilr_vare = c1vare
     c                   eval      fprilr_enhe = c1enhe
     c                   eval      fprilr_lety = c1lety
     c     fprilr_key    chain     fprilr                             90
     c                   if        *in90 = *off
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
5.70 c                   eval      c1prgr = *blank
     c                   eval      c1rkat = 0
     c                   eval      c1kkat = 0
     c                   eval      c1kund = 0
     c                   eval      c1kpro = 0
     c                   eval      c1enhe = *blank
     c                   eval      c1vare = *blank
     c                   eval      c1lety = 0
      *
     c                   eval      c1rtxt = *blank
     c                   eval      c1ktxt = *blank
     c                   eval      c1knav = *blank
     c                   eval      c1pnav = *blank
     c                   eval      c1vtxt = *blank
     c                   eval      c1etxt = *blank
     c                   eval      c1ltxt = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
5.70 c                   eval      b2prgr = *blank
     c                   eval      b2rkat = 0
     c                   eval      b2kkat = 0
     c                   eval      b2kund = 0
     c                   eval      b2kpro = 0
     c                   eval      b2enhe = *blank
     c                   eval      b2vare = *blank
     c                   eval      b2lety = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * C2BLD Behandle bilde for ny - endring - vise
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
5.70 c                   eval      fprilr_prgr = c1prgr
     c                   eval      fprilr_rkat = c1rkat
     c                   eval      fprilr_kkat = c1kkat
     c                   eval      fprilr_kund = c1kund
     c                   eval      fprilr_kpro = c1kpro
     c                   eval      fprilr_vare = c1vare
     c                   eval      fprilr_enhe = c1enhe
     c                   eval      fprilr_lety = c1lety
     c     fprilr_key    chain     fprilr                             90
      *
     c                   if        *in90 = *off
      *
     c                   eval      c2sapr = fpsapr
     c                   eval      c2rab1 = fprab1
     c                   eval      c2rab2 = fprab2
     c                   eval      c2kopr = fpkopr
     c                   eval      c2fdat = 0
     c                   eval      c2tdat = 0
     c                   eval      c2numm = fpnumm
     c                   eval      c2eusr = fpeusr
      *
     c                   if        fpfdat <> *loval
     c     *dmy          move      fpfdat        c2fdat
     c                   endif
      *
     c                   if        fptdat <> *loval
     c     *dmy          move      fptdat        c2tdat
     c                   endif
      *
     c     *dmy          move      fpedat        c2edat
      *
     c                   else
      *
     c                   eval      c2sapr = 0
     c                   eval      c2rab1 = 0
     c                   eval      c2rab2 = 0
     c                   eval      c2kopr = 0
     c                   eval      c2fdat = 0
     c                   eval      c2tdat = 0
     c                   eval      c2numm = 0
     c                   eval      c2edat = 0
     c                   eval      c2eusr = *blank
      *
     c                   endif
      *
     c                   eval      c2dekn = 0
      *
     c                   if        b1valg = 3
5.70 c                   eval      c1prgr = k1prgr
     c                   eval      c1rkat = k1rkat
     c                   eval      c1kkat = k1kkat
     c                   eval      c1kund = k1kund
     c                   eval      c1kpro = k1kpro
     c                   eval      c1vare = k1vare
     c                   eval      c1enhe = k1enhe
     c                   eval      c1lety = k1lety
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
      * Henter beskrivelse av nivå
      *
     c                   exsr      hent_info
      *
      * Kalkuler DG
      *
     c                   exsr      kalk_dg
      *
      * Behandler bildet
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2sapr = 0 and
     c                             c2rab1 = 0
     c                   eval      *in31 = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        c2fdat <> 0
     c     *dmy          test(d)                 c2fdat                 35
     c                   if        *in35 = *on
     c                   goto      c2tagb
     c                   endif
     c                   endif
      *
     c                   if        c2tdat <> 0
     c     *dmy          test(d)                 c2tdat                 36
     c                   if        *in36 = *on
     c                   goto      c2tagb
     c                   endif
     c                   endif
      *
     c                   if        c2tdat <> 0 and
     c                             c2fdat = 0
     c     *dmy          move      w_udat        c2fdat
     c                   endif
      *
     c                   if        c2fdat <> 0
     c     *dmy          move      c2fdat        w_fdat
     c                   if        c2tdat <> 0
     c     *dmy          move      c2tdat        w_tdat
     c                   else
     c                   eval      w_tdat = *loval
     c                   endif
     c                   if        w_fdat > w_tdat
     c                   eval      *in37 = *on
     c                   goto      c2tagb
     c                   endif
     c                   endif
      *
      * Kalkuler DG
      *
     c                   exsr      kalk_dg
      *
      * Oppdatering av rad
      *
     c                   clear                   fprilur
      *
5.70 c                   eval      fprilu_prgr = c1prgr
     c                   eval      fprilu_rkat = c1rkat
     c                   eval      fprilu_kkat = c1kkat
     c                   eval      fprilu_kund = c1kund
     c                   eval      fprilu_kpro = c1kpro
     c                   eval      fprilu_vare = c1vare
     c                   eval      fprilu_enhe = c1enhe
     c                   eval      fprilu_lety = c1lety
     c     fprilu_key    chain     fprilur                            90
      *
     c                   eval      fpsapr = c2sapr
     c                   eval      fprab1 = c2rab1
     c                   eval      fprab2 = c2rab2
     c                   eval      fpkopr = c2kopr
     c                   eval      fpfdat = *loval
     c                   eval      fptdat = *loval
     c                   eval      fpnumm = c2numm
      *
     c                   if        c2fdat <> 0
     c     *dmy          move      c2fdat        fpfdat
     c                   endif
      *
     c                   if        c2tdat <> 0
     c     *dmy          move      c2tdat        fptdat
     c                   endif
      *
     c                   eval      fpeusr = l_user
      *
     c                   time                    fpetim
     c                   time                    fpedat
      *
     c                   if        *in90 = *off
6.10 c                   time                    fpedat
6.10 c                   time                    fpetim
6.10 c                   eval      fpeusr = l_user
     c                   update    fprilur
     c                   else
     c                   eval      fpfirm = w_firm
5.70 c                   eval      fpprgr = c1prgr
     c                   eval      fprkat = c1rkat
     c                   eval      fpkkat = c1kkat
     c                   eval      fpkund = c1kund
     c                   eval      fpkpro = c1kpro
     c                   eval      fpvare = c1vare
     c                   eval      fpenhe = c1enhe
     c                   eval      fplety = c1lety
6.10 c                   time                    fpodat
6.10 c                   time                    fpotim
6.10 c                   eval      fpousr = l_user
6.10 c                   time                    fpedat
6.10 c                   time                    fpetim
6.10 c                   eval      fpeusr = l_user
     c                   write     fprilur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1numm = c2numm
     c                   eval      b1sapr = c2sapr
     c                   eval      b1rab1 = c2rab1
     c                   eval      b1kopr = c2kopr
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
5.70 c                   eval      fprilr_prgr = d1prgr
     c                   eval      fprilr_rkat = d1rkat
     c                   eval      fprilr_kkat = d1kkat
     c                   eval      fprilr_kund = d1kund
     c                   eval      fprilr_kpro = d1kpro
     c                   eval      fprilr_vare = d1vare
     c                   eval      fprilr_enhe = d1enhe
     c                   eval      fprilr_lety = d1lety
     c     fprilr_key    chain     fprilr                             90
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
5.70 c                   eval      fprilu_prgr = d1prgr
     c                   eval      fprilu_rkat = d1rkat
     c                   eval      fprilu_kkat = d1kkat
     c                   eval      fprilu_kund = d1kund
     c                   eval      fprilu_kpro = d1kpro
     c                   eval      fprilu_vare = d1vare
     c                   eval      fprilu_enhe = d1enhe
     c                   eval      fprilu_lety = d1lety
     c     fprilu_key    chain     fprilur                            90
     c                   delete    fprilur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
5.70 c                   eval      s_prgr = k1prgr
     c                   eval      s_rkat = k1rkat
     c                   eval      s_kkat = k1kkat
     c                   eval      s_kund = k1kund
     c                   eval      s_kpro = k1kpro
     c                   eval      s_vare = k1vare
     c                   eval      s_enhe = k1enhe
     c                   eval      s_lety = k1lety
      *
      * Fyller kopierings-bildet
      *
5.70 c                   eval      fprilr_prgr = k1prgr
     c                   eval      fprilr_rkat = k1rkat
     c                   eval      fprilr_kkat = k1kkat
     c                   eval      fprilr_kund = k1kund
     c                   eval      fprilr_kpro = k1kpro
     c                   eval      fprilr_vare = k1vare
     c                   eval      fprilr_enhe = k1enhe
     c                   eval      fprilr_lety = k1lety
     c     fprilr_key    chain     fprilr                             90
      *
     c     k1tagb        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd = *on
     c                   exsr      spØrring
     c                   goto      k1tagb
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
5.70 c                   eval      fprilr_prgr = k1prgr
     c                   eval      fprilr_rkat = k1rkat
     c                   eval      fprilr_kkat = k1kkat
     c                   eval      fprilr_kund = k1kund
     c                   eval      fprilr_kpro = k1kpro
     c                   eval      fprilr_vare = k1vare
     c                   eval      fprilr_enhe = k1enhe
     c                   eval      fprilr_lety = k1lety
     c     fprilr_key    chain     fprilr                             90
     c                   if        *in90 = *off
     c                   eval      *in32 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Sjekker input
      *
5.70 c                   eval      w_prgr = k1prgr
     c                   eval      w_rkat = k1rkat
     c                   eval      w_kkat = k1kkat
     c                   eval      w_kund = k1kund
     c                   eval      w_kpro = k1kpro
     c                   eval      w_vare = k1vare
     c                   eval      w_enhe = k1enhe
     c                   eval      w_lety = k1lety
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
5.70 c                   eval      c1prgr = s_prgr
     c                   eval      c1rkat = s_rkat
     c                   eval      c1kkat = s_kkat
     c                   eval      c1kund = s_kund
     c                   eval      c1kpro = s_kpro
     c                   eval      c1vare = s_vare
     c                   eval      c1enhe = s_enhe
     c                   eval      c1lety = s_lety
      *
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Fyll opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   read      fpril2
     c                   when      w_seqe = 'K'
     c                   read      fpril3
     c                   when      w_seqe = 'P'
     c                   read      fpril4
     c                   when      w_seqe = 'V'
     c                   read      fpril5
     c                   when      w_seqe = 'E'
     c                   read      fpril6
     c                   when      w_seqe = 'L'
     c                   read      fpril7
     c                   when      w_seqe = 'O'
     c                   read      fpril8
5.70 c                   when      w_seqe = 'R'
5.70 c                   read      fpril9
     c                   other
     c                   read      fpril1
     c                   endsl
      *
     c                   if        %eof or
     c                             fpfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        f1kund <> 0
     c                   if        fprkat <> 0 and
     c                             fprkat <> f1rkat or
     c                             fpkkat <> 0 and
     c                             fpkkat <> f1kkat or
     c                             fpkund <> 0 and
     c                             fpkund <> f1kund
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        f1kpro <> 0 and
     c                             fpkpro <> 0 and
     c                             fpkpro <> f1kpro
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   eval      fpril2_kkat = fpkkat
     c                   when      w_seqe = 'K'
     c                   eval      fpril3_kund = fpkund
     c                   eval      fpril3_kpro = fpkpro
     c                   when      w_seqe = 'P'
     c                   eval      fpril4_kpro = fpkpro
     c                   when      w_seqe = 'V'
     c                   eval      fpril5_vare = fpvare
5.70 c                   eval      fpril5_prgr = fpprgr
     c                   eval      fpril5_enhe = fpenhe
     c                   eval      fpril5_lety = fplety
     c                   when      w_seqe = 'E'
     c                   eval      fpril6_enhe = fpenhe
     c                   eval      fpril6_lety = fplety
     c                   when      w_seqe = 'L'
     c                   eval      fpril7_lety = fplety
     c                   when      w_seqe = 'O'
     c                   eval      fpril8_numm = fpnumm
5.70 c                   when      w_seqe = 'R'
5.70 c                   eval      fpril9_rkat = fprkat
     c                   other
     c                   eval      fpril1_rkat = fprkat
     c                   eval      fpril1_kkat = fpkkat
     c                   eval      fpril1_kund = fpkund
     c                   eval      fpril1_kpro = fpkpro
     c                   eval      fpril1_vare = fpvare
     c                   eval      fpril1_enhe = fpenhe
     c                   eval      fpril1_lety = fplety
     c                   endsl
      *
5.70 c                   eval      b1prgr = fpprgr
     c                   eval      b1rkat = fprkat
     c                   eval      b1kkat = fpkkat
     c                   eval      b1kund = fpkund
     c                   eval      b1kpro = fpkpro
     c                   eval      b1vare = fpvare
     c                   eval      b1tek1 = *blank
     c                   eval      b1enhe = fpenhe
     c                   eval      b1lety = fplety
     c                   eval      b1numm = fpnumm
6.31 c                   if        fpsapr > 999999,99
6.31 c                   eval      b1sapr = 999999,99
6.31 c                   else
     c                   eval      b1sapr = fpsapr
6.31 c                   endif
     c                   eval      b1rab1 = fprab1
6.31 c                   if        fpkopr > 99999,99
6.31 c                   eval      b1kopr = 99999,99
6.31 c                   else
     c                   eval      b1kopr = fpkopr
6.31 c                   endif
      *
     c                   eval      vvarl1_vare = fpvare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      b1tek1 = vvtek1
6.30 c                   else
6.30 c                   eval      jvarl1_vare = fpvare
6.30 c     jvarl1_key    chain     jvarl1
6.30 c                   if        %found
6.30 c                   eval      b1tek1 = jvtek1
6.30 c                   endif
6.30 c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'A'
     c     fpril2_key    setgt     fpril2
     c                   readp     fpril2
     c                   when      w_seqe = 'K'
     c     fpril3_key    setgt     fpril3
     c                   readp     fpril3
     c                   when      w_seqe = 'P'
     c     fpril4_key    setgt     fpril4
     c                   readp     fpril4
     c                   when      w_seqe = 'V'
     c     fpril5_key    setgt     fpril5
     c                   readp     fpril5
     c                   when      w_seqe = 'E'
     c     fpril6_key    setgt     fpril6
     c                   readp     fpril6
     c                   when      w_seqe = 'L'
     c     fpril7_key    setgt     fpril7
     c                   readp     fpril7
     c                   when      w_seqe = 'O'
     c     fpril8_key    setgt     fpril8
     c                   readp     fpril8
5.70 c                   when      w_seqe = 'R'
5.70 c     fpril9_key    setgt     fpril9
5.70 c                   readp     fpril9
     c                   other
     c     fpril1_key    setgt     fpril1
     c                   readp     fpril1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   readp     fpril2
     c                   when      w_seqe = 'K'
     c                   readp     fpril3
     c                   when      w_seqe = 'P'
     c                   readp     fpril4
     c                   when      w_seqe = 'V'
     c                   readp     fpril5
     c                   when      w_seqe = 'E'
     c                   readp     fpril6
     c                   when      w_seqe = 'L'
     c                   readp     fpril7
     c                   when      w_seqe = 'O'
     c                   readp     fpril8
5.70 c                   when      w_seqe = 'R'
5.70 c                   readp     fpril9
     c                   other
     c                   readp     fpril1
     c                   endsl
      *
     c                   if        %eof or
     c                             fpfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   if        f1kund <> 0
     c                   if        fprkat <> 0 and
     c                             fprkat <> f1rkat or
     c                             fpkkat <> 0 and
     c                             fpkkat <> f1kkat or
     c                             fpkund <> 0 and
     c                             fpkund <> f1kund
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        f1kpro <> 0 and
     c                             fpkpro <> 0 and
     c                             fpkpro <> f1kpro
     c                   iter
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   eval      fpril2_kkat = fpkkat
     c                   when      w_seqe = 'K'
     c                   eval      fpril3_kund = fpkund
     c                   eval      fpril3_kpro = fpkpro
     c                   when      w_seqe = 'P'
     c                   eval      fpril4_kpro = fpkpro
     c                   when      w_seqe = 'V'
     c                   eval      fpril5_vare = fpvare
5.70 c                   eval      fpril5_prgr = fpprgr
     c                   eval      fpril5_enhe = fpenhe
     c                   eval      fpril5_lety = fplety
     c                   when      w_seqe = 'E'
     c                   eval      fpril6_enhe = fpenhe
     c                   eval      fpril6_lety = fplety
     c                   when      w_seqe = 'L'
     c                   eval      fpril7_lety = fplety
     c                   when      w_seqe = 'O'
     c                   eval      fpril8_numm = fpnumm
5.70 c                   when      w_seqe = 'R'
5.70 c                   eval      fpril9_rkat = fprkat
     c                   other
5.70 c                   eval      fpril1_prgr = fpprgr
5.70 c                   eval      fpril1_rkat = fprkat
     c                   eval      fpril1_kkat = fpkkat
     c                   eval      fpril1_kund = fpkund
     c                   eval      fpril1_kpro = fpkpro
     c                   eval      fpril1_vare = fpvare
     c                   eval      fpril1_enhe = fpenhe
     c                   eval      fpril1_lety = fplety
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'A'
     c     fpril2_key    setll     fpril2
     c                   when      w_seqe = 'K'
     c     fpril3_key    setll     fpril3
     c                   when      w_seqe = 'P'
     c     fpril4_key    setll     fpril4
     c                   when      w_seqe = 'V'
     c     fpril5_key    setll     fpril5
     c                   when      w_seqe = 'E'
     c     fpril6_key    setll     fpril6
     c                   when      w_seqe = 'L'
     c     fpril7_key    setll     fpril7
     c                   when      w_seqe = 'O'
     c     fpril8_key    setll     fpril8
5.70 c                   when      w_seqe = 'R'
5.70 c     fpril9_key    setll     fpril9
     c                   other
     c     fpril1_key    setll     fpril1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
5.70  *
5.70  * Prisgruppe
5.70  *
5.70 c                   if        w_afld = 'B2PRGR'
5.70 c                   call      'RA530R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    b2prgr
5.70 c                   parm                    w_dtxt
5.70 c                   leavesr
5.70 c                   endif
5.70  *
5.70 c                   if        w_afld = 'C1PRGR'
5.70 c                   call      'RA530R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    c1prgr
5.70 c                   parm                    w_dtxt
5.70 c                   leavesr
5.70 c                   endif
      *
      * Rabattkategori
      *
     c                   if        w_afld = 'B2RKAT'
     c                   call      'RA506R'
     c                   parm                    w_firm
     c                   parm                    b2rkat
     c                   parm                    w_rtxt
     c                   goto      end_spØrr
     c                   endif
5.70  *
5.70 c                   if        w_afld = 'C1RPGR'
5.70 c                   call      'RA530R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    c1prgr
5.70 c                   parm                    c1ptxt
5.70 c                   leavesr
5.70 c                   endif
      *
     c                   if        w_afld = 'C1RKAT'
     c                   call      'RA506R'
     c                   parm                    w_firm
     c                   parm                    c1rkat
     c                   parm                    c1rtxt
     c                   goto      end_spØrr
     c                   endif
5.70  *
5.70 c                   if        w_afld = 'K1PRGR'
5.70 c                   call      'RA530R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    k1prgr
5.70 c                   parm                    w_dtxt
5.70 c                   leavesr
5.70 c                   endif
      *
     c                   if        w_afld = 'K1RKAT'
     c                   call      'RA506R'
     c                   parm                    w_firm
     c                   parm                    k1rkat
     c                   parm                    w_rtxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Kundekategori
      *
     c                   if        w_afld = 'B2KKAT'
     c                   call      'RA511R'
     c                   parm                    w_firm
     c                   parm                    b2kkat
     c                   parm                    w_ktxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1KKAT'
     c                   call      'RA511R'
     c                   parm                    w_firm
     c                   parm                    c1kkat
     c                   parm                    c1ktxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1KKAT'
     c                   call      'RA511R'
     c                   parm                    w_firm
     c                   parm                    k1kkat
     c                   parm                    w_ktxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Kunde
      *
     c                   if        w_afld = 'B2KUND'
     c                   eval      b2kpro = 0
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    b2kund
     c                   parm                    w_knav
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1KUND'
     c                   eval      c1kpro = 0
     c                   eval      c1pnav = *blank
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    c1kund
     c                   parm                    c1knav
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1KUND'
     c                   eval      k1kpro = 0
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    k1kund
     c                   parm                    w_knav
     c                   goto      end_spØrr
     c                   endif
      *
      * Kundeprosjekt
      *
     c                   if        w_afld = 'B2KPRO'
     c                   call      'FL500R'
     c                   parm                    w_firm
     c                   parm                    b2kund
     c                   parm                    b2kpro
     c                   parm                    w_knav
     c                   parm                    w_pnav
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1KPRO'
     c                   call      'FL500R'
     c                   parm                    w_firm
     c                   parm                    c1kund
     c                   parm                    c1kpro
     c                   parm                    c1knav
     c                   parm                    c1pnav
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1KPRO'
     c                   call      'FL500R'
     c                   parm                    w_firm
     c                   parm                    k1kund
     c                   parm                    k1kpro
     c                   parm                    w_knav
     c                   parm                    w_pnav
     c                   goto      end_spØrr
     c                   endif
      *
      * Varenummer
      *
     c                   if        w_afld = 'B2VARE'
     c                   eval      w_vare = b2vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   eval      b2vare = w_vare
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1VARE'
     c                   eval      w_vare = c1vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    c1vtxt
     c                   eval      c1vare = w_vare
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1VARE'
     c                   eval      w_vare = k1vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   eval      k1vare = w_vare
     c                   goto      end_spØrr
     c                   endif
      *
      * Enhet
      *
     c                   if        w_afld = 'B2ENHE'
     c                   if        b2vare <> *blank
     c                   eval      w_vare = b2vare
     c                   call      'VE550R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    b2enhe
     c                   parm                    w_etxt
     c                   else
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    b2enhe
     c                   parm                    w_etxt
     c                   endif
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1ENHE'
     c                   if        c1vare <> *blank
     c                   eval      w_vare = c1vare
     c                   call      'VE550R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    c1enhe
     c                   parm                    c1etxt
     c                   else
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    c1enhe
     c                   parm                    c1etxt
     c                   endif
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1ENHE'
     c                   if        k1vare <> *blank
     c                   eval      w_vare = k1vare
     c                   call      'VE550R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    k1enhe
     c                   parm                    w_etxt
     c                   else
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    k1enhe
     c                   parm                    w_etxt
     c                   endif
     c                   goto      end_spØrr
     c                   endif
      *
      * Leveringstype
      *
     c                   if        w_afld = 'B2LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    b2lety
     c                   parm                    w_ltxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    c1lety
     c                   parm                    c1ltxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    k1lety
     c                   parm                    w_ltxt
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Filter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     filter        begsr
      *
     c     f1tagb        tag
      *
     c                   exfmt     f1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
      *
     c                   if        w_afld = 'F1KUND'
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    f1kund
     c                   parm                    f1knav
     c                   goto      f1tagb
     c                   endif
      *
     c                   if        w_afld = 'F1KPRO'
     c                   call      'FL500R'
     c                   parm                    w_firm
     c                   parm                    f1kund
     c                   parm                    f1kpro
     c                   parm                    f1knav
     c                   parm                    f1pnav
     c                   goto      f1tagb
     c                   endif
      *
     c                   endif
      *
      * Validering
      *
     c                   if        f1kund <> 0
     c                   eval      rkunl1_kund = f1kund
     c     rkunl1_key    chain     rkunl1                             31
     c                   if        *in31 = *on
     c                   goto      f1tagb
     c                   endif
     c                   eval      f1knav = rknavn
     c                   endif
      *
     c                   if        f1kund = 0 and
     c                             f1kpro <> 0
     c                   eval      *in32 = *on
     c                   goto      f1tagb
     c                   endif
      *
     c                   if        f1kpro <> 0
     c                   eval      fkprl1_kund = f1kund
     c                   eval      fkprl1_kpro = f1kpro
     c     fkprl1_key    chain     fkprl1                             33
     c                   if        *in33 = *on
     c                   goto      f1tagb
     c                   endif
     c                   eval      f1pnav = flnavn
     c                   endif
      *
      * Henter verdier til liste-bildet
      *
     c                   eval      b2kunf = f1kund
     c                   eval      b2kprf = f1kpro
      *
      * Nullstiller og avbryter dersom kunde ikke er utfylt
      *
     c                   if        f1kund = 0
     c                   eval      f1rkat = 0
     c                   eval      f1kkat = 0
     c                   eval      f1kpro = 0
     c                   eval      f1knav = *blank
     c                   eval      f1pnav = *blank
     c                   goto      end_filter
     c                   endif
      *
      * Finner rabattkategori/kundekategori for kunde
      *
     c                   eval      rkunl1_kund = f1kund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      f1rkat = rkrabk
     c                   eval      f1kkat = rkkatg
     c                   endif
      *
      * Henter overstyringer fra kundeprosjekt
      *
     c                   if        f1kpro <> 0
      *
     c                   eval      fkprl1_kund = f1kund
     c                   eval      fkprl1_kpro = f1kpro
     c     fkprl1_key    chain     fkprl1
     c                   if        %found
      *
     c                   if        florka <> 0
     c                   eval      f1rkat = florka
     c                   endif
      *
     c                   if        flokun <> 0
     c                   eval      f1kund = flokun
     c                   eval      f1kpro = 0
     c                   endif
      *
     c                   if        flokpr <> 0
     c                   eval      f1kpro = flokpr
     c                   endif
      *
     c                   if        flokun <> 0
     c                   eval      rkunl1_kund = flokun
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      f1rkat = rkrabk
     c                   eval      f1kkat = rkkatg
     c                   endif
     c                   endif
      *
     c                   endif
     c                   endif
      *
      * Blanker subfile, og fyller opp i.h.t. filter
      *
     c     end_filter    tag
      *
     c                   eval      *in22 = *off
      *
     c                   eval      w_seqe = *blank
      *
     c                   eval      fpril1_rkat = 0
     c                   eval      fpril1_kkat = 0
     c                   eval      fpril1_kund = 0
     c                   eval      fpril1_kpro = 0
     c     fpril1_key    setll     fpril1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker inntastede verdier, og henter beskrivelser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
     c                   eval      w_rtxt = *blank
     c                   eval      w_ktxt = *blank
     c                   eval      w_knav = *blank
     c                   eval      w_pnav = *blank
     c                   eval      w_vtxt = *blank
     c                   eval      w_ltxt = *blank
      *
      * Sjekk om komplett spesialpris-nivå
      *
     c                   if        w_rkat = 0 and
     c                             w_kkat = 0 and
     c                             w_kund = 0 or
     c                             w_vare = *blank or
     c                             w_enhe = *blank
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   if        w_rkat <> 0 and
     c                             w_kkat <> 0 or
     c                             w_rkat <> 0 and
     c                             w_kund <> 0 or
     c                             w_kkat <> 0 and
     c                             w_kund <> 0
     c                   eval      *in34  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
5.70  *
5.70  * Prisgruppe
5.70  *
5.70 c                   if        w_prgr <> *blank
5.70 c                   eval      ra30lr_dkod = w_prgr
5.70 c     ra30lr_key    chain     ra30lr                             49
5.70 c                   if        *in49 = *on
5.70 c                   eval      b_feil = *on
5.70 c                   leavesr
5.70 c                   endif
5.70 c                   eval      w_dtxt = redtxt
5.70 c                   endif
      *
      * Rabattkategori
      *
     c                   if        w_rkat <> 0
     c                   eval      ra06pf_fkod = w_rkat
     c     ra06pf_key    chain     ra06pf                             35
     c                   if        *in35 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      w_rtxt = raftxt
     c                   endif
      *
      * Kundekategori
      *
     c                   if        w_kkat <> 0
     c                   eval      ra11pf_kkod = w_kkat
     c     ra11pf_key    chain     ra11pf                             41
     c                   if        *in41 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      w_ktxt = raktxt
     c                   endif
      *
      * Kunde
      *
     c                   if        w_kund <> 0
     c                   eval      rkunl1_kund = w_kund
     c     rkunl1_key    chain     rkunl1                             36
     c                   if        *in36 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      w_knav = rknavn
     c                   endif
      *
      * Kundeprosjekt
      *
     c                   if        w_kpro <> 0
     c                   eval      fkprl1_kund = w_kund
     c                   eval      fkprl1_kpro = w_kpro
     c     fkprl1_key    chain     fkprl1                             37
     c                   if        *in37 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      w_pnav = flnavn
     c                   endif
      *
      * Varenummer
      *
     c                   if        w_vare <> *blank
     c                   eval      vvarl1_vare = w_vare
     c     vvarl1_key    chain     vvarl1                             38
     c                   if        *in38 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      w_vtxt = vvtek1
     c                   endif
      *
      * Enhet
      *
     c                   if        w_enhe <> *blank
     c                   eval      venhl1_eenh = w_enhe
     c     venhl1_key    chain     venhl1                             39
     c                   if        *in39 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      w_etxt = vaetxt
     c                   endif
      *
      * Leveringstype
      *
     c                   if        w_lety <> 0
     c                   eval      vltpl1_lety = w_lety
     c     vltpl1_key    chain     vltpl1                             40
     c                   if        *in40 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      w_ltxt = vybesk
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter beskrivelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_info     begsr
      *
     c                   eval      c1rtxt = *blank
     c                   eval      c1ktxt = *blank
     c                   eval      c1knav = *blank
     c                   eval      c1pnav = *blank
     c                   eval      c1vtxt = *blank
     c                   eval      c1etxt = *blank
     c                   eval      c1ltxt = *blank
     c                   eval      c1ptxt = *blank
5.70  *
5.70  * Prisgruppe
5.70  *
5.70 c                   if        c1prgr <> *blank
5.70 c                   eval      ra30lr_dkod = c1prgr
5.70 c     ra30lr_key    chain     ra30lr
5.70 c                   if        %found
5.70 c                   eval      c1ptxt = redtxt
5.70 c                   endif
5.70 c                   endif
      *
      * Rabattkategori
      *
     c                   if        c1rkat <> 0
     c                   eval      ra06pf_fkod = c1rkat
     c     ra06pf_key    chain     ra06pf                             91
     c                   if        *in91 = *off
     c                   eval      c1rtxt = raftxt
     c                   endif
     c                   endif
      *
      * Kundekategori
      *
     c                   if        c1kkat <> 0
     c                   eval      ra11pf_kkod = c1kkat
     c     ra11pf_key    chain     ra11pf                             91
     c                   if        *in91 = *off
     c                   eval      c1ktxt = raktxt
     c                   endif
     c                   endif
      *
      * Kunde
      *
     c                   if        c1kund <> 0
     c                   eval      rkunl1_kund = c1kund
     c     rkunl1_key    chain     rkunl1                             91
     c                   if        *in91 = *off
     c                   eval      c1knav = rknavn
     c                   endif
     c                   endif
      *
      * Prosjekt
      *
     c                   if        c1kpro <> 0
     c                   eval      fkprl1_kund = c1kund
     c                   eval      fkprl1_kpro = c1kpro
     c     fkprl1_key    chain     fkprl1                             91
     c                   if        *in91 = *off
     c                   eval      c1pnav = flnavn
     c                   endif
     c                   endif
      *
      * Varenummer
      *
     c                   if        c1vare <> *blank
     c                   eval      vvarl1_vare = c1vare
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *off
     c                   eval      c1vtxt = vvtek1
     c                   endif
     c                   endif
      *
      * Enhet
      *
     c                   if        c1enhe <> *blank
     c                   eval      venhl1_eenh = c1enhe
     c     venhl1_key    chain     venhl1                             91
     c                   if        *in91 = *off
     c                   eval      c1etxt = vaetxt
     c                   endif
     c                   endif
      *
      * Leveringstype
      *
     c                   if        c1lety <> 0
     c                   eval      vltpl1_lety = c1lety
     c     vltpl1_key    chain     vltpl1                             91
     c                   if        *in91 = *off
     c                   eval      c1ltxt = vybesk
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Kalkuler DG for spesialpris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kalk_dg       begsr
      *
     c                   eval      c2dekn = 0
      *
      * Dersom salgspris eller kostpris ikke er utfylt, kalles program som
      * henter priser på vare
      *
     c                   if        c2sapr = 0 or
     c                             c2kopr = 0
      *
     c                   eval      w_vare = c1vare
     c                   eval      w_inlr = *blank
      *
     c                   call      'VP701R'
     c                   parm                    w_firm
     c                   parm                    w_vare
5.70 c                   parm                    c1prgr
     c                   parm                    c1enhe
     c                   parm                    c1lety
     c                   parm                    w_udat
     c                   parm                    w_inlr
     c                   parm                    w_sapr
     c                   parm                    w_bupr
     c                   parm                    w_kopr
     c                   parm                    w_inpr
      *
     c                   endif
      *
      * Bestemmer salgspris og kostpris
      *
     c                   if        c2sapr <> 0
     c                   eval      w_sapr = c2sapr
     c                   endif
      *
     c                   if        c2kopr <> 0
     c                   eval      w_kopr = c2kopr
     c                   endif
      *
      * Finner salgspris etter rabatt
      *
     c                   if        c2rab1 <> 0
     c                   eval(h)   w_sapr = w_sapr - (w_sapr * c2rab1 / 100)
     c                   endif
      *
     c                   if        c2rab2 <> 0
     c                   eval(h)   w_sapr = w_sapr - (w_sapr * c2rab2 / 100)
     c                   endif
      *
      * Beregner DG
      *
     c                   if        w_sapr <> 0
     c                   select
     c                   when      (100 - (w_kopr * 100 / w_sapr)) < -999,99
     c                   eval      c2dekn = -999,99
     c                   when      (100 - (w_kopr * 100 / w_sapr)) > 999,99
     c                   eval      c2dekn = 999,99
     c                   other
     c                   eval(h)   c2dekn = 100 - (w_kopr * 100 / w_sapr)
     c                   endsl
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på spesialpriser
      *
     c     fprilr_key    klist
     c                   kfld                    w_firm
5.70 c                   kfld                    fprilr_prgr
     c                   kfld                    fprilr_rkat
     c                   kfld                    fprilr_kkat
     c                   kfld                    fprilr_kund
     c                   kfld                    fprilr_kpro
     c                   kfld                    fprilr_vare
     c                   kfld                    fprilr_enhe
     c                   kfld                    fprilr_lety
      *
      * Key for oppdatering av spesialpriser
      *
     c     fprilu_key    klist
     c                   kfld                    w_firm
5.70 c                   kfld                    fprilu_prgr
     c                   kfld                    fprilu_rkat
     c                   kfld                    fprilu_kkat
     c                   kfld                    fprilu_kund
     c                   kfld                    fprilu_kpro
     c                   kfld                    fprilu_vare
     c                   kfld                    fprilu_enhe
     c                   kfld                    fprilu_lety
      *
      * Key for posisjonering på rabattkategori
      *
     c     fpril1_key    klist
     c                   kfld                    w_firm
5.70 c                   kfld                    fpril1_prgr
     c                   kfld                    fpril1_rkat
     c                   kfld                    fpril1_kkat
     c                   kfld                    fpril1_kund
     c                   kfld                    fpril1_kpro
     c                   kfld                    fpril1_vare
     c                   kfld                    fpril1_enhe
     c                   kfld                    fpril1_lety
      *
      * Key for posisjonering på kundekategori
      *
     c     fpril2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fpril2_kkat
      *
      * Key for posisjonering på kunde
      *
     c     fpril3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fpril3_kund
     c                   kfld                    fpril3_kpro
      *
      * Key for posisjonering på kundeprosjekt
      *
     c     fpril4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fpril4_kpro
      *
      * Key for posisjonering på varenummer
      *
     c     fpril5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fpril5_vare
5.70 c                   kfld                    fpril5_prgr
     c                   kfld                    fpril5_enhe
     c                   kfld                    fpril5_lety
      *
      * Key for posisjonering på enhet
      *
     c     fpril6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fpril6_enhe
     c                   kfld                    fpril6_lety
      *
      * Key for posisjonering på leveringstype
      *
     c     fpril7_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fpril7_lety
      *
      * Key for posisjonering på ordrenummer
      *
     c     fpril8_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fpril8_numm
5.70  *
5.70  * Key for posisjonering på rabatt.kategori
5.70  *
5.70 c     fpril9_key    klist
5.70 c                   kfld                    w_firm
5.70 c                   kfld                    fpril9_rkat
      *
      * Key for oppslag på rabattkategori
      *
     c     ra06pf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra06pf_fkod
5.70  *
5.70  * Key for oppslag på prisgruppe
5.70  *
5.70 c     ra30lr_key    klist
5.70 c                   kfld                    w_firm
5.70 c                   kfld                    ra30lr_dkod
      *
      * Key for oppslag på kundekategori
      *
     c     ra11pf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra11pf_kkod
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på enhet
      *
     c     venhl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    venhl1_eenh
      *
      * Key for oppslag på leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
      *
6.30  * Key for oppslag på vare (VA)
6.30  *
6.30 c     jvarl1_key    klist
6.30 c                   kfld                    jvarl1_vare
      *
      * Henter firmanummer fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjermbilde
      *
     c                   eval      *in22 = *on
      *
      * Henter dagens dato
      *
     c                   time                    w_udat
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     fpril1_key    setll     fpril1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
