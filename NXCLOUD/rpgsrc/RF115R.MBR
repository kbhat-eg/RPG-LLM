      /TITLE       Program : RF115R     ASOKON     Sist endret: 09.08.01  RF115R
     h datedit(*dmy.) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RF115R                                              *
      * Beskrivelse . . : Utskrift av journal-liste                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
T5.00 * R5.00     270802  Tatt vekk parameter firma i CALL RS-pgm.        HFL *
T5.00 * R5.00     141002  Overflow-ind. ikke satt av.                     HFL *
T5.10 * R5.20     071103  Fjernet bruk av subrutiner for å hente tekst    KOB *
T5.10 * R5.40     130905  Endret utskrift av sumnivåer                    KOB *
T5.10 * R5.41     141006  Ved bruk av bilagsnummer 999999 oppsto feil     KOB *
T5.50 * R5.50     211207  2 journallister kunne komme med samme journalnr.HFL *
T5.50 *                   Journalnr. som blir hentet i RF110R brukes.     HFL *
T6.10 * R6.10     220610  Totallinjer må ikke skrives når flere kjørt     NHO *
      *                   samtidig som frembringer brudd (*in86)              *
pdf   * R6.10     120710  Innført PDF utskrift. Innebærer primært å finne BSA *
pdf   *                   generert spool, og kall av AP620R med dette som     *
pdf   *                   parametre.                                          *
6.11  * R6.10 071010 BSA  Overfører bacthtype som en del parameterlista. Viser
6.11  *                   hva slags rutine som kjøres. Legges med i arkivet.
6.20  * 6.20  051212 bsa  Tillegg for å berike metataggger                    *
6.21  * 6.20  040113 bsa  Endret logikk for oppbygging av refn                *
6.nu  * 6.30  100614 bof  endringer mtp. nummutvidelse
6.30  * R6.30 180816 bsa  Rokkert på rekkefølge av close printerfile
7.01  *K00        060720  logging til ajltpf       + switchstyring       bof  *
      *                                                                       *
8.01  *K000   230821 bsa  RJMVAG utvidet til 13,2. Endring i printerfile      *
8.02  * R8.xx 241121 bsa  Problemer med at teller sprekker.
      *                                                                       *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frjoupf    ip   e           k disk
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frhovl1    if   e           k disk    rename(rhovpfr:rhovl1r)
S5.50f*raa4l1    if   e           k disk    rename(raa4pfr:raa4l1r)
     fra16l1    if   e           k disk    rename(ra16pfr:ra16l1r)
     frf115p    o    e             printer oflind(*in30)
pdf  f                                     usropn
      *
     d                 ds
     d umtid                         12  0
     d  umtime                        4  0 overlay(umtid)
     d  umdato                        6  0 overlay(umtid:7)
      *
     d                uds
pdf  d l_poff                584    584
pdf  d l_bach                595    635
pdf  d l_skje                637    637  0
     d l_wsid                901    910
     d l_user                911    920
7.01 d l_filg                931    933
      *
      * Definering av nøkkelfelt
      *
     d rkunl1_kund     s                   like(rkkund)
     d rlevl1_levr     s                   like(rllevr)
     d rhovl1_kont     s                   like(rhkont)
     d rhovl1_spes     s                   like(rhspes)
     d rhovl1_avde     s                   like(rhavde)
     d ra16l1_pkod     s                   like(rapkod)
      *
     d antpos          s              5  0
     d vatxt           s             20
8.02 d wfbinr          s              9  0
      *
6.10 d w_ant           s              1  0
6.10 d w_mem1          s                   like(rjmemb)
     d w_firm          s                   like(rjfirm)
7.01 d w_jkey          s             41
7.01 d w_jnav          s             15
7.01 d w_jsta          s              2  0
7.01 d w_jmld          s            200
      *
     d p_stat          s              1
     d p_pkod          s              3
     d p_pmna          s             20
     d p_text          s             30
     d p_kund          s              6  0
     d p_levr          s              6  0
     d p_navn          s             30
     d p_kont          s              5  0
     d p_firm          s                   like(rjfirm)
     d p_avde          s                   like(rjavde)
     d p_spes          s                   like(rjspes)
6.11 d p_btyp          s            100
      *
pdf  d splfname2       s             10a
pdf  d jobname2        s             10a
pdf  d username2       s             10a
pdf  d jobnbr2         s              6a
pdf  d splfnbr2        s             10i 0
pdf  d splfname3       s             10a
pdf  d jobname3        s             10a
pdf  d username3       s             10a
pdf  d jobnbr3         s              6a
pdf  d splfnbr3        s             10i 0
6.20 d p_tagg0         s             20
6.20 d p_tagg0val      s             50
6.20 d p_tagg1         s             20
6.20 d p_tagg1val      s             50
6.20 d p_tagg2         s             20
6.20 d p_tagg2val      s             50
6.20 d p_tagg3         s             20
6.20 d p_tagg3val      s             50
6.20 d p_tagg4         s             20
6.20 d p_tagg4val      s             50
6.20 d p_tagg5         s             20
6.20 d p_tagg5val      s             50
6.20 d p_tagg6         s             20
6.20 d p_tagg6val      s             50
6.20 d p_tagg7         s             20
6.20 d p_tagg7val      s             50
6.20 d p_tagg8         s             20
6.20 d p_tagg8val      s             50
6.20 d p_tagg9         s             20
6.20 d p_tagg9val      s             50
6.20 d p_refn          s             50
6.20 d p_dspl          s            100
pdf   *
pdf  d qsprilsp        pr                  extpgm('QSPRILSP')
pdf  d rcvvar                     32767a   options(*varsize)
pdf  d rcvvarlen                     10i 0 const
pdf  d format                         8a   const
pdf  d errorcode                  32767a   options(*varsize)
pdf   *
pdf  d sprl0100        ds
pdf  d  bytesrtn                     10i 0
pdf  d  bytesavl                     10i 0
pdf  d  splfname                     10a
pdf  d  jobname                      10a
pdf  d  username                     10a
pdf  d  jobnbr                        6a
pdf  d  splfnbr                      10i 0
pdf  d  systemname                    8a
pdf  d  createdate                    7a
pdf  d                                1a
pdf  d  createtime                    6a
pdf   *
pdf  d errorcode       ds
pdf  d  bytesprov                    10i 0 inz(0)
pdf  d  byteavail                    10i 0 inz(0)
pdf   *
7.01   dcl-s co402_verdi1  char(1);
7.01   dcl-s co402_verdi2  char(2048);
7.01   DCL-PR CO402R EXTPGM('CO402R');
7.01     p_filgrp            char(3)     const;
7.01     p_firm              packed(3:0) const;
7.01     p_lager             packed(2:0) const;
7.01     p_nokkel            char(50)    const;
7.01     p_verdi1            char(1);
7.01     p_verdi2            char(2048);
7.01   END-PR;
7.01 d u_logg          s              1    inz(*off)                            logging ajltpf
      *
     irjoupfr
      *  Posteringer inn
     i                                          rjfirm        l6
     i                                          rjbdat
     i                                          rjbiln        l2
     i                                          rjmvab
     i                                          rjiavg
     i                                          rjmvak
     i                                          rjtype        l4
     i                                          rjraar        l5
     i                                          rjrper        l5
     i                                          rjbunt        l3
     i                                          rjfdat
     i                                          rjvalk
      *
     c/eject
      *
     c     dummy         tag
6.10  *
6.10 c                   if        rjmemb > w_mem1
6.10 c                   move      *on           *inlr
6.10 c                   move      *on           *in86
6.10 c                   goto      end
6.10 c                   endif
6.10  *
6.10 c                   if        rjmemb <> w_mem1
6.10 c                   move      *on           *in86
6.10 c                   goto      end
6.10 c                   else
6.10 c                   if        w_ant = 0
6.10 c                   add       1             w_ant
6.10 c                   move      *on           *inl6
6.10 c                   move      *on           *inl5
6.10 c                   move      *on           *inl4
6.10 c                   move      *on           *inl3
6.10 c                   move      *on           *inl2
6.10 c                   move      *on           *inl1
6.10 c                   end
6.10 c                   move      *off          *in86
6.10 c                   endif
      *
      * Brudd på firma
      *
     c                   if        *inl6 = *on
      *
     c                   time                    umtid
     c                   move      rjfirm        w_firm
      *
     c     afirl1_key    chain     afirl1                             61
     c                   eval      a1firm = aaffir
     c                   eval      a1fnav = aafnav
      *
     c                   eval      page = 0
      *
     c                   endif
      *
      * Brudd på Periode/År
      *
     c                   if        *inl5 = *on
      *
S5.50c*                  move      rjraar        ra4aar
S5.50c*    raa4l1_key    chain     raa4l1                             61
      *
S5.50c*                  eval      a1jonr = ra4sjÅ
T5.50c                   eval      a1jonr = rjjour
     c                   eval      a1rper = rjrper
     c                   eval      a1raar = rjraar
      *
      * Skriv heading
      *
     c                   eval      a1bunt = rjbunt
     c                   write     a1hdr
      *
     c                   endif
      *
      * Brudd på bilagstype
      *
     c                   if        *inl4 = *on
     c                   eval      a1bunt = rjbunt
     c                   endif
      *
      * Brudd på bunt
      *
     c                   if        *inl3 = *on
     c                   eval      a1bunt = rjbunt
     c                   move      rjbiln        wfbinr
     c                   write     a1bun
     c                   endif
      *
      * Brudd på bilagsnummer
      *
     c                   eval      *in52 = *off
      *
     c                   if        *inl2 = *on and
     c                             rjbiln <> wfbinr
     c                   eval      *in52 = *on
     c                   move      rjbiln        wfbinr
     c                   endif
      *
     c/eject
      *
      * Test posteringstype
      *
     c                   eval      *in81 = *off
     c                   eval      *in82 = *off
     c                   eval      *in83 = *off
      *
     c                   if        rjtype = 1
     c                   eval      *in81 = *on
     c                   endif
      *
     c                   if        rjtype = 2
     c                   eval      *in82 = *on
     c                   endif
      *
     c                   if        rjtype = 3
     c                   eval      *in83 = *on
     c                   endif
      *
      * Det finnes prosjektposter, men de behandles ikke
      *
     c                   if        rjreid = 'P'
     c                   eval      *inu6 = *on
     c                   goto      end
     c                   endif
      *
     c                   eval      d1text = rjtext
      *
     c                   if        rjbdat = *loval
     c                   move      0             d1bdat
     c                   else
     c     *dmy          move      rjbdat        d1bdat
     c                   endif
      *
     c                   eval      d1navn = *blanks
      *
     c/eject
      *
      * Behandle hovedboksposter
      *
     c                   if        rjreid = 'H'
      *
     c                   move      rjfirm        w_firm
     c                   eval      rhovl1_kont = rjkont
     c                   eval      rhovl1_spes = rjspes
     c                   eval      rhovl1_avde = rjavde
     c     rhovl1_key    chain     rhovl1
      *
     c                   if        not%found
     c                   move      *zeros        rhovl1_avde
     c     rhovl1_key    chain     rhovl1
      *
     c                   if        not%found
     c                   move      *zeros        rhovl1_spes
     c     rhovl1_key    chain     rhovl1
      *
     c                   if        not%found
     c                   eval      rhtext = *blank
     c                   endif
     c                   endif
     c                   endif
      *
     c                   movel     rhtext        d1navn
      *
     c                   if        rjneto > 0
     c                   eval      t3dsum = t3dsum + rjneto
     c                   else
     c                   eval      t3ksum = t3ksum + rjneto
     c                   endif
      *
     c                   endif
      *
     c/space 3
      *
      * Behandle kundeposter
      *
     c                   if        rjreid = 'K'
      *
      * Hent kundenavn
      *
     c                   eval      rkunl1_kund = rjkont
     c     rkunl1_key    chain     rkunl1
      *
     c                   if        %found
     c                   movel     rknavn        d1navn
     c                   endif
      *
     c                   eval      t3kund = t3kund + rjneto
      *
     c                   endif
      *
     c/space 3
      *
      * Behandle leverandørposter
      *
     c                   if        rjreid = 'L'
      *
      * Hent leverandørnavn
      *
     c                   eval      rlevl1_levr = rjkont
     c     rlevl1_key    chain     rlevl1
      *
     c                   if        %found
     c                   movel     rlnavn        d1navn
     c                   endif
      *
     c                   eval      t3levr = t3levr + rjneto
      *
     c                   endif
      *
     c/space 3
      *
      * Overflow ?
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   write     a1bun
     c                   move      *off          *in30
     c                   endif
      *
      * Skriv linje
      *
     c                   eval      d1post = rjneto + rjmvab
      *
     c                   if        d1post > 0
     c                   eval      d1debe = rjneto
     c                   eval      *in31 = *on
     c                   else
     c                   eval      d1kred = rjneto
     c                   eval      *in31 = *off
     c                   endif
      *
     c                   write     d1det
     c/space 3
      *
      * Henter valutatekst
      *
     c                   if        rjvalk <> *blanks
     c                   eval      ra16l1_pkod = rjvalk
      *
     c     ra16l1_key    chain     ra16l1
      *
     c                   if        %found
     c                   movel     rapmna        vatxt
     c                   endif
      *
     c                   write     d1val
      *
     c                   endif
      *
     c/eject
      *
      * Summer debet/kredit
      *
     c                   eval      antpos = antpos + 1
      *
     c                   if        rjneto > 0
     c                   eval      t1dsum = t1dsum + rjneto + rjmvab
     c                   eval      t2dsum = t2dsum + rjneto + rjmvab
     c                   else
     c                   eval      t1ksum = t1ksum + rjneto + rjmvab
     c                   eval      t2ksum = t2ksum + rjneto + rjmvab
     c                   endif
      *
     c     end           tag
      *
     c/eject
      *
      * Brudd på bilagnummer
      *
6.10 cl2N86              eval      wfbinr = wfbinr + 1
      *
      * Brudd på bunt
      *
6.10 cl3 81              if        *in86 = *off
     cl3 81              write     t2tot
6.10 cl3                 endif
      *
6.10 cl3N86              eval      t2dsum = 0
6.10 cl3N86              eval      t2ksum = 0
      *
      * Brudd på bilagstype
      *
6.10 cl4N86              write     t1tot
6.10 cl4N86              eval      t1dsum = *zeros
6.10 cl4N86              eval      t1ksum = *zeros
      *
      * Brudd på periode
6.10  * Dersom LR er på og summer er lik 0 er avslutning skrevet
6.10  * tidligere.
6.10 cl5N86              if        *inlr = *on and
6.10 c                             t3dsum = *zeros and
6.10 c                             t3ksum = *zeros and
6.10 c                             t3kund = *zeros and
6.10 c                             t3levr = *zeros and
6.10 c                             antpos = *zeros
6.10 cl5                 endif
      *
      *
6.10 cl5N86              eval      *in25 = *off
6.10 cl5N86              eval      t3diff = t3dsum + t3ksum
      *
6.10 cl5N86              if        t3diff <> 0
6.10 cl5N86              eval      *in25 = *on
     cl5                 endif
6.10 cl5N86              write     t3hdr
      *
6.10 cl5N86              eval      t3dsum = 0
6.10 cl5N86              eval      t3ksum = 0
6.10 cl5N86              eval      t3kund = 0
6.10 cl5N86              eval      t3levr = 0
6.10 cl5N86              eval      antpos = 0
pdf   * Generer xml for videre utskrift
pdf  clr                 if        l_poff = '1'
pdf  clr                 exsr      spoolnbr
6.30 clr                 close     rf115p
6.30 clr                 exsr      xml_create
pdf  clr                 exsr      pdf_preview
pdf   * Avslutt jobbiden
pdf  clr                 call      'AB710R'
pdf  c                   parm                    l_bach
pdf  clr                 endif
      * logger slutt
      *
7.01 clr                 if        u_logg = *on
7.01 clr                 eval      w_jsta = 10
7.01 clr                 eval      w_jmld = 'Slutt RF115R ved LR'
7.01 clr                 call      'AB705R'
7.01 clr                 parm                    w_jkey
7.01 clr                 parm                    w_jsta
7.01 clr                 parm                    w_jmld
7.01 clr                 endif
      *
     c/eject
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Finner spool som er generert av jobben
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     spoolnbr      begsr
pdf   /Free
pdf
pdf            QSPRILSP( SPRL0100
pdf                    : %size(SPRL0100)
pdf                    : 'SPRL0100'
pdf                    : errorcode );
pdf   /End-free
pdf  c*
pdf   *
pdf  c                   endsr
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Kaller eget program for generering av xml fil. Eget pgm         *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     xml_create    begsr
pdf   *
pdf  c                   eval      splfname2 = *blanks
pdf  c                   eval      splfname3 = *blanks
pdf  c                   eval      jobname2 = *blanks
pdf  c                   eval      jobname3 = *blanks
pdf  c                   eval      username2 = *blanks
pdf  c                   eval      username3 = *blanks
pdf  c                   eval      jobnbr2 = *blanks
pdf  c                   eval      jobnbr3 = *blanks
pdf  c                   eval      splfnbr2 = *zeros
pdf  c                   eval      splfnbr3 = *zeros
6.11 c                   eval      p_btyp = 'JOURNALLISTE'
6.20 c                   eval      p_dspl = 'Journalliste - nummer: '+
6.20 c                                      %char(a1jonr)+
6.20 c                                      '   Periode ' +
6.20 c                                      %char(a1rper)+'/'+%char(a1raar)
6.21 c*                  eval      p_refn = 'JOL-'+%char(a1jonr)+'-'+
6.21 c*                                     %char(a1raar)
6.21 c                   eval      p_refn = %char(a1raar) + %char(a1jonr)
6.20 c                   eval      p_tagg0 = 'JOURNAL'
6.20 c                   eval      p_tagg0val = %char(a1jonr)
6.20 c                   eval      p_tagg1 = 'BUNT'
6.20 c                   eval      p_tagg1val = %char(a1bunt)
6.20 c                   eval      p_tagg2 = 'KONTROLLSUM'
6.20 c                   eval      p_tagg2val = %char(t3ksum)
6.20 c                   eval      p_tagg3 = *blanks
6.20 c                   eval      p_tagg3val = *blanks
6.20 c                   eval      p_tagg4 = *blanks
6.20 c                   eval      p_tagg4val = *blanks
6.20 c                   eval      p_tagg5 = *blanks
6.20 c                   eval      p_tagg5val = *blanks
6.20 c                   eval      p_tagg6 = *blanks
6.20 c                   eval      p_tagg6val = *blanks
6.20 c                   eval      p_tagg7 = *blanks
6.20 c                   eval      p_tagg7val = *blanks
6.20 c                   eval      p_tagg8 = *blanks
6.20 c                   eval      p_tagg8val = *blanks
6.20 c                   eval      p_tagg9 = *blanks
6.20 c                   eval      p_tagg9val = *blanks
pdf   *
pdf  c**6.20             call      'AP620R'
6.20 c                   call      'AP627R'
pdf  c                   parm                    splfname
pdf  c                   parm                    jobname
pdf  c                   parm                    username
pdf  c                   parm                    jobnbr
pdf  c                   parm                    splfnbr
pdf  c                   parm                    splfname2
pdf  c                   parm                    jobname2
pdf  c                   parm                    username2
pdf  c                   parm                    jobnbr2
pdf  c                   parm                    splfnbr2
pdf  c                   parm                    splfname3
pdf  c                   parm                    jobname3
pdf  c                   parm                    username3
pdf  c                   parm                    jobnbr3
pdf  c                   parm                    splfnbr3
6.20 c                   parm                    p_tagg0
6.20 c                   parm                    p_tagg0val
6.20 c                   parm                    p_tagg1
6.20 c                   parm                    p_tagg1val
6.20 c                   parm                    p_tagg2
6.20 c                   parm                    p_tagg2val
6.20 c                   parm                    p_tagg3
6.20 c                   parm                    p_tagg3val
6.20 c                   parm                    p_tagg4
6.20 c                   parm                    p_tagg4val
6.20 c                   parm                    p_tagg5
6.20 c                   parm                    p_tagg5val
6.20 c                   parm                    p_tagg6
6.20 c                   parm                    p_tagg6val
6.20 c                   parm                    p_tagg7
6.20 c                   parm                    p_tagg7val
6.20 c                   parm                    p_tagg8
6.20 c                   parm                    p_tagg8val
6.20 c                   parm                    p_tagg9
6.20 c                   parm                    p_tagg9val
pdf  c                   parm                    l_bach
6.11 c                   PARM                    p_btyp
6.20 c                   PARM                    p_refn
6.20 c                   PARM                    p_dspl
pdf   *
pdf  c                   endsr
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Subrutiner for XML/jasper integrasjon - Skjermvisning           *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     pdf_preview   begsr
pdf  c                   if        l_skje = 1
pdf   * Vi kaller program for launch av PDF i browser
pdf  c                   call      'AP621R'
pdf  c                   parm                    l_bach
pdf   *
pdf  c                   endif
pdf   *
pdf  c                   endsr
pdf   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
6.10  *
6.10 c     *entry        plist
6.10 c                   parm                    w_mem1
      *
      * Key for firma-opplysninger - firmaregister
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for statusopplysninger - koderegister
      *
S5.50c*    raa4l1_key    klist
S5.50c*                  kfld                    w_firm
S5.50c*                  kfld                    ra4aar
      *
      * Key for kontoplan
      *
     c     rhovl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovl1_kont
     c                   kfld                    rhovl1_spes
     c                   kfld                    rhovl1_avde
      *
      * Key for kunder
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for leverandører
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for valutakoder
      *
     c     ra16l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra16l1_pkod
      *
pdf  c                   open      rf115p
      *
      *
7.01  * Test om logging skal gjøres
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'RF-PROG LOGGING':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_logg = *on;
7.01   endif;
      *
7.01 c                   if        u_logg = *on
7.01 c                   eval      w_jsta = 10
7.01 c                   eval      w_jmld = 'Start RF115R        '
7.01 c                   eval      w_jnav = 'OKON_RF115R-'
7.01 c                   call      'AB700R'
7.01 c                   parm                    w_jnav
7.01 c                   parm                    w_jkey
7.01 c                   parm                    w_jmld
7.01 c                   eval      w_jsta = 10
7.01 c                   eval      w_jmld = 'Start RF115R       '
7.01 c                   call      'AB705R'
7.01 c                   parm                    w_jkey
7.01 c                   parm                    w_jsta
7.01 c                   parm                    w_jmld
7.01 c                   endif
      *
     c                   endsr
