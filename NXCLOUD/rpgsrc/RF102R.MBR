      /TITLE  Generering av poster utfra fordelingsparametre
     h datedit(*dmy.) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RF102R                                              *
      * Beskrivelse . . : Foretar B = Beregning                               *
      *                           P = Prisberegning (ikke i bruk foreløpig)   *
      *                           O = Omfordeling                             *
      *                   dersom konto finnes i fordelingsparametre           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * V3.40     270202  Rettet feil i forbindelse med sjekk av konto   KOB  *
T5.30 * R5.30     040604  Endret kontroll mot koderegistre fra CALL til       *
T5.30 *                   CHAIN. CALL-program sjekker firma i LDA. Det        *
T5.30 *                   ble feil hvis posteringer for flere firmaer i       *
T5.30 *                   samme kjøring.                                 HFL  *
      * R5.50     050907  Endret slik at mva-kode blir hentet fra  konto KOB  *
      *                   senere i rutinen                                    *
      * R5.60     150908  Endret logikk ved behandling av omfordeling    KOB  *
 6.11 * R6.11     180112  Test på firma må skje dersom manuell registr.  NHO  *
 6.30 * R6.30     280616  Dersom det er lov å overstyre mva kode ved     NHO  *
      *                   registrering så blankes disse dersom konto
      *                   finnes i fordelingsregistere. Prøver ta vare
      *                   på verdiene.
 6.31 * R6.30     303017  Feil flytt av mva
 6.32 * R6.30 sst 140120  Legge på periode i rtria fra rbunpf
7.01  *K00        060720  lagt på return + ab705r + switchstyrt          bof  *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
6.32 frtral1    uf   e           k disk    rename(rtrapfr:rtral1r)
     frbunl1    if   e           k disk    rename(rbunpfr:rbunl1r)
     fraa1pf    if   e           k disk
     frforl1    if   e           k disk    rename(rforpfr:rforl1r)
T5.30fra01l1    if   e           k disk    rename(ra01pfr:ra01l1r)
     frtripf    o    e             disk    rename(rtripfr:rtripfr)
      *
     d dekr            s              1
     d wtbelØ          s                   like(rtbelØ)
     d wtmngd          s                   like(rtmngd)
     d wtvalb          s                   like(rtvalb)
     d wwbelØ          s                   like(rtbelØ)
     d wwcavd          s                   like(rtcavd)
     d wwckto          s                   like(rtckto)
     d wwcmva          s                   like(rtcmva)
     d wwcsps          s                   like(rtcsps)
     d wwdavd          s                   like(rtdavd)
     d wwdkto          s                   like(rtdkto)
     d wwdmva          s                   like(rtdmva)
     d wwdsps          s                   like(rtdsps)
     d wwfakt          s              7  4
     d wwmngd          s                   like(rtmngd)
     d wwpros          s              5  4
     d wwprot          s              5  4
     d wwtext          s                   like(rttext)
     d wwtx17          s             17
     d wwvalb          s                   like(rtvalb)
     d wwxmva          s                   like(rtdmva)
     d xbelØp          s                   like(rtbelØ)
     d xmengd          s                   like(rtmngd)
     d xvalbe          s                   like(rtvalb)
      *
610  d w_memb          s                   like(rtmemb)
610  d w_memx          s                   like(rtmemb)
     d w_firm_gml      s                   like(rtfirm)
     d w_bunt_gml      s                   like(rtbunt)
      *
7.01 d w_jkey          s             41
7.01 d w_jnav          s             15
7.01 d w_jsta          s              2  0
7.01 d w_jmld          s            200
      *
     d p_akod          s                   like(rtbilk)
     d p_text          s                   like(rttext)
     d p_stat          s              1
      *
610  d rbunl1_nivo     s                   like(rsnivo)
610  d rbunl1_memb     s                   like(rsmemb)
610  d rtral1_nivo     s                   like(rtnivo)
610  d rtral1_memb     s                   like(rtmemb)
610  d rtral1_bunt     s                   like(rtbunt)
610  d rtral1_linj     s                   like(rtlinj)
     d w_firm          s                   like(rtfirm)
     d rforl1_kont     s              5  0
     d rforl1_spes     s                   like(rtdsps)
     d rforl1_avde     s                   like(rtdavd)
     d rbunl1_bunt     s                   like(rtbunt)
T5.30d ra01l1_kod      s                   like(raakod)
      *
     d w_funnet        s              1
     d w_funnet_deb    s              1
     d w_funnet_kre    s              1
      *
6.11 d                uds
6.11 d l_firm                944    946  0
7.01 d l_filg                931    933
      *
7.01   dcl-s co402_verdi1  char(1);
7.01   dcl-s co402_verdi2  char(2048);
7.01   DCL-PR CO402R EXTPGM('CO402R');
7.01     p_filgrp            char(3)     const;
7.01     p_firm              packed(3:0) const;
7.01     p_lager             packed(2:0) const;
7.01     p_nokkel            char(50)    const;
7.01     p_verdi1            char(1);
7.01     p_verdi2            char(2048);
7.01   END-PR;
7.01 d u_logg          s              1    inz(*off)                            logging ajltpf
      /eject
7.01 c                   if        u_logg = *on
7.01 c                   eval      w_jsta = 10
7.01 c                   eval      w_jmld = 'Start RF102R        '
7.01 c                   eval      w_jnav = 'OKON_RF102R-'
7.01 c                   call      'AB700R'
7.01 c                   parm                    w_jnav
7.01 c                   parm                    w_jkey
7.01 c                   parm                    w_jmld
7.01 c                   eval      w_jsta = 10
7.01 c                   eval      w_jmld = 'Start RF102R       '
7.01 c                   call      'AB705R'
7.01 c                   parm                    w_jkey
7.01 c                   parm                    w_jsta
7.01 c                   parm                    w_jmld
7.01 c                   endif
7.01  *
610   *    leser fil/ ble lest med I P
610  c                   eval      rtral1_nivo = *blank
610  c                   eval      rtral1_memb = w_memx
610  c                   eval      rtral1_bunt = *zero
610  c     rtral1_key    setll     rtral1
610   *
     c     dummy         tag
610  c                   read      rtral1                                 15
610   *
610  c                   if        rtmemb <> w_memx
610  c                   move      *on           *in15
7.01 c                   if        u_logg = *on
7.01 c                   eval      w_jsta = 10
7.01 c                   eval      w_jmld = 'Slutt RF102R før LR'
7.01 c                   call      'AB705R'
7.01 c                   parm                    w_jkey
7.01 c                   parm                    w_jsta
7.01 c                   parm                    w_jmld
7.01 c                   endif
7.01  *
610  c                   move      *on           *inlr
610  c                   goto      ut
610  c                   endif
610   *
6.11 c                   if        rtfirm <> w_firm
6.11 c                   move      *on           *in15
7.01 c                   if        u_logg = *on
7.01 c                   eval      w_jsta = 10
7.01 c                   eval      w_jmld = 'Slutt RF102R før LR'
7.01 c                   call      'AB705R'
7.01 c                   parm                    w_jkey
7.01 c                   parm                    w_jsta
7.01 c                   parm                    w_jmld
7.01 c                   endif
7.01  *
6.11 c                   move      *on           *inlr
6.11 c                   goto      ut
6.11 c                   endif
610   *
610  c                   if        *in15  = '1'
7.01 c                   if        u_logg = *on
7.01 c                   eval      w_jsta = 10
7.01 c                   eval      w_jmld = 'Slutt RF102R før LR'
7.01 c                   call      'AB705R'
7.01 c                   parm                    w_jkey
7.01 c                   parm                    w_jsta
7.01 c                   parm                    w_jmld
7.01 c                   endif
7.01  *
610  c                   move      *on           *inlr
610  c                   goto      ut
610  c                   endif
610   *
610  c     dumm1         tag
      *  Hent statusopplysninger
      *
     c                   if        rtfirm <> w_firm_gml
     c                   move      rtfirm        w_firm
     c     raa1pf_key    chain     raa1pf                             52
     c                   eval      w_firm_gml = rtfirm
     c                   eval      w_bunt_gml = *loval
     c                   endif
      *
      *  Sjekk om bunt finnes/utgått
      *
     c                   if        rtbunt <> w_bunt_gml
     c                   eval      rbunl1_bunt = rtbunt
610  c                   eval      rbunl1_nivo = *blank
610  c                   eval      rbunl1_memb = w_memx
     c     rbunl1_key    chain     rbunl1                             53
     c                   eval      w_bunt_gml = rtbunt
     c                   endif
      *
610   *
610  c                   if        rtmemb <> w_memx
610  c                   goto      ut
610  c                   endif
610   *
     c                   if        *in53 = *on or
     c                             rsstat = 'U'
     c                   goto      ut
     c                   endif
      *
      *  Hent bilagstekst hvis blank
      *
     c                   if        rttext = *blanks
      *
E5.30c                   eval      ra01l1_kod = rtbilk
E5.30c     ra01l1_key    chain     ra01l1
      *
T5.30c                   if        %found
T5.30c                   eval      rttext = raatxt
T5.30c                   else
T5.30c                   eval      rttext = 'Koden er ugyldig'
T5.30c                   endif
      *
     c                   endif
      *
      * Legger ut periode pr. transaksjon
      *
     c                   move      rsraar        rtraar
     c                   move      rsrper        rtrper
      *
      *  Bilagskode 85/98 skal ikke behandles
      *
     c                   if        rtbilk = 85 or
     c                             rtbilk = 98
610  c                   eval      rtmemb = w_memb
     c                   write     rtripfr
     c                   goto      ut
     c                   endif
      *
      * Ta vare på kontobegrep/mva/tekst
      *
     c                   move      rtdkto        wwdkto
     c                   move      rtdsps        wwdsps
     c                   move      rtdavd        wwdavd
     c                   move      rtdmva        wwdmva
     c                   move      rtbelØ        wwbelØ
     c                   move      rtvalb        wwvalb
     c                   move      rtmngd        wwmngd
     c                   move      rtckto        wwckto
6.30 c                   move      rtcmva        wwcmva
     c                   move      rtcsps        wwcsps
     c                   move      rtcavd        wwcavd
     c                   move      rttext        wwtext
     c                   movel     rttext        wwtx17
      *
     c                   eval      w_funnet_deb = ' '
     c                   eval      w_funnet_kre = ' '
      *
      * Sjekk om hovedbokskonto
      *
     c                   if        rtdkto <> *zero and
     c                             rtdkto <= ra1hkt
     c                   eval      rforl1_kont = rtdkto
     c                   eval      rforl1_spes = rtdsps
     c                   eval      rforl1_avde = rtdavd
     c                   exsr      sjekk_kto
     c                   eval      w_funnet_deb = w_funnet
     c                   endif
      *
     c                   if        rtckto <> *zero and
     c                             rtckto <= ra1hkt
     c                   eval      rforl1_kont = rtckto
     c                   eval      rforl1_spes = rtcsps
     c                   eval      rforl1_avde = rtcavd
     c                   exsr      sjekk_kto
     c                   eval      w_funnet_kre = w_funnet
     c                   endif
      *
      * Hvis ikke konto finnes i fordelingsregisteret
      * skrives opprinnelig post ut
      *
     c                   if        w_funnet_deb = *blank and
     c                             w_funnet_kre = *blank
610  c                   eval      rtmemb = w_memb
     c                   write     rtripfr
     c                   goto      ut
     c                   endif
      *
      * Hvis konto finnes
      * behandle konto
      *
     c                   if        w_funnet_deb = 'F'
     c                   exsr      beh_deb
     c                   endif
      *
     c                   if        w_funnet_kre = 'F'
     c                   exsr      beh_kre
     c                   endif
      *
      * Behandling av omfordeling motkonto
      *
     c                   if        rfkode = 'O'
      *
     c                   if        w_funnet_deb = 'F' and
     c                             w_funnet_kre = ' ' and
     c                             wwckto <> *zero
     c                   eval      rtckto = wwckto
     c                   eval      rtcsps = wwcsps
     c                   eval      rtcavd = wwcavd
6.30 c                   eval      rtcmva = wwcmva
     c                   eval      rtdkto = *zeros
     c                   eval      rtdsps = *zeros
     c                   eval      rtdavd = *zeros
     c                   eval      rtdmva = *blank
     c                   eval      rtbelØ = wwbelØ
     c                   eval      rtvalb = wwvalb
     c                   eval      rtmngd = wwmngd
     c                   eval      rttext = wwtext
610  c                   eval      rtmemb = w_memb
     c                   write     rtripfr
     c                   goto      ut
     c                   endif
      *
     c                   if        w_funnet_kre = 'F' and
     c                             w_funnet_deb = ' ' and
     c                             wwdkto <> *zero
     c                   eval      rtdkto = wwdkto
     c                   eval      rtdsps = wwdsps
     c                   eval      rtdavd = wwdavd
6.30 c*                  eval      rtcmva = wwdmva
6.31 c                   eval      rtdmva = wwdmva
     c                   eval      rtckto = *zeros
     c                   eval      rtcsps = *zeros
     c                   eval      rtcavd = *zeros
     c                   eval      rtcmva = *blank
     c                   eval      rtbelØ = wwbelØ
     c                   eval      rtvalb = wwvalb
     c                   eval      rtmngd = wwmngd
     c                   eval      rttext = wwtext
610  c                   eval      rtmemb = w_memb
     c                   write     rtripfr
     c                   goto      ut
     c                   endif
      *
     c                   endif
      /space 3
      *
      ******************************************************
      *  AVSLUTT DENNE TRANS                               *
      ******************************************************
      *
     c     ut            tag
610  c                   if        *inlr = '0'
6.32 c*                  eval      rtraar = rsraar
6.32 c*                  eval      rtrper = rsrper
6.32 c*                  eval      rtmemb = w_memx
6.32 c*                  update    rtral1r
610  c                   goto      dummy
610  c                   endif
      *
7.01 c                   if        u_logg = *on
7.01 c                   eval      w_jsta = 10
7.01 c                   eval      w_jmld = 'Slutt RF102R etter LR'
7.01 c                   call      'AB705R'
7.01 c                   parm                    w_jkey
7.01 c                   parm                    w_jsta
7.01 c                   parm                    w_jmld
7.01 c                   endif
7.01 c                   return

      *
     c/eject
      *
      * Sjekk om konto finnes i fordelingsregisteret
      *
     c     sjekk_kto     begsr
      *
     c     rforl1_key    chain     rforl1                             61
      *
     c                   if        *in61 = *on
     c                   eval      w_funnet = *blanks
     c                   else
     c                   eval      w_funnet = 'F'
     c                   endif
      *
     c                   endsr
     c/eject
      *
      * Behandle debetkonto
      *
     c     beh_deb       begsr
      *
     c                   eval      wtbelØ = 0
     c                   eval      wtvalb = 0
     c                   eval      wtmngd = 0
     c                   eval      wwpros = 0
     c                   eval      wwprot = 0
     c                   eval      wwfakt = 0
      *
     c                   move      wwdkto        rforl1_kont
     c                   move      wwdsps        rforl1_spes
     c                   move      wwdavd        rforl1_avde
     c                   move      'D'           dekr
      *
      * Les fordelingsregister
      *
     c     rforl1_key    setll     rforl1                                 54
     c     rforl1_key    reade     rforl1                                 54
      *
     c                   dow       *in54 = *off
      *
      * Beregn faktor basert på %-andel
      *
     c                   if        rfkode = 'B'
     c                   if        rfkto1 = *zero
     c                   eval      wwpros = rfpros / 100
     c                   goto      nydeb
     c                   else
     c                   eval      wwprot = rfprot / 100
     c                   endif
     c                   endif
      *
     c                   if        rfkode = 'O'
     c                   if        rfkto1 = *zero
     c                   eval      wwpros = rfpros / 100
     c                   goto      nydeb
     c                   else
     c                   eval      wwprot = rfprot / 100
     c                   endif
     c                   endif
      *
     c                   eval      wwfakt = wwpros * wwprot
      *
      * Beregning
      *
     c                   if        rfkode = 'B'
     c                   move      rtdmva        wwxmva
     c                   exsr      beregn
     c                   endif
      *
      * Prissetting
      *
     c                   if        rfkode = 'P'
     c                   exsr      pris
     c                   endif
      *
      * Omfordeling
      *
     c                   if        rfkode = 'O'
     c                   exsr      omford
     c                   endif
      *
     c     nydeb         tag
      *
     c     rforl1_key    reade     rforl1                                 54
     c                   enddo
      *
      *DEBET************************************************
      *   Opprinnelig post blir skrevet ved B og P mens    *
      *   rest blir skrevet ved O (Omfordeling)            *
      ******************************************************
      *
     c                   eval      rtbelØ = wwbelØ - wtbelØ
     c                   eval      rtvalb = wwvalb - wtvalb
     c                   eval      rtmngd = wwmngd - wtmngd
      *
     c*                  eval      rtckto = wwckto
     c*                  eval      rtcsps = wwcsps
     c*                  eval      rtcavd = wwcavd
     c                   eval      rtckto = 0
     c                   eval      rtcsps = 0
     c                   eval      rtcavd = 0
     c                   eval      rtdkto = wwdkto
     c                   eval      rtdsps = wwdsps
     c                   eval      rtdavd = wwdavd
     c                   move      wwtx17        rttext
     c                   movel     'O* '         rttext
      *
     c                   if        rtbelØ > *zero or
     c                             rtvalb > *zero or
     c                             rtmngd > *zero
610  c                   eval      rtmemb = w_memb
     c                   write     rtripfr
     c                   endif
      *
     c                   endsr
      *
     c/eject
      *
      * Behandle kreditkonto
      *
     c     beh_kre       begsr
      *
     c                   eval      wtbelØ = 0
     c                   eval      wtvalb = 0
     c                   eval      wtmngd = 0
     c                   eval      wwpros = 0
     c                   eval      wwprot = 0
     c                   eval      wwfakt = 0
      *
     c                   move      wwckto        rforl1_kont
     c                   move      wwcsps        rforl1_spes
     c                   move      wwcavd        rforl1_avde
     c                   move      'K'           dekr
      *
     c     rforl1_key    setll     rforl1                                 54
     c     rforl1_key    reade     rforl1                                 54
      *
      * Les fordelingsregister
      *
     c                   dow       *in54 = *off
      *
      * Beregn faktor basert på %-andel
      *
     c                   if        rfkode = 'B'
     c                   if        rfkto1 = *zero
     c                   eval      wwpros = rfpros / 100
     c                   goto      nykre
     c                   else
     c                   eval      wwprot = rfprot / 100
     c                   endif
     c                   endif
      *
     c                   if        rfkode = 'O'
     c                   if        rfkto1 = *zero
     c                   eval      wwpros = rfpros / 100
     c                   goto      nykre
     c                   else
     c                   eval      wwprot = rfprot / 100
     c                   endif
     c                   endif
      *
     c                   eval      wwfakt = wwpros * wwprot
      *
      * Beregning
      *
     c                   if        rfkode = 'B'
     c                   exsr      beregn
     c                   endif
      *
      * Prissetting
      *
     c                   if        rfkode = 'P'
     c                   exsr      pris
     c                   endif
      *
      * Omfordeling
      *
     c                   if        rfkode = 'O'
     c                   exsr      omford
     c                   endif
      *
     c     nykre         tag
      *
     c     rforl1_key    reade     rforl1                                 54
     c                   enddo
      *
      *KREDIT***********************************************
      *   Opprinnelig post blir skrevet ved B og P mens    *
      *   rest blir skrevet ved O (Omfordeling)            *
      ******************************************************
      *
     c                   eval      rtbelØ = wwbelØ - wtbelØ
     c                   eval      rtvalb = wwvalb - wtvalb
     c                   eval      rtmngd = wwmngd - wtmngd
     c                   eval      rtckto = wwckto
     c                   eval      rtcsps = wwcsps
     c                   eval      rtcavd = wwcavd
     c*                  eval      rtdkto = wwdkto
     c*                  eval      rtdsps = wwdsps
     c*                  eval      rtdavd = wwdavd
     c                   eval      rtdkto = 0
     c                   eval      rtdsps = 0
     c                   eval      rtdavd = 0
     c                   move      wwtext        rttext
      *
     c                   if        rtbelØ > *zero or
     c                             rtvalb > *zero or
     c                             rtmngd > *zero
610  c                   eval      rtmemb = w_memb
     c                   write     rtripfr
     c                   endif
      *
     c                   endsr
      /eject
      *
      ******************************************************
      * Foretar beregning og legger ut                     *
      * en ny postering med debet- eller kreditkonto       *
      ******************************************************
      *
     c     beregn        begsr
      *
     c                   eval(h)   rtbelØ = wwbelØ * wwfakt
     c                   eval      rtvalb = *zero
     c                   eval      rtmngd = *zero
      *
     c                   move      wwtx17        rttext
     c                   movel     'B* '         rttext
      *
     c                   if        dekr = 'D'
     c                   eval      rtdkto = rfkto1
     c                   eval      rtdavd = rfavd1
     c                   eval      rtdsps = rfsps1
     c                   eval      rtckto = rfkto2
     c                   eval      rtcavd = rfavd2
     c                   eval      rtcsps = rfsps2
      *
     c                   else
      *
     c                   eval      rtckto = rfkto1
     c                   eval      rtcavd = rfavd1
     c                   eval      rtcsps = rfsps1
     c                   eval      rtdkto = rfkto2
     c                   eval      rtdavd = rfavd2
     c                   eval      rtdsps = rfsps2
     c                   endif
      *
     c                   if        rtbelØ > *zero
610  c                   eval      rtmemb = w_memb
     c                   write     rtripfr
     c                   endif
      *
     c                   endsr
      /eject
      *
      ******************************************************
      * Foretar prisberegning og legger ut                 *
      * en ny postering med debet- eller kreditkonto       *
      ******************************************************
      *
     c     pris          begsr
      *
     c                   eval(h)   rtbelØ = wwmngd * rfpris
     c                   eval      rtvalb = *zero
     c                   move      wwmngd        rtmngd
      *
     c                   move      wwtx17        rttext
     c                   movel     'P* '         rttext
      *
     c                   if        rtbelØ > *zero or
     c                             rtvalb > *zero or
     c                             rtmngd > *zero
610  c                   eval      rtmemb = w_memb
     c                   write     rtripfr
     c                   endif
      *
     c                   endsr
      *
     c/eject
      *
      ******************************************************
      * Foretar omfordeling og legger ut                   *
      * en ny postering med debet- eller kreditkonto       *
      ******************************************************
      *
     c     omford        begsr
      *
     c                   eval(h)   rtbelØ = wwbelØ * wwfakt
     c                   eval(h)   rtvalb = wwvalb * wwfakt
     c                   eval(h)   rtmngd = wwmngd * wwfakt
      *
      * Akkumuler fordelte summer
      *
     c                   eval      wtbelØ = wtbelØ + rtbelØ
     c                   eval      wtvalb = wtvalb + rtvalb
     c                   eval      wtmngd = wtmngd + rtmngd
      *
      * Sjekk om avrunding
      *
     c                   exsr      avrund
      *
     c                   move      wwtx17        rttext
     c                   movel     'O* '         rttext
      *
     c                   if        dekr = 'D'
     c                   eval      rtdkto = rfkto1
     c                   eval      rtdavd = rfavd1
     c                   eval      rtdsps = rfsps1
6.30 c                   eval      rtdmva = wwdmva
     c                   eval      rtckto = *zero
     c                   eval      rtcavd = *zero
     c                   eval      rtcsps = *zero
     c                   eval      rtcmva = *blank
      *
     c                   else
      *
     c                   eval      rtckto = rfkto1
     c                   eval      rtcavd = rfavd1
     c                   eval      rtcsps = rfsps1
6.30 c                   eval      rtcmva = wwcmva
     c                   eval      rtdkto = *zero
     c                   eval      rtdavd = *zero
     c                   eval      rtdsps = *zero
     c                   eval      rtdmva = *blank
     c                   endif
      *
     c                   if        rtbelØ > *zero or
     c                             rtvalb > *zero or
     c                             rtmngd > *zero
610  c                   eval      rtmemb = w_memb
     c                   write     rtripfr
     c                   endif
      *
     c                   endsr
      *
     c/eject
      *
      *-------------------------------------------------------------------
      *  A V R U N D :  Hvis beløp/mengde som gjenstår til fordeling er
      *                 mindre enn 1, betraktes dette som avrunding
      *-------------------------------------------------------------------
      *
     c     avrund        begsr
      *
     c                   eval      xbelØp = wwbelØ - wtbelØ
     c                   eval      xvalbe = wwvalb - wtvalb
     c                   eval      xmengd = wwmngd - wtmngd
      *
     c                   if        xbelØp <= 1
     c                   eval      rtbelØ = rtbelØ + xbelØp
     c                   eval      wtbelØ = wtbelØ + xbelØp
     c                   eval      xbelØp = 0
     c                   endif
      *
     c                   if        xvalbe <= 1
     c                   eval      rtvalb = rtvalb + xvalbe
     c                   eval      wtvalb = wtvalb + xvalbe
     c                   eval      xvalbe = 0
     c                   endif
      *
     c                   if        xmengd <= 1
     c                   eval      rtmngd = rtmngd + xmengd
     c                   eval      wtmngd = wtmngd + xmengd
     c                   eval      xmengd = 0
     c                   endif
      *
     c                   endsr
      *
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
610   *
610   * Parameterliste
610   *
610  c     *entry        plist
610  c                   parm                    w_memb
610  c                   parm                    w_memx
     c                   eval      w_firm_gml = *loval
     c                   eval      w_bunt_gml = *loval
6.11 c                   eval      w_firm     = l_firm
      *
      * Key for fordelings-parametre
610  c     rtral1_key    klist
610  c                   kfld                    rtral1_nivo
610  c                   kfld                    rtral1_memb
610  c                   kfld                    w_firm
610  c                   kfld                    rtral1_bunt
610  c                   kfld                    rtral1_linj
      *
     c     rforl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rforl1_kont
     c                   kfld                    rforl1_spes
     c                   kfld                    rforl1_avde
      *
      * Key for buntregister
      *
     c     rbunl1_key    klist
610  c                   kfld                    rbunl1_nivo
610  c                   kfld                    rbunl1_memb
     c                   kfld                    w_firm
     c                   kfld                    rbunl1_bunt
      *
      * Key for statusopplysninger
      *
     c     raa1pf_key    klist
     c                   kfld                    w_firm
      *
      *
T5.30 * Key for bilagskode
      *
T5.30c     ra01l1_key    klist
T5.30c                   kfld                    w_firm
T5.30c                   kfld                    ra01l1_kod
6.32  *
6.32  *   Setter periode på transer = periode fra bunt
6.32  *
6.32 c                   eval      rtral1_nivo = *blank
6.32 c                   eval      rtral1_memb = w_memx
6.32 c                   eval      rtral1_bunt = *zero
6.32 c     rtral1_key    setll     rtral1
6.32 c                   read      rtral1
6.32  *
6.32 c                   dow       not %eof(rtral1)
6.32 c                             and rtmemb = w_memx
6.32 c                   if        rsmemb <> w_memx
6.32 c                             or rsbunt <> rtbunt
6.32 c                   eval      rbunl1_memb = rtmemb
6.32 c                   eval      rbunl1_bunt = rtbunt
6.32 c     rbunl1_key    chain     rbunl1
6.32 c                   endif
6.32  *
6.32 c                   if        rtmemb = w_memx
6.32 c                   eval      rtraar = rsraar
6.32 c                   eval      rtrper = rsrper
6.32 c                   update    rtral1r
6.32 c                   endif
6.32 c                   read      rtral1
6.32 c                   enddo
      *
7.01  * Test om logging skal gjøres
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'RF-PROG LOGGING':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_logg = *on;
7.01   endif;
     c                   endsr
