     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VP757R
      * Beskrivelse . . : Sjekker om enhet er gyldig innkj.enhet for
      *                   leverandør
      *
      *                   Input:  Firma, vare, enhet, leverandør
      *                   Output: Status = 1 = ok
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       020118 bof  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvenpf    if   e           k disk    rename(vvenpfr:vvenpfr)
6.31 fvveppf    if   e           k disk    rename(vveppfr:vveppfr)
      *
      * Definering av pamametre
      *
     d p_firm          s                   like(vefirm)
     d p_vare          s                   like(vevare)
5.70 d p_enhe          s                   like(veenhe)
5.70 d p_ldor          s                   like(vepldo)
5.70 d p_stat          s              1
      *
      * Definering av variable i nøkler
      *
      * Definering av variable
      *
6.20 d b_eof           s              1    inz(*off)
6.20 d b_ok            s              1    inz(*off)
      *
     d w_firm          s                   like(vefirm)
     d w_dato          s                   like(vepeda)
6.20 d w_ldor          s                   like(vepldo)
6.20 d w_lage          s              2  0
      *
      *
6.20 d                uds
6.20 d l_lage               1001   1002  0
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * skal bruke bestillingens leverandør
6.32 c                   eval      p_stat = *Blank
6.32 c                   eval      w_ldor = p_ldor
      *
      *

6.31  *
6.31  * sjekker om enhet ligger som innkjøpsenheter på vare-enhet-pris
6.31  *
6.31 c                   exsr      sjekk_vvep
     c                   if        b_ok = *on
     c                   eval      p_stat = '1'
     c                   endif
6.31  *
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
6.31  *
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  * Subrutine for å sjekke om enhet er en bestillbar enhet på vare-lev-enhet
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *
6.31 c     sjekk_vvep    begsr
6.31  *
6.31 c/exec sql
6.31 c+                  declare sok_o cursor for
6.31 c+                  select vepvar,
6.31 c+                         vepenh,
6.31 c+                         vepbes,
6.31 c+                         vevare,
6.31 c+                         veomre,
6.31 c+                         veenhe,
6.31 c+                         veinen
6.31 c+                  from   vveppf
6.31 c+                  inner  join vvenpf
6.31 c+                  on     vepfir = vefirm and
6.31 c+                         vepvar = vevare and
6.31 c+                         vepenh = veenhe
6.31 c+                  where  vepfir = :w_firm and
6.31 c+                         vepldo = :w_ldor and
6.31 c+                         vepvar = :p_vare and
6.31 c+                         vepbes = 1       and
6.34 c+                         vepgti <> 0
6.31 c+                  order by vepvar, veinen
6.31 c+                  for read only
6.31 c/end-exec
6.31 c/exec sql
6.31 c+                  close sok_o
6.31 c/end-exec
6.31 c/exec sql
6.31 c+                  open sok_o
6.31 c/end-exec
6.31  *
6.31  *
6.31 c     fetch_vare    tag
     c                   eval      b_eof = *off
      *
     c                   dow       b_eof = *off
6.31  *
6.31 c/exec sql
6.31 c+                  fetch next
6.31 c+                  from  sok_o
6.31 c+                  into  :vepvar,
6.31 c+                        :vepenh,
6.31 c+                        :vepbes,
6.31 c+                        :vevare,
6.31 c+                        :veomre,
6.31 c+                        :veenhe,
6.31 c+                        :veinen
6.31 c/end-exec
6.31  *
6.31 c                   if        sqlcod = 100 or sqlstt = '02000'
6.31 c                   eval      b_eof = *on
6.31 c/exec sql
6.31 c+                  close sok_o
6.31 c/end-exec
     c                   iter
6.31 c                   endif
     c                   if        p_enhe = vepenh
     c                   eval      b_ok = *on
     c                   leavesr
     c                   endif
6.31 c                   enddo
6.31 c                   endsr
6.31  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
5.70 c                   parm                    p_enhe
     c                   parm                    p_ldor
     c                   parm                    p_stat
      *
     c                   eval      w_firm = p_firm
      *
      * Initierer output-parametre
      *
     c                   endsr
