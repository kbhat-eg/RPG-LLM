     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASNAVIOKO
      * Program . . . . : IT110R
      * Beskrivelse . . : Oppdaterer kundesaldo fra NAV
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       071114 ask  Nytt program
7.01  *  7.01 281020 ask  Lagt inn oppdatering av 2DP flagg på kunderegister
8.01  *  800  070521 ask  Lagt inn LR styring for å øke hastigheten
8.02  *  800  111021 ask  Endret på logikk for innlesning av saldofelt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunlu    uf   e           k disk    rename(rkunpfr:rkunlur)
      *
      * Parameter-inn
      *
     d p_firm          s                   like(rkfirm)
     d p_comd          s             20
     d p_parm          s           2048a
     d p_user          s             10
     d p_stat          s              1
8.01 d p_inlr          s              1
      *
      * Definering av variabler i nøkler
      *
     d rkunlu_kund     s                   like(rkkund)
      *
      *  Definering av datastrukturer for parameter
      *
     d d_parm          ds          2048
     d  d_kund                        6
     d  d_sald                       12
      *
      * Definering av variable
      *
     d w_firm          s                   like(rkfirm)
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_kund          s                   like(rkkund)
     d w_sald          s                   like(rksald)
     d w_stat          s              1
     d w_hela          s              9
     d w_heln          s              9  0
     d w_desa          s              2
     d w_desn          s              2  0
     d w_desm          s              2  2
8.02 d w_pos1          s              2  0
8.02 d w_pos2          s              2  0
     d w_strt          s              1  0 inz(1)
7.00 d w_2dpt          s               z
      *
     d b_sign          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram - oppdaterer saldo på kunde, setter statusflagg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
8.01  *
8.01  * Sjekk om det er "siste runde" for å lukke tabeller
8.01  *
8.01 c                   if        p_inlr = '1'
8.01 c                   eval      *inlr = *on
8.01 c                   goto      slutt
8.01 c                   endif
      *
      * Omformer felter fra parameterfelt til variable
      *
8.02 c                   eval      w_pos1 = 0
8.02 c                   eval      w_pos2 = 0
8.02 c                   eval      w_strt = 1
8.02 c                   eval      b_sign = *off
     c                   eval      d_parm = p_parm
      *
     c                   if        d_sald = *blanks
     c     ' ':'0'       xlate     d_sald        d_sald
     c                   endif
      *
     c                   eval      w_kund = %dec(d_kund:6:0)
      *
8.02 c                   eval      w_pos1 = %scan('-':d_sald)
8.02 c                   if        w_pos1 > 0
     c                   eval      b_sign = *on
     c                   eval      w_strt = 2
     c                   endif
      *
8.02 c                   eval      w_pos2 = %scan('.':d_sald)
8.02 c                   eval      w_hela = %subst(d_sald:w_strt:w_pos2-w_strt)
8.02 c                   eval      w_desa = %subst(d_sald:w_pos2+1:2)
     c                   eval      w_heln = %dec(w_hela:9:0)
     c                   eval      w_desn = %dec(w_desa:2:0)
     c                   eval      w_desm = w_desn / 100
     c                   eval      w_sald = w_heln + w_desm
     c                   if        b_sign = *on
     c                   eval      w_sald = w_sald * -1
     c                   endif
      *
      * Oppdaterer kundepost
      *
     c                   eval      w_firm = p_firm
     c                   eval      rkunlu_kund = w_kund
     c     rkunlu_key    chain     rkunlu
     c                   if        %found
     c                   eval      rksald = w_sald
     c                   eval      rkedat = w_date
     c                   eval      rketim = w_time
     c                   eval      rkesig = p_user
7.00 c                   eval      rk2dpt = w_2dpt
     c                   update    rkunlur
     c                   eval      p_stat = '1'
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slutt         tag
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      p_stat = ' '
     c                   eval      *inlr = *on
     c                   return
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_comd
     c                   parm                    p_parm
     c                   parm                    p_user
     c                   parm                    p_stat
8.01 c                   parm                    p_inlr
      *
      * Key for posisjonering på valgt kode
      *
     c     rkunlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunlu_kund
      *
      * Setter "timestamp" for postoppdateringer
      *
     c                   time                    w_date
     c                   time                    w_time
7.00 c                   time                    w_2dpt
      *
     c                   endsr
