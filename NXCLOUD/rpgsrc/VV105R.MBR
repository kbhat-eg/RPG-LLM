     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VV105R
      * Beskrivelse . . : Vare, vedlikehold - prisjustering
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       111199 HKO  Nytt program
      *       050199 HKO  Endret lengde på parametre ved kall av VP800R
6.20  *       010411 bof  Utvidet med prisgiver,default vvldor
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.20 fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
6.20 fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
     fvv105d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_vare          s             15
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
     d b_inlr          s              1    inz(*off)
6.20 d b_ldor          s              1    inz(*off)
      *
     d w_udat          s               d   datfmt(*iso)
     d w_dato          s               d   datfmt(*iso)
     d w_spro          s              7  4
     d w_kpro          s              7  4
     d w_ipro          s              7  4
     d w_bpro          s              7  4
     d w_dekn          s              5  2
6.20 d w_firm          s                   like(vvfirm)
6.20 d w_vare          s                   like(vvvare)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.20 c     vvarl1_key    chain     vvarl1
      *
      * Initierer skjermbildet
      *
     c                   time                    w_udat
     c     *dmy          move      w_udat        b1dato
6.20 c                   eval      b1ldor = vvldor
      *
      * Behandler bildet
      *
     c     b1taga        tag
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   goto      avslutt
     c                   endif
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
      * Kaller oppdateringsprogram
      *
     c     *dmy          move      b1dato        w_dato
     c                   eval      b_inlr = *off
      *
     c                   eval      w_spro = b1spro
     c                   eval      w_kpro = b1kpro
     c                   eval      w_ipro = b1ipro
     c                   eval      w_bpro = b1bpro
     c                   eval      w_dekn = b1dekn
      *
     c                   call      'VP800R'
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    w_dato
     c                   parm                    w_spro
     c                   parm                    w_kpro
     c                   parm                    w_ipro
     c                   parm                    w_bpro
     c                   parm                    w_dekn
     c                   parm                    b_inlr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
6.20  *
6.20  * leverandør
6.20  *
6.20 c                   eval      b_ldor = *off
6.20 c     vvprl1_key    setll     vvprl1
6.20 c     vvprl1_key    reade     vvprl1
6.20 c                   dow       not %eof(vvprl1)
6.20 c                   if        vpldor = b1ldor
6.20 c                   eval      b_ldor = *on
6.20 c                   endif
6.20 c     vvprl1_key    reade     vvprl1
6.20 c                   enddo
6.20 c                   if        b_ldor = *off
6.20 c                   eval      b_feil = *on
6.20 c                   eval      *in35 = *on
6.20 c                   goto      end_sjekk                                                 .
6.20 c                   endif
      *
      * Dato
      *
     c                   if        b1dato = 0
     c     *dmy          move      w_udat        b1dato
     c                   endif
      *
     c     *dmy          test(d)                 b1dato                 31
     c                   if        *in31  = *on                                          .....
     c                   eval      b_feil = *on
     c                   goto      end_sjekk                                                 .
     c                   endif                                                           .....
      *
     c     *dmy          move      b1dato        w_dato
     c                   if        w_dato < w_udat
     c                   eval      *in32  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk                                                 .
     c                   endif
      *
      * Justering
      *
     c                   if        b1spro = 0 and
     c                             b1kpro = 0 and
     c                             b1ipro = 0 and
     c                             b1bpro = 0 and
     c                             b1dekn = 0
     c                   eval      *in33  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   if        b1spro <> 0 and
     c                             b1dekn <> 0
     c                   eval      *in34  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
6.20  *
6.20  * Key for oppslag på vare
6.20  *
6.20 c     vvarl1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    w_vare
6.20  *
6.20  * Key for oppslag på vare-pris
6.20  *
6.20 c     vvprl1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    w_vare
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
      *
6.20 c                   eval      w_firm = p_firm
6.20 c                   eval      w_vare = p_vare
     c                   endsr
