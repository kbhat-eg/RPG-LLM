     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JP720R
      * Beskrivelse . . : Rabattelement, kalkuler faktor og nettopris
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       140898 HKO  Nytt program
      *       110202 HKO  Tar hensyn til aktivisering av rabattelement
      *       130605 HKO  Utvidet nivå med varetype og leveringstype
6.10  *       040211 bof  Takle hvis faktor blir over 999
6.3x  *       100615 bof  Utvidet med prisgruppe
8.01  *PRG2   200622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjrael1    if   e           k disk    rename(jraepfr:jrael1r)
     fjrael4    if   e           k disk    rename(jraepfr:jrael4r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(jpefir)
     d p_eldo          s                   like(jpeldo)
6.3x d p_eprg          s                   like(jpeprg)
     d p_eogr          s                   like(jpeogr)
     d p_ehgr          s                   like(jpehgr)
     d p_eugr          s                   like(jpeugr)
     d p_emod          s                   like(jpemod)
     d p_evar          s                   like(jpevar)
     d p_evty          s                   like(jpevty)
     d p_elet          s                   like(jpelet)
     d p_ebrs          s                   like(jpebrs)
     d p_ebli          s                   like(jpebli)
      *
     d p_fakt          s              8  5
6.10 d w_fakt          s             15  5
     d p_ntpr          s              9  2
      *
      * Definering av variable i nøkler
      *
     d jrael1_eldo     s                   like(jpeldo)
6.3x d jrael1_eprg     s                   like(jpeprg)
     d jrael1_eogr     s                   like(jpeogr)
     d jrael1_ehgr     s                   like(jpehgr)
     d jrael1_eugr     s                   like(jpeugr)
     d jrael1_emod     s                   like(jpemod)
     d jrael1_evar     s                   like(jpevar)
     d jrael1_evty     s                   like(jpevty)
     d jrael1_elet     s                   like(jpelet)
     d jrael1_ebrs     s                   like(jpebrs)
     d jrael1_ebli     s                   like(jpebli)
     d jrael4_eldo     s                   like(jpeldo)
6.3x d jrael4_eprg     s                   like(jpeprg)
     d jrael4_eogr     s                   like(jpeogr)
     d jrael4_ehgr     s                   like(jpehgr)
     d jrael4_eugr     s                   like(jpeugr)
     d jrael4_emod     s                   like(jpemod)
     d jrael4_evar     s                   like(jpevar)
     d jrael4_evty     s                   like(jpevty)
     d jrael4_elet     s                   like(jpelet)
     d jrael4_ebrs     s                   like(jpebrs)
     d jrael4_ebli     s                   like(jpebli)
     d jrael4_eakt     s                   like(jpeakt)
      *
      * Definering av variable
      *
     d b_akti          s              1    inz(*off)
      *
     d w_firm          s                   like(jpefir)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Dersom det finnes aktiviserte rabattelement under betingelsen er
      * det kun disse som skal benyttes.
      *
     c                   eval      jrael4_eldo = p_eldo
6.3x c                   eval      jrael4_eprg = p_eprg
     c                   eval      jrael4_eogr = p_eogr
     c                   eval      jrael4_ehgr = p_ehgr
     c                   eval      jrael4_eugr = p_eugr
     c                   eval      jrael4_emod = p_emod
     c                   eval      jrael4_evar = p_evar
     c                   eval      jrael4_evty = p_evty
     c                   eval      jrael4_elet = p_elet
     c                   eval      jrael4_ebrs = p_ebrs
     c                   eval      jrael4_ebli = p_ebli
     c                   eval      jrael4_eakt = 1
     c     jrael4_key    chain     jrael4
     c                   if        %found
     c                   eval      b_akti = *on
     c                   endif
      *
      * Kalkulerer faktor og/eller nettopris for betingelse
      *
     c                   eval      p_fakt = 1
6.10 c                   eval      w_fakt = 1
     c                   eval      p_ntpr = 0
      *
     c                   eval      jrael1_eldo = p_eldo
6.3x c                   eval      jrael1_eprg = p_eprg
     c                   eval      jrael1_eogr = p_eogr
     c                   eval      jrael1_ehgr = p_ehgr
     c                   eval      jrael1_eugr = p_eugr
     c                   eval      jrael1_emod = p_emod
     c                   eval      jrael1_evar = p_evar
     c                   eval      jrael1_evty = p_evty
     c                   eval      jrael1_elet = p_elet
     c                   eval      jrael1_ebrs = p_ebrs
     c                   eval      jrael1_ebli = p_ebli
     c     jrael1_key    setll     jrael1
     c     jrael1_key    reade     jrael1
     c                   dow       not %eof
     c                   if        b_akti = *off or
     c                             b_akti = *on and
     c                             jpeakt = 1
     c                   select
     c                   when      jpereg = '1'
6.10 c                   eval      w_fakt = w_fakt - (jpever / 100 * w_fakt)
6.10 c*                  eval      p_fakt = p_fakt - (jpever / 100 * p_fakt)
     c                   when      jpereg = '2'
6.10 c                   eval      w_fakt = w_fakt - (jpever / 100)
6.10 c*                  eval      p_fakt = p_fakt - (jpever / 100)
     c                   when      jpereg = 'N'
     c                   eval      p_ntpr = p_ntpr + jpever
     c                   endsl
     c                   endif
     c     jrael1_key    reade     jrael1
     c                   enddo
6.10  *
6.10 c                   if        w_fakt <> 0
6.10 c                   if        %abs(w_fakt) < 1000
6.10 c                   eval      p_fakt = w_fakt
6.10 c                   else
6.10 c                   eval      p_fakt = 999,99999
6.10 c                   endif
6.10 c                   else
6.10 c                   eval      p_fakt = w_fakt
6.10 c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_eldo
6.3x c                   parm                    p_eprg
     c                   parm                    p_eogr
     c                   parm                    p_ehgr
     c                   parm                    p_eugr
     c                   parm                    p_emod
     c                   parm                    p_evar
     c                   parm                    p_evty
     c                   parm                    p_elet
     c                   parm                    p_ebrs
     c                   parm                    p_ebli
     c                   parm                    p_fakt
     c                   parm                    p_ntpr
      *
      * Key for les på rabattelement
      *
     c     jrael1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jrael1_eldo
6.3x c                   kfld                    jrael1_eprg
     c                   kfld                    jrael1_eogr
     c                   kfld                    jrael1_ehgr
     c                   kfld                    jrael1_eugr
     c                   kfld                    jrael1_emod
     c                   kfld                    jrael1_evar
     c                   kfld                    jrael1_evty
     c                   kfld                    jrael1_elet
     c                   kfld                    jrael1_ebrs
     c                   kfld                    jrael1_ebli
      *
      * Key for oppslag på rabattelement
      *
     c     jrael4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jrael4_eldo
6.3x c                   kfld                    jrael4_eprg
     c                   kfld                    jrael4_eogr
     c                   kfld                    jrael4_ehgr
     c                   kfld                    jrael4_eugr
     c                   kfld                    jrael4_emod
     c                   kfld                    jrael4_evar
     c                   kfld                    jrael4_evty
     c                   kfld                    jrael4_elet
     c                   kfld                    jrael4_ebrs
     c                   kfld                    jrael4_ebli
     c                   kfld                    jrael4_eakt
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
