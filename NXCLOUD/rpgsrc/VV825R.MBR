     h option(*nodebugio : *srcstmt) datedit(*dmy)

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VV824R
      * Beskrivelse . . : Oppdater fra SOLVE pris
      *
      *                   Oppdaterer pris SOLVE (XML-format)
      *                   Det kommer priser på basisenhet
      *                   Regnes om til alle enheter på varen.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       020519 BKV
      *       131219 bof  Endret til ny xml, utivdet med div. logikk.
      *       240120 hko  Endret div. logikk etter testing
7.00  *       130820 hko  Leverandør reskontronummer hentes på bakgrunn
      *                   av mottatt Nobb deltakernr
7.01  *       210820 bof  Feilretting vedr. finne enheter på inkjøpsenheter
7.02  *       290121 bof  Finner også enheter som ikke er innkj/salgsenh.
      *
8.01  *PRG2   140622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
8.01  *PRG2               Endringer består kun i kompilering, da alle
8.01  *PRG2               referanser er eksternt definert.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvprlu    uf a e           k disk    rename(vvprpfr:vvprlur)
7.00 frlevlb    if   e           k disk    rename(rlevpfr:rlevlbr)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fjstsl1    if   e           k disk    rename(jstspfr:jstsl1r)
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
     fvvenl4    if   e           k disk    rename(vvenpfr:vvenl4r)
     fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
      *
      * Definering av variable i nøkler
      *
     d vvprlu_vare     s                   like(vpvare)
     d vvprlu_ldor     s                   like(vpldor)
     d vvprlu_prgr     s                   like(vpprgr)
     d vvprlu_enhe     s                   like(vpenhe)
     d vvprlu_lety     s                   like(vplety)
     d vvprlu_pdat     s                   like(vppdat)
7.00 d rlevlb_nobb     s                   like(rlnobb)
     d vvenl1_vare     s                   like(vevare)
     d vvenl1_enhe     s                   like(veenhe)
     d vvenl2_vare     s                   like(vevare)
     d vvenl2_saen     s                   like(vesaen)
     d vvenl4_vare     s                   like(vevare)
     d vvenl4_inen     s                   like(veinen)
     d vvprl1_vare     s                   like(vpvare)
     d vvprl1_ldor     s                   like(vpldor)
     d vvprl1_prgr     s                   like(vpprgr)
     d vvprl1_enhe     s                   like(vpenhe)
     d vvprl1_lety     s                   like(vplety)
      *
      * Brukes som parametre til jobblogging
      *
     d p_jnav          s             15
     d p_jbid          s             41
     d p_jtxt          s             35
     d p_jsts          s              2  0
     d p_jmld          s            200
      *
      * Definering av Local Data Area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
     d w_firm          s              3  0
     d w_dato          s                   like(vpedat)
      *
      * Definering av parametre
      *
     D p_filNavn       s            255a
     D p_lengde        s              3p 0
     d p_sistDato      s             10a
     d p_stat          s              1
      *
      * Definering av variable
      *
     d x_vare          s             15
7.00 d x_nldo          s             20
7.00 d x_ldor          s             20
     d x_prgr          s             20
     d x_enhe          s              3
     d x_lety          s             20
     d x_pdat          s             19
     d x_sapr          s             20
     d x_kopr          s             20
     d x_inpr          s             20
     d x_inp2          s             20
     d x_bupr          s             20
     d x_nopr          s             20
     d x_lvar          s             20
      *
7.00 d w_enum          s                   like(rllevr)
     d w_enh1          s                   like(veenhe)
     d w_enh2          s                   like(veenhe)
     d w_enh3          s                   like(veenhe)
     d w_enh4          s                   like(veenhe)
     d w_enh5          s                   like(veenhe)
     d w_enhp          s                   like(veenhe)
     d w_omr1          s                   like(veomre)
     d w_omr2          s                   like(veomre)
     d w_omr3          s                   like(veomre)
     d w_omr4          s                   like(veomre)
     d w_omr5          s                   like(veomre)
     d w_omre          s                   like(veomre)
     d w_sapr          s                   like(vpsapr)
     d w_kopr          s                   like(vpkopr)
     d w_inpr          s                   like(vpinpr)
     d w_inp2          s                   like(vpinpr)
     d w_bupr          s                   like(vpbupr)
     d w_nopr          s                   like(vpnopr)
     d s_sapr          s                   like(vpsapr)
     d s_kopr          s                   like(vpkopr)
     d s_inpr          s                   like(vpinpr)
     d s_inp2          s                   like(vpinpr)
     d s_nopr          s                   like(vpnopr)
      *
     d b_pris          s              1    inz(*off)
      *
     d d_nlev          s              6  0
     d d_lety          s              1  0
     d d_pdat          s               d   datfmt(*iso)
     d d_sapr          s              9  2
     d d_kopr          s              9  2
     d d_inpr          s              9  2
     d d_inp2          s              9  2
     d d_bupr          s              9  2
     d d_nopr          s              9  2
     d d_lvar          s                   like(vplvar)
      *
     D   navnet        s            100a   varying
     D   len           s              3i 0
      *
      * Definering av konstanter
      *
     d c_digi          s             10    inz('0123456789')
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
       len = p_lengde;
       navnet = %trim(%subst(p_filNavn:1:len));
       w_dato = %date;

       // setter navn på XML fil
       exec sql SET PRIS_SOLVE_XML_V = :navnet;

      *
       exec sql close c1;
       // bruk view til å lese XML fil
       exec sql
         declare c1 cursor for
            select
                 ArticleNo
                 ,SupplierNo
                 ,PriceGroup
                 ,UnitCode
                 ,ValidFrom
                 ,SalesPrice
                 ,CostPrice
                 ,purchasePriceExShipping
                 ,PurchasePriceIncShipping
                 ,BasePrice
                 ,SupplierArticleNumber
7.00             ,IFNULL(jlenum, 0)
                 from PRIS_XML_SOLVE_VIEW
                 LEFT OUTER JOIN JLEVPF ON
                 JLLDOR  = SupplierNo
                for read only;

       exec sql
         Open c1;

       doW (1 = 1);

         // FIXME  verdi kommer fra solve ?
         x_lety = '0';
         x_inp2 = '0';
         x_bupr = '0';

         exec sql
           Fetch c1 INTO
                 :x_vare
7.00            ,:x_nldo
                ,:x_prgr
                ,:x_enhe
                ,:x_pdat
                ,:x_sapr
                ,:x_kopr
                ,:x_inpr
                ,:x_inp2
                ,:x_nopr
                ,:x_lvar
7.00            ,:x_ldor
                ;

         if ((SQLCode < 0) or (SQLCode = 100));
           leave;
         endif;

         if %check(c_digi : %trim(x_vare)) = 0;
           exsr skr_pris;
         endif;

       enddo;

       exec sql close c1;

       p_sistDato = %char(%date(): *iso);  // OK

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
      * Vi kvitterer ut jobben fra jobbstatusfil
     c                   if        p_jnav <> *blank
     c                   call      'AB710R'
     c                   parm                    p_jnav
     c                   parm                    p_jbid
     c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Start Logg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     start_logg    begsr
      *
      * Vi starter jobblogging
      *
     c                   if        p_jnav = *blank
     c                   eval      p_jnav = 'VV825R'
     c                   eval      p_jtxt = *blank
     c                   call      'AB700R'
     c                   parm                    p_jnav
     c                   parm                    p_jbid
     c                   parm                    p_jtxt
     c                   endif
     c
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer pris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_pris      begsr
      *
7.00  *  sjekk om NLDOR er oppgitt
7.00 c                   if        x_nldo = '0'
     c                   exsr      start_logg
     c                   eval      p_jsts = 40
     c                   eval      p_jmld = *blanks
7.00 c                   eval      p_jmld = 'Mangler leverandør: V:'+x_vare+
7.00 c                              ' , L:' + x_nldo + '  , E:' + x_enhe
     c                   call      'AB705R'
     c                   parm                    p_jbid
     c                   parm                    p_jsts
     c                   parm                    p_jmld
     c                   leavesr
     c                   endif
7.00  *
7.00  * Henter reskontronummer på vareleverandør
      *
7.00 c                   eval      w_enum = 0
7.00 c                   eval      rlevlb_nobb = %dec(x_nldo:6:0)
7.00 c     rlevlb_key    chain     rlevlb
7.00 c                   if        %found
7.00 c                   eval      w_enum = rllevr
7.00 c                   else
7.00 c                   exsr      start_logg
7.00 c                   eval      p_jsts = 40
7.00 c                   eval      p_jmld = *blanks
7.00 c                   eval      p_jmld = 'Knytn. reskontr/NOBB-lev. mangler'+
7.00 c                                      ' i Nexstep ** skr_pris ** ' +
7.00 c                                      x_nldo
7.00 c                   call      'AB705R'
7.00 c                   parm                    p_jbid
7.00 c                   parm                    p_jsts
7.00 c                   parm                    p_jmld
7.00 c                   leavesr
7.00 c                   endif
7.00  *
      * Sjekk enhet finnes
      *
     c                   eval      vvenl1_vare = x_vare
     c                   eval      vvenl1_enhe = x_enhe
     c     vvenl1_key    chain     vvenl1r
     c                   if        not %found
     c                   exsr      start_logg
     c                   eval      p_jsts = 40
     c                   eval      p_jmld = *blanks
     c                   eval      p_jmld = 'Fant ikke enhet: V:' + x_vare +
7.00 c                              ' , L:' + x_nldo + '  , E:' +  x_enhe
     c                   call      'AB705R'
     c                   parm                    p_jbid
     c                   parm                    p_jsts
     c                   parm                    p_jmld
     c                   leavesr
     c                   endif
      *
     c                   exsr      finn_enhet
      *
     c                   eval      d_sapr = %Dech(x_sapr:9:2)
     c                   eval      d_bupr = %Dech(x_bupr:9:2)
     c                   eval      d_kopr = %Dech(x_kopr:9:2)
     c                   eval      d_inpr = %Dech(x_inpr:9:2)
     c                   if        (x_inp2 = '0')
     c                   eval      d_inp2 = %Dech(x_inpr:9:2)
     c                   else
     c                   eval      d_inp2 = %Dech(x_inp2:9:2)
     c                   endif
     c                   eval      d_nopr = %Dech(x_nopr:9:2)
7.00 c                   eval      d_nlev = %dec(x_nldo:6:0)
     c                   eval      d_lety = %dec(x_lety:1:0)
     c                   eval      d_pdat = %date(%subst(x_pdat:1:10):*ISO)
     c                   eval      d_lvar = %trim(x_lvar)
      *
     c                   if        w_enh1 <> *blank
     c                   eval      w_enhp = w_enh1
     c                   eval      w_omre = w_omr1
     c                   exsr      pris_enhet
     c                   endif
      *
     c                   if        w_enh2 <> *blank
     c                   eval      w_enhp = w_enh2
     c                   eval      w_omre = w_omr2
     c                   exsr      pris_enhet
     c                   endif
      *
     c                   if        w_enh3 <> *blank
     c                   eval      w_enhp = w_enh3
     c                   eval      w_omre = w_omr3
     c                   exsr      pris_enhet
     c                   endif
      *
     c                   if        w_enh4 <> *blank
     c                   eval      w_enhp = w_enh4
     c                   eval      w_omre = w_omr4
     c                   exsr      pris_enhet
     c                   endif
      *
     c                   if        w_enh5 <> *blank
     c                   eval      w_enhp = w_enh5
     c                   eval      w_omre = w_omr5
     c                   exsr      pris_enhet
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne inntil tre salgsenheter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_enhet    begsr
      *
      * Finner tre salgsenheter på varen
      *
     c                   eval      w_enh1 = *blank
     c                   eval      w_enh2 = *blank
     c                   eval      w_enh3 = *blank
     c                   eval      w_enh4 = *blank
     c                   eval      w_enh5 = *blank
     c                   eval      w_omr1 = 0
     c                   eval      w_omr2 = 0
     c                   eval      w_omr3 = 0
     c                   eval      w_omr4 = 0
     c                   eval      w_omr5 = 0
      *
     c                   eval      vvenl2_vare = x_vare
     c                   eval      vvenl2_saen = 1
     c     vvenl2_key    setll     vvenl2
     c     vvenl2_key2   reade     vvenl2
     c                   dow       not %eof and
     c                             vesaen > 0
      *
     c                   select
     c                   when      w_enh1 = *blank
     c                   eval      w_enh1 = veenhe
     c                   eval      w_omr1 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 = *blank
     c                   eval      w_enh2 = veenhe
     c                   eval      w_omr2 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 <> *blank and
     c                             w_enh3 = *blank
     c                   eval      w_enh3 = veenhe
     c                   eval      w_omr3 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 <> *blank and
     c                             w_enh3 <> *blank and
     c                             w_enh4 = *blank
     c                   eval      w_enh4 = veenhe
     c                   eval      w_omr4 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 <> *blank and
     c                             w_enh3 <> *blank and
     c                             w_enh4 <> *blank and
     c                             w_enh5 = *blank
     c                   eval      w_enh5 = veenhe
     c                   eval      w_omr5 = veomre
     c                   leave
     c                   endsl
      *
     c     vvenl2_key2   reade     vvenl2
     c                   enddo
      * henter også evt. innkjøps enheter som ikke er salgenheter
     c                   eval      vvenl4_vare = x_vare
     c                   eval      vvenl4_inen = 1
     c     vvenl4_key    setll     vvenl4
     c     vvenl4_key2   reade     vvenl4
     c                   dow       not %eof and
     c                             veinen > 0
7.01 c                   if        veenhe = w_enh1 or
7.01 c                             veenhe = w_enh2 or
7.01 c                             veenhe = w_enh3 or
7.01 c                             veenhe = w_enh4 or
7.01 c                             veenhe = w_enh5
7.01 c                   goto      nest_enh
7.01 c                   endif
      *
     c                   select
     c                   when      w_enh1 = *blank
     c                   eval      w_enh1 = veenhe
     c                   eval      w_omr1 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 = *blank
     c                   eval      w_enh2 = veenhe
     c                   eval      w_omr2 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 <> *blank and
     c                             w_enh3 = *blank
     c                   eval      w_enh3 = veenhe
     c                   eval      w_omr3 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 <> *blank and
     c                             w_enh3 <> *blank and
     c                             w_enh4 = *blank
     c                   eval      w_enh4 = veenhe
     c                   eval      w_omr4 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 <> *blank and
     c                             w_enh3 <> *blank and
     c                             w_enh4 <> *blank and
     c                             w_enh5 = *blank
     c                   eval      w_enh5 = veenhe
     c                   eval      w_omr5 = veomre
     c                   leave
     c                   endsl
7.01 c     nest_enh      tag
7.01  *
     c     vvenl4_key2   reade     vvenl4
     c                   enddo
7.02  * henter også evt. enheter som ikke er innkj/salgsenh
7.02 c                   eval      vvenl2_vare = x_vare
7.02 c                   eval      vvenl2_saen = 0
7.02 c     vvenl2_key    setll     vvenl2
7.02 c     vvenl2_key2   reade     vvenl2
7.02 c                   dow       not %eof and
7.02 c                             vesaen = 0
7.02 c                   if        veenhe = w_enh1 or
7.02 c                             veenhe = w_enh2 or
7.02 c                             veenhe = w_enh3 or
7.02 c                             veenhe = w_enh4 or
7.02 c                             veenhe = w_enh5
7.02 c                   goto      nest_en0
7.02 c                   endif
7.02  *
7.02 c                   select
7.02 c                   when      w_enh1 = *blank
7.02 c                   eval      w_enh1 = veenhe
7.02 c                   eval      w_omr1 = veomre
7.02 c                   when      w_enh1 <> *blank and
7.02 c                             w_enh2 = *blank
7.02 c                   eval      w_enh2 = veenhe
7.02 c                   eval      w_omr2 = veomre
7.02 c                   when      w_enh1 <> *blank and
7.02 c                             w_enh2 <> *blank and
7.02 c                             w_enh3 = *blank
7.02 c                   eval      w_enh3 = veenhe
7.02 c                   eval      w_omr3 = veomre
7.02 c                   when      w_enh1 <> *blank and
7.02 c                             w_enh2 <> *blank and
7.02 c                             w_enh3 <> *blank and
7.02 c                             w_enh4 = *blank
7.02 c                   eval      w_enh4 = veenhe
7.02 c                   eval      w_omr4 = veomre
7.02 c                   when      w_enh1 <> *blank and
7.02 c                             w_enh2 <> *blank and
7.02 c                             w_enh3 <> *blank and
7.02 c                             w_enh4 <> *blank and
7.02 c                             w_enh5 = *blank
7.02 c                   eval      w_enh5 = veenhe
7.02 c                   eval      w_omr5 = veomre
7.02 c                   leave
7.02 c                   endsl
7.02 c     nest_en0      tag
7.02  *
7.02 c     vvenl2_key2   reade     vvenl2
7.02 c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle hver enhet i prisregisteret
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     pris_enhet    begsr
      *
      * sjekker om prisen kan være for høy
      * test salgsris
     c                   if        d_sapr > 9999999,99 or
     c                             (d_sapr * w_omre) > 9999999,99
     c                   exsr      start_logg
     c                   eval      p_jsts = 40
     c                   eval      p_jmld = *blanks
     c                   eval      p_jmld = 'Kalk. for høy innkj.pris'+x_vare+
7.00 c                              ' , L:' + x_nldo +'  , E:' +  w_enhp
     c                   call      'AB705R'
     c                   parm                    p_jbid
     c                   parm                    p_jsts
     c                   parm                    p_jmld
     c                   leavesr
     c                   endif
      * test kostpris
     c                   if        d_kopr > 9999999,99  or
     c                             (d_kopr * w_omre) > 9999999,99
     c                   exsr      start_logg
     c                   eval      p_jsts = 40
     c                   eval      p_jmld = *blanks
     c                   eval      p_jmld = 'Kalk. for høy kost.pris'+x_vare+
7.00 c                              ' , L:' + x_nldo +'  , E:' +  w_enhp
     c                   call      'AB705R'
     c                   parm                    p_jbid
     c                   parm                    p_jsts
     c                   parm                    p_jmld
     c                   leavesr
     c                   endif
      * test innkjøpspris
     c                   if        d_inpr > 9999999,99 or
     c                             (d_inpr * w_omre) > 9999999,99
     c                   exsr      start_logg
     c                   eval      p_jsts = 40
     c                   eval      p_jmld = *blanks
     c                   eval      p_jmld = 'Kalk. for høy innkj.pris'+x_vare+
7.00 c                              ' , L:' + x_nldo +'  , E:' +  w_enhp
     c                   call      'AB705R'
     c                   parm                    p_jbid
     c                   parm                    p_jsts
     c                   parm                    p_jmld
     c                   leavesr
     c                   endif
      * test innkjøpspris2
     c                   if        d_inp2 > 9999999,99 or
     c                             (d_inp2 * w_omre) > 9999999,99
     c                   exsr      start_logg
     c                   eval      p_jsts = 40
     c                   eval      p_jmld = *blanks
     c                   eval      p_jmld = 'Kalk. for høy innkj.pris2'+x_vare+
7.00 c                              ' , L:' + x_nldo +'  , E:' +  w_enhp
     c                   call      'AB705R'
     c                   parm                    p_jbid
     c                   parm                    p_jsts
     c                   parm                    p_jmld
     c                   leavesr
     c                   endif
      * test nobbpris
     c                   if        d_nopr > 9999999,99 or
     c                             (d_nopr * w_omre) > 9999999,99
     c                   exsr      start_logg
     c                   eval      p_jsts = 40
     c                   eval      p_jmld = *blanks
     c                   eval      p_jmld = 'Kalk. for høy nobb.pris'+x_vare+
7.00 c                              ' , L:' + x_nldo +'  , E:' +  w_enhp
     c                   call      'AB705R'
     c                   parm                    p_jbid
     c                   parm                    p_jsts
     c                   parm                    p_jmld
     c                   leavesr
     c                   endif
      * test butikk-pris
     c                   if        d_bupr > 9999999,99 or
     c                             (d_bupr * w_omre) > 9999999,99
     c                   exsr      start_logg
     c                   eval      p_jsts = 40
     c                   eval      p_jmld = *blanks
     c                   eval      p_jmld = 'Kalk. for høy butikkpris'+x_vare+
7.00 c                              ' , L:' +  x_nldo +'  , E:' +  w_enhp
     c                   call      'AB705R'
     c                   parm                    p_jbid
     c                   parm                    p_jsts
     c                   parm                    p_jmld
     c                   leavesr
     c                   endif
      *
     c                   eval(h)   w_sapr = d_sapr * w_omre
     c                   eval      w_kopr = d_kopr * w_omre
     c                   eval      w_inpr = d_inpr * w_omre
     c                   eval      w_inp2 = d_inp2 * w_omre
     c                   eval      w_nopr = d_nopr * w_omre
     c                   eval      w_bupr = d_bupr * w_omre
      *
      * sjekker om det er reell  prisendring
      *
     c                   exsr      finn_gml_pr
      *
     c                   if        b_pris = *on and
     c                             w_sapr = s_sapr and
     c                             w_kopr = s_kopr and
     c                             w_nopr = s_nopr and
     c                             w_inpr = s_inpr and
     c                             w_inp2 = s_inp2
     c                   goto      end_pris
     c                   endif
      *
     c                   clear                   vvprlur
      *
     c                   eval      vvprlu_vare = x_vare
7.00 c                   eval      vvprlu_ldor = w_enum
     c                   eval      vvprlu_prgr = x_prgr
     c                   eval      vvprlu_enhe = w_enhp
     c                   eval      vvprlu_lety = d_lety
     c                   eval      vvprlu_pdat = d_pdat
     c     vvprlu_key    chain     vvprlur
      *
     c                   eval      vpnopr = w_nopr
     c                   eval      vpsapr = w_sapr
     c                   eval      vpkopr = w_kopr
     c                   eval      vpinpr = w_inpr
     c                   eval      vpinp2 = w_inp2
     c                   eval      vpbupr = w_bupr
     c                   eval      vpnlev = d_nlev
     c                   eval      vplvar = %trim(d_lvar)
     c                   eval      vpedat = %date
     c                   eval      vpetim = %time
     c                   eval      vpeusr = l_user
      *
     c                   if        %found(vvprlu)
     c                   update    vvprlur
     c                   else
     c                   eval      vpfirm = w_firm
     c                   eval      vpvare = vvprlu_vare
     c                   eval      vpldor = vvprlu_ldor
     c                   eval      vpprgr = vvprlu_prgr
     c                   eval      vpenhe = vvprlu_enhe
     c                   eval      vplety = vvprlu_lety
     c                   eval      vppdat = vvprlu_pdat
     c                   eval      vpodat = %date
     c                   eval      vpotim = %time
     c                   eval      vpousr = l_user
     c                   write     vvprlur
     c                   endif
      *
     c     end_pris      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne gammel pris - ordinær
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_gml_pr   begsr
      *
     c                   eval      b_pris = *off
      *
     c                   eval      s_sapr = 0
     c                   eval      s_inpr = 0
     c                   eval      s_inp2 = 0
     c                   eval      s_kopr = 0
     c                   eval      s_nopr = 0
      *
     c                   eval      vvprl1_vare = x_vare
7.00 c                   eval      vvprl1_ldor = w_enum
     c                   eval      vvprl1_prgr = x_prgr
     c                   eval      vvprl1_enhe = w_enhp
     c                   eval      vvprl1_lety = d_lety
     c     vvprl1_key    setll     vvprl1
     c     vvprl1_key    reade     vvprl1
     c                   dow       not %eof(vvprl1)
      *
     c                   eval      s_sapr = vpsapr
     c                   eval      s_inpr = vpinpr
     c                   eval      s_inp2 = vpinp2
     c                   eval      s_kopr = vpkopr
     c                   eval      s_nopr = vpnopr
     c                   eval      b_pris = *on
      *
     c                   if        w_dato >= vppdat
     c                   leave
     c                   endif
      *
     c     vvprl1_key    reade     vvprl1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_filNavn
     c                   parm                    p_lengde
     c                   parm                    p_sistDato
     c                   parm                    p_stat
      *
      * Key for oppdatering av EVR-vare-pris
      *
     c     vvprlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprlu_vare
     c                   kfld                    vvprlu_ldor
     c                   kfld                    vvprlu_prgr
     c                   kfld                    vvprlu_enhe
     c                   kfld                    vvprlu_lety
     c                   kfld                    vvprlu_pdat
      *
     c     vvprlu_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvprlu_vare
     c                   kfld                    vvprlu_ldor
     c                   kfld                    vvprlu_prgr
     c                   kfld                    vvprlu_enhe
     c                   kfld                    vvprlu_lety
      *
     c     vvprlu_key3   klist
     c                   kfld                    w_firm
     c                   kfld                    vvprlu_vare
      *
     c     vvprlu_key4   klist
     c                   kfld                    w_firm
     c                   kfld                    vvprlu_vare
     c                   kfld                    vvprlu_ldor
      *
7.00  * Key for oppslag på leverandør
7.00  *
7.00 c     rlevlb_key    klist
7.00 c                   kfld                    w_firm
7.00 c                   kfld                    rlevlb_nobb
7.00  *

      * Key for les på EVR-vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
      *
      * Key for les på vare-enhet
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
     c                   kfld                    vvenl2_saen
      *
      * Key2 for les på vare-enhet
      *
     c     vvenl2_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
      *
     c     vvenl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl4_vare
     c                   kfld                    vvenl4_inen
      *
      * Key2 for les på vare-enhet
      *
     c     vvenl4_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl4_vare
      *
      * Key for les av vare-pris
      *
     c     vvprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprl1_vare
     c                   kfld                    vvprl1_ldor
     c                   kfld                    vvprl1_prgr
     c                   kfld                    vvprl1_enhe
     c                   kfld                    vvprl1_lety
      *
      * Key for oppslag på status
      *
     c     jstsl1_key    klist
     c                   kfld                    w_firm
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = l_firm
      *
     c     jstsl1_key    chain     jstsl1
      *
     c                   endsr
