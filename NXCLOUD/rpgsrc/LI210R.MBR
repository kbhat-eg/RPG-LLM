     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LI210R
      * Beskrivelse . . : Varemottak - scanne kolli
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
7.00  *       170919 bsa  Nytt program
7.01  *       171019 bsa  Sjekke om pakkseddel er valgt ut til manuelt varemottak
7.02  *       281119 bsa  Oppdaterer feil kolli.
7.03  *       131219 bsa  Ved F7, lag IP av det som er scannet.
7.04  *       090120 bsa  Div. små rettelser og justeringer
7.05  *       100120 bsa  Hvis det har startet med scanning, oppdater status.
7.06  *       290120 bsa  Feil i scrolling
7.07  *       300120 bsa  Fjerne bruken av LWVAPF.
7.08  *       030220 bsa  Antall kolli må leses fra hele pakkseddelen, ikke bare
7.08  *                   de som listes på skjermen.
7.09  *       040220 bsa  Sjekk at alle bestillingslinjer på linjenivå peker
7.09  *                   på gyldig bestillingslinje.
7.10  * K000  300420 bsa  Flagg feil hvis den er hentet ut på HT. Da kan den
7.10  * K000              ikke scannes her.
7.11  * K000  290620 bsa  Overfører leverandør som parameter.
7.12  * K000  131120 bsa  Hvis noe er mottatt, kan vi ikke tilbakeføre
7.12  * K000              pakkseddelen.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = avslutt
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flphoiu    uf   e           k disk    rename(lphost:lphoiur)
7.01 flphoi1    if   e           k disk    rename(lphost:lphoi1r)
     flpkoiu    uf   e           k disk    rename(lpkost:lpkoiur)
     flpkliu    uf   e           k disk    rename(lpklst:lpkliur)
7.12 flpklir    if   e           k disk    rename(lpklst:lpklirr)
     flpkoi3    if   e           k disk    rename(lpkost:lpkoi3r)
     flpkoi2    if   e           k disk    rename(lpkost:lpkoi2r)
     flbhoi3    uf   e           k disk    rename(lbhost:lbhoi3r)
     flbhoiu    uf   e           k disk    rename(lbhost:lbhoiur)
     flohelu    uf   e           k disk    rename(lohepfr:lohelur)
     flblii3    uf   e           k disk    rename(lblist:lblii3r)
     flblii2    uf   e           k disk    rename(lblist:lblii2r)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
      *
     fli210d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av LDA
      *
     d                uds
     d l_vise                700    700
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fkey               369    369
     d  d_srow               370    370I 0
     d  d_scol               371    371I 0
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d lohelu_numm     s                   like(lonumm)
     d lohelu_suff     s                   like(losuff)
      *
     d lpkoi2_type     s                   like(potype)
     d lpkoi2_sscc     s                   like(posscc)
     d lpkoi2_aarr     s                   like(poaarr)
     d lpkoi2_paid     s                   like(popaid)
     d lpkoi2_pver     s                   like(popver)
      *
     d lpkoi3_type     s                   like(potype)
     d lpkoi3_aarr     s                   like(poaarr)
     d lpkoi3_paid     s                   like(popaid)
     d lpkoi3_pver     s                   like(popver)
      *
     d lbhoi3_hpid     s                   like(pbhpid)
     d lbhoi3_hpve     s                   like(pbhpve)
     d lbhoi3_hssc     s                   like(pbhssc)
     d lblii3_lpid     s                   like(pblpid)
     d lblii3_lpve     s                   like(pblpve)
     d lblii3_lssc     s                   like(pblssc)
      *
     d lbhoiu_hssc     s                   like(pbhssc)
     d lbhoiu_hnum     s                   like(pbhnum)
     d lbhoiu_hsuf     s                   like(pbhsuf)
     d lbhoiu_hpid     s                   like(pbhpid)
     d lbhoiu_hpve     s                   like(pbhpve)
     d lbhoiu_haar     s                   like(pbhaar)
      *
     d lblii2_lpid     s                   like(pblpid)
     d lblii2_lpve     s                   like(pblpve)
      *
     d lodtl1_numm     s                   like(lonumm)
     d lodtl1_suff     s                   like(losuff)
     d lodtl1_line     s                   like(ldline)
      *
     d lpkoiu_type     s                   like(potype)
     d lpkoiu_mnum     s                   like(pomnum)
     d lpkoiu_aarr     s                   like(poaarr)
     d lpkoiu_paid     s                   like(popaid)
     d lpkoiu_pver     s                   like(popver)
     d lpkoiu_fsni     s                   like(pofsni)
     d lpkoiu_fniv     s                   like(pofniv)
     d lpkoiu_sscc     s                   like(posscc)
      *
     d lpkliu_type     s                   like(potype)
     d lpkliu_mnum     s                   like(pomnum)
     d lpkliu_aarr     s                   like(poaarr)
     d lpkliu_paid     s                   like(popaid)
     d lpkliu_pver     s                   like(popver)
     d lpkliu_fsni     s                   like(pofsni)
     d lpkliu_fniv     s                   like(pofniv)
7.12  *
7.12 d lpklir_type     s                   like(potype)
7.12 d lpklir_mnum     s                   like(pomnum)
7.12 d lpklir_aarr     s                   like(poaarr)
7.12 d lpklir_paid     s                   like(popaid)
7.12 d lpklir_pver     s                   like(popver)
7.12 d lpklir_fsni     s                   like(pofsni)
7.12 d lpklir_fniv     s                   like(pofniv)
      *
     d lphoiu_type     s                   like(phtype)
     d lphoiu_mnum     s                   like(phmnum)
     d lphoiu_aarr     s                   like(phaarr)
     d lphoiu_paid     s                   like(phpaid)
     d lphoiu_pver     s                   like(phpver)
      *
7.01 d lphoi1_type     s                   like(phtype)
7.01 d lphoi1_mnum     s                   like(phmnum)
7.01 d lphoi1_aarr     s                   like(phaarr)
7.01 d lphoi1_paid     s                   like(phpaid)
7.01 d lphoi1_pver     s                   like(phpver)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_scan          s              1    inz(*off)
     d b_eror          s              1    inz(*off)
7.09 d b_best          s              1    inz(*off)
7.12 d b_stop          s              1    inz(*off)
      *
     d w_firm          s                   like(pbhfir)
     d w_modu          s             10    inz('NED     ')
     d w_disp          s              1    inz('1')
     d w_tilg          s              1
     d w_navn          s             10
     d w_hent          s              1
     d w_ctl1          s              2  0
     d w_ctpo          s              3  0
     d w_cmd           s            256
     d w_len           s             15  5
     d w_jsta          s              2  0
     d w_onof          s              1  0
     d w_benr          s                   like(lonumm)
     d w_lpro          s              1
     d w_prec          s             45
     d w_type          s              4
     d w_ltxt          s             20
     d w_sscc          s             18  0
     d w_mld1          s             50
      *
     d p_type          s                   like(potype)
     d p_mnum          s                   like(pomnum)
     d p_aarr          s                   like(poaarr)
     d p_paid          s                   like(popaid)
     d p_pver          s                   like(popver)
     d p_stat          s              1
     d p_orgi          s              5
7.11 d p_levr          s                   like(loldor)
      *
      * Definering av konstanter
      *
7.06 d c_sfil          s              2  0 inz(12)
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * - - - - - - - - - - - - - - - - -
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * w_virn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * B4WIN  - B4 Vindu for slette (Delete)
      * F1BLD  - F1 Søke på meldingstype
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * A1BLD Sjekk om meldingstype er fast eller skal velges
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Kontrollerer gyldig tilgang
      *
     c                   call      'AX700R'
     c                   parm                    w_modu
     c                   parm                    w_disp
     c                   parm                    w_tilg
      *
     c                   if        w_tilg = '1'
     c                   goto      end_pgm
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   exsr      xb3win
     c                   goto      end_pgm
     c                   when      *inke
     c                   eval      w_fcrn = *zero
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xb6win
7.06 c                   eval      w_fcrn = *zero
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkg
     c                   exsr      xb7win
7.06 c                   eval      w_fcrn = *zero
7.06 c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkh
     c                   exsr      xb8win
7.06 c                   eval      w_fcrn = *zero
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inki
     c                   exsr      xb9win
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   endsl
      *
     c                   if        b2sscc <> 0 and
     c                             b2sscc <> w_sscc
     c                   eval      b_eror = *off
     c                   exsr      sjk_sscc
     c                   if        b_eror = *on
     c                   goto      b2tagb
     c                   endif
     c                   eval      w_sscc = b2sscc
7.06 c                   eval      w_fcrn = *zero
     c                   exsr      forny
     c                   endif
      *
      * Behandling av subfile
      * Foreløpig skal vi ikke kunne gjøre noe på linjenivå. Fjernes ikke da jeg
      * mistenker at behovet kan dukke opp.
      *
     c                   exsr      subfile
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekk av endret scanne kode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_sscc      begsr
      *
     c                   eval      b_eror = *off
      *
      * Må angi kolli
      *
     c                   if        b2sscc = 0
     c                   eval      w_mld1 = 'Du må angi en SSCC kode'
     c                   call      'AA005R'
     c                   parm                    w_mld1
     c                   eval      b_eror = *on
     c                   goto      sjk_sscc_end
     c                   endif
      *
      * Sjekk om scannet kolli kommer fra denne pakkseddelen
      *
     c                   eval      lpkoi2_type = *blanks
     c                   eval      lpkoi2_sscc = b2sscc
     c     lpkoi2_key2   setll     lpkoi2
     c     lpkoi2_key2   reade     lpkoi2
     c                   if        not %eof
     c                   if        w_sscc <> 0
     c                   if        popaid <> b2paid
     c                   eval      w_mld1 = 'SSCC kode finnes ikke på +
     c                                       denne pakkseddelen'
     c                   call      'AA005R'
     c                   parm                    w_mld1
     c                   eval      b_eror = *on
     c                   goto      sjk_sscc_end
     c                   endif
     c                   endif
     c     lpkoi2_key2   reade     lpkoi2
     c                   else
     c                   eval      w_mld1 = 'SSCC kode finnes ikke på +
     c                                       noen pakkseddel'
     c                   call      'AA005R'
     c                   parm                    w_mld1
     c                   eval      b_eror = *on
     c                   goto      sjk_sscc_end
     c                   endif
      *
7.08 c                   eval      b2pant = 0
7.05 c                   eval      b2mnum = pomnum
7.05 c                   eval      b2aarr = poaarr
7.05 c                   eval      b2paid = popaid
7.05 c                   eval      b2pver = popver
7.01  *
7.01  * Sjekk om pakkseddel er plukket til manuelt varemottak
7.01  *
7.01 c                   eval      lphoi1_type = *blanks
7.01 c                   eval      lphoi1_mnum = b2mnum
7.01 c                   eval      lphoi1_aarr = b2aarr
7.01 c                   eval      lphoi1_paid = b2paid
7.01 c                   eval      lphoi1_pver = b2pver
7.01 c     lphoi1_key    chain     lphoi1
7.01 c                   if        %found
7.01 c                   if        phmvar = 1
7.01 c                   eval      w_mld1 = 'Pakkseddel er plukket til +
7.01 c                                      manuelt varemottak'
7.01 c                   call      'AA005R'
7.01 c                   parm                    w_mld1
7.01 c                   eval      b_eror = *on
7.01 c                   goto      sjk_sscc_end
7.01 c                   endif
7.01 c                   endif
      *
7.05 c                   eval      b2stat = phstat
7.08  *
7.08  * Leser alle kolli på pakkseddelen
7.08 c                   eval      lpkoi3_type = *blanks
7.08 c                   eval      lpkoi3_aarr = b2aarr
7.08 c                   eval      lpkoi3_paid = b2paid
7.08 c                   eval      lpkoi3_pver = b2pver
7.08 c     lpkoi3_key    setll     lpkoi3
7.08 c     lpkoi3_key    reade     lpkoi3
7.08 c                   dow       not %eof
7.08 c                   eval      b2pant = b2pant + popant
7.08 c     lpkoi3_key    reade     lpkoi3
7.08 c                   enddo
7.08  *
     c     sjk_sscc_end  endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      * Vi leser alle bestillinger fra pakkseddelen
     c                   eval      lpkoi2_type = *blanks
     c                   eval      lpkoi2_sscc = b2sscc
     c     lpkoi2_key2   setll     lpkoi2
     c     lpkoi2_key2   reade     lpkoi2
     c                   dow       not %eof
     c                   eval      lpkoi3_type = potype
     c                   eval      lpkoi3_aarr = poaarr
     c                   eval      lpkoi3_paid = popaid
     c                   eval      lpkoi3_pver = popver
     c                   leave
     c     lpkoi2_key2   reade     lpkoi2
     c                   enddo
      *
     c     lpkoi3_key    setll     lpkoi3
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
     c                   eval      b_forn = *off
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        w_fcrn > 0
     c                   eval      w_virn = w_fcrn
     c                   else
     c                   eval      w_virn = 1
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Hvem behandler ordre ?
      *
     c                   if        b1valg = 1
      *
     c                   eval      p_type = *blanks
     c                   eval      p_mnum = b2mnum
     c                   eval      p_aarr = b2aarr
     c                   eval      p_paid = b2paid
     c                   eval      p_pver = b2pver
      *
     c                   call      'LI500R'
     c                   parm                    p_type
     c                   parm                    p_mnum
     c                   parm                    p_aarr
     c                   parm                    p_paid
     c                   parm                    p_pver
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for nullstille låsing av bestilling.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb3win        begsr
      *
      * Les alle kolli, og finn alle bestillinger som er innvolvert
      *
     c                   eval      lpkoi3_type = *blanks
     c                   eval      lpkoi3_aarr = b2aarr
     c                   eval      lpkoi3_paid = b2paid
     c                   eval      lpkoi3_pver = b2pver
     c     lpkoi3_key    setll     lpkoi3
     c     lpkoi3_key    reade     lpkoi3
     c                   dow       not %eof
      *
     c                   eval      lbhoi3_hpid = popaid
     c                   eval      lbhoi3_hpve = popver
     c                   eval      lbhoi3_hssc = posscc
     c     lbhoi3_key    setll     lbhoi3
     c     lbhoi3_key    reade     lbhoi3
     c                   dow       not %eof
     c                   unlock    lbhoi3
     c                   eval      lohelu_numm = pbhnum
     c                   eval      lohelu_suff = pbhsuf
     c     lohelu_key    chain     lohelu
      *
     c                   if        %found
      *
      * Tilbakestill sperrekode på bestilling
      *
     c                   eval      lokode = 0
     c                   move      *loval        lokoti
     c                   move      *loval        lokoda
     c                   eval      lokous = *blank
     c                   eval      lokows = *blank
     c                   time                    loedat
     c                   time                    loetim
     c                   eval      loeusr = l_user
     c                   update    lohelur
     c                   endif
      *
     c     lbhoi3_key    reade     lbhoi3
     c                   enddo
      *
     c     lpkoi3_key    reade     lpkoi3
     c                   enddo
7.05  *
7.05  * Hvis kolli er scannet må vi oppdatere status på hodet
7.05  *
7.05 c                   eval      b_scan = *off
7.05 c                   eval      lpkoi3_type = *blanks
7.05 c                   eval      lpkoi3_aarr = b2aarr
7.05 c                   eval      lpkoi3_paid = b2paid
7.05 c                   eval      lpkoi3_pver = b2pver
7.05 c     lpkoi3_key    setll     lpkoi3
7.05 c     lpkoi3_key    reade     lpkoi3
7.05 c                   dow       not %eof
7.05  *
7.05 c                   if        postat = '50'
7.05 c                   eval      b_scan = *on
7.05 c                   endif
7.05  *
7.05 c     lpkoi3_key    reade     lpkoi3
7.05 c                   enddo
7.05  *
7.05  * Sjekk om den er scannet
7.05  *
7.05 c                   if        b_scan = *on
7.05 c                   eval      lphoiu_type = *blanks
7.05 c                   eval      lphoiu_mnum = b2mnum
7.05 c                   eval      lphoiu_aarr = b2aarr
7.05 c                   eval      lphoiu_paid = b2paid
7.05 c                   eval      lphoiu_pver = b2pver
7.05 c     lphoiu_key    chain     lphoiu
7.05 c                   if        %found
7.05 c                   eval      phstat = '50'
7.05 c                   update    lphoiur
7.05 c                   endif
7.05 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å godkjenning av kolli
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb6win        begsr
      *
     c                   eval      b_eror = *off
      *
     c                   exsr      sjk_sscc
     c                   if        b_eror = *on
     c                   goto      xb6win_end
     c                   endif
      *
      * Sjekk om den er allerede er scannet
      *
     c                   eval      lpkoi2_type = *blanks
     c                   eval      lpkoi2_sscc = b2sscc
     c                   eval      lpkoi2_aarr = b2aarr
     c                   eval      lpkoi2_paid = b2paid
     c                   eval      lpkoi2_pver = b2pver
     c     lpkoi2_key    chain     lpkoi2
     c                   if        %found
     c                   if        postat > '49'
     c                   eval      w_mld1 = 'SSCC kode er allerede scannet'
     c                   call      'AA005R'
     c                   parm                    w_mld1
     c                   eval      b_eror = *on
     c                   goto      xb6win_end
     c                   endif
7.10  * Hvis SSCC kode er hentet ut på HT, kan den ikke scannes
7.10  * Sjekk om pakkseddelen har linjer som ligger plukket på HT
7.10 c                   eval      p_stat = *blanks
7.10 c                   eval      p_paid = b2paid
7.10 c                   eval      p_pver = b2pver
7.10 c
7.10 c                   call      'LI307R'
7.10 c                   parm                    w_firm
7.10 c                   parm                    p_paid
7.10 c                   parm                    p_pver
7.10 c                   parm                    p_stat
7.10  *
7.10 c                   if        p_stat = '1'
7.10 c                   eval      w_mld1 = 'SSCC kode er hentet ut på HT. +
7.10 c                                      Kan ikke scannes!'
7.10 c                   call      'AA005R'
7.10 c                   parm                    w_mld1
7.10 c                   eval      b_eror = *on
7.10 c                   goto      xb6win_end
7.10 c                   endif
     c                   endif
7.09  *
7.09  * Sjekk at alle linjer har gyldig bestillinsgreferanse
7.09  *
7.09 c                   eval      b_best = *on
7.09 c                   eval      lblii3_lpid = b2paid
7.09 c                   eval      lblii3_lpve = b2pver
7.09 c                   eval      lblii3_lssc = b2sscc
7.09 c     lblii3_key    setll     lblii3
7.09 c     lblii3_key    reade     lblii3
7.09 c                   dow       not %eof
7.09 c                   eval      lodtl1_numm = pblnum
7.09 c                   eval      lodtl1_suff = pblsuf
7.09 c                   eval      lodtl1_line = pbllin
7.09 c     lodtl1_key    chain     lodtl1
7.09 c                   if        not %found
7.09 c                   eval      b_best = *off
7.09 c                   endif
7.09  *
7.09 c     lblii3_key    reade     lblii3
7.09 c                   enddo
7.09  *
7.09 c                   if        b_best = *off
7.09 c                   eval      w_mld1 = 'SSCC kode har linjer uten best.ref'
7.09 c                   call      'AA005R'
7.09 c                   parm                    w_mld1
7.09 c                   eval      b_eror = *on
7.09 c                   goto      xb6win_end
7.09 c                   endif
      *
      * Legg ut info i LBLI om at det er scannet. Flagg statuskode.
      *
     c                   eval      lbhoi3_hpid = b2paid
     c                   eval      lbhoi3_hpve = b2pver
     c                   eval      lbhoi3_hssc = b2sscc
     c     lbhoi3_key    setll     lbhoi3
     c     lbhoi3_key    reade     lbhoi3
     c                   dow       not %eof
     c                   eval      pbhsta = '50'
     c                   update    lbhoi3r
     c     lbhoi3_key    reade     lbhoi3
     c                   enddo
      *
     c                   eval      lblii3_lpid = b2paid
     c                   eval      lblii3_lpve = b2pver
     c                   eval      lblii3_lssc = b2sscc
     c     lblii3_key    setll     lblii3
     c     lblii3_key    reade     lblii3
     c                   dow       not %eof
7.02 c                   eval      lpkoiu_fsni = pblfsn
7.02 c                   eval      lpkoiu_fniv = pblfni
     c                   eval      pblsta = '50'
     c                   update    lblii3r
      *
     c     lblii3_key    reade     lblii3
     c                   enddo
      *
      * Flagg status i LPKO scannet. Flagg statuskode.
      *
     c                   eval      lpkoiu_type = *blanks
     c                   eval      lpkoiu_mnum = b2mnum
     c                   eval      lpkoiu_aarr = b2aarr
     c                   eval      lpkoiu_paid = b2paid
     c                   eval      lpkoiu_pver = b2pver
7.02 c**                 eval      lpkoiu_fsni = b1fsni
7.02 c**                 eval      lpkoiu_fniv = b1fniv
     c                   eval      lpkoiu_sscc = b2sscc
     c     lpkoiu_key    chain     lpkoiu
     c                   if        %found
     c                   eval      postat = '50'
     c                   update    lpkoiur
     c                   endif
      *
      * Flagg status i LPKL scannet. Flagg statuskode.
      *
     c                   eval      lpkliu_type = *blanks
     c                   eval      lpkliu_mnum = b2mnum
     c                   eval      lpkliu_aarr = b2aarr
     c                   eval      lpkliu_paid = b2paid
     c                   eval      lpkliu_pver = b2pver
     c                   eval      lpkliu_fsni = pofsni
     c                   eval      lpkliu_fniv = pofniv
     c     lpkliu_key    setll     lpkliu
     c     lpkliu_key    reade     lpkliu
     c                   dow       not %eof
     c                   if        plsscc = b2sscc
     c                   eval      plstat = '50'
     c                   endif
     c                   update    lpkliur
     c     lpkliu_key    reade     lpkliu
     c                   enddo
      *
     c     xb6win_end    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å ferdigstille varemottak
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb7win        begsr
      *
     c                   eval      b_eror = *off
     c                   exsr      sjk_sscc
     c                   if        b_eror = *on
     c                   goto      xb7win_end
     c                   endif
      *
      * Sjekk at alle kolli er scannet
      *
7.04 c                   eval      b_scan = *off
7.04  *
7.04  * Minst et kolli må være scannet
7.04  *
     c                   eval      lpkoi3_type = *blanks
     c                   eval      lpkoi3_aarr = b2aarr
     c                   eval      lpkoi3_paid = b2paid
     c                   eval      lpkoi3_pver = b2pver
     c     lpkoi3_key    setll     lpkoi3
     c     lpkoi3_key    reade     lpkoi3
     c                   dow       not %eof
      *
7.04 c                   if        postat = '50'
7.04 c                   eval      b_scan = *on
     c                   endif
      *
     c     lpkoi3_key    reade     lpkoi3
     c                   enddo
      *
7.04 c                   if        b_scan = *off
7.04 c                   eval      w_mld1 = 'Minst ett kolli må være scannet'
     c                   call      'AA005R'
     c                   parm                    w_mld1
     c                   goto      xb7win_end
     c                   endif
      *
     c                   if        b2sscc = 0
     c                   eval      b_eror = *on
     c                   eval      w_mld1 = 'Pakkseddel er ikke scannet'
     c                   call      'AA005R'
     c                   parm                    w_mld1
     c                   goto      xb7win_end
     c                   endif
      *
7.11 c                   eval      p_levr = b2ldor
     c                   eval      p_type = *blanks
     c                   eval      p_mnum = b2mnum
     c                   eval      p_aarr = b2aarr
     c                   eval      p_paid = b2paid
     c                   eval      p_pver = b2pver
     c                   eval      p_orgi = 'LI210'
      *
7.03 c                   call      'LI305R'
     c                   parm                    p_type
     c                   parm                    p_mnum
     c                   parm                    p_aarr
     c                   parm                    p_paid
     c                   parm                    p_pver
     c                   parm                    p_orgi
7.11 c                   parm                    p_levr
      *
     c     xb7win_end    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for overgang til EDI pakksedler
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb8win        begsr
      *
     c                   eval      p_type = *blanks
      *
     c                   call      'LI200R'
     c                   parm                    p_type
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å tilbakestille scannede kolli
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb9win        begsr
      *
     c                   if        %trim(b2paid) = *blanks
     c                   eval      w_mld1 = 'Pakkseddel ikke scannet, ikke noe +
     c                                       å tilbakeføres'
     c                   call      'AA005R'
     c                   parm                    w_mld1
     c                   goto      xb9win_end
     c                   endif
7.04  *
7.04 c                   if        %trim(b2stat) = '90'
7.04 c                   eval      w_mld1 = 'Pakkseddel ferdig scannet. Kan ikk+
7.04 c                                       e tilbakeføres'
7.04 c                   call      'AA005R'
7.04 c                   parm                    w_mld1
7.04 c                   goto      xb9win_end
7.04 c                   endif
7.12  *
7.12  * Sjekk om noen linjer allerede er scannet, i så fall må det avbrytes.
7.12  *
7.12 c                   eval      b_stop = *off
7.12 c                   eval      lpklir_type = *blanks
7.12 c                   eval      lpklir_mnum = b2mnum
7.12 c                   eval      lpklir_aarr = b2aarr
7.12 c                   eval      lpklir_paid = b2paid
7.12 c                   eval      lpklir_pver = b2pver
7.12 c                   eval      lpklir_fsni = pofsni
7.12 c                   eval      lpklir_fniv = pofniv
7.12 c     lpklir_key    setll     lpklir
7.12 c     lpklir_key    reade     lpklir
7.12 c                   dow       not %eof
7.12 c                   if        plstat = '90'
7.12 c                   eval      b_stop = *on
7.12 c                   endif
7.12 c     lpklir_key    reade     lpklir
7.12 c                   enddo
7.12  *
7.12 c                   if        b_stop = *on
7.12 c                   eval      w_mld1 = 'Deler er scannet. Kan ikke tilbake+
7.12 c                                       føres'
7.12 c                   call      'AA005R'
7.12 c                   parm                    w_mld1
7.12 c                   goto      xb9win_end
7.12 c                   endif
      *
      * Sjekk om den er allerede er scannet
      *
     c                   eval      lpkoiu_type = *blanks
     c                   eval      lpkoiu_mnum = b2mnum
     c                   eval      lpkoiu_aarr = b2aarr
     c                   eval      lpkoiu_paid = b2paid
     c                   eval      lpkoiu_pver = b2pver
     c     lpkoiu_key    setll     lpkoiu
     c     lpkoiu_key2   reade     lpkoiu
     c                   dow       not %eof
     c                   eval      postat = *blanks
     c                   update    lpkoiur
      *
      * Legg ut info i LBLI om at det er scannet. Flagg statuskode.
      *
     c                   eval      lblii2_lpid = b2paid
     c                   eval      lblii2_lpve = b2pver
     c     lblii2_key    setll     lblii2
     c     lblii2_key    reade     lblii2
     c                   dow       not %eof
     c                   eval      pblsta = *blanks
     c                   update    lblii2r
      *
     c                   eval      lbhoiu_hssc = pblssc
     c                   eval      lbhoiu_hnum = pblnum
     c                   eval      lbhoiu_hsuf = pblsuf
     c                   eval      lbhoiu_hpid = pblpid
     c                   eval      lbhoiu_hpve = pblpve
     c     lbhoiu_key    chain     lbhoiu
     c                   if        %found
     c                   eval      pbhsta = *blanks
     c                   update    lbhoiur
     c                   endif
      *
     c     lblii2_key    reade     lblii2
     c                   enddo
      *
      * Tilbakestill statuskode.
      *
     c                   eval      lpkliu_type = *blanks
     c                   eval      lpkliu_mnum = b2mnum
     c                   eval      lpkliu_aarr = b2aarr
     c                   eval      lpkliu_paid = b2paid
     c                   eval      lpkliu_pver = b2pver
     c                   eval      lpkliu_fsni = pofsni
     c                   eval      lpkliu_fniv = pofniv
     c     lpkliu_key    setll     lpkliu
     c     lpkliu_key    reade     lpkliu
     c                   dow       not %eof
     c                   eval      plstat = *blanks
     c                   update    lpkliur
     c     lpkliu_key    reade     lpkliu
     c                   enddo
      *
      * Les neste kolli
      *
     c     lpkoiu_key2   reade     lpkoiu
     c                   enddo
      *
      * Sjekk om den er scannet
      *
     c                   eval      lphoiu_type = *blanks
     c                   eval      lphoiu_mnum = b2mnum
     c                   eval      lphoiu_aarr = b2aarr
     c                   eval      lphoiu_paid = b2paid
     c                   eval      lphoiu_pver = b2pver
     c     lphoiu_key    chain     lphoiu
     c                   if        %found
     c                   eval      phstat = *blanks
     c                   update    lphoiur
     c                   endif
      *
     c     xb9win_end    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
7.08 c**                 eval      b2pant = 0
7.05 c**                 eval      b2paid = *blanks
7.05 c**                 eval      b2mnum = 0
7.05 c**                 eval      b2aarr = 0
     c                   eval      b2pver = 0
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c     lpkoi3_key    reade     lpkoi3
      *
     c                   if        %eof
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        postat = '99'
     c                   iter
     c                   endif
      *
     c                   eval      b1valg = 0
      *
     c                   eval      b1scan = *blanks
     c                   if        postat = '90' or
     c                             postat = '50'
     c                   eval      b1scan = 'X'
     c                   endif
      *
     c                   eval      b1stat = postat
      *
7.08 c**                 eval      b2pant = b2pant + popant
     c                   eval      b2paid = popaid
     c                   eval      b2pver = popver
     c                   eval      b2mnum = pomnum
     c                   eval      b2aarr = poaarr
     c                   eval      b1fsni = pofsni
     c                   eval      b1fniv = pofniv
      *
     c                   eval      b1sscc = posscc
      *
     c                   eval      b1kord = *blanks
     c                   eval      lbhoi3_hpid = popaid
     c                   eval      lbhoi3_hpve = popver
     c                   eval      lbhoi3_hssc = posscc
     c     lbhoi3_key    setll     lbhoi3
     c     lbhoi3_key    reade     lbhoi3
     c                   dow       not %eof
     c                   unlock    lbhoi3
     c                   eval      lohelu_numm = pbhnum
     c                   eval      lohelu_suff = pbhsuf
     c     lohelu_key    chain     lohelu
     c                   if        %found
     c                   eval      b2ldor = loldor
     c                   eval      b2lnav = lonavn
     c                   if        loornr <> 0
     c                   eval      b1kord = 'X'
     c                   endif
      *
      * Sett "sperrekode" på bestilling
      *
     c                   eval      lokode = 1
     c                   time                    loedat
     c                   time                    loetim
     c                   eval      loeusr = l_user
     c                   update    lohelur
      *
      *  Oppdater BESTILLING-HODE-REGISTER, Bestillingen er til behandling
      *
     c                   call      'LO709R'
     c                   parm                    lonumm
     c                   parm                    losuff
     c                   endif
     c     lbhoi3_key    reade     lbhoi3
     c                   enddo
      *
     c                   eval      w_srrn = w_srrn + 1
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     lpkoi3_key    setgt     lpkoi3
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c     lpkoi3_key    readpe    lpkoi3
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   enddo
      *
     c                   if        %eof
     c     lpkoi3_key    setll     lpkoi3
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_bck       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c*    *entry        plist
     c*                  parm                    p_type
     c*                  parm                    p_mnum
     c*                  parm                    p_aarr
     c*                  parm                    p_paid
     c*                  parm                    p_pver
      *
      *  Key for posisjonering av bestilling
      *
     c     lohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelu_numm
     c                   kfld                    lohelu_suff
      *
      *  Key for posisjonering av pakkseddel kolli
      *
     c     lpkoi2_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    lpkoi2_type
     c                   kfld                    lpkoi2_sscc
      *
      *  Key for posisjonering av pakkseddel kolli
      *
     c     lpkoi2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lpkoi2_type
     c                   kfld                    lpkoi2_sscc
     c                   kfld                    lpkoi2_aarr
     c                   kfld                    lpkoi2_paid
     c                   kfld                    lpkoi2_pver
      *
      *  Key for posisjonering av pakkseddel kolli
      *
     c     lpkoi3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lpkoi3_type
     c                   kfld                    lpkoi3_aarr
     c                   kfld                    lpkoi3_paid
     c                   kfld                    lpkoi3_pver
      *
      *  Key for posisjonering av bestillinger på kolli
      *
     c     lbhoi3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbhoi3_hpid
     c                   kfld                    lbhoi3_hpve
     c                   kfld                    lbhoi3_hssc
      *
      *  Key for posisjonering av pakkseddel kolli
      *
     c     lpkoiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lpkoiu_type
     c                   kfld                    lpkoiu_mnum
     c                   kfld                    lpkoiu_aarr
     c                   kfld                    lpkoiu_paid
     c                   kfld                    lpkoiu_pver
     c                   kfld                    lpkoiu_fsni
     c                   kfld                    lpkoiu_fniv
     c                   kfld                    lpkoiu_sscc
      *
      *  Key for posisjonering av pakkseddel hode
      *
     c     lphoiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lphoiu_type
     c                   kfld                    lphoiu_mnum
     c                   kfld                    lphoiu_aarr
     c                   kfld                    lphoiu_paid
     c                   kfld                    lphoiu_pver
7.01  *
7.01  *  Key for posisjonering av pakkseddel hode
7.01  *
7.01 c     lphoi1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    lphoi1_type
7.01 c                   kfld                    lphoi1_mnum
7.01 c                   kfld                    lphoi1_aarr
7.01 c                   kfld                    lphoi1_paid
7.01 c                   kfld                    lphoi1_pver
      *
      *  Key for posisjonering av pakkseddel kolli
      *
     c     lpkoiu_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    lpkoiu_type
     c                   kfld                    lpkoiu_mnum
     c                   kfld                    lpkoiu_aarr
     c                   kfld                    lpkoiu_paid
     c                   kfld                    lpkoiu_pver
      *
      *  Key for posisjonering av pakkseddel kolli linjer
      *
     c     lpkliu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lpkliu_type
     c                   kfld                    lpkliu_mnum
     c                   kfld                    lpkliu_aarr
     c                   kfld                    lpkliu_paid
     c                   kfld                    lpkliu_pver
     c                   kfld                    lpkliu_fsni
     c                   kfld                    lpkliu_fniv
7.12  *
7.12  *  Key for posisjonering av pakkseddel kolli linjer
7.12  *
7.12 c     lpklir_key    klist
7.12 c                   kfld                    w_firm
7.12 c                   kfld                    lpklir_type
7.12 c                   kfld                    lpklir_mnum
7.12 c                   kfld                    lpklir_aarr
7.12 c                   kfld                    lpklir_paid
7.12 c                   kfld                    lpklir_pver
7.12 c                   kfld                    lpklir_fsni
7.12 c                   kfld                    lpklir_fniv
      *
      *  Key for posisjonering av bestillinger på kollilinjer
      *
     c     lblii3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lblii3_lpid
     c                   kfld                    lblii3_lpve
     c                   kfld                    lblii3_lssc
      *
      *  Key for posisjonering av bestillinger på kollilinjer
      *
     c     lblii2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lblii2_lpid
     c                   kfld                    lblii2_lpve
      *
      *  Key for posisjonering av bestillingslinjer
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
     c                   kfld                    lodtl1_line
      *
      *  Key for posisjonering av bestillings hode
      *
     c     lbhoiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbhoiu_hssc
     c                   kfld                    lbhoiu_hnum
     c                   kfld                    lbhoiu_hsuf
     c                   kfld                    lbhoiu_hpid
     c                   kfld                    lbhoiu_hpve
      *
      * Henter firmanummer fra LDA
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_sscc = 0
     c                   eval      lpkoi2_type = *blanks
      *
     c                   endsr
      *
