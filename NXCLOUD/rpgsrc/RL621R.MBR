      /TITLE  Utskrift av leverandørregister
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RL621R                                        *
      * Beskrivelse . . : Utskrift av leverandørregister                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      * R3.30     021200  Rettet feil pga endring i parameter            KOB  *
      * R3.30     021200  Endret datokontroll                            KOB
T5.00 * R5.00     250902  Bruker eksterntdefinert printerfil.            HFL
 6.20 * R6.20     080911  Rekompilert pga endring i RPARRKDS            *NHO
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frlevl2    if   e           k disk    rename(rlevpfr:rlevl2r)
     frl621p    o    e             printer oflind(*in30)
      *
      *************************  T A B E L L E R  ************************
     d f               s              1    dim(30)
      *
     d                 ds
     d w_tdat                         8  0
     d  w_taar                        4  0 overlay(w_tdat)
     d  w_tmnd                        2  0 overlay(w_tdat:5)
     d  w_tdag                        2  0 overlay(w_tdat:7)
      *
     d w_xdat                        10d   datfmt(*iso)
      *
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      * LOCAL DATA
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     d/COPY QCPYLESRC,RPARRKDS
      *
     d antpos          s              3  0
     d wakjli          s              5  0
     d wakjre          s              5  0
     d wantli          s              5  0
     d wantre          s              5  0
     d wdebli          s              5  0
     d wdebre          s              5  0
     d wdsali          s             13  2
     d wdsare          s             13  2
     d wkjØli          s             13  2
     d wkjØre          s             13  2
     d wkreli          s              5  0
     d wkrere          s              5  0
     d wksali          s             13  2
     d wksare          s             13  2
     d wsalli          s             13  2
     d wsalre          s             13  2
     d w_alfa          s                   like(rlalfa)
     d w_firm          s                   like(rlfirm)
     d w_levr          s                   like(rllevr)
      *
      /eject
      *
      * Les leverandørregister
      * avhengig av valg num./alfa
      *
     c     lesl          tag
      *
     c                   if        psort = ' '
     c                   read      rlevl1                                 10
     c                   else
     c                   read      rlevl2                                 10
     c                   endif
      *
     c                   if        rlfirm > w_firm
     c                   eval      *in10 = *on
     c                   endif
      *
     c                   if        *in10 = *on
     c                   goto      slutt                                        FERDIG
     c                   endif
      *
      * Sjekk leverandørnr. fra/til
      *
     c                   if        rllevr < pknrf or
     c                             rllevr > pknrt
     c                   goto      alle
     c                   endif
      *
      * Sjekk avdeling fra/til
      *
     c                   if        rlavde < pavdf or                                     .....
     c                             rlavde > pavdt                                        .....
     c                   goto      alle                                                      .
     c                   endif                                                           .....
      *
      * Sjekk distrikt fra/til
      *
     c                   if        rldist < pdisf or                                     .....
     c                             rldist > pdist                                        .....
     c                   goto      alle                                                      .
     c                   endif                                                           .....
      *
      * Sjekk leverandørkategori fra/til
      *
     c                   if        rlkatg < pkatf or                                     .....
     c                             rlkatg > pkatt                                        .....
     c                   goto      alle                                                      .
     c                   endif                                                           .....
      *
      * Sjekk siste trans.dato
      *
     c                   if        rltdat > pedat                                            .
     c                   goto      alle                                                      .
     c                   endif                                                           .....
      *
      * Selekterte leverandører -
      * summer for siste side og skriv
      *
     c                   eval      wantli = wantli + 1
      *
     c                   if        rlsald > *zero
     c                   eval      wdebli = wdebli + 1
     c                   eval      wdsali = wdsali + rlsald
     c                   endif
      *
     c                   if        rlsald < *zero
     c                   eval      wkreli = wkreli + 1
     c                   eval      wksali = wksali + rlsald
     c                   endif
      *
     c                   if        rlkjØp <> *zero
     c                   eval      wakjli = wakjli + 1
     c                   eval      wkjØli = wkjØli + rlkjØp
     c                   endif
      *
     c                   eval      wsalli = wsalli + rlsald
      *
     c                   eval      antpos = 12
     c                   movea     rlalfa        f
     c                   movea     f             rlalfa
      *
     c                   if        ptype = 'E'
     c                   exsr      skrive
     c                   else
     c                   if        rlndat <> *loval
     c     *dmy          move      rlndat        wlndat
     c                   else                                                            .....
     c                   move      *zeros        wlndat
     c                   endif                                                           .....
      *
     c                   if        rledat <> *loval
     c     *dmy          move      rledat        wledat
     c                   else                                                            .....
     c                   move      *zeros        wledat
     c                   endif                                                           .....
     c                   exsr      skrivt
     c                   endif
      *
     c     alle          tag
      *
      * Alle leverandører -
      * summer til siste side
      *
     c                   if        rlsald > *zero
     c                   eval      wdebre = wdebre + 1
     c                   eval      wdsare = wdsare + rlsald
     c                   endif
      *
     c                   if        rlsald < *zero
     c                   eval      wkrere = wkrere + 1
     c                   eval      wksare = wksare + rlsald
     c                   endif
      *
     c                   if        rlkjØp <> *zero
     c                   eval      wakjre = wakjre + 1
     c                   eval      wkjØre = wkjØre + rlkjØp
     c                   endif
      *
     c                   eval      wantre = wantre + 1
     c                   eval      wsalre = wsalre + rlsald
      *
     c                   goto      lesl
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
      *
     clr                 write     total
      *
      /eject
      ****************************************************************
      *   SUBRUTINE FOR Å SKRIVE LEVERANDØR - HEADING (ENKEL LISTE)  *
      ****************************************************************
     c     skrive        begsr
      *
      * Sjekker om overflow
      *
     c                   if        *in30 = *on                                        .....  .
     c                   do
     c                   write     head1
     c                   write     head2
     c                   write     head2e                                                    .
     c                   eval      *in30 = *off                                              .
     c                   enddo
     c                   endif                                                           .....
      *
     c                   write     levr1
      *
     c                   if        rladr2 <> *blank
     c                   write     levr1a
     c                   endif
      *
     c                   endsr
      ****************************************************************
      *   SUBRUTINE FOR Å SKRIVE LEVERANDØR - HEADING (TOTAL LISTE)  *
      ****************************************************************
     c     skrivt        begsr
      *
      * Sjekker om overflow
      *
     c                   if        *in30 = *on                                        .....  .
     c                   do
     c                   write     head1
     c                   write     head2
     c                   eval      *in30 = *off                                              .
     c                   enddo
     c                   endif                                                           .....
      *
     c                   write     levert
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      *
      * Behandle parameter
      *
     c                   if        psald = 'J'                                           .....
     c                   eval      *in12 = *on                                               .
     c                   endif                                                           .....
      *
     c                   if        ptype = 'E'                                           .....
     c                   eval      *in11 = *on                                               .
     c                   endif                                                           .....
      *
      * Sette startverdier
      *
     c                   move      l_firm        w_firm
      *
     c                   if        psort = ' '                                           .....
     c     key01         setll     rlevl1                                                    .
     c                   else                                                                .
     c     key02         setll     rlevl2                                                    .
     c                   endif                                                           .....
      *
      * Skriv heading på første side
      *
     c                   write     head1
     c                   write     headle
      *
     c                   if        pavdt > *zero                                         .....
     c                   write     headav                                                    .
     c                   endif                                                           .....
      *
     c                   if        pdist > *zero                                         .....
     c                   write     headdi                                                    .
     c                   endif                                                           .....
      *
     c                   if        pkatt > *zero                                         .....
     c                   write     headlk                                                    .
     c                   endif                                                           .....
      *
     c                   if        pedat > *loval
     c     *dmy          move      pedat         wpedat
     c                   write     headda
     c                   endif
      *
     c                   write     head1
     c                   write     head2
      * Sjekk om enkel heading
     c                   if        *in11 = *on
     c                   write     head2e
     c                   endif
      *
      * Key for leverandørregister
      * via leverandørnummer
      *
     c     key01         klist
     c                   kfld                    w_firm
     c                   kfld                    w_levr
      *
      * Key for leverandørregister
      * via alfafeltet
      *
     c     key02         klist
     c                   kfld                    w_firm
     c                   kfld                    w_alfa
     c                   kfld                    w_levr
      *
     c                   endsr
