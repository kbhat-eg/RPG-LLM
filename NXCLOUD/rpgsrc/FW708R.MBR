      /title  ASOFAK Lev.vare, henter vareregister - Farveringen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FW708R
      * Beskrivelse . . : Danning av leverandør vareregister, Farveringen
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       060699 HKO  Nytt program
      *       270901 HKO  Tar hensyn til at dato kan være blank
      *       301101 HKO  Tar i mot informasjon om varegruppe
      * 6.10  230609 BSA  Oppdatert med sporingsinformasjon
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflvwpf    if   e           k disk
     ffenkl1    if   e           k disk    rename(fenkpfr:fenkl1r)
     fflvapf    o  a e             disk
      *
6.10  * Local data area
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av datastruktur
      *
      *  Recordbeskrivelse - Farveringen
      *
     d                 ds
     d d_inpu                       256
     d  d_kode                        2  0 overlay(d_inpu)                      IO-kode
     d  d_vare                        6    overlay(d_inpu:3)                    Varenr
     d  d_tek1                       25    overlay(d_inpu:9)                    Varetekst
     d  d_anta                        6  2 overlay(d_inpu:34)                   Antall i pakning
     d  d_enhe                        4    overlay(d_inpu:40)                   Enhet
     d  d_stor                        8    overlay(d_inpu:44)                   Størrelse (mål)
     d  d_grpr                        7  2 overlay(d_inpu:52)                   Grunnpris
     d  d_raba                        3  1 overlay(d_inpu:59)                   Linjerabatt
     d  d_sapr                        7  2 overlay(d_inpu:62)                   Salgspris
     d  d_rgrp                        3  0 overlay(d_inpu:69)                   Rabattgruppe
     d  d_hgrp                        2    overlay(d_inpu:72)                   Hovedgruppe
     d  d_ugrp                        3    overlay(d_inpu:74)                   Undergruppe
     d  d_pgrp                        3  0 overlay(d_inpu:77)                   Produktgruppe
     d  d_pbok                        7  0 overlay(d_inpu:80)                   Prisboksekvens
     d  d_ldor                        4  0 overlay(d_inpu:87)                   Leverandør
     d  d_lvar                       13    overlay(d_inpu:91)                   Lev. varenummer
     d  d_eann                       13  0 overlay(d_inpu:104)                  Ean-nummer
     d  d_htxt                       30    overlay(d_inpu:117)                  Hovedgruppe-tekst
     d  d_utxt                       30    overlay(d_inpu:147)                  Undergruppe-tekst
     d  d_ptxt                       30    overlay(d_inpu:177)                  Prod.gruppe-tekst
     d  d_inpr                        7  2 overlay(d_inpu:207)                  Innkjøpspris
     d  d_tryk                        1  0 overlay(d_inpu:214)                  Trykknummer kode
     d  d_omre                        4  2 overlay(d_inpu:215)                  Omregningsfaktor
     d  d_egpr                        7  2 overlay(d_inpu:219)                  Egenkalkulert pris
     d  d_skip                        1  0 overlay(d_inpu:226)                  Skip kode
     d  d_dato                        6    overlay(d_inpu:227)                  Endringsdato
     d  d_fill1                       4    overlay(d_inpu:233)                  Filler
     d  d_plun                        4  0 overlay(d_inpu:237)                  PLU-nummer
     d  d_nklf                        4  0 overlay(d_inpu:241)                  NKLF-nummer
     d  d_fill2                       5    overlay(d_inpu:245)                  Filler
     d  d_udat                        6    overlay(d_inpu:250)                  Utlagt dato
     d  d_ekod                        1  0 overlay(d_inpu:256)                  Endringskode
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_ldor          s              6  0
      *
      * Definering av variable i nøkler
      *
     d fenkl1_enhf     s                   like(feenhf)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_tek2          s              8
     d w_posi          s              1  0
     d w_dato          s              6  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser input-fil, og legger data ut på leverandør vareregister
      *
     c                   read      flvwpf                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      d_inpu = fwirad
      *
      * Skriver til leverandør vareregister
      *
     c                   eval      fwfirm = p_firm
     c                   eval      fwldor = p_ldor
     c                   eval      fwekod = *blank
     c                   eval      fwtek1 = d_tek1
     c                   eval      fwtek2 = %trim(d_stor)
     c                   eval      fwlvar = d_vare
     c                   eval      fweann = d_eann
     c                   eval      fwnobb = 0
     c                   eval      fwdato = *loval
     c                   eval      fwenhe = d_enhe
     c                   eval      fwinpr = d_inpr
     c                   eval      fwsapr = d_sapr
     c                   eval      fwdato = *loval
     c                   eval      fwvgrp = %trim(d_hgrp) + %trim(d_ugrp)
     c                   eval      fwbmen = d_anta
     c                   eval      fwhkod = 1
      *
      * Konkatinerer størrelse inn i varetekst-1
      *
     c                   if        fwtek1 <> *blank and
     c                             fwtek2 <> *blank
      *
     c                   eval      w_tek2 = fwtek2
      *
     c     ' '           scan      w_tek2        w_posi                   91
     c                   if        *in91 = *on and
     c                             w_posi > 0 and
     c                             w_posi < 8
     c                   eval      w_tek2 = %trim(%subst(w_tek2:1:w_posi) +
     c                             %trim(%subst(w_tek2:w_posi+1:8-w_posi)))
     c                   endif
      *
     c                   eval      fwtek1 = %trim(%trim(fwtek1) + ' ' +
     c                                            w_tek2)
      *
     c                   endif
      *
     c                   eval      fwline = fwline + 1
      *
      * Konverterer enhet
      *
     c                   eval      fenkl1_enhf = d_enhe
     c     fenkl1_key    chain     fenkl1                             91
     c                   if        *in91 = *off
     c                   eval      fwenhe = feenht
     c                   endif
      *
     c                   if        d_dato <> *blank
     c                   move      d_dato        w_dato
     c     *ymd          move      w_dato        fwdato
     c                   endif
      *
     c                   select
     c                   when      d_ekod = 1
     c                   eval      fwekod = 'E'
     c                   when      d_ekod = 3
     c                   eval      fwekod = 'N'
     c                   when      d_ekod = 9
     c                   eval      fwekod = 'S'
     c                   endsl
      *
6.10 c                   time                    fwodat
6.10 c                   time                    fwotim
6.10 c                   eval      fwousr = l_user
6.10 c                   time                    fwedat
6.10 c                   time                    fwetim
6.10 c                   eval      fweusr = l_user
     c                   write     flvapfr
      *
     c                   read      flvwpf                                 90
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
      *
      * Key for oppslag på enhet-konvertering
      *
     c     fenkl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fenkl1_enhf
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
