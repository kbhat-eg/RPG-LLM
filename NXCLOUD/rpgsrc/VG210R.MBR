     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : VG210R
      * Beskrivelse . . : Fakturatyper, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
6.30  *       311018 NHO  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvgktiu    uf a e           k disk    rename(vgktst:vgktiur)
     fvgkti1    if   e           k disk    rename(vgktst:vgkti1r)
     frhovl1    if   e           k disk    rename(rhovpfr:rhovl1r)
      *
     fvg210d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d vgktiu_ftyp     s                   like(vtftyp)
      *
      *
     d vgkti1_ftyp     s                   like(vtftyp)
      *
     d rhovl1_kont     s                   like(rhkont)
     d rhovl1_spes     s                   like(rhspes)
     d rhovl1_avde     s                   like(rhavde)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_fnul          s              1    inz(*off)
      *
     d w_kont          s              5  0
     d w_spes          s              4  0
     d w_avde          s              4  0
     d w_tx30          s             30
      *
     d w_firm          s                   like(vtfirm)
      *
     d s_ftyp          s                   like(vtftyp)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Siden som skal vises
      * w_ffld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *inkg
     c                   exsr      param
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2ftyp <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   eval      vgkti1_ftyp = b1ftyp
     c     vgkti1_key    setll     vgkti1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Faktortype
      *
     c                   if        b2ftyp <> ' '
     c                   eval      w_seqe = *blank
     c                   eval      vgkti1_ftyp = b2ftyp
     c     vgkti1_key    setll     vgkti1
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2ftyp = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1ftyp = b1ftyp
     c                   exsr      hent_txt
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1ftyp = b1ftyp
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1ftyp = b1ftyp
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1ftyp = b1ftyp
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1ftyp = b2ftyp
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Feilindikator 'off'
     c                   eval      *in41 = *off
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1ftyp = ' '
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      vgktiu_ftyp = c1ftyp
     c     vgktiu_key    chain     vgktiu
     c                   if        %found
     c                   eval      *in41 = '1'
     c                   exsr      xc1msg
     c                   if        b_fnul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
     c                   eval      c2htxt = *blank
     c                   eval      c2mtxt = *blank
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1ftyp = ' '
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2ftyp = ' '
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_fnul = *off
      *nina
     c   43              exfmt     c2bld
     c   47              exfmt     c2bld
     c
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_fnul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      vgktiu_ftyp = c1ftyp
     c     vgktiu_key    chain     vgktiu
      *
     c                   if        %found
     c**                 eval      c2ftyp = vtftyp
     c                   eval      c2kalk = vtkalk
     c                   eval      c2hkto = vthkto
     c                   eval      c2motk = vtmotk
     c                   exsr      hent_txt
     c                   eval      c2eusr = vttusr
     c     *dmy          move      vttdat        c2edat
     c                   else
     c                   eval      c2kalk = *blank
     c                   eval      c2hkto = *zero
     c                   eval      c2mtxt = *blanks
     c                   eval      c2htxt = *blanks
     c                   eval      c2motk = *zero
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1ftyp = k1ftyp
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
      * Feilindikatorer off
     c                   eval      *in42 = *off
     c                   eval      *in43 = *off
     c                   eval      *in47 = *off
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
      * Call kontoplan
      *
     c                   if        w_afld = 'C2HKTO'
      *
     c                   eval      w_kont = 0
     c                   eval      w_spes = 0
     c                   eval      w_avde = 0
     c                   eval      w_tx30 = ' '
      *
     c                   call      'RH500R'
     c                   parm                    w_firm
     c                   parm                    w_kont
     c                   parm                    w_spes
     c                   parm                    w_avde
     c                   parm                    w_tx30
      *
     c                   if        w_kont <> 0
     c                   eval      c2hkto = w_kont
     c                   eval      c2htxt = w_tx30
     c                   endif
      *
     c                   goto      c2taga
     c                   endif
      * Mot konto
     c                   if        w_afld = 'C2MOTK'
      *
     c                   eval      w_kont = 0
     c                   eval      w_spes = 0
     c                   eval      w_avde = 0
     c                   eval      w_tx30 = ' '
      *
     c                   call      'RH500R'
     c                   parm                    w_firm
     c                   parm                    w_kont
     c                   parm                    w_spes
     c                   parm                    w_avde
     c                   parm                    w_tx30
      *
     c                   if        w_kont <> 0
     c                   eval      c2motk = w_kont
     c                   eval      c2mtxt = w_tx30
     c                   endif
      *
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
      * Validering av input
      *
      *Faktortype må være utfylt
      *
     c                   if        c2kalk = *blank
     c                   eval      *in42 = *on
     c                   exsr      xc1msg
     c                   goto      c2taga
     c                   endif
      *
      * Dersom kontering er valgt må begge kontofelter være utfylt
      *
     c                   if        c2hkto = 0
     c                             and c2motk <> 0
     c                   eval      c2htxt = *blanks
     c                   eval      *in43 = *on
     c                   exsr      xc1msg
     c                   goto      c2taga
     c                   endif
      *
     c                   if        c2motk = 0
     c                             and c2hkto <> 0
     c                   eval      c2htxt = *blanks
     c                   eval      *in43 = *on
     c                   exsr      xc1msg
     c                   goto      c2taga
     c                   endif
      *
      * Hovedbokskonto må finnes i kontoplan
      *
     c                   if        c2hkto <> 0
     c                   move      c2hkto        rhovl1_kont
     c                   eval      rhovl1_spes = 0
     c                   eval      rhovl1_avde = 0
     c     rhovl1_key    chain     rhovl1
      *
     c                   if        not%found
     c                   eval      *in47 = *on
     c                   eval      c2htxt = *blanks
     c                   exsr      xc1msg
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
      *
      * Motkonto må finnes i kontoplan
      *
     c                   if        c2motk <> 0
     c                   move      c2motk        rhovl1_kont
     c                   eval      rhovl1_spes = 0
     c                   eval      rhovl1_avde = 0
     c     rhovl1_key    chain     rhovl1
      *
     c                   if        not%found
     c                   eval      *in47 = *on
     c                   eval      c2mtxt = *blanks
     c                   exsr      xc1msg
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   vgktiur
      *
     c                   eval      vgktiu_ftyp = c1ftyp
     c     vgktiu_key    chain     vgktiur
      *
     c                   eval      vtkalk = c2kalk
     c                   eval      vthkto = c2hkto
     c                   eval      vtmotk = c2motk
      *
     c                   time                    vttdat
     c                   eval      vttusr = l_user
      *
     c                   if        %found
     c                   update    vgktiur
     c                   else
     c                   eval      vtfirm = w_firm
     c                   eval      vtftyp = c1ftyp
     c                   write     vgktiur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1ftyp = vtftyp
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      vgktiu_ftyp = d1ftyp
     c     vgktiu_key    chain     vgktiu
      *
6.20 c     d1tagb        tag
6.20  *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
6.20  * Sjekker om rabattkategori finnes i kundefilen
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      vgktiu_ftyp = d1ftyp
     c     vgktiu_key    chain     vgktiur
      *
     c                   delete    vgktiur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_ftyp = k1ftyp
      *
      * Fyller kopierings-bildet
      *
     c                   eval      vgktiu_ftyp = k1ftyp
     c     vgktiu_key    chain     vgktiu
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
      * Sett av feilindikator
     c                   eval      *in46 = *off
     c                   eval      *in45 = *off
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1ftyp = ' '
     c                   eval      *in45 = *on
     c                   exsr      xc1msg
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      vgktiu_ftyp = k1ftyp
     c     vgktiu_key    chain     vgktiu
     c                   if        %found
     c                   eval      *in46 = *on
     c                   exsr      xc1msg
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1ftyp = s_ftyp
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      vgkti1
      *
     c                   if        %eof or
     c                             vtfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      vgkti1_ftyp = vtftyp
      *
     c                   eval      b1valg = 0
      *
     c                   eval      b1ftyp = vtftyp
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c     vgkti1_key    setgt     vgkti1
     c                   readp     vgkti1
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     vgkti1
      *
     c                   if        %eof or
     c                             vtfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      vgkti1_ftyp = vtftyp
      *
     c                   enddo
      *
     c                   if        %eof
     c     vgkti1_key    setll     vgkti1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for vedlikehold av kostprisfaktorer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     param         begsr
      *
     c                   call      'VG205R'
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på faktyratype
      *
     c     vgktiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vgktiu_ftyp
      *
      *
     c     vgkti1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vgkti1_ftyp
      *
      * Key for kontoplan
      *
     c     rhovl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovl1_kont
     c                   kfld                    rhovl1_spes
     c                   kfld                    rhovl1_avde
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      vgktiu_ftyp = ' '
     c     vgktiu_key    setll     vgktiu
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent tekster når linje skal vedlikeholdes/Se på
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_txt      begsr
      *
      * Konto
      *
      * Henter tekst
     c                   move      c2hkto        rhovl1_kont
     c                   eval      rhovl1_spes = 0
     c                   eval      rhovl1_avde = 0
     c     rhovl1_key    chain     rhovl1
      *
     c                   if        %found
     c                   eval      c2htxt = rhtext
     c                   else
     c                   move      *blanks       c2htxt
     c                   endif
      *
      * Mot konto
      *
      * Henter tekst
     c                   move      c2motk        rhovl1_kont
     c                   eval      rhovl1_spes = 0
     c                   eval      rhovl1_avde = 0
     c     rhovl1_key    chain     rhovl1
      *
     c                   if        %found
     c                   eval      c2mtxt = rhtext
     c                   else
     c                   move      *blanks       c2mtxt
     c                   endif
      *
     c                   endsr
