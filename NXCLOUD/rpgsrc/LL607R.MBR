      /TITLE  Utplukk til lagerverdiliste
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : LL607R
      * Beskrivelse . . : Utplukk til lagerverdiliste
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.
      * --------- ------  --------------------------------------------------
      *           010200  Nytt program                             KOB
5.20  * R5.20 171003 AMD  Utvidet med mulighet for å kunne kontere på
      *                   leveringstype
5.40  * T5.40 290305 BOF  Hvis l.type = 0, hentes lagerbeh. pr. dato
5.50  * T5.50 151107 BOF  Hvis pr. dato - skal også pris være pr.dato
5.70  *  PRGR 021208 bof  Utvidet med prisgruppe
6.30  * 6.30  030117 bsa  Sender med lager til prisprogram.
7.01  * 7.01 140319  ask  Mulighet lagerverdiliste til gj.sn. kostpris
7.02  *  6.30 130117 bsa  Legger ut varetekst i tilleggsregister.
7.03  * 7.03 191021  ask  Henting av gl.gjsn. kostpris er flyttet til LL609R
8.01  *PRG2   150622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  BRUK AV INDIKATORER
      *
      *       LR = Avslutt program
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer
      *    70-79 = Arbeidsindikatorer i sub-rutiner
      *    80-89 = Arbeidsindikatorer ordinære
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
5.40 fvhisl3    if   e           k disk    rename(vhispfr:vhisl3r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     flwa1lu    o    e             disk    rename(lwa1pfr:lwa1lur)
7.02 flwa4lu    o    e             disk    rename(lwa4pfr:lwa4lur)
      *
      * Local Data Area
      *
     d                uds
7.02 d l_user                911    920
     d l_firm                944    946  0
      *
      *  Parameter-rec
      *
     d                 ds
     d p_prec                       128
     d  p_fvar                        8  0 overlay(p_prec)
     d  p_tvar                        8  0 overlay(p_prec:9)
     d  p_flag                        2  0 overlay(p_prec:17)
     d  p_tlag                        2  0 overlay(p_prec:19)
     d  p_fogr                        2  0 overlay(p_prec:21)
     d  p_togr                        2  0 overlay(p_prec:23)
     d  p_fhgr                        2  0 overlay(p_prec:25)
     d  p_thgr                        2  0 overlay(p_prec:27)
     d  p_fugr                        3  0 overlay(p_prec:29)
     d  p_tugr                        3  0 overlay(p_prec:32)
     d  p_levn                        6  0 overlay(p_prec:35)
     d  p_sakb                        4    overlay(p_prec:41)
     d  p_sort                        1  0 overlay(p_prec:45)
     d  p_type                        1  0 overlay(p_prec:46)
     d  p_vlin                        1  0 overlay(p_prec:47)
5.40 d  p_batc                       10    overlay(p_prec:48)
5.40 d  p_dath                        6  0 overlay(p_prec:58)
7.01 d  p_gjko                        1  0 overlay(p_prec:64)
7.01 d  p_dumm                       10    overlay(p_prec:65)
      *
      * Parametere for kontohenvisning
      *
     d p_stat          s              1
     d p_otyp          s              2
     d p_ogrp          s              2  0
     d p_hgrp          s              2  0
     d p_ugrp          s              3  0
     d p_vare          s             15
5.20 d p_hele          s            128
6.30 d p_lage          s              2  0
      *
      * Definering av datastruktur
      *
     d                 ds
     d d_dato_8                       8  0
     d  d_aar                         4  0 overlay(d_dato_8)
     d  d_mnd                         2  0 overlay(d_dato_8:5)
     d  d_dag                         2  0 overlay(d_dato_8:7)
      *
     d                 ds
5.20 d d_hele                       128
     d  d_kav1                        4  0 overlay(d_hele)
     d  d_kko1                        6  0 overlay(d_hele:5)
     d  d_ksp1                        4  0 overlay(d_hele:11)
     d  d_kav2                        4  0 overlay(d_hele:15)
     d  d_kko2                        6  0 overlay(d_hele:19)
     d  d_ksp2                        4  0 overlay(d_hele:25)
     d  d_kav3                        4  0 overlay(d_hele:29)
     d  d_kko3                        6  0 overlay(d_hele:33)
     d  d_ksp3                        4  0 overlay(d_hele:39)
     d  d_kav4                        4  0 overlay(d_hele:43)
     d  d_kko4                        6  0 overlay(d_hele:47)
     d  d_ksp4                        4  0 overlay(d_hele:53)
     d  d_kav5                        4  0 overlay(d_hele:57)
     d  d_kko5                        6  0 overlay(d_hele:61)
     d  d_ksp5                        4  0 overlay(d_hele:67)
     d  d_kav6                        4  0 overlay(d_hele:71)
     d  d_kko6                        6  0 overlay(d_hele:75)
     d  d_ksp6                        4  0 overlay(d_hele:81)
     d  d_kav7                        4  0 overlay(d_hele:85)
     d  d_kko7                        6  0 overlay(d_hele:89)
     d  d_ksp7                        4  0 overlay(d_hele:95)
     d  d_khev                        4    overlay(d_hele:99)
5.20 d  d_nivo                        1    overlay(d_hele:103)
      *
      * Key på arbeidsfil
      *
     d                 ds
     d d_mkey                        65
     d  d_mk01                        8  0 overlay(d_mkey)
     d  d_mk01a                       8    overlay(d_mkey)
     d  d_mk02                        8  0 overlay(d_mkey:9)
     d  d_mk02a                       8    overlay(d_mkey:9)
     d  d_mk03                        8  0 overlay(d_mkey:17)
     d  d_mk04                        8  0 overlay(d_mkey:25)
     d  d_mk05                        8  0 overlay(d_mkey:33)
      *
      * Datastrukturer for testing på varegrupper
      *
     d                 ds
     d f_vgrp                         7
     d  f_ogrp                        2  0 overlay(f_vgrp)
     d  f_hgrp                        2  0 overlay(f_vgrp:3)
     d  f_ugrp                        3  0 overlay(f_vgrp:5)
      *
     d                 ds
     d t_vgrp                         7
     d  t_ogrp                        2  0 overlay(t_vgrp)
     d  t_hgrp                        2  0 overlay(t_vgrp:3)
     d  t_ugrp                        3  0 overlay(t_vgrp:5)
      *
     d                 ds
     d v_vgrp                         7
     d  v_ogrp                        2  0 overlay(v_vgrp)
     d  v_hgrp                        2  0 overlay(v_vgrp:3)
     d  v_ugrp                        3  0 overlay(v_vgrp:5)
      *
      * Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vlagl1_vare     s                   like(vlvare)
     d vlagl1_lage     s                   like(vllage)
5.40 d vhisl3_vare     s                   like(vhvare)
5.40 d vhisl3_lage     s                   like(vhlage)
5.40 d vhisl3_dato     s                   like(vhdato)
5.40 d vhisl3_time     s                   like(vhtime)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vogrl1_oogr     s                   like(vgoogr)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vvfirm)
     d w_parm          s            128
     d w_vare          s              8  0
     d w_behd          s                   like(vlbehl)
      *
     d w_dato          s               d   datfmt(*iso)
     d w_mvaf          s             10  9
     d w_mvas          s              6  2
     d w_mvat          s              5  4
     d w_kpny          s              9  2
     d w_spny          s              9  2
     d w_khev          s              5
5.70 d w_avde          s              4  0
8.01 d w_prgr          s              2
      *
     d p_firm          s                   like(vlfirm)
     d p_lety          s              1  0
     d p_dato          s                   like(vltrda)
     d p_enhe          s              3
     d p_sapr          s              9  2
     d p_gmpr          s              9  2
     d p_kopr          s              9  2
     d p_inpr          s              9  2
      *
      * Definering av midlertidige felt
      *
     d vgopra          s                   like(vgupra)
     d vghpra          s                   like(vgupra)
     d vgolag          s                   like(vgulag)
     d vghlag          s                   like(vgulag)
7.01 d w_gjko          s              9  2
7.01 d w_datg          s              8  0
      *
      *
      *---------------------------------------------------------------
      *  Behandle lagerrecords
      *---------------------------------------------------------------
      *
     c     les_lager     tag
      *
     c                   read      vlagl1                                 90
     c                   if        *in90 = *on
     c                   goto      end_pgm
     c                   endif
     c                   movel     vlvare        w_vare
     c                   move      vlvare        p_vare
      *
      * Sjekk riktig firma
      *
     c                   if        vlfirm <> l_firm
     c                   goto      les_lager
     c                   endif
      *
      * Sjekk varenr. fra - til
      *
     c                   if        w_vare < p_fvar
     c                             or w_vare > p_tvar
     c                   goto      les_lager
     c                   endif
      *
      * Sjekk lagernr. fra - til
      *
     c                   if        vllage < p_flag
     c                             or vllage > p_tlag
     c                   goto      les_lager
     c                   endif
      *
      * Sjekk om varenr. i registeret
      *
     c                   eval      vvarl1_vare = vlvare
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *on
     c                   goto      les_lager
     c                   endif
      *
      * Sjekk om varegruppe fra - til
      *
     c                   eval      v_ogrp = vvogrp
     c                   eval      v_hgrp = vvhgrp
     c                   eval      v_ugrp = vvugrp
      *
     c                   if        v_vgrp < f_vgrp
     c                             or v_vgrp > t_vgrp
     c                   goto      les_lager
     c                   endif
      *
      * Sjekk om undergruppe skal lagerstyres
      *
     c                   eval      vugrl1_uogr = vvogrp
     c                   eval      vugrl1_uhgr = vvhgrp
     c                   eval      vugrl1_uugr = vvugrp
     c     vugrl1_key    chain     vugrl1                             92
     c                   if        *in92 = *off and
     c                             vgulag = 1
     c                   goto      les_lager
     c                   endif
      *
      * Sjekk om hovedgruppe skal lagerstyres
      *
     c                   eval      vhgrl1_hogr = vvogrp
     c                   eval      vhgrl1_hhgr = vvhgrp
     c     vhgrl1_key    chain     vhgrl1                             92
     c                   if        *in92 = *off and
     c                             vghlag = 1
     c                   goto      les_lager
     c                   endif
      *
      * Sjekk om overgruppe skal lagerstyres
      *
     c                   eval      vogrl1_oogr = vvogrp
     c     vogrl1_key    chain     vogrl1                             92
     c                   if        *in92 = *off and
     c                             vgolag = 1
     c                   goto      les_lager
     c                   endif
      *
      * Sjekk om riktig leverandør - hvis leverandør er valgt
      *
     c                   if        p_levn <> *zero
     c                             and p_levn <> vvldor
     c                   goto      les_lager
     c                   endif
      *
      * Sjekk om riktig saksbehandler - hvis saksbehandler er valgt
      *
     c                   if        p_sakb <> *blank and
     c                             p_sakb <> vgopra and
     c                             p_sakb <> vghpra and
     c                             p_sakb <> vgupra
     c                   goto      les_lager
     c                   endif
      *
      * Sjekk om lagervare
      *
5.70 c                   if        vlvtyp <> 'L'
     c                   goto      les_lager
     c                   endif
      *
      * Bygg opp sorteringsbegrep
      *
     c                   eval      d_mkey = *blank
      *
     c                   select
     c                   when      p_sort = 1
     c                   eval      d_mk01 = w_vare
     c                   eval      d_mk02 = vllage
     c                   when      p_sort = 2
     c                   eval      d_mk01 = vvogrp
     c                   eval      d_mk02 = vvhgrp
     c                   eval      d_mk03 = vvugrp
     c                   eval      d_mk04 = w_vare
     c                   eval      d_mk05 = vllage
     c                   when      p_sort = 3
     c                   exsr      hnt_konto
     c                   movel     w_khev        d_mk01a
     c                   eval      d_mk02 = vllage
     c                   eval      d_mk03 = w_vare
     c                   when      p_sort = 4
     c                   eval      d_mk01 = vllage
     c                   eval      d_mk02 = w_vare
     c                   when      p_sort = 5
     c                   eval      d_mk01 = vllage
     c                   eval      d_mk02 = vvogrp
     c                   eval      d_mk03 = vvhgrp
     c                   eval      d_mk04 = vvugrp
     c                   eval      d_mk05 = w_vare
     c                   when      p_sort = 6
     c                   exsr      hnt_konto
     c                   eval      d_mk01 = vllage
     c                   movel     w_khev        d_mk02a
     c                   eval      d_mk03 = w_vare
     c                   endsl
     c                   eval      mlakey = d_mkey
      *
      * Finn riktige priser
      *
5.70 c                   exsr      hent_prisgr
     c                   eval      p_lety = 0
6.30 c                   eval      p_lage = vllage
      *
     c                   call      'VP730R'
     c                   parm                    w_firm
     c                   parm                    p_vare
5.70 c                   parm                    w_prgr
     c                   parm                    p_lety
     c                   parm                    p_dato
     c                   parm                    p_enhe
     c                   parm                    p_sapr
     c                   parm                    p_gmpr
     c                   parm                    p_kopr
     c                   parm                    p_inpr
6.30 c                   parm                    p_lage
      *
     c                   eval      mlenhl = p_enhe
     c                   eval      mlspri = p_sapr
7.03 c*                  if        p_gjko = 1
7.03 c*                  eval      w_datg = %dec(p_dato:*iso)
7.03 c*                  call      'VG701R'
7.03 c*                  parm                    p_vare
7.03 c*                  parm                    p_lage
7.03 c*                  parm                    w_datg
7.03 c*                  parm                    w_gjko
7.03 c*                  eval      mlkpri = w_gjko
7.03 c*                  else
     c                   eval      mlkpri = p_kopr
7.03 c*                  endif
     c                   eval      mlipri = p_inpr
      *
      * Beregn lagerverdi
      *
     c                   if        p_type = 0
5.40 c                   if        p_dath <> UDATE
5.40 c                   exsr      hnt_behl
5.40 c                   eval      mlomst = mlspri * w_behd
5.40 c                   eval      mlkost = mlkpri * w_behd
5.40 c                   eval      mlinkt = mlipri * w_behd
5.40 c                   else
     c                   eval      mlomst = mlspri * vlbehl
     c                   eval      mlkost = mlkpri * vlbehl
     c                   eval      mlinkt = mlipri * vlbehl
5.40 c                   endif
     c                   endif
      *
      * Lagerverdiliste pr. 01.01
      *
     c                   if        p_type = 1
     c                   eval      mlomst = mlspri * vlb101
     c                   eval      mlkost = mlkpri * vlb101
     c                   eval      mlinkt = mlipri * vlb101
     c                   endif
      *
      * Flytt til arbeidsfil
      *
     c                   eval      mlfirm = vlfirm
     c                   movel     vlvare        mlvare
     c                   eval      mllage = vllage
     c                   eval      mlplas = vlplas
     c                   eval      mlpakk = vlpakk
     c                   eval      mloord = vloord
     c                   eval      mlores = vlores
     c                   eval      mlbbes = vlbbes
     c                   eval      mlbbes = vlbbes
     c                   eval      mlbres = vlbres
     c                   eval      mlinng = vlinng
     c                   eval      mlutta = vlutta
     c                   eval      mljust = vljust
     c                   eval      mltell = vltell
     c                   eval      mlsalg = vlsalg
     c                   eval      mlretu = vlretu
     c                   eval      mlb101 = vlb101
      *
5.40 c                   select
5.40 c                   when      p_type = 0
     c                   if        p_dath <> UDATE
5.40 c                   eval      mlbehl = w_behd
     c                   else
     c                   eval      mlbehl = vlbehl
     c                   endif
5.40 c                   when      p_type = 1
5.40 c                   eval      mlbehl = vlb101
5.40 c                   endsl
      *
     c                   eval      mlbeve = vlbeve
     c                   eval      mlkpny = vlkpny
     c                   eval      mlmini = vlmini
     c                   eval      mlmaks = vlmaks
     c                   eval      mlltid = vlltid
     c                   eval      mlbpun = vlbpun
     c                   eval      mlbmen = vlbmen
     c                   eval      mlsikl = vlsikl
     c                   eval      mltrda = vltrda
     c                   eval      mlopda = vlopda
     c                   eval      mlsdat = vlsdat
     c                   eval      mlogrp = vvogrp
     c                   eval      mlhgrp = vvhgrp
     c                   eval      mlugrp = vvugrp
     c                   eval      mlkhev = w_khev
7.02  *
7.02  * Legg ut varetekst - ingen stor nytte her, men LL608 er det viktig
7.02  * Gjøres her også, slik at alle linjer behandles likt i LL609.
7.02  *
7.02 c                   eval      l4firm = vvfirm
7.02 c                   eval      l4akey = d_mkey
7.02 c                   eval      l4tek1 = vvtek1
7.02  *
7.02 c                   eval      l4ousr = l_user
7.02 c                   time                    l4odat
7.02 c                   time                    l4otim
7.02  *
7.02 c                   eval      l4eusr = l_user
7.02 c                   time                    l4edat
7.02 c                   time                    l4etim
7.02 c                   write     lwa4lur
      *
     c                   write     lwa1lur
      *
     c                   goto      les_lager
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
      *
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av kontohenvisning
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hnt_konto     begsr
      *
     c                   eval      p_stat = *blank
     c                   eval      p_otyp = *blank
     c                   eval      p_ogrp = vvogrp
     c                   eval      p_hgrp = vvhgrp
     c                   eval      p_ugrp = vvugrp
     c                   movel     vlvare        p_vare
      *
     c                   call      'VA720R'
     c                   parm                    p_stat
5.20 c                   parm                    p_lety
     c                   parm                    p_otyp
     c                   parm                    p_ogrp
     c                   parm                    p_hgrp
     c                   parm                    p_ugrp
     c                   parm                    p_vare
     c                   parm                    p_hele
      *
     c                   eval      d_hele = p_hele
     c                   eval      w_khev = d_khev
      *
     c                   endsr
5.40 c/eject
5.40  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.40  * Subrutine for henting av beholdning pr. en dato (hist.lagerb)
5.40  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.40  *
5.40 c     hnt_behl      begsr
5.40  *
5.40 c                   eval      vhisl3_vare = vlvare
5.40 c                   eval      vhisl3_lage = vllage
5.40 c     *dmy          move      p_dath        vhisl3_dato
5.40 c                   eval      vhisl3_time = *hival
5.40 c     vhisl3_key    setgt     vhisl3
5.40 c                   readp     vhisl3
5.40 c                   if        not %eof and
5.40 c                             vhvare = vhisl3_vare and
5.40 c                             vhlage = vhisl3_lage
5.40 c                   eval      w_behd = vhbehl
5.40 c                   else
5.40 c                   eval      w_behd = 0
5.40 c                   endif
5.40  *
5.40 c                   endsr
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5 70  * Subrutine for å hente prisgruppe
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.70  *
5.70 c     hent_prisgr   begsr
5.70  *
5.70 c                   eval      w_avde = 0
5.70 c                   call      'VL712R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vllage
5.70 c                   parm                    w_avde
5.70 c                   parm                    w_prgr
5.70  *
5.70 c                   endsr
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key les av varer
      *
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les av lager
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
5.40  *
5.40  * Key for les av lager historikk
5.40  *
5.40 c     vhisl3_key    klist
5.40 c                   kfld                    w_firm
5.40 c                   kfld                    vhisl3_vare
5.40 c                   kfld                    vhisl3_lage
5.40 c                   kfld                    vhisl3_dato
5.40 c                   kfld                    vhisl3_time
      *
      * Key for les av undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for laes av hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for les av overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    w_parm
     c                   eval      p_prec = w_parm
      *
      * Legge inn firmanummer
      *
     c                   move      l_firm        w_firm
      *
      * Bygg opp varegryppe fra
      *
     c                   eval      f_ogrp = p_fogr
     c                   eval      f_hgrp = p_fhgr
     c                   eval      f_ugrp = p_fugr
      *
      * Bygg opp varegryppe til
      *
     c                   eval      t_ogrp = p_togr
     c                   eval      t_hgrp = p_thgr
     c                   eval      t_ugrp = p_tugr
      *
      * Sett startpunkt for les
      *
     c                   movel     p_fvar        vlagl1_vare
     c                   move      p_flag        vlagl1_lage
     c     vlagl1_key    setll     vlagl1
      *
      * Sett prisberegningsdato
      *
     c                   if        p_type = 0
5.50 c                   if        p_dath <> 0
5.50 c     *dmy          move      p_dath        p_dato
5.50 c                   else
     c                   time                    p_dato
5.50 c                   endif
     c                   else
     c                   eval      p_dato = *loval
     c                   extrct    p_dato:*d     d_dag
     c                   extrct    p_dato:*m     d_mnd
     c                   time                    p_dato
     c                   extrct    p_dato:*y     d_aar
     c     *ymd          move      d_dato_8      p_dato
     c                   endif
      *
     c                   endsr
