      /title  ASOKON Leverandør, oppdaterer søketekst på alle leverandører
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON
      * Program . . . . : RL751R
      * Beskrivelse . . : Leverandør, oppdaterer søketekst på alle lev.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       250101 KOB  Nytt program
      * R5.10 100603 KOB  Lagt inn alfafelt først i søketekst
      * R5.20 101103 KOB  Lagt inn foretaksnummer i søketekst
      * R5.60 120107 KOB  Lagt inn bankkontonummer i søketekst
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frlsolu    uf a e           k disk    rename(rlsopfr:rlsolur)
      *
      * Definering av LDA
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d rlsolu_slev     s                   like(rlslev)
      *
      * Definering av variable
      *
     d w_l             s              2  0
     d w_leng          s              2  0
     d w_levr_alf      s              6
     d w_ftnr_alf      s              9
     d w_bgir_alf      s             11
      *
     d w_firm          s                   like(rlfirm)
     d w_tlfn          s                   like(rltlfn)
     d w_mobn          s                   like(rlmobn)
      *
      * Definering av konstanter
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøå'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅ'
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     rlevl1_key    setll     rlevl1
     c     rlevl1_key    reade     rlevl1                                 90
     c                   dow       *in90 = *off
      *
     c                   exsr      oppdater
      *
     c     rlevl1_key    reade     rlevl1                                 90
     c                   enddo
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer søketekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
      * Sletter eksisterende søketekst
      *
     c                   eval      rlsolu_slev = rllevr
     c     rlsolu_key    chain     rlsolur                            91
     c                   if        *in91 = *off
     c                   delete    rlsolur
     c                   endif
      *
      * Oppretter ny søketekst....
      *
     c                   eval      rlsfir = w_firm
     c                   eval      rlslev = rllevr
      *
      * Alfafelt
      *
     c                   eval      rlstek = rlalfa
      *
      * Navn
      *
     c     Lo:Up         xlate     rlnavn        rlnavn
     c                   eval      rlstek = %trimr(rlstek) + ' ' +
     c                                      %trim(rlnavn)
      *
      * Adresse-1
      *
     c                   if        rlgate <> *blank
     c     Lo:Up         xlate     rlgate        rlgate
     c                   eval      rlstek = %trimr(rlstek) + ' ' +
     c                                      %trim(rlgate)
     c                   endif
      *
      * Adresse-2
      *
     c                   if        rladr2 <> *blank
     c     Lo:Up         xlate     rladr2        rladr2
     c                   eval      rlstek = %trimr(rlstek) + ' ' +
     c                                      %trim(rladr2)
     c                   endif
      *
      * Poststed
      *
     c                   if        rlsted <> *blank
     c     Lo:Up         xlate     rlsted        rlsted
     c                   eval      rlstek = %trimr(rlstek) + ' ' +
     c                                      %trim(rlsted)
     c                   endif
      *
      * Telefonnummer
      *
     c                   if        rltlfn <> *blank
      *
     c                   eval      w_tlfn = %trim(rltlfn)
     c                   eval      w_leng = %len(%trim(rltlfn))
      *
     c     ' '           scan      w_tlfn        w_l
     c                   dow       w_l <> 0 and
     c                             w_l <  w_leng
     c                   eval      w_tlfn = %subst(w_tlfn:1:w_l-1) +
     c                                      %subst(w_tlfn:w_l+1:11-w_l)
     c                   eval      w_tlfn = %trim(w_tlfn)
     c                   eval      w_leng = %len(%trim(w_tlfn))
     c     ' '           scan      w_tlfn        w_l
     c                   enddo
      *
     c                   eval      rlstek = %trimr(rlstek) + ' ' +
     c                                      %trim(w_tlfn)
      *
     c                   endif
      *
      * Mobiltelefon-nummer
      *
     c                   if        rlmobn <> *blank
      *
     c                   eval      w_mobn = %trim(rlmobn)
     c                   eval      w_leng = %len(%trim(rlmobn))
      *
     c     ' '           scan      w_mobn        w_l
     c                   dow       w_l <> 0 and
     c                             w_l <  w_leng
     c                   eval      w_mobn = %subst(w_mobn:1:w_l-1) +
     c                                      %subst(w_mobn:w_l+1:11-w_l)
     c                   eval      w_mobn = %trim(w_mobn)
     c                   eval      w_leng = %len(%trim(w_mobn))
     c     ' '           scan      w_mobn        w_l
     c                   enddo
      *
     c                   eval      rlstek = %trimr(rlstek) + ' ' +
     c                                      %trim(w_mobn)
      *
     c                   endif
      *
      * Leverandørnummer
      *
     c                   move      rllevr        w_levr_alf
      *
     c                   eval      rlstek = %trimr(rlstek) + ' ' +
     c                                      w_levr_alf
      *
      * Foretaksnummer
      *
     c                   if        rlftnr <> 0
     c                   move      rlftnr        w_ftnr_alf
     c                   eval      rlstek = %trimr(rlstek) + ' ' +
     c                                      w_ftnr_alf
     c                   endif
      *
      * Bankgironummer
      *
     c                   if        rlbgir <> 0
     c                   move      rlbgir        w_bgir_alf
     c                   eval      rlstek = %trimr(rlstek) + ' ' +
     c                                      w_bgir_alf
     c                   endif
      *
      * Oppretter søketekst
      *
     c                   write     rlsolur
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les på leverandørregister (sortert alfabetisk)
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppdatering av søketekst-register
      *
     c     rlsolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlsolu_slev
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
