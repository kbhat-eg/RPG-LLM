     h option(*nodebugio) datedit(*dmy)
     h decedit('0.')
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : RB014R
      * Beskrivelse . . : Leser linjer fra FOVF til kommaseparert til
      *                   (Business)
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * ----- ------ ---- -------------------------------------------------
      *       181012 nho  Visma  Business
6.22  * V6.20 051113 nho  Avstand mellom felter
6.23  * V6.20 141014 nho  Test på firma
6.30  *       170117 nho  Legger ut bilagsnr i første felt
6.31  *       170117 nho  Tester på hvilken kid som skal legges ut
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     ffoffut    ip   e           k disk
     fsohel1    if   e           k disk    rename(sohepfr:sohel1r)
6.31 faforl1    if   e           k disk    rename(aforpfr:aforl1r)
      *
      *
     frv01pf    o    e           k disk
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
     d valdat          s                   like(fffdat)
     d nval08          s              8  0
     d nval04          s              4  0
     d nfor08          s              8  0
     d nfor04          s              4  0
     d nbil08          s              8  0
     d nbil04          s              4  0
     d xxbiln          s                   like(ffbiln)
     d tell            s              1  0
     d f1momd          s                   like(ffdmom)
     d f1momc          s                   like(ffcmom)
     d zfbelp          s             12  4
     d w_binr          s                   like(sobinr)
     d w_firm          s                   like(sofirm)
     d w_aarr          s                   like(soaarr)
6.31 d t1kkid          s             21
6.31 d w_kidd          s             24
6.31 d w_kid2          s             24
6.31 d w_kidr          s              8  0
      *
6.31 d aforl1_ruti     s                   like(afruti)
      * Datastruktur for valuteringsdato
     d                 ds
     d d_fdati                 1     10
     d  d_fa                   1      4
     d  d_f1                   5      5
     d  d_fn                   6      7
     d  d_f2                   8      8
     d  d_fd                   9     10
     d*
      * Nøkkel dato
     d                 ds
     d d_dati                  1     10
     d  d_aa                   1      4
     d  d_a1                   5      5
     d  d_mn                   6      7
     d  d_a2                   8      8
     d  d_dd                   9     10
      *
      *
      * Datastruktur for forfallsdato
     d                 ds
     d f_fdati                 1     10
     d  f_fa                   1      4
     d  f_f1                   5      5
     d  f_fn                   6      7
     d  f_f2                   8      8
     d  f_fd                   9     10
     d*
      *
      * Datastruktur for bilagsdato
     d                 ds
     d b_fdati                 1     10
     d  b_fa                   1      4
     d  b_f1                   5      5
     d  b_fn                   6      7
     d  b_f2                   8      8
     d  b_fd                   9     10
     d*
      *
      * Key for oppslag på faktura-hode-register
     c     sohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_aarr
     c                   kfld                    w_binr
      *
6.31  * Key for oppslag på rutineregister
6.31  *
6.31 c     aforl1_key    klist
6.31 c                   kfld                    aforl1_ruti
      *
      *
6.23  *
6.23 c                   if        l_firm <> fffirm
6.23 c                   goto      slutt
6.23 c                   endif
     c* Heading for kjøring
     c                   if        tell   = 0
     c                   add       1             tell
     c                   eval      rvdata = *blank
     c                   eval      rvdata = %trim('@FIRM_BEGIN(279159)')
     c                   write     rv01pfr
      *
     c                   eval      rvdata = *blank
     c                   eval      rvdata = %trim('@IMPORT_METHOD(3)')
     c                   write     rv01pfr
      *
     c                   eval      rvdata = *blank
     c                   eval      rvdata = %trim('@WaBnd(Descr,ValDT)')
     c                   write     rv01pfr
      *
     c                   eval      rvdata = *blank
      *
      *
      * Dagense dato
     c                   time                    valdat
     c                   movel     valdat        d_fdati
     c                   movel     d_fa          nval08
     c                   movel     d_fn          nval04
     c                   move      d_fd          nval04
     c                   move      nval04        nval08
6.22 c                   eval      rvdata = %trim('"KREDITSYSTEM" "') +
     c                                      %char(nval08) +
     c                                      %trim('"')
     c                   write     rv01pfr
      *
     c                   eval      rvdata = *blank
     c                   eval      rvdata = %trim('@WaVo(VoNo,VoDt,') +
     c                                           ('ValDt,Votp,Txt,') +
     c                                           ('DbAcNo,DbTxcd,CrAcNo,') +
     c                                           ('CrTxCd,InvoNo,Am,R1,') +
     c                                           ('DueDt,Cid,R2)')
     c                   write     rv01pfr
      *
     c                   endif
      *
      ***********************************************************
     c
     c* Blanker felt
     c                   eval      d_dati  = *blanks
      *
     c                   move      ffbdat        d_dati
      *
      * Forfallsdato
     c                   movel     fffdat        f_fdati
     c                   movel     f_fa          nfor08
     c                   movel     f_fn          nfor04
     c                   move      f_fd          nfor04
     c                   move      nfor04        nfor08
      *
      * Bilagsdato
     c                   movel     ffbdat        b_fdati
     c                   movel     b_fa          nbil08
     c                   movel     b_fn          nbil04
     c                   move      b_fd          nbil04
     c                   move      nbil04        nbil08
     c*
      * Beløp skal legges ut med 4 desimaler
     c                   z-add     ffbelp        zfbelp
      * Legger ut detaljer fra bovf
      * Nullstiller
     c                   eval      f1momd = '0'
     c                   eval      f1momc = '0'
      *
     c                   exsr      control_init
6.31  * Finner hvilken kid som skal legges ut
6.31 c                   eval      aforl1_ruti = 'FFAKT'
6.31 c     aforl1_key    chain     aforl1r
6.31  *
6.31 c                   eval      t1kkid = *blank
6.31 c                   if        afkidk = 1
6.31 c                   movel     sokidd        t1kkid
6.31 c                   endif
6.31  *
6.31 c                   if        afkidk = 2
6.31 c                   movel     sokidr        t1kkid
6.31 c                   endif
6.31  *
6.31  *
6.31 c                   if        afkidk = 3
6.31 c                   movel     sokidr        w_kidr
6.31 c                   call      'AK710R'
6.31 c                   parm                    w_firm
6.31 c                   parm                    w_kidr
6.31 c                   parm                    w_kidd
6.31 c                   parm                    w_kid2
6.31 c                   movel     w_kid2        t1kkid
6.31 c                   endif
6.31  *
      *
     c                   if        ffdkto <> *zeros and
     c                             ffdkto >=  1000 and
     c                             ffdkto <=  9999
     c                   eval      f1momd = ffdmom
     c                   endif
      *
     c                   if        ffckto <> *zeros and
     c                             ffckto >=  1000 and
     c                             ffckto <=  9999
     c                   eval      f1momc = ffcmom
     c                   eval      f1momd = '0'
     c                   endif
      *
     c*
     c**********
     c                   eval      rvdata = *blank
      * Start
     c                   eval      rvdata = %trim('"') +
      * Bilagsnr.
6.30 c                                     %char(ffbiln)     + '" "' +
6.30 c**                                   %char(xxbiln)     + '" "' +
      * Bil.dato
     c                                     %char(nbil08)     + '" "' +
      * Val.dat fra heading
     c                                     %char(nval08)    + '" "' +
      * Bil.type
     c                                     %char(ffbilk)     + '" "' +
      * Bil.tekst
     c                                     %trim(fftext)     + '" "' +
      * Deb.konto
     c                                     %char(ffdkto)     + '" "' +
      * Deb.mva kode
     c                                     %trim(f1momd)     + '" "' +
      *
      * Kredkonto
     c                                     %char(ffckto)     + '" "' +
      * Kredit mva kode
     c                                     %trim(f1momc)     + '" "' +
      * Fakturanr (Bilagsnummer)
     c                                     %char(ffbiln)     + '" "' +
      *
      * Beløp på linje
     c                                     %char(zfbelp)     + '" "' +
      * Avdeling?? Kan prosjekt legges her?
     c                                     %trim('" "') +
      * Forfallsdato
     c                                     %char(nfor08)    + '" "' +
      * Kid nr ???????
6.31 c**                                   %trim(sokidd)    + '" "' +
6.31 c                                     %trim(t1kkid)    + '" "' +
      * Prosjekt
     c                                     %trim(ffproj)    + '"'
      *
     c                   write     rv01pfr
      *
      *
6.23 c     slutt         tag
      *
     c     dd999         tag
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     control_init  begsr
      *
     c                   eval      w_firm = fffirm
     c                   eval      w_binr = ffbiln
     c                   move      d_aa          w_aarr
      * Finner siste faktura-hode for faktura
      *
     c     sohel1_key    setll     sohel1
     c     sohel1_key    reade     sohel1                                 90
     c                   if        *in90 = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   dow       *in90 = *off
     c     sohel1_key    reade     sohel1                                 90
     c                   enddo
      *
     c     avslutt       tag
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
