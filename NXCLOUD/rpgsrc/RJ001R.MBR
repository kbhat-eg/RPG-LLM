     h/title  ASOKON: Jobb-register status, Oppdatering av koder
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.10  * 030511   NHO      Nytt program
      *
      * System. . . . . : ASOKON
      * Program . . . . : RJ001R
      * Beskrivelse . . : Jobb-register status, Legger ut jobb post
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
Q.01  *       270319 bsa  Sjekker om det ligger poster før vi flagger at
Q.01  *                   det finnes poster.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frjoblu    uf a e           k disk    rename(rjobpfr:rjoblur)
Q.01 frtripf    if a e           k disk    rename(rtripfr:rtrapfr)
Q.01 frjoupf    if a e           k disk    rename(rjoupfr:rjoupfr)
      *
      * Definering av parametre
      *
     d w_memb          s             10
     d w_info          s              1
      *
      * Definering av variable
      *
     d w_firm          s                   like(rjbfir)
     d w_date          s                   like(rjbda1)
     d w_time          s                   like(rjbtim)
Q.01 d w_amem          s                   like(rtmemb)
      *
     d                uds
     d  l_wsid               901    910
     d  l_user               911    920
     d  l_firm               944    946  0
      *
Q.01 c/Exec SQL
Q.01 c+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
Q.01 c/End-Exec
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer status for igangsatt jobb
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sletter post dersom parameter med inn = 'S'
      *
     c                   if        w_info = 'S'
     c     rjoblu_key    chain     rjoblur
     c                   if        %found
     c                   delete    rjoblur
     c                   endif
     c                   goto      xslutt
     c                   endif
      *
      *  Henter inn dagens dato og
      *  klokkeslett
      *
     c                   time                    w_date
     c                   time                    w_time
      *
      *  Oppdater Jobb-status-register
      *
     c     rjoblu_key    chain     rjoblur
     c                   if        not %found
      *
     c                   eval      rjbfir = l_firm
     c                   eval      rjbmem = w_memb
     c                   eval      rjbaws = l_wsid
     c                   eval      rjbusr = l_user
     c                   eval      rjbda1 = w_date
     c                   eval      rjbtim = w_time
      *
     c                   eval      w_info = '1'
      *
     c                   write     rjoblur
     c                   else
Q.01  *
Q.01  * Sjekk om det er poster i RTRIPF
Q.01  *
Q.01 c/exec sql
Q.01 c+ select rtmemb
Q.01 c+ into   :w_amem
Q.01 c+ from   rtripf
Q.01 c+ where  rtmemb = :w_memb
Q.01 c+ fetch first 1 rows only
Q.01 c/end-exec
Q.01  *
Q.01 c                   if        w_amem <> *blanks
     c                   eval      w_info = '2'
Q.01 c                   goto      xslutt
Q.01 c                   endif
Q.01  *
Q.01  * Sjekk om det er poster i RJOUPF
Q.01  *
Q.01 c/exec sql
Q.01 c+ select rjmemb
Q.01 c+ into   :w_amem
Q.01 c+ from   rjoupf
Q.01 c+ where  rjmemb = :w_memb
Q.01 c+ fetch first 1 rows only
Q.01 c/end-exec
Q.01  *
Q.01 c                   if        w_amem <> *blanks
Q.01 c                   eval      w_info = '2'
Q.01 c                   goto      xslutt
Q.01 c                   endif
      *
Q.01 c**                 eval      w_info = '2'
      *
     c                   endif
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
     c                   eval      *inlr  = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      *  Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    w_memb
     c                   parm                    w_info
      *
      * Key for file : Jobb-register
      *
     c     rjoblu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_memb
      *
      * Legger inn firma i nøkkler
      *
     c                   eval      w_firm = l_firm
      *
     csr                 endsr
