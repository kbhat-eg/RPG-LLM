     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LI710R
      *  Beskrivelse . . : Oppdatering av ordrebekreftelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  -------------------------------------------------
      *  5.60 TSV  170108  Legger ut lev.ordrenummer på LOHEPF.LOSORN
5.70  * PRGR  BOF  151208  Utvidet med prisgruppe /varetype
      *  6.10  170609 BSA  Oppdatert med sporingsinformasjon
      *  6.10  220909 ASK  Nye parametre til VL001R
6.11  *  6.10  111010 BSA  Sjekker mot GTIN register, hvis nobb nummer er 0
6.11  *                    på innlest register.
6.12  * R6.10 310311 BSA  Miks av formater på linjer pga av GTIN nr
6.14  * R6.10 301111 ASK  Edringer ifbm. testing mot NeB
6.15  *  6.10  230512 bsa  NEB standard krever bruk av GTIN. For å dekke opp
6.15  *                    både NEB og TBF, går vi først på GTIN deretter NOBB
6.15  *                    og vedlagte enheter.
6.16  *  6.10  080612 bsa  Legger ut tillegg/fradrag på hodenivå som nye
6.16  *                    varelinjer.
6.17  *  6.10  020413 bsa  Fjerner ikke bestillingslinjer som ikke bekreftes.
6.18  *  6.10  020413 bsa  Bruker antall fra EDI linja uansett
6.19  *  6.10  020413 bsa  Drar med seg for mye verdier fra tidligere linjer
6.1A  *  6.10  020413 bsa  Flytter sekvens for oppslag ved utlegg av nye linjer
6.1B  *  6.10  030413 bsa  Hvis bestilling ikke finnes på EDI, skal den hoppes over
6.21  * 6.20  190511 bø   Utvidet vl710r med div. info
6.22  * 6.20  170912 bsa  Prosjekt er alfa
6.23  *  6.20  250913 bsa  Endret logikk i test på 13 eller 14 siffers GTIN
6.30  *  6.30  270113 bof  Omskriving i forb. endring pakning VA
6.nu  *  6.30  050614 bof  endring mtp. nummerutvidelse
6.31  *  6.30  080914 bsa  Justerer leveringsdato ut fra "ledetid" på leverandør.
6.32  *  6.20  170415 bsa  Håndterer ordre og linje tekster.
6.33  *  6.30  120515 bkv  Endret LVAR pga økt fra 15 til 20 tegn
6.34  * 6.30  040615 bkv  Bestilling økt fra 6 til 8
6.35  * 6.30  210815 bsa  Utvidet rabattfelt, synkes mot lengden i mottaket
6.36  * 6.30  030117 bof  Tester at dato ikke er loval før add av dageraket
6.37  * 6.30  280617 bsa  Utvidet bruk av datastruktur
6.38  * 6.30  170717 bsa  Utvidet test mot rabatt på linjenivå. Glemmer å
6.38  *                   trekke ut rabatten.
6.39  * 6.30  260717 bsa  Hvis det bekreftes en "negativ" ordretype må vi
6.39  *                   snu fortegnet på bekreftelsen.
6.3a  * 6.30  270917 bsa  Setter ikke kode for lokalt vareregister
6.3b  * 6.30  050718 bsa  Setter ikke mva kode hvis skaffevarer.
6.3c  * 6.30  030818 bsa  Tar høyde for leverandørens ordrenummer
6.3d  * 6.30  170918 bkv  Utvidet med trelast sortiment
6.3e  * 6.30  291118 bof  rettelse vedr. gjennfinning av logistikk-vare
6.3f  * 6.30  191218 bsa  Kan ikke sjekke mot logistikkvare hvis vi
6.3f  *                   ikke opererer med NOBB nummer.
7.01  * K000  300620 bsa  Viktig at leveringstypen følger linja som slettes,
7.01  *                   eller legges til nye linjer. NB Forutsetter at
7.01  *                   samme leveringstype beholdes på alle linjer.
7.02  * K000  180820 bsa  Utvider kodene for gyldige rabatter.
      *
8.01  *K0000  230621 BSA  Utvidet meldingsnummer fra 6-8 siffer
8.02  *K0000  011121 bsa  Ta vare på varenummer før vi sjekker mot
8.02  *K0000              logistikkvare. Bruker dette hvis det ruturnerer
8.02  *K0000              uten.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *  Bruk av linjenummersekvenser (2 første siffer i linjenr LIMLIN)
      *
      *    10 : Hodeinformasjon
      *    20 : Partsinformasjon
      *    30 : Referanser
      *    40 : Transportinformasjon
      *    50 : Linjer
      *    55 : Linjer fra best. (ikke match i EDI-melding)
      *    60 : Tillegg/fradrag hodenivå
      *    65 : Fritkstlinjer på hodenivå
      *    90 : Totaler
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flohelu    uf   e           k disk    rename(lohepfr:lohelur)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     flodtlu    uf a e           k disk    rename(lodtpfr:lodtlur)
6.32 flotxlu    uf a e           k disk    rename(lotxpfr:lotxlur)
     fledilr    if   e           k disk    rename(ledipfr:ledilrr)
6.32 fledil1    if   e           k disk    rename(ledipfr:ledil1r)
6.32 f                                     prefix(xx:2)
     fvvenlr    if   e           k disk    rename(vvenpfr:vvenlrr)
     fvvarl3    if   e           k disk    rename(vvarpfr:vvarl3r)
     fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
     fjvpkl2    if   e           k disk    rename(jvpkpfr:jvpkl2r)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
6.31 flldgl1    if   e           k disk    rename(lldgpfr:lldgl1r)
6.39 fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
6.3d fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
      * Parameter-inn
      *
     d                 ds
8.01 d d_prec                        22
     d  d_firm                        3  0 overlay(d_prec)
     d  d_type                        4    overlay(d_prec:4)
8.01 d  d_mnum                        8  0 overlay(d_prec:8)
8.01 d  d_mlin                        6  0 overlay(d_prec:16)
8.01 d  d_kode                        1    overlay(d_prec:22)
      *
      * Parameter logg
      *
     d                 ds
     d d_urec                        80
     d  d_firu                        3  0 overlay(d_urec)
6.nu d  d_numm                        8  0 overlay(d_urec:4)
6.nu d  d_suff                        2  0 overlay(d_urec:12)
6.nu d  d_kodu                        1    overlay(d_urec:14)
6.nu d  d_date                         d   overlay(d_urec:15) datfmt(*iso)
6.nu d  d_time                         t   overlay(d_urec:25) timfmt(*iso)
6.nu d  d_user                       10    overlay(d_urec:33)
6.nu d  d_wsid                       10    overlay(d_urec:43)
      *
      *
      * Definering av datastrukturer
      *
      * Lageroppdatering
      *
     d                 ds
6.nu d hlrec                        128
     d  hlvare                       15    overlay(hlrec)
     d  hllage                        2  0 overlay(hlrec:16)
     d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
     d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
     d  hlotyp                        2    overlay(hlrec:36)
     d  hlltyp                        2    overlay(hlrec:38)
     d  hlanta                       11  3 overlay(hlrec:40)
     d  hlkopr                        9  2 overlay(hlrec:51)
     d  hlrefr                       20    overlay(hlrec:60)
     d  hlsyst                        1    overlay(hlrec:80)
     d  hlaltk                        2    overlay(hlrec:81)
     d  hlrest                        1    overlay(hlrec:83)
     d  hltype                        1    overlay(hlrec:84)
     d  hlenhe                        3    overlay(hlrec:85)
     d  hlinlr                        1    overlay(hlrec:88)
     d  hllety                        1    overlay(hlrec:89)
6.nu d  hlonum                        8  0 overlay(hlrec:90)
6.nu d  hlolin                        4  0 overlay(hlrec:98)
      *
      *  Definering av datastrukturer for meldinger
      *
      *
      * ORDREBEKREFTELSE Hode
      *
     d                 ds
     d d_obkh                       400
     d  d_oh_obnr                    35    overlay(d_obkh)
     d  d_oh_olda                      d   datfmt(*iso)
     d                                     overlay(d_obkh:36)
     d  d_oh_llda                      d   datfmt(*iso)
     d                                     overlay(d_obkh:46)
     d  d_oh_sean                    13  0 overlay(d_obkh:56)
     d  d_oh_bean                    13  0 overlay(d_obkh:69)
     d  d_oh_fean                    13  0 overlay(d_obkh:82)
     d  d_oh_kref                    30    overlay(d_obkh:95)
     d  d_oh_aksp                     3    overlay(d_obkh:125)
     d  d_oh_valk                     3    overlay(d_obkh:128)
     d  d_oh_ttyp                     3    overlay(d_obkh:131)
     d  d_oh_fmat                     3    overlay(d_obkh:134)
     d  d_oh_lbet                     3    overlay(d_obkh:137)
     d  d_oh_lste                    13  0 overlay(d_obkh:140)
     d  d_oh_txt1                    70    overlay(d_obkh:153)
     d  d_oh_txt2                    70    overlay(d_obkh:223)
     d  d_oh_txt3                    70    overlay(d_obkh:293)
6.37 d  d_oh_mfor                     6    overlay(d_obkh:363)
      *
      * ORDREBEKREFTELSE Tilegg/fradragslinjer
      *
     d                 ds
     d d_obkh2                      400
     d  d_oh_ftkv                     3    overlay(d_obkh2)
     d  d_oh_tsek                     3    overlay(d_obkh2:4)
     d  d_oh_ftty                     3    overlay(d_obkh2:7)
     d  d_oh_fttx                    30    overlay(d_obkh2:10)
     d  d_oh_tbel                    17  2 overlay(d_obkh2:40)
      *
      * Fritekst/hodelinje
      *
     d                 ds
     d d_obkh3                      400
     d  d_oh_ftxq                     3    overlay(d_obkh3)
     d  d_oh_ftxt                    70    overlay(d_obkh3:4)
      *
      * Ordrebekr.referanser/hode
      *
     d                 ds
     d d_obkh4                      400
     d  d_oh_bstn                    35    overlay(d_obkh4)
     d  d_oh_proj                    35    overlay(d_obkh4:36)
     d  d_oh_kunr                    35    overlay(d_obkh4:71)
     d  d_oh_konr                    35    overlay(d_obkh4:106)
6.3c d  d_oh_lonr                    35    overlay(d_obkh4:141)
      *
      * Ordrebekr. partsinfo./hode
      *
     d                 ds
     d d_obkh5                      400
     d  d_oh_part                     3    overlay(d_obkh5)
     d  d_oh_pean                    13  0 overlay(d_obkh5:4)
     d  d_oh_pnav                    30    overlay(d_obkh5:17)
     d  d_oh_pad1                    30    overlay(d_obkh5:47)
     d  d_oh_pad2                    30    overlay(d_obkh5:77)
     d  d_oh_pste                    30    overlay(d_obkh5:107)
     d  d_oh_porg                    20    overlay(d_obkh5:137)
      *
      * Ordrebekr. transport/lev. - hode
      *
     d                 ds
     d d_obkh6                      400
     d  d_oh_trmo                     3    overlay(d_obkh6)
     d  d_oh_trna                    35    overlay(d_obkh6:4)
     d  d_oh_lebe                     3    overlay(d_obkh6:39)
6.32  *
6.32  * Fritekst varelinje
6.32  *
6.32 d                 ds
6.32 d d_obkl15                     400
6.32 d  d_ol_ftxq                     3    overlay(d_obkl15)
6.32 d  d_ol_ftxt                    70    overlay(d_obkl15:4)
6.32 d  d_ol_lref                     6  0 overlay(d_obkl15:74)
      *
      * ORDREBEKREFTELSE Linje
      *
     d                 ds
     d d_obkl                       400
     d  d_ol_llin                     6  0 overlay(d_obkl)
     d  d_ol_laks                     3    overlay(d_obkl:7)
6.12 d  d_ol_vean                    14  0 overlay(d_obkl:10)
6.12 d  d_ol_vnob                     8  0 overlay(d_obkl:24)
6.12 d  d_ol_vlev                    15    overlay(d_obkl:32)
6.12 d  d_ol_bkva                    15  3 overlay(d_obkl:47)
6.12 d  d_ol_benh                     3    overlay(d_obkl:62)
6.12 d  d_ol_lkva                    15  3 overlay(d_obkl:65)
6.12 d  d_ol_lenh                     3    overlay(d_obkl:80)
6.12 d  d_ol_llda                      d   datfmt(*iso)
6.12 d                                     overlay(d_obkl:83)
6.12 d  d_ol_pris                    15  3 overlay(d_obkl:93)
6.12 d  d_ol_penh                     3    overlay(d_obkl:108)
6.12 d  d_ol_prpr                     9  0 overlay(d_obkl:111)
6.12 d  d_ol_prkv                     3    overlay(d_obkl:120)
6.12 d  d_ol_epri                    15  3 overlay(d_obkl:123)
6.12 d  d_ol_evar                     8  0 overlay(d_obkl:138)
6.12 d  d_ol_linr                     6  0 overlay(d_obkl:146)
6.12 d  d_ol_linb                    15  3 overlay(d_obkl:152)
6.12 d  d_ol_mvap                     4  2 overlay(d_obkl:167)
6.12 d  d_ol_tkv1                     3    overlay(d_obkl:171)
6.12 d  d_ol_tty1                     3    overlay(d_obkl:174)
6.12 d  d_ol_ttx1                    35    overlay(d_obkl:177)
6.12 d  d_ol_tse1                     3    overlay(d_obkl:212)
6.12 d  d_ol_tme1                    11  3 overlay(d_obkl:215)
6.12 d  d_ol_ten1                     3    overlay(d_obkl:226)
6.12 d  d_ol_tpr1                     5  2 overlay(d_obkl:229)
6.12 d  d_ol_tbe1                    15  2 overlay(d_obkl:234)
6.12 d  d_ol_tkv2                     3    overlay(d_obkl:250)
6.12 d  d_ol_tty2                     3    overlay(d_obkl:252)
6.12 d  d_ol_ttx2                    35    overlay(d_obkl:255)
6.12 d  d_ol_tse2                     3    overlay(d_obkl:290)
6.12 d  d_ol_tme2                    11  3 overlay(d_obkl:293)
6.12 d  d_ol_ten2                     3    overlay(d_obkl:304)
6.12 d  d_ol_tpr2                     5  2 overlay(d_obkl:307)
6.12 d  d_ol_tbe2                    15  2 overlay(d_obkl:312)
6.12 d  d_ol_txt1                    35    overlay(d_obkl:327)
6.12 d  d_ol_txt2                    35    overlay(d_obkl:362)
6.12  *
6.12  * ORDREBEKREFTELSE Linje
6.12  *
6.12 d                 ds
6.12 d x_obkl                       400
6.12 d  x_ol_llin                     6  0 overlay(x_obkl)
6.12 d  x_ol_laks                     3    overlay(x_obkl:7)
6.12 d  x_ol_vean                    13  0 overlay(x_obkl:10)
6.12 d  x_ol_vnob                     8  0 overlay(x_obkl:23)
6.12 d  x_ol_vlev                    15    overlay(x_obkl:31)
6.12 d  x_ol_bkva                    15  3 overlay(x_obkl:46)
6.12 d  x_ol_benh                     3    overlay(x_obkl:61)
6.12 d  x_ol_lkva                    15  3 overlay(x_obkl:64)
6.12 d  x_ol_lenh                     3    overlay(x_obkl:79)
6.12 d  x_ol_llda                      d   datfmt(*iso)
6.12 d                                     overlay(x_obkl:82)
6.12 d  x_ol_pris                    15  3 overlay(x_obkl:92)
6.12 d  x_ol_penh                     3    overlay(x_obkl:107)
6.12 d  x_ol_prpr                     9  0 overlay(x_obkl:110)
6.12 d  x_ol_prkv                     3    overlay(x_obkl:119)
6.12 d  x_ol_epri                    15  3 overlay(x_obkl:122)
6.12 d  x_ol_evar                     8  0 overlay(x_obkl:137)
6.12 d  x_ol_linr                     6  0 overlay(x_obkl:145)
6.12 d  x_ol_linb                    15  3 overlay(x_obkl:151)
6.12 d  x_ol_mvap                     4  2 overlay(x_obkl:166)
6.12 d  x_ol_tkv1                     3    overlay(x_obkl:170)
6.12 d  x_ol_tty1                     3    overlay(x_obkl:173)
6.12 d  x_ol_ttx1                    35    overlay(x_obkl:176)
6.12 d  x_ol_tse1                     3    overlay(x_obkl:211)
6.12 d  x_ol_tme1                    11  3 overlay(x_obkl:214)
6.12 d  x_ol_ten1                     3    overlay(x_obkl:225)
6.12 d  x_ol_tpr1                     5  2 overlay(x_obkl:228)
6.12 d  x_ol_tbe1                    15  2 overlay(x_obkl:233)
6.12 d  x_ol_tkv2                     3    overlay(x_obkl:249)
6.12 d  x_ol_tty2                     3    overlay(x_obkl:251)
6.12 d  x_ol_ttx2                    35    overlay(x_obkl:254)
6.12 d  x_ol_tse2                     3    overlay(x_obkl:289)
6.12 d  x_ol_tme2                    11  3 overlay(x_obkl:292)
6.12 d  x_ol_ten2                     3    overlay(x_obkl:303)
6.12 d  x_ol_tpr2                     5  2 overlay(x_obkl:306)
6.12 d  x_ol_tbe2                    15  2 overlay(x_obkl:311)
6.12 d  x_ol_txt1                    35    overlay(x_obkl:326)
6.12 d  x_ol_txt2                    35    overlay(x_obkl:361)
      *
      * ORDREBEKREFTELSE Ordretotaler
      *
     d                 ds
     d d_obks                       400
     d  d_os_suml                    17  2 overlay(d_obks)     inz(0)
     d  d_os_ntot                    17  2 overlay(d_obks:18)  inz(0)
     d  d_os_mvag                    17  2 overlay(d_obks:35)  inz(0)
     d  d_os_mvat                    17  2 overlay(d_obks:52)  inz(0)
     d  d_os_btot                    17  2 overlay(d_obks:69)  inz(0)
     d  d_os_tilt                    17  2 overlay(d_obks:86)  inz(0)
     d  d_os_frat                    17  2 overlay(d_obks:103) inz(0)
      * Definering av LDA
      *
     d                uds
     d l_firm                944    946  0
     D  l_wsid               901    910
     D  l_user               911    920
      *
      * Definering av variable i nøkler
      *
     d lohelu_numm     s                   like(lonumm)
     d lohelu_suff     s                   like(losuff)
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d lodtl1_line     s                   like(ldline)
     d lodtlu_numm     s                   like(ldnumm)
     d lodtlu_suff     s                   like(ldsuff)
     d lodtlu_line     s                   like(ldline)
     d ledilr_type     s                   like(litype)
     d ledilr_mnum     s                   like(limnum)
     d ledilr_mlin     s                   like(limlin)
     d vvenlr_vare     s                   like(vevare)
     d vvenlr_enhe     s                   like(veenhe)
     d vvarl3_nobb     s                   like(vvnobb)
     d jvarl3_nobb     s                   like(jvnobb)
     d jvpkl2_vare     s                   like(jwvare)
6.30 d jvpkl2_ldor     s                   like(jwldor)
     d jvpkl2_enhe     s                   like(jwenhe)
6.31 d lldgl1_gldo     s                   like(lpgldo)
6.31 d lldgl1_glag     s                   like(lpglag)
6.32 d lotxlu_numm     s                   like(ltnumm)
6.32 d lotxlu_suff     s                   like(ltsuff)
6.32 d lotxlu_line     s                   like(ltline)
6.32 d ledil1_type     s                   like(litype)
6.32 d ledil1_mnum     s                   like(limnum)
6.32 d ledil1_mlin     s                   like(limlin)
6.39 d votyl1_otyp     s                   like(vaotyp)
      *
      * Definering av parametre
      *
     d p_parm          s             45
     d p_errk          s              2
6.11 d p_eann          s             14  0
6.11 d p_vare          s             15
6.11 d p_enhe          s              3
6.11 d p_nobb          s              8  0
6.11 d p_stat          s              1
      *
      * Definering av øvrige variable
      *
     d w_firm          s                   like(lifirm)
     d w_pris          s                   like(ldipny)
6.35 d*w_rabb          s                   like(ldipny)
6.35 d w_rabb          s             15  3
     d w_line          s                   like(ldline)
     d w_omr1          s                   like(veomre)
     d w_omr2          s                   like(veomre)
     d w_anta          s                   like(ldanta)
     d w_retu          s              1
     d w_enor          s              1
     d w_valg          s              1  0
     d w_olda          s               d   datfmt(*iso) inz(*hival)
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_vare          s             15
     d w_nobb          s                   like(vvnobb)
6.16 d w_enob          s                   like(vvnobb)
     d w_lvar          s             15
     d w_tek1          s             35
     d w_tek2          s             35
     d w_ogrp          s              2  0
     d w_hgrp          s              2  0
     d w_ugrp          s              3  0
     d w_lvre          s              1  0
     d w_stat          s              1
     d w_urec          s             80
5.70 d w_vtyp          s                   like(vvvtyp)
6.34 d w_numa          s              8
5.02 d w_lina          s              4
6.21 d w_utda          s                   like(vvudat)
6.21 d w_ldor          s                   like(vvldor)
6.3d d w_lv_nobb       s                   like(vvlnob)
6.3d d w_lv_modn       s                   like(vvlmod)
6.3d d w_lv_lldo       s                   like(vvlnle)
6.3d d w_lv_llva       s                   like(vvllva)
6.31 d w_days          s              3  0
7.01 d w_lety          s                   like(ldlety)
      *
     d b_slet          s              1    inz(*off)
     d b_best          s              1    inz(*off)
     d b_meld          s              1    inz(*off)
     d b_vare          s              1    inz(*off)
     d b_omrf          s              1    inz(*off)
6.1B d b_edil          s              1    inz(*off)
6.21 d b_inlr          s              1    inz(*off)
6.32 d w_newl          s              4  0
6.32 d w_detl          s              4  0
      *
8.02 d s_vare          s                   like(vvvare)
      *
6.12  *
6.12  * Ordrebekreftelse Linje
6.12  *
6.12 d w_obkl          ds           400
6.23 d* w_test                        1    overlay(w_obkl:46)
6.23 d  w_test                        1    overlay(w_obkl:64)
6.14  *
6.14  * Definering av konstanter
6.14  *
6.14 d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
6.14 d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO
6.3q C/End-Exec
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hoved program styrerutine - oppdatering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   exsr      sjk_meld
      *
     c                   if        p_errk = *blank
     c                   if        b_meld = *on
     c                   exsr      best_hode
      *
     c                   if        b_best = *on
     c                   exsr      log_best
     c                   exsr      best_linje
     c                   exsr      nye_linjer
     c                   exsr      hode_dato
6.16 c                   exsr      til_linjer
      *
     c                   exsr      kalk_best
      *
     c                   if        d_kode = '1'
     c                   exsr      skriv_best
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å oppdatere bestillingshode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     best_hode     begsr
      *
     c                   eval      b_best = *off
      *
     c     lohelu_key    chain     lohelur
     c                   if        %found
      *
     c                   if        d_oh_llda <> *loval
     c                   eval      lomoda = d_oh_llda
     c                   endif
      *
     c                   if        d_oh_kref <> *blank
     c                   if        lodref = *blank
     c                   movel     d_oh_kref     lodref
     c                   endif
     c                   endif
5.60 c                   movel     d_oh_obnr     losorn
6.3c  *
6.3c c                   if        %trim(d_oh_lonr) <> *blanks
6.3c c                   eval      losorn = %trim(d_oh_lonr)
6.3c c                   endif
      *
     c                   eval      b_best = *on
6.10 c                   time                    loedat
6.10 c                   time                    loetim
6.10 c                   eval      loeusr = l_user
     c                   update    lohelur
6.32  * Sjekk om det generell topptekst. Bruker L1 nindeksen for å slippe
6.32  * gjøre store endringer med prefiksing av eksisterende felter.
6.32 c                   eval      w_newl = 1000
6.32 c                   eval      ledil1_type = d_type
6.32 c                   eval      ledil1_mnum = d_mnum
6.32 c                   eval      ledil1_mlin = 350000
6.32 c     ledil1_key    setll     ledil1r
6.32 c     ledil1_key2   reade     ledil1r
6.32  *
6.32 c                   dow       not %eof
6.32  *
6.32 c                   if        xxmlin < 350000 or
6.32 c                             xxmlin > 359999
6.32 c                   leavesr
6.32 c                   endif
6.32  * Må finne siste linjenummer som er brukt, hvis det finnes noe da
6.32 c                   eval      lotxlu_numm = lohelu_numm
6.32 c                   eval      lotxlu_suff = lohelu_suff
6.32 c     new_try       tag
6.32 c                   eval      w_newl = w_newl + 10
6.32 c                   eval      lotxlu_line = w_newl
6.32 c     lotxlu_key    chain     lotxlur
6.32 c                   if        %found
6.32 c                   goto      new_try
6.32 c                   endif
6.32  *
6.32 c                   eval      d_obkh3 = xxmeld
6.32 c                   clear                   lotxlur
6.32 c                   eval      ltfirm = w_firm
6.32 c                   eval      ltnumm = lohelu_numm
6.32 c                   eval      ltsuff = lohelu_suff
6.32 c                   eval      ltline = w_newl
6.32 c                   eval      lttext = d_oh_ftxt
6.32  *
6.32 c                   time                    ltodat
6.32 c                   time                    ltotim
6.32 c                   eval      ltousr = l_user
6.32 c                   time                    ltedat
6.32 c                   time                    ltetim
6.32 c                   eval      lteusr = l_user
6.32 c                   write     lotxlur
6.32  *
6.32 c     ledil1_key2   reade     ledil1r
6.32  *
6.32 c                   enddo
      *
6.39 c                   eval      votyl1_otyp = lootyp
6.39 c     votyl1_key    chain     votyl1
6.39 c                   if        not %found
6.39 c                   eval      vaokre = *blanks
6.39 c                   endif
6.32  *
     c                   endif
7.01  *
7.01  *  Les gjennom linjene, og ta vare på evnt leveringstype
7.01  *  Forutsetter at alle linjer har samme leveringstypen
7.01  *
7.01 c                   eval      w_lety = 0
7.01 c                   eval      lodtl1_numm = lonumm
7.01 c                   eval      lodtl1_suff = losuff
7.01 c                   eval      lodtl1_line = 0
7.01 c     lodtl1_key2   setll     lodtl1
7.01 c     lodtl1_key    reade     lodtl1
7.01 c                   dow       not %eof
7.01 c                   if        ldlety <> 0
7.01 c                   eval      w_lety = ldlety
7.01 c                   endif
7.01 c     lodtl1_key    reade     lodtl1
7.01 c                   enddo
7.01  *
     c     end_hode      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å logge mottatt ordrebekreftelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     log_best      begsr
      *
     c                   eval      d_firu = w_firm
     c                   eval      d_numm = lonumm
     c                   eval      d_suff = losuff
     c                   eval      d_kodu = '4'
     c                   eval      d_date = w_date
     c                   eval      d_time = w_time
     c                   eval      d_user = l_user
     c                   eval      d_wsid = l_wsid
     c                   eval      w_urec = d_urec
      *
5.51 c                   call      'LM900R'
5.51 c                   parm                    w_stat
5.51 c                   parm                    w_urec
      *
     c     end_log       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å oppdatere bestillingslinjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     best_linje    begsr
      *
     c     lodtl1_key    setll     lodtl1
     c     lodtl1_key    reade     lodtl1
      *
     c                   dow       not %eof
     c                   eval      b_slet = *off
     c                   eval      w_line = ldline
      *
     c                   eval      lodtlu_line = ldline
     c     lodtlu_key    chain     lodtlur
      *
     c                   exsr      les_edilin
6.1B  * Leser neste hvis den ikke finnes i EDI melding
6.1B c                   if        b_edil = *off
6.1B c                   unlock    lodtlu
6.1B c                   goto      next_linje
6.1B c                   endif
      *
     c                   if        b_slet = *on
     c                   goto      oppd_linje
     c                   endif
      *
     c                   eval      ldanta = ldanta * -1
     c                   exsr      oppdat_lager
      *
      * Ikke akseptert?
      *
     c                   if        d_ol_laks = '7  '
     c                   eval      b_slet = *on
     c                   goto      oppd_linje
     c                   endif
      *
      * Oppdat. antall også om akseptkode = '5' (Akseptert "uendret")
      *
6.18 c* Bruk antall uansett kode
6.18 c**                 if        d_ol_laks = '1  ' or
6.18 c**                           d_ol_laks = '3  ' or
6.18 c**                           d_ol_laks = '5  ' or
6.18 c**                           d_ol_laks = '   '
     c                   eval      ldanta = d_ol_lkva
6.39  * Snu fortegn hvis vi oppdaterer "negativ" ordretype og kvantum er < 0
6.39 c                   if        vaokre = '-' and
6.39 c                             d_ol_lkva < 0
6.39 c                   eval      ldanta = ldanta * -1
6.39 c                   endif
6.18 c**                 endif
      *
      * Beregn pris
      *
     c                   exsr      beregn_pris
6.31  * Finn om dato skal justeres via ledetid på leverandøren
6.31 c                   eval      w_days = 0
6.31 c                   eval      lldgl1_gldo = loldor
6.31 c                   eval      lldgl1_glag = lolage
6.31 c     lldgl1_key    chain     lldgl1
6.31 c                   if        %found
6.31 c                   eval      w_days = lpgdag
6.31 c                   else
6.31 c                   eval      lldgl1_gldo = loldor
6.31 c                   eval      lldgl1_glag = 0
6.31 c     lldgl1_key    chain     lldgl1
6.31 c                   if        %found
6.31 c                   eval      w_days = lpgdag
6.31 c                   endif
6.31 c                   endif
6.31  *
     c                   if        d_ol_llda <> *loval
     c                   eval      ldldat = d_ol_llda
6.36 c                   if        w_days > 0  and
6.36 c                             ldldat <> *loval
6.31 c                   adddur    w_days:*d     ldldat
6.31 c                   endif
6.31 c**                 if        w_olda <> *loval
     c                   if        d_ol_llda < w_olda
     c                   eval      w_olda = d_ol_llda
6.31 c                   endif
6.31 c**                 else
6.31 c**                 eval      w_olda = d_ol_llda
6.31 c**                 endif
     c                   endif
      *
     c*                  if        d_ol_lenh <> *blank and
     c*                            ldenh1 <> d_ol_lenh
     c*                  exsr      omregn_pris
     c*                  if        b_omrf = *on
     c*                  eval      ldanta = w_anta
     c*                  eval      ldenh1 = d_ol_lenh
     c*                  endif
     c*                  endif
      *
     c                   eval      ldenh1 = d_ol_lenh
     c                   eval      ldipny = w_pris
     c                   eval      ldkpny = w_pris
      *
     c     oppd_linje    tag
      *
     c                   if        b_slet = *on
     c                   delete    lodtlur
     c                   else
6.10 c                   time                    ldedat
6.10 c                   time                    ldetim
6.10 c                   eval      ldeusr = l_user
     c                   update    lodtlur
     c                   exsr      oppdat_lager
6.32  * Legg til tekstlinjer hvis det er kommet inn via EDI melding
6.32  * Sjekk om det varelinjetekst. Bruker L1 nindeksen for å slippe
6.32  * gjøre store endringer med prefiksing av eksisterende felter.
6.32 c                   eval      ledil1_type = d_type
6.32 c                   eval      ledil1_mnum = d_mnum
6.32 c                   eval      ledil1_mlin = 700000
6.32 c     ledil1_key    setll     ledil1r
6.32 c     ledil1_key2   reade     ledil1r
6.32  *
6.32 c                   dow       not %eof
6.32  *
6.32 c                   if        xxmlin < 700000 or
6.32 c                             xxmlin > 749999
6.32 c                   goto      next_linje
6.32 c                   endif
6.32  *
6.32 c                   eval      d_obkl15 = xxmeld
6.32 c                   if        d_ol_lref = limlin
6.32 c                   exsr      gen_tekst
6.32 c                   endif
6.32  *
6.32 c     ledil1_key2   reade     ledil1r
6.32  *
6.32 c                   enddo
6.32  *
     c                   endif
6.1B  *
6.1B c     next_linje    tag
6.1B  *
     c     lodtl1_key    reade     lodtl1
     c                   enddo
      *
     c     end_linje     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å legge inn nye bestillingslinjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     nye_linjer    begsr
      *
     c                   eval      ledilr_mlin = 500000
     c     ledilr_key    setll     ledilr
     c                   read      ledilr
      *
     c                   dow       not %eof and
     c                             limlin < 549999
      *
     c                   eval      lodtl1_line = liline
     c     lodtl1_key2   chain     lodtl1
     c                   if        %found
     c                   goto      les_forbi
     c                   endif
6.19  * Nullstille felter i ny linje
6.19 c                   clear                   lodtlur
      *
      * Legger inn data fra vareregister
      *
6.12 c**                 eval      d_obkl = limeld
6.12 c                   eval      w_obkl = limeld
6.12 c                   exsr      obkl_konv
     c                   exsr      hent_vare
      *
      * Opprett ny bestillingslinje
      *
     c                   eval      w_line = w_line + 10
      *
6.19 c                   eval      ldfirm = lofirm
     c                   eval      ldnumm = lonumm
     c                   eval      ldsuff = losuff
     c                   eval      ldline = w_line
     c                   eval      ldotyp = lootyp
     c                   eval      ldvare = w_vare
     c                   eval      ldenh1 = d_ol_lenh
     c                   eval      ldlage = lolage
     c                   eval      ldldor = loldor
     c                   eval      ldldat = d_ol_llda
7.01 c                   eval      ldlety = w_lety
6.36 c                   if        w_days > 0  and
6.36 c                             ldldat <> *loval
6.31 c                   adddur    w_days:*d     ldldat
6.31 c                   endif
6.33 c*                  eval      ldlvar = w_lvar
6.33 c                   movel     w_lvar        ldlvar
     c                   eval      ldnobb = w_nobb
     c                   eval      ldtek1 = w_tek1
     c                   eval      ldtek2 = w_tek2
     c                   eval      ldanta = d_ol_lkva
6.39  * Snu fortegn hvis vi oppdaterer "negativ" ordretype og kvantum er < 0
6.39 c                   if        vaokre = '-' and
6.39 c                             d_ol_lkva < 0
6.39 c                   eval      ldanta = ldanta * -1
6.39 c                   endif
6.3b  * Vi må sette mva kode på linja. Setter full eller fri kode.
6.3b c                   eval      ldomva = '1'
6.3b c                   if        lomkod = 1
6.3b c                   eval      ldomva = '0'
6.3b c                   goto      end_mva
6.3b c                   endif
6.3b c     end_mva       tag
6.3b  *
     c                   exsr      beregn_pris
     c                   eval      ldipny = w_pris
     c                   eval      ldkpny = w_pris
     c                   eval      ldsumi = w_pris * d_ol_lkva
     c                   eval      ldsumk = w_pris * d_ol_lkva
      *
     c                   eval      ldogrp = w_ogrp
     c                   eval      ldhgrp = w_hgrp
     c                   eval      ldugrp = w_ugrp
6.3a c                   eval      ldlvre = w_lvre
6.10 c                   time                    ldodat
6.10 c                   time                    ldotim
6.10 c                   eval      ldousr = l_user
6.10 c                   time                    ldedat
6.10 c                   time                    ldetim
6.10 c                   eval      ldeusr = l_user
     c                   write     lodtlur
     c                   exsr      oppdat_lager
6.32  * Legg også til tekstlinjer hvis det er kommet inn via EDI melding
6.32  * Sjekk om det varelinjetekst. Bruker L1 nindeksen for å slippe
6.32  * gjøre store endringer med prefiksing av eksisterende felter.
6.32 c                   eval      ledil1_type = d_type
6.32 c                   eval      ledil1_mnum = d_mnum
6.32 c                   eval      ledil1_mlin = 700000
6.32 c     ledil1_key    setll     ledil1r
6.32 c     ledil1_key2   reade     ledil1r
6.32  *
6.32 c                   dow       not %eof
6.32  *
6.32 c                   if        xxmlin < 700000 or
6.32 c                             xxmlin > 749999
6.32 c                   goto      les_forbi
6.32 c                   endif
6.32  *
6.32 c                   eval      d_obkl15 = xxmeld
6.32 c                   if        d_ol_lref = limlin
6.32 c                   exsr      gen_tekst
6.32 c                   endif
6.32  *
6.32 c     ledil1_key2   reade     ledil1r
6.32  *
6.32 c                   enddo
6.32  *
      *
     c     les_forbi     tag
     c                   read      ledilr
     c                   enddo
      *
     c     end_nye       endsr
      *
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *  Subrutine for å legge inn nye varetekstlinjer
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     gen_tekst     begsr
6.32  * Sjekk at vi ikke får dupkey
6.32 c                   eval      w_detl = w_line
6.32 c                   eval      lodtlu_numm = lonumm
6.32 c                   eval      lodtlu_suff = losuff
6.32 c     new_tryl      tag
6.32 c                   eval      w_detl = w_detl + 1
6.32 c                   eval      lodtlu_line = w_detl
6.32 c     lodtlu_key    chain     lodtlur
6.32 c                   if        %found
6.32 c                   goto      new_tryl
6.32 c                   endif
6.32  * Nullstille felter i ny linje
6.32 c                   clear                   lodtlur
6.32  *
6.32 c                   eval      ldfirm = lofirm
6.32 c                   eval      ldnumm = lonumm
6.32 c                   eval      ldsuff = losuff
6.32 c                   eval      ldline = w_detl
6.32 c                   eval      ldotyp = lootyp
6.32 c                   eval      ldvare = *blanks
6.32 c                   eval      ldenh1 = *blanks
6.32 c                   eval      ldlage = lolage
7.01 c                   eval      ldlety = w_lety
6.32 c                   eval      ldldor = 0
6.32 c                   eval      ldldat = *loval
6.32 c                   eval      ldlvar = *blanks
6.32 c                   eval      ldnobb = 0
6.32 c                   eval      ldtek1 = %subst(d_ol_ftxt:1:35)
6.32 c                   eval      ldtek2 = %subst(d_ol_ftxt:36:35)
6.32 c                   eval      ldanta = 0
6.32  *
6.32 c                   eval      ldipny = 0
6.32 c                   eval      ldkpny = 0
6.32 c                   eval      ldsumi = 0
6.32 c                   eval      ldsumk = 0
6.32  *
6.32 c                   eval      ldogrp = 0
6.32 c                   eval      ldhgrp = 0
6.32 c                   eval      ldugrp = 0
6.32 c                   time                    ldodat
6.32 c                   time                    ldotim
6.32 c                   eval      ldousr = l_user
6.32 c                   time                    ldedat
6.32 c                   time                    ldetim
6.32 c                   eval      ldeusr = l_user
6.32 c                   write     lodtlur
6.32  *
6.32 c                   endsr
6.32  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å hente vareinformasjon fra eget vareregister
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_vare     begsr
      *
      * Nullstiller verdier
      *
     c                   eval      w_vare = *blank
     c                   eval      w_lvar = *blank
     c                   eval      w_tek1 = *blank
     c                   eval      w_tek2 = *blank
     c                   eval      w_nobb = 0
     c                   eval      w_ogrp = 0
     c                   eval      w_hgrp = 0
     c                   eval      w_ugrp = 0
     c                   eval      w_lvre = 0
6.11  * Hvis nobbnummer er 0, prøv å hente nobbnummer via GTIN kode
6.11  * hvis denne finnes. Resten av rutine fortsetter som før.
6.11 c                   if        d_ol_vean <> 0
6.15 c**                 if        d_ol_vnob = 0 and
6.15 c**                           d_ol_vean <> 0
6.11 c                   exsr      hent_gtin
6.11 c                   endif
      *
6.3f c                   if        d_ol_vnob <> 0
      *
6.3d  * sjekker om logistikk-vare
6.3d c                   eval      vvvare = *blank
6.3d c/exec sql
6.3d c+ select vvlvnr,
6.3d c+        vvllva,
6.3d c+        vvtek1,
6.3d c+        vvtek2,
6.3d c+        vvogrp,
6.3d c+        vvhgrp,
6.3d c+        vvugrp
6.3d c+ into   :vvvare,
6.3d c+        :vvlvar,
6.3d c+        :vvtek1,
6.3d c+        :vvtek2,
6.3d c+        :vvogrp,
6.3d c+        :vvhgrp,
6.3d c+        :vvugrp
6.3d c+ from   vvlepf
6.3d c+ inner join vvarpf on
6.3d c+               vvfirm = vvlfir and
6.3d c+               vvvare = vvlvnr
6.3d c+ where  vvlnob = :d_ol_vnob
6.3d c+ fetch first 1 rows only
6.3d c/end-exec
6.3d c                   if        vvvare = *blank
      *
     c                   eval      vvarl3_nobb = d_ol_vnob
     c     vvarl3_key    chain     vvarl3r
     c                   if        not %found or
     c                             d_ol_vnob = *zero
     c                   exsr      hent_vadm
     c                   goto      end_vare
     c                   endif
6.3d c                   endif
6.3f c                   else
6.3f c                   exsr      hent_vadm
6.3f c                   goto      end_vare
6.3f c                   endif
      *
     c                   eval      w_vare = vvvare
6.33 c*                  eval      w_lvar = vvlvar
6.33 c                   movel     vvlvar        w_lvar
     c                   eval      w_tek1 = vvtek1
     c                   eval      w_tek2 = vvtek2
     c                   eval      w_nobb = vvnobb
     c                   eval      w_ogrp = vvogrp
     c                   eval      w_hgrp = vvhgrp
     c                   eval      w_ugrp = vvugrp
     c                   eval      w_lvre = 0
      *
     c     end_vare      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å hente vareinformasjon fra vareadministrasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_vadm     begsr
      *
     c                   eval      jvarl3_nobb = d_ol_vnob
     c     jvarl3_key    chain     jvarl3r
     c                   if        %found and
     c                             d_ol_vnob <> *zero
     c                   eval      w_vare = jvvare
6.33 c*                  eval      w_lvar = jvlvar
6.33 c                   movel     jvlvar        w_lvar
     c                   eval      w_tek1 = jvtek1
     c                   eval      w_tek2 = jvtek2
     c                   eval      w_nobb = jvnobb
     c                   eval      w_ogrp = jvogrp
     c                   eval      w_hgrp = jvhgrp
     c                   eval      w_ugrp = jvugrp
     c                   eval      w_lvre = 1
     c                   else
      *
      * Varen finnes ikke i EVR/VA
      *
     c                   movel     d_ol_vnob     w_vare
     c                   eval      w_lvar = d_ol_vlev
     c                   eval      w_tek1 = d_ol_txt1
     c                   eval      w_tek2 = d_ol_txt2
     c                   eval      w_nobb = d_ol_vnob
      *
     c                   endif
      *
     c     end_vadm      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke leveringsdato på hode mot linjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode_dato     begsr
      *
     c                   if        w_olda <> *hival and
     c                             w_olda <> *loval
     c     lohelu_key    chain     lohelur
     c                   if        %found
6.31 c**                 if        lomoda = *loval
     c                   eval      lomoda = w_olda
6.36 c                   if        w_days > 0  and
6.36 c                             lomoda <> *loval
6.31 c                   adddur    w_days:*d     lomoda
6.31 c                   endif
6.31 c**                 endif
6.10 c                   time                    loedat
6.10 c                   time                    loetim
6.10 c                   eval      loeusr = l_user
     c                   update    lohelur
     c                   endif
     c                   endif
      *
     c     end_dato      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne linje på EDI melding
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_edilin    begsr
6.17  * Kun hvis akseptansekode er 7 skal den slettes
6.17 c                   if        d_ol_laks = '7  '
     c                   eval      b_slet = *on
6.17 c                   endif
6.17  *
6.1B c                   eval      b_edil = *off
     c                   eval      ledilr_mlin = 500000
     c     ledilr_key    setll     ledilr
     c                   read      ledilr
      *
     c                   dow       not %eof and
     c                             limlin < 549999
     c                   if        liline = ldline
6.12 c**                 eval      d_obkl = limeld
6.12 c                   eval      w_obkl = limeld
6.12 c                   exsr      obkl_konv
6.17 c**                 eval      b_slet = *off
6.1B c                   eval      b_edil = *on
     c                   leave
     c                   endif
     c                   read      ledilr
     c                   enddo
      *
      *
     c     end_edilin    endsr
      *
6.16  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.16  *  Subrutine for å bygge opp bestillingslinjer fra tillegg/fradrag
6.16  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.16  *
6.16 c     til_linjer    begsr
6.16  *
6.16  * Leser meldingslinjer og bygger nye bestillingslinjer
6.16  *
6.16 c                   eval      ledilr_mlin = 600001
6.16 c     ledilr_key    setll     ledilr
6.16 c                   read      ledilr
6.16  *
6.16 c                   dow       not %eof and
6.16 c                             limlin < 650000
6.16  *
6.16 c                   eval      d_obkh2 = limeld
6.1A  * Flytter sekvens for oppdpatering
6.1A c                   eval      w_line = w_line + 10
6.1A c                   eval      lodtlu_line = w_line
6.1A c     lodtlu_key    chain     lodtlur
6.16  *
6.16  * Legger inn data fra bestillingshode
6.16  *
6.16 c                   eval      ldfirm = lofirm
6.16 c                   eval      ldnumm = lonumm
6.1A c                   eval      ldline = w_line
6.16 c                   eval      ldsuff = losuff
6.16 c                   eval      ldotyp = lootyp
6.16 c                   eval      ldlage = lolage
6.16 c                   eval      ldornu = loornr
6.16 c                   eval      ldorsu = loorsu
6.16 c                   eval      ldorli = 0
6.16  *
6.16  * Legger inn data fra vareregister
6.16  *
6.16 c                   exsr      hent_nobb
6.16 c                   exsr      hent_vare2
6.16  *
6.16 c                   eval      ldvare = w_vare
6.33 c*                  eval      ldlvar = w_lvar
6.33 c                   movel     w_lvar        ldlvar
6.16 c                   eval      ldtek1 = w_tek1
6.16 c                   eval      ldtek2 = w_tek2
6.16 c                   eval      ldnobb = w_nobb
6.16 c                   eval      ldogrp = w_ogrp
6.16 c                   eval      ldhgrp = w_hgrp
6.16 c                   eval      ldugrp = w_ugrp
6.16 c                   eval      ldlvre = w_lvre
6.16  *
6.16  * Legger inn data fra EDI melding
6.16  *
6.16 c                   if        d_oh_fttx <> *blank
6.16 c                   eval      ldtek1 = d_oh_fttx
6.16 c                   eval      ldtek2 = *blank
6.16 c                   endif
6.16 c                   eval      ldenh1 = 'STK'
6.16 c                   eval      ldanta = 1
6.16 c                   eval      ldipny = d_oh_tbel
6.16 c                   eval      ldkpny = d_oh_tbel
6.16 c                   eval(h)   ldsumi = d_oh_tbel
6.16 c                   eval(h)   ldsumk = d_oh_tbel
6.16  *
6.16  * Nullstiller øvrige verdier
6.16  *
7.01 c**                 eval      ldlety = 0
7.01 c                   eval      ldlety = w_lety
6.16 c                   eval      ldavde = 0
6.16 c                   eval      ldkont = 0
6.16 c                   eval      ldspes = 0
6.22 c                   eval      ldproj = *blank
6.16 c                   eval      ldakti = *blank
6.16 c                   eval      ldrab1 = 0
6.16 c                   eval      ldrab2 = 0
6.16 c                   eval      ldsumr = 0
6.16 c                   eval      ldkplj = 0
6.16  *
6.16  * Legger ut ny bestillingslinje
6.16  *
6.16 c                   if        d_oh_ftkv = 'A  '
6.16 c                   eval      ldltyp = '-'
6.16 c                   else
6.16 c                   eval      ldltyp = ' '
6.16 c                   endif
6.16  *
6.1A c**                 eval      w_line = w_line + 10
6.1A c**                 eval      lodtlu_line = w_line
6.1A c**                 eval      ldline = w_line
6.1A c**   lodtlu_key    chain     lodtlur
6.1A c                   if        not %found
6.16 c                   time                    ldodat
6.16 c                   time                    ldotim
6.16 c                   eval      ldousr = l_user
6.16 c                   time                    ldedat
6.16 c                   time                    ldetim
6.16 c                   eval      ldeusr = l_user
6.16 c                   write     lodtlur
6.1A c                   else
6.1A c                   unlock    lodtlu
6.16 c                   endif
6.16  *
6.16 c                   read      ledilr
6.16 c                   enddo
6.16  *
6.16 c     end_til       endsr
6.16  *
6.16  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.16  *  Subrutine for å hente vareinformasjon fra eget vareregister
6.16  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.16  *
6.16 c     hent_vare2    begsr
6.16  *
6.16  * Nullstiller verdier
6.16  *
6.16 c                   eval      w_vare = *blank
6.16 c                   eval      w_lvar = *blank
6.16 c                   eval      w_tek1 = *blank
6.16 c                   eval      w_tek2 = *blank
6.16 c**                 eval      w_nobb = 0
6.16 c                   eval      w_ogrp = 0
6.16 c                   eval      w_hgrp = 0
6.16 c                   eval      w_ugrp = 0
6.16 c                   eval      w_lvre = 0
6.16  *
6.16  * Hvis nobbnummer er 0, prøv å hente nobbnummer via GTIN kode
6.16  * hvis denne finnes. Resten av rutine fortsetter som før.
6.16 c**                 if        w_enob = 0 and
6.16 c**                           d_fl_vean <> 0
6.16 c**                 exsr      hent_gtin
6.16 c**                 endif
6.16  *
6.3f c                   if        w_enob <> 0
6.3e  * sjekker om logistikk-vare
6.3e c                   eval      vvvare = *blank
6.3e c/exec sql
6.3e c+ select vvlvnr,
6.3e c+        vvllva,
6.3e c+        vvtek1,
6.3e c+        vvtek2,
6.3e c+        vvogrp,
6.3e c+        vvhgrp,
6.3e c+        vvugrp
6.3e c+ into   :vvvare,
6.3e c+        :vvlvar,
6.3e c+        :vvtek1,
6.3e c+        :vvtek2,
6.3e c+        :vvogrp,
6.3e c+        :vvhgrp,
6.3e c+        :vvugrp
6.3e c+ from   vvlepf
6.3e c+ inner join vvarpf on
6.3e c+               vvfirm = vvlfir and
6.3e c+               vvvare = vvlvnr
6.3e c+ where  vvlnob = :w_enob
6.3e c+ fetch first 1 rows only
6.3e c/end-exec
6.3e c                   if        vvvare = *blank
6.1e c                   eval      vvarl3_nobb = w_enob
6.16 c     vvarl3_key    chain     vvarl3r
6.16 c                   if        not %found or
6.16 c                             w_enob = *zero
6.16 c                   exsr      hent_vadm2
6.16 c                   goto      end_vare2
6.16 c                   endif
6.3e c                   endif
6.3f c                   else
6.3f c                   exsr      hent_vadm2
6.3f c                   goto      end_vare2
6.3f c                   endif
6.16  *
6.16 c                   eval      w_vare = vvvare
6.33 c*                  eval      w_lvar = vvlvar
6.33 c                   movel     vvlvar        w_lvar
6.16 c                   eval      w_tek1 = vvtek1
6.16 c                   eval      w_tek2 = vvtek2
6.16 c                   eval      w_nobb = vvnobb
6.16 c                   eval      w_ogrp = vvogrp
6.16 c                   eval      w_hgrp = vvhgrp
6.16 c                   eval      w_ugrp = vvugrp
6.16 c                   eval      w_lvre = 0
6.16  *
6.16 c     end_vare2     endsr
6.16  *
6.16  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.16  *  Subrutine for å hente vareinformasjon fra vareadministrasjon
6.16  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.16  *
6.16 c     hent_vadm2    begsr
6.16  *
6.16 c                   eval      jvarl3_nobb = w_enob
6.16 c     jvarl3_key    chain     jvarl3r
6.16 c                   if        %found and
6.16 c                             w_enob <> *zero
6.16 c                   eval      w_vare = jvvare
6.33 c*                  eval      w_lvar = jvlvar
6.33 c                   movel     jvlvar        w_lvar
6.16 c                   eval      w_tek1 = jvtek1
6.16 c                   eval      w_tek2 = jvtek2
6.16 c                   eval      w_nobb = jvnobb
6.16 c                   eval      w_ogrp = jvogrp
6.16 c                   eval      w_hgrp = jvhgrp
6.16 c                   eval      w_ugrp = jvugrp
6.16 c                   eval      w_lvre = 1
6.16 c                   else
6.16  *
6.16  * Varen finnes ikke i EVR/VA
6.16  *
6.16 c                   movel     w_enob        w_vare
6.16 c                   eval      w_lvar = *blank
6.16 c                   eval      w_tek1 = d_oh_fttx
6.16 c                   eval      w_tek2 = *blank
6.16 c                   eval      w_nobb = w_enob
6.16  *
6.16 c                   endif
6.16  *
6.16 c     end_vadm2     endsr
6.16  *
6.16  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.16  *  Subrutine for å finne nobb-nummer på tillegg/fradrag
6.16  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.16  *
6.16 c     hent_nobb     begsr
6.16  *
6.16 c                   select
6.16 c                   when      d_oh_ftty = 'FRA'
6.16 c                   eval      w_enob = 26924696
6.16 c                   when      d_oh_ftty = 'FC '
6.16 c                   eval      w_enob = 26924696
6.16 c                   when      d_oh_ftty = 'IN '
6.16 c                   eval      w_enob = 26924696
6.16 c                   when      d_oh_ftty = 'HD '
6.16 c                   eval      w_enob = 26924761
6.16 c                   when      d_oh_ftty = 'PI '
6.16 c                   eval      w_enob = 26924803
6.16 c                   when      d_oh_ftty = 'PBE'
6.16 c                   eval      w_enob = 26924829
6.16 c                   when      d_oh_ftty = 'PC '
6.16 c                   eval      w_enob = 26924837
6.16 c                   when      d_oh_ftty = 'ZEE'
6.16 c                   eval      w_enob = 26924985
6.16 c                   when      d_oh_ftty = 'RCH'
6.16 c                   eval      w_enob = 26925024
6.16 c                   when      d_oh_ftty = 'IS '
6.16 c                   eval      w_enob = 26925032
6.16 c                   endsl
6.16  *
6.16 c     end_nobb      endsr
6.16  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å rekalkulere bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kalk_best     begsr
      *
     c                   eval      w_enor = ' '
      *
     c                   call      'LO730R'
     c                   parm                    w_enor
     c                   parm                    w_firm
     c                   parm                    lohelu_numm
     c                   parm                    lohelu_suff
      *
     c     end_kalk      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å skrive ut bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_best    begsr
      *
     c                   eval      w_valg = 0
      *
     c                   call      'LO600R'
     c                   parm                    w_retu
     c                   parm                    w_firm
     c                   parm                    lohelu_numm
     c                   parm                    lohelu_suff
     c                   parm                    w_valg
      *
     c     end_skriv     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for bergning av pris med tanke på rabatter mm.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beregn_pris   begsr
      *
      * Rabatt?
      *
6.19 c                   eval      w_pris = 0
     c                   eval      w_rabb = 0
6.38 c                   if        d_ol_tty1 = 'DI ' or
7.02 c                             d_ol_tty1 = 'PAR' or
7.02 c                             d_ol_tty1 = 'PAD' or
7.02 c                             d_ol_tty1 = 'CL ' or
7.02 c                             d_ol_tty1 = 'QD ' or
7.02 c                             d_ol_tty1 = 'VAB' or
7.02 c                             d_ol_tty1 = 'OTE' or
7.02 c                             d_ol_tty1 = 'WHE' or
7.02 c                             d_ol_tty1 = 'PI '
     c                   if        d_ol_prkv = 'AAB'
     c                   if        d_ol_tpr1 <> 0
     c                   eval      w_rabb = (d_ol_pris * d_ol_tpr1) / 100
     c                   else
     c                   eval      w_rabb = d_ol_tbe1
     c                   endif
     c                   endif
     c                   endif
      *
      * Nobb pris?
      *
     c                   if        d_ol_prkv = 'AAB'
     c                   eval      w_pris = d_ol_pris - w_rabb
     c                   else
     c                   eval      w_pris = d_ol_pris
     c                   endif
      *
      * Om nettobeløp er sendt så brukes dette
      *
     c                   if        d_ol_linb <> *zero
     c                   if        d_ol_lkva <> *zero
     c                   eval(h)   w_pris = d_ol_linb / d_ol_lkva
     c                   else
     c                   eval(h)   w_pris = d_ol_linb / 1
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for omregning av pris hvis avvikende prisenhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     omregn_pris   begsr
      *
     c                   eval      b_omrf = *on
      *
     c                   eval      vvenlr_vare = ldvare
     c                   eval      vvenlr_enhe = ldenh1
     c     vvenlr_key    chain     vvenlr
     c                   if        not %found
     c                   eval      w_omr1 = 1
     c                   eval      b_omrf = *off
     c                   else
     c                   eval      w_omr1 = veomre
     c                   endif
      *
     c                   eval      vvenlr_enhe = d_ol_penh
     c     vvenlr_key    chain     vvenlr
     c                   if        not %found
     c                   eval      w_omr2 = 1
     c                   eval      b_omrf = *off
     c                   else
     c                   eval      w_omr2 = veomre
     c                   endif
      *
     c                   eval(h)   w_pris = (w_omr1 / w_omr2) * w_pris
      *
     c                   if        b_omrf = *on
     c                   eval      w_pris = (w_omr1 / w_omr2) * w_pris
     c                   eval      w_anta = d_ol_lkva / w_omr1
6.39  * Snu fortegn hvis vi oppdaterer "negativ" ordretype og kvantum er < 0
6.39 c                   if        vaokre = '-' and
6.39 c                             d_ol_lkva < 0
6.39 c                   eval      w_anta = w_anta * -1
6.39 c                   endif
     c                   else
     c                   eval      w_pris = *zero
     c                   eval      w_anta = *zero
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Kontroll av meldingsdata
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_meld      begsr
      *
     c                   if        b_meld = *off
     c                   eval      p_errk = '99'
     c                   leavesr
     c                   endif
      *
     c                   eval      b_vare = *on
      *
      * Sjekke om vare finnes i varereg.
      *
     c                   eval      ledilr_mlin = 500000
     c     ledilr_key    setll     ledilr
     c                   read      ledilr
      *
     c                   dow       not %eof and
     c                             limnum = d_mnum and
     c                             limlin < 549999
      *
6.12 c**                 eval      d_obkl = limeld
6.12 c                   eval      w_obkl = limeld
6.12 c                   exsr      obkl_konv
6.15 c                   exsr      hent_gtin
6.3f  *
6.3f c                   if        d_ol_vnob <> 0
6.3e  * sjekker om logistikk-vare
6.3e c                   eval      vvvare = *blank
6.3e c/exec sql
6.3e c+ select vvlvnr,
6.3e c+        vvllva,
6.3e c+        vvtek1,
6.3e c+        vvtek2,
6.3e c+        vvogrp,
6.3e c+        vvhgrp,
6.3e c+        vvugrp
6.3e c+ into   :vvvare,
6.3e c+        :vvlvar,
6.3e c+        :vvtek1,
6.3e c+        :vvtek2,
6.3e c+        :vvogrp,
6.3e c+        :vvhgrp,
6.3e c+        :vvugrp
6.3e c+ from   vvlepf
6.3e c+ inner join vvarpf on
6.3e c+               vvfirm = vvlfir and
6.3e c+               vvvare = vvlvnr
6.3e c+ where  vvlnob = :d_ol_vnob
6.3e c+ fetch first 1 rows only
6.3e c/end-exec
8.02  *
8.02 c                   if        vvvare = *blank
8.02 c                   eval      vvvare = s_vare
8.02 c                   endif
8.02  *
6.3e c                   if        vvvare <> *blank
6.3e c                   goto      les_neste
6.3e c                   endif
6.3f c                   endif
      *
     c                   eval      vvarl3_nobb = d_ol_vnob
     c     vvarl3_key    chain     vvarl3r
     c                   if        %found(vvarl3)
     c                   eval      vvenlr_vare = vvvare
6.14 c     lo:up         xlate     d_ol_lenh     d_ol_lenh
     c                   eval      vvenlr_enhe = d_ol_lenh
     c     vvenlr_key    chain     vvenlr
     c                   if        not %found(vvenlr)
     c                   eval      b_vare = *off
     c                   leave
     c                   else
     c                   goto      les_neste
     c                   endif
     c                   endif
      *
     c                   eval      jvarl3_nobb = d_ol_vnob
     c     jvarl3_key    chain     jvarl3r
     c                   if        not %found(jvarl3)
     c                   eval      b_vare = *off
     c                   leave
     c                   else
     c                   eval      jvpkl2_vare = jvvare
6.30 c                   eval      jvpkl2_ldor = jvldor
     c                   eval      jvpkl2_enhe = d_ol_lenh
     c     jvpkl2_key    chain     jvpkl2
     c                   if        not %found(jvpkl2)
     c                   eval      b_vare = *off
     c                   leave
     c                   endif
     c                   endif
      *
     c     les_neste     tag
     c                   read      ledilr
     c                   enddo
      *
     c                   if        b_vare = *off
     c                   eval      p_errk = '1'
     c                   leavesr
     c                   endif
      *
     c     end_sjk       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdat_lager  begsr
      *
      * henter varetype
      *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    ldvare
5.70 c                   parm                    lolage
5.70 c                   parm                    w_vtyp
6.21 c                   parm                    w_utda
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.3d c                   parm                    w_lv_nobb
6.3d c                   parm                    w_lv_modn
6.3d c                   parm                    w_lv_lldo
6.3d c                   parm                    w_lv_llva
      *
     c                   if        laalag = 1 and
5.70 c                             w_vtyp = 'L'
     c                   else
     c                   goto      end_lager
     c                   endif
      *
      * Legger over verdier i overførings-felter
      *
     c                   eval      hlvare = ldvare
     c                   eval      hllage = lolage
     c                   eval      hlenhe = ldenh1
     c                   eval      hldato = *loval
     c                   eval      hltime = *loval
     c                   eval      hlotyp = lootyp
     c                   eval      hlltyp = ldltyp
      *
     c                   eval      hlanta = ldanta
     c                   eval      hlkopr = ldkpny
      *
     c                   eval      hlrefr = *blank
     c                   eval      hlsyst = 'L'
     c                   eval      hlaltk = *blank
     c                   eval      hlrest = *blank
     c                   move      ldnumm        w_numa
     c                   move      ldline        w_lina
     c                   eval      hlrefr = 'B:' + w_numa +
     c                                      ' L:' + w_lina
     c                   eval      hlonum = ldnumm
     c                   eval      hlolin = ldline
     c                   move      ldlety        hllety
      *
      * Kaller lageroppdaterings-program
      *
     c                   eval      w_stat = *blank
      *
     c                   call      'VL001R'
     c                   parm                    w_stat
     c                   parm                    hlrec
      *
     c     end_lager     endsr
      *
6.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.11  *  Subrutine for å hente nobbnummer via GTIN koden.
6.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.11 c     hent_gtin     begsr
6.11  *
6.11 c                   move      d_ol_vean     p_eann
6.11 c                   eval      p_stat = *blank
6.11 c                   eval      p_enhe = *blank
6.11 c                   eval      p_nobb = *zero
6.11 c                   eval      p_vare = *blank
8.02 c                   eval      s_vare = *blank
6.11  *
6.15 c**                 call      'VE715R'
6.15 c                   call      'VE717R'
6.11 c                   parm                    w_firm
6.11 c                   parm                    p_eann
6.11 c                   parm                    p_vare
6.11 c                   parm                    p_enhe
6.11 c                   parm                    p_nobb
6.11 c                   parm                    p_stat
6.11  *
6.11 c                   if        p_stat = '1'
6.11  * Sjekk at det er nobbnummer som kommer i retur
6.11 c                   if        p_nobb <> 0
6.11 c                   eval      d_ol_vnob = p_nobb
6.11 c                   endif
6.15  * Overstyrer også eventuelle enheter på LEDI, hvis det er avvikende
6.15 c                   if        %trim(p_enhe) <> *blanks
6.15 c     lo:up         xlate     p_enhe        p_enhe
6.15 c                   endif
6.15 c                   if        %trim(p_enhe) <> *blanks and
6.15 c                             %trim(p_enhe) <> d_ol_lenh
6.15 c                   eval      d_ol_lenh = p_enhe
6.15 c                   endif
6.11  *
8.02 c                   eval      s_vare = p_vare
6.11  *
6.11 c                   endif
6.11  *
6.11 c     end_gtin      endsr
6.12  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.12  * Subrutine for plassere ordrelinje inn i riktig ds struktur
6.12  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.12  *
6.12 c     obkl_konv     begsr
6.12  *
6.12 c                   if        w_test <> '0'
6.12 c                   eval      d_obkl = limeld
6.12 c                   else
6.12  * GTIN nr er lagt ut med 13 siffers struktur (gammel)
6.12 c                   eval      x_obkl = limeld
6.12 c                   eval      d_ol_llin = x_ol_llin
6.12 c                   eval      d_ol_laks = x_ol_laks
6.12 c                   eval      d_ol_vean = x_ol_vean
6.12 c                   eval      d_ol_vnob = x_ol_vnob
6.12 c                   eval      d_ol_vlev = x_ol_vlev
6.12 c                   eval      d_ol_bkva = x_ol_bkva
6.12 c                   eval      d_ol_benh = x_ol_benh
6.12 c                   eval      d_ol_lkva = x_ol_lkva
6.12 c                   eval      d_ol_lenh = x_ol_lenh
6.12 c                   eval      d_ol_llda = x_ol_llda
6.12 c                   eval      d_ol_pris = x_ol_pris
6.12 c                   eval      d_ol_penh = x_ol_penh
6.12 c                   eval      d_ol_prpr = x_ol_prpr
6.12 c                   eval      d_ol_prkv = x_ol_prkv
6.12 c                   eval      d_ol_epri = x_ol_epri
6.12 c                   eval      d_ol_evar = x_ol_evar
6.12 c                   eval      d_ol_linr = x_ol_linr
6.12 c                   eval      d_ol_linb = x_ol_linb
6.12 c                   eval      d_ol_mvap = x_ol_mvap
6.12 c                   eval      d_ol_tkv1 = x_ol_tkv1
6.12 c                   eval      d_ol_tty1 = x_ol_tty1
6.12 c                   eval      d_ol_ttx1 = x_ol_ttx1
6.12 c                   eval      d_ol_tse1 = x_ol_tse1
6.12 c                   eval      d_ol_tme1 = x_ol_tme1
6.12 c                   eval      d_ol_ten1 = x_ol_ten1
6.12 c                   eval      d_ol_tpr1 = x_ol_tpr1
6.12 c                   eval      d_ol_tbe1 = x_ol_tbe1
6.12 c                   eval      d_ol_tkv2 = x_ol_tkv2
6.12 c                   eval      d_ol_tty2 = x_ol_tty2
6.12 c                   eval      d_ol_ttx2 = x_ol_ttx2
6.12 c                   eval      d_ol_tse2 = x_ol_tse2
6.12 c                   eval      d_ol_tme2 = x_ol_tme2
6.12 c                   eval      d_ol_ten2 = x_ol_ten2
6.12 c                   eval      d_ol_tpr2 = x_ol_tpr2
6.12 c                   eval      d_ol_tbe2 = x_ol_tbe2
6.12 c                   eval      d_ol_txt1 = x_ol_txt1
6.12 c                   eval      d_ol_txt2 = x_ol_txt2
6.12 c                   endif
6.12  *
6.12 c                   endsr
6.12  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    p_parm
     c                   parm                    p_errk
      *
     c                   eval      d_prec = p_parm
      *
      *  Key for oppdatering av BESTILLING-HODE
      *
     c     lohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelu_numm
     c                   kfld                    lohelu_suff
      *
      *  Key for les av BESTILLING-LINJE (numm/suff)
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
      *
      *  Key for oppdatering BESTILLING-LINJE
      *
     c     lodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlu_numm
     c                   kfld                    lodtlu_suff
     c                   kfld                    lodtlu_line
      *
      *  Key for les av BESTILLING-LINJE (numm/suff/line)
      *
     c     lodtl1_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
     c                   kfld                    lodtl1_line
      *
      * Key for les av EDI-mottaksfil
      *
     c     ledilr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ledilr_type
     c                   kfld                    ledilr_mnum
     c                   kfld                    ledilr_mlin
      *
      * Key for les av vare-enhet
      *
     c     vvenlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenlr_vare
     c                   kfld                    vvenlr_enhe
      *
      * Key for posisjonering på nobb-nummer
      *
     c     vvarl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl3_nobb
      *
      * Key for les av vare på nobb-nummer (Vareadm.)
      *
     c     jvarl3_key    klist
     c                   kfld                    jvarl3_nobb
      *
      * Key for les av vare/enhet (Vareadm.)
      *
     c     jvpkl2_key    klist
     c                   kfld                    jvpkl2_vare
6.30 c                   kfld                    jvpkl2_ldor
     c                   kfld                    jvpkl2_enhe
      *
      * Key for oppslag på status
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
6.31  *
6.31  *  Key for oppdatering av ledetid
6.31  *
6.31 c     lldgl1_key    klist
6.31 c                   kfld                    w_firm
6.31 c                   kfld                    lldgl1_gldo
6.31 c                   kfld                    lldgl1_glag
6.32  *
6.32  *  Key for oppdatering av BESTILLING-TEKST
6.32  *
6.32 c     lotxlu_key    klist
6.32 c                   kfld                    w_firm
6.32 c                   kfld                    lotxlu_numm
6.32 c                   kfld                    lotxlu_suff
6.32 c                   kfld                    lotxlu_line
6.32  *
6.32  * Key for les av EDI-mottaksfil
6.32  *
6.32 c     ledil1_key    klist
6.32 c                   kfld                    w_firm
6.32 c                   kfld                    ledil1_type
6.32 c                   kfld                    ledil1_mnum
6.32 c                   kfld                    ledil1_mlin
6.32  *
6.32  * Key for les av EDI-mottaksfil
6.32  *
6.32 c     ledil1_key2   klist
6.32 c                   kfld                    w_firm
6.32 c                   kfld                    ledil1_type
6.32 c                   kfld                    ledil1_mnum
6.39  *
6.39  * Key for oppslag på ordretype
6.39  *
6.39 c     votyl1_key    klist
6.39 c                   kfld                    w_firm
6.39 c                   kfld                    votyl1_otyp
      *
      *  Legger inn firmanummer
      *
     c                   eval      w_firm = l_firm
      *
      *  Legger inn dagens dato
      *
     c                   time                    w_date
     c                   time                    w_time
      *
      * Hent opplysninger fra koderegister
      *
     c     lstsl1_key    chain     lstsl1r
      *
      * Legg inn nøkkelbegreper fra parametre
      *
     c                   eval      ledilr_type = d_type
     c                   eval      ledilr_mnum = d_mnum
     c                   eval      ledilr_mlin = d_mlin
      *
      * Les meldingshode
      *
     c                   eval      ledilr_mlin = 100001
     c     ledilr_key    chain     ledilr
     c                   if        %found
     c                   eval      lohelu_numm = linumm
     c                   eval      lohelu_suff = lisuff
     c                   eval      lodtl1_numm = linumm
     c                   eval      lodtl1_suff = lisuff
     c                   eval      lodtlu_numm = linumm
     c                   eval      lodtlu_suff = lisuff
     c                   eval      d_obkh = limeld
     c                   eval      b_meld = *on
     c                   else
     c                   eval      b_meld = *off
     c                   endif
      *
     c                   eval      p_errk = *blank
      *
     c                   endsr
