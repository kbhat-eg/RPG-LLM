      /TITLE  Felles, utskrift kontaktperson - kundekort
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RF632R                                              *
      * Beskrivelse . . : Felles, utskrift kontaktperson - kundekort          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.      *
      * --------- ------  ----------------------------------------------      *
      * R5.50     290806  Nytt program.                                  KOB  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                       *
      *  BRUK AV INDIKATORER                                                  *
      *                                                                       *
      *       KA = F1  = Valg av firma                                        *
      *       KC = F3  = Exit                                                 *
      *                                                                       *
      *       LR = Avslutt program                                            *
      *       30 = Protect av felter ved vise                                 *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer                   *
      *    60-69 = Fileindikatorer chain etc.                                 *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                           *
      *    80-89 = Arbeidsindikatorer ordinære                                *
      *    90-99 = Benyttes ved std. sub-rutiner                              *
      *                                                                       *
6.30  *    260918 nho Feil nøkkel inn i kundereg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frukpl2    if   e           k disk    rename(rukppfr:rukpl2r)
     frf632p    o    e             printer oflind(*in30)
      *
      * Datastruktur for local
     d                uds
      *
     d l_wsid                901    910                                         Workstation
     d l_user                911    920                                         Bruker
     d l_firm                944    946  0                                      Firma
     d l_fnav                951    980                                         Navn
      *
      * Definering av variabler i nøkler
      *
     d rkunl1_kund     s                   like(rkkund)
     d rukpl2_kpnr     s                   like(rukpnr)
      *
      * Parameter
      *
     d p_kpnr          s                   like(rukpnr)
      *
      * Definering av variable
      *
     d w_firm          s                   like(rkfirm)
     d w_kund          s                   like(rkkund)
      *
      * Finnes kontaktperson
      *
     c                   eval      rukpl2_kpnr = p_kpnr
     c     rukpl2_key    chain     rukpl2
      *
     c                   if        %found(rukpl2)
      *
      * Les kunderegister
      *
     c                   eval      rkunl1_kund = rukund
     c     rkunl1_key    chain     rkunl1
      *
     c                   if        %found(rkunl1)
      *
     c                   write     kund1
      *
     c                   write     kpers1
      *
     c                   endif
      *
     c                   endif
      *
     c                   eval      *inlr = *on
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_kpnr
      *
      * Key for kunderegister
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
6.30 c                   kfld                    rkunl1_kund
      *
      * Key for firmaregisteret
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for kontaktpersonregister
      *
     c     rukpl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rukpl2_kpnr
      *
      * Sette startverdier
      *
     c                   move      l_firm        w_firm
      *
      * Skriv firmaopplysninger
      *
     c     afirl1_key    chain     afirl1
      *
     c                   write     firm1
      *
     c                   endsr
