      /TITLE  ASOFAK Tilbudsstatistikk, skjermbilde med valg
     H datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FP810R
      * Beskrivelse . . : Tilbudsstatistikk, Skjermbilde med valg
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * R3.31 020101 AMD  Utvidet med en ordretype til + lagt inn mulighet
      *                   for seleksjon også på historisk fakt.reg.
      *                   Endringer ikke anmerket på linjenivå
5.60  *  5.60 030708 AMD  Programmet gikk i time-stamp-error.  Parameter-
      *                   lister må samordnes da det benyttes fellesprogr.
      *                   for ordrestatistikk og tilbudsstatistikk
7.xx  * 7.00  290316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  040321 bsa  Problemer med datofelter
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     fra07pf    if   e           k disk    rename(ra07pfr:ra07pfr)
     fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvinfl1    if   e           k disk    rename(vinfpfr:vinfl1r)
     ffp810d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
      *
     d w_fdat          s               d   datfmt(*iso)
     d w_tdat          s               d   datfmt(*iso)
     d w_udat          s               d   datfmt(*iso)
     d w_firm          s              3  0
     d w_stxt          s             30
     d w_atxt          s             30
     d w_itxt          s             30
     d w_time          s               z
     d w_timestamp     s             26  0
     d w_timem         s             13  0
     d d_memb          s              9
      *
      * Datastruktur for parameterliste -ut
      *
     d                 ds
5.60 d d_list                       128
     d  d_firm                        3  0 overlay(d_list)
     d  d_hist                        1    overlay(d_list:4)
     d  d_oiar                        1    overlay(d_list:5)
     d  d_bong                        1    overlay(d_list:6)
3.11 d  d_selg                        4  0 overlay(d_list:7)
3.11 d  d_avde                        4  0 overlay(d_list:11)
3.11 d  d_hot1                        2    overlay(d_list:15)
3.11 d  d_hot2                        2    overlay(d_list:17)
3.11 d  d_hot3                        2    overlay(d_list:19)
3.11 d  d_hot4                        2    overlay(d_list:21)
3.11 d  d_hot5                        2    overlay(d_list:23)
3.11 d  d_hot6                        2    overlay(d_list:25)
3.11 d  d_hvlg                        1    overlay(d_list:27)
3.11 d  d_oot1                        2    overlay(d_list:28)
3.11 d  d_oot2                        2    overlay(d_list:30)
3.11 d  d_oot3                        2    overlay(d_list:32)
3.11 d  d_oot4                        2    overlay(d_list:34)
3.11 d  d_oot5                        2    overlay(d_list:36)
3.11 d  d_oot6                        2    overlay(d_list:38)
3.11 d  d_ovlg                        1    overlay(d_list:40)
3.11 d  d_bot1                        2    overlay(d_list:41)
3.11 d  d_bot2                        2    overlay(d_list:43)
3.11 d  d_bot3                        2    overlay(d_list:45)
3.11 d  d_bot4                        2    overlay(d_list:47)
3.11 d  d_bot5                        2    overlay(d_list:49)
3.11 d  d_bot6                        2    overlay(d_list:51)
3.11 d  d_bvlg                        1    overlay(d_list:53)
5.60 d  d_inf1                        2    overlay(d_list:54)
5.60 d  d_inf2                        2    overlay(d_list:56)
5.60 d  d_inf3                        2    overlay(d_list:58)
5.60 d  d_inf4                        2    overlay(d_list:60)
5.60 d  d_inf5                        2    overlay(d_list:62)
5.60 d  d_inf6                        2    overlay(d_list:64)
5.60 d  d_ivlg                        1    overlay(d_list:66)
5.60 d  d_fdat                         d   overlay(d_list:67) datfmt(*iso)
5.60 d  d_tdat                         d   overlay(d_list:77) datfmt(*iso)
5.60 d  d_dvlg                        1    overlay(d_list:87)
5.60 d  d_dumy                       15    overlay(d_list:88)
5.60 d  d_sort                        1    overlay(d_list:103)
5.60 d  d_ityp                        2    overlay(d_list:104)
      *
      * Definering av nøkkel-felt
      *
     d ra07pf_avde     s                   like(b1avde)
     d ra09l1_selg     s                   like(b1selg)
     d votyl1_otyp     s                   like(vaotyp)
     d vinfl1_ityp     s                   like(vaityp)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer bildet
      *
      *
      * Behandler bildet
      *
     c     b1taga        tag
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   eval      d_selg = b1selg
     c                   eval      d_avde = b1avde
     c                   eval      d_ityp = b1ityp
     c                   eval      d_oot1 = b1oot1
     c                   eval      d_oot2 = b1oot2
     c                   eval      d_oot3 = b1oot3
     c                   eval      d_oot4 = b1oot4
     c                   eval      d_oot5 = b1oot5
     c                   eval      d_oot6 = b1oot6
     c                   eval      d_ovlg = b1ovlg
     c                   eval      d_sort = b1sort
      *
     c                   exsr      sjekk_inp
      *
     c                   if        b_feil = *on
     c                   eval      b_feil = *off
     c                   goto      b1taga
     c                   endif
      *
      * Kaller utskriftprogrammet
      *
      * Lag membernavn for wohepf
     c                   time                    w_time
     c                   move      w_time        w_timestamp
     c                   move      w_timestamp   w_timem
     c
     c                   movel     w_timem       d_memb
     c                   movel     'W'           d_memb
     c
     c                   call      'FP810C'
     c                   parm                    d_list
     c                   parm                    d_memb
      *
     c                   goto      b1taga
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av spørringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Ordretype
      *
     c                   if        w_afld = 'B1OOT1'
     c                   call      'VA550R'
     c                   parm                    w_firm
     c                   parm                    b1oot1
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'B1OOT2'
     c                   call      'VA550R'
     c                   parm                    w_firm
     c                   parm                    b1oot2
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'B1OOT3'
     c                   call      'VA550R'
     c                   parm                    w_firm
     c                   parm                    b1oot3
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'B1OOT4'
     c                   call      'VA550R'
     c                   parm                    w_firm
     c                   parm                    b1oot4
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'B1OOT5'
     c                   call      'VA550R'
     c                   parm                    w_firm
     c                   parm                    b1oot5
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'B1OOT6'
     c                   call      'VA550R'
     c                   parm                    w_firm
     c                   parm                    b1oot6
     c                   goto      end_spØrr
     c                   endif
      *
      * Selger
      *
     c                   if        w_afld = 'B1SELG'
     c                   call      'RA509R'
     c                   parm                    w_firm
     c                   parm                    b1selg
     c                   parm                    w_stxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Avdeling
      *
     c                   if        w_afld = 'B1AVDE'
     c                   call      'RA507R'
     c                   parm                    w_firm
     c                   parm                    b1avde
     c                   parm                    w_atxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Informasjonstype/Årsak
      *
     c                   if        w_afld = 'B1ITYP'
     c                   call      'VA540R'
     c                   parm                    w_firm
     c                   parm                    b1ityp
     c                   parm                    w_itxt
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_inp     begsr
      *
     c                   eval      b_feil = *off
3.10 c                   eval      w_fdat = *loval
3.10 c                   eval      w_tdat = *loval
      *
      *
      * Kontroll av inntastet dato
      *
     c                   if        b1fdat <> 0
     c     *dmy          test(d)                 b1fdat                 46
     c                   if        *in46  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
     c                   if        b1tdat <> 0
     c     *dmy          test(d)                 b1tdat                 48
     c                   if        *in48  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
     c                   if        b1tdat <> 0 and
     c                             b1fdat = 0
     c     *dmy          move      w_udat        b1fdat
     c                   endif
      *
     c                   if        b1fdat <> 0
     c     *dmy          move      b1fdat        w_fdat
     c                   if        b1tdat <> 0
     c     *dmy          move      b1tdat        w_tdat
     c                   else
     c                   eval      w_tdat = *loval
     c                   endif
     c                   if        w_fdat > w_tdat
     c                   eval      *in47  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
     c                   eval      d_fdat = w_fdat
     c                   eval      d_tdat = w_tdat
      *
      * Kontroll selger-nummer
      *
     c                   eval      ra09l1_selg = b1selg
     c                   if        b1selg <> 0
     c     ra09l1_key    chain     ra09l1                             61
     c                   if        *in61 <> *off
     c                   eval      *in40  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
      * Kontroll avdelings-nummer
      *
     c                   eval      ra07pf_avde = b1avde
     c                   if        b1avde <> 0
     c     ra07pf_key    chain     ra07pf                             61
     c                   if        *in61 <> *off
     c                   eval      *in37  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
      * Kontroll Informasjonstype/Årsak
      *
     c                   eval      vinfl1_ityp = b1ityp
     c                   if        b1ityp <> *blank
     c     vinfl1_key    chain     vinfl1                             61
     c                   if        *in61 <> *off
     c                   eval      *in39  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
      * Kontroll ordre-typene
      *
     c                   eval      votyl1_otyp = b1oot1
     c                   if        b1oot1 <> '  '
     c     votyl1_key    chain     votyl1                             61
     c                   if         *in61 <> *off
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
     c                   eval      votyl1_otyp = b1oot2
     c                   if        b1oot2 <> '  '
     c     votyl1_key    chain     votyl1                             61
     c                   if         *in61 <> *off
     c                   eval      *in32  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
     c                   eval      votyl1_otyp = b1oot3
     c                   if        b1oot3 <> '  '
     c     votyl1_key    chain     votyl1                             61
     c                   if         *in61 <> *off
     c                   eval      *in33  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
     c                   eval      votyl1_otyp = b1oot4
     c                   if        b1oot4 <> '  '
     c     votyl1_key    chain     votyl1                             61
     c                   if         *in61 <> *off
     c                   eval      *in34  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
     c                   eval      votyl1_otyp = b1oot5
     c                   if        b1oot5 <> '  '
     c     votyl1_key    chain     votyl1                             61
     c                   if         *in61 <> *off
     c                   eval      *in35  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
     c                   eval      votyl1_otyp = b1oot6
     c                   if        b1oot6 <> '  '
     c     votyl1_key    chain     votyl1                             61
     c                   if         *in61 <> *off
     c                   eval      *in36  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Henter firmakode fra LDA
      *
     c                   eval      w_firm = l_firm
     c                   eval      d_firm = l_firm
      *
      * Key for oppslag på avdelingsregister
      *
     c     ra07pf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra07pf_avde
      *
      * Key for oppslag på selgerregister
      *
     c     ra09l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra09l1_selg
      *
      * Key for oppslag på ordretypr-register
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      *
      * Key for oppslag på ordreinfo-register
      *
     c     vinfl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vinfl1_ityp
      *
      * Initierer bildet
      *
     c                   eval      b1selg = *zeros
     c                   eval      b1sort = '1'
     c                   eval      d_oiar = '1'
      *
      * Henter dagens dato
      *
     c                   time                    w_udat
      *
     c                   endsr
