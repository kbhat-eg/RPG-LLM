      /TITLE  GL120R Faste data for elektronisk betalingsformidling
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * System  . . . . : ASOKON                                        *
      * Program . . . . : GL120R                                        *
      * Beskrivelse . . : Faste data for elektronisk betalingsformidling*
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      *           281094  Nytt program oktober-1994                RH   *
      *                                                                 *
090605*           090605  Tar med "kommunikasjonstype" i pmt       RH   *
090605*                   ved call av GL321C.                      RH   *
6.20  *           170611  IFS legges inn som default               NHO  *
      *                   '3' i lønn
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KC = F3  = Exit                                           *
      *       KL = F12 = Annuller                                       *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *                                                                 *
      * A1PAR  - Parameterside på rapport
      * A1HDR  - Heading A1 på rapport
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fgl120d    cf   e             workstn
     fgustl1    uf a e           k disk
     fdirrem    if   e             disk
     fgfaspf    if   e           k disk    rename(gfaspfr:gfaspfp)
     fgfaslu    uf a e           k disk    rename(gfaspfr:gfaspfu)
      *
     d oin             s              1    dim(11)
     d out             s              1    dim(11)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Kommentarer til program.                                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Programmet vedlikeholder file GFASPF, faste data for
      * elektronisk betalingsformidling.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * A1BLD  - A1 Hovedbilde for vedlikehold.
      *
      *
      * DS til bruk for å snu dato i fra åååå.mm.dd til dd.mm.åååå.
      *
     d                 ds
     d dsdat1                         8
     d  dsuÅr1                        4  0 overlay(dsdat1)
     d  dsumn1                        2  0 overlay(dsdat1:5)
     d  dsuda1                        2  0 overlay(dsdat1:7)
     d                 ds
     d dsdat2                         8
     d  dsuda2                        2  0 overlay(dsdat2)
     d  dsumn2                        2  0 overlay(dsdat2:3)
     d  dsuÅr2                        4  0 overlay(dsdat2:5)
      *
      * Datastruktur for å ta ut sist brukte sekvensteller fra DIRREM:
      *
     d                 ds
     d dsqrem                        80
     d  dsdase                        6  0 overlay(dsqrem:14)
     d  dsefse                        4  0 overlay(dsqrem:71)
      *
     d                uds
     d l_fgrp                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d i               s              2  0
     d j               s              2  0
     d w_arow          s              2  0
     d w_acol          s              3  0
     d w_in12          s              1  0
     d w_lest          s              3
09065d l_komm          s              4
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Test på parameterinformasjon.                                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Send Alt
      *
     c     a1taga        tag
6.20 c                   eval      a17kom = 'IFS'
6.20 c                   eval      a17lon = '3'
     c                   exfmt     a1bld
      *
      * Avslutt program ved F3 eller F12.
      *
     c     *inkc         cabeq     *on           xslutt
      *
      * Test på verdier i skjermbildet.
      *
     c                   exsr      xa1tst
      *
      * Sender bildet opp på nytt dersom feil i registrerte verdier.
      *
     c     *in30         cabeq     *on           a1taga
     c     *in31         cabeq     *on           a1taga
     c     *in32         cabeq     *on           a1taga
     c     *in33         cabeq     *on           a1taga
     c     *in34         cabeq     *on           a1taga
     c     *in35         cabeq     *on           a1taga
     c     *in36         cabeq     *on           a1taga
09065c     *in37         cabeq     *on           a1taga
      *
      * Redigerer operatørnummer dersom bankkode lik "DN" (DnB):
      *
     c                   if        a17ban = 'DN'
     c                   movea     a17ope        oin
     c                   eval      i = 1
     c                   dow       i <= 11
     c                   if        oin(i) = *blanks
     c                   goto      red
     c                   endif
     c                   eval      i = i + 1
     c                   enddo
      *                                                          .
     c     red           tag
     c                   eval      i = i - 1
     c                   eval      j = 11
     c                   dow       i > 0
     c                   move      oin(i)        out(j)
     c                   eval      i = i - 1
     c                   eval      j = j - 1
     c                   enddo
     c                   dow       j > 0
     c                   move      '0'           out(j)
     c                   eval      j = j - 1
     c                   enddo
     c                   movea     out           a17ope
     c                   endif
      *
      * Oppdaterer diskfile med verdier i fra skjermbilde A1.
      *
     c                   exsr      xovfa1
      *
      * Kaller opp deklarasjonskoder:
      *
     c                   if        *inkh = *on
     c                   call      'GL122R'
     c                   goto      a1taga
     c                   endif
      *
      * Kaller opp avtaleregister:
      *
     c                   if        *inki = *on
     c                   call      'GL114R'
     c                   goto      a1taga
     c                   endif
      *
      * Kaller opp sekvensteller.
      *
     c                   if        *inkj = *on
     c                   call      'GL121R'
     c                   parm                    a17fin
     c                   parm                    w_in12
     c     w_in12        cabeq     1             xslutt
     c                   goto      a1taga
     c                   endif
      *
      * Kaller opp navnsetting av PC-Filer:
      *
17118c                   if        *inkp = *on
17118c                   call      'GL321C'
17118c                   parm                    l_fgrp
17118c                   parm                    l_firm
09065c                   parm                    l_komm
17118c                   goto      a1taga
17118c                   endif
      *
      * Henter sist brukte sekvensteller:
      *
     c                   if        *inkk = *on
      *                                                        .
      * Det er kun den første posten av DIRREM (sekvensiell)   .
      * filen som er aktuell å lese, d.v.s. det skal kun ut-   .
      * føres en les av filen.  Dette styres v.h.a. hjelpe-    .
      * feltet w_LEST:                                         .
      *                                                        .
     c                   if        w_lest = 'NEI'
     c                   read      dirrem                                 60
     c                   move      gdqrem        dsqrem
     c                   movel     'JA '         w_lest
     c                   endif
      *                                                        .
     c                   eval      e1dase = dsdase - 1
     c                   eval      e1efse = dsefse - 1
      *                                                        .
     c                   exfmt     e1win
     c                   goto      a1taga
      *                                                        .
     c                   endif
      *
      /space 3
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for test av verdier i skjermbildet A1.                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xa1tst        begsr
      *
      * Foretaksnummer er obligatorisk.
      *
     c                   if        a17fin = 0
     c                   move      *on           *in35
     c                   goto      xuttst
     c                   endif
      *
      * Eget/Felles sekvens nr
      *
     c                   if        a17efs = *blanks
     c                   move      *on           *in30
     c                   goto      xuttst
     c                   endif
      *
      * Bankkode
      *
     c                   if        a17ban = *blanks
     c                   move      *on           *in31
     c                   goto      xuttst
     c                   endif
      *
      * Type kommunikasjon
      *
     c                   if        a17kom = *blanks
     c                   move      *on           *in32
     c                   goto      xuttst
     c                   endif
      *
      * Type kommunikasjon
      *
09065c                   if        a17kom = 'IFS'
09065c                             and a17ifs = *blanks
09065c                   move      *on           *in37
09065c                   goto      xuttst
09065c                   endif
      *
      * Sortering av betalingsforslag
      *
     c                   if        a17sbf = *blanks
     c                   move      *on           *in33
     c                   goto      xuttst
     c                   endif
      *
      * Sammenslåing til samlefil
     c                   if        a17sam = *blanks
     c                   move      *on           *in34
     c                   goto      xuttst
     c                   endif
      *
      * Type lønnsrutiner som skal benyttes:
      *
     c                   if        a17lon <> '3'
     c                   move      *on           *in36
     c                   goto      xuttst
     c                   endif
      *
     c     xuttst        tag
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for overføring av data fra diskfile til skjermbilde.  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xovfdi        begsr
      *
     c                   if        *in60 = *off
     c                   eval      a17fin = geffin
     c                   movel     gefope        a17ope
     c                   movel     gefpwd        a17pwd
     c                   movel     gefdiv        a17div
     c                   eval      a17ubf = gefubf
     c                   movel     gefefs        a17efs
     c                   eval      a17fos = geffos
     c                   eval      a17fo2 = geffo2
      *
      * Snur dato:                                             .
      *
     c                   movel     gefdat        dsdat1
     c                   eval      dsuÅr2 = dsuÅr1
     c                   eval      dsumn2 = dsumn1
     c                   eval      dsuda2 = dsuda1
     c                   movel     dsdat2        a17dat
      *                                                        .
     c                   eval      a17tim = geftim
     c                   eval      a17das = gefdas
      *
      * Snur dato:                                             .
      *
     c                   movel     gefdda        dsdat1
     c                   eval      dsuÅr2 = dsuÅr1
     c                   eval      dsumn2 = dsumn1
     c                   eval      dsuda2 = dsuda1
     c                   movel     dsdat2        a17dda
      *                                                        .
     c                   movel     gefban        a17ban
     c                   movel     gefkom        a17kom
     c                   movel     gefsbf        a17sbf
     c                   movel     gefsam        a17sam
     c                   movel     geflon        a17lon
09065c                   movel     gefifs        a17ifs
      *
     c                   endif
      *
09065c                   if        gefifs = *blanks
09065c                   movel     '/Bankfiler/' a17ifs
09065c                   endif
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for overføring av data fra skjermbilde til diskfile.  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xovfa1        begsr
      *
      * Oppdatere file.
      *
     c     key01         chain     gfaslu                             60
      *
     c                   eval      geffir = l_firm
     c                   eval      geffin = a17fin
     c                   movel     a17ope        gefope
     c                   movel     a17pwd        gefpwd
     c                   movel     a17div        gefdiv
     c                   eval      gefubf = a17ubf
     c                   movel     a17efs        gefefs
     c                   eval      geffos = a17fos
     c                   eval      geffo2 = a17fo2
     c                   eval      geftim = a17tim
     c                   eval      gefdas = a17das
     c                   movel     a17ban        gefban
     c                   movel     a17kom        gefkom
09065c                   movel     a17kom        l_komm
09065c                   movel     a17ifs        gefifs
     c                   movel     a17sbf        gefsbf
     c                   movel     a17sam        gefsam
     c                   movel     a17lon        geflon
      *
      * Legger ut på file.
      *
     c                   if        *in60 = *off
     c                   update    gfaspfu
     c                   else
     c                   write     gfaspfu
     c                   endif
      *
      * Oppretter fil for felles sekvensteller:
      *
     c                   if        a17fin <> 0
     c     a17fin        chain     gustl1                             60
     c                   if        *in60 = *on
     c                   eval      gdufor = a17fin
     c                   write     gustpfr
     c                   else
     c                   unlock    gustl1
     c                   endif
     c                   endif
      *
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Key for faste data - GFASPF
      *
     c     key01         klist
     c                   kfld                    l_firm
      *
      * Hjelpefelt.
      *
     c                   eval      w_in12 = 0
     c                   eval      i = 0
     c                   eval      j = 0
      *
     c                   movel     'NEI'         w_lest
      *
     c                   eval      w_arow = 10
     c                   eval      w_acol = 37
      *
      * Henter informasjon fra file.
      *
     c     key01         chain     gfaspf                             60
      *
     c                   if        *in60 = *off
     c                   exsr      xovfdi
     c                   endif
      *
     c                   endsr
