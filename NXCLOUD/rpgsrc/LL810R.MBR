      /TITLE  ASLAGR: Lagertelling
     h datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LL810R
      *  Beskrivelse . . : Oppdater telle-register, Parameterinntasting
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign
      * --------- ------  --------------------------------------------------
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fll810d    cf   e             workstn
      *
      * Parameter-inn
      *
     d                 ds
     d d_prec                        64
     d  d_batc                       10    overlay(d_prec)
     d  d_val1                        1  0 overlay(d_prec:11)
     d  d_val2                        1  0 overlay(d_prec:12)
     d  d_val3                        1  0 overlay(d_prec:13)
     d  d_val4                        1  0 overlay(d_prec:14)
      *
      * Local Data Area
      *
     d                uds
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable
      *
     d a1batc          s             10
     d w_retu          s              1
     d w_valg          s              1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  A1 Kaller opp subprogram for inntasting av batchnummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     a1tag         tag
      *
      *  Kaller opp subprogrammet
      *
     c                   eval      w_retu = 'D'
     c                   call      'LC500R'
     c                   parm                    w_retu
     c                   parm                    a1batc
      *
     c                   if        w_retu <> *blank
     c                   goto      pgm_end
     c                   endif
      *
     c                   eval      d_batc = a1batc
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandle bilde for registrering av parametre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     b1tag         tag
      *
      *  Nullstiller variable som skal vises i skjermbildet
      *
     c                   eval      b1val1 = 0
     c                   eval      b1val2 = 0
     c                   eval      b1val3 = 0
     c                   eval      b1val4 = 0
      *
     c     b1tagb        tag
     c                   exfmt     b1bld
      *
      *  F3=Gå tilbake  (Avslutt)
      *
     c                   if        *inkc = *on
     c                   goto      pgm_end
     c                   endif
      *
      *  Sjekk, Er innstastet verdi 1 (en)
      *
     c                   if        b1val1 = 0 and
     c                             b1val2 = 0 and
     c                             b1val3 = 0 and
     c                             b1val4 = 0
     c                   goto      pgm_end
     c                   endif
      *
      *  Legger inntastede opplysninger i Hjelpe-felter for Parameter
      *
     c                   eval      d_val1 = b1val1
     c                   eval      d_val2 = b1val2
     c                   eval      d_val3 = b1val3
     c                   eval      d_val4 = b1val4
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandle vindu for bekreftelse av oppdatering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     c1tag         tag
      *
     c                   eval      c1batc = a1batc
     c                   eval      c1tek1 = *blank
     c                   eval      c1tek2 = *blank
     c                   eval      c1tek3 = *blank
     c                   eval      c1tek4 = *blank
      *
     c                   if        b1val1 = 1
     c                   eval      c1tek1 = '- Salgspriser og Kostpriser.'
     c                   endif
      *
     c                   if        b1val2 = 1
     c                   eval      c1tek2 = '- Enhet og evt. varetekst.'
     c                   endif
      *
     c                   if        b1val3 = 1
     c                   eval      c1tek3 = '- Over/Hoved/Undergruppe'
     c                   endif
      *
     c                   if        b1val4 = 1
     c                   eval      c1tek4 = '- Saldoinformasjon lager'
     c                   endif
     c                   exfmt     c1win
      *
      *  F3=Gå tilbake  (Avslutt)
      *
     c                   if        *inkc = *on
     c                   goto      b1tagb
     c                   endif
      *
      *  F10=Legge jobb på jobbkø
      *
     c                   if        *inkj = *on
     c                   eval      w_valg = '1'
     c                   endif
      *
      *  Kaller opp subprogram
      *
     c                   call      'LL812C'
     c                   parm                    d_prec
     c                   parm                    w_valg
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     pgm_end       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Definisjon av local data area for output
      *
     c                   eval      a1batc = *blank
     c                   eval      w_retu = *blank
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     c                   eval      w_valg = *blank
      *
     c                   endsr
