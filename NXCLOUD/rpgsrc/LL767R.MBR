     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LL767R
      * Beskrivelse . . : Sjekker lagerinformasjon på en vare
      *                   + om varetype = 'L'
      *                   LR-flagg må være lik '1' for at *inLR ikke
      *                   skal settes
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       231110 bof  Nytt program
6.10  *       100311 bof  tatt bort test på justering, da dette har mindre
      *                   betydning.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
5.60 flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vlfirm)
     d p_vare          s                   like(vlvare)
     d p_inlr          s              1
     d p_akti          s              1
      *
      * Definering av variable i nøkler
      *
     d vlagl1_vare     s                   like(vlvare)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vlfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
      * Initierer parametre
      *
     c                   eval      p_akti = *off
      *
      * Sjekker beholdning
      *
     c                   exsr      sjekk_lager
5.60  *
5.60  * Sjekker at vare ikke er øreavrunding-varenr
5.60  *
5.60 c     lstsl1_key    chain     lstsl1
5.60 c                   if        p_vare = laavnr
5.60 c                   eval      p_akti = *on
5.60 c                   endif
      *
      * Avslutt program
      *
     c                   if        p_inlr <> *on
     c                   eval      *inlr = *on
     c                   endif
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke aktive poster på lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_lager   begsr
      *
     c                   eval      vlagl1_vare = p_vare
     c     vlagl1_key    setll     vlagl1
     c     vlagl1_key    reade     vlagl1
     c                   dow       not %eof
      *
     c                   if        vlbehl <> 0 or
     c                             vlbbes <> 0 or
     c                             vloord <> 0 or
     c                             vlpakk <> 0 or
     c                             vlinng <> 0 or
     c                             vlutta <> 0 or
6.10 c*                            vljust <> 0 or
     c                             vltell <> 0 or
     c                             vlvtyp = 'L' or
     c                             vlsalg <> 0
      *
     c                   eval      p_akti = *on
     c                   leavesr
      *
     c                   endif
      *
     c     vlagl1_key    reade     vlagl1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_inlr
     c                   parm                    p_akti
      *
      * Key for les på lagerregister
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
5.60  *
5.60  * Key for les på lager-status
5.60  *
5.60 c     lstsl1_key    klist
5.60 c                   kfld                    w_firm
      *
     c                   endsr
