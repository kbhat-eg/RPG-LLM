      /TITLE  Kontroll av gyldig valuta-kode
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RS216R                                              *
      * Beskrivelse . . : Kontroll av gyldig valuta-kode                      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      *           020100  Nytt program                                   KOB  *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                                                       *
      *  BRUK AV INDIKATORER                                                  *
      *                                                                       *
      *    60-61 = Fileindikatorer chain etc.                                 *
      *                                                                       *
      *  BRUK AV RETURKODE                                                    *
      *                                                                       *
      *  Kode i statusfelt(p_stat = N) : post finnes ikke                     *
      *                                                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fra16l1    if   e           k disk    rename(ra16pfr:ra16l1r)
      *
     d w_firm          s                   like(rapfir)
      *
     d p_stat          s              1
     d p_pkod          s                   like(rapkod)
     d p_pmna          s                   like(rapmna)
     d p_pkda          s                   like(rapkda)
     d p_pomr          s                   like(rapmkg)
      *
     d ra16l1_pkod     s                   like(rapkod)
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Sjekke kode
      *
     c                   eval      ra16l1_pkod = p_pkod                                      .
     c     ra16l1_key    chain     ra16l1r                            60
      *
     c                   if        *in60 = *on                                           .....
     c                   eval      p_stat = 'N'                                              .
     c                   eval      p_pmna = 'Koden er ugyldig  '                             .
     c                   else                                                            .....
     c                   eval      p_stat = *blank                                           .
     c                   eval      p_pmna = rapmna                                           .
     c                   endif                                                           .....
      *
      * Finne hvilken kurs
      * som skal benyttes
      *
     c                   if        p_pkda >= rapkda                                      .....
     c                   eval      p_pomr = rapmku                              Ny sats      .
     c                   else                                                                .
     c                   eval      p_pomr = rapmkg                              Gml sats     .
     c                   endif                                                           .....
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     slutt         tag
     c                   eval      *inlr = *on
     c                   return
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av prograp                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     *inzsr        begsr
      *
     c                   eval      w_firm = l_firm
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_stat
     c                   parm                    p_pkod
     c                   parm                    p_pmna
     c                   parm                    p_pkda
     c                   parm                    p_pomr
      *
      * Key for kode
      *
     c     ra16l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra16l1_pkod
      *
     c                   endsr
