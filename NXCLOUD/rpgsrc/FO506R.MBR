     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FO506R
      * Beskrivelse . . : Vise slettede ordrelinjer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       010508 KOB  Nytt program
6.10  * R6.10 250809 BSA  Utvidet ref. felter fra 20 til 30 posisjoner
6.nu  * R6.30 270514 bsa  Programmet gjennomgått mtp nummerutvidelser.
      *
7.xx  * 7.00  270916 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
8.01  *PGRP   090124 bsa  Utvidet prisgruppe. Kompilert
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsohel1    if   e           k disk    rename(sohepfr:sohel1r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffdesl1    if   e           k disk    rename(fdespfr:fdesl1r)
     fvpkol1    if   e           k disk    rename(vpkopfr:vpkol1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
      *
     ffo506d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      *  Datastruktur for detaljlinje
      *
     d                 ds
     d d_text                        70
     d  d_vare                        8    overlay(d_text)
     d  d_tek1                       30    overlay(d_text:10)
     d  d_anta                       13    overlay(d_text:37)
     d  d_enh1                        3    overlay(d_text:50)
     d  d_raba                        6    overlay(d_text:53)
     d  d_salg                       13    overlay(d_text:58)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(sofirm)
     d p_aarr          s              4  0
     d p_kund          s                   like(sokund)
     d p_binr          s                   like(sobinr)
     d p_numm          s                   like(sonumm)
     d p_suff          s                   like(sosuff)
      *
      * Definering av variabler i nøkler
      *
     d rkunl1_kund     s                   like(rkkund)
     d vpkol1_pkod     s                   like(vapkod)
     d vltyl1_ltyp     s                   like(valtyp)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_dato          s               d   datfmt(*iso)
     d w_raba          s              5  2
     d w_csts          s              1
     d w_ctx2          s             20
      *
     d w_firm          s                   like(sofirm)
     d w_kund          s                   like(sokund)
     d w_numm          s                   like(sonumm)
     d w_suff          s                   like(sosuff)
     d w_vare          s                   like(fsvare)
     d w_eror          s              1
     d w_aarr          s              4  0
6.nu d w_binr          s              8  0
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(12)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Fyller control-bildet
      *
     c                   exsr      control_init
      *
      * Fyller subfile
      *
     c     fdesl1_key    setll     fdesl1
     c                   exsr      crt_subfile                                  CRT SFL
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile                                  CRT SFL
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on                                  CRT SFL
     c                   goto      b2taga
     c                   endsl
      *
      * F9=Spørre på lagerbeholdning
      *
     c                   if        *inki = *on
3.31 c                   call      'LL500R'
3.31  *                  parm                    w_eror
3.31  *                  parm                    w_firm
3.31  *                  parm                    w_vare
     c                   goto      b2tagb
     c                   endif
      *
      * F14=Vis kunde-/kundeprosjekt-informasjon
      *
     c                   if        *inkn = *on
     c                   call      'FL510R'
     c                   parm                    w_firm
     c                   parm                    sokund
     c                   parm                    sokpro
     c                   goto      b2tagb
     c                   endif
      *
      * F21=Spørring på vare/pris
      *
     C                   if        *inkv = *on
     C                   call      'VV550R'
     C                   goto      b2tagb
     C                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å initierer/fylle opp control-bildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     control_init  begsr
      *
      * Finner ordre-hode
      *
     c     fohel1_key    chain     fohel1
     c                   if        %found(fohel1)
      *
     c                   eval      b2kund = fokund
     c                   eval      b2navn = fonavn
     c                   eval      b2adr1 = foadr1
     c                   eval      b2sted = fosted
     c                   eval      b2numm = fonumm
     c                   eval      b2vref = fovref
     c                   eval      b2dref = fodref
      *
     c                   if        sobdat <> *loval
     c     *dmy          move      fobdat        b2bdat
     c                   endif
      *
     c                   endif
      *
      * Finner faktura-hode
      *
     c                   if        not%found(fohel1)
     c     sohel1_key    chain     sohel1
      *
     c                   eval      b2kund = sokund
     c                   eval      b2navn = sonavn
     c                   eval      b2adr1 = soadr1
     c                   eval      b2sted = sosted
     c                   eval      b2numm = sonumm
     c                   eval      b2vref = sovref
     c                   eval      b2dref = sodref
      *
     c                   if        sobdat <> *loval
     c     *dmy          move      sobdat        b2bdat
     c                   endif
      *
     c                   endif
      *
      * Henter kundenavn
      *
     c                   if        w_kund <> 0
     c                   eval      rkunl1_kund = w_kund
     c     rkunl1_key    chain     rkunl1                             90
     c                   if        *in90 = *off
     c                   eval      b2kund = w_kund
     c                   eval      b2knav = rknavn
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c     fdesl1_key    setll     fdesl1
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vise
      *
     c                   if        b1valg = 5
     c                   call      'FD506R'
     c                   parm                    w_firm
6.nu c                   parm                    b1numm
     c                   parm                    b1suff
     c                   parm                    b1line
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c     fdesl1_key    reade     fdesl1                                 15
      *
     c                   if        *in15                                        EOF
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1numm = fsnumm
     c                   eval      b1suff = fssuff
     c                   eval      b1lety = fslety
     c                   eval      b1line = fsline
     c                   eval      b1text = *blank
      *
     c                   eval      w_raba = 0
      *
      * Finner linjetype
      *
     c                   eval      vltyl1_ltyp = fsltyp
     c     vltyl1_key    chain     vltyl1                             92
      *
      * Tekstlinje
      *
     c                   if        valktx = 1
     c                   eval      b1text = fstek1 + fstek2
     c                   eval      b1lety = *zero
      *
      * Varelinje
      *
     c                   else
      *
     c                   if        fsrab2 = 0 and
     c                             fsbrr1 = 0 and
     c                             fsbrr2 = 0
     c                   eval      w_raba = fsrab1
     c                   else
     c                   if        fssums <> 0
     c                   eval(h)   w_raba = fssumr * 100 / fssums
     c                   endif
     c                   endif
      *
     c                   eval(h)   fssums = fssums - (fssums * w_raba / 100)
      *
     c                   eval      d_anta = %editc(fsanta : 'M')
     c                   eval      d_salg = %editc(fssums : 'M')
      *
     c                   eval      d_vare = fsvare
     c                   eval      d_tek1 = fstek1
     c                   eval      d_enh1 = fsenh1
     c                   eval      d_raba = *blank
      *
     c                   if        w_raba <> 0
     c                   eval      d_raba = %editc(w_raba : '4')
     c                   endif
      *
     c                   if        w_raba = 0
     c                   eval      vpkol1_pkod = fskpri
     c     vpkol1_key    chain     vpkol1                             91
     c                   if        *in91 = *off
     c                   move      vaptxt        d_raba
     c                   endif
     c                   endif
      *
     c                   eval      b1text = d_text
      *
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_aarr
V5.30c                   parm                    p_kund
V5.30c                   parm                    p_binr
     c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Key for oppslag på faktura-hode-register
      *
     c     sohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_aarr
     c                   kfld                    w_binr
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for oppslag på ordre-hode-register
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for les på ordre-linje-slettet register
      *
     c     fdesl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for oppslag på priskode-register
      *
     c     vpkol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vpkol1_pkod
      *
      * Key for oppslag på linje-type-register
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
      * Henter parameter-verdier
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_aarr = p_aarr
     c                   eval      w_kund = p_kund
     c                   eval      w_binr = p_binr
     c                   eval      w_numm = p_numm
     c                   eval      w_suff = p_suff
      *
     c                   eval      *in22 = *on
      *
     c                   endsr
