     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : Vare-pris, prisjustering på vare
      * Beskrivelse . . : Prisjustering
      *
      *                   DG for beregning av salgspris blir kun benyttet på
      *                   varer med kostpris
      *                   Dersom det finnes priser nyere enn -eller lik
      *                   prisdato, oppdateres disse med endringen. Ellers
      *                   opprettes nye priser på prisdato.
      *
      *                   LR-flagg må være lik '1' for at *inLR ikke skal
      *                   settes
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       111199 HKO  Nytt program
      *       050199 HKO  Endret lengde på parametre for mer korrekt
      *                   utregning
5.70  * PRGR  241008 BOF  Utvidet med prisgruppe
      * 6.10  290609 BSA  Oppdatert med sporingsinformasjon
6.20  * 6.20  030411 bof  Utvidet vvprpf med prisgiver
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
     fvvprlu    uf a e           k disk    rename(vvprpfr:vvprlur)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vpfirm)
     d p_vare          s                   like(vpvare)
     d p_dato          s               d   datfmt(*iso)
     d p_spro          s              7  4
     d p_kpro          s              7  4
     d p_ipro          s              7  4
     d p_bpro          s              7  4
     d p_dekn          s              5  2
     d p_inlr          s              1
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av variabler i nøkler
      *
     d vvprl1_vare     s                   like(vpvare)
     d vvprlu_vare     s                   like(vpvare)
6.20 d vvprlu_ldor     s                   like(vpldor)
5.70 d vvprlu_prgr     s                   like(vpprgr)
     d vvprlu_enhe     s                   like(vpenhe)
     d vvprlu_lety     s                   like(vplety)
     d vvprlu_pdat     s                   like(vppdat)
      *
      * Definering av variable
      *
     d b_frst          s              1    inz(*off)
     d w_udat          s               d   datfmt(*iso)
      *
     d w_firm          s                   like(vpfirm)
     d w_sapr          s                   like(vpsapr)
     d w_kopr          s                   like(vpkopr)
     d w_inpr          s                   like(vpinpr)
     d w_bupr          s                   like(vpbupr)
     d w_enhe_gml      s                   like(vpenhe)
     d w_lety_gml      s                   like(vplety)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
      * Henter dagen dato
      *
     c                   time                    w_udat
      *
      * Behandling av vare
      *
     c                   exsr      behandling
      *
      * Avslutt
      *
     c                   if        p_inlr <> *on
     c                   eval      *inlr = *on
     c                   endif
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av priser knyttet mot vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
      * Dersom det finnes priser nyere enn -eller lik prisdato, oppdateres
      * disse med endringen. Ellers opprettes nye priser.
      * Oppdaterer alle leveringstyper og enheter
      *
     c                   eval      b_frst = *on
     c                   eval      w_enhe_gml = *blank
     c                   eval      w_lety_gml = -1
      *
     c                   eval      vvprl1_vare = p_vare
     c     vvprl1_key    setll     vvprl1
     c     vvprl1_key    reade     vvprl1                                 91
     c                   dow       *in91 = *off
      *
     c                   if        w_udat >= vppdat
      *
     c                   if        b_frst = *on or
     c                             w_enhe_gml <> vpenhe or
     c                             w_lety_gml <> vplety
     c                   exsr      oppdater
     c                   endif
      *
     c                   eval      b_frst = *off
     c                   eval      w_enhe_gml = vpenhe
     c                   eval      w_lety_gml = vplety
      *
     c                   endif
      *
     c     vvprl1_key    reade     vvprl1                                 91
     c                   enddo
      *
     c     end_behandl   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av priser i vare-pris-registeret
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
      * Finner eksisterende priser
      *
     c                   eval      w_sapr = vpsapr
     c                   eval      w_kopr = vpkopr
     c                   eval      w_inpr = vpinpr
     c                   eval      w_bupr = vpbupr
      *
      * Kalkulerer nye priser
      *
     c                   if        p_spro <> 0
     c                   eval(h)   w_sapr = vpsapr + (vpsapr * p_spro / 100)
     c                   endif
      *
     c                   if        p_kpro <> 0
     c                   eval(h)   w_kopr = vpkopr + (vpkopr * p_kpro / 100)
     c                   endif
      *
     c                   if        p_ipro <> 0
     c                   eval(h)   w_inpr = vpinpr + (vpinpr * p_ipro / 100)
     c                   endif
      *
     c                   if        p_bpro <> 0
     c                   eval(h)   w_bupr = vpbupr + (vpbupr * p_bpro / 100)
     c                   endif
      *
     c                   if        p_dekn <> 0 and
     c                             vpkopr <> 0
     c                   eval(h)   w_sapr = (100 * vpkopr) / (100 - p_dekn)
     c                   endif
      *
      * Avbryter dersom det ikke er prisendring
      *
     c                   if        w_sapr = vpsapr and
     c                             w_kopr = vpkopr and
     c                             w_inpr = vpinpr and
     c                             w_bupr = vpbupr
     c                   goto      end_oppd
     c                   endif
      *
      * Oppretter/oppdaterer vare-pris
      *
     c                   eval      vvprlu_vare = vpvare
6.20 c                   eval      vvprlu_ldor = vpldor
5.70 c                   eval      vvprlu_prgr = vpprgr
     c                   eval      vvprlu_enhe = vpenhe
     c                   eval      vvprlu_lety = vplety
     c                   eval      vvprlu_pdat = p_dato
     c     vvprlu_key    chain     vvprlur                            92
      *
     c                   eval      vppdat = p_dato
     c                   eval      vpsapr = w_sapr
     c                   eval      vpkopr = w_kopr
     c                   eval      vpinpr = w_inpr
     c                   eval      vpbupr = w_bupr
     c                   eval      vpeusr = l_user
     c                   time                    vpedat
     c                   time                    vpetim
      *
     c                   if        *in92 = *off
     c                   update    vvprlur
     c                   else
6.20 c                   eval      vpfirm = w_firm
6.20 c                   eval      vpvare = vvprlu_vare
6.20 c                   eval      vpldor = vvprlu_ldor
6.20 c                   eval      vpprgr = vvprlu_prgr
6.20 c                   eval      vpenhe = vvprlu_enhe
6.20 c                   eval      vplety = vvprlu_lety
6.20 c                   eval      vppdat = vvprlu_pdat
6.10 c                   time                    vpodat
6.10 c                   time                    vpotim
6.10 c                   eval      vpousr = l_user
     c                   write     vvprlur
     c                   endif
      *
     c     end_oppd      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_dato
     c                   parm                    p_spro
     c                   parm                    p_kpro
     c                   parm                    p_ipro
     c                   parm                    p_bpro
     c                   parm                    p_dekn
     c                   parm                    p_inlr
      *
      * Key for les på vare-pris
      *
     c     vvprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprl1_vare
      *
      * Key for oppdatering av vare-pris
      *
     c     vvprlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprlu_vare
6.20 c                   kfld                    vvprlu_ldor
5.70 c                   kfld                    vvprlu_prgr
     c                   kfld                    vvprlu_enhe
     c                   kfld                    vvprlu_lety
     c                   kfld                    vvprlu_pdat
      *
     c                   endsr
