     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : MO701R
      * Beskrivelse . . : Overvåking - danner grunndata for type 7
      *                   Antall restordre på lagervarer pr. dag
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       090107 bø   Nytt program
      *       060507 bø   Feilretting vedr. skriv av riktige data
6.21  * 6.20  190511 bø   Utvidet vl710r med div. info
6.nu  * 6.30  230614 bof  sjekket mtp. nummerutvidelse
6.31  * 6.30  030118 bof  Gen. lwo1pf,lwo2pf,lwo3pf,lwo4pf,lwo7pf (num=8)
6.32  * 6.30  170918 bkv  Utvidet med trelast sortiment
8.01  *PRG2   220622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffoheld    if   e           k disk    rename(fohepfr:foheldr)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffodtlv    if   e           k disk    rename(fodtpfr:fodtlvr)
     fsodtlr    if   e           k disk    rename(sodtpfr:sodtlrr)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvotylr    if   e           k disk    rename(votypfr:votylrr)
     flwo7lu    uf a e           k disk    rename(lwo7pfr:lwo7lur)
6.32 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
      *
     d foheld_bdat     s                   like(fobdat)
     d fodtlv_vare     s                   like(fdvare)
     d fodtlv_numm     s                   like(fdnumm)
     d fodtlv_suff     s                   like(fdsuff)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d sodtlr_aarr     s                   like(sdaarr)
     d sodtlr_binr     s                   like(sdbinr)
     d sodtlr_numm     s                   like(sdnumm)
     d sodtlr_suff     s                   like(sdsuff)
     d vvarl1_vare     s                   like(vvvare)
     d votylr_otyp     s                   like(vaotyp)
     d lwo7lu_odat     s                   like(mwodat)
     d lwo7lu_osys     s                   like(mwosys)
     d lwo7lu_oonr     s                   like(mwoonr)
     d lwo7lu_osuf     s                   like(mwosuf)
     d lwo7lu_olin     s                   like(mwolin)
     d lwo7lu_ovnr     s                   like(mwovnr)
     d lwo7lx_odat     s                   like(mwodat)
      *
     d w_firm          s                   like(vvfirm)
     d w_vare          s                   like(vvvare)
     d w_dato          s                   like(vvedat)
     d w_aar           s              4  0
     d p_dato          s                   like(vvedat)
     d w_usr           s                   like(fouser)
     d w_oda           s                   like(fodate)
     d w_tim           s                   like(fotime)
     d w_bong          s                   like(fobong)
     d w_avde          s                   like(foavde)
     d w_suff          s                   like(fdsuff)
     d w_numm          s                   like(fdnumm)
     d w_line          s                   like(fdline)
     d w_ant           s              4  0
     d w_sys           s              1
     d w_vtyp          s                   like(vvvtyp)
6.21 d w_udat          s                   like(vvudat)
6.21 d w_ldor          s                   like(vvldor)
6.32 d w_lv_nobb       s                   like(vvlnob)
6.32 d w_lv_modn       s                   like(vvlmod)
6.32 d w_lv_lldo       s                   like(vvlnle)
6.32 d w_lv_llva       s                   like(vvllva)
      *
6.21 d b_inlr          s              1    inz(*off)
     d b_rest          s              1    inz(*off)
     d b_lagv          s              1    inz(*off)
      *
      * Local Data Area
      *
     d                uds
      *
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
      *
      * Definering av variable
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * sletter først for dato som det skal kjøres for.
      *
     c                   eval      lwo7lx_odat = w_dato
readec     lwo7lx_key    setll     lwo7lu
readec     lwo7lx_key    reade     lwo7lu
readec                   dow       not %eof(lwo7lu)
     c                   delete    lwo7lur
readec     lwo7lx_key    reade     lwo7lu
     c                   enddo
      *
     c                   eval      w_aar = %subdt(w_dato:*years)
     c                   exsr      les_ordre
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     avslutt       tag
     c                   eval      *inlr = *on
      *
     c                   return
      *
      * --------------------------------------------------------------
      *  Leser ordre - logger ut varer med endret varetekst
      * --------------------------------------------------------------
      *
     c     les_ordre     begsr
      *
     c                   eval      foheld_bdat = w_dato
     c     foheld_key    setll     foheld
     c     foheld_key    reade     foheld
     c                   dow       not %eof(foheld)
      *
      * det gjelder ikke behandlingskode 0
     c                   eval      votylr_otyp = footyp
     c     votylr_key    chain     votylr
     c                   if        %found(votylr) and
     c                             vaoakk <> 0
      * ikke tilbud
     c                   eval      fodtl1_numm = fonumm
     c                   eval      fodtl1_suff = 0
     c     fodtl1_key    setll     fodtl1
     c     fodtl1_key    reade     fodtl1
     c                   dow       not %eof(fodtl1)
     c                   eval      w_vare = fdvare
     c                   exsr      ctrl_vare
     c                   eval      w_sys  = *blank
      *
     c                   if        b_lagv = *on
     c                   eval      w_numm = fdnumm
     c                   eval      w_vare = fdvare
     c                   eval      w_line = fdline
     c
     c                   exsr      ctrl_rest
     c
     c                   if        w_ant > 0
     c                   eval      lwo7lu_odat = fobdat
     c                   eval      lwo7lu_osys = w_sys
     c                   eval      lwo7lu_oonr = w_numm
     c                   eval      lwo7lu_ovnr = w_vare
     c                   eval      lwo7lu_olin = w_line
     c                   eval      lwo7lu_osuf = 0
     c                   eval      w_usr       = fouser
     c                   eval      w_oda       = fodate
     c                   eval      w_tim       = fotime
     c                   eval      w_avde      = foavde
     c                   exsr      skriv_logg
     c                   endif
     c                   endif
      *
readec     fodtl1_key    reade     fodtl1
readec                   enddo
      *
     c                   endif
      *
readec     foheld_key    reade     foheld
readec                   enddo
      *
     c                   endsr
      *
      * --------------------------------------------------------------
      *  Skriver logg ved feil-situasjon
      * --------------------------------------------------------------
      *
     c     skriv_logg    begsr
      *
     c                   clear                   lwo7lur
     c     lwo7lu_key    chain     lwo7lu
     c                   eval      mwoant = w_ant
     c                   eval      mwosys = w_sys
     c                   eval      mwousr = fouser
     c                   eval      mwooda = fodate
     c                   eval      mwotim = fotime
     c                   eval      mwoavd = foavde
     c                   eval      mwovtx = vvtek1
     c                   eval      mwoogr = vvogrp
     c                   eval      mwohgr = vvhgrp
     c                   eval      mwougr = vvugrp
     c                   if        %found(lwo7lu)
     c                   update    lwo7lur
     c                   else
     c                   eval      mwofir = w_firm
     c                   eval      mwodat = lwo7lu_odat
     c                   eval      mwosys = lwo7lu_osys
     c                   eval      mwoonr = lwo7lu_oonr
     c                   eval      mwosuf = lwo7lu_osuf
     c                   eval      mwovnr = lwo7lu_ovnr
     c                   eval      mwolin = lwo7lu_olin
     c                   write     lwo7lur
     c                   endif
      *
     c                   endsr
      *
      * --------------------------------------------------------------
      *  Tester om atistikk - legger ut varer med endret varetekst
      * --------------------------------------------------------------
      *
     c     ctrl_vare     begsr
      *
     c                   eval      b_lagv = *off
      * henter varetype
      *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    fdvare
5.70 c                   parm                    fdlage
5.70 c                   parm                    w_vtyp
6.21 c                   parm                    w_udat
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.32 c                   parm                    w_lv_nobb
6.32 c                   parm                    w_lv_modn
6.32 c                   parm                    w_lv_lldo
6.32 c                   parm                    w_lv_llva
     c                   if        w_vtyp = 'L'
     c                   eval      b_lagv = *on
     c                   endif
      *
     c                   endsr
      *
      * --------------------------------------------------------------
      *  Kontrollerer om det er rest på lagervarer
      * --------------------------------------------------------------
      *
     c     ctrl_rest     begsr
      *
     c                   eval      w_suff = 0
     c                   eval      w_ant  = 0
      *
     c     ctrl1         tag
     c                   eval      w_suff = w_suff + 1
     c                   if        w_suff > 98
     c                   goto      neste
     c                   endif
     c                   eval      fodtlv_vare = w_vare
     c                   eval      fodtlv_numm = fonumm
     c                   eval      fodtlv_suff = w_suff
     c     fodtlv_key    setll     fodtlv
     c                   read      fodtlv
     c                   if        fdvare = fodtlv_vare  and
     c                             fdnumm = fodtlv_numm  and
     c                             fdsuff = fodtlv_suff
     c                   eval      w_ant  = w_Ant + 1
     c                   eval      w_sys  = 'O'
     c                   goto      ctrl1
     c                   endif
      *
     c     neste         tag
     c                   eval      w_suff = 0
     c                   if        w_ant = 0
     c     ctrl2         tag
     c                   eval      w_suff = w_suff + 1
     c                   if        w_suff > 98
     c                   goto      slutt
     c                   endif
     c                   eval      w_suff = w_suff + 1
     c                   eval      sodtlr_aarr = %subdt(fobdat:*years)
     c                   eval      sodtlr_binr = fobinr
     c                   eval      sodtlr_numm = fonumm
     c                   eval      sodtlr_suff = w_suff
     c     sodtlr_key    setll     sodtlr
     c                   read      sodtlr
     c                   if        sdvare = fodtlv_vare  and
     c                             sdnumm = fodtlv_numm  and
     c                             sdsuff = fodtlv_suff
     c                   eval      w_ant  = w_Ant + 1
     c                   eval      w_sys  = 'S'
     c                   goto      ctrl2
     c                   endif
     c                   endif
      *
     c     slutt         endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *  Key for les av ordre
      *
     c     foheld_key    klist
     c                   kfld                    w_firm
     c                   kfld                    foheld_bdat
      *
      *  Key for les av ordre detaljer
      *
     c     fodtlv_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlv_vare
     c                   kfld                    fodtlv_numm
     c                   kfld                    fodtlv_suff
      *
      *
      *  Key for les av ordre detaljer
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
      *
      *  Key for les av historisk faktura
      *
     c     sodtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sodtlr_aarr
     c                   kfld                    sodtlr_binr
     c                   kfld                    sodtlr_numm
     c                   kfld                    sodtlr_suff
      *
      *  Key for les av ordretype
      *
     c     votylr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votylr_otyp
      *
      * Key for oppdatering av overvåking
      *
     c     lwo7lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwo7lu_odat
     c                   kfld                    lwo7lu_osys
     c                   kfld                    lwo7lu_ovnr
     c                   kfld                    lwo7lu_oonr
     c                   kfld                    lwo7lu_osuf
     c                   kfld                    lwo7lu_olin
      *
      * Key for slett av overvåking på den dato man vil kjøre på.
      *
     c     lwo7lx_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwo7lx_odat
      *
     c     *entry        plist
     c                   parm                    p_dato
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_dato = p_dato
      *
     c                   endsr
