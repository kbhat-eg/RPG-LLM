      /TITLE  ASOKON Spørring v.h.a. rapportter (Utvalg)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : AS???
      * Program . . . . :
      * Beskrivelse . . : Bygger FTP parametre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       111001 MHA  Nytt program
6.10  * 6.10  230611 bsa  Utvidet filnavn
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fafpapf    o  a e             disk    rename(afpapf:afpapfr)
     faflfpf    if   e             disk    rename(aflfpf:aflfpfr)
     faftpl1    if a e           k disk    rename(aftppfr:aftpl1r)
      *
     dLinje1           ds
     d line1                   1     80
     d bruker                  1     36
     d passord                40     50
      *
     dLinje2           ds           200
     d line2                   1    200
      *
     dLinje3           ds           200
     d line3                   1    200
      *
      * Definering av parametre
     d p_navn          s                   like(afnavn)
     d p_status        s              1
     d p_konvin        s                   like(afkoni)
     d p_konvinl       s                   like(afkoil)
     d p_konvut        s                   like(afkono)
     d p_konvutl       s                   like(afkool)
     d p_host          s                   like(afhost)
6.10 d*p_xnavn         s             20a
6.10 d p_xnavn         s             30a
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable
      *
     d aftpl1_navn     s                   like(afnavn)
      *
     d w_bruker        s             10a
     d w_passord       s             10a
     d w_status        s              1
      *
     d w_firm          s                   like(affirm)
      *
      * Definering av konstanter
      *
      /eject
      *
      *
     c     start         tag
      * Sjekke om FTP forespørsel finnes
     c     aftpl1_key    chain     aftpl1r
     c                   if        not %found
     c                   eval      p_status = 'N'
     c                   goto      avslutt
     c                   endif
      *
      *
     c                   if        afqyps = 'J'
     c
     c                   endif
      *
      * Setter opp første linje med bruker og passord
     c                   eval      bruker = afuser
     c                   if        afprxu <> *blank
     c                   eval      bruker = %trim(bruker) + '@' +
     c                             %trim(afhost)
     c                   endif
     c                   if        afqyps = 'J'
     c                   eval      w_bruker = afuser
     c                   eval      w_passord = *blank
     c                   eval      w_status = *blank
     c                   call      'AF709R'
     c                   parm                    w_bruker
     c                   parm                    w_passord
     c                   parm                    w_status
     c                   if        w_status = *blank
     c                   eval      passord = w_passord
     c                   eval      bruker = w_bruker
     c                   else
     c                   eval      p_status = 'A'
     c                   goto      avslutt
     c                   endif
     c                   else
     c                   eval      passord = afpass
     c                   endif
     c                   eval      SrcDta = Linje1
     c                   write     afpapfr
      *
      * Setter riktig filformat
     c                   if        affils = 'A'
     c                   eval      line2 = 'namefmt 0'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   else
     c                   eval      line2 = 'namefmt 1'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   endif
      *
      * Setter riktig aktiv/passiv
     c                   if        afspas = 'A'
     c                   eval      line2 = 'sendpasv 0'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   else
     c                   eval      line2 = 'sendpasv 1'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   endif
      *
      * Setter muligens binær overføring
     c                   if        afbina = 'J'
     c                   eval      line2 = 'binary'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   endif
      *
      * Vi skriver inn kommandoen
     c                   if        affilt = *blank
     c                   eval      line2 = 'lcd ' + %trim(aflocd)
     c                   else
     c                   eval      line2 = 'lcd /temp'
     c                   endif
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   if        afxcm1 <> *blank
     c                   eval      line2 = %trim(afxcm1)
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   endif
     c                   if        afxcm2 <> *blank
     c                   eval      line2 = %trim(afxcm2)
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   endif
     c                   if        afxcm3 <> *blank
     c                   eval      line2 = %trim(afxcm3)
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   endif
     c                   eval      SrcDta = *blank
     c                   read      aflfpfr
     c                   dou       %EOF
     c                   if        SrcDta <> *blank
     c                   eval      linje3 = SrcDta
     c                   eval      line2 = %trim(aftype)
     c                   if        afchdr <> *blank
     c                   eval      line2 = %trim(line2) + ' ' +
     c                             %trim(afchdr) + '/' + %trim(line3)
     c                   else
     c                   eval      line2 = %trim(line2) + ' ' +
     c                             %trim(line3)
     c                   endif
      *
     c                   if        afrepl = 'J'
     c                   eval      line2 = %trim(line2) + ' (replace'
     c                   endif
      *
     c                   eval      SrcDta = Linje2
     c                   write     afpapfr
      *
      * Skal filen(e) slettes??
     c                   if        afslet = 'J'
     c                   if        afchdr <> *blank
     c                   eval      line2 = 'delete ' + %trim(afchdr) +
     c                             '/' + %trim(line3)
     c                   else
     c                   eval      line2 = 'delete ' +
     c                             %trim(line3)
     c                   endif
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   endif
     c                   endif
     c                   read      aflfpfr
     c                   enddo
      *
      * Vi legger inn slutt kommandoen
     c                   eval      line2 = 'quit'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
      *
      * Vi gir tilbake parametre ang. host etc...
     c                   if        afprxu <> *blank
     c                   eval      p_host = afprxu
     c                   else
     c                   eval      p_host = afhost
     c                   endif
     c                   eval      p_konvin = afkoni
     c                   eval      p_konvinl = afkoil
     c                   eval      p_konvut = afkono
     c                   eval      p_konvutl = afkool
      *
     c     avslutt       tag
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     aftpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    aftpl1_navn
      *
     c     *entry        plist
     c                   parm                    p_navn
     c                   parm                    p_status
     c                   parm                    p_konvin
     c                   parm                    p_konvinl
     c                   parm                    p_konvut
     c                   parm                    p_konvutl
     c                   parm                    p_host
     c                   parm                    p_xnavn
      *
     c                   eval      aftpl1_navn = p_navn
     c                   eval      w_firm = l_firm
      *
     c                   endsr
      *
