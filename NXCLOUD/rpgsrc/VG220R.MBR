     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : VG220R
      * Beskrivelse . . : Faktorer i forb. gj.kostpris, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
6.30  *       131118 NHO  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvgkfiu    uf a e           k disk    rename(vgkfst:vgkfiur)
     fvgkfi1    if   e           k disk    rename(vgkfst:vgkfi1r)
     fvgkfir    if   e           k disk    rename(vgkfst:vgkfirr)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvgkkir    if   e           k disk    rename(vgkkst:vgkkirr)
     fvgktir    if   e           k disk    rename(vgktst:vgktirr)
      *
     fvg220d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
      *
     d vgkfiu_lage     s                   like(vgflag)
     d vgkfiu_ftyp     s                   like(vgftyp)
     d vgkfiu_flev     s                   like(vgflev)
     d vgkfiu_fogr     s                   like(vgfogr)
     d vgkfiu_fhgr     s                   like(vgfhgr)
     d vgkfiu_fugr     s                   like(vgfugr)
     d vgkfiu_fvar     s                   like(vgfvar)
      *
     d vgkfir_lage     s                   like(vgflag)
     d vgkfir_ftyp     s                   like(vgftyp)
     d vgkfir_flev     s                   like(vgflev)
     d vgkfir_fogr     s                   like(vgfogr)
     d vgkfir_fhgr     s                   like(vgfhgr)
     d vgkfir_fugr     s                   like(vgfugr)
     d vgkfir_fvar     s                   like(vgfvar)
      *
     d vgkfi1_lage     s                   like(vgflag)
     d vgkfi1_ftyp     s                   like(vgftyp)
     d vgkfi1_flev     s                   like(vgflev)
     d vgkfi1_fogr     s                   like(vgfogr)
     d vgkfi1_fhgr     s                   like(vgfhgr)
     d vgkfi1_fugr     s                   like(vgfugr)
     d vgkfi1_fvar     s                   like(vgfvar)
      *
     d vgktir_ftyp     s                   like(vtftyp)
      *
     d rlevl1_levr     s                   like(rllevr)
      *
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
      *
     d vvarl1_vare     s                   like(vvvare)
      *
      *
      * Definering av variable
      *
     d c2ltxt          s             30
     d w_tek1          s             35
     d w_gtxt          s             35
     d w_vare          s             15
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_fnul          s              1    inz(*off)
     d b_kopi          s              1    inz(*off)
      *
     d w_firm          s                   like(vgfirm)
     d w_lage          s                   like(vgflag)
      *
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Siden som skal vises
      * w_ffld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      sok
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   eval      b1ftyp = *blank
     c                   exsr      xc2bld
     c                   exsr      forny
     c                   when      *inkg
     c                   exsr      upd_fakt
     c                   when      *inkk
     c                   exsr      les_excel
     c                   exsr      forny
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2ldor <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
     c                   if        b2lage <> w_lage
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   eval      vgkfi1_flev = b1flev
     c     vgkfi1_key    setll     vgkfi1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på leverandør
      *
     c                   if        b2ldor <> 0 or
     c                             b2lage <> 0
     c                   eval      w_seqe = *blank
     c                   eval      vgkfi1_flev = b2ldor
     c                   eval      vgkfi1_lage = b2lage
     c                   eval      w_lage = b2lage
      *
     c     vgkfi1_key    setll     vgkfi1
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2ldor = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      *in30 = *on
     c                   eval      *in31 = *off
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      *in30 = *off
     c                   eval      *in31 = *off
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      *in31 = *on
     c                   eval      *in30 = *on
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_fnul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_fnul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      vgkfir_ftyp = b1ftyp
     c                   eval      vgkfir_flev = b1flev
     c                   eval      vgkfir_lage = b1flag
     c                   eval      vgkfir_fogr = b1fogr
     c                   eval      vgkfir_fhgr = b1fhgr
     c                   eval      vgkfir_fugr = b1fugr
     c                   movel     b1fvar        vgkfir_fvar
     c     vgkfir_key    chain     vgkfir
      *
     c                   if        %found
      *
     c                   if        b_kopi = *on
     c                   eval      c2ftyp = k1ftyt
     c                   eval      b_kopi = *off
     c                   else
     c                   eval      c2ftyp = vgftyp
     c                   endif
      *
     c                   eval      c2flev = vgflev
     c                   eval      c2fogr = vgfogr
     c                   eval      c2fhgr = vgfhgr
     c                   eval      c2fugr = vgfugr
     c                   eval      c2flev = vgflev
     c                   eval      c2ffak = vgffak
     c                   movel     vgfvar        c2fvar
     c                   eval      c2eusr = vgfusr
     c     *dmy          move      vgfdat        c2edat
     c                   exsr      hent_txt
     c                   else
     c                   eval      c2flev = *zero
     c                   eval      c2fogr = *zero
     c                   eval      c2fhgr = *zero
     c                   eval      c2fugr = *zero
     c                   eval      c2fvar = *blank
     c                   eval      c2ffak = *zero
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   eval      c2ftyp = *blank
     c                   eval      c2ltxt = *blank
     c                   eval      c2otxt = *blank
     c                   eval      c2htxt = *blank
     c                   eval      c2utxt = *blank
     c                   eval      c2vtxt = *blank
      *
     c                   endif
      *
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
      * Feilindikatorer off
     c                   eval      *in30 = *off
     c                   eval      *in42 = *off
     c                   eval      *in43 = *off
     c                   eval      *in44 = *off
     c                   eval      *in45 = *off
     c                   eval      *in46 = *off
     c                   eval      *in47 = *off
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      * Faktortype
      *
     c                   if        *inkd
      *
     c                   if        w_afld = 'C2FTYP'
     c                   call      'VG250R'
     c                   parm                    w_firm
     c                   parm                    c2ftyp
     c                   goto      c2taga
     c                   endif
      *
     c                   if        c2ftyp = ' '
     c                   endif
      *
      * Leverandør
      *
     c                   if        w_afld = 'C2FLEV'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    c2flev
     c                   parm                    c2ltxt
     c                   goto      c2taga
     c                   endif
      *
      * Overgruppe
      *
     c                   if        w_afld = 'C2FOGR'
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    c2fogr
     c                   parm                    w_gtxt
     c                   eval      c2otxt = w_gtxt
     c                   goto      c2taga
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_afld = 'C2FHGR'
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    c2fogr
     c                   parm                    c2fhgr
     c                   parm                    w_gtxt
     c                   eval      c2htxt = w_gtxt
     c                   goto      c2taga
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_afld = 'C2FUGR'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    c2fogr
     c                   parm                    c2fhgr
     c                   parm                    c2fugr
     c                   parm                    w_gtxt
     c                   eval      c2utxt = w_gtxt
     c                   goto      c2taga
     c                   endif
      *
      * Varenummer
     c                   if        w_afld = 'C2FVAR'
     c*                  eval      w_vare = C2FVAR
     c                   movel     c2fvar        w_vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   movel     w_vare        c2fvar
     c                   eval      c2vtxt = w_tek1
     c                   goto      c2taga
     c                   endif
      *
      *
     c                   endif
      *
      * Validering av input
      *
      *
      *  Sjekk om gyldig faktortype
      *
     c                   eval      vgktir_ftyp = c2ftyp
     c     vgktir_key    chain     vgktir
     c                   if        not %found
     c                   eval      *in41 = '1'
     c                   exsr      xc1msg
     c                   goto      c2taga
     c                   endif
      *  Sjekk om leverandøren finnes
      *
     c                   if        c2flev <> 0
     c                   eval      rlevl1_levr = c2flev
      *
     c     rlevl1_key    chain     rlevl1R
     c                   if        not %found
     c                   eval      *in47 = '1'
     c                   exsr      xc1msg
     c                   goto      c2taga
     c                   else
     c                   eval      c2ltxt = rlnavn
     c                   endif
     c                   endif
      * Test om overgruppe finnes
      *
     c                   if        c2fogr <> 0 and
     c                             c2fhgr = 0
     c                   eval      vogrl1_oogr = c2fogr
     c     vogrl1_key    chain     vogrl1
     c                   if        not %found
     c                   eval      *in44 = '1'
     c                   exsr      xc1msg
     c                   goto      c2taga
     c                   endif
     c                   eval      c2otxt = vgotxt
     c                   endif
      *
      * Test om hovedgruppe finnes
      *
     c                   if        c2fhgr <> 0 and
     c                             c2fugr = 0
     c                   eval      vhgrl1_hogr = c2fogr
     c                   eval      vhgrl1_hhgr = c2fhgr
     c     vhgrl1_key    chain     vhgrl1
     c                   if        not %found
     c                   eval      *in45 = '1'
     c                   exsr      xc1msg
     c                   goto      c2taga
     c                   endif
     c                   eval      c2htxt = vghtxt
     c                   endif
      *
      * Test om undergruppe finnes
      *
     c                   if        c2fugr <> 0
     c                   eval      vugrl1_uogr = c2fogr
     c                   eval      vugrl1_uhgr = c2fhgr
     c                   eval      vugrl1_uugr = c2fugr
     c     vugrl1_key    chain     vugrl1
     c                   if        not %found
     c                   eval      *in46 = '1'
     c                   exsr      xc1msg
     c                   goto      c2taga
     c                   else
     c                   eval      c2utxt = vgutxt
     c                   endif
     c                   endif
      *
      * Test om varenummer  finnes
      *
     c                   if        c2fvar <> *blank
     c                   movel     c2fvar        vvarl1_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   eval      *in46 = '1'
     c                   exsr      xc1msg
     c                   goto      c2taga
     c                   else
     c                   eval      c2vtxt = vvtek1
     c                   endif
     c                   endif
      *
      * Faktor må fylles ut
      *
     c                   if        c2ffak = 0
     c                   eval      *in42 = '1'
     c                   exsr      xc1msg
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   vgkfiur
      *
     c                   eval      vgkfiu_ftyp = c2ftyp
     c                   eval      vgkfiu_lage = b2lage
     c                   eval      vgkfiu_flev = c2flev
     c                   eval      vgkfiu_fogr = c2fogr
     c                   eval      vgkfiu_fhgr = c2fhgr
     c                   eval      vgkfiu_fugr = c2fugr
     c                   eval      vgkfiu_flev = c2flev
     c                   movel     c2fvar        vgkfiu_fvar
     c     vgkfiu_key    chain     vgkfiu
      *
     c                   if        %found
     c                   eval      vgfirm = w_firm
     c                   eval      vgftyp = c2ftyp
     c                   eval      vgflev = c2flev
     c                   eval      vgfogr = c2fogr
     c                   eval      vgfhgr = c2fhgr
     c                   eval      vgfugr = c2fugr
     c                   movel     c2fvar        vgfvar
     c                   eval      vgffak = c2ffak
     c                   time                    vgfdat
     c                   eval      vgfusr = l_user
     c                   update    vgkfiur
     c                   else
     c                   eval      vgfirm = w_firm
     c                   eval      vgftyp = c2ftyp
     c                   eval      vgflag = b2lage
     c                   eval      vgflev = c2flev
     c                   eval      vgfogr = c2fogr
     c                   eval      vgfhgr = c2fhgr
     c                   eval      vgfugr = c2fugr
     c                   movel     c2fvar        vgfvar
     c                   eval      vgffak = c2ffak
     c                   time                    vgfdat
     c                   eval      vgfusr = l_user
     c                   write     vgkfiur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1ftyp = vgftyp
     c                   endif
     c                   eval      b_forn = *on
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      vgkfiu_ftyp = b1ftyp
     c                   eval      vgkfiu_lage = b2lage
     c                   eval      vgkfiu_flev = b1flev
     c                   eval      vgkfiu_fogr = b1fogr
     c                   eval      vgkfiu_fhgr = b1fhgr
     c                   eval      vgkfiu_fugr = b1fugr
     c                   movel     b1fvar        vgkfiu_fvar
     c     vgkfiu_key    chain     vgkfiu
      *
     c     d1tagb        tag
      *
     c                   eval      d1ftyp = b1ftyp
     c                   eval      d1flag = b1flag
     c                   eval      d1flev = b1flev
     c                   eval      d1fnav = b1fnav
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
      * Sletter rad
      *
     c                   eval      vgkfiu_ftyp = b1ftyp
     c                   eval      vgkfiu_lage = b2lage
     c                   eval      vgkfiu_flev = b1flev
     c                   eval      vgkfiu_fogr = b1fogr
     c                   eval      vgkfiu_fhgr = b1fhgr
     c                   eval      vgkfiu_fugr = b1fugr
     c                   movel     b1fvar        vgkfiu_fvar
      *
     c     vgkfiu_key    chain     vgkfiur
      *
     c                   delete    vgkfiur
      *
     c                   eval      b_forn = *on
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      vgkfir_ftyp = b1ftyp
     c                   eval      vgkfir_lage = b2lage
     c                   eval      vgkfir_flev = b1flev
     c                   eval      vgkfir_fogr = b1fogr
     c                   eval      vgkfir_fhgr = b1fhgr
     c                   eval      vgkfir_fugr = b1fugr
     c                   movel     b1fvar        vgkfir_fvar
     c     vgkfir_key    chain     vgkfir
      *
      * Fyller kopierings-bildet
      *
     c                   eval      k1ftyf = b1ftyp
     c                   eval      k1ftyt = *blank
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkd
     c                   call      'VG250R'
     c                   parm                    w_firm
     c                   parm                    k1ftyt
     c                   goto      k1taga
     c                   endif
      *
      *
      * Sett av feilindikator
      *
     c                   eval      *in46 = *off
     c                   eval      *in45 = *off
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1ftyt = ' '
     c                   eval      *in45 = *on
     c                   exsr      xc1msg
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      vgkfiu_ftyp = k1ftyt
     c     vgkfiu_key    chain     vgkfiu
     c                   if        %found
     c                   eval      *in46 = *on
     c                   exsr      xc1msg
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      b_kopi = *on
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      vgkfi1
      *
     c                   if        %eof or
     c                             vgfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
     c                   if        vgflag <> 0 and
     c                             vgflag <> b2lage
     c                   goto      les_forbi
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
      *
     c                   eval      b1ftyp = vgftyp
     c                   eval      b1flev = vgflev
     c                   eval      b1flag = vgflag
      * Finne leverandørnavn
     C                   eval      rlevl1_levr = vgflev
     c                   movel     *blanks       b1fnav
      *
     c                   if        vgflev <> 0
     c     rlevl1_key    chain     rlevl1R
     c                   if        %found
     c                   movel     rlnavn        b1fnav
     c                   endif
     c                   endif
     c                   eval      b1fogr = vgfogr
     c                   eval      b1fhgr = vgfhgr
     c                   eval      b1fugr = vgfugr
     c                   movel     vgfvar        b1fvar
     c                   eval      b1ffak = vgffak
      *
      *
     c                   write     b1sfl
      *
     c     les_forbi     tag
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     vgkfi1_key    setgt     vgkfi1
     c                   readp     vgkfi1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     vgkfi1
      *
     c                   if        %eof or
     c                             vgfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   enddo
      *
     c                   if        %eof
     c     vgkfi1_key    setll     vgkfi1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for vedlikehold av kostprisfaktorer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     upd_fakt      begsr
      *
     c                   call      'VG210R'
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å hente inn faktorer fra excel
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_excel     begsr
      *
     c                   call      'VG810R'
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å søke på leverandør eller lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sok           begsr
      *
     c                   if        w_afld = 'B2LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    b2ldor
     c                   parm                    b2lena
     c                   endif
      *
     c                   if        w_afld = 'B2LAGE'
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    b2lage
     c                   parm                    b2lana
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent tekster når linje skal vedlikeholdes/Se på
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_txt      begsr
      *
      * Leverandørnavn
      *
     c                   eval      rlevl1_levr = b1flev
     c     rlevl1_key    chain     rlevl1                             91
     c                   if        *in91 = *off
     c                   eval      c2ltxt = rlnavn
     c                   else
     c                   move      *blanks       c2ltxt
     c                   endif
      *
      * Overgruppe
      *
     c                   eval      vogrl1_oogr = b1fogr
     c     vogrl1_key    chain     vogrl1                             91
     c                   if        *in91 = *off
     c                   eval      c2otxt = vgotxt
     c                   else
     c                   move      *blanks       c2otxt
     c                   endif
      *
      * Hovedgruppe
      *
     c                   eval      vhgrl1_hogr = b1fogr
     c                   eval      vhgrl1_hhgr = b1fhgr
     c     vhgrl1_key    chain     vhgrl1                             91
     c                   if        *in91 = *off
     c                   eval      c2htxt = vghtxt
     c                   else
     c                   move      *blanks       c2htxt
     c                   endif
      *
      * Undergruppe
      *
     c                   eval      vugrl1_uogr = b1fogr
     c                   eval      vugrl1_uhgr = b1fhgr
     c                   eval      vugrl1_uugr = b1fugr
     c     vugrl1_key    chain     vugrl1                             91
     c                   if        *in91 = *off
     c                   eval      c2utxt = vgutxt
     c                   else
     c                   move      *blanks       c2utxt
     c                   endif
      *
      * Varenummer
      *
     c                   movel     b1fvar        vvarl1_vare
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *off
     c                   eval      c2vtxt = vvtek1
     c                   else
     c                   move      *blanks       c2vtxt
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på faktortype / update file
      *
     c     vgkfiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vgkfiu_lage
     c                   kfld                    vgkfiu_flev
     c                   kfld                    vgkfiu_ftyp
     c                   kfld                    vgkfiu_fogr
     c                   kfld                    vgkfiu_fhgr
     c                   kfld                    vgkfiu_fugr
     c                   kfld                    vgkfiu_fvar
      *
      *
     c     vgkfir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vgkfir_lage
     c                   kfld                    vgkfir_flev
     c                   kfld                    vgkfir_ftyp
     c                   kfld                    vgkfir_fogr
     c                   kfld                    vgkfir_fhgr
     c                   kfld                    vgkfir_fugr
     c                   kfld                    vgkfir_fvar
      *
      * Key for posisjonering på faktortype / crt_sub
      *
     c     vgkfi1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vgkfi1_lage
     c                   kfld                    vgkfi1_flev
     c                   kfld                    vgkfi1_ftyp
     c                   kfld                    vgkfi1_fogr
     c                   kfld                    vgkfi1_fhgr
     c                   kfld                    vgkfi1_fugr
     c                   kfld                    vgkfi1_fvar
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for posisjonering på varenummer
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for kostpris parameter
      *
     c     vgkkir_key    klist
     c                   kfld                    w_firm
      *
      * Key for posisjonering på faktortype
      *
     c     vgktir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vgktir_ftyp
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Slår opp i koderegister og henter standardverdier
      *
     c     vgkkir_key    chain     vgkkir
      *
     c                   if        vgprlg = 'N'
     c                   eval      *in35 = *on
     c                   else
     c                   eval      *in35 = *off
     c                   endif
      *
     c                   eval      b2lage = 0
     c                   eval      w_lage = 0
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      vgkfi1_lage = b2lage
     c                   eval      vgkfi1_flev = 0
     c     vgkfi1_key    setll     vgkfi1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
