     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RT010R
      * Beskrivelse . . : Danne total kontoplan
      *
      *                   Legger på firma- og kontonavn
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       060804 HFL  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frhovl1    if   e           k disk    rename(rhovpfr:rhovl1r)
     Frtotl1    uf   e           k disk    rename(rtotpfr:rtotl1r)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d lcnavn                951    980
      *
      * Definering av variabler i nøkler
      *
     d rhovl1_kont     s                   like(rhkont)
     d rhovl1_spes     s                   like(rhspes)
     d rhovl1_avde     s                   like(rhavde)
      *
     d w_firm          s                   like(rhfirm)
     d wkonto          s                   like(rhkont)
      *
      *   RENAME AV FELTER I RTOTL1
      *----------------------------------------------------------------
     IRTOTL1R
     I              FIRMA                       TOFIRM
     I              KONTO                       TOKONT
      *
      *------------------------------------------------------------------*
      *   HOVEDPROGRAM                                                   *
      *------------------------------------------------------------------*
      *
     C     *IN50         DOWEQ     *OFF
     C                   READ      RTOTL1R                                50
     C     *IN50         IFEQ      *OFF
      *
      *  CHAIN KONTOPLAN VED BRUDD KONTO
      *
     C     TOKONT        IFNE      WKONTO
     c                   eval      rhovl1_kont = tokont
     c                   eval      rhovl1_spes = *zeros
     c                   eval      rhovl1_avde = *zeros
     c     rhovl1_key    chain     rhovl1                             90
     C                   MOVE      TOKONT        WKONTO
     C                   END                                                    TOKONT
      *
     C                   MOVE      lcnavn        FINAVN
     C                   MOVE      rhtext        KONAVN
     c                   eval      refnr  = rhrefn
     c                   eval      reftxt = rhreft
     C                   UPDATE    RTOTL1R
     C                   ENDIF                                                  <IN50
     C                   ENDDO                                                  <IN50
      *
     C                   MOVE      '1'           *INLR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
     c     rhovl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovl1_kont
     c                   kfld                    rhovl1_spes
     c                   kfld                    rhovl1_avde
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
