     h datedit(*dmy.) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RH961R                                        *
      * Beskrivelse . . : Hovedbok - fjerne MVA-merke/periode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign *
      * --------- ------  ----------------------------------------------*
6.10  * V6.10     210909  Lagt inn sporingsfelter                  BSA  *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
     frhtrl1    uf   e           k disk    rename(rhtrpfr:rhtrl1r)
      *
      * Definering av Local data area
      *
     d                uds
     d rhpkda                300    305  0
     d rhprÅr                         4  0
     d rhpmva                         1  0
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d rhtrl1_raar     s                   like(rbraar)
      *
     d w_firm          s                   like(rbfirm)
      *
      * Les hovedboks-transaksjoner
      *
     c                   eval      w_firm = l_firm                                       .....
     c                   eval      rhtrl1_raar = rhprÅr                                  .....
     c     rhtrl1_key    setll     rhtrl1r                                60
     c     rhtrl1_key    reade     rhtrl1r                                60
      *
     c                   dow       *in60 = *off                                          .....
      *
     c                   if        rbmvap = rhpmva                                           .
     c                   eval      rbmvap = 0                                                .
     c                   eval      rbmmrk = ' '                                              .
6.10 c                   time                    rbedat
6.10 c                   time                    rbetim
6.10 c                   eval      rbeusr = l_user
     c                   update    rhtrl1r                                                   .
     c                   endif                                                           .....
      *
     c     rhtrl1_key    reade     rhtrl1r                                60
     c                   enddo                                                           .....
      *
     c     slutt         tag
      *
     c                   move      *on           *inlr                                       .
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på regnskapsår
      *
     c     rhtrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhtrl1_raar
      *
     c                   endsr
