     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RF020R                                              *
      * Beskrivelse . . : Vedlikehold av notatark                             *
      *                                                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
T5.20 * V5.00 211003 HFL  Konto-/kunde-/lev.-navn på notatbildet
T5.40 * V5.40 150605 HFL  Leser aklaL2 i stedet for aklaPF.
T5.40 * V5.40 270705 HFL  Notatark på bilagsnr.
T5.40 * V5.40 061205 HFL  Feil i bla'ing.
6.10  * V6.10 210909 BSA  Lagt inn sporingsfelter
8.00  *       270821 sst  Sperre notat for skriving på kunder 'Sperr_Notat_Kunde'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *                                                                   *
      *  BRUK AV INDIKATORER                                              *
      *                                                                   *
      *       KC = F3  = Exit                                             *
      *       KE = F5  = Forny                                            *
      *       KI = F09 = Kopiere inn standard tekst                       *
      *       KJ = F10 = Standard tekst                                   *
      *       KL = F12 = Annuller                                         *
      *                                                                   *
      *       LR = Avslutt program                                        *
      *       10 = Roll                                                   *
      *       12 = Sfldsp                                                 *
      *       13 = Sfldspctl                                              *
      *       14 = Sflclr                                                 *
      *       15 = Sflend                                                 *
      *       16 = Readc 16 on, no more records                           *
      *       30 = Protect av felter ved vise                             *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer               *
      *    60-69 = Fileindikatorer chain etc.                             *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                       *
      *    80-89 = Arbeidsindikatorer ordinære                            *
      *    90-99 = Benyttes ved std. sub-rutiner                          *
      *                                                                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
E5.40faklal2    if   e           k disk    rename(aklapfr:aklal2r)
T5.40faklal3    if   e           k disk    rename(aklapfr:aklal3r)
     faklalu    uf a e           k disk    rename(aklapfr:aklalur)
T5.20fraa1l1    if   e           k disk    rename(raa1pfr:raa1l1r)
T5.20frhovl1    if   e           k disk    rename(rhovpfr:rhovl1r)
T5.20frkunlr    if   e           k disk    rename(rkunpfr:rkunlrr)
T5.20frlevlr    if   e           k disk    rename(rlevpfr:rlevlrr)
T5.40frp1pl1    if   e           k disk    rename(rp1ppfr:rp1pl1r)
     frf020d    cf   e             workstn
     f                                     sfile(b1sfl:srrn01)
      *
     d mld             s             18    dim(7) ctdata perrcd(1)
T5.20d rhovl1_kont     s                   like(rhkont)
T5.20d rhovl1_spes     s                   like(rhspes)
T5.20d rhovl1_avde     s                   like(rhavde)
T5.20d rkunlr_kund     s                   like(rkkund)
T5.20d rlevlr_levr     s                   like(rllevr)
T5.20d w_firm          s                   like(l_firm)
T5.40d rp1pl1_frm      s                   like(rp1frm)
T5.40d rp1pl1_pro      s                   like(rp1pro)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
8.00 d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d b1vala          s              2
     d b1valb          s              1
     d f10std          s              4
     d gmakti          s                   like(rxakti)
     d gmavde          s                   like(rxavde)
     d gmkont          s                   like(rxkont)
     d gmpros          s                   like(rxpros)
     d gmspes          s                   like(rxspes)
T5.40d gmtist          s                   like(rxtist)
     d rxupar          s                   like(rxulda)
     d sfrn01          s                   like(srrn01)
     d slod01          s              1  0
     d spge01          s              3  0
     d srrn01          s              4  0
     d ssrn01          s                   like(srrn01)
     d stakti          s                   like(rxakti)
     d stavde          s                   like(rxavde)
     d stkont          s                   like(rxkont)
     d stpros          s                   like(rxpros)
     d stspes          s                   like(rxspes)
T5.40d sttist          s                   like(rxtist)
     d wactli          s              2  0
     d wactpo          s              3  0
     d wpakti          s                   like(rxakti)
     d wpantl          s              1  0
     d wpavde          s                   like(rxavde)
     d wpfirm          s                   like(rxfirm)
     d wpkont          s                   like(rxkont)
T5.40d wptist          s                   like(rxtist)
     d wplinj          s                   like(rxlinj)
     d wplin2          s                   like(rxlinj)
     d wppros          s                   like(rxpros)
     d wprenu          s              4
     d wpspes          s                   like(rxspes)
     d wpsyst          s                   like(rxsyst)
      *
     i/COPY QCPYLESRC,RF020PAR
      /eject
8.00  *
8.00  * Eksternt program
8.00   dcl-s co402_verdi1  char(1);
8.00   dcl-s co402_verdi2  char(2048);
8.00   DCL-PR CO402R EXTPGM('CO402R');
8.00     p_filgrp            char(3)     const;
8.00     p_firm              packed(3:0) const;
8.00     p_lager             packed(2:0) const;
8.00     p_nokkel            char(50)    const;
8.00     p_verdi1            char(1);
8.00     p_verdi2            char(2048);
8.00   END-PR;
7.02 d w_spr_not_kund  s              1    inz(*off)
8.00  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av felter                                              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Foaklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      *     nn - er 01 for subfile 01,
      *             02 for subfile 02,
      *             03 for subfile 03, o.s.v.
      * SRRNnn - Relativt recordnummer i subfile
      * SPGEnn - Sidestørrelse i subfile (page)
      * SFRNnn - Første recordnummer på siden
      * SSRNnn - Siste  recordnummer på siden
      * SVRNnn - Vis den side som inneholder denne record.
      * SLODnn - er et statustfelt, som styrer om det er behov
      *          for å loade subfil pånytt eller ikke.
      *
      * Foaklaring på spesielle felter som brukes
      * i programmet forøvrig.
      * - - - - - - - - - - - - - - - - -
      *
      * F10STD - Angir om man er i notatarket eller standardtekst.
      *          I programmet er det brukt betegnelsen MODUS.
      * WPANTL - Angir antall nye linjer som skal settes inn.
      * WPRENU - Statusfelt som angir om renummerering skal kjøres.
      *          Aktuelt dersom det er lagt til eller fjernet poster.
      * WPLIN2 - Brukes i sammenheng med utvalg på grupperingstyper,
      *          og inneholder linjenummer for siste posten i nøkkelen.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Eget skjermbilde for valg av grupperingstype.
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * B2CTL Behandle åpningsbildet med subfile                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      xdsp01
      *
      * Grupperingsvalg.
      *
     c                   if        *inkb = *on
     c                   exfmt     c1bld
     c                   movel     c1tgrp        b2tgrp
      *
      * Leser den siste posten på notatarket.                    .
      *
T5.40c                   if        *in88 = *on
E5.40c     key02         setgt     aklal2r
E5.40c                   readp     aklal2r                                15
T5.40c                   else
T5.40c     aklal3_key    setgt     aklal3r
T5.40c                   readp     aklal3r                                15
T5.40c                   endif
      *
     c                   if        *in15 = *off
     c                   eval      wplin2 = rxlinj
     c                   else
     c                   eval      wplin2 = 0
     c                   endif
     c                   exsr      xforny
     c                   endif
      *
      * Avslutt program
      *
     c                   if        *inkc = *on
      *
     c                   if        wprenu = '*JA '
     c                   exsr      xrenum
     c                   endif
      *                                                          .
      * Dersom std.tekst er kalt opp må opprinnelige parameter-  .
      * verdier legges tilbake før det avsluttes:                .
      *                                                          .
     c                   if        f10std = '*JA '
     c                   eval      wppros = gmpros
     c                   movel     gmakti        wpakti
     c                   eval      wpkont = gmkont
     c                   eval      wpspes = gmspes
     c                   eval      wpavde = gmavde
T5.40c                   eval      wptist = gmtist
     c                   eval      wplinj = 0
     c                   endif
      *                                                          .
     c                   goto      xslutt
     c                   endif
      *
      * Forny
      *
     c                   if        *inke = *on
     c                   exsr      xforny
     c                   goto      b2tagb
     c                   endif
      *
      *
      * Henter inn standard tekst i notatarket.
      *
     c                   if        *inki = *on
     c                   exsr      xhente
     c                   goto      b2taga
     c                   endif
      *
      * Kaller opp Standardtekst ELLER går tilbake til notatarket.
      * Dersom det har vært gjort endringer i notatarket vil det
      * først bli kjørt renummerering av file.
      *
     c                   if        *inkj = *on
      * renummerering                                          .
     c                   if        wprenu = '*JA '
     c                   exsr      xrenum
     c                   endif
      * til standard tekst                                     .
     c                   if        f10std = *blanks
     c                   exsr      xstand
     c                   goto      b2taga
     c                   endif
      * tilbake til notatark                                   .
     c                   if        f10std = '*JA '
     c                   exsr      xnotat
     c                   goto      b2taga
     c                   endif
      *                                                        .
     c                   endif
      *
      * Roll
      *
     c                   if        *in10 = *on
     c                   exsr      xcrt01
     c                   goto      b2tagb
     c                   endif
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Behandling av subfile                                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Sjekk om valg
      * er foretatt i
      * subfile
      *
     c                   eval      slod01 = *zero
      *
     c                   do        ssrn01
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16 = *on
     c                   leave
     c                   endif
      *                                                                  .
      * Det er gjort endringer i subfile, og renummerering må kjøres.    .
      *                                                                  .
     c                   movel     '*JA '        wprenu
      *                                                                  .
     c                   eval      wpfirm = b1firm
     c                   move      b1syst        wpsyst
     c                   eval      wppros = b1pros
     c                   movel     b1akti        wpakti
     c                   eval      wpkont = b1kont
     c                   eval      wpspes = b1spes
     c                   eval      wpavde = b1avde
T5.40c                   eval      wptist = b1tist
     c                   eval      wplinj = b1linj
      *                                                                  .
      * Oppdaterer diskfile med post i fra subfile dersom                .
      * tekstfeltet er utfylt.                                           .
      *                                                                  .
     c                   if        b1text <> *blanks
     c     key01         chain     aklalu                             60
     c                   eval      rxfirm = b1firm
     c                   move      b1syst        rxsyst
     c                   eval      rxpros = b1pros
     c                   movel     b1akti        rxakti
     c                   eval      rxkont = b1kont
     c                   eval      rxspes = b1spes
     c                   eval      rxavde = b1avde
T5.40c                   eval      rxtist = b1tist
     c                   eval      rxlinj = b1linj
     c                   movel     b1tgrp        rxtgrp
     c                   movel     b1text        rxtext
      *                                                           .      .
      * Tester om det er valgt innsett av ny linje.  I såfall må  .      .
      * de to første posisjonene i teksten "legges tilbake".      .      .
      *                                                           .      .
     c                   movel     b1text        b1vala
      *                                                           .      .
     c                   if        b1vala = 'I1'
     c                             or b1vala = 'i1'
     c                             or b1vala = 'I2'
     c                             or b1vala = 'i2'
     c                             or b1vala = 'I3'
     c                             or b1vala = 'i3'
     c                   movel     b12pos        rxtext
     c                   endif
      *                                                           .      .
6.10 c                   time                    rxedat
6.10 c                   time                    rxetim
6.10 c                   eval      rxeusr = l_user
      *                                                           .      .
     c                   if        *in60 = *off
     c                   update    aklalur
     c                   else
6.10 c                   time                    rxodat
6.10 c                   time                    rxotim
6.10 c                   eval      rxousr = l_user
     c                   write     aklalur
     c                   endif
      *                                                           .      .
     c                   endif
      *                                                                  .
      * Slette post dersom tekstfelt er blankt + setter verdi i          .
      * statusfelt for reload subfile.                                   .
      *                                                                  .
     c                   if        b1text = *blanks
     c                   exsr      xdelet
     c                   eval      slod01 = 1
     c                   endif
      *                                                                  .
      * Sette inn nye linjer i subfile.                                  .
      *                                                                  .
     c                   movel     b1text        b1valb
     c                   if        b1valb = 'I'
     c                             or b1valb = 'i'
     c                   movel     b1text        b1vala
     c                   move      b1vala        b1valb
     c                   if        b1valb = '1'
     c                             or b1valb = '2'
     c                             or b1valb = '3'
      *                                                      . .
      * Da de 2 første posisjonene i teksten er "ødelagt"    . .         .
      * av innsetkoden, så må nå opprinnelig verdi legges    . .         .
      * tilbake.                                             . .         .
      *                                                      . .
     c                   exsr      xinset
     c                   exsr      xrenum
     c                   eval      slod01 = 1
     c                   iter
     c                   endif
     c                   endif
      *                                                                  .
     c                   enddo
      *
      * Load subfile
      * pånytt dersom
      * SLOD01 = 1
      *
     c                   if        slod01 = 1
     c     sfrn01        chain     b1sfl                              60
      *                                                                  .
     c                   eval      wppros = b1pros
     c                   movel     b1akti        wpakti
     c                   eval      wpkont = b1kont
     c                   eval      wpspes = b1spes
     c                   eval      wpavde = b1avde
T5.40c                   eval      wptist = b1tist
      *                                                                  .
T5.40c                   if        *in88 = *on
E5.40c     key02         setll     aklal2r
T5.40c                   else
T5.40c     aklal3_key    setll     aklal3r
T5.40c                   endif
     c                   exsr      xclr01
     c                   exsr      xcrt01
     c                   eval      slod01 = 0
     c                   endif
      *
     c                   goto      b2taga
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine som kalles opp dersom behov for "refresh" av bildet.    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xforny        begsr
      *
     c                   if        sfrn01 <> *zero
     c     sfrn01        chain     b1sfl                              60
     c                   endif
      *
      ** Her må startverdi i KEY01 initieres.
      *
     c                   eval      wplinj = 0
T5.40c                   if        *in88 = *on
E5.40c     key01         setll     aklal2r
T5.40c                   else
T5.40c     key01         setll     aklal3r
T5.40c                   endif
     c                   exsr      xclr01
     c                   exsr      xcrt01
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Sletter post dersom tekstfelt er blanket ut.                      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xdelet        begsr
      *
     c                   eval      wpfirm = b1firm
     c                   move      b1syst        wpsyst
     c                   eval      wppros = b1pros
     c                   movel     b1akti        wpakti
     c                   eval      wpkont = b1kont
     c                   eval      wpspes = b1spes
     c                   eval      wpavde = b1avde
T5.40c                   eval      wptist = b1tist
     c                   eval      wplinj = b1linj
      *
     c     key01         chain     aklalu                             60
      *
     c                   if        *in60 = *off
     c                   delete    aklalur
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * XINSET Behandle innsetting av nye linjer.                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xinset        begsr
      *
      * Antall nye linjer
      *
     c                   if        b1valb = '1'
     c                   eval      wpantl = 1
     c                   endif
      *
     c                   if        b1valb = '2'
     c                   eval      wpantl = 2
     c                   endif
      *
     c                   if        b1valb = '3'
     c                   eval      wpantl = 3
     c                   endif
      *
      * Oppdaterer diskfile med nye poster.
      *
     c                   dou       wpantl = 0
      *                                                        .
     c     key01         chain     aklalu                             60
      *                                                        .
     c                   eval      rxfirm = b1firm
     c                   move      b1syst        rxsyst
     c                   eval      rxpros = b1pros
     c                   movel     b1akti        rxakti
     c                   eval      rxkont = b1kont
     c                   eval      rxspes = b1spes
     c                   eval      rxavde = b1avde
T5.40c                   eval      rxtist = b1tist
     c                   eval      rxtext = *blanks
     c                   movel     '*'           rxtext
     c                   movel     c1tgrp        rxtgrp
     c                   eval      rxlinj = b1linj - wpantl
      *
6.10 c                   time                    rxodat
6.10 c                   time                    rxotim
6.10 c                   eval      rxousr = l_user
6.10 c                   time                    rxedat
6.10 c                   time                    rxetim
6.10 c                   eval      rxeusr = l_user
      *
     c                   write     aklalur
     c                   eval      wpantl = wpantl - 1
     c                   enddo
      *
     c                   eval      b1vala = *blanks
     c                   eval      b1valb = *blanks
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * XHENTE Henter inn i notatark FRA standard tekst.                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xhente        begsr
      *
      * Initierer variable, statusfelt og indikator for å fortelle
      * at gjeldende modus skal være NOTATARK uavhengig av tidligere
      * modus.
      *
      * GMKONT inneholder kontonummeret forepsørselen gjelder for,
      * men vil ikke inneholde verdi dersom standardtekst ikke har
      * vært kalt opp.
      *
     c                   if        gmkont <> 0
     c                   eval      wppros = gmpros
     c                   movel     gmakti        wpakti
     c                   eval      wpkont = gmkont
     c                   eval      wpspes = gmspes
     c                   eval      wpavde = gmavde
T5.40c                   eval      wptist = gmtist
     c                   endif
      *
     c                   movel     mld(5)        b2cf10
     c                   eval      b2stex = *blanks
     c                   move      *off          *in33
      *
     c                   eval      f10std = *blanks
      *
      * Leser den siste posten på notatarket.
      *
T5.40c                   if        *in88 = *on
E5.40c     key02         setgt     aklal2r
E5.40c                   readp     aklal2r                                15
T5.40c                   else
T5.40c     aklal3_key    setgt     aklal3r
E5.40c                   readp     aklal3r                                15
T5.40c                   endif
      *
     c                   if        *in15 = *on
     c                   eval      wplinj = 0
     c                   else
     c                   eval      wplinj = rxlinj
     c                   endif
      *
      * Leser inn første post fra standard tekst.
      *
T5.40c                   if        *in88 = *on
E5.40c     key03         setll     aklal2r
E5.40c     key03         reade     aklal2r                                15
T5.40c                   else
T5.40c     aklal3_key    setll     aklal3r
E5.40c     aklal3_key    reade     aklal3r                                15
T5.40c                   endif
      *
      * Kopierer standardtekst over til notatarket.  Kun linjer som
      * har grupperingskode lik det som er valgt blir tatt med over.
      * Dersom det er valgt blank i grupperingskode blir alle linjer
      * kopiert i fra standardtekst over til notatarket.
      *
     c                   dow       *in15 = *off
     c                   if        c1tgrp = *blanks
     c                             or c1tgrp = rxtgrp
     c                   eval      wplinj = wplinj + 10
     c                   eval      rxpros = wppros
     c                   movel     wpakti        rxakti
     c                   eval      rxkont = wpkont
     c                   eval      rxspes = wpspes
     c                   eval      rxavde = wpavde
T5.40c                   eval      rxtist = wptist
     c                   eval      rxlinj = wplinj
     c                   movel     c1tgrp        rxtgrp
6.10  *
6.10 c                   time                    rxodat
6.10 c                   time                    rxotim
6.10 c                   eval      rxousr = l_user
6.10 c                   time                    rxedat
6.10 c                   time                    rxetim
6.10 c                   eval      rxeusr = l_user
6.10  *
     c                   write     aklalur
     c                   endif
T5.40c                   if        *in88 = *on
E5.40c     key03         reade     aklal2r                                15
T5.40c                   else
T5.40c     aklal3_key    reade     aklal3r                                15
T5.40c                   endif
     c                   enddo
      *
      * Tømmer- og fyller opp subfile på nytt.
      *
T5.40c                   if        *in88 = *on
E5.40c     key02         setll     aklal2r
T5.40c                   else
T5.40c     aklal3_key    setll     aklal3r
T5.40c                   endif
      *
     c                   exsr      xclr01
     c                   exsr      xcrt01
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * XSTAND Vedlikehold av standardtekster.                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xstand        begsr
      *
      * Lagrer gjeldende verdier.
      *
     c                   eval      gmpros = b1pros
     c                   movel     b1akti        gmakti
     c                   eval      gmkont = b1kont
     c                   eval      gmspes = b1spes
     c                   eval      gmavde = b1avde
T5.40c                   eval      gmtist = b1tist
      *
      * Initierer key nye verdier for standard tekst.
      *
     c                   eval      wppros = *blank
     c                   eval      wpakti = *blank
     c                   eval      wpkont = 0
     c                   eval      wpspes = 0
     c                   eval      wpavde = 0
T5.40c                   eval      wptist = *loval
     c                   eval      wplinj = 0
      *
      * Initierer skjermvariable og skjermindikator.
      *
     c                   movel     mld(6)        b2cf10
     c                   movel     mld(7)        b2stex
     c                   move      *on           *in33
      *
      * Initierer statusfelt som forteller at gjeldende modus
      * skal være endring av standard tekst.
      *
     c                   movel     '*JA '        f10std
      *
      * Initierer skjermvariable og skjermindikator.
      *
T5.40c                   if        *in88 = *on
E5.40c     key03         setll     aklal2r
T5.40c                   else
T5.40c     aklal3_key    setll     aklal3r
T5.40c                   endif
      *
     c                   exsr      xclr01
     c                   exsr      xcrt01
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * XNOTAT Oppkall av notatark etter å ha vært i standardtekst.       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xnotat        begsr
      *
      * Legger tilbake verdier.
      *
     c                   eval      wppros = gmpros
     c                   movel     gmakti        wpakti
     c                   eval      wpkont = gmkont
     c                   eval      wpspes = gmspes
     c                   eval      wpavde = gmavde
T5.40c                   eval      wptist = gmtist
     c                   eval      wplinj = 0
      *
      * Initierer skjermvariable og indikator.
      *
     c                   movel     mld(5)        b2cf10
     c                   eval      b2stex = *blanks
     c                   move      *off          *in33
      *
      * Initierer statusfelt som forteller at gjeldende modus
      * skal være endring av notatark.
      *
     c                   eval      f10std = *blanks
      *
      * Initierer skjermvariable og skjermindikator.
      *
T5.40c                   if        *in88 = *on
E5.40c     key02         setll     aklal2r
T5.40c                   else
T5.40c     aklal3_key    setll     aklal3r
T5.40c                   endif
      *
     c                   exsr      xclr01
     c                   exsr      xcrt01
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Tøm subfile                                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xclr01        begsr
     c                   move      *on           *in14
     c                   write     b2ctl
     c                   move      *off          *in14
      *
     c                   move      *off          *in15
     c                   eval      srrn01 = 0
     c                   eval      sfrn01 = 0
     c                   eval      ssrn01 = 0
     c                   eval      spge01 = 0
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Fyll opp subfile                                                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xcrt01        begsr
E5.40c                   eval      spge01 = spge01 + 14
     c                   eval      srrn01 = ssrn01
      *
     c                   dou       srrn01 = spge01
T5.40c                   if        *in88 = *on
E5.40c     key02         reade     aklal2r                                15
T5.40c                   else
T5.40c     aklal3_key    reade     aklal3r                                15
T5.40c                   endif
      *                                                                 .
      * Leser alle poster som oppfyller nøkkelbegrepet.  *IN15 er       .
      * *ON ved EOF.  Dersom modus ikke er standardtekst, så skal       .
      * kun linjer som er lik C1TGRP vises, såfremt ikke feltet         .
      * er blankt.                                                      .
      *                                                                 .
     c                   if        *in15 = *off
      *                                                              .  .
     c                   if        c1tgrp <> *blanks
     c                   if        c1tgrp <> rxtgrp
     c                   iter
     c                   endif
     c                   endif
      *                                                              .  .
     c                   eval      wplinj = rxlinj
      *                                                              .  .
      * Gjemte outputfelter i subfile.                               .  .
      *                                                              .  .
     c                   eval      b1firm = rxfirm
     c                   move      rxsyst        b1syst
     c                   eval      b1pros = rxpros
     c                   movel     rxakti        b1akti
     c                   eval      b1kont = rxkont
     c                   movel     rxspes        b1spes
     c                   movel     rxavde        b1avde
T5.40c                   movel     rxtist        b1tist
     c                   eval      b1linj = rxlinj
     c                   movel     rxtgrp        b1tgrp
     c                   movel     rxtext        b12pos
      *                                                              .  .
      * Vanlige outputfelter i subfile.                              .  .
      *                                                              .  .
     c                   movel     rxtext        b1text
     c                   eval      srrn01 = srrn01 + 1
     c                   write     b1sfl
     c                   endif
      *                                                                 .
      * Fyller opp med tomme linjer.                                    .
      *                                                                 .
     c                   if        *in15 = *on
      *                                                                 .
     c                   if        wplin2 <> 0 and
     c                             c1tgrp <> *blanks
     c                   eval      wplinj = wplin2
     c                   eval      wplin2 = 0
     c                   endif
      *                                                              .  .
     c                   eval      wplinj = wplinj + 10
      *                                                              .  .
      * Gjemte outputfelter i subfile.                               .  .
      *                                                              .  .
     c                   eval      b1firm = wpfirm
     c                   move      wpsyst        b1syst
     c                   eval      b1pros = wppros
     c                   movel     wpakti        b1akti
     c                   eval      b1kont = wpkont
     c                   movel     wpspes        b1spes
     c                   movel     wpavde        b1avde
T5.40c                   move      wptist        b1tist
     c                   eval      b1linj = wplinj
     c                   movel     c1tgrp        b1tgrp
      *                                                              .  .
      * Vanlige outputfelter i subfile.                              .  .
      *                                                              .  .
     c                   eval      b1text = *blanks
     c                   eval      b12pos = *blanks
     c                   eval      srrn01 = srrn01 + 1
     c                   write     b1sfl
     c                   endif
      *                                                                 .
     c                   enddo
      *
     c     xend01        tag
      *
     c                   if        srrn01 > ssrn01
     c                   eval      sfrn01 = ssrn01 + 1
     c                   endif
      *
     c                   eval      ssrn01 = srrn01
     c                   eval      w_virn = sfrn01
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Display subfile                                                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xdsp01        begsr
      *
     c                   if        srrn01 <> 0
     c                   move      *on           *in12
     c                   endif
      *
     c                   move      *on           *in13
     c                   exfmt     b2ctl
     c                   move      *off          *in12
     c                   move      *off          *in13
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * XRENUM Renummerering av diskfile m.h.p. linjenummer lik 10,20..   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xrenum        begsr
      *
      * Tømmer subfile.
      *
     c                   exsr      xclr01
      *
      * Fyller opp subfile med poster i fra diskfile.
      *
     c     key02         setll     aklalu
      *
     c                   eval      wplinj = 0
     c                   eval      srrn01 = 0
      *
     c     key02         reade     aklalu                                 15
      *
     c                   dow       *in15 = *off
      *                                                        .
     c                   eval      wplinj = wplinj + 10
     c                   eval      srrn01 = srrn01 + 1
      *                                                        .
     c                   eval      b1firm = rxfirm
     c                   move      rxsyst        b1syst
     c                   eval      b1pros = rxpros
     c                   movel     rxakti        b1akti
     c                   eval      b1kont = rxkont
     c                   movel     rxspes        b1spes
     c                   movel     rxavde        b1avde
T5.40c                   move      rxtist        b1tist
     c                   eval      b1linj = wplinj
     c                   movel     rxtext        b1text
     c                   movel     rxtext        b12pos
     c                   movel     rxtgrp        b1tgrp
      *                                                        .
      * Oppdater subfile.                                      .
      *                                                        .
     c                   write     b1sfl
      *                                                        .
      * Slett post fra disk file.                              .
      *                                                        .
     c                   delete    aklalur
      *                                                        .
      * Hent neste post fra disk file.                         .
      *                                                        .
     c     key02         reade     aklalu                                 15
      *                                                        .
     c                   enddo
      *
      * Fyller opp diskfile med poster i fra subfile.
      *
     c                   eval      wplinj = srrn01
     c                   dow       wplinj >= 1
     c     wplinj        chain     b1sfl                              16
     c                   if        *in16 = *off
     c                   eval      rxfirm = b1firm
     c                   move      b1syst        rxsyst
     c                   eval      rxpros = b1pros
     c                   movel     b1akti        rxakti
     c                   eval      rxkont = b1kont
     c                   movel     b1spes        rxspes
     c                   movel     b1avde        rxavde
T5.40c                   move      b1tist        rxtist
     c                   eval      rxlinj = wplinj * 10
     c                   movel     b1text        rxtext
     c                   movel     b1tgrp        rxtgrp
      * Oppdater disk file.                                    .  .
6.10 c                   time                    rxodat
6.10 c                   time                    rxotim
6.10 c                   eval      rxousr = l_user
6.10 c                   time                    rxedat
6.10 c                   time                    rxetim
6.10 c                   eval      rxeusr = l_user
      *
     c                   write     aklalur
     c                   endif
     c                   eval      wplinj = wplinj - 1
     c                   enddo
      *
     c                   eval      wprenu = *blanks
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   eval      wpantl = 0
     c                   eval      slod01 = 0
     c                   eval      spge01 = 0
     c                   eval      srrn01 = 0
     c                   eval      wactli = 0
     c                   eval      wactpo = 0
      *
      * Initierer hjelpefelt som skal fortelle om det skal settes inn
      * ekstra linjer.
      *
     c                   eval      b1vala = *blanks
     c                   eval      b1valb = *blanks
      *
      * Legger inn verdi i kommandotastbildet + initierer variabel som
      * skal fortelle om standard tekst vises (har da verdien '*ja ').
      *
     c                   movel     mld(5)        b2cf10
     c                   eval      f10std = *blanks
     c                   eval      wprenu = *blanks
      *
      * Kontroll av valg som er gjort før inngang til program
      *
     c                   move      rxupar        rxulda
     c                   move      *off          *in30
     c                   if        rxvalg = '5'
     c                   move      *on           *in30
     c                   endif
      *
      * Klargjør diverse felter ved inngang til program
     c                   move      rxfirm        wpfirm
     c                   move      rxsyst        wpsyst
E5.40c                   move      rxpros        wppros
     c                   move      rxakti        wpakti
     c                   move      rxkont        wpkont
     c                   move      rxspes        wpspes
     c                   move      rxavde        wpavde
T5.40c                   move      rxtist        wptist
     c                   move      rxpros        stpros
     c                   move      rxakti        stakti
S5.00c*                  move      rxkont        stkont
T5.00c                   move      *zeros        stkont
     c                   move      rxspes        stspes
     c                   move      rxavde        stavde
T5.40c                   move      rxtist        sttist
     c                   eval      wplinj = *zeros
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    rxupar
      *
T5.40 * Leser aklaL2 hvis kontonivå
T5.40 * og    aklaL3 hvis postnivå
      *
T5.40c                   if        rxtist = *loval
T5.40c                   eval      *in88 = *on
T5.40c                   endif
      *
      * Key for oppdatering av file notatark
      *
     c     key01         klist
     c                   kfld                    wpfirm
     c                   kfld                    wpsyst
     c                   kfld                    wppros
     c                   kfld                    wpakti
     c                   kfld                    wpkont
     c                   kfld                    wpspes
     c                   kfld                    wpavde
T5.40c                   kfld                    wptist
     c                   kfld                    wplinj
      *
T5.40 * Key for les av file notatark kontonivå
      *
     c     key02         klist
     c                   kfld                    wpfirm
     c                   kfld                    wpsyst
     c                   kfld                    wppros
     c                   kfld                    wpakti
     c                   kfld                    wpkont
     c                   kfld                    wpspes
     c                   kfld                    wpavde
      *
T5.40 * Key for oppdatering av file notatark postnivå
      *
T5.40c     aklal3_key    klist
T5.40c                   kfld                    wpfirm
T5.40c                   kfld                    wpsyst
T5.40c                   kfld                    wppros
T5.40c                   kfld                    wpakti
T5.40c                   kfld                    wpkont
T5.40c                   kfld                    wpspes
T5.40c                   kfld                    wpavde
T5.40c                   kfld                    wptist
      *
      * Key for les av standard tekst.
      *
     c     key03         klist
     c                   kfld                    wpfirm
     c                   kfld                    wpsyst
     c                   kfld                    stpros
     c                   kfld                    stakti
     c                   kfld                    stkont
     c                   kfld                    stspes
     c                   kfld                    stavde
      *
T5.20 * Key for statusrecord
      *
T5.20c     raa1l1_key    klist
T5.20c                   kfld                    w_firm
      *
T5.20 * Key for oppslag på hovedbok
      *
T5.20c     rhovl1_key    klist
T5.20c                   kfld                    w_firm
T5.20c                   kfld                    rhovl1_kont
T5.20c                   kfld                    rhovl1_spes
T5.20c                   kfld                    rhovl1_avde
      *
T5.20 * Key for oppslag på kunde
      *
T5.20c     rkunlr_key    klist
T5.20c                   kfld                    w_firm
T5.20c                   kfld                    rkunlr_kund
      *
T5.20 * Key for oppslag på leverandør
      *
T5.20c     rlevlr_key    klist
T5.20c                   kfld                    w_firm
T5.20c                   kfld                    rlevlr_levr
      *
T5.40 * Key for posisjonering på prosjekt
      *
T5.40c     rp1pl1_key    klist
T5.40c                   kfld                    rp1pl1_frm
T5.40c                   kfld                    rp1pl1_pro
      *
T5.20c                   eval      w_firm = l_firm
      *
      * Slår opp status-record
      *
T5.20c     raa1l1_key    chain     raa1l1r                            91
T5.20c                   if        %found
      *                                                                 .
T5.40 * Sjekke om prosjekt                                              .
      *                                                                 .
T5.40c                   if        rxpros <> *blank
T5.40c                   eval      rp1pl1_frm  = w_firm
T5.40c                   eval      rp1pl1_pro  = rxpros
T5.40c     rp1pl1_key    chain     rp1pl1                             91
T5.20c                   if        %found
T5.20c                   eval      b2kl   = 'Prosjektnr...:'
T5.40c                   move      rxpros        b2kont
T5.40c                   eval      b2navn = rp1txt
T5.40c                   if        *in88 = *on
T5.40c                   eval      b2bilt = *blank
T5.40c                   eval      b2biln = *zero
T5.40c                   eval      b2pert = *blank
T5.40c                   eval      b2rper = *zero
T5.40c                   eval      b2raar = *zero
T5.40c                   else
T5.40c                   eval      b2bilt = 'Bilag:'
T5.40c                   eval      b2biln = rxbiln
T5.40c                   eval      b2pert = 'Per.:'
T5.40c                   eval      b2rper = rxrper
T5.40c                   eval      b2raar = rxraar
T5.40c                   endif
T5.40c                   endif
T5.40c                   endif
S5.40c*                  else
      *
T5.20 * Sjekke om konto er hovedbok                                     .
      *                                                                 .
T5.20c                   if        rxkont < ra1hkt
T5.20c                   eval      rhovl1_kont = rxkont
T5.20c                   eval      rhovl1_spes = rxspes
T5.20c                   eval      rhovl1_avde = rxavde
T5.20c     rhovl1_key    chain     rhovl1
      *
T5.20c                   if        %found
T5.20c                   eval      b2kl   = 'Kontonr......:'
T5.20c                   move      rxkont        b2kont
T5.20c                   eval      b2navn = rhtext
T5.20c                   eval      b2avdt = 'Avd.:'
T5.20c                   eval      b2avde = rxavde
T5.20c                   eval      b2spet = 'Spes.:'
T5.20c                   eval      b2spes = rhspes
T5.20c                   endif
T5.20c                   else
      *                                                                 .
T5.20 * Sjekke om konto er kunde                                        .
      *                                                                 .
T5.20c                   if        rxkont > ra1hkt and
T5.20c                             rxkont < ra1hrs
      *
T5.20c                   eval      rkunlr_kund = rxkont
T5.20c     rkunlr_key    chain     rkunlr                             91
T5.20c                   if        %found
T5.20c                   eval      b2kl   = 'Kundenr......:'
T5.20c                   move      rxkont        b2kont
T5.20c                   eval      b2navn = rknavn
T5.40c                   if        *in88 = *on
T5.40c                   eval      b2bilt = *blank
T5.40c                   eval      b2biln = *zero
T5.40c                   eval      b2pert = *blank
T5.40c                   eval      b2rper = *zero
T5.40c                   eval      b2raar = *zero
T5.40c                   else
T5.40c                   eval      b2bilt = 'Bilag:'
T5.40c                   eval      b2biln = rxbiln
T5.40c                   eval      b2pert = 'Per.:'
T5.40c                   eval      b2rper = rxrper
T5.40c                   eval      b2raar = rxraar
T5.40c                   endif
T5.20c                   endif
T5.20c                   else
      *                                                                 .
T5.20 * Sjekke om konto er leverandør                                   .
      *                                                                 .
T5.20c                   if        rxkont > ra1hrs
T5.20c                   eval      rlevlr_levr = rxkont
T5.20c     rlevlr_key    chain     rlevlr                             91
T5.20c                   if        %found
T5.20c                   eval      b2kl   = 'Leverandørnr.:'
T5.20c                   move      rxkont        b2kont
T5.20c                   eval      b2navn = rlnavn
T5.40c                   if        *in88 = *on
T5.40c                   eval      b2bilt = *blank
T5.40c                   eval      b2biln = *zero
T5.40c                   eval      b2pert = *blank
T5.40c                   eval      b2rper = *zero
T5.40c                   eval      b2raar = *zero
T5.40c                   else
T5.40c                   eval      b2bilt = 'Bilag:'
T5.40c                   eval      b2biln = rxbiln
T5.40c                   eval      b2pert = 'Per.:'
T5.40c                   eval      b2rper = rxrper
T5.40c                   eval      b2raar = rxraar
T5.40c                   endif
T5.20c                   endif
T5.20c                   endif
T5.20c                   endif
T5.20c                   endif
T5.20c                   endif
      *
T5.40c                   if        *in88 = *on
E5.40c     key02         setll     aklal2r
T5.40c                   else
T5.40c     aklal3_key    setll     aklal3r
T5.40c                   endif
8.00  *
8.00  * Sperre Kunder for skriving i notatark på kunder
8.00  *
8.00   CallP CO402R(l_filg:w_firm:0:'Sperr_Notat_Kunde':
8.00                    co402_verdi1:co402_verdi2);
8.00   if co402_verdi1 = '1';
8.00     w_spr_not_kund = *on;
8.00   endif;
8.00  *
8.00 c                   if        w_spr_not_kund = *on
8.00 c                             and rxsyst = 'K'
8.00 c                   eval      *in30 = *on
8.00 c                   endif
      *
     c                   exsr      xcrt01
      *
     c                   endsr
      *
**
  Ny post                  001
  Endring                  002
  Vise                     003
* Slettet *                004
F10=Standard tekst         005
F10=Notatark               006
* Standard tekst *         007
