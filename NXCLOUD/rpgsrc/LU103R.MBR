     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LU103R
      * Beskrivelse . . : Registrering av tellebunt fra subliste
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * 6.10  160609 BSA  Oppdatert med sporingsinformasjon
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = avslutt
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     flbchl1    if   e           k disk    rename(lbchpfr:lbchl1r)
     fltell1    if   e           k disk    rename(ltelpfr:ltell1r)
     flbhel1    if   e           k disk    rename(lbhepfr:lbhel1r)
     flbhelu    uf a e           k disk    rename(lbhepfr:lbhelur)
     flbdtlu    uf a e           k disk    rename(lbdtpfr:lbdtlur)
     flu103d    cf   e             workstn
      *
      * Parameter-rec
      *
     d                 ds
     d p_prec                        20
     d  p_firm                        3  0 overlay(p_prec)
     d  p_batc                       10    overlay(p_prec:4)
     d  p_numm                        6  0 overlay(p_prec:14)
     d  p_kode                        1    overlay(p_prec:20)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d lbchl1_batc     s                   like(lcbatc)
     d ltell1_batc     s                   like(lebatc)
     d ltell1_numm     s                   like(lenumm)
     d ltell1_suff     s                   like(lesuff)
     d lbhel1_batc     s                   like(lubatc)
     d lbhel1_numm     s                   like(lunumm)
     d lbhelu_batc     s                   like(lubatc)
     d lbhelu_numm     s                   like(lunumm)
      *
      * Definering av variable
      *
     d w_firm          s                   like(lvfirm)
     d w_line          s                   like(lvline)
     d w_prec          s             20
      *
     d b_feil          s              1    inz(*off)
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * A1WIN  - A1 Vindu for å velge subfile
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  A1 Behandler inntasting av telle-liste + sub-liste
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      a1numm = lunumm
     c                   eval      a1lagb = lulage
      *
      *  Send vindu
      *
     c     a1tagb        tag
     c                   exfmt     a1win
      *
      *  F3=Avslutt program
      *
     c                   if        *inkc = *on
     c                   goto      end_pgm
     c                   endif
      *
      * Sjekk gyldig valg av subliste
      *
     c                   exsr      sjekk_lager
     c                   if        b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      a1tagb
     c                   endif
      *
      * Oppdater bunthode med valgt subliste
      *
     c     lbhelu_key    chain     lbhelur
      *
     c                   if        %found
     c                   eval      lutnum = a1suff
6.10 c                   time                    luedat
6.10 c                   time                    luetim
6.10 c                   eval      lueusr = l_user
     c                   update    lbhelur
     c                   end
      *
      * Bygger  bunt på grunnlag av subfile
      *
     c                   exsr      bygg_bunt
      *
      * Avslutt program
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke lager på bunt mot subliste
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_lager   begsr
      *
     c                   eval      b_feil = *off
     c                   eval      ltell1_suff = a1suff
     c     ltell1_key    setll     ltell1
      *
     c                   read      ltell1
     c                   if        %eof = *on or
     c                             lelage <> lulage
     c                   eval      b_feil = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bygge bunt fra subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bygg_bunt     begsr
      *
     c                   eval      w_line = 0
     c                   eval      ltell1_suff = a1suff
     c     ltell1_key    setll     ltell1
     c     ltell1_key    reade     ltell1
      *
     c                   dow       not %eof
     c                   eval      w_line = w_line + 1
      *
     c                   eval      lvfirm = lefirm
     c                   eval      lvbatc = lebatc
     c                   eval      lvnumm = p_numm
     c                   eval      lvline = w_line
     c                   eval      lvtlin = leline
     c                   eval      lvdlop = 0
     c                   eval      lvogrp = leogrp
     c                   eval      lvhgrp = lehgrp
     c                   eval      lvugrp = leugrp
     c                   eval      lvvare = levare
     c                   eval      lvlage = lelage
     c                   eval      lvplas = leplas
     c                   eval      lvtek1 = letek1
     c                   eval      lvenh1 = leenh1
     c                   eval      lvspny = lespny
     c                   eval      lvkpny = lekpny
      *
     c                   if        a1sald = 1 and
     c                             leantl > 0
     c                   eval      lvantt = leantl
     c                   else
     c                   eval      lvantt = 0
     c                   endif
      *
6.10 c                   time                    lvodat
6.10 c                   time                    lvotim
6.10 c                   eval      lvousr = l_user
6.10 c                   time                    lvedat
6.10 c                   time                    lvetim
6.10 c                   eval      lveusr = l_user
     c                   write     lbdtlur
      *
      *
     c     ltell1_key    reade     ltell1
      *
     c                   enddo
      *
     c     end_bygg      tag
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    w_prec
     c                   eval      p_prec = w_prec
      *
      *  Key for les av BATCH
      *
     c     lbchl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbchl1_batc
      *
      *  Key for les av TELLE
      *
     c     ltell1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ltell1_batc
     c                   kfld                    ltell1_numm
     c                   kfld                    ltell1_suff
      *
      *  Key for les av TELLEBUNT-HODE
      *
     c     lbhel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbhel1_batc
     c                   kfld                    lbhel1_numm
      *
      *  Key for oppdatering av LAGERTELLEBUNT-HODE
      *
     c     lbhelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbhelu_batc
     c                   kfld                    lbhelu_numm
      *
      * Legger inn startverdier
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      lbchl1_batc = p_batc
     c                   eval      ltell1_batc = p_batc
     c                   eval      lbhel1_batc = p_batc
     c                   eval      lbhelu_batc = p_batc
      *
     c                   eval      lbhel1_numm = p_numm
     c                   eval      lbhelu_numm = p_numm
      *
      *  Hent opplysninger fra BATCH-REGISTER
      *
     c     lbchl1_key    chain     lbchl1
     c                   eval      ltell1_numm = lcnumm
      *
      *  Hent opplysninger fra BUNT-HODE
      *
     c     lbhel1_key    chain     lbhel1r
      *
     c                   endsr
      *
