      /title  ASPGPL Vedlikehold av filkopiering-hode-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASPGPL
      * Program . . . . : AA070R
      * Beskrivelse . . : Vedlikehold av filkopiering-hode-register
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       190298 HKO  Nytt program
7.xx  * 7.xx      140618  Endringer reltart til ny GUI. NB Kan kun gjelde BKV
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     facphl1    if   e           k disk    rename(acphpfr:acphl1r)
     facphl2    if   e           k disk    rename(acphpfr:acphl2r)
     facphlr    if   e           k disk    rename(acphpfr:acphlrr)
     facphlu    uf a e           k disk    rename(acphpfr:acphlur)
     facpllr    if   e           k disk    rename(acplpfr:acpllrr)
     facpllu    uf a e           k disk    rename(acplpfr:acpllur)
      *
     faa070d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av array
      *
     d a_meld          s             11    dim(3) ctdata perrcd(1)               MELDINGER
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av parametre
      *
      *
      * Definering av variabler i nøkler
      *
     d acphl1_hfgr     s                   like(achfgr)
     d acphl1_hfir     s                   like(achfir)
     d acphl2_hfir     s                   like(achfir)
     d acphlr_hfgr     s                   like(achfgr)
     d acphlr_hfir     s                   like(achfir)
     d acphlu_hfgr     s                   like(achfgr)
     d acphlu_hfir     s                   like(achfir)
     d acpllr_lfgr     s                   like(aclfgr)
     d acpllr_lfir     s                   like(aclfir)
     d acpllu_lfgr     s                   like(aclfgr)
     d acpllu_lfir     s                   like(aclfir)
      *
      * Definering av variable
      *
     d w_spge          s              3  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d w_forn          s              1    inz(*off)
     d w_anul          s              1    inz(*off)
     d w_feil          s              1    inz(*off)
      *
     d w_hfgr          s                   like(achfgr)
     d w_hfir          s                   like(achfir)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(11)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * wvisrn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meleingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      * S1WIN  - S1 Standardvindu som inneholder definisjon av
      *             størrelse og utseende.  Bildene D1WIN og K1WIN
      *             er referert til dette.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile                                  CRT SFL
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on                                  CRT SFL
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2valg = *blank
     c                   if        b2hfgr <> *blank or
     c                             b2hfir <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
     c                   endif
      *
      * Behandling av subfile control
      *
     c                   if        b2valg <> *blank
     c                   exsr      control
     c                   if        w_feil = *on
     c                   goto      b2tagb
     c                   else
     c                   goto      b2taga
     c                   endif
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   select
     c                   when      w_seqe = 'F'
     c     acphl2_key    setll     acphl2
     c                   other
     c     acphl1_key    setll     acphl1
     c                   endsl
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på filgruppe
      *
     c                   if        b2hfgr <> *blank
     c                   eval      w_seqe = ' '
     c                   eval      acphl1_hfgr = b2hfgr
     c                   eval      acphl1_hfir = b2hfir
     c     acphl1_key    setll     acphl1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på firma
      *
     c                   if        b2hfir <> 0
     c                   eval      w_seqe = 'F'
     c                   eval      acphl2_hfir = b2hfir
     c     acphl2_key    setll     acphl2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2hfgr = *blank
     c                   eval      b2hfir = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behanding av subfile-control
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     control       begsr
      *
     c                   eval      w_forn = *off
     c                   eval      w_feil = *off
      *
      * Opprette
      *
     c                   if        b2valg = '1'
     c                   eval      c1hfgr = b2hfgr
     c                   eval      c1hfir = b2hfir
     c                   exsr      xc1bld
     c                   goto      end_ctl
     c                   endif
      *
      * Validering av nøkkel
      *
     c                   if        b2hfgr = *blank
     c                   eval      w_feil = *on
     c                   eval      *in31  = *on
     c                   goto      end_ctl
     c                   endif
      *
     c                   eval      acphlr_hfgr = b2hfgr
     c                   eval      acphlr_hfir = b2hfir
     c     acphlr_key    chain     acphlr                             90
     c                   if        *in90
     c                   eval      w_feil = *on
     c                   eval      *in32  = *on
     c                   goto      end_ctl
     c                   endif
      *
      * Endre
      *
     c                   if        b2valg = '2'
     c                   eval      c1hfgr = b2hfgr
     c                   eval      c1hfir = b2hfir
     c                   exsr      xc2bld
     c                   goto      end_ctl
     c                   endif
      *
      * Kopiere
      *
     c                   if        b2valg = '3'
     c                   eval      k1hfgr = b2hfgr
     c                   eval      k1hfir = b2hfir
     c                   exsr      xk1win
     c                   goto      end_ctl
     c                   endif
      *
      * Slette
      *
     c                   if        b2valg = '4'
     c                   eval      d1hfgr = b2hfgr
     c                   eval      d1hfir = b2hfir
     c                   exsr      xd1win
     c                   goto      end_ctl
     c                   endif
      *
      * Vise
      *
     c                   if        b2valg = '5'
     c                   eval      c1hfgr = b2hfgr
     c                   eval      c1hfir = b2hfir
     c                   exsr      xc2bld
     c                   goto      end_ctl
     c                   endif
      *
      * Vedlikehold av filkopiering-linje
      *
     c                   if        b2valg = '7'
     c                   call      'AA071R'
     c                   parm                    b2hfgr
     c                   parm                    b2hfir
     c                   goto      end_ctl
     c                   endif
      *
     c     end_ctl       tag
      *
     c                   eval      b2valg = *blank
     c                   eval      b2hfgr = *blank
     c                   eval      b2hfir = 0
      *
     c                   if        w_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        wcurrn > 0
     c                   eval      wvisrn =  wcurrn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   eval      w_forn = *off
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
      * Endre post
      *
     c                   if        b1valg = '2'
     c                   eval      c1hfgr = b1hfgr
     c                   eval      c1hfir = b1hfir
     c                   exsr      xc2bld
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopiere post
      *
     c                   if        b1valg = '3'
     c                   eval      k1hfgr = b1hfgr
     c                   eval      k1hfir = b1hfir
     c                   exsr      xk1win
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slette post
      *
     c                   if        b1valg = '4'
     c                   eval      d1hfgr = b1hfgr
     c                   eval      d1hfir = b1hfir
     c                   exsr      xd1win
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise post
      *
     c                   if        b1valg = '5'
     c                   eval      c1hfgr = b1hfgr
     c                   eval      c1hfir = b1hfir
     c                   exsr      xc2bld
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vedlikehold av filkopiering-linje
      *
     c                   if        b1valg = '7'
     c                   call      'AA071R'
     c                   parm                    b1hfgr
     c                   parm                    b1hfir
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        w_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   goto      c1taga
     c                   endsl
      *
      * Validering
      *
     c                   if        c1hfgr = *blank
     c                   eval      *in31  = *on
     c                   goto      c1taga
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      acphlr_hfgr = c1hfgr
     c                   eval      acphlr_hfir = c1hfir
     c     acphlr_key    chain     acphlr                             90
     c                   if        *in90 = *off
     c                   exsr      xc1msg
     c                   if        w_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   goto      c1taga
      *
     c     end_c1bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      w_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      w_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      acphlr_hfgr = c1hfgr
     c                   eval      acphlr_hfir = c1hfir
     c     acphlr_key    chain     acphlr                             90
      *
     c                   if        *in90 = *off
     c                   eval      c2nyen = a_meld(2)
     c                   eval      c2eusr = acheus
     c     *dmy          move      acheda        c2edat
     c                   else
     c                   eval      c2nyen = a_meld(1)
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = *zero
     c                   eval      c2eusr = *blank
     c                   endif
      *
     c                   if        b1valg = '3' or b2valg = '3'
     c                   eval      c2nyen = a_meld(1)
     c                   eval      c1hfgr = k1hfgr
     c                   eval      c1hfir = k1hfir
     c                   eval      acphlr_hfgr = k1hfgr
     c                   eval      acphlr_hfir = k1hfir
     c                   endif
      *
     c                   if        b1valg = '5' or b2valg = '5'
     c                   eval      *in30 = *on
     c                   eval      c2nyen = a_meld(3)
     c                   endif
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Validering av input
      *
      * Oppdatering av filkopiering-hode
      *
     c                   clear                   acphlur
      *
     c                   eval      acphlu_hfgr = c1hfgr
     c                   eval      acphlu_hfir = c1hfir
     c     acphlu_key    chain     acphlur                            90
      *
     c                   time                    acheda
     c                   time                    acheti
     c                   eval      acheus = l_user
      *
     c                   if        *in90 = *off
     c                   update    acphlur
     c                   else
     c                   eval      achfgr = acphlr_hfgr
     c                   eval      achfir = acphlr_hfir
     c                   write     acphlur
     c                   endif
      *
     c                   if        b1valg = '1' or b2valg = '1' or
     c                             b1valg = '3' or b2valg = '3'
     c                   eval      w_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      acphlr_hfgr = d1hfgr
     c                   eval      acphlr_hfir = d1hfir
     c     acphlr_key    chain     acphlr                             90
      *
     c                   write     s1win
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      w_forn = *on
      *
      * Sletter filkopiering-hode
      *
     c                   eval      acphlu_hfgr = d1hfgr
     c                   eval      acphlu_hfir = d1hfir
     c     acphlu_key    chain     acphlur                            90
     c                   delete    acphlur
      *
      * Sletter tilhørende filkopiering-linjer
      *
     c                   eval      acpllu_lfgr = acphlu_hfgr
     c                   eval      acpllu_lfir = acphlu_hfir
     c     acpllu_key    setll     acpllu
     c     acpllu_key    reade     acpllur                                91
     c                   dow       *in91 = *off
     c                   delete    acpllur
     c     acpllu_key    reade     acpllur                                91
     c                   enddo
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      w_hfgr = k1hfgr
     c                   eval      w_hfir = k1hfir
      *
      * Fyller kopierings-bildet
      *
     c                   eval      acphlr_hfgr = k1hfgr
     c                   eval      acphlr_hfir = k1hfir
     c     acphlr_key    chain     acphlr                             90
      *
     c                   write     s1win
      *
     c     k1tagb        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1hfgr = *blank
     c                   eval      *in31 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      acphlr_hfgr = k1hfgr
     c                   eval      acphlr_hfir = k1hfir
     c     acphlr_key    chain     acphlr                             90
     c                   if        *in90 = *off
     c                   eval      *in32 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1hfgr = w_hfgr
     c                   eval      c1hfir = w_hfir
     c                   exsr      xc2bld
      *
      * Dersom hode er opprettet, kopieres linjene
      *
     c                   eval      acphlr_hfgr = k1hfgr
     c                   eval      acphlr_hfir = k1hfir
     c     acphlr_key    chain     acphlr                             90
     c                   if        *in90 = *off
     c                   eval      acpllr_lfgr = w_hfgr
     c                   eval      acpllr_lfir = w_hfir
     c     acpllr_key    setll     acpllr
     c     acpllr_key    reade     acpllr                                 91
     c                   dow       *in91 = *off
     c                   eval      aclfgr = k1hfgr
     c                   eval      aclfir = k1hfir
     c                   write     acpllur
     c     acpllr_key    reade     acpllr                                 91
     c                   enddo
     c                   endif
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'F'
     c                   read      acphl2                                 15
     c                   other
     c                   read      acphl1                                 15
     c                   endsl
      *
     c                   if        *in15                                        EOF
     c                   goto      end_crt
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = *blank
     c                   eval      b1hfgr = achfgr
     c                   eval      b1hfir = achfir
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      wvisrn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på filgruppe
      *
     c     acphl1_key    klist
     c                   kfld                    acphl1_hfgr
     c                   kfld                    acphl1_hfir
      *
      * Key for posisjonering på firma
      *
     c     acphl2_key    klist
     c                   kfld                    acphl2_hfir
      *
      * Key for les på filkopiering-hode
      *
     c     acphlr_key    klist
     c                   kfld                    acphlr_hfgr
     c                   kfld                    acphlr_hfir
      *
      * Key for oppdatering av filkopiering-hode
      *
     c     acphlu_key    klist
     c                   kfld                    acphlu_hfgr
     c                   kfld                    acphlu_hfir
      *
      * Key for les på filkopiering-linje
      *
     c     acpllr_key    klist
     c                   kfld                    acpllr_lfgr
     c                   kfld                    acpllr_lfir
      *
      * Key for oppdatering av filkopiering-linje
      *
     c     acpllu_key    klist
     c                   kfld                    acpllu_lfgr
     c                   kfld                    acpllu_lfir
      *
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      acphl1_hfgr = *blank
     c                   eval      acphl1_hfir = 0
     c     acphl1_key    setll     acphl1
      *
     c                   eval      *in22 = *on
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
**
  Ny post                  001
  Endring                  002
  Vise                     003
