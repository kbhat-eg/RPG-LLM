     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VL719R
      * Beskrivelse . . : Henter varetype,utg.dato, lev.nr og beholdning
      *                   fra lager
      *
      *                   beholdning = lagerbeholdning - i ordre
      *                   Hvis lagerenhet <> salgsenhet 1, så omberegnes beholdn
      *
      *                   Input : firma, vare, lager
      *                   Output: varetype, utg.dato, lev.nr, beholdning
      *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       010324 bsa  Nytt program - basert på VL716. Regner
      *                   disponibel beholdning på egen måte.
8.01  *K0000  050424 bsa  Feil utregning.
8.02  *K0000  270524 bsa  Feil omregning ved uttrekk av ordre.
8.03  *K0000  070624 bsa  Regn om hvis forskjell på enheter.
8.04  *K0000  010824 bsa  Direkte leverte ordre, skal heller ikke
8.04  *K0000              påvirke disponibel beholdning.
8.05  *K0000  080824 bsa  Ordren må også oppdatere lager for at den skal
8.05  *K0000              telle med.
8.06  *K0000  300425 bsa  Hvis ikke varen finnes på lagertabell, hopp ut.
8.06  *K0000              Jirasak NXS-20647
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     ffodtlx    if   e           k disk    rename(fodtpfr:fodtlxr)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
      *
      * Definering av variable i nøkler
      *
     d vlagl1_firm     s                   like(vlfirm)
     d vlagl1_vare     s                   like(vlvare)
     d vlagl1_lage     s                   like(vllage)
     d vvarl1_firm     s                   like(vvfirm)
     d vvarl1_vare     s                   like(vvvare)
     d fstsl1_firm     s                   like(faafir)
     d vvenl2_vare     s                   like(vevare)
     d vvenl2_saen     s                   like(vesaen)
     d vvenl1_vare     s                   like(vevare)
     d vvenl1_enhe     s                   like(veenhe)
     d fodtlx_lage     s                   like(fdlage)
     d fodtlx_vare     s                   like(fdvare)
     d fodtlx_edat     s                   like(fdedat)
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d lodtl1_line     s                   like(ldline)
     d votyl1_otyp     s                   like(vaotyp)
     d fohel1_numm     s                   like(fdnumm)
     d fohel1_suff     s                   like(fdsuff)
      *
      * Definering av pamametre
      *
     d p_firm          s                   like(vlfirm)
     d p_vare          s                   like(vlvare)
     d p_lage          s                   like(vllage)
     d p_enhe          s                   like(vvenhe)
     d p_vtyp          s                   like(vlvtyp)
     d p_udat          s                   like(vludat)
     d p_ldor          s                   like(vlbldo)
     d p_behl          s                   like(vlbehl)
     d p_inlr          s              1
      *
      * Definering av variable
      *
     d w_firm          s                   like(vefirm)
     d w_enhe          s                   like(veenhe)
     d w_omre          s                   like(veomre)
8.02 d w_omro          s                   like(veomre)
     d w_enh4          s                   like(veenhe)
     d w_enhl          s                   like(veenhe)
     d w_omrl          s                   like(veomre)
     d w_oord          s                   like(vlbehl)
     d w_word          s                   like(vlbehl)
      *
     d                uds
     d l_user                911    920
     d l_sfir                944    946  0
     d l_savd                947    950  0
     d l_lagr               1001   1002  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      p_behl = 0
     c                   eval      w_oord = 0
      *
      * Initierer output-parametre
      *
     c                   eval      p_vtyp = *blank
     c                   eval      p_udat = *loval
     c                   eval      p_ldor = 0
      *
     c                   if        faalag = 1
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
      *
     c                   exsr      hent_enhe
      *
      * Henter prisgruppe
      *
     c                   eval      vlagl1_vare = p_vare
     c                   eval      vlagl1_lage = p_lage
     c     vlagl1_key    chain     vlagl1
8.06  * Hopp ut hvis ikke på lagertabell
8.06 c                   if        not %found(vlagl1)
8.06 c                   goto      avslutt
8.06 c                   endif
      *
     c                   if        %found(vlagl1)
     c                   if        vlvtyp <> *blank
     c                   eval      p_vtyp = vlvtyp
     c                   endif
     c                   if        vludat <> *loval
     c                   eval      p_udat = vludat
     c                   endif
     c                   if        vlbldo <> 0
     c                   eval      p_ldor = vlbldo
     c                   endif
      *
     c                   if        vlvtyp <> 'D' and
     c                             vlvtyp <> 'T' and
     c                             vlvtyp <> 'S'
     c**                 eval      p_behl = vlbehl - vloord
      * Ny beregning av i ordre.
     c                   eval      fodtlx_lage = p_lage
     c                   eval      fodtlx_vare = p_vare
     c                   eval      fodtlx_edat = *loval
     c     fodtlx_key    setll     fodtlx
     c     fodtlx_key2   reade     fodtlx
     c                   dow       not %eof
      *
8.04  * Ikke ta hensyn til direkte leverte ordrelinjer
      *
8.04 c                   if        fdlety = 0
      *
      * Finnes det tilkoblet bestilling ? Hvis ja, skal den ikke telles opp
      *
     c                   eval      lodtl1_numm = fdbenr
     c                   eval      lodtl1_suff = fdbsuf
     c                   eval      lodtl1_line = fdblin
     c     lodtl1_key    chain     lodtl1
     c                   if        not %found(lodtl1)
      *
      * Hent ordretype fra ordrehodet
      *
     c                   eval      fohel1_numm = fdnumm
     c                   eval      fohel1_suff = fdsuff
     c     fohel1_key    chain     fohel1
     c                   if        %found(fohel1)
      *
      * Er dette ordretype av akkumulatortype 1 ?
      *
     c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1
     c                   if        %found(votyl1) and
8.05 c                             vaoakk = 1 and
8.05 c                             vaolag = 1
      *
8.02 c                   if        vvenhl <> fdenh1
8.02 c*                  if        vvenhl <> fdenh1  and
8.02 c*                            w_omre <> 0
8.02 c                   exsr      omr_ordre
8.02 c*                  eval      w_word = fdanta * w_omrl/w_omre
8.02 c                   eval      w_word = fdanta * w_omro/w_omrl
8.01 c                   eval      w_oord = w_oord + w_word
     c                   else
     c                   eval      w_oord = w_oord + fdanta
     c                   endif
      *
     c                   endif
     c                   endif
     c                   endif
      *
8.04 c                   endif
      *
     c     fodtlx_key2   reade     fodtlx
     c                   enddo
      *
     c                   endif
      *
     c                   endif
      *
     c                   eval      p_behl = vlbehl - w_oord
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found(vvarl1)
      *
8.03 c                   if        vvenhl <> p_enhe  and
8.03 c                             w_omre <> 0
8.03 c                   eval      p_behl = p_behl *  w_omrl/w_omre
8.03 c                   endif
      *
     c                   if        p_vtyp = *blank
     c                   eval      p_vtyp = vvvtyp
     c                   endif
      *
     c                   if        p_udat = *loval
     c                   eval      p_udat = vvudat
     c                   endif
      *
     c                   if        p_ldor = 0
     c                   eval      p_ldor = vvldor
     c                   endif
      *
     c                   endif
      *
6.30 c                   else
6.30  *
6.30 c                   if        p_vtyp = *blank
6.30 c                   eval      vvarl1_firm = p_firm
6.30 c                   eval      vvarl1_vare = p_vare
6.30 c     vvarl1_key    chain     vvarl1
6.30 c                   if        %found(vvarl1)
6.30 c                   eval      p_vtyp = vvvtyp
6.30 c                   endif
6.30 c                   endif
6.30  *
6.30 c                   if        p_udat = *loval
6.30 c                   eval      vvarl1_firm = p_firm
6.30 c                   eval      vvarl1_vare = p_vare
6.30 c     vvarl1_key    chain     vvarl1
6.30 c                   if        %found(vvarl1)
6.30 c                   eval      p_udat = vvudat
6.30 c                   endif
6.30 c                   endif
6.30  *
6.30 c                   if        p_ldor = 0
6.30 c                   eval      vvarl1_firm = p_firm
6.30 c                   eval      vvarl1_vare = p_vare
6.30 c     vvarl1_key    chain     vvarl1
6.30 c                   if        %found(vvarl1)
6.30 c                   eval      p_ldor = vvldor
6.30 c                   endif
6.30 c                   endif
      *
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   if        p_inlr <> *on
     c                   eval      *inlr = *on
     c                   endif
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for omregning ev enheter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_enhe     begsr
      * finner lager enhet
     c                   eval      w_omrl = 1
     c                   eval      vvenl1_vare = p_vare
     c                   eval      vvenl1_enhe = vvenhl
     c     vvenl1_key    chain     vvenl1
     c                   if        %found(vvenl1)
     c                   eval      w_enhl = veenhe
     c                   eval      w_omrl = veomre
     c                   endif
      *
      * finner vist enhet
     c                   eval      w_omre = 1
     c                   eval      vvenl1_vare = p_vare
     c                   eval      vvenl1_enhe = p_enhe
     c     vvenl1_key    chain     vvenl1
     c                   if        %found(vvenl1)
     c                   eval      w_enhe = veenhe
     c                   eval      w_omre = veomre
     c                   endif
      *
     c                   endsr
      *
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02  * Subrutine for initiering av program
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02  *
8.02 c     omr_ordre     begsr
8.02  *
8.02  * Antall regnes om etter forholdet mellom disse
8.02  *
8.02 c                   eval      w_omro = 1
8.02 c                   eval      vvenl1_vare = p_vare
8.02 c                   eval      vvenl1_enhe = *blanks
8.02 c     vvenl1_key    setll     vvenl1
8.02 c     vvenl1_key2   reade     vvenl1
8.02 c                   dow       not %eof
8.02 c                   if        fdenh1 = veenhe
8.02 c                   eval      w_omro = veomre
8.02 c                   endif
8.02 c     vvenl1_key2   reade     vvenl1
8.02 c                   enddo
8.02  *
8.02 c                   endsr
8.02  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_lage
     c                   parm                    p_enhe
     c                   parm                    p_vtyp
     c                   parm                    p_udat
     c                   parm                    p_ldor
     c                   parm                    p_behl
     c                   parm                    p_inlr
      *
      * Key for les på lager-register
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
      *
      * Key for les på vare-register
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på OFAK-kode (til)
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les på vare-enhet
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
     c                   kfld                    vvenl2_saen
      *
     c     vvenl2_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
      *
8.02 c     vvenl1_key2   klist
8.02 c                   kfld                    w_firm
8.02 c                   kfld                    vvenl1_vare
      *
      * Key for les på ordreregister
      *
     c     fodtlx_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlx_lage
     c                   kfld                    fodtlx_vare
     c                   kfld                    fodtlx_edat
      *
      * Key for les på ordreregister
      *
     c     fodtlx_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlx_lage
     c                   kfld                    fodtlx_vare
      *
      * Key for les på ordretype tabell
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for les på ordre
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for les på bestilling
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
     c                   kfld                    lodtl1_line
      *
     c                   eval      w_firm = p_firm
      *
     c                   eval      fstsl1_firm = p_firm
     c     fstsl1_key    chain     fstsl1                             91
      *
     c                   endsr
