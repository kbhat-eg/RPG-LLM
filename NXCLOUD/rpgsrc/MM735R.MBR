     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLOGI
      * Program . . . . : MM735R
      * Beskrivelse . . : Beregner prognose ut fra progose pr. uke
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       261104 bø   Nytt program
5.40  *       120106 bø   Hvis registrert ant. på prognose -brukes den
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fmprulr    if   e           k disk    rename(mprupfr:mprul1r)
      *
      *
     d mprulr_vare     s                   like(mrvare)
     d mprulr_lage     s                   like(mrlage)
     d mprulr_aar      s                   like(mraar)
     d mprulr_uke      s                   like(mruke)
      *
     d p_list          s            128
     d w_dato          s               d   datfmt(*iso)
     d w_fdatu         s               d   datfmt(*iso)
     d w_tdatu         s               d   datfmt(*iso)
     d w_dato1         s               d   datfmt(*iso)
     d w_dato2         s               d   datfmt(*iso)
     d w_pdat          s               d   datfmt(*iso)
     d w_psts          s              1
     d w_paar          s              4  0
     d w_puke          s              2  0
     d w_dgr           s              4  0
     d w_dagsb         s             11  3
     d w_113           s             11  3
     d w_aar           s              4  0
     d w_uke           s              2  0
     d w_stat          s              1
     d w_tilau         s              6
     d w_firm          s                   like(mrfirm)
     d w_tant          s             11  3
      *
     d b_ok            s              1    inz(*off)
     d b_matr          s              1    inz(*off)
     d b_funn          s              1    inz(*off)
      *
      * Local Data Area
      *
     d                uds
      *
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d                 ds
     d d_list                       128
     d p_vare                        15    overlay(d_list)
     d p_lage                         2  0 overlay(d_list:16)
     d p_fuke                         2  0 overlay(d_list:18)
     d p_faar                         4  0 overlay(d_list:20)
     d p_tuke                         2  0 overlay(d_list:24)
     d p_taar                         4  0 overlay(d_list:26)
     d p_ltid                         3  0 overlay(d_list:30)
     d p_helu                         3  0 overlay(d_list:33)
     d p_dage                         1  0 overlay(d_list:36)
     d p_tdtt                         6  0 overlay(d_list:37)
     d p_lr                           1    overlay(d_list:43)
     d p_brkg                         1  0 overlay(d_list:44)
     d p_dagsb                       11  3 overlay(d_list:45)
      *
     d w_aaruk         ds
     d mraar                          4  0
     d mruke                          2  0
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_list
      *
     c                   eval      d_list  = p_list
     c                   eval      w_firm  = l_firm
     c                   eval      p_dagsb = 0
      *
     c                   if        p_lr = 'J'
     c                   goto      avslutt
     c                   endif
      *
      *
     c                   movel     p_taar        w_tilau
     c                   move      p_tuke        w_tilau
     c                   eval      b_funn = *off
     c                   eval      w_tant = 0
      * finner hele uker
     c                   if        p_helu <> 0
     c                   eval      mprulr_vare = p_vare
     c                   eval      mprulr_lage = p_lage
     c                   eval      mprulr_aar  = p_faar
     c                   eval      mprulr_uke  = p_fuke
     c     mprulr_key    setll     mprulr
     c                   read      mprulr
     c                   dou       %eof(mprulr)
      *
     c                   if        mrvare <> p_vare or
     c                             mrlage <> p_lage or
     c                             w_aaruk > w_tilau
     c                   if        b_funn = *on
     c                   leave
     c                   else
     c                   goto      avslutt
     c                   endif
     c                   endif
      *
     c                   eval      b_funn = *on
     c                   if        mrantr <> 0
     c                   eval      w_tant = w_tant + mrantr
     c                   else
     c                   if        p_brkg = 1
     c                   eval      w_tant = w_tant + mrantg
     c                   else
     c                   eval      w_tant = w_tant + mrantp
     c                   endif
     c                   endif
      *
     c                   read      mprulr
     c                   enddo
      *
     c                   endif
      * finner evt for overskytende dager
      *
     c     *dmy          move      p_tdtt        w_pdat
     c                   call      'AX711R  '
     c                   parm                    w_pdat
     c                   parm                    w_paar
     c                   parm                    w_puke
     c                   parm                    w_psts
     c                   if        w_puke = 53
      * uke 53 skal ikke eksistere - går til neste uke og tar deler av den
     c                   eval      w_puke = 1
     c                   eval      w_paar = w_paar + 1
     c                   endif
      *
      * finner ukenr for denne uken
      *
     c                   if        p_dage <> 0
     c                   eval      mprulr_vare = p_vare
     c                   eval      mprulr_lage = p_lage
     c                   eval      mprulr_aar  = w_paar
     c                   eval      mprulr_uke  = w_puke
     c     mprulr_key    chain     mprulr
     c                   if        %found(mprulr)
     c                   if        mrantr <> 0
      * bruker reg.antall hvis dette finne, elles glattet eller ordinært
     c                   eval(h)   w_113 = mrantr * p_dage / 7
     c                   else
     c                   if        p_brkg = 1
     c                   eval(h)   w_113 = mrantg * p_dage / 7
     c                   else
     c                   eval(h)   w_113 = mrantp * p_dage / 7
     c                   endif
     c                   endif
     c                   eval      w_tant = w_tant + w_113
     c                   endif
     c                   endif
      *
      * beregn dagsbehov
      *
     c                   eval      w_dgr = 7 * p_helu
      *
     c                   eval      w_dagsb = w_tant / (w_dgr + p_dage)
     c                   eval      p_dagsb = w_dagsb
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     avslutt       tag
      *
     c                   if        p_lr = 'J'
     c                   eval      *inlr = *on
     c                   endif
      *
     c                   eval      p_list  = d_list
     c                   return
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les av prognose matrise
      *
     c     mprulr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mprulr_vare
     c                   kfld                    mprulr_lage
     c                   kfld                    mprulr_aar
     c                   kfld                    mprulr_uke
      *
     c                   endsr
