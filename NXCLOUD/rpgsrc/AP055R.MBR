     H decedit('0.') datedit(*dmy-) option(*nodebugio)
     h DftActgrp(*No) Actgrp(*NEW) BndDir('CGIDEV2/CGIDEV2')
      /copy hspecsbnd
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : AP055R
      * Beskrivelse . . : Oppslag mot Lognet transporttjeneste
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       191321 bsa  Nytt program
7.01  * K000  250521 bsa  Hvis ordren er ferdig plukket, sett riktig
7.01  * K000              status på 'readyfordispatch'
7.02  * K000  230521 bsa  Info om varelinjer og kontaktperson skal også
7.02  * K000              overføres.
7.03  * K000  020621 bsa  Endret til å hente tekst fra lager, og ikke
7.03  * K000              avdeling.
7.04  * K000  021221 bsa  Legger ut kundens kontaktperson.
7.05  * K000  061221 bsa  Legger ut kundens telefonnummer.
      *
8.01  * K000  060122 bsa  Sjekk også forrige år da, det muligens
8.01  * K000              ordren er logget forrige år.
8.02  * K000  020222 bsa  Må sjekke om det skal brukes avvikende
8.02  * K000              id ved sending av request (trp_tracid)
8.03  * K000  160322 bsa  Switch styrer om internordre skal overføres.
8.03  * K000              NB Styrt på lager.
8.04  * K000  011122 bsa  Switch styrer om datofelt skal editeres før
8.04  * K000              overføring.
8.05  *K0000  041223 bsa  Endrer tegnsett på returfile, hvis ikke klarer
8.05  *K0000              vi ikke lese de.
8.06  *K0000  111223 bsa  Sjekk om det er fil i retur.
8.07  *K0000  110324 bsa  Sletter alle filer hvis det går OK.
8.08  * K000  120424 bsa  Justert mottak.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
7.03 fra10l1    if   e           k disk    rename(ra10pfr:ra10l1r)
7.01 ffloglr    if   e           k disk    rename(flogpfr:floglrr)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtlr    if   e           k disk    rename(fodtpfr:fodtlrr)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fftrpi2    if   e           k disk    rename(ftrpst:ftrpi2r)
     fftrpiu    uf a e           k disk    rename(ftrpst:ftrpiur)
7.02 fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
7.02 fvvasl1    if   e           k disk    rename(vvaspfr:vvasl1r)
7.02 fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
7.02 fjvpkl2    if   e           k disk    rename(jvpkpfr:jvpkl2r)
7.04 ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
8.02 fftrli1    if   e           k disk    rename(ftrlst:ftrli1r)
8.03 fvot1l1    if   e           k disk    rename(vot1pfr:vot1l1r)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_figr                934    939
     d l_filg                937    939
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av parametere
      *
     d p_firm          s              3  0
     d p_numm          s              8  0
     d p_suff          s              2  0
     d p_toid          s             35
     d p_2fid          s             35
     d p_stat          s              5
      *
      * Definering av variabler i nøkler
      *
8.02 d ftrli1_llag     s                   like(ftllag)
     d ftrpiu_roid     s                   like(ftroid)
     d ftrpi2_rnum     s                   like(ftrnum)
     d ftrpi2_rsuf     s                   like(ftrsuf)
     d rkunl1_kund     s                   like(rkkund)
7.03 d ra10l1_jkod     s                   like(rajkod)
     d fodtlr_line     s                   like(fdline)
7.01 d floglr_aarr     s                   like(fuaarr)
7.01 d floglr_numm     s                   like(funumm)
7.01 d floglr_suff     s                   like(fusuff)
7.02 d vvarl1_vare     s                   like(vvvare)
7.02 d vvasl1_vare     s                   like(vvvare)
7.02 d vvenl1_vare     s                   like(vevare)
7.02 d vvenl1_enhe     s                   like(veenhe)
7.02 d jvpkl2_vare     s                   like(jwvare)
7.02 d jvpkl2_ldor     s                   like(jwldor)
7.02 d jvpkl2_enhe     s                   like(jwenhe)
7.04 d fkprl1_kund     s                   like(flkund)
7.04 d fkprl1_kpro     s                   like(flkpro)
8.03 d vot1l1_otyp     s                   like(va1typ)
      *
      * Definering av variable
      *
     d w_token         s           4000
     d w_tokeut        s           4014
     d w_indata        s           2000
     d w_url           s            200
     d w_url2          s            200
     d w_reqf          s            100
     d w_rspf          s            100
     d w_usrn          s             20
     d w_ident         s            221
     d*w_pass          s             50
     d w_reqt          s              6
     d w_enco          s             20
     d w_stat          s              1  0
     d w_kode          s              1
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
     d w_numm          s              8  0
     d w_size          s              8  0
     d w_start         s              4  0
     d w_slutt         s              4  0
     d w_lengd         s              4  0
     d w_coref         s             50
     d w_trid          s             35
     d w_toid          s             35
     d w_oref          s             15
     d w_odat          s               d   datfmt(*iso)
     d w_date          s               d   datfmt(*iso)
     d w_mal           s            200
     d w_ponr          s             10
     d w_city          s             35
     d w_lponr         s             10
     d w_lcity         s             35
     d w_kcity         s             35
     d w_len           s              3  0
     d w_totw          s              9  3
     d w_totv          s              9  3
     d w_akol          s              4  0
     d w_akola         s              3
     d w_avvi          s              1
7.01 d w_disp          s              5
7.02 d w_line          s              4  0
7.02 d w_vekt          s             11  2
7.02 d w_vektv         s             11  2
7.02 d w_volu          s              9  3
7.02 d w_voluv         s              9  3
7.02 d w_lv_nobb       s                   like(vvnobb)
7.02 d w_lv_modn       s                   like(vvnmod)
7.02 d w_lv_lldo       s                   like(vvldor)
7.02 d w_lv_llva       s                   like(vvlvar)
7.02 d w_utda          s                   like(vvudat)
7.02 d w_vtyp          s                   like(vvvtyp)
7.04 d w_dref          s             50
7.05 d w_mobi          s             15
8.01 d w_aarr          s              4  0
8.04 d w_date_cnv      s             18
8.05 d w_ifsf          s              1
      *
7.02 d b_inlr          s              1    inz(*off)
      *
8.03 d u_inte          s              1    inz(*on)
8.04 d u_konv          s              1    inz(*on)
      *
     d w_kurl          s            200
     d w_kusr          s            200
     d w_pass          s            200
     d w_prox          s            200
     d IFS_Output      s            256a   Varying
     d IFS_Output1     s            256a   Varying
     d IFS_Output2     s            256a   Varying
     d IFS_Input       s            256a   Varying
     d API_Output2     s            256a   Varying
     d API_Input       s            256a   Varying
     d IFS_path        s            256a   Varying
8.05 d IFS_file        s            256
     D Len             S             10I 0
      *
      /copy prototypeb
      *--------------------------------------------------------------------
      * Change Directory
      *
      * int chdir(const char *path)
      *--------------------------------------------------------------------
     D chdir           PR            10I 0 ExtProc('chdir')
     D   path                          *   Value Options(*string)

     D fd              S             10I 0
     D RW              C                   6
     D R               C                   4
     D OWNER           C                   64
     D GROUP           C                   8
      /copy usec
      *
      * Datastruktur for IFS fra 1881
      *
     d Kundeionfo      ds                  qualified
     d                                     dim(99)
     d   Posting_Date                10a
     d   Document_Type...
     d                               10a
     d   Document_No                  8a
     d   Customer_No                  6a
     d   Description                 26a
     d   Amount                      11a
     d   Remaining_Amount...
     d                               11a
     d   Due_Date                    10a
8.03  *
8.03  * Definering av eksterne prg
8.03  *
8.03   dcl-s co402_verdi1  char(1);
8.03   dcl-s co402_verdi2  char(2048);
8.03   DCL-PR CO402R EXTPGM('CO402R');
8.03     p_filgrp            char(3)     const;
8.03     p_firm              packed(3:0) const;
8.03     p_lager             packed(2:0) const;
8.03     p_nokkel            char(50)    const;
8.03     p_verdi1            char(1);
8.03     p_verdi2            char(2048);
8.03   END-PR;
      *
      *
      * Hovedprogram:
      *
      *  - slår opp med telefonnummer
      *  - mottar responsfil fra API
      *  - henter ut data fra responsfil
      *  - rydder på IFS
      *
      * Sjekk om vi har mal for fletting av json fil
      *
     c                   if        %trim(w_mal) = *blanks or
     c                             %trim(w_url) = *blanks or
     c                             %trim(w_token) = *blanks or
     c                             %trim(w_trid) = *blanks
     c                   eval      p_stat = 'FALSE'
     c                   goto      avslutt
     c                   endif
      *
     c     p_firm        chain     afirl1
     c                   if        not %found
     c                   eval      p_stat = 'FALSE'
     c                   goto      avslutt
     c                   endif
      *
      * Hent inn info fra ordren.
      *
     c     fohel1_key    chain     fohel1
     c                   if        not %found
     c                   eval      p_stat = 'FALSE'
     c                   goto      avslutt
     c                   endif
8.03  *
8.03  * Sjekk om vi skal utelate interne ordretyper
8.03  *
8.03 c                   if        u_inte = *on
8.03 c                   eval      vot1l1_otyp = footyp
8.03 c     vot1l1_key    chain     vot1l1
8.03 c                   if        %found and
8.03 c                             va1int = 1
8.03 c                   eval      p_stat = 'FALSE'
8.03 c                   goto      avslutt
8.03 c                   endif
8.03 c                   endif
8.02  *
8.02  * Sjekk om det er avvikende "transportprovidercode"
8.02  *
8.02 c                   eval      ftrli1_llag = folage
8.02 c     ftrli1_key    chain     ftrli1
8.02 c                   if        %found and
8.02 c                             %trim(ftltid) <> *blanks
8.02 c                   eval      w_trid = %trim(ftltid)
8.02 c                   endif
8.02  *
7.04 c                   eval      w_dref = *blanks
7.04 c                   eval      w_dref = %trim(fodref)
7.05 c                   eval      w_mobi = *blanks
7.05 c                   eval      w_mobi = %trim(fomobi)
      *
      * Hent inn info om kolli fra ordrelinjer
      *
     c                   eval      fodtlr_line = 0
     c                   eval      w_akol = 0
     c     fodtlr_key    setll     fodtlr
     c     fodtlr_key2   reade     fodtlr
     c                   dow       not %eof
     c                   if        fdline = 9000 or
     c                             fdline = 9001 or
     c                             fdline = 9002
     c                   eval      w_akola = *blanks
     c                   eval      w_start = %scan(':' : fdtek1)
     c                   eval      w_start = w_start + 1
     c                   eval      w_akola = %subst(fdtek1:w_start:3)
     c**   ' ':'0'       xlate     w_akola       w_akola
     c                   eval      w_akol = w_akol + %int(%trim(w_akola))
     c                   endif
     c     fodtlr_key2   reade     fodtlr
     c                   enddo
      *
      * Må minimum sende 1 i antall kolli pga tjenesten til Lognet
      * Hvis 0, antagelig første request.
      *
     c                   if        w_akol = 0
     c                   eval      w_akol = 1
     c                   endif
      *
      * Hent inn vekt og volum for ordren
      *
     c                   eval      w_totv = 0
     c                   eval      w_totw = 0
     c                   eval      w_avvi = *blanks
      *
     c                   call      'FO780R'
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    w_totw
     c                   parm                    w_totv
     c                   parm                    w_avvi
      *
      * Hent inn info fra kunden.
      *
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1
     c                   if        not %found
     c                   eval      rknavn = *blanks
     c                   eval      rkgate = *blanks
     c                   eval      rkadr2 = *blanks
     c                   eval      rksted = *blanks
     c                   eval      rkponr = 0
     c                   endif
      *
      * Hent inn info fra avdling
      *
7.03 c                   eval      ra10l1_jkod = folage
7.03 c     ra10l1_key    chain     ra10l1
     c                   if        not %found
7.03 c                   eval      rajtxt = *blanks
7.03 c                   eval      rajadr = *blanks
7.03 c                   eval      rajad2 = *blanks
7.03 c                   eval      rajste = *blanks
7.03 c                   eval      rajpnr = 0
     c                   endif
      *
      * Hvis vi ikke har unik ID fra før, lager vi en nå.
      *
     c                   eval      w_toid = *blanks
     c                   eval      ftrpi2_rnum = p_numm
     c                   eval      ftrpi2_rsuf = p_suff
     c     ftrpi2_key    chain     ftrpi2
     c                   if        %found
     c                   eval      w_toid = ftroid
     c                   endif
      *
     c                   if        %trim(w_toid) = *blanks
8.04 c**                 eval      w_toid = %trim(%char(w_date)+'-'+
8.04 c**                                   %char(w_numm))
8.04 c                   eval      w_toid = %trim(%trim(w_date_cnv)+':'+
8.04 c                                     %char(w_numm))
     c                   endif
      *
      * Bygger opp felter i requesten.
      *
     c                   eval      w_oref = *blanks
     c                   eval      w_oref = %trim(%char(fonumm) + '-' +
     c                             %char(fosuff))
     c                   eval      w_odat = fobdat
     c                   if        foldat <> *loval
     c                   eval      w_odat = foldat
     c                   endif
      *
     c                   eval      w_city = *blanks
     c                   eval      w_ponr = *blanks
     c                   eval      w_len = %scan(' ' : aafste)
     c                   eval      w_ponr = %subst(aafste:1:w_len-1)
     c                   eval      w_city = %subst(aafste:w_len+1)
      *
     c                   eval      w_kcity = *blanks
     c                   eval      w_len = %scan(' ' : rksted)
     c                   eval      w_kcity = %subst(rksted:w_len+1)
      *
     c                   eval      w_lcity = *blanks
     c                   eval      w_lponr = *blanks
     c                   eval      w_len = %scan(' ' : fosted)
     c                   eval      w_lponr = %subst(fosted:1:w_len-1)
     c                   eval      w_lcity = %subst(fosted:w_len+1)
      *
7.01  * Finn ut om den er klar for utsendelse, dvs loggkode 6 eller høyere
      *
7.01 c                   eval      w_disp = 'false'
8.01 c                   eval      w_aarr = %subdt(w_date:*years)
8.01 c                   eval      floglr_aarr = w_aarr
8.01 c**                 eval      floglr_aarr = %subdt(w_date:*years)
7.01 c                   eval      floglr_numm = fonumm
7.01 c                   eval      floglr_suff = fosuff
7.01 c     floglr_key    setll     floglr
7.01 c     floglr_key    reade     floglr
7.01 c                   dow       not %eof
7.01 c                   if        fukode = '6'
7.01 c                   eval      w_disp = 'true '
7.01 c                   endif
7.01 c     floglr_key    reade     floglr
7.01 c                   enddo
8.01  *
8.01  * Sjekk på forrige år, hvis ikke ordren er plukket. Året følger
8.01  * første hendelse på ordren.
8.01  *
8.01 c                   if        w_disp = 'false'
8.01 c                   eval      floglr_aarr = w_aarr - 1
8.01 c     floglr_key    setll     floglr
8.01 c     floglr_key    reade     floglr
8.01 c                   dow       not %eof
8.01 c                   if        fukode = '6'
8.01 c                   eval      w_disp = 'true '
8.01 c                   endif
8.01 c     floglr_key    reade     floglr
8.01 c                   enddo
8.01 c                   endif
7.04  *
7.04  * Hent kundens kontaktperson
7.04  *
7.05 c                   if        %trim(w_dref) = *blanks or
7.05 c                             %trim(w_mobi) = *blanks
7.04  *
7.04 c                   if        fokpro <> 0
7.04 c                   eval      fkprl1_kund = fokund
7.04 c                   eval      fkprl1_kpro = fokpro
7.04 c     fkprl1_key    chain     fkprl1
7.04 c                   if        %found
7.05 c                   if        w_dref = *blanks
7.04 c                   eval      w_dref = %trim(flkprs)
7.05 c                   endif
7.05 c                   if        w_mobi = *blanks
7.05 c                   eval      w_mobi = %trim(fltlfm)
7.05 c                   endif
7.05 c                   if        w_mobi = *blanks
7.05 c                   eval      w_mobi = %trim(fltlfa)
7.05 c                   endif
7.04 c                   endif
7.04 c                   endif
7.04  *
7.04 c                   endif
      *
7.05 c                   if        w_mobi = *blanks
7.05 c                   eval      w_mobi = %trim(rkmobn)
7.05 c                   endif
      *
      * Hent inn json templaten
      *
     c                   callp     GetHTMLIFS(w_mal :'<AS400>')
     c                   callp     ClrHtmlBuffer
      *
      /Free
           // Bygge json request - id info
               UpdHTMLVar('transportprovidercode':   %trim(w_trid));
7.03           UpdHTMLVar('warehousenumber':         %char(folage));
               UpdHTMLVar('shipmentreference':       %trim(w_toid));
               UpdHTMLVar('orderreference':          %trim(w_oref));
               UpdHTMLVar('shipmentdate':            %char(w_odat));
7.01           UpdHTMLVar('readyfordispatch':        %trim(w_disp));

           // Avsender
               UpdHTMLVar('consignorname':           %trim(aafnav));
               UpdHTMLVar('consignoraddress1':       %trim(aafad1));
               UpdHTMLVar('consignoraddress2':       %trim(aafad2));
               UpdHTMLVar('consignorpostalcode':     %trim(w_ponr));
               UpdHTMLVar('consignorcity':           %trim(w_city));
               UpdHTMLVar('consignorcountrycode':             'NO');

           // Mottaker
               UpdHTMLVar('consigneename':           %trim(rknavn));
               UpdHTMLVar('consigneeaddress1':       %trim(rkgate));
               UpdHTMLVar('consigneeaddress2':       %trim(rkadr2));
               UpdHTMLVar('consigneepostalcode':     %char(rkponr));
               UpdHTMLVar('consigneecity':          %trim(w_kcity));
               UpdHTMLVar('consigneecountrycode':             'NO');

7.03       // Avsenderadresse - lager
7.03           UpdHTMLVar('fromname':                %trim(rajtxt));
7.03           UpdHTMLVar('fromaddress1':            %trim(rajadr));
7.03           UpdHTMLVar('fromaddress2':            %trim(rajad2));
7.03           UpdHTMLVar('frompostalcode':          %char(rajpnr));
7.03           UpdHTMLVar('fromcity':                %trim(rajste));
               UpdHTMLVar('fromcountrycode':                  'NO');

           // Leveringsadresse på ordren
               UpdHTMLVar('toname':                  %trim(fonavn));
               UpdHTMLVar('toaddress1':              %trim(foadr1));
               UpdHTMLVar('toaddress2':              %trim(foadr2));
               UpdHTMLVar('topostalcode':            %trim(w_lponr));
               UpdHTMLVar('tocity':                  %trim(w_lcity));
               UpdHTMLVar('tocountrycode':                    'NO');
7.04    //     UpdHTMLVar('tocontact':               %trim(fovref));
7.04           UpdHTMLVar('tocontact':               %trim(w_dref));
7.05    //     UpdHTMLVar('tophone':                 %trim(fomobi));
7.05           UpdHTMLVar('tophone':                 %trim(w_mobi));

           // Godsinformasjon
               UpdHTMLVar('deliveryInformation1':              ' ');
               UpdHTMLVar('deliveryInformation2':              ' ');
               UpdHTMLVar('totalpackages':           %char(w_akol));
               UpdHTMLVar('totalweight':             %char(w_totw));
               UpdHTMLVar('totalvolume':             %char(w_totv));

               WrtSection('Request');

      /End-Free
7.02  *
7.02  * Da skal vi lese linjer, og hent ned høyeste nummer
7.02  *
7.02 c                   eval      fodtlr_line = 8999
7.02 c     fodtlr_key    setll     fodtlr
7.02 c     fodtlr_key2   readpe    fodtlr
7.02 c                   dow       not %eof
7.02 c                   eval      w_line = fdline
7.02 c                   leave
7.02 c                   enddo
7.02  *
7.02  * Da skal vi lese linjer
7.02  *
7.02 c                   eval      fodtlr_line = 0
7.02 c     fodtlr_key    setll     fodtlr
7.02 c     fodtlr_key2   reade     fodtlr
7.02 c                   dow       not %eof
7.02  *
7.02  * Finn vekt og volum for varelinje
7.02  *
7.02 c                   exsr      kalk_linje
7.02  *
7.02  /Free
7.02
7.02       // Godsinformasjon
7.02           UpdHTMLVar('linenumber':               %char(fdline));
7.02           UpdHTMLVar('articlenumber':            %trim(fdvare));
7.02           UpdHTMLVar('articletext':              %trim(fdtek1));
7.02           UpdHTMLVar('quantity':                 %char(fdanta));
7.02           UpdHTMLVar('unitcode':                 %trim(fdenh1));
7.02           UpdHTMLVar('weight':                  %char(w_vektv));
7.02           UpdHTMLVar('volume':                  %char(w_voluv));
7.02
7.02           WrtSection('RequestLineStart');
7.02
7.02           if fdline <> w_line;
7.02               WrtSection('RequestLineEnd');
7.02           else;
7.02               WrtSection('RequestLinesEnd');
7.02           endif;
7.02
7.02  /End-Free
7.02 c                   if        fdline = w_line
7.02 c                   leave
7.02 c                   endif
7.02  *
7.02 c     fodtlr_key2   reade     fodtlr
7.02 c                   enddo
      *
      * Skriv json fila til IFS området
      *
     c                   eval      IFS_path = '/home/' +
     c                                        l_figr +
     c                                        '/trp_tracking/'
      *
     c                   eval      IFS_Output1 = 'trp' +
     c                                           %trim(%char(w_numm)) +
     c                                           '.request'
      *
     c                   eval      IFS_Output = %trim(IFS_path) +
     c                                           %trim(IFS_Output1)
      *
     c                   callp     WrtHtmlToStmf(IFS_Output: 819)
      *
      * Oppretter headers file
      *
     c                   eval      IFS_Output2 = 'trp' +
     c                                           %trim(%char(w_numm)) +
     c                                           '.request.headers'
      *
     c                   if        chdir(%Trim(IFS_path)) >= 0
     c                   eval      fd = open(IFS_Output2 :
     c                               O_CREAT+O_WRONLY+O_CODEPAGE :
     c                               RW*OWNER + RW*GROUP + R : 1252  )
     c                   if        fd < 0
     c                   goto      avslutt
     c                   endif
     c                   endif
     c                   callp     close(fd)
      *
      * Åpnes igjen for å skrive inn APIKEY
      *
     c                   eval      w_tokeut = %triml('eg-apps-token=' +
     c                                        %trim(w_token))
      *
     c                   eval      fd = open(IFS_Output2 :
     c                               O_WRONLY+O_TEXTDATA)
     c                   if        fd < 0
     c                   goto      avslutt
     c                   endif
     c                   eval      w_size = %size(%trim(w_tokeut))
     c                   callp     write(fd: %addr(w_tokeut): w_size)
     c                   callp     close(fd)
      *
      * Bygger navn på retur file
      *
     c                   eval      IFS_Input = 'trp' +
     c                                           %trim(%char(w_numm)) +
     c                                           '.response'
      *
      * Initierer og kaller webservice
      *
     c                   eval      w_url2 = %trim(w_url) + '/api/shipment'
     c                   eval      API_Output2 = %triml(IFS_path + IFS_Output1)
     c                   eval      API_Input = %triml(IFS_path + IFS_Input)
     c                   eval      w_reqt = 'POST'
     c**                 eval      w_usrn = '           '
     c**                 eval      w_pass = '            '
     c                   eval      w_reqf = API_Output2
     c                   eval      w_rspf = API_Input
     c                   eval      w_enco = 'ISO-8859-1'
     c*                  eval      w_enco = 'UTF-8'
     c                   eval      w_stat = 0
     c                   eval      w_usrn = *blanks
     c                   eval      w_pass = *blanks
      *
     c                   call      'AW702C'
     c                   parm                    w_url2
     c                   parm                    w_reqt
     c                   parm                    w_reqf
     c                   parm                    w_rspf
     c                   parm                    w_usrn
     c                   parm                    w_pass
     c                   parm                    w_enco
     c                   parm                    w_stat
      *
      * Henter ut data fra returfil
      *
     c                   eval      w_coref = *blanks
     c                   eval      IFS_Input = 'trp' +
     c                                           %trim(%char(w_numm)) +
     c                                           '.response'
      *
     c                   eval      API_Input = %triml(IFS_path + IFS_Input)
8.06  * Sjekk om vi har fått returfil
8.06 c                   eval      fd = open(API_Input :
8.06 c                               O_WRONLY+O_TEXTDATA)
8.06 c                   if        fd < 0
8.06 c                   goto      avslutt
8.06 c                   else
8.06 c                   callp     close(fd)
8.06 c                   endif
      *
8.05 c                   eval      IFS_file = %trim(API_input)
8.05  *
8.05  * Prøv å endre ccsid på mottatt tabell
8.05  *
8.05 c                   eval      w_ifsf = *blanks
8.05 c                   call      'AP060C'
8.05 c                   parm                    IFS_file
8.05 c                   parm                    w_ifsf
8.05  *
8.05 c                   if        w_ifsf = *blanks
8.05 c                   eval      p_stat = 'FALSE'
8.05 c                   goto      avslutt
8.05 c                   endif
8.05  *
     c                   eval      fd = open(API_Input :
     c                               O_RDONLY+O_TEXTDATA)
     c                   if        fd < 0
     c                   eval      p_stat = 'FALSE'
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      len = read(fd: %addr(w_indata):
     c                                            %size(w_indata))
      /FREE
      *
      * Partstype
      *
           w_start = %scan('consignmentReference' : w_indata);
8.08   //  w_start = w_start + 23;
8.08       w_start = w_start + 22;
           w_slutt = %scan('"': w_indata : w_start);
           w_lengd = w_slutt - w_start;
           w_coref = %subst(w_indata:w_start : w_lengd);
      *
      /END-FREE
      *
      * Oppretter post med Consignment ref
      *
     c                   eval      ftrpiu_roid = w_toid
     c     ftrpiu_key    chain     ftrpiu
      *
     c                   eval      ftrnum = p_numm
     c                   eval      ftrsuf = p_suff
     c                   eval      ftrcre = w_coref
     c                   eval      ftrsta = 'SUCCESS'
     c                   eval      ftrako = w_akol
      *
     c                   time                    ftreda
     c                   time                    ftreti
     c                   eval      ftreus = l_user
      *
     c                   if        %found
     c                   update    ftrpiur
     c                   else
     c                   eval      ftrfir = p_firm
     c                   eval      ftroid = w_toid
      *
     c                   time                    ftroda
     c                   time                    ftroti
     c                   eval      ftrous = l_user
      *
     c                   write     ftrpiur
     c                   endif
      *
      * Rydder opp i filer på IFS
      *
     c                   eval      fd = open(IFS_Output2 :
     c                               O_WRONLY+O_TEXTDATA)
     c                   if        fd < 0
     c                   goto      avslutt
     c                   endif
      *
      * NBNBNB Temporært fjernet sletting av filer på IFS
      *
8.07 c                   if        1=2
     c                   callp     unlink(IFS_Output1)
     c                   callp     unlink(IFS_Output2)
     c                   callp     unlink(IFS_Input)
8.07 c                   endif
      *
     c                   eval      p_stat = 'TRUE'
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine  - Brukes ved mislykket web-service kall
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      p_stat = 'FALSE'
     c                   goto      avslutt
      *
     c                   endsr
      *
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  * Subrutine for å behandle kalkulering av vekt og volum
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  *
7.02 c     kalk_linje    begsr
7.02  *
7.02 c                   eval      w_vekt = 0
7.02 c                   eval      w_vektv = 0
7.02 c                   eval      w_volu = 0
7.02 c                   eval      w_voluv = 0
7.02  *
7.02 c                   if        fdvare = *blank
7.02 c                   goto      end_kalk
7.02 c                   endif
7.02  *
7.02  * Behandler ikke tjenester og diversevarer
7.02  *
7.02 c                   eval      vvarl1_vare = fdvare
7.02 c     vvarl1_key    chain     vvarl1
7.02 c                   if        not %found
7.02 c     vvarl1_key    chain     vvasl1
7.02 c                   endif
7.02  *
7.10  * Kaller program for henting av varetype
7.10  *
7.02 c                   call      'VL710R'
7.02 c                   parm                    p_firm
7.02 c                   parm                    vvvare
7.02 c                   parm                    fdlage
7.02 c                   parm                    w_vtyp
7.02 c                   parm                    w_utda
7.02 c                   parm                    vvldor
7.02 c                   parm                    b_inlr
7.02 c                   parm                    w_lv_nobb
7.02 c                   parm                    w_lv_modn
7.02 c                   parm                    w_lv_lldo
7.02 c                   parm                    w_lv_llva
7.02  *
7.02 c                   if        w_vtyp = 'T' or
7.02 c                             w_vtyp = 'D'
7.02 c                   goto      end_kalk
7.02 c                   endif
7.02  *
7.02  * Finner vekt og volum
7.02  *
7.02 c                   eval      vvenl1_vare = fdvare
7.02 c                   eval      vvenl1_enhe = fdenh1
7.02 c     vvenl1_key    chain     vvenl1
7.02 c                   if        not %found or
7.02 c                             vevekt = 0 or
7.02 c                             vevolu = 0
7.02 c                   eval      jvpkl2_vare = fdvare
7.02 c                   eval      jvpkl2_ldor = vvldor
7.02 c                   eval      jvpkl2_enhe = fdenh1
7.02 c     jvpkl2_key    chain     jvpkl2
7.02 c                   if        not %found
7.02 c                   eval      jvpkl2_vare = fdvare
7.02 c                   eval      jvpkl2_ldor = vvnlev
7.02 c                   eval      jvpkl2_enhe = fdenh1
7.02 c     jvpkl2_key    chain     jvpkl2
7.02 c                   endif
7.02 c                   endif
7.02  *
7.02 c                   if        vevekt <> 0
7.02 c                   eval      w_vekt = vevekt
7.02 c                   else
7.02 c                   eval      w_vekt = jwvekt / 1000
7.02 c                   endif
7.02  *
7.02 c                   if        vevolu <> 0
7.02 c                   eval      w_volu = vevolu
7.02 c                   else
7.02 c                   eval      w_volu = jwvolu
7.02 c                   endif
7.02  *
7.02  * Summerer vekt og volum
7.02  *
7.02 c                   eval      w_vektv = (w_vekt * fdanta)
7.02 c                   eval      w_voluv = (w_volu * fdanta)
7.02  *
7.02  *
7.02 c     end_kalk      endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Henter inn parameter
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_toid
     c                   parm                    p_2fid
     c                   parm                    p_stat
      *
      * Key for posisjonering på ordrehodet
      *
     c     fohel1_key    klist
     c                   kfld                    p_firm
     c                   kfld                    p_numm
     c                   kfld                    p_suff
      *
      * Key for posisjonering på ordrelinjer
      *
     c     fodtlr_key    klist
     c                   kfld                    p_firm
     c                   kfld                    p_numm
     c                   kfld                    p_suff
     c                   kfld                    fodtlr_line
      *
      * Key for posisjonering på ordrelinjer
      *
     c     fodtlr_key2   klist
     c                   kfld                    p_firm
     c                   kfld                    p_numm
     c                   kfld                    p_suff
      *
      * Key for posisjonering på fraktregister
      *
     c     ftrpi2_key    klist
     c                   kfld                    p_firm
     c                   kfld                    ftrpi2_rnum
     c                   kfld                    ftrpi2_rsuf
      *
      * Key for posisjonering på fraktregister
      *
     c     ftrpiu_key    klist
     c                   kfld                    p_firm
     c                   kfld                    ftrpiu_roid
      *
      * Key for posisjonering på ordrehodet
      *
     c     rkunl1_key    klist
     c                   kfld                    p_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for posisjonering på avdeling
      *
7.03 c     ra10l1_key    klist
7.03 c                   kfld                    p_firm
7.03 c                   kfld                    ra10l1_jkod
7.01  *
7.01  * Key for oppslag på loggfil
7.01  *
7.01 c     floglr_key    klist
7.01 c                   kfld                    p_firm
7.01 c                   kfld                    floglr_aarr
7.01 c                   kfld                    floglr_numm
7.01 c                   kfld                    floglr_suff
7.02  *
7.02  * Key for posisjonering på vareregister
7.02  *
7.02 c     vvarl1_key    klist
7.02 c                   kfld                    p_firm
7.02 c                   kfld                    vvarl1_vare
7.02  *
7.02  * Key for les av enhet
7.02  *
7.02 c     vvenl1_key    klist
7.02 c                   kfld                    p_firm
7.02 c                   kfld                    vvenl1_vare
7.02 c                   kfld                    vvenl1_enhe
7.02  *
7.02  * Key for oppslag på vare-pakning (VA)
7.02  *
7.02 c     jvpkl2_key    klist
7.02 c                   kfld                    jvpkl2_vare
7.02 c                   kfld                    jvpkl2_ldor
7.02 c                   kfld                    jvpkl2_enhe
7.04  *
7.04  * Key for les av kundeprosjekt
7.04  *
7.04 c     fkprl1_key    klist
7.04 c                   kfld                    p_firm
7.04 c                   kfld                    fkprl1_kund
7.04 c                   kfld                    fkprl1_kpro
8.02  *
8.02  * Key for posisjonering på transportinfo pr lager
8.02  *
8.02 c     ftrli1_key    klist
8.02 c                   kfld                    p_firm
8.02 c                   kfld                    ftrli1_llag
8.03  *
8.03  * Key for posisjonering på ordretyper
8.03  *
8.03 c     vot1l1_key    klist
8.03 c                   kfld                    p_firm
8.03 c                   kfld                    vot1l1_otyp
      *
      * Henter inn løpenummer teller
      *
     c                   eval      w_kode = '0'
     c                   eval      w_fell = 'NXTKLI'
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
     c                   eval      w_numm = *zero
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    p_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      /Free
      *
      * Henter inn verdier for å bygge opp streng til json
      *
      * Henter inn evnt bruker
      *
       exec sql
        select afudss
        into   :w_usrn
        from   afpspf
        where  afufil = :l_filg and
               afuide = 'WSSOKUSR';

      *
      * Henter inn passord til bruker
      *
       exec sql
        select afudss
        into   :w_pass
        from   afpspf
        where  afufil = :l_filg and
               afuide = 'WSSOKPASS';

      *
      * Henter inn URL hvor tjenesten ligger
      *
       exec sql
        select afudss
        into   :w_url
        from   afpspf
        where  afufil = :l_filg and
               afuide = 'cldTrpUrl';

      *
      * Henter inn mal for requst
      *
       exec sql
        select afudss
        into   :w_mal
        from   afpspf
        where  afufil = :l_filg and
               afuide = 'trp_reques';

      *
      * Henter inn appkey fra COMPANY_SETTINGS
      *
       exec sql
        select aposkval
        into   :w_token
        from   aposextnst
        where  aposfirm = :p_firm and
               apossyst = 'EGAPPS' and
               aposckey = 'CLOUD_API_KEY';

      *
      * Henter inn Flåteidentitet
      *
       exec sql
        select afudss
        into   :w_trid
        from   afpspf
        where  afufil = :l_filg and
               afuide = 'trp_tracid';

      /End-Free
      *
     c                   if        %trim(p_2fid) <> *blanks
     c                   eval      w_trid = p_2fid
     c                   endif
     c                   eval      w_toid = p_toid
     c                   time                    w_date
8.04 c                   eval      w_date_cnv = %char(w_date)
8.03  *
8.03  * Hent inn info fra ordren.
8.03  *
8.03 c     fohel1_key    chain     fohel1
8.03 c                   if        not %found
8.03 c                   eval      folage = 0
8.03 c                   endif
8.03  *
8.03  * Test om internordre skal overføres.
8.03  *
8.03   u_inte = *off;
8.03  *
8.03   CallP CO402R(l_filg:p_firm:0:'AP055_INTERNORDRE_LAGER':
8.03                    co402_verdi1:co402_verdi2);
8.03   if co402_verdi1 = '1';
8.03     u_inte = *on;
8.03   endif;
8.03  *
8.03   CallP CO402R(l_filg:p_firm:folage:'AP055_INTERNORDRE_LAGER':
8.03                    co402_verdi1:co402_verdi2);
8.03   if co402_verdi1 = '1';
8.03     u_inte = *on;
8.03     else;
8.03     u_inte = *off;
8.03   endif;
8.03  *
8.04   CallP CO402R(l_filg:p_firm:0:'AP055_KONVERTER_TEGN':
8.04                    co402_verdi1:co402_verdi2);
8.04   if co402_verdi1 = '1';
8.04     u_konv = *on;
8.04   endif;
8.04  *
8.04 c                   if        u_konv = *on
8.04  /FREE
8.04      w_date_cnv = %ScanRpl('-':':':w_date_cnv:1);
8.04  /END-FREE
8.04 c                   endif
8.04  *
     c                   endsr
