     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSHOP
      * Program . . . . : BU730R
      * Beskrivelse . . : Butikk, eksport av kundeinformasjon
      *
      *                   Tar ikke med sperrede og passive kunder
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       220304 HKO  Nytt program
6.20  * 6.20  140611 bsa  memosaldo er lagt ut på eget register
6.30  * 6.30  150914 BKV  Henter nye koder fra kundeprosjekt
6.31  * 6.30  020816 bof  Skal alltid ta med kontantkunde, selv om sperret
7.01  * MGR   151216 sst  Ber flytende Memosaldo m/ordre pr ant. dager
7.01  *       170117 sst  Hente parameter meosaldo fra AFPSPF
8.01  *       221123 sst  Flyttet ant memo-dager og utv.kreditgrense til  UTVIDET_MEMOSALDO (nx)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
6.20 frkmel1    if   e           k disk    rename(rkmepfr:rkmel1r)
     frksol1    if   e           k disk    rename(rksopfr:rksol1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     ffkpsl1    if   e           k disk    rename(fkpspfr:fkpsl1r)
     fapospf    if   e           k disk    rename(apospfr:apospfr)
     fra06l1    if   e           k disk    rename(ra06pfr:ra06l1r)
     fra11l1    if   e           k disk    rename(ra11pfr:ra11l1r)
     frksal1    if   e           k disk    rename(rksapfr:rksal1r)
     frmknl2    if   e           k disk    rename(rmknpfr:rmknl2r)
6.31 fbstsl1    if   e           k disk    rename(bstspfr:bstsl1r)
8.01 f*afpslr    if   e           k disk    rename(afpspfr:afpslrr)
      *
     fbokust    uf a e           k disk    rename(bokust:bokustr)
     fbokpst    uf a e           k disk    rename(bokpst:bokpstr)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(rkfirm)
     d p_dato          s               d   datfmt(*iso)
     d p_time          s               t   timfmt(*iso)
      *
      * Definering av variabler i nøkler
      *
     d rksol1_skun     s                   like(rkskun)
     d fkprl1_kund     s                   like(flkund)
     d fkpsl1_skun     s                   like(flskun)
     d fkpsl1_skpr     s                   like(flskpr)
     d apospf_ponr     s                   like(apponr)
     d ra06l1_fkod     s                   like(rafkod)
     d ra11l1_kkod     s                   like(rakkod)
     d rksal1_saku     s                   like(rksaku)
     d rmknl2_xknr     s                   like(rmxknr)
6.20 d rkmel1_kund     s                   like(rkkund)
8.01 d*afpslr_ufil     s                   like(l_filg)
8.01 d*afpslr_uide     s                   like(afuide)
      *
      * Definering av variable
      *
     d w_firm          s                   like(rkfirm)
7.01  *
7.01  *w_utvgrns = utvidelse av memosaldo(Mestergruppen)
7.01 d w_utvgrns       s              2  0
7.01  *w_memodagr = antall dager ordre skal med i memosaldo
7.01 d w_memodagr      s              3  0
7.01 d w_3x            s              3
7.01 d p_fir           s              3
7.01 d p_kun           s              6
      *
7.01 d u_kmem          s              1    inz(*off)                            Kredit og memodager
      *
7.01 d                uds
7.01 d  l_filg               931    933
      *
7.01   dcl-s co402_verdi1  char(1);
7.01   dcl-s co402_verdi2  char(2048);
7.01   DCL-PR CO402R EXTPGM('CO402R');
7.01     p_filgrp            char(3)     const;
7.01     p_firm              packed(3:0) const;
7.01     p_lager             packed(2:0) const;
7.01     p_nokkel            char(50)    const;
7.01     p_verdi1            char(1);
7.01     p_verdi2            char(2048);
7.01   END-PR;
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer filer
      *
     c                   clear                   bokustr
     c                   clear                   bokpstr
      *
     c                   eval      firm = w_firm
      *
      * Behandler kunde og kundeprosjekt
      *
     c     rkunl1_key    setll     rkunl1
     c     rkunl1_key    reade     rkunl1
     c                   dow       %eof = *off
      *
6.31 c                   if        baakun = 0 or
6.31 c                             (baakun <> 0 and
6.31 c                              baakun <> rkkund)
     c                   if        rksprk <> ' ' or
     c                             rkpass <> ' '
     c                   goto      neste_kunde
     c                   endif
6.31 c                   endif
      *
     c                   if        p_dato <> *loval
     c                   if        p_dato > rkedat or
     c                             p_dato = rkedat and
     c                             p_time > rketim
     c                   goto      neste_kunde
     c                   endif
     c                   endif
      *
     c                   eval      kund = rkkund
     c                   eval      navn = rknavn
     c                   eval      adr1 = rkgate
     c                   eval      adr2 = rkadr2
     c                   eval      ponr = %subst(rksted:1:4)
     c                   eval      sted = %subst(rksted:6:25)
     c                   eval      kprs = rkpers
     c                   eval      tlfn = rktlfn
     c                   eval      mobn = rkmobn
     c                   eval      tlfx = rktlfx
     c                   eval      mail = rkemal
     c                   eval      orgn = rkftnr
     c                   eval      eanl = rkeank
     c                   eval      faku = rkfaku
     c                   eval      koku = rkkoku
     c                   eval      avde = rkavde
     c                   eval      lage = rklagr
     c                   eval      selg = rkslgr
     c                   eval      dist = rkdist
     c                   eval      kjor = rkkjØr
     c                   eval      kjnr = rkkjnr
     c                   eval      kkat = rkkatg
     c                   eval      kkbe = *blank
     c                   eval      rabo = rkorab
     c                   eval      rkat = rkrabk
     c                   eval      rkbe = *blank
     c                   eval      betb = rkbetb
     c                   eval      betm = rkbetm
     c                   eval      land = rkland
     c                   eval      valu = rkvalk
     c                   eval      kmva = rkmkod
     c                   eval      alme = 0
     c                   eval      kfgb = rkfagb
     c                   eval      sekv = rksekv
     c                   eval      kres = rkkres
     c                   eval      grns = rkgrns
     c                   eval      sald = rksald
6.20 c**                 eval      mems = rkmems
     c                   eval      spa1 = *blank
     c                   eval      spa2 = *blank
     c                   eval      spa3 = *blank
     c                   eval      stek = *blank
     c                   eval      odat = rkedat
     c                   eval      edat = rkedat
     c                   eval      etim = rketim
     c                   eval      eusr = rkesig
      *
      * Rabattkategori
      *
     c                   if        rkrabk <> 0
     c                   eval      ra06l1_fkod = rkrabk
     c     ra06l1_key    chain     ra06l1
     c                   if        %found
     c                   eval      rkbe = raftxt
     c                   endif
     c                   endif
      *
      * Kundekategori
      *
     c                   if        rkkatg <> 0
     c                   eval      ra11l1_kkod = rkkatg
     c     ra11l1_key    chain     ra11l1
     c                   if        %found
     c                   eval      kkbe = raktxt
     c                   endif
     c                   endif
      *
      * Spesialavtale
      *
     c                   eval      rksal1_saku = rkkund
     c     rksal1_key    chain     rksal1r
     c                   if        %found
     c                   eval      spa1 = rksat1
     c                   eval      spa2 = rksat2
     c                   eval      spa3 = rksat3
     c                   endif
      *
      * Almenning
      *
     c                   eval      rmknl2_xknr = rkkund
     c     rmknl2_key    chain     rmknl2
     c                   if        %found
     c                   eval      alme = rmxalm
     c                   endif
      *
      * Søketekst
      *
     c                   eval      rksol1_skun = rkkund
     c     rksol1_key    chain     rksol1
     c                   if        %found
     c                   eval      stek = %trim(rkstek)
     c                   endif
7.01  *
7.01  * Beregne memosaldo pr dato, Mestergruppen
7.01 c                   if        u_kmem = *on
7.01 c                   if        w_memodagr > 0
7.01 c                   move      w_firm        p_fir
7.01 c                   move      rkkund        p_kun
7.01 c                   call      'FO763R'
7.01 c                   parm                    p_fir
7.01 c                   parm                    p_kun
7.01 c                   endif
7.01 c                   endif
6.20  * Henter inn informasjon om memosaldo
6.20 c                   eval      rkmel1_kund = rkkund
6.20 c     rkmel1_key    chain     rkmel1
6.20 c                   if        %found
6.20 c                   eval      mems = rkmems
6.20 c                   else
6.20 c                   eval      mems = *zeros
6.20 c                   endif
6.20  *
      * Skriver til fil
      *
     c                   write     bokustr
      *
      * Kundeprosjekt
      *
     c                   exsr      kundeprosj
      *
     c     neste_kunde   tag
      *
     c     rkunl1_key    reade     rkunl1
     c                   enddo
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av kundeprosjekt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kundeprosj    begsr
      *
     c                   eval      fkprl1_kund = rkkund
     c     fkprl1_key    setll     fkprl1
     c     fkprl1_key    reade     fkprl1
     c                   dow       %eof = *off
      *
     c                   eval      kund = flkund
     c                   eval      kpro = flkpro
     c                   eval      fdat = flfdat
     c                   eval      tdat = fltdat
     c                   eval      navn = flnavn
     c                   eval      adr1 = fladr1
     c                   eval      adr2 = fladr2
     c                   eval      ponr = *blank
     c                   eval      sted = *blank
     c                   eval      kprs = flkprs
     c                   eval      tlfa = fltlfa
     c                   eval      tlfp = fltlfp
     c                   eval      mobn = fltlfm
     c                   eval      mail = flmail
     c                   eval      selg = flselg
     c                   eval      priv = flpriv
     c                   eval      kjor = flkjor
     c                   eval      kjnr = flkjnr
     c                   eval      proj = flproj
     c                   eval      orka = florka
     c                   eval      okun = flokun
     c                   eval      okpr = flokpr
     c                   eval      opda = flopda
     c                   eval      stek = *blank
6.30 c                   eval      betb = flbetb
6.30 c                   eval      sekv = flsekv
6.30 c                   eval      kmva = flmkod
6.30 c                   eval      kfgb = flfagb
     c                   eval      odat = fledat
     c                   eval      edat = fledat
     c                   eval      etim = fletim
     c                   eval      eusr = fleusr
      *
      * Postadresse
      *
     c                   movel     flponr        ponr
      *
     c                   if        flponr <> 0
     c                   eval      apospf_ponr = flponr
     c     apospf_key    chain     apospf
     c                   if        %found
     c                   eval      sted = apsted
     c                   endif
     c                   endif
      *
      * Søketekst
      *
     c                   eval      fkpsl1_skun = flkund
     c                   eval      fkpsl1_skpr = flkpro
     c     fkpsl1_key    chain     fkpsl1
     c                   if        %found
     c                   eval      stek = %trim(flstek)
     c                   endif
      *
      * Skriver til fil
      *
     c                   write     bokpstr
      *
     c     fkprl1_key    reade     fkprl1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameter
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_dato
     c                   parm                    p_time
      *
      * Key for les på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på kunde-søketekst
      *
     c     rksol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rksol1_skun
      *
      * Key for les på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
      *
      * Key for oppslag på kundeprosjekt-søketekst
      *
     c     fkpsl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkpsl1_skun
     c                   kfld                    fkpsl1_skpr
      *
      * Key for oppslag på poststed-register
      *
     c     apospf_key    klist
     c                   kfld                    apospf_ponr
      *
      * Key for oppslag på rabattkategori
      *
     c     ra06l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra06l1_fkod
      *
      * Key for oppslag på kundekategori
      *
     c     ra11l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra11l1_kkod
      *
      * Key for oppslag på spesialavtale
      *
     c     rksal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rksal1_saku
      *
      *
      * Key for oppslag på almenning, eiendomsknytning
      *
     c     rmknl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rmknl2_xknr
6.20  *
6.20  * Key for oppslag på memosaldo
6.20  *
6.20 c     rkmel1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    rkmel1_kund
6.31  *
6.31  * Key for oppslag på status
6.31  *
6.31 c     bstsl1_key    klist
6.31 c                   kfld                    w_firm
7.01  *
7.01  * Key for propertiesfil
7.01  *
8.01 c*    afpslr_key    klist
8.01 c*                  kfld                    afpslr_ufil
8.01 c*                  kfld                    afpslr_uide
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
6.31  *
6.31  * leser status
6.31  *
6.31 c     bstsl1_key    chain     bstsl1
      *
7.01  *
7.01  * NOBB frakt på direkte leverte ordre
7.01  *
7.01  *   Hent inn % utvidelse av kreditgrense
7.01  *   Hent inn ant dager fram ordre skal med i memosaldo
8.01  *
8.01 c                   eval      w_memodagr = 999
8.01 c                   eval      w_utvgrns  = 0
8.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'UTVIDET_MEMOSALDO':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_kmem = *on;
8.01     if  %subst(co402_verdi2:1:3) <> *blank;
8.01       w_memodagr = %dec(%subst(co402_verdi2:1:3):3:0);
8.01     endif;
8.01     if  %subst(co402_verdi2:133:2) <> *blank;
8.01       w_utvgrns = %dec(%subst(co402_verdi2:133:2):2:0);
8.01     endif;
7.01   endif;
7.01  *
7.01  *
7.01  *   Hent inn % utvidelse av kreditgrense
8.01 c*                  eval      afpslr_ufil = l_filg
8.01 c*                  eval      afpslr_uide = 'KRGRNSUTV'
8.01 c*    afpslr_key    chain     afpslrr
8.01 c*                  if        %found(afpslr)
8.01 c*                  movel     afudss        w_utvgrns
8.01 c*                  endif
7.01  *
7.01  *   Hent inn ant dager fram ordre skal med i memosaldo
8.01 c*                  eval      afpslr_uide = 'MEMODAGER'
8.01 c*    afpslr_key    chain     afpslrr
8.01 c*                  if        %found(afpslr)
8.01 c*                  movel     afudss        w_3x
8.01 c*                  move      w_3x          w_memodagr
8.01 c*                  endif
7.01  *
     c                   endsr
