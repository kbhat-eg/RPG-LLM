     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LD505R
      * Beskrivelse . . : Bestillingslinje-linje, vis
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
5.60  *       030108 bof  Hvis eannr ikke finnes - prøver VA-vare
6.11  * 6.10  130709 bof  Utvidet eannr til 14 og brukt std. oppslag
6.20  * 6.20  210415 bsa  Henter GTIN nummer fra varelinja
6.nu  * 6.30  100614 bof  Gjennomgått mtp. nummerutvidelse
6.30  * 6.30  221014 bsa  Innfører F tast for vsining av NOBB info
6.31  * 6.30  121018 bof  Utvidet med trelast-sortiment
7.00  * 6.30  021019 bkv  Utvidet skjerm
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     flusrl1    if   e           k disk    rename(lusrpfr:lusrl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fjvarlr    if   e           k disk    rename(jvarpfr:jvarlrr)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
      *
     fld505d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av parametre
      *
     d p_firm          s                   like(ldfirm)
     d p_numm          s                   like(ldnumm)
     d p_suff          s                   like(ldsuff)
     d p_line          s                   like(ldline)
6.30 d p_nobb          s              8
      *
      * Definering av variabler i nøkler
      *
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d lodtl1_line     s                   like(ldline)
     d lusrl1_user     s                   like(lbuser)
     d rlevl1_levr     s                   like(rllevr)
     d vvarl1_vare     s                   like(vvvare)
     d jvarlr_vare     s                   like(jvvare)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
      *
      * Definering av variable
      *
     d w_firm          s                   like(ldfirm)
     d w_raba          s                   like(ldrab1)
     d w_net1          s                   like(ldsumi)
     d w_net2          s                   like(ldsumi)
6.11 d w_eann          s             14  0
6.11 d w_east          s              1
6.31 d w_lv_modn       s                   like(vvnmod)
      *
6.31 C/Exec SQL
6.31 C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
6.31 C/End-Exec
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle bildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer skjermbildet
      *
     c                   eval      lohel1_numm = p_numm
     c                   eval      lohel1_suff = p_suff
     c     lohel1_key    chain     lohel1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      lodtl1_numm = p_numm
     c                   eval      lodtl1_suff = p_suff
     c                   eval      lodtl1_line = p_line
     c     lodtl1_key    chain     lodtl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      vvarl1_vare = ldvare
     c     vvarl1_key    chain     vvarl1
      *
     c                   eval      w_eann = 0
6.11  *
6.11  * Kaller program for henting av ean-nr
6.11  *
6.11 c                   call      'VE711R'
6.11 c                   parm                    w_firm
6.11 c                   parm                    ldvare
6.11 c                   parm                    ldenh1
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_east
     c                   eval      w_eann = w_eann
     c                   if        w_eann = 0
     c                   eval      jvarlr_vare = ldvare
     c     jvarlr_key    chain     jvarlr
     c                   if        %found(jvarlr)
     c                   eval      w_eann = jvean1
     c                   endif
     c                   endif
6.31  * sjekker om logistikk-vare
6.31 c                   eval      w_lv_modn = 0
6.31 c/exec sql
6.31 c+ select vvlmod
6.31 c+ into   :w_lv_modn
6.31 c+ from   vvlepf
6.31 c+ where  vvlvnr = :ldvare and
6.31 c+        vvlldo = :loldor
6.31 c+ fetch first 1 rows only
6.31 c/end-exec
      *
     c                   if        lobdat <> *loval
     c     *dmy          move      lobdat        b1bdat
     c                   endif
      *
     c                   if        ldldat <> *loval
     c     *dmy          move      ldldat        b1ldat
     c                   else
     c                   if        loldat <> *loval
     c     *dmy          move      loldat        b1ldat
     c                   endif
     c                   endif
      *
     c                   eval      b1numm = ldnumm
     c                   eval      b1otyp = lootyp
     c                   eval      b1ltyp = ldltyp
     c                   eval      b1vare = ldvare
     c                   eval      b1tek1 = ldtek1
     c                   eval      b1tek2 = ldtek2
     c                   eval      b1ogrp = ldogrp
     c                   eval      b1hgrp = ldhgrp
     c                   eval      b1ugrp = ldugrp
     c                   eval      b1utxt = *blank
     c                   eval      b1ldor = ldldor
     c                   eval      b1lnav = *blank
     c                   eval      b1lvar = ldlvar
     c                   eval      b1nobb = ldnobb
6.31 c                   if        w_lv_modn <> 0
6.31 c                   eval      b1nmod = w_lv_modn
6.31 c                   else
     c                   eval      b1nmod = vvnmod
6.31 c                   endif
6.20 c                   if        ldeann = 0
     c                   eval      b1eann = w_eann
6.20 c                   else
6.20 c                   eval      b1eann = ldeann
6.20 c                   endif
     c                   eval      b1anta = ldanta
     c                   eval      b1enh1 = ldenh1
     c                   eval      b1lety = ldlety
     c                   eval      b1kplj = ldkplj
     c                   eval      b1kopr = ldkpny
     c                   eval      b1inpr = ldipny
     c                   eval      b1rab1 = ldrab1
     c                   eval      b1rab2 = ldrab2
     c                   eval      b1rkr1 = 0
     c                   eval      b1rkr2 = 0
      *
      * Linjerabatt
      *
     c                   eval(h)   b1rkr1 = ldipny * ldrab1 / 100
     c                   eval(h)   w_net1 = ldipny - b1rkr1
     c                   eval(h)   b1rkr2 = w_net1 * ldrab2 / 100
      *
     c                   eval      b1nett = ldipny - b1rkr1 - b1rkr2
      *
      * Legger inn priser totalt
      *
     c                   eval      b1sumk = ldsumk
     c                   eval      b1sumi = ldsumi
     c                   eval      b1srk1 = 0
     c                   eval      b1srk2 = 0
      *
      * Linjerabatt
      *
     c                   eval(h)   b1srk1 = ldsumi * ldrab1 / 100
     c                   eval(h)   w_net2 = ldsumi - b1srk1
     c                   eval(h)   b1srk2 = w_net2 * ldrab2 / 100
      *
     c                   eval      b1sumn = ldsumi - b1srk1 - b1srk2
      *
      * Kostpriser og DG skal kun vises dersom dette er tillatt for bruker
      *
     c                   if        lbko01 = 0
     c                   eval      b1kopr = 0
     c                   eval      b1sumk = 0
     c                   endif
      *
      * Hent navn på leverandør
      *
     c                   eval      rlevl1_levr = ldldor
     c     rlevl1_key    chain     rlevl1
     c                   if        %found
     c                   eval      b1lnav = rlnavn
     c                   endif
      *
      * Hent beskrivelse av varegruppe
      *
     c                   eval      vugrl1_uogr = ldogrp
     c                   eval      vugrl1_uhgr = ldhgrp
     c                   eval      vugrl1_uugr = ldugrp
     c     vugrl1_key    chain     vugrl1
     c                   if        %found
     c                   eval      b1utxt = vgutxt
     c                   endif
      *
      * Behandler bildet
      *
     c     b1taga        tag
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   endsl
6.30  *
6.30  * F18=NOBB informasjon
6.30  *
6.30 c                   if        *inks = *on
6.30 c                   exsr      vis_nobb
6.30 c                   goto      b1taga
6.30 c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  * Vis NOBB informasjon for varen. Samme prinsipp som preview av utskrifter
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  *
6.30 c     vis_nobb      begsr
6.30 c
6.30 c                   eval      p_nobb = %char(b1nobb)
6.30 c                   call      'AP633R'
6.30 c                   parm                    p_nobb
6.30  *
6.30 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_line
      *
      * Key for oppslag på ordre-hode
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      * Key for oppslag på ordre-linje
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
     c                   kfld                    lodtl1_line
      *
      * Key for oppslag på bruker
      *
     c     lusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lusrl1_user
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på vare i VA
      *
     c     jvarlr_key    klist
     c                   kfld                    jvarlr_vare
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
      * Hente inn brukerregister
      *
     c                   eval      lusrl1_user = l_user
     c     lusrl1_key    chain     lusrl1r
      *
      *
     c                   endsr
