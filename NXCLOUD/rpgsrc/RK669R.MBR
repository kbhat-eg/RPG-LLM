     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK669R                                              *
      * Beskrivelse . . : Oppdatering av rentedato/rentekode                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * R5.40     080208  Når generalnota, merkes hver enkelt faktura    HFL  *
6.10  * V6.10     220909  Lagt inn sporingsfelter                        BSA  *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frkwbl2    if   e           k disk    rename(rkwbpfr:rkwbl2r)
     f                                     prefix(x:1)
T5.40frktrl5    if   e           k disk    rename(rktrpfr:rktrl5r)
     frktrlu    uf   e           k disk    rename(rktrpfr:rktrlur)
     fraa6lu    uf   e           k disk    rename(raa6pfr:raa6lur)
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_navn                951    980
      *
     d rktrlu_kund     s                   like(rnkund)
     d rktrlu_bdat     s                   like(rnbdat)
     d rktrlu_tist     s                   like(rntist)
T5.40d rktrl5_samf     s                   like(rnsamf)
      *
     d w_firm          s                   like(rnfirm)
     d w_rdat          s                   like(rnrdat)
     d w_renk          s                   like(rnrenk)
      *
      *  Les posteringer
      *
     c     rkwbl2_key    setll     rkwbl2r                                61
     c     rkwbl2_key    reade     rkwbl2r                                61
      *
     c                   dow       *in61 = *off
      *
      *  Oppdater rentedato
      *
     c                   eval      w_rdat = xwrtda
     c                   eval      w_renk = xnrenk
      *
T5.40 * Dersom generalnota, finn  og oppdater enkeltfaktura
      *
T5.40c                   if        xnsamf <> *zero
T5.40c                   exsr      gennot
T5.40c                   goto      neste
T5.40c                   endif
      *
     c                   eval      rktrlu_kund = xnkund
     c                   eval      rktrlu_bdat = xnbdat
     c                   eval      rktrlu_tist = xntist
     c     rktrlu_key    chain     rktrlu
      *
     c                   if        %found
     c                   eval      rnrdat = w_rdat
     c                   eval      rnrenk = w_renk
6.10 c                   time                    rnedat
6.10 c                   time                    rnetim
6.10 c                   eval      rneusr = l_user
     c                   update    rktrlur
     c                   endif
      *
T5.40c     neste         tag
     c     rkwbl2_key    reade     rkwbl2r                                61
     c                   enddo
      *
      *  Oppdater rentedato - statuskode 6
      *
     c     raa6lu_key    chain     raa6lu
      *
     c                   if        %found
     c                   eval      ra6pda = w_rdat
     c                   update    raa6lur
     c                   endif
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
T5.40 * Subrutine for oppdatering av fakturaene i en generalnota        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
T5.40c     gennot        begsr
      *
     c                   eval      rktrl5_samf = xnsamf
     c     rktrl5_key    setll     rktrl5r                                62
     c     rktrl5_key    reade     rktrl5r                                62
      *
     c                   dow       *in62 = *off
      *
      * Sjekk om generalnota på riktig år
      *
     c                   if        xnraar <> rnraar
     c                   goto      neste2
     c                   endif
      *
     c                   eval      rktrlu_kund = rnkund
     c                   eval      rktrlu_bdat = rnbdat
     c                   eval      rktrlu_tist = rntist
     c     rktrlu_key    chain     rktrlu
      *
     c                   if        %found
     c                   eval      rnrdat = w_rdat
     c                   eval      rnrenk = w_renk
6.10 c                   time                    rnedat
6.10 c                   time                    rnetim
6.10 c                   eval      rneusr = l_user
     c                   update    rktrlur
     c                   endif
      *
     c     neste2        tag
     c     rktrl5_key    reade     rktrl5r                                62
     c                   enddo
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      *
     c                   eval      w_firm = l_firm
      *
      * Key for kundeposteringer
      *
     c     rkwbl2_key    klist
     c                   kfld                    w_firm
      *
      * Key for kundeposteringer
      *
     c     rktrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrlu_kund
     c                   kfld                    rktrlu_bdat
     c                   kfld                    rktrlu_tist
      *
      * Key for statuskode 6
      *
     c     raa6lu_key    klist
     c                   kfld                    w_firm
      *
T5.40 * Key for kundeposteringer
      *
T5.40c     rktrl5_key    klist
T5.40c                   kfld                    w_firm
T5.40c                   kfld                    rktrl5_samf
      *
     c                   endsr
