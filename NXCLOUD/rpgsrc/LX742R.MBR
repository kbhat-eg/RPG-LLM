     h option(*nodebugio : *srcstmt) datedit(*dmy)
      /TITLE  Spesial program - lageroppdatering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System  . . . . : ASLAGR
      *  Program . . . . : LX742R
      *  Beskrivelse . . : Oppdatering av informasjon på lagerregister
      *                    fra ekstern fil.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. : Excel-fil med lagerinfo
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      * --------- ------  --------------------------------------------------
      *           040619  Nytt program  (kompi lx701r sgr)         bkv
7.01  *           130220  Takle at det ikke er semikolon bak       bof
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flwexpf    if   e           k disk
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvlaglu    uf   e           k disk    rename(vlagpfr:vlaglur)
      *
      *
     d  d_lage         s              2
     d  d_vare         s              8
     d  d_plas         s              8
     d  d_pla1         s              8
     d  d_pla2         s              8
      *
      *  UDS Local Data Area
      *
     d                uds
     d l_firm                944    946  0
     d l_user                911    920
      *
      *  Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vlaglu_lage     s                   like(vllage)
     d vlaglu_vare     s                   like(vlvare)
      *
      *  Definering av variable
      *
     d w_firm          s                   like(vvfirm)
     d w_posi          s              2  0
      *
     d b_skaf          s              1    inz(*off)
     d b_akti          s              1    inz(*off)
     d b_ok            s              1    inz(*off)
      *
     d digits          c                   ' 0123456789'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Lese ekstern fil for å hente neste vare for behandling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      *  Lese file
      *
     c                   read      lwexpf                                 90
     c                   dow       *in90 = *off
     c                   exsr      utled_ds
      *
      *  Sjekk at ikke heading-rad er lest inn
      *
     c                   if        b_ok = *on
     c                   exsr      behand
     c                   endif
      *
     c                   read      lwexpf                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppdatering av LAGER-informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behand        begsr
     c                   eval      b_akti = *off
     c                   eval      b_skaf = *off
      *
     c     ' ':'0'       xlate     d_vare        d_vare
      *
     c                   eval      vvarl1_vare = d_vare
     c     vvarl1_key    chain     vvarl1r
     c                   if        %found(vvarl1)
      *
     c                   eval      vlaglu_vare = vvvare
     c                   move      d_lage        vlaglu_lage
     c     vlaglu_key    chain     vlaglur
     c                   if        %found(vlaglu)
      *
     c                   eval      vlplas = d_plas
     c                   eval      vlpla1 = d_pla1
     c                   eval      vlpla2 = d_pla2
      *
     c                   time                    vledat
     c                   time                    vletim
     c                   eval      vleusr = l_user
     c                   update    vlaglur
     c                   endif
      *
     c                   endif
     c     behand_ut     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utleding av datastruktur
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utled_ds      begsr
      *
     c     '"':' '       xlate     exclin        exclin
      *
      * Lager
      *
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_lage = %trim(%subst(exclin:1:w_posi-1))
     c                   if        d_lage = *blank
     c                   leavesr
     c                   endif
     c                   if        %check(digits : d_lage) <>0
     c                   leavesr
     c                   endif
      *
      * vare
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_vare = %trim(%subst(exclin:1:w_posi-1))
     c                   if        d_vare = *blank
     c                   leavesr
     c                   endif
     c                   if        %check(digits : d_vare) <>0
     c                   leavesr
     c                   endif
      *
      * Plass
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_plas = %trim(%subst(exclin:1:w_posi-1))
      *
      * Plass 1
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
7.01 c                   if        w_posi > 0
     c                   eval      d_pla1 = %trim(%subst(exclin:1:w_posi-1))
7.01 c                   else
7.01 c                   eval      d_pla1 = %trim(%subst(exclin:1:8))
7.01 c                   endif
      *
      * Plass 2
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
7.01 c                   if        w_posi > 0
     c                   eval      d_pla2 = %trim(%subst(exclin:1:w_posi-1))
7.01 c                   else
7.01 c                   eval      d_pla2 = %trim(%subst(exclin:1:8))
7.01 c                   endif
      *
     c                   eval      b_ok = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *  Key for oppdatering av LAGER
      *
     c     vlaglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglu_vare
     c                   kfld                    vlaglu_lage
      *
      *  Legge inn opplysninger i nøklene
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
