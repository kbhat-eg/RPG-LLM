     h option(*nodebugio) decedit('0,') datedit(*dmy.)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FO647R
      * Beskrivelse . . : Utskrift av kjøre-liste - sammendrag
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *                   Nytt program
      *       040805 HKO  Henter volum- og vekt-informasjon fra enhet
      * 5.50  090307 KOB  Benytter leveringsdato i stedet for ordredato
      * 5.50  090307 KOB  Skriver ut ordretype
      * 5.50  090307 KOB  Endret bredden på utskriften for å få plass til hele navnet
5.53  * R5.53 230307 AMD  Utvidet sorteringsbegrep slik at både Navn og
      *                   Adresse 1 kan skape brudd
6.nu  * R6.nu 190614 bof  Endret i hht. nummerutvidelse
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffpsol1    if   e           k disk    rename(fpsopfr:fpsol1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     faforl1    if   e           k disk    rename(aforpfr:aforl1r)
      *
     ffo647p    o    e             printer
      *
     d                 ds
      *                                                      Parameter-inn
     d ldprec                        64
     d  ldotyp                        2    overlay(ldprec)
     d  ldfdat                       10d   datfmt(*iso)
     d                                     overlay(ldprec:03)
     d  ldtdat                       10d   datfmt(*iso)
     d                                     overlay(ldprec:13)
     d  ldfavd                        4  0 overlay(ldprec:23)
     d  ldtavd                        4  0 overlay(ldprec:27)
     d  ldfkjo                        2  0 overlay(ldprec:31)
     d  ldtkjo                        2  0 overlay(ldprec:33)
     d  ldvari                        1  0 overlay(ldprec:35)
      *
      * - - - - - - - - - - - - - -
      * Henter inn data fra LDA
      * - - - - - - - - - - - - - -
      *
     d                uds
     d dsruti                800    809
     d dsalin                856    858  0
     d dswsid                901    910
     d dsuser                911    920
     d dsfirm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vvenl1_vare     s                   like(vevare)
     d vvenl1_enhe     s                   like(veenhe)
      *
      * Definering av variable
      *
     d d1npri          s                   like(fdsapr)
     d fcycle          s              1  0
     d sanumm          s                   like(fdnumm)
     d utime           s              6  0
     d wltell          s              3  0
     d wltest          s              3  0
     d wpkont          s              5  0
     d wpmvaf          s             10  9
     d wpmvas          s              6  2
     d wpmvat          s              5  4
     d wppdat          s              8  0
     d wppomr          s             10  6
     d wpptx1          s             30
     d wpptx2          s             30
     d w_ytxt          s             30
     d w_retk          s              1
     d wpspes          s              2  0
     d wwdekn          s             13  4
     d w_firm          s                   like(fofirm)
     d wwkund          s                   like(fokund)
     d wwline          s                   like(fdline)
     d wwltyp          s                   like(fdltyp)
     d wwnumm          s                   like(fonumm)
     d wwotyp          s                   like(vaotyp)
     d wwparm          s             64
     d wwruti          s             10
     d wwsuff          s                   like(fosuff)
     d wwwsid          s                   like(fowsid)
     d w0kund          s                   like(faakun)
     d w1anta          s                   like(fdanta)
     d w1npri          s                   like(fdsapr)
     d w1rab1          s                   like(fdsapr)
      *
     d                 ds
      *                                                      SORTERING
6.nu d w_sort                       114
     d  w_kjor                        2  0 overlay(w_sort)
     d  w_kjnr                        5  0 overlay(w_sort:03)
     d  w_kund                        6  0 overlay(w_sort:08)
     d  w_navn                       30    overlay(w_sort:14)
6.nu d  w_numm                        8  0 overlay(w_sort:44)
6.nu d  w_suff                        2  0 overlay(w_sort:52)
      *
     d w_kjor_gml      s                   like(w_kjor)
     d w_kund_gml      s                   like(w_kund)
     d w_antal         s              5  0
      *
     ifpsol1r       01
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av formater                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede formater
      * - - - - - - - - - -
      *
      * A1HDR  - Heading A1 på rapport
      * D1DET  - Detail  D1 på rapport, Varelinje
      * E1DET  - Detail  E1 på rapport, Tekstlinje
      * T1TOT  - Total   T1 på rapport, Sum kunde
      * T2TOT  - Total   T2 på rapport, Sum rute
      *
      *
      * Forklaring på variabler
      * - - - - - - - - - - - -
      *
      * D1ANT0 - Antall med 0 desimaler
      * D1ANT1 - Antall med 1 desimaler
      * D1ANT2 - Antall med 2 desimaler
      * D1ANT3 - Antall med 3 desimaler
      * D1NPRI - Netto  pris
      * D1BPRI - Brutto pris
      * D1NBEL - Netto  beløp
      * D1KBEL - Kost   beløp
      * D1RABP - Rabatt i prosent (Sum prosent 1 og 2)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Styre-rutine                                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     les           tag
      *
     c                   read      fpsol1r                                lr
      *
     c     *inlr         cabeq     *on           avslutt
      *
      * Ordrehode
      *
     c     fkskod        cabeq     'H'           tag01
      *
      * Varelinje
      *
     c     fkskod        cabeq     'V'           tag02
      *
      * Kommentar/
      * tekstlinje
      *
     c     fkskod        cabeq     'K'           tag03
      *
      * Ferdig
      *
     c                   goto      xslutt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A1HDR Behandle heading A1                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     tag01         tag
      *
      * Null-stilling av
      * variabler
      *
     c                   move      *off          *in37
      *
      * Sorteringsbegrep
      *
     c                   move      fkssrt        w_sort
      *
      * Sjekk om brudd på kunde
      *
     c                   if        w_kund_gml <> *hival and
     c                             w_kund <> w_kund_gml
     c                   exsr      brd_kund
     c                   eval      w_kund_gml = w_kund
     c                   else
     c                   eval      w_kund_gml = w_kund
     c                   endif
      *
      * Sjekk om brudd på kjørerute
      *
     c                   if        w_kjor_gml <> *hival and
     c                             w_kjor <> w_kjor_gml
     c                   exsr      brd_rute
     c                   eval      w_kjor_gml = w_kjor
     c                   else
     c                   eval      w_kjor_gml = w_kjor
     c                   endif
      *
      * Hent ordre-heading
      *
     c                   move      fksonr        wwnumm
     c                   move      fkssuf        wwsuff
     c     fohel1_key    chain     fohel1r                            61
      *
      * Snu dato
      *
     c                   if        foldat <> *loval
     c     *dmy          move      foldat        a1ldat
     c                   endif
      *
      * Legg inn ordrenummer
      *
     c                   move      fonumm        a1numm
     c                   movel     '/'           a1suff
     c                   move      fosuff        a1suff
      *
      * Hente inn kunderegister
      *
     c                   move      fokund        wwkund
     c     key04         chain     rkunl1r                            64
     c                   move      fokund        a1kund
     c                   move      rknavn        a1navn
      *
      *  Hent tekst for kjørerute
      *
     c                   eval      a1ktxt = *blank
     c                   move      footyp        a1otyp
      *
     c                   if        fokjor <> *zeros
     c                   call      'RS725R'
     c                   parm                    w_retk
     c                   parm                    fokjor
     c                   parm                    w_ytxt
     c                   move      fokjor        a1kjor
     c                   move      w_ytxt        a1ktxt
     c                   endif
      *
      * Skrive heading
      *
     c                   eval      wltell = 0
      *
     c                   if        w_antal = *zeros
     c                   write     a1hdr
     c                   endif
      *
     c                   write     a1hdr1                               30
      *
      * Er det overflow ?
      *
     c     *in30         caseq     *on           xovrfl
     c                   endcs
      *
     c                   goto      xslutt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * D1DET Behandle detail D1                                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     tag02         tag
      *
      * Nullstilling av variabler
      *
     c                   eval      d1vare = *blank
     c                   eval      d1tek1 = *blank
     c                   eval      d1enh1 = *blank
     c                   eval      d1ant0 = *zero
     c                   eval      d1ant1 = *zero
     c                   eval      d1ant2 = *zero
     c                   eval      d1ant3 = *zero
     c                   move      *off          *in44
     c                   move      *off          *in46
      *
     c                   move      fksonr        wwnumm
     c                   move      fkssuf        wwsuff
     c                   move      fkslin        wwline
     c     key02         chain     fodtl1r                            62
      *
      * Hent linje-type
      *
     c                   move      fdltyp        wwltyp
     c     key08         chain     vltyl1r                            68
      *
      * Sjekker om tekstlinjer
      *
     c     valktx        cabeq     1             tag02a
      *
      * Oppslag mot vareregister
      *
     c                   eval      vvarl1_vare = fdvare
     c     vvarl1_key    chain     vvarl1r
      *
      * Test på antall desimaler
      *
     c                   eval      *in40 = vvdesi = 0
     c                   eval      *in41 = vvdesi = 1
     c                   eval      *in42 = vvdesi = 2
     c                   eval      *in43 = vvdesi = 3
      *
     c                   if        *in40 = *on
     c                   eval      d1ant0 = fdanta
     c                   endif
     c                   if        *in41 = *on
     c                   eval      d1ant1 = fdanta
     c                   endif
     c                   if        *in42 = *on
     c                   eval      d1ant2 = fdanta
     c                   endif
     c                   if        *in43 = *on
     c                   eval      d1ant3 = fdanta
     c                   endif
      *
      * Varelinje
      *
     c                   move      fdvare        d1vare
     c                   movel     fdtek1        d1tek1
     c                   move      fdenh1        d1enh1
      *
      * Henter volum og vektinformasjon fra enhet
      *
     c                   eval      vvenl1_vare = fdvare
     c                   eval      vvenl1_enhe = fdenh1
     c     vvenl1_key    chain     vvenl1
     c                   if        %found
     c                   eval      d1leng = veleng
     c                   eval      d1bred = vebred
     c                   eval      d1hoyd = vehoyd
     c                   eval(h)   d1brut = vevekt * fdanta
     c                   eval(h)   d1volu = vevolu * fdanta
     c                   else
     c                   eval      d1hoyd = 0
     c                   eval      d1bred = 0
     c                   eval      d1leng = 0
     c                   eval(h)   d1brut = 0
     c                   eval(h)   d1volu = 0
     c                   endif
      *
      * Summer til total
      *
     c                   eval      t1brut = t1brut + d1brut
     c                   eval      t1volu = t1volu + d1volu
      *
     c                   eval      t2brut = t2brut + d1brut
     c                   eval      t2volu = t2volu + d1volu
      *
      * Skriv varelinjer
      *
     c     tag02a        tag
     c                   if        valktx <> 1
     c                   eval      wltell = wltell + 1
     c                   write     d1det                                30
     c                   endif
      *
     c                   eval      w_antal = w_antal + 1
      *
      * Er det overflow ?
      *
     c     *in30         caseq     *on           xovrfl
     c                   endcs
      *
     c                   goto      xslutt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Behandling av tekstlinjer                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     tag03         tag
      *
      * Behandler ikke tekstlinjer
      *
     c                   goto      xslutt
      *
     c     xslutt        tag
      *
     c                   goto      les
      *
     c     avslutt       tag
      *
     c                   exsr      brd_kund
     c                   exsr      brd_rute
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Behandle brudd på kjørerute                                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     brd_rute      begsr
      *
      * Skrive sum kjørerute
      *
     c                   write     t2tot                                30
      *
     c                   eval      t2brut = *zeros
     c                   eval      t2volu = *zeros
      *
     c                   eval      w_antal = *zeros
      *                                                               . .
      * Er det overflow ?                                             . .
      *                                                               . .
     c                   if        *in30 = *on                                             . .
     c                   write     a1hdr                                                   . .
     c                   write     a1hdr1                                                  . .
     c                   eval      wltell = 0                                              . .
     c                   endif                                                             . .
      *
     c                   endsr
     c                                                                                   .....
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Behandle brudd på kunde                                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     brd_kund      begsr
      *
      * Null-stille variabler
      *
      *
      * Finne ut om det er
      * plass til å skrive
      * avslutning uten å
      * skifte til ny side
      * først.
      *
      * Trekker ut heading
      *
     c                   eval      wltest = dsalin - 24
      *
      * Trekker ut antall
      * linjer skrevet
      *
     c                   eval      wltest = wltest - wltell
      *
      * Trekker ut plassbehov
      * for avslutning
      *
     c                   eval      wltest = wltest - 3
      *
      * Skrive ny heading ?
      *
     c                   if        wltest < 0
     c                   write     a1hdr
     c                   write     a1hdr1
     c                   eval      wltell = 0
     c                   endif
      *
      * Skrive sum kunde
      *
     c                   write     t1tot                                30
      *
     c                   eval      t1brut = *zeros
     c                   eval      t1volu = *zeros
      *                                                               . .
      * Er det overflow ?                                             . .
      *                                                               . .
     c                   if        *in30 = *on                                             . .
     c                   write     a1hdr                                                   . .
     c                   write     a1hdr1                                                  . .
     c                   eval      wltell = 0                                              . .
     c                   endif                                                             . .
      *
     c                   endsr
     c                                                                                   .....
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for overflow                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xovrfl        begsr
      *
     c                   write     a1hdr
     c                   write     a1hdr1
      *
     c                   eval      wltell = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   eval      fcycle = *zero
     c                   eval      w_kund_gml = *hival
     c                   eval      w_kjor_gml = *hival
      *
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     c                   eval      w_retk = *blank
      *
     c                   eval      wpmvas = *zero
     c                   eval      wpmvat = *zero
     c                   eval      wpmvaf = *zero
      *
     c                   eval      wppdat = *zero
     c                   eval      wppomr = *zero
      *
     c                   eval      wpkont = *zero
     c                   eval      wpspes = *zero
     c                   eval      wpptx1 = *blank
     c                   eval      wpptx2 = *blank
      *
     c                   eval      wwdekn = *zero
      *
     c                   eval      wwruti = *blank
     c                   eval      wltell = *zero
     c                   eval      wltest = *zero
      *
      * Key for Heading-file
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    wwnumm
     c                   kfld                    wwsuff
      *
      * Key for Detalj-file
      *
     c     key02         klist
     c                   kfld                    w_firm
     c                   kfld                    wwnumm
     c                   kfld                    wwsuff
     c                   kfld                    wwline
      *
      * Key for Tekst-linjer
      *
     c     key03         klist
     c                   kfld                    w_firm
     c                   kfld                    wwnumm
     c                   kfld                    wwsuff
     c                   kfld                    wwline
      *
      * Key for kunderegister
      *
     c     key04         klist
     c                   kfld                    w_firm
     c                   kfld                    wwkund
      *
      * Key for firmaregister
      *
     c     key06         klist
     c                   kfld                    w_firm
      *
      * Key for ordretyper
      *
     c     key07         klist
     c                   kfld                    w_firm
     c                   kfld                    wwotyp
      *
      * Key for linjetyper
      *
     c     key08         klist
     c                   kfld                    w_firm
     c                   kfld                    wwltyp
      *
      * Key for statusregister
      *
     c     key09         klist
     c                   kfld                    w_firm
      *
      * Key for formular-register
      *
     c     key10         klist
     c                   kfld                    wwruti
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     *entry        plist
     c                   parm                    wwparm
      *
     c                   move      wwparm        ldprec
      *
      * Legger inn arb.stasjon og bruker i faste felter
      *
     c                   move      dsuser        a1user
     c                   move      dswsid        a1wsid
     c                   move      udate         a1date
     c                   time                    utime
     c                   move      utime         a1time
      *
      * Hente inn rutinenavn
      * og meldingstekst
      *
     c                   move      dsruti        wwruti
     c     key10         chain     aforl1r
      *
      * Legger inn firma i nøkkel
      *
     c                   move      fksfir        w_firm
     c                   move      fksfir        a1firm
      *
      * Klargjøring dersom firma -
      * opplysninger skal printes ut
      *
     c     key06         chain     afirl1r                            66
     c                   move      aafnav        a1fnav
      *
      * Hente Kunde-tilbuds-nummer
      *
     c     key09         chain     fstsl1r
     c                   if        %found
     c                   move      faakun        w0kund
     c                   endif
      *
      * Slår opp ordretype
      *
     c                   move      fksoty        wwotyp
     c     key07         chain     votyl1r                            67
      *
     c                   move      fksoty        a1otyp
      *
      * Er ordretype negativ ?
      *
     c                   if        vaokre = '-'
     c                   move      *on           *in36
     c                   endif
      *
     c                   endsr
