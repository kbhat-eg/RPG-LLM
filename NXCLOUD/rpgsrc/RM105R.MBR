     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RM105R
      * Beskrivelse . . : Eiendommer, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       160303 KOB  Nytt program
7.xx  * 7.xx  120516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frmeil1    if   e           k disk    rename(rmeipfr:rmeil1r)
     frmeil2    if   e           k disk    rename(rmeipfr:rmeil2r)
     frmeilr    if   e           k disk    rename(rmeipfr:rmeilrr)
     frmeilu    uf a e           k disk    rename(rmeipfr:rmeilur)
      *
     frm105d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d rmeil1_alme     s                   like(rmealm)
     d rmeil1_egnr     s                   like(rmegnr)
     d rmeil1_ebnr     s                   like(rmebnr)
     d rmeil2_alme     s                   like(rmealm)
     d rmeil2_enav     s                   like(rmenav)
     d rmeilr_alme     s                   like(rmealm)
     d rmeilr_egnr     s                   like(rmegnr)
     d rmeilr_ebnr     s                   like(rmebnr)
     d rmeilu_alme     s                   like(rmealm)
     d rmeilu_egnr     s                   like(rmegnr)
     d rmeilu_ebnr     s                   like(rmebnr)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d p_stat          s              1
     d p_ponr          s                   like(rmepnr)
     d p_sted          s             25
     d p_kund          s                   like(rmest1)
     d p_knav          s             30
     d p_navn          s             30
     d p_firm          s                   like(rmefir)
     d p_alme          s                   like(rmealm)
      *
     d w_firm          s                   like(rmefir)
      *
     d s_egnr          s                   like(rmegnr)
     d s_ebnr          s                   like(rmebnr)
      *
      * Definering av konstanter
      *
7.xx d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2egnr <> 0 or
     c                             b2enav <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      rmeil2_enav = b1enav
     c     rmeil2_key    setll     rmeil2
     c                   other
     c                   eval      rmeil1_egnr = b1egnr
     c     rmeil1_key    setll     rmeil1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Eiendom
      *
     c                   if        b2egnr <> 0
     c                   eval      w_seqe = *blank
     c                   eval      rmeil1_egnr = b2egnr
     c     rmeil1_key    setll     rmeil1
     c                   goto      end_pos
     c                   endif
      *
      * Beskrivelse
      *
     c                   if        b2enav <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      rmeil2_enav = b2enav
     c     rmeil2_key    setll     rmeil2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2egnr = 0
     c                   eval      b2ebnr = 0
     c                   eval      b2enav = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1egnr = b1egnr
     c                   eval      c1ebnr = b1ebnr
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1egnr = b1egnr
     c                   eval      k1ebnr = b1ebnr
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1egnr = b1egnr
     c                   eval      d1ebnr = b1ebnr
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1egnr = b1egnr
     c                   eval      c1ebnr = b1ebnr
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1egnr = b2egnr
     c                   eval      c1ebnr = b2ebnr
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1egnr = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
     c***                if        c1egnr = 0
     c***                eval      *in31  = *on
     c***                goto      c1taga
     c***                endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      rmeilr_egnr = c1egnr
     c                   eval      rmeilr_ebnr = c1ebnr
     c     rmeilr_key    chain     rmeilr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1egnr = 0
     c                   eval      c1ebnr = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2egnr = 0
     c                   eval      b2ebnr = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      rmeilr_egnr = c1egnr
     c                   eval      rmeilr_ebnr = c1ebnr
     c     rmeilr_key    chain     rmeilr
      *
     c                   if        %found
     c                   eval      c2enav = rmenav
     c                   eval      c2ead1 = rmead1
     c                   eval      c2ead2 = rmead2
     c                   eval      c2epnr = rmepnr
     c                   eval      c2eare = rmeare
     c                   eval      c2edyr = rmedyr
     c                   eval      c2eutm = rmeutm
     c                   eval      c2est1 = rmest1
     c                   eval      c2est2 = rmest2
      *
     c                   eval      p_ponr = rmepnr
     c                   exsr      hent_post
     c                   eval      c2sted = p_sted
      *
     c                   if        rmest1 <> 0
     c                   eval      p_kund = rmest1
     c                   exsr      hent_kunde
     c                   eval      c2nav1 = p_knav
     c                   endif
      *
     c                   if        rmest2 <> 0
     c                   eval      p_kund = rmest2
     c                   exsr      hent_kunde
     c                   eval      c2ena2 = p_knav
     c                   else
     c                   eval      c2ena2 = rmena2
     c                   endif
      *
     c                   eval      c2ting = rmting
     c                   eval      c2saer = rmsaer
     c                   eval      c2drif = rmdrif
     c                   eval      c2kara = rmkara
     c                   eval      c2bort = rmbort
     c                   eval      c2hbyg = rmhbyg
     c                   eval      c2lbyg = rmlbyg
     c                   eval      c2dbyg = rmdbyg
     c                   eval      c2ahus = rmahus
      *
     c                   eval      c2eusr = rmeeus
     c***                eval      c2enav = *blank
      *
     c                   if        rmeeda = *loval
     c                   move      0             c2edat
     c                   else
     c     *dmy          move      rmeeda        c2edat
     c                   endif
      *
     c                   else
      *
     c                   eval      c2enav = *blank
     c                   eval      c2ead1 = *blank
     c                   eval      c2ead2 = *blank
     c                   eval      c2epnr = 0
     c                   eval      c2eare = 0
     c                   eval      c2edyr = 0
     c                   eval      c2eutm = 0
     c                   eval      c2est1 = 0
     c                   eval      c2est2 = 0
     c                   eval      c2ena2 = *blank
     c                   eval      c2ting = ' '
     c                   eval      c2saer = ' '
     c                   eval      c2drif = ' '
     c                   eval      c2kara = ' '
     c                   eval      c2bort = ' '
     c                   eval      c2hbyg = 0
     c                   eval      c2lbyg = 0
     c                   eval      c2dbyg = 0
     c                   eval      c2ahus = 0
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1egnr = k1egnr
     c                   eval      c1ebnr = k1ebnr
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
      *
      * Spørring på poststed
      *
     c                   if        *inkd = *on
      *
     c                   if        w_afld = 'C2EPNR'
     c                   call      'AF010R'
     c                   parm                    c2epnr
     c                   goto      c2taga
     c                   endif
      *
      * Spørring på stemmeberettiget 1
      *
     c                   if        w_afld = 'C2EST1'
     c                   call      'RK500R'
     c                   parm                    p_firm
     c                   parm                    p_kund
     c                   parm                    p_knav
      *
     c                   eval      c2est1 = p_kund
     c                   eval      c2nav1 = p_knav
     c                   goto      c2taga
     c                   endif
      *
      * Spørring på stemmeberettiget 2
      *
     c                   if        w_afld = 'C2EST2'
     c                   call      'RK500R'
     c                   parm                    p_firm
     c                   parm                    p_kund
     c                   parm                    p_knav
      *
     c                   eval      c2est2 = p_kund
     c                   eval      c2ena2 = p_knav
     c                   goto      c2taga
     c                   endif
      *
     c                   goto      c2taga
      *
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2enav = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   rmeilur
      *
     c                   eval      rmeilu_egnr = c1egnr
     c                   eval      rmeilu_ebnr = c1ebnr
     c     rmeilu_key    chain     rmeilur
      *
     c                   eval      rmenav = c2enav
     c                   eval      rmead1 = c2ead1
     c                   eval      rmead2 = c2ead2
     c                   eval      rmepnr = c2epnr
     c                   eval      rmeare = c2eare
     c                   eval      rmedyr = c2edyr
     c                   eval      rmeutm = c2eutm
     c                   eval      rmest1 = c2est1
     c                   eval      rmest2 = c2est2
     c                   eval      rmena2 = c2ena2
     c                   eval      rmting = c2ting
     c                   eval      rmsaer = c2saer
     c                   eval      rmdrif = c2drif
     c                   eval      rmkara = c2kara
     c                   eval      rmbort = c2bort
     c                   eval      rmhbyg = c2hbyg
     c                   eval      rmlbyg = c2lbyg
     c                   eval      rmdbyg = c2dbyg
     c                   eval      rmahus = c2ahus
      *
     c                   time                    rmeeda
     c                   time                    rmeeti
     c                   eval      rmeeus = l_user
      *
     c                   if        %found
     c                   update    rmeilur
     c                   else
     c                   eval      rmefir = w_firm
     c                   eval      rmealm = p_alme
     c                   eval      rmegnr = c1egnr
     c                   eval      rmebnr = c1ebnr
     c                   write     rmeilur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1enav = rmenav
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      rmeilr_egnr = d1egnr
     c                   eval      rmeilr_ebnr = d1ebnr
     c     rmeilr_key    chain     rmeilr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      rmeilu_egnr = d1egnr
     c                   eval      rmeilu_ebnr = d1ebnr
     c     rmeilu_key    chain     rmeilur
      *
     c                   delete    rmeilur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_egnr = k1egnr
     c                   eval      s_ebnr = k1ebnr
      *
      * Fyller kopierings-bildet
      *
     c                   eval      rmeilr_egnr = k1egnr
     c                   eval      rmeilr_ebnr = k1ebnr
     c     rmeilr_key    chain     rmeilr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1egnr = 0 or
     c                             k1ebnr = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      rmeilr_egnr = k1egnr
     c                   eval      rmeilr_ebnr = k1ebnr
     c     rmeilr_key    chain     rmeilr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1egnr = s_egnr
     c                   eval      c1ebnr = s_ebnr
     c                   exsr      xc2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      rmeil2
     c                   other
     c                   read      rmeil1
     c                   endsl
      *
     c                   if        %eof or
     c                             rmefir <> w_firm or
     c                             rmealm <> p_alme
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      rmeil2_enav = rmenav
     c                   other
     c                   eval      rmeil1_egnr = rmegnr
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1egnr = rmegnr
     c                   eval      b1ebnr = rmebnr
     c                   eval      b1enav = rmenav
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     rmeil2_key    setgt     rmeil2
     c                   readp     rmeil2
     c                   other
     c     rmeil1_key    setgt     rmeil1
     c                   readp     rmeil1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     rmeil2
     c                   other
     c                   readp     rmeil1
     c                   endsl
      *
     c                   if        %eof or
     c                             rmefir <> w_firm or
     c                             rmealm <> p_alme
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      rmeil2_enav = rmenav
     c                   other
     c                   eval      rmeil1_egnr = rmegnr
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     rmeil2_key    setll     rmeil2
     c                   other
     c     rmeil1_key    setll     rmeil1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting at tekst postnr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_post     begsr
      *
      * Hent poststed
      *
     c                   eval      p_sted = *blank
      *
     c                   if        p_ponr <> 0
      *
     c                   call      'RS730R'
     c                   parm                    p_stat
     c                   parm                    p_ponr
     c                   parm                    p_sted
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting at tekst kunder
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_kunde    begsr
      *
      * Hent kundenavn
      *
     c                   eval      p_knav = *blank
      *
     c                   if        p_kund <> 0
      *
     c                   call      'RS750R'
     c                   parm                    p_stat
     c                   parm                    p_kund
     c                   parm                    p_knav
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_alme
     c                   parm                    p_navn
      *
      * Key for posisjonering på programegnr
      *
     c     rmeil1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rmeil1_alme
     c                   kfld                    rmeil1_egnr
     c                   kfld                    rmeil1_ebnr
      *
      * Key for posisjonering på beskrivelse
      *
     c     rmeil2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rmeil2_alme
     c                   kfld                    rmeil2_enav
      *
      * Key for les på programegnr
      *
     c     rmeilr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rmeilr_alme
     c                   kfld                    rmeilr_egnr
     c                   kfld                    rmeilr_ebnr
      *
      * Key for oppdatering av programegnr
      *
     c     rmeilu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rmeilu_alme
     c                   kfld                    rmeilu_egnr
     c                   kfld                    rmeilu_ebnr
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rmeil1_alme = p_alme
     c                   eval      rmeil2_alme = p_alme
     c                   eval      rmeilr_alme = p_alme
     c                   eval      rmeilu_alme = p_alme
     c                   eval      b2alme = p_alme
     c                   eval      b2navn = p_navn
      *
     c                   eval      rmeil1_egnr = 0
     c     rmeil1_key    setll     rmeil1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
