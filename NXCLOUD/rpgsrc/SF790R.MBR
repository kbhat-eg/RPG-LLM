      /title  ASSTAT Bytting av kundenummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSTAT
      * Program . . . . : SF790R
      * Beskrivelse . . : Bytting av kundenummer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       030500 HKO  Nytt program
      *       290500 HKO  Opprettet og byttet til indeks for direkte les
      *                   både for varekunde og statistikkunde
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner - COMP
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsstale    uf   e           k disk    rename(sstapfr:sstaler)
     fsstalf    uf   e           k disk    rename(sstapfr:sstalfr)
      *
      * Definering av parametere
      *
     d p_firm          s                   like(sffirm)
     d p_gkun          s                   like(sfvkun)
     d p_kund          s                   like(sfvkun)
      *
      * Definering av variable i nøkler
      *
     d sstale_vkun     s                   like(sfvkun)
     d sstalf_fkun     s                   like(sffkun)
      *
      * Definering av variable
      *
     d w_firm          s                   like(sffirm)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Salgsstatistikk - varekunde
      *
     c                   eval      sstale_vkun = p_gkun
     c     sstale_key    setll     sstale
     c     sstale_key    reade     sstaler                                90
     c                   dow       *in90 = *off
     c                   eval      sfvkun = p_kund
     c                   if        sffkun = p_gkun
     c                   eval      sffkun = p_kund
     c                   endif
     c                   update    sstaler
     c     sstale_key    reade     sstaler                                90
     c                   enddo
      *
      * Salgsstatistikk - statistikkunde
      *
     c                   eval      sstalf_fkun = p_gkun
     c     sstalf_key    setll     sstalf
     c     sstalf_key    reade     sstalfr                                90
     c                   dow       *in90 = *off
     c                   eval      sffkun = p_kund
     c                   update    sstalfr
     c     sstalf_key    reade     sstalfr                                90
     c                   enddo
      *
      * Avslutt program
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine sfr initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_gkun
     c                   parm                    p_kund
      *
      * Key for oppdatering av salgsstatistikk - varekunde
      *
     c     sstale_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sstale_vkun
      *
      * Key for oppdatering av salgsstatistikk - statistikkunde
      *
     c     sstalf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sstalf_fkun
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
