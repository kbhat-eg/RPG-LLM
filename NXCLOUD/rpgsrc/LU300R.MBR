      /TITLE  ASLAGR: Lagertelling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LU300R
      *  Beskrivelse . . : Kopiering av lagertelle-trans-bunt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  -------------------------------------------------
      *            220900  Nytt program                            ASK
      * 6.10  170609 BSA  Oppdatert med sporingsinformasjon
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      *
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     flbhel1    if   e           k disk    rename(lbhepfr:lbhel1r)
     flbhelu    uf a e           k disk    rename(lbhepfr:lbhelur)
     flbdtl1    if   e           k disk    rename(lbdtpfr:lbdtl1r)
     flbdtlu    uf a e           k disk    rename(lbdtpfr:lbdtlur)

      *
      * Local Data Area
      *
     d                uds
6.10 d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable i n√∏kler
      *
     d lbdtlu_batc     s                   like(lvbatc)
     d lbdtlu_numm     s                   like(lvnumm)
     d lbdtlu_line     s                   like(lvline)
     d lbdtl1_batc     s                   like(lvbatc)
     d lbdtl1_numm     s                   like(lvnumm)
     d lbhelu_batc     s                   like(lubatc)
     d lbhelu_numm     s                   like(lunumm)
     d lbhel1_batc     s                   like(lubatc)
     d lbhel1_numm     s                   like(lunumm)
      *
      * Definering av variable i parametre
      *
     d p_batc          s                   like(lubatc)
     d p_coor          s              1
     d p_firm          s                   like(lufirm)
     d p_numm          s                   like(lunumm)
     d p_nyor          s                   like(lunumm)
     d p_type          s                   like(lutype)
      *
      * Definering av variable
      *
     d w_firm          s                   like(lufirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Kopiering av tellebunt - linjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lbdtl1_key    setll     lbdtl1r
      *
     c     lbdtl1_key    reade     lbdtl1r                                90
      *
     c                   dow       *in90 = *off
      *
     c                   eval      lbdtlu_line = lvline
     c     lbdtlu_key    chain     lbdtlur                            92
      *
     c                   if        *in92 = *on
     c                   eval      lvfirm = w_firm
     c                   eval      lvbatc = lbdtlu_batc
     c                   eval      lvnumm = lbdtlu_numm
     c                   eval      lvline = lbdtlu_line
6.10 c                   time                    lvodat
6.10 c                   time                    lvotim
6.10 c                   eval      lvousr = l_user
6.10 c                   time                    lvedat
6.10 c                   time                    lvetim
6.10 c                   eval      lveusr = l_user
     c                   write     lbdtlur
     c                   endif
      *
     c     lbdtl1_key    reade     lbdtl1r                                90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Kopiering av tellebunt - hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lbhel1_key    chain     lbhel1r                            90
      *
      *  Legger ut igjen tellebunten  med nytt tellebuntnummer
      *
     c     lbhelu_key    chain     lbhelur                            92
      *
     c                   if        *in92 = *on
     c                   if        p_type <> *blank
     c                   eval      lutype = p_type
     c                   endif
      *
     c                   eval      luskod = 0
     c                   eval      lukode = 0
      *
      *  Legger inn dagens dato
      *
     c                   time                    ludato
     c                   time                    lutime
6.10 c                   time                    luedat
6.10 c                   time                    luetim
6.10 c                   eval      lueusr = l_user
      *
     c                   eval      lufirm = w_firm
     c                   eval      lubatc = lbhelu_batc
     c                   eval      lunumm = lbhelu_numm
     c                   write     lbhelur
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av LAGER-STATUS
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for les av LAGER-TELLEBUNT-HODE
      *
     c     lbhel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbhel1_batc
     c                   kfld                    lbhel1_numm
      *
      *  Key for oppdatering av LAGER-TELLEBUNT-HODE
      *
     c     lbhelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbhelu_batc
     c                   kfld                    lbhelu_numm
      *
      *  Key for les av LAGER-TELLEBUNT-LINJE
      *
     c     lbdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbdtl1_batc
     c                   kfld                    lbdtl1_numm
      *
      *  Key for oppdatering av LAGER-TELLEBUNT-LINJE
      *
     c     lbdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbdtlu_batc
     c                   kfld                    lbdtlu_numm
     c                   kfld                    lbdtlu_line
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_coor
     c                   parm                    p_firm
     c                   parm                    p_batc
     c                   parm                    p_numm
     c                   parm                    p_nyor
     c                   parm                    p_type
      *
      *  Legg inn startverdier
      *
     c                   eval      w_firm = p_firm
     c                   eval      lbhel1_batc = p_batc
     c                   eval      lbhel1_numm = p_numm
      *
     c                   eval      lbdtl1_batc = p_batc
     c                   eval      lbdtl1_numm = p_numm
      *
     c                   eval      lbhelu_batc = p_batc
     c                   eval      lbhelu_numm = p_nyor
      *
     c                   eval      lbdtlu_batc = p_batc
     c                   eval      lbdtlu_numm = p_nyor
      *
      *  Hent opplysninger fra LAGER-STATUS-REGISTER
      *
     c     lstsl1_key    chain     lstsl1r                            90
      *
     c                   endsr
