     h decedit('0,') datedit(*dmy.)
     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : LO614R
      * Beskrivelse . . : Varselliste til leverandør, utskrift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
6.10  * R6.10 070909 BSA  Utskriften endret pga utvidelse av referansefelter.
6.11  * R6.10 090809 bof  Lagt på utvalg på lager
6.nu  * R6.30 120614 bof  Sjekket mtp. nummerutvidelse
6.31  * 6.30  101018 bof  Utvidet med trelast sortiment
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     flo614p    o    e             printer
      *
      * Definering av parametere
      *
     d p_ldor          s                   like(loldor)
     d p_dato          s              6  0
     d p_dagr          s              3  0
     d p_onbe          s              1  0
     d p_lage          s                   like(lolage)
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d vvarl1_vare     s                   like(vvvare)
     d rlevl1_levr     s                   like(rllevr)
     d votyl1_otyp     s                   like(vaotyp)
     d vltyl1_ltyp     s                   like(valtyp)
      *
      * Definering av variable
      *
     d w_firm          s                   like(lofirm)
     d w_suff          s              2
     d w_dato_p        s               d   datfmt(*iso)
     d w_dato_f        s               d   datfmt(*iso)
6.31 d w_utda          s                   like(vvedat)
6.31 d w_lv_ldor       s                   like(vvldor)
6.31 d w_lv_vtyp       s                   like(vvvtyp)
6.31 d w_lv_nobb       s                   like(vvnobb)
6.31 d w_lv_modn       s                   like(vvnmod)
6.31 d w_lv_lldo       s                   like(vvldor)
6.31 d w_lv_llva       s                   like(vvlvar)
6.31 d b_inlr          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hoved-logikk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Finne dato som det skal sjekkes i mot
      *
     c     *dmy          move      p_dato        w_dato_p
     c     w_dato_p      adddur    p_dagr:*d     w_dato_p
      *
      * Behandle bestillings-hode
      *
     c                   eval      lohel1_numm = 0
     c                   eval      lohel1_suff = 0
     c     lohel1_key    setll     lohel1
     c                   read      lohel1
      *
     c                   dow       not %eof
      *
      * Nytt firma ?
      *
     c                   if        lofirm <> w_firm
     c                   leave
     c                   endif
      *
      * Behandlingskode <> 1 ?
      *
     c                   eval      votyl1_otyp = lootyp
     c     votyl1_key    chain     votyl1r
     c                   if        vaoakk <> 1
     c                   goto      xnyles
     c                   endif
      *
      * Enkelt lager valgt?
      *
     c                   if        p_lage <> 0 and
     c                             lolage <> p_lage
     c                   goto      xnyles
     c                   endif
      *
      * Enkelt kunde valgt?
      *
     c                   if        p_ldor <> 0 and
     c                             loldor <> p_ldor
     c                   goto      xnyles
     c                   endif
      *
      * Finne riktig dato for test
      *
     c                   eval      *in48    = *off
     c                   eval      *in49    = *off
      *
     c                   if        p_onbe = 1
     c                   eval      w_dato_f = loldat
     c                   eval      *in48    = *on
     c                   else
     c                   eval      w_dato_f = lomoda
     c                   eval      *in49    = *on
     c                   endif
      *
      * Gå videre dersom dato ikke oppgitt
      *
     c                   if        w_dato_f = *loval
     c                   goto      xnyles
     c                   endif
      *
      * Gå videre dersom dato ikke forfalt
      *
     c                   if        w_dato_f > w_dato_p
     c                   goto      xnyles
     c                   endif
      *
      * Behandling av aktuell bestilling
      *
     c                   exsr      xhodet
     c                   exsr      xlinje
     c                   exsr      xtotal
      *
     c     xnyles        tag
     c                   read      lohel1
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av bestillings-hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xhodet        begsr
      *
     c                   eval      a1bdat = 0
     c                   eval      a1ldat = 0
      *
     c                   if        lobdat <> *loval
     c     *dmy          move      lobdat        a1bdat
     c                   endif
      *
      * Er det registrert
      *  - ønsket    leveringsdato?
      *  - bekreftet leveringsdato?
      *
     c                   eval      *in32  = *off
      *
     c                   if        p_onbe =  1
     c                   if        loldat <> *loval
     c                   eval      *in32  =  *on
     c     *dmy          move      loldat        a1ldat
     c                   endif
     c                   endif
      *
     c                   if        p_onbe =  2
     c                   if        lomoda <> *loval
     c                   eval      *in32  =  *on
     c     *dmy          move      lomoda        a1ldat
     c                   endif
     c                   endif
      *
      * Legg inn bestillingsnummer
      *
     c                   move      losuff        w_suff
     c                   eval      a1numm = lonumm
     c                   eval      a1suff = '/' + w_suff
      *
      * Hente inn leverandørregister
      *
     c                   eval      rlevl1_levr = loldor
     c     rlevl1_key    chain     rlevl1r
     c                   eval      a1navn = rlnavn
     c                   eval      a1gate = rlgate
     c                   eval      a1adr2 = rladr2
     c                   eval      a1sted = rlsted
     c                   eval      a1ltlf = rltlfn
     c                   eval      a1lfax = rltlfx
      *
      * Er det registrert
      * leveringsadresse?
      *
     c                   eval      *in31  = *off
     c                   if        lonavn <> *blank
     c                   eval      *in31  = *on
     c                   eval      a2navn = lonavn
     c                   eval      a2adr1 = loadr1
     c                   eval      a2adr2 = loadr2
     c                   eval      a2sted = losted
     c                   endif
      *
      * Er det registrert leverandørens referanse?
      *
     c                   eval      *in33  = *off
     c                   if        lodref <> *blank
     c                   eval      *in33  = *on
     c                   eval      a1dref = lodref
     c                   endif
      *
      * Er det registrert ordrenummer?
      *
     c                   eval      *in36  = *off
     c                   if        loornr <> *zero
     c                   eval      *in36  = *on
     c                   eval      a1ornr = loornr
     c                   endif
      *
      * Flytte øvrige felter
      *
     c                   eval      a1vref = lovref
      *
      * Skrive heading
      *
     c                   write     a1hdr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av bestillings-linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xlinje        begsr
      *
      * Nullstilling av variabler
      *
     c                   eval      d1varn = *blank
     c                   eval      d1varl = *blank
     c                   eval      d1tek1 = *blank
     c                   eval      d1tek2 = *blank
     c                   eval      d1enh1 = *blank
     c                   eval      d1ant0 = *zero
     c                   eval      d1ant1 = *zero
     c                   eval      d1ant2 = *zero
     c                   eval      d1ant3 = *zero
     c                   eval      *in44  = *off
      *
     c                   eval      lodtl1_numm = lonumm
     c                   eval      lodtl1_suff = losuff
     c     lodtl1_key    setll     lodtl1
     c     lodtl1_key    reade     lodtl1r
      *
     c                   dow       not %eof
      *
      * Hent linje-type
      *
     c                   eval      vltyl1_ltyp = ldltyp
     c     vltyl1_key    chain     vltyl1r
      *
      * Sjekker om linjen ikke skrives på ordretypen
      *
     c                   if        lootyp = valo01 or
     c                             lootyp = valo02 or
     c                             lootyp = valo03 or
     c                             lootyp = valo04
     c                   goto      xforbi
     c                   endif
      *
      * Sjekker om tekstlinjer
      *
     c                   if        valktx = 1
     c                   goto      tag02a
     c                   endif
      *
      * Oppslag mot vareregister
      *
     c                   eval      vvarl1_vare = ldvare
     c     vvarl1_key    chain     vvarl1r
      * henter info om logistikk-vare
6.31 c                   call      'VL710R'
6.31 c                   parm                    w_firm
6.31 c                   parm                    ldvare
6.31 c                   parm                    ldlage
6.31 c                   parm                    w_lv_vtyp
6.31 c                   parm                    w_utda
6.31 c                   parm                    w_lv_ldor
6.31 c                   parm                    b_inlr
6.31 c                   parm                    w_lv_nobb
6.31 c                   parm                    w_lv_modn
6.31 c                   parm                    w_lv_lldo
6.31 c                   parm                    w_lv_llva
      *
      * Test på antall desimaler
      *
     c                   if        ldlvre = 0
     c                   eval      *in40 = vvdesi = 0
     c                   eval      *in41 = vvdesi = 1
     c                   eval      *in42 = vvdesi = 2
     c                   eval      *in43 = vvdesi = 3
     c                   if        *in40 = *on
     c                   eval      d1ant0 = ldanta
     c                   endif
     c                   if        *in41 = *on
     c                   eval      d1ant1 = ldanta
     c                   endif
     c                   if        *in42 = *on
     c                   eval      d1ant2 = ldanta
     c                   endif
     c                   if        *in43 = *on
     c                   eval      d1ant3 = ldanta
     c                   endif
     c                   else
     c                   eval      d1ant2 = ldanta
     c                   eval      *in42 = *on
     c                   endif
      *
      * Varelinje - bruk varenr fra NOBB0 og LEV
      *
     c                   if        ldlvre = 0
6.31 c                   if        w_lv_nobb <> 0
6.31 c                   movel     w_lv_nobb     d1varn
6.31 c                   movel     w_lv_llva     d1varl
6.31 c                   else
     c                   movel     vvlvar        d1varl
     c                   movel     vvnobb        d1varn
6.31 c                   endif
     c                   else
     c                   movel     ldlvar        d1varl
     c                   movel     ldnobb        d1varn
     c                   endif
      *
     c                   eval      d1tek1 = ldtek1
     c                   eval      d1enh1 = ldenh1
      *
      * Skriv varelinjer
      *
     c     tag02a        tag
      *
     c                   if        valktx <> 1
     c                   write     d1det                                30
     c                   else
     c                   eval      d1tek1 = ldtek1
     c                   eval      d1tek2 = ldtek2
     c                   eval      *in44  = *on
     c                   write     d2det                                30
     c                   endif
      *
      * Er det overflow ?
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
      *
     c     xforbi        tag
      *
     c     lodtl1_key    reade     lodtl1r
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av tekstlinjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c***  tag03         tag
      *
      * Ingen behandling foretas
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av totaler
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xtotal        begsr
      *
      * Skrive total-linjer
      *
     c                   write     t1tot                                30
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametere
      *
     c     *entry        plist
     c                   parm                    p_ldor
     c                   parm                    p_dato
     c                   parm                    p_dagr
     c                   parm                    p_onbe
     c                   parm                    p_lage
      *
      * Key for file : BESTILLING-HODE
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      * Key for file : BESTILLING-LINJE
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
      *
      * Key for file : LEVERANDØR-REGISTER
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for file : ORDRETYPE-REGISTER
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for file : LINJETYPE-REGISTER
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
      * Key for file : VARE-REGISTER
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Legge inn formanummer
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
