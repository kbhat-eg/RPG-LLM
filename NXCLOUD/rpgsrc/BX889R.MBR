     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : BX889R
      * Beskrivelse . . : Legger på fakturatype på bonghode  i forh. til
      *                   kunde
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       240108 bof  Nytt program
      * 6.10  110909 BSA  Oppdatert med sporingsinformasjon
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fbohelu    uf   e           k disk    rename(bohepfr:bohelur)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
      *
     d rkunl1_firm     s                   like(rkfirm)
     d rkunl1_kund     s                   like(rkkund)
      *
      * Definering av Local data area
      *
6.10 d                uds
6.10 d l_user                911    920
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      * Bonghode
      *
     c                   read      bohelur
     c                   dow       not %eof
      *
     c                   eval      rkunl1_firm = bofirm
     c                   eval      rkunl1_kund = bokund
     c     rkunl1_key    chain     rkunl1                             60
      *
     c                   if        %found(rkunl1) and
     c                             rkfaty <> 0
     c                   eval      bofaty = rkfaty
      *
6.10 c                   time                    boedat
6.10 c                   time                    boetim
6.10 c                   eval      boeusr = l_user
     c                   update    bohelur
     c                   endif
      *
     c                   read      bohelur
     c                   enddo
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for kunderegister via kundenummer
      *
     c     rkunl1_key    klist
     c                   kfld                    rkunl1_firm
     c                   kfld                    rkunl1_kund
      *
     c                   endsr
