     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VV790R
      * Beskrivelse . . : Vare, bytt enhet i annen filgruppe - styreprogram
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       290304 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     facpll1    if   e           k disk    rename(acplpfr:acpll1r)
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_vare          s             15
     d p_enhg          s              3
     d p_enhn          s              3
      *
      * Definering av Local data area
      *
     d                uds
     d l_fgrp                931    933
      *
      * Datastruktur for parameterliste -ut
      *
     d                 ds
     d d_list                        40
     d  d_fgrt                        3    overlay(d_list:1)
     d  d_firt                        3  0 overlay(d_list:4)
     d  d_vare                       15    overlay(d_list:7)
     d  d_enhg                        3    overlay(d_list:22)
     d  d_enhn                        3    overlay(d_list:25)
      *
      * Definering av variable i nøkler
      *
     d acpll1_lfgr     s                   like(aclfgr)
     d acpll1_lfir     s                   like(aclfir)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer output-parameter
      *
     c                   eval      d_fgrt = *blank
     c                   eval      d_firt = 0
     c                   eval      d_vare = p_vare
     c                   eval      d_enhg = p_enhg
     c                   eval      d_enhn = p_enhn
      *
      * Leser filkopiering-linje og kaller program for alle
      * filgrupper/firmaer det skal byttes navn på enhet i (kopieres til)
      *
     c                   eval      acpll1_lfgr = l_fgrp
     c                   eval      acpll1_lfir = p_firm
     c     acpll1_key    setll     acpll1
     c     acpll1_key    reade     acpll1
     c                   dow       not %eof
      *
     c                   eval      d_fgrt = aclfgt
     c                   eval      d_firt = aclfit
      *
     c                   call      'VV791C'
     c                   parm                    d_list
      *
     c     acpll1_key    reade     acpll1
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   move      *on           *inlr
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_enhg
     c                   parm                    p_enhn
      *
      * Key for les på filkopiering-linje
      *
     c     acpll1_key    klist
     c                   kfld                    acpll1_lfgr
     c                   kfld                    acpll1_lfir
      *
     c                   endsr
