     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System  . . . . : ASLAGR
      *  Program . . . . : LQ601R
      *  Beskrivelse . . : Utskrift av lagertransaksjons-liste
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      * --------- ------  --------------------------------------------------
5.30  *           310305   Justert feil test på type
5.70  *  PRGR     021208  Utvidet med prisgruppe                   bof
6.21  *  6.20     190511  Utvidet vl710r med div. info             bof
6.nu  *  6.30     040614  Utvidelser vedr. nummerutvidelse         bof
6.30  * 6.30  170918 bkv  Utvidet med trelast sortiment
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     fllhel1    if   e           k disk    rename(llhepfr:llhel1r)
     flldtl1    if   e           k disk    rename(lldtpfr:lldtl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
6.30 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
     flq601p    o    e             printer
      *
     d ltx             s             10    dim(5) ctdata perrcd(1)
      *
      * Parameter-inn
      *
     d                 ds
     d d_prec                        64
6.nu d  d_numm                        8  0 overlay(d_prec)
6.nu d  d_kode                        1    overlay(d_prec:9)
      *
      *  Local Data Area
      *
     d                uds
     d l_firm                944    946  0
     d l_navn                951    980
      *
      * Definering av variable i nøkler
      *
     d llhel1_numm     s                   like(lqnumm)
     d lldtl1_numm     s                   like(lsnumm)
     d vvarl1_vare     s                   like(vvvare)
     d vvenl1_vare     s                   like(vevare)
      *
      * Definering av variable
      *
     d w_firm          s                   like(laafir)
     d w_anta          s                   like(lsanta)
     d w_omrl          s                   like(veomre)
     d w_omrs          s                   like(veomre)
     d w_parm          s             64
5.70 d w_vtyp          s                   like(vvvtyp)
6.21 d w_udat          s                   like(vvudat)
6.21 d w_ldor          s                   like(vvldor)
6.21 d b_inlr          s              1    inz(*off)
6.30 d w_lv_nobb       s                   like(vvlnob)
6.30 d w_lv_modn       s                   like(vvlmod)
6.30 d w_lv_lldo       s                   like(vvlnle)
6.30 d w_lv_llva       s                   like(vvllva)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Definering av formater
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Benyttede formater
      *  - - - - - - - - - -
      *
      *  A1HDR  - Heading A1 på rapport
      *  D1DET  - Detail  D1 på rapport
      *  T1TOT  - Total   T1 på rapport
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  A1HDR Behandle heading A1
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Hent opplysninger fra LAGERBUNT-HODE
      *
     c     llhel1_key    chain     llhel1
      *
      *  Legger inn opplysninger i utfeltene
      *
     c                   eval      a1numm = lqnumm
     c                   eval      a1type = lqtype
     c                   eval      a1text = lqtext
     c                   eval      a1user = lquser
     c                   eval      a1wsid = lqwsid
      *
      *  Bestemmer hvilken hodetekst som skal skrives (Kontroll/Kvittering)
      *
     c                   eval      *in41 = *off
B001 c                   if        d_kode <> *blank
     c                   eval      *in41 = *on
E001 c                   endif
      *
      * Skrive heading
      *
     c                   write     a1hdr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  D1DET Behandle file
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Lese file
      *
     c     lldtl1_key    setll     lldtl1
     c     lldtl1_key    reade     lldtl1
      *
B001 c                   dow       not %eof
 001  *
 001  *  Legger inn opplysninger i utfeltene
 001  *
     c                   exsr      omr_antall
      *
 001 c                   eval      d1line = lsline
 001 c                   eval      d1vare = lsvare
 001 c                   eval      d1tek1 = lstek1
 001 c                   eval      d1anta = lsanta
 001 c                   eval      d1enhr = lsenh1
 001 c                   eval      d1antl = w_anta
 001 c                   eval      d1enhl = vvenhl
 001 c                   eval      d1lage = lslage
 001 c                   eval      d1last = lslast
 001 c                   eval      d1type = lstype
 001  *
 001  *  Legger inn overliggende type dersom blank-type
 001  *
B002 c                   if        d1type = *blank
 002 c                   eval      d1type = lqtype
E002 c                   endif
 001  *
 001  *  Legger inn overliggende lager, dersom blank-type
 001  *
B002 c                   if        d1lage = 0
 002 c                   eval      d1lage = lqlage
E002 c                   endif
 001  *
 001  *  Legger inn overliggende lager til, dersom blank-type
 001  *
B002 c                   if        d1last = 0
 002 c                   eval      d1last = lqlast
E002 c                   endif
 001  *
 001  *  Sjekker om dette er en tekst-linje
 001  *
 001 c                   move      *off          *in51
B002 c                   if        d1type = 'TX'
 002 c                   move      *on           *in51
E002 c                   endif
 001  *
 001  *  Sjekker om dette er en overføring
 001  *
 001 c                   move      *off          *in52
B002 c                   if        d1type = 'O '
 002 c                   move      *on           *in52
E002 c                   endif
 001  *
 001  *  Legger inn lager-type-tekst i heading
 001  *
 001 c                   movel     ltx(01)       d1otxt
B002 c                   if        d1type = 'I '
 002 c                   movel     ltx(02)       d1otxt
E002 c                   endif
B002 c                   if        d1type = 'U '
 002 c                   movel     ltx(03)       d1otxt
E002 c                   endif
B002 c                   if        d1type = 'J '
 002 c                   movel     ltx(04)       d1otxt
E002 c                   endif
B002 c                   if        d1type = 'O '
 002 c                   movel     ltx(05)       d1otxt
E002 c                   endif
 001  *
 001  *  Behandle record
 001  *
 001 c                   add       1             t1post
 001 c                   write     d1det                                30
 001  *
 001  *  Er det overflow ?
 001  *
B002 c                   if        *in30 = *on
 002 c                   write     a1hdr
E002 c                   endif
 001  *
     c     lldtl1_key    reade     lldtl1
E001 c                   enddo
      *
     c     xend01        tag
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  T1TOT Behandle total T1
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Skrive total
      *
     c                   write     t1tot                                30
     c                   goto      xslutt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for omregning av antall fra registrert til lagerenhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     omr_antall    begsr
      *
      * Finn lagerenhet på vareregister
      *
     c                   eval      vvarl1_vare = lsvare
     c     vvarl1_key    chain     vvarl1r
     c                   if        not %found
     c                   eval      w_anta = lsanta
     c                   eval      vvenhl = lsenh1
     c                   goto      end_omr
     c                   endif
      *
      * Dersom opptalt enhet er lik lagerenhet eller diversevare
      * beholdes antall som registrert
      *
     c                   if        lsenh1 = vvenhl
     c                   eval      w_anta = lsanta
     c                   goto      end_omr
     c                   endif
      *
      * henter varetype
      *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    lsvare
5.70 c                   parm                    lslage
5.70 c                   parm                    w_vtyp
6.21 c                   parm                    w_udat
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
      *
5.70 c                   if        w_vtyp = 'D'
     c                   goto      end_omr
     c                   endif
      *
      * Finner omregningsfaktor volum for lagerenhet og for opptalt enhet
      * Antall regnes om etter forholdet mellom disse
      *
     c                   eval      vvenl1_vare = lsvare
     c     vvenl1_key    setll     vvenl1
     c     vvenl1_key    reade     vvenl1
     c                   dow       not %eof
     c                   if        lsenh1 = veenhe
     c                   eval      w_omrl = veomre
     c                   endif
     c                   if        vvenhl = veenhe
     c                   eval      w_omrs = veomre
     c                   endif
     c     vvenl1_key    reade     vvenl1
     c                   enddo
      *
     c                   if        w_omrs = 0
     c                   eval      w_omrs = 1
     c                   endif
      *
     c                   if        w_omrl <> 0
     c                   eval      w_anta = lsanta * w_omrl / w_omrs
     c                   else
     c                   eval      w_anta = 0
     c                   endif
      *
     c     end_omr       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    w_parm
      *
     c                   eval      d_prec = w_parm
     c                   eval      a1fnav = l_navn
      *
      *  Key for status
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for LAGERBUNT-HODE
      *
     c     llhel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    llhel1_numm
      *
      *  Key for LAGERBUNT-LINJE
      *
     c     lldtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lldtl1_numm
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les av vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
      *
      *  Legge inn verdier i nøkler
      *
     c                   eval      w_firm = l_firm
     c                   eval      llhel1_numm = d_numm
     c                   eval      lldtl1_numm = d_numm
      *
      *  Hent opplysninger fra LAGER-STATUS-KODER
      *
     c     lstsl1_key    chain     lstsl1r
      *
     c                   endsr
**     LAGER - TYPER
Feilkode
Inngang
Uttak
Justering
Overføring
