     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK699R                                              *
      * Beskrivelse . . : Sletting av generalnota                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
6.10  * V6.10     220909  Lagt inn sporingsfelter                        BSA  *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frktrlu    uf   e           k disk    rename(rktrpfr:rktrlur)
     fliste     o    f  132        printer oflind(*inof)
      *
      /EJECT
      *
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      * LOCAL DATA
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     d                uds
6.NU d psafr                 300    307  0
6.NU d psati                          8  0
     d psaar                          4  0
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d antnul          s              7  0
      *
      ** Leser kundetransaksjoner ######################## START ######
      *
     c     nyles         tag
      *
     c                   read      rktrlu                                 91
     c                   if        *in91 = *on
     c                   goto      slutt
     c                   endif
      *
      ** Riktig firma
      *
     c                   if        rnfirm <> l_firm
     c                   goto      slutt
     c                   endif
      *
      ** Riktig år
      *
     c                   if        rnraar <> psaar
     c                   goto      nyles
     c                   endif
      *
      ** Med på generalnota tidligere
      *
     c                   if        rnsamf < psafr
     c                   goto      nyles
     c                   endif
      *
     c                   if        rnsamf > psati
     c                   goto      nyles
     c                   endif
      *
      * Oppdater transaksjon
      *
     c                   eval      rnsamf = *zero
     c                   eval      antnul = antnul + 1
6.10 c                   time                    rnedat
6.10 c                   time                    rnetim
6.10 c                   eval      rneusr = l_user
     c                   update    rktrlur
      *
     c                   goto      nyles
      *
      ** Leser kundetransaksjoner ######################## SLUTT ######
      *
      /space 3
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c     rnkey         klist
     c                   kfld                    rnfirm
     c                   kfld                    rnkund
     c                   kfld                    rnbdat
      **
      ** Setter startverdier
      **
     c                   eval      antnul = *zero
     c                   move      l_firm        rnfirm
     c                   eval      rnkund = *zero
     c                   eval      rnbdat = *loval
     c     rnkey         setll     rktrlu
      *
     c                   endsr
      *
     oliste     d    lr                     2 05
     o                       l_firm              13
     o                       l_fnav              45
      *
     o          t    lr                     2
     o                                           30 'GENERALNOTA NR. FRA'
     o                       psafr         z     37
     o                                           41 'TIL'
     o                       psati         z     48
     o                                           61 'ER NULLSTILT'
      *
     o          t    lr                     2
     o                                           34 'HUSK Å KORRIGERE SIST BR'
     o                                           58 'UKTE NR. I KODEREGISTERE'
     o                                           73 'T FØR OMKJØRING'
      *
     o          t    lr                     2
     o                                           34 'ANTALL POSTER NULLSTILLT'
     o                       antnul        z     42
