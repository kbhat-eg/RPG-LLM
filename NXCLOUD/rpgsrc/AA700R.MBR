     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASPGPL
      * Program . . . . : AA700R
      * Beskrivelse . . : Syst. Adm. Oppsett bib.lister v/batch
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fausrl1    if   e           k disk    rename(ausrpfr:ausrl1r)
     falibl1    if   e           k disk    rename(alibpfr:alibl1r)
      *
      * Definering av parametere
      *
     d p_user          s                   like(abuser)
      *
      * Definering av variable i nøkler
      *
     d ausrl1_user     s                   like(abuser)
     d alibl1_user     s                   like(aluser)
      *
      * Definering av variable
      *
     d w_bibl          s                   like(albibl)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedrutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Slå opp i brukerfilen
      *
     c                   eval      ausrl1_user = p_user
     c     ausrl1_key    chain     ausrl1
      *
      * Dersom bruker ikke ble funnet
      * avsluttes programmet
      *
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
      * Bygger bibliotekslister
      *
     c                   eval      alibl1_user = p_user
     c     alibl1_key    setll     alibl1
     c     alibl1_key    reade     alibl1
      *
     c                   dow       not %eof
      *
      * Kall program som
      * bygger biblioteksliste
      *
     c                   if        albibl <> *blank
     c                   eval      w_bibl =  albibl
     c                   call      'AA700C'
     c                   parm                    w_bibl
     c                   endif
      *
     c     alibl1_key    reade     alibl1
     c                   enddo
      *
      * Legger inn current library
      *
     c                   if        absbib <> *blank
     c                   eval      w_bibl =  absbib
     c                   call      'AA701C'
     c                   parm                    w_bibl
     c                   endif
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_user
      *
      * Key for brukerregister
      *
     c     ausrl1_key    klist
     c                   kfld                    ausrl1_user
      *
      * Key for bibliotekslister
      *
     c     alibl1_key    klist
     c                   kfld                    alibl1_user
      *
     c                   endsr
      *
