     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VE719R
      * Beskrivelse . . : Henter GTIN nr ut fra gitt varenr + enhet + leverandør
      *
      *                   Input : firma, leverandør, varenr., enhet
      *                   Output: GTIN nr  status (1 = ok)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       190112 bsa  Nytt program
6.10  * NEB   230512 bsa  Sjekker også opp uten leverandør, samt mot
6.10  *                   vareadministrasjon.
6.30  * 6.30  260113 bof  Omskriving i forb. endring pakning VA
6.32  *       140314 bof  Lagt inn test på fra /til dato på pakning
6.33  * 6.30  100414 bof  Feilretting vedr. hente gtin fra va
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarlr    if   e           k disk    rename(vvarpfr:vvarlrr)
     fvveplr    if   e           k disk    rename(vveppfr:vveplrr)
6.10 fjvpkl1    if   e           k disk    rename(jvpkpfr:jvpkl1r)
6.30 fjlevl3    if   e           k disk    rename(jlevpfr:jlevl3r)
      *
      * Definering av variable i nøkler
      *
     d vvarlr_firm     s                   like(vvfirm)
     d vvarlr_vare     s                   like(vvvare)
     d vveplr_pfir     s                   like(vepfir)
     d vveplr_pvar     s                   like(vepvar)
     d vveplr_penh     s                   like(vepenh)
     d vveplr_pldo     s                   like(vepldo)
6.10 d vvepl1_pfir     s                   like(vepfir)
6.10 d vvepl1_pvar     s                   like(vepvar)
6.10 d vvepl1_penh     s                   like(vepenh)
6.10 d jvpkl1_vare     s                   like(jwvare)
6.30 d jvpkl1_ldor     s                   like(jwldor)
6.30 d jlevl3_enum     s                   like(jlenum)
      *
      * Definering av pamametre
      *
     d p_firm          s                   like(vepfir)
     d p_eann          s                   like(vepgti)
     d p_vare          s                   like(vepvar)
     d p_ldor          s                   like(vvldor)
     d p_enhe          s                   like(vepenh)
     d p_stat          s              1
      *
6.32 d w_dato          s               d   datfmt(*iso)
6.32 d b_pakn          s              1    inz(*off)
      *
     d                uds
     d l_user                911    920
     d l_sfir                944    946  0
     d l_savd                947    950  0
     d l_lagr               1001   1002  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer output-parametre
      *
     c                   eval      p_eann = 0
     c                   eval      p_stat = *blank
      *
     c                   eval      vvarlr_firm = p_firm
     c                   eval      vvarlr_vare = p_vare
     c     vvarlr_key    chain     vvarlr
     c                   if        not %found(vvarlr)
     c                   goto      avslutt
     c                   endif
      *
6.10 c                   if        p_ldor <> 0
      * Henter GTIN kode fra VVEP på angitt enhet og leverandør
      *
     c                   eval      vveplr_pfir = p_firm
     c                   eval      vveplr_pvar = p_vare
     c                   eval      vveplr_penh = p_enhe
     c                   eval      vveplr_pldo = p_ldor
     c     vveplr_key    chain     vveplr
     c                   if        %found(vveplr) and
     c                             vepgti <> 0
     c                   eval      p_eann = vepgti
     c                   eval      p_stat = '1'
     c                   goto      avslutt
     c                   endif
6.10 c                   endif
6.10  *
6.10  * Sjekk en gang til på VVEP uavhengig av leverandør, men med angitt enhet
6.10  *
6.10 c                   eval      vvepl1_pfir = p_firm
6.10 c                   eval      vvepl1_pvar = p_vare
6.10 c                   eval      vvepl1_penh = p_enhe
6.10 c     vvepl1_key    setll     vveplr
6.10 c     vvepl1_key    reade     vveplr                                 91
6.10 c                   dow       *in91 = *off
6.10  * Har vi match på leverandør ?
6.10 c                   if        p_enhe = vepenh and
6.10 c                             vepgti <> 0
6.10 c                   eval      p_eann = vepgti
6.10 c                   eval      p_stat = '1'
6.10 c                   goto      avslutt
6.10 c                   endif
6.10 c     vvepl1_key    reade     vveplr                                 91
6.10 c                   enddo
6.10  *
6.10  * Hvis ikke funnet - sjekk mot vareadministrasjon.
6.10  * Først forutsetter vi match på leverandør
6.10  *
6.30 c                   eval      jvpkl1_vare = p_vare
6.33 c                   eval      jvpkl1_ldor = jlldor
6.30 c     jvpkl1_key    setll     jvpkl1
6.30 c     jvpkl1_key    reade     jvpkl1
6.30 c                   dow       not %eof(jvpkl1)
6.32  *
6.32  * sjekker fra / tildato
6.32 c                   exsr      sjekk_pkdato
6.32 c                   if        b_pakn = *on
6.32  *
6.33 c                   if        jlldor = jwldor and
6.30 c                             jwgtin <> 0
6.30 c                   eval      p_eann = jwgtin
6.30 c                   eval      p_stat = '2'
6.30 c                   goto      avslutt
6.30 c                   endif
6.32 c                   endif
6.30 c     jvpkl1_key    reade     jvpkl1
6.30 c                   enddo
6.30  *
6.30  * Prøver til slutt uten krav om leverandør
6.30  *
6.30 c                   eval      jvpkl1_vare = p_vare
6.30 c     jvpkl1_key2   setll     jvpkl1
6.30 c     jvpkl1_key2   reade     jvpkl1
6.30 c                   dow       not %eof(jvpkl1)
6.30 c                   if        jwgtin <> 0   and
6.30 c                             jwenhe = p_enhe
6.30 c                   eval      p_eann = jwgtin
6.30 c                   eval      p_stat = '2'
6.30 c                   goto      avslutt
6.30 c                   endif
6.30 c     jvpkl1_key2   reade     jvpkl1                                 91
6.30 c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
      *
     c                   return
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * Subrutine for å sjekke om dato er innenfor fra /til dato
6.32  * hvis *loval i begge datoer = ok
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     sjekk_pkdato  begsr
6.32  *
6.32 c                   eval      b_pakn = *on
6.32 c                   time                    w_dato
6.32  *
6.32 c                   if        jwfdat = *loval and
6.32 c                             jwtdat = *loval
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        jwfdat <= w_dato and
6.32 c                             jwtdat >  w_dato
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        jwfdat = *loval  and
6.32 c                             jwtdat >  w_dato
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        jwfdat <= w_dato and
6.32 c                             jwtdat =  *loval
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   eval      b_pakn = *off
6.32  *
6.32 c                   endsr
6.32  *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
     c                   parm                    p_vare
     c                   parm                    p_enhe
     c                   parm                    p_eann
     c                   parm                    p_stat
      *
      * Key for les på vareenhets-register
      *
     c     vveplr_key    klist
     c                   kfld                    vveplr_pfir
     c                   kfld                    vveplr_pvar
     c                   kfld                    vveplr_penh
     c                   kfld                    vveplr_pldo
      *
      * Key for les på vareregister
      *
     c     vvarlr_key    klist
     c                   kfld                    vvarlr_firm
     c                   kfld                    vvarlr_vare
6.10  *
6.10  * Key for les på VVEP-register
6.10  *
6.10 c     vvepl1_key    klist
6.10 c                   kfld                    vvepl1_pfir
6.10 c                   kfld                    vvepl1_pvar
6.10 c                   kfld                    vvepl1_penh
6.10  *
6.10  * Key for les på vare-pakning
6.10  *
6.10 c     jvpkl1_key    klist
6.10 c                   kfld                    jvpkl1_vare
6.30 c                   kfld                    jvpkl1_ldor
6.30  *
6.30  * Key for les på vare-pakning
6.30  *
6.30 c     jvpkl1_key2   klist
6.30 c                   kfld                    jvpkl1_vare
6.30  *
6.30  * Key for les på leverandør i VA
6.30  *
6.30 c     jlevl3_key    klist
6.30 c                   kfld                    jlevl3_enum
6.30  *
6.30  * leser leverandør i VA
6.30  *
6.30 c                   eval      jlevl3_enum = p_ldor
6.30 c     jlevl3_key    chain     jlevl3
      *
     c                   endsr
