     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JR100R
      * Beskrivelse . . : Rapportør, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       111099 HKO  Nytt program
6.11  *       020311 bof  Må godta blank i levnr. også ved S4/FELLESKJ
7.xx  * 7.00  030616 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjrapl1    if   e           k disk    rename(jrappfr:jrapl1r)
     fjrapl2    if   e           k disk    rename(jrappfr:jrapl2r)
     fjrapl3    if   e           k disk    rename(jrappfr:jrapl3r)
     fjraplr    if   e           k disk    rename(jrappfr:jraplrr)
     fjraplu    uf a e           k disk    rename(jrappfr:jraplur)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fjvrcl1    if   e           k disk    rename(jvrcpfr:jvrcl1r)
      *
     fjr100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d jrapl1_rapp     s                   like(jrrapp)
     d jrapl2_navn     s                   like(jrnavn)
     d jrapl3_rfmt     s                   like(jrrfmt)
     d jraplr_rapp     s                   like(jrrapp)
     d jraplu_rapp     s                   like(jrrapp)
     d jvrcl1_rfmt     s                   like(jfrfmt)
     d jlevl1_ldor     s                   like(jlldor)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(jrfirm)
      *
     d s_rapp          s                   like(jrrapp)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2rapp <> 0 or
     c                             b2navn <> *blank or
     c                             b2rfmt <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'N'
     c                   eval      jrapl2_navn = b1navn
     c     jrapl2_key    setll     jrapl2
     c                   when      w_seqe = 'R'
     c                   eval      jrapl3_rfmt = b1rfmt
     c     jrapl3_key    setll     jrapl3
     c                   other
     c                   eval      jrapl1_rapp = b1rapp
     c     jrapl1_key    setll     jrapl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Rapportør
      *
     c                   if        b2rapp <> 0
     c                   eval      w_seqe = *blank
     c                   eval      jrapl1_rapp = b2rapp
     c     jrapl1_key    setll     jrapl1
     c                   goto      end_pos
     c                   endif
      *
      * Navn
      *
     c                   if        b2navn <> *blank
     c                   eval      w_seqe = 'N'
     c                   eval      jrapl2_navn = b2navn
     c     jrapl2_key    setll     jrapl2
     c                   goto      end_pos
     c                   endif
      *
      * Recordformat
      *
     c                   if        b2rfmt <> *blank
     c                   eval      w_seqe = 'R'
     c                   eval      jrapl3_rfmt = b2rfmt
     c     jrapl3_key    setll     jrapl3
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2rapp = 0
     c                   eval      b2navn = *blank
     c                   eval      b2rfmt = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1rapp = b1rapp
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1rapp = b1rapp
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1rapp = b1rapp
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1rapp = b1rapp
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Varegruppereferanse
      *
     c                   if        b1valg = 7
     c                   call      'JR120R'
     c                   parm                    w_firm
     c                   parm                    b1rapp
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Mottaksfilter
      *
     c                   if        b1valg = 8
     c                   call      'JR130R'
     c                   parm                    w_firm
     c                   parm                    b1rapp
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1rapp = b2rapp
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1rapp = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      jraplr_rapp = c1rapp
     c     jraplr_key    chain     jraplr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1rapp = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2rapp = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      jraplr_rapp = c1rapp
     c     jraplr_key    chain     jraplr
      *
     c                   if        %found
     c                   eval      c2navn = jrnavn
     c                   eval      c2ldor = jrldor
     c                   eval      c2rfmt = jrrfmt
     c                   eval      c2fldr = jrfldr
     c                   eval      c2file = jrfile
     c                   eval      c2eusr = jreusr
     c     *dmy          move      jredat        c2edat
     c                   else
     c                   eval      c2navn = *blank
     c                   eval      c2ldor = 0
     c                   eval      c2rfmt = *blank
     c                   eval      c2fldr = *blank
     c                   eval      c2file = *blank
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   endif
      *
      * Henter leverandørnavn og recordformat-beskrivelse
      *
     c                   eval      c2lnav = *blank
     c                   eval      c2rbes = *blank
      *
     c                   if        c2ldor <> 0
     c                   eval      jlevl1_ldor = c2ldor
     c     jlevl1_key    chain     jlevl1
     c                   if        %found
     c                   eval      c2lnav = jlnavn
     c                   endif
     c                   endif
      *
     c                   if        c2rfmt <> *blank
     c                   eval      jvrcl1_rfmt = c2rfmt
     c     jvrcl1_key    chain     jvrcl1
     c                   if        %found
     c                   eval      c2rbes = jfbesk
     c                   endif
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1rapp = k1rapp
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
      *
     c                   if        w_afld = 'C2LDOR'
     c                   call      'JL500R'
     c                   parm                    c2ldor
     c                   parm                    c2lnav
     c                   endif
      *
     c                   if        w_afld = 'C2RFMT'
     c                   call      'JF500R'
     c                   parm                    c2rfmt
     c                   parm                    c2rbes
     c                   endif
      *
     c                   goto      c2taga
      *
     c                   endif
      *
      * Validering av input
      *
     c                   eval      c2lnav = *blank
     c                   eval      c2rbes = *blank
      *
     c                   if        c2navn = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   if        c2ldor = 0 and                               Betingelse 1
     c                             c2rfmt <> 'NOBB' and
6.11 c                             c2rfmt <> 'S4'   and
6.11 c                             c2rfmt <> 'S4/FELLESK'
     c                   eval      *in32 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   if        c2ldor <> 0
     c                   eval      jlevl1_ldor = c2ldor
     c     jlevl1_key    chain     jlevl1
     c                   if        not %found
     c                   eval      *in33 = *on
     c                   goto      c2taga
     c                   endif
     c                   eval      c2lnav = jlnavn
     c                   endif
      *
     c                   if        c2rfmt = *blank
     c                   eval      *in35 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   eval      jvrcl1_rfmt = c2rfmt
     c     jvrcl1_key    chain     jvrcl1
     c                   if        not %found
     c                   eval      *in36 = *on
     c                   goto      c2taga
     c                   endif
     c                   eval      c2rbes = jfbesk
      *
     c                   if        c2file = *blank
     c                   eval      *in37 = *on
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   jraplur
      *
     c                   eval      jraplu_rapp = c1rapp
     c     jraplu_key    chain     jraplur
      *
     c                   eval      jrnavn = c2navn
     c                   eval      jrldor = c2ldor
     c                   eval      jrrfmt = c2rfmt
     c                   eval      jrfile = c2file
     c                   eval      jrfldr = c2fldr
      *
     c                   time                    jredat
     c                   time                    jretim
     c                   eval      jreusr = l_user
      *
     c                   if        %found
     c                   update    jraplur
     c                   else
     c                   eval      jrfirm = w_firm
     c                   eval      jrrapp = c1rapp
     c                   write     jraplur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1navn = jrnavn
     c                   eval      b1ldor = jrldor
     c                   eval      b1rfmt = jrrfmt
     c                   eval      b1file = jrfile
     c                   eval      b1fldr = jrfldr
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      jraplr_rapp = d1rapp
     c     jraplr_key    chain     jraplr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      jraplu_rapp = d1rapp
     c     jraplu_key    chain     jraplur
      *
     c                   delete    jraplur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_rapp = k1rapp
      *
      * Fyller kopierings-bildet
      *
     c                   eval      jraplr_rapp = k1rapp
     c     jraplr_key    chain     jraplr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1rapp = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      jraplr_rapp = k1rapp
     c     jraplr_key    chain     jraplr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1rapp = s_rapp
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'N'
     c                   read      jrapl2
     c                   when      w_seqe = 'R'
     c                   read      jrapl3
     c                   other
     c                   read      jrapl1
     c                   endsl
      *
     c                   if        %eof or
     c                             jrfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'N'
     c                   eval      jrapl2_navn = jrnavn
     c                   when      w_seqe = 'R'
     c                   eval      jrapl3_rfmt = jrrfmt
     c                   other
     c                   eval      jrapl1_rapp = jrrapp
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1rapp = jrrapp
     c                   eval      b1navn = jrnavn
     c                   eval      b1ldor = jrldor
     c                   eval      b1rfmt = jrrfmt
     c                   eval      b1file = jrfile
     c                   eval      b1fldr = jrfldr
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'N'
     c     jrapl2_key    setgt     jrapl2
     c                   readp     jrapl2
     c                   when      w_seqe = 'R'
     c     jrapl3_key    setgt     jrapl3
     c                   readp     jrapl3
     c                   other
     c     jrapl1_key    setgt     jrapl1
     c                   readp     jrapl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'N'
     c                   readp     jrapl2
     c                   when      w_seqe = 'R'
     c                   readp     jrapl3
     c                   other
     c                   readp     jrapl1
     c                   endsl
      *
     c                   if        %eof or
     c                             jrfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'N'
     c                   eval      jrapl2_navn = jrnavn
     c                   when      w_seqe = 'R'
     c                   eval      jrapl3_rfmt = jrrfmt
     c                   other
     c                   eval      jrapl1_rapp = jrrapp
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'N'
     c     jrapl2_key    setll     jrapl2
     c                   when      w_seqe = 'R'
     c     jrapl3_key    setll     jrapl3
     c                   other
     c     jrapl1_key    setll     jrapl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på rapportør
      *
     c     jrapl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jrapl1_rapp
      *
      * Key for posisjonering på navn
      *
     c     jrapl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jrapl2_navn
      *
      * Key for posisjonering på recordformat
      *
     c     jrapl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jrapl3_rfmt
      *
      * Key for les på rapportør
      *
     c     jraplr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jraplr_rapp
      *
      * Key for oppdatering av rapportør
      *
     c     jraplu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jraplu_rapp
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på vare-recordformat
      *
     c     jvrcl1_key    klist
     c                   kfld                    jvrcl1_rfmt
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      jrapl1_rapp = 0
     c     jrapl1_key    setll     jrapl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
