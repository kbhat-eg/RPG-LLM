     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSHOP
      * Program . . . . : BU920R
      * Beskrivelse . . : Butikk, henter kalulert pris og rabatt
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       280604 HKO  Nytt program
      *       041104 HKO  Endret parameter ved kall til VP900R
5.70  * PRGR  121208 bof  Utvidet med prisgruppe
6.31  * 6.30  210815 bof  feilretting i parameter til VP900R
6.32  * 6.30  040816 bsa  Sender med prioritert leverandør
6.33  * 6.30  170918 bkv  Utvidet med trelast sortiment
6.34  * 6.30  080318 bof  Utvidet med kundeklubb
8.01  *PRG2   080622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Definering av parametre
      *
     d p_firm          s              3
     d p_kund          s              6  0
     d p_kpro          s              6  0
     d p_vare          s             15
     d p_lety          s              1  0
     d p_lagr          s              2  0
     d p_avde          s              4  0
     d p_enhe          s              3
     d p_priv          s              1  0
     d p_olet          s              1  0
     d p_kpri          s              1
     d p_oppr          s              9  2
     d p_sapr          s              9  2
     d p_kopr          s              9  2
     d p_rab1          s              5  2
     d p_rab2          s              5  2
     d p_kamp          s              5
     d p_alme          s              4  0
     d p_brty          s              1  0
     d p_brr1          s              5  2
     d p_brr2          s              5  2
     d p_kmva          s              1
6.34 d p_klub          s              1
      *
      * Definering av datastrukturer
      *
      * Parametre for henting av pris -inn
      *
     d                 ds
     d hirec                        120
     d  hifirm                        3  0 overlay(hirec)
     d  hirkat                        3  0 overlay(hirec:4)
     d  hikund                        6  0 overlay(hirec:7)
     d  hikpro                        6  0 overlay(hirec:13)
     d  hivare                       15    overlay(hirec:19)
     d  hilety                        1  0 overlay(hirec:34)
     d  hienhe                        3    overlay(hirec:35)
     d  hidato                         d   overlay(hirec:38) datfmt(*iso)
     d  hitime                         t   overlay(hirec:48) timfmt(*iso)
6.31 d  hinumm                        8  0 overlay(hirec:56)
6.31 d  hikode                        1    overlay(hirec:64)
6.31 d  hidekn                        5  2 overlay(hirec:65)
6.31 d  hiavgf                        1  0 overlay(hirec:70)
6.31 d  hipriv                        1  0 overlay(hirec:71)
6.31 d  hitilb                        1    overlay(hirec:72)
6.31 d  hiskaf                        1    overlay(hirec:73)
6.31 d  hildor                        6  0 overlay(hirec:74)
6.31 d  hisapr                        9  2 overlay(hirec:80)
6.31 d  hikopr                        9  2 overlay(hirec:89)
8.01 d  hiprgr                        2    overlay(hirec:98)
8.01 d  hiklub                        1    overlay(hirec:100)
     d  hiinlr                        1    overlay(hirec:120)
      *
      * Parametre for henting av pris -ut
      *
     d                 ds
     d horec                         80
     d  holety                        1  0 overlay(horec)
     d  hokpri                        1    overlay(horec:2)
     d  hooppr                        9  2 overlay(horec:3)
     d  hosapr                        9  2 overlay(horec:12)
     d  hokopr                        9  2 overlay(horec:21)
     d  horab1                        5  2 overlay(horec:30)
     d  horab2                        5  2 overlay(horec:35)
     d  hodekn                        5  2 overlay(horec:40)
     d  hopros                        5  2 overlay(horec:45)
     d  hokamp                        5    overlay(horec:50)
     d  hoalme                        4  0 overlay(horec:55)
     d  hobrty                        1  0 overlay(horec:59)
     d  hobrr1                        5  2 overlay(horec:60)
     d  hobrr2                        5  2 overlay(horec:65)
     d  hoomva                        1    overlay(horec:70)
8.01 d  hoprgr                        2    overlay(horec:71)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
6.32 d w_ldor          s              6  0
6.32 d w_utda          s               d   datfmt(*iso)
6.32 d w_vtyp          s              1
6.32 d b_inlr          s              1    inz(*off)
6.33 d w_lv_nobb       s              8  0
6.33 d w_lv_modn       s              8  0
6.33 d w_lv_lldo       s              6  0
6.33 d w_lv_llva       s             20
      *
8.01 d w_prgr          s              2
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Utleder parametre for kall av program
      *
      *
      * Fyller opp datastruktur for kall av program
      *
     c                   eval      hifirm = w_firm
     c                   eval      hirkat = 0
     c                   eval      hikund = p_kund
     c                   eval      hikpro = p_kpro
     c                   eval      hivare = p_vare
     c                   eval      hilety = p_lety
     c                   eval      hienhe = p_enhe
     c                   eval      hidato = *loval
     c                   eval      hitime = *loval
     c                   eval      hinumm = 0
     c                   eval      hikode = *blank
     c                   eval      hidekn = 0
     c                   eval      hiavgf = 0
     c                   eval      hipriv = p_priv
     c                   eval      hitilb = *off
     c                   eval      hiskaf = *off
6.32 c**                 eval      hildor = 0
6.32 c                   eval      hildor = w_ldor
     c                   eval      hisapr = 0
     c                   eval      hikopr = 0
     c                   eval      hiinlr = *off
5.70 c                   eval      hiprgr = w_prgr
6.34 c                   eval      hiklub = p_klub
      *
     c                   time                    hidato
     c                   time                    hitime
      *
      * Kaller program
      *
     c                   call      'VP900R'
     c                   parm                    hirec
     c                   parm                    horec
      *
      * Henter ouput-parametre
      *
     c                   eval      p_olet = holety
     c                   eval      p_kpri = hokpri
     c                   eval      p_oppr = hooppr
     c                   eval      p_sapr = hosapr
     c                   eval      p_kopr = hokopr
     c                   eval      p_rab1 = horab1
     c                   eval      p_rab2 = horab2
     c                   eval      p_kamp = hokamp
     c                   eval      p_alme = hoalme
     c                   eval      p_brty = hobrty
     c                   eval      p_brr1 = hobrr1
     c                   eval      p_brr2 = hobrr2
     c                   eval      p_kmva = hoomva
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameter
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kund
     c                   parm                    p_kpro
     c                   parm                    p_vare
     c                   parm                    p_lety
     c                   parm                    p_lagr
     c                   parm                    p_avde
     c                   parm                    p_enhe
     c                   parm                    p_priv
     c                   parm                    p_olet
     c                   parm                    p_kpri
     c                   parm                    p_oppr
     c                   parm                    p_sapr
     c                   parm                    p_kopr
     c                   parm                    p_rab1
     c                   parm                    p_rab2
     c                   parm                    p_kamp
     c                   parm                    p_alme
     c                   parm                    p_brty
     c                   parm                    p_brr1
     c                   parm                    p_brr2
     c                   parm                    p_kmva
6.34 c                   parm                    p_klub
      *
      *
      *
     c                   move      p_firm        w_firm
5.70  *
5.70  * henter prisgruppe
5.70  *
5.70 c                   call      'VL712R   '
     c                   parm                    w_firm
     c                   parm                    p_lagr
     c                   parm                    p_avde
5.70 c                   parm                    w_prgr
6.32  *
6.32  * henter varetype (Primært for å finne leverandør)
6.32  *
6.32 c                   call      'VL710R'
6.32 c                   parm                    w_firm
6.32 c                   parm                    p_vare
6.32 c                   parm                    p_lagr
6.32 c                   parm                    w_vtyp
6.32 c                   parm                    w_utda
6.32 c                   parm                    w_ldor
6.32 c                   parm                    b_inlr
6.33 c                   parm                    w_lv_nobb
6.33 c                   parm                    w_lv_modn
6.33 c                   parm                    w_lv_lldo
6.33 c                   parm                    w_lv_llva
      *
     c                   endsr
