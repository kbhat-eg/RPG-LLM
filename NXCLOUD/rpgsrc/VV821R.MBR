     h option(*nodebugio : *srcstmt) datedit(*dmy)

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VV821R
      * Beskrivelse . . : Oppdater fra SOLVE enhet
      *
      *                   Oppdaterer enhet   SOLVE  (XML-format)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       020519 BKV
7.01  *       120120 bof  [@$ konv.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvenhlu    uf a e           k disk    rename(venhpfr:venhlur)
     f                                     infsr(FileErr)
     fjstsl1    if   e           k disk    rename(jstspfr:jstsl1r)
     f                                     infsr(FileErr)
      *
      * Definering av variable i nøkler
      *
     d venhlu_eenh     s                   like(vaeenh)
      *
      * Brukes som parametre til jobblogging
     d p_jbid          s             41
     d p_jsts          s              2  0
     d p_jmld          s            200
      *
      * Definering av Local Data Area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
     d w_firm          s              3  0
      *
      * Definering av parametre
      *
     D p_filNavn       s            255a
     D p_lengde        s              3p 0
     d p_sistDato      s             10a
     d p_stat          s              1
      *
      *  Dataelementer enhet
      *
      * Definering av variable
      *
     d w_eenh          s                   like(vaeenh)
     d w_etxt          s                   like(vaetxt)
      * Datastruktur for informasjon ved exceptions
      *
     d PgmError       sds
     d  proc_name        *proc
     d  pgm_status       *STATUS
     d  prv_status            16     20s 0
     d  line_numm             21     28
     d  routine          *ROUTINE
     d  parms            *PARMS
     d  excp_type             40     42
     d  excp_numm             43     46
     d  pgm_lib               81     90
     d  excp_data             91    170
     d  excp_id              171    174
     d  date                 191    198
     d  year                 199    200s 0
     d  last_file            201    208
     d  file_info            209    243
     d  job_name             244    253
     d  job_user             254    263
     d  job_numb             264    269s 0
      *
     D   navnet        s            100a   varying
6.33 D   len           s              3i 0
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
         len = p_lengde;
         navnet = %trim(%subst(p_filNavn:1:len));

         // setter navn på XML fil
         exec sql SET ENHET_SOLVE_XML_V = :navnet;

         // bruk view til å lese XML fil
         exec sql
           declare c1 cursor for
              select Enhet,Beskrivelse from ENHET_XML_SOLVE_VIEW
                  for read only;

         exec sql
           Open c1;

         exec sql
           Fetch c1 INTO :w_eenh, :w_etxt;

         doW (SQLCode >= 0 and SQLCode <> 100);

           exsr skr_enhe;

           exec sql
             Fetch c1 INTO :w_eenh, :w_etxt;
         enddo;

         exec sql close c1;

         p_sistDato = %char(%date(): *iso);  // OK

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     avslutt       tag

     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppdaterer enhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_enhe      begsr
      *
7.01  * [@$ konv.
7.01 c     X'46':'['     xlate     w_eenh        w_eenh
7.01 c     X'A0':'{'     xlate     w_eenh        w_eenh
7.01 c     X'77':'@'     xlate     w_eenh        w_eenh
7.01 c     X'90':'¦'     xlate     w_eenh        w_eenh
7.01 c     X'B4':'¦'     xlate     w_eenh        w_eenh
7.01 c     X'2A':'$'     xlate     w_eenh        w_eenh
7.01 c     X'EF':'}'     xlate     w_eenh        w_eenh
7.01 c     X'69':'}'     xlate     w_eenh        w_eenh
7.01  *
7.01 c     X'46':'['     xlate     w_etxt        w_etxt
7.01 c     X'A0':'{'     xlate     w_etxt        w_etxt
7.01 c     X'77':'@'     xlate     w_etxt        w_etxt
7.01 c     X'90':'¦'     xlate     w_etxt        w_etxt
7.01 c     X'B4':'¦'     xlate     w_etxt        w_etxt
7.01 c     X'2A':'$'     xlate     w_etxt        w_etxt
7.01 c     X'EF':'}'     xlate     w_etxt        w_etxt
7.01 c     X'69':'}'     xlate     w_etxt        w_etxt
      *
     c                   clear                   venhlur
      *
     c                   eval      venhlu_eenh = w_eenh
     c     venhlu_key    chain     venhlur
      *
     c                   time                    vaeeda
     c                   time                    vaeeti
     c                   eval      vaeeus = l_user
      *
     c                   if        %found
     c                   if        vaetxt = w_etxt
     c                   unlock    venhlu
     c                   else
     c                   eval      vaetxt = w_etxt
     c                   update    venhlur
     c                   endif
     c                   else
     c                   clear                   venhlur
     c                   eval      vaeenh = w_eenh
     c                   eval      vaetxt = w_etxt
     c
     c                   eval      vaefir = w_firm
     c                   time                    vaeoda
     c                   time                    vaeoti
     c                   eval      vaeous = l_user
     c                   time                    vaeeda
     c                   time                    vaeeti
     c                   eval      vaeeus = l_user
     c                   write     venhlur
     c                   endif
      *
     c                   endsr

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Error rutine for file errors
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     FileErr       begsr
      *
     c                   eval      p_stat = '1'
     c                   eval      p_jsts = 50
     c                   eval      p_jmld = 'Fil ' + %trim(last_file) +
     c                             ' feil. Info=' + %trim(excp_data) +
     c                             '. Job=' + %trim(job_name) + '/' +
     c                             %trim(job_user) + '/' +
     c                             %char(job_numb)
      *
     c                   call      'AB705R'
     c                   parm                    p_jbid
     c                   parm                    p_jsts
     c                   parm                    p_jmld
      *
     c                   goto      avslutt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_filNavn
     c                   parm                    p_lengde
     c                   parm                    p_sistDato
     c                   parm                    p_stat
      *
      * Key for oppdatering av enhet
      *
     c     venhlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    venhlu_eenh
      *
      * Key for oppslag på status
      *
     c     jstsl1_key    klist
     c                   kfld                    w_firm
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = l_firm
      *
     c     jstsl1_key    chain     jstsl1
      *
     c                   endsr
