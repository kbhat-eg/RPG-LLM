     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLOGI
      * Program . . . . : MM734R
      * Beskrivelse . . : Leser vareregister
      *                   Kaller beregning av prognose
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       261104 bø   Nytt program
5.50  *       281106 bø   Justert les av varesortiment
5.50  *       160307 bø   skal ikke ha med varer som er utgått
5.51  *       270807 bø   Oppdatere dato på parameter.
5.52  *       130508 tsv  Legger ut med nullverdier når innkjøp/år = 0
5.53  *       130508 tsv  Bruker abc-kode fra lager når bare ett lager
5.54  *       090608 tsv  Fikser hvis lagerstreng ikke er initiert
5.55  *       160608 tsv  Rettet en liten feil med danne prognose null.
5.70  *       151208 bof  Utvidet med varetype på lager
5.61  *       190409 bof  Rettet logikk i forb. med abc-kode fra lager
5.62  *       040509 bof  Utvidet søket etter parameter
5.63  *       180509 bof  Justert les av parameter for sortiment
      * 6.10  180609 BSA  Oppdatert med sporingsinformasjon
6.10  *       200812 bof  Feil-retting vedr. parameter-innhold
6.12  *       260413 bof  Feil vedr. vsort. og les av leverandør
6.20  * 6.20  021110 bof  Utvidet med utg.dato pr. lager
6.20  * 6.21  071110 bof  Utvidet med lager på varesortiment
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarlr    if   e           k disk    rename(vvarpfr:vvarlrr)
     fvvarl8    if   e           k disk    rename(vvarpfr:vvarl8r)
     fvvarla    if   e           k disk    rename(vvarpfr:vvarlar)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
5.63 fvsdtl1    if   e           k disk    rename(vsdtpfr:vsdtl1r)
     fvsdtl6    if   e           k disk    rename(vsdtpfr:vsdtl6r)
     fmmatlr    if   e           k disk    rename(mmatpfr:mmatlrr)
5.63 fmmatl2    if   e           k disk    rename(mmatpfr:mmatl2r)
     fmmatlu    uf   e           k disk    rename(mmatpfr:mmatlur)
     fmprolu    uf a e           k disk    rename(mpropfr:mprolur)
     fmpralu    uf a e           k disk    rename(mprapfr:mpralur)
      *
      *
     d mmatlr_vsor     s                   like(mmvsor)
     d mmatlr_ogrp     s                   like(mmogrp)
     d mmatlr_hgrp     s                   like(mmhgrp)
     d mmatlr_ugrp     s                   like(mmugrp)
     d mmatlr_ldor     s                   like(mmldor)
     d mmatlr_vare     s                   like(mmvare)
     d mmatlr_crdo     s                   like(mmcrdo)
5.63 d mmatl2_vsor     s                   like(mmvsor)
     d vlagl1_vare     s                   like(vlvare)
     d vvarl8_ogrp     s                   like(vvogrp)
     d vvarl8_hgrp     s                   like(vvhgrp)
     d vvarl8_ugrp     s                   like(vvugrp)
     d vvarlx_ogrp     s                   like(vvogrp)
     d vvarlx_hgrp     s                   like(vvhgrp)
     d vvarly_ogrp     s                   like(vvogrp)
     d vvarlr_vare     s                   like(vvvare)
     d vvarla_ldor     s                   like(vvldor)
5.63 d vsdtl1_vsor     s                   like(vdvsor)
6.20 d vsdtl1_lage     s                   like(vdlage)
6.20 d vsdtl1_ldor     s                   like(vdldor)
5.63 d vsdtl1_ogrp     s                   like(vdogrp)
5.63 d vsdtl1_hgrp     s                   like(vdhgrp)
5.63 d vsdtl1_ugrp     s                   like(vdugrp)
5.63 d vsdtl1_modn     s                   like(vdmodn)
5.63 d vsdtl1_vare     s                   like(vdvare)
     d vsdtl6_vsor     s                   like(vdvsor)
6.20 d vsdtl6_lage     s                   like(vdlage)
5.63 d vsdtl6_vare     s                   like(vdvare)
     d mprolu_vare     s                   like(movare)
     d mprolu_lage     s                   like(molage)
     d mpralu_vare     s                   like(mqvare)
     d mpralu_lage     s                   like(mqlage)
      *
     d w_dato          s               d   datfmt(*iso)
     d w_pdat          s               d   datfmt(*iso)
     d w_fdat          s               d   datfmt(*iso)
     d w_fdat_F        s               d   datfmt(*iso)
     d w_tdat          s               d   datfmt(*iso)
     d w_tdatt         s               d   datfmt(*iso)
     d w_ddat          s               d   datfmt(*iso)
     d w_paar          s              4  0
     d w_stdat         s              6  0
     d w_puke          s              2  0
     d w_psts          s              1
     d w_112           s             11  2
     d w_90            s              9  0
     d w_20            s              2  0
     d w_antd          s              9  0
     d w_antdw         s              9  0
     d w_faar          s              4  0
     d w_taar          s              4  0
     d w_fuke          s              2  0
     d w_tuke          s              2  0
     d w_uker          s             11  2
     d w_22            s              2  2
     d w_dagsb         s             11  3
     d w_dags_F        s             11  3
     d w_dags_S        s             11  3
     d w_dags_L        s             11  3
     d w_tot_dags      s             11  3
     d w_tot_dagsF     s             11  3
     d w_tot_dagsS     s             11  3
     d w_tot_dagsL     s             11  3
     d w_firm          s                   like(vvfirm)
     d w_vare          s                   like(vvvare)
     d w_lage          s                   like(vllage)
     d w_bmen          s                   like(mmdbax)
     d w_stid          s                   like(mmdsax)
     d w_ogrp          s                   like(mmogrp)
     d w_hgrp          s                   like(mmhgrp)
     d w_ugrp          s                   like(mmugrp)
6.12 d w_ldor          s                   like(mmldor)
     d p_list          s            128
     d p_listp         s            128
     d w_avvik         s              9  1
6.20 d w_udat          s               d   datfmt(*iso)
      *
     d                 ds
     d d_list                       128
     d p_vare                        15    overlay(d_list)
     d p_lage                         2  0 overlay(d_list:16)
     d p_fuke                         2  0 overlay(d_list:18)
     d p_faar                         4  0 overlay(d_list:20)
     d p_tuke                         2  0 overlay(d_list:24)
     d p_taar                         4  0 overlay(d_list:26)
     d p_ltid                         3  0 overlay(d_list:30)
     d p_helu                         3  0 overlay(d_list:33)
     d p_dage                         1  0 overlay(d_list:36)
     d p_tdtt                         6  0 overlay(d_list:37)
     d p_lr                           1    overlay(d_list:43)
     d p_brkg                         1  0 overlay(d_list:44)
     d p_dagsb                       11  3 overlay(d_list:45)
      *
     d                 ds
     d d_listp                      128
     d  d_pvsor                      10    overlay(d_listp)
     d  d_pogrp                       2  0 overlay(d_listp:11)
     d  d_phgrp                       2  0 overlay(d_listp:13)
     d  d_pugrp                       3  0 overlay(d_listp:15)
     d  d_pldor                       6  0 overlay(d_listp:18)
     d  d_pvare                       8  0 overlay(d_listp:24)
     d  d_crdo                        1  0 overlay(d_listp:32)
     d  d_etprm                       1  0 overlay(d_listp:33)
     d  d_dato                        6  0 overlay(d_listp:34)
      *
     d d_lagr          ds
     d  d_la01                        2  0
     d  d_la02                        2  0
     d  d_la03                        2  0
     d  d_la04                        2  0
     d  d_la05                        2  0
     d  d_la06                        2  0
     d  d_la07                        2  0
     d  d_la08                        2  0
     d  d_la09                        2  0
     d  d_la10                        2  0
5.54 d  d_la01c                       2    overlay(d_la01)
5.54 d  d_la02c                       2    overlay(d_la02)
      *
      *
     d b_ok            s              1    inz(*off)
     d b_matr          s              1    inz(*off)
5.63 d b_sort          s              1    inz(*off)
6.20 d b_inlr          s              1    inz(*off)
5.53  *
5.53 d w_kabc          s                   like(vvkabc)
5.53 d vlagl1_lage     s                   like(vllage)
      *
      * Local Data Area
      *
     d                uds
      *
6.10 d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
      *
      * Definering av variable
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   if        d_pvsor <> *blank or
     c                             d_pogrp <> 0 or
     c                             d_phgrp <> 0 or
     c                             d_pugrp <> 0 or
     c                             d_pldor <> 0 or
     c                             d_pvare <> 0
     c                   exsr      les_utvalg
     c                   else
      *
     c                   read      vvarl1
     c                   dow       not %eof
      *
     c                   exsr      beh_vare
      *
     c                   read      vvarl1
     c                   enddo
      *
     c                   exsr      upd_parm
      *
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     avslutt       tag
      * setter lr
     c                   eval      p_lr   = 'J'
     c                   eval      p_list = d_list
     c                   call      'MM735R  '
     c                   parm                    p_list
      *
     c                   eval      *inlr = *on
      *
     c                   return
      * --------------------------------------------------------------
      *  leser utvalg av varer
      * --------------------------------------------------------------
      *
     c     les_utvalg    begsr
      *
     c                   if        d_pvare <> 0
     c                   exsr      les_vare
     c                   goto      end_utvalg
     c                   endif
      *
     c                   if        d_pvsor <> *blank
     c                   exsr      les_vsor
     c                   goto      end_utvalg
     c                   endif
      *
     c                   if        d_pugrp <> 0
     c                   eval      w_ogrp = d_pogrp
     c                   eval      w_hgrp = d_phgrp
     c                   eval      w_ugrp = d_pugrp
     c                   exsr      les_ugr
     c                   goto      end_utvalg
     c                   endif
      *
     c                   if        d_phgrp <> 0
     c                   eval      w_ogrp = d_pogrp
     c                   eval      w_hgrp = d_phgrp
     c                   exsr      les_hgr
     c                   goto      end_utvalg
     c                   endif
      *
     c                   if        d_pogrp <> 0
     c                   eval      w_ogrp = d_pogrp
     c                   exsr      les_ogr
     c                   goto      end_utvalg
     c                   endif
      *
     c                   if        d_pldor <> 0
6.12 c                   eval      w_ldor = d_pldor
     c                   exsr      les_ldor
     c                   goto      end_utvalg
     c                   endif
      *
     c     end_utvalg    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser tom leverandør på vareregister kaller oppdatering av lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_vare      begsr
      *
     c                   movel     d_pvare       vvarlr_vare
     c     vvarlr_key    chain     vvarlr
     c                   if        %found(vvarlr)
     c                   exsr      beh_vare
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser varesortiment
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_vsor      begsr
      *
     c                   eval      vsdtl6_vsor = d_pvsor
6.20 c                   eval      vsdtl6_lage = 0
     c     vsdtl6_key    setll     vsdtl6
     c     vsdtl6_key    reade     vsdtl6
     c                   dow       not %eof(vsdtl6)
      *
     c                   select
     c                   when      vdvare <> *blank
     c                   exsr      beh_vare
     c                   eval      vvarlr_vare = vdvare
     c     vvarlr_key    chain     vvarlr
     c                   if        %found(vvarlr)
     c                   exsr      beh_vare
     c                   endif
      *
     c                   when      vdugrp <> 0
     c                   eval      w_ogrp = vdogrp
     c                   eval      w_hgrp = vdhgrp
     c                   eval      w_ugrp = vdugrp
     c                   exsr      les_ugr
     c                   when      vdhgrp <> 0
     c                   eval      w_ogrp = vdogrp
     c                   eval      w_hgrp = vdhgrp
     c                   exsr      les_hgr
     c                   when      vdogrp <> 0
     c                   eval      w_ogrp = vdogrp
     c                   exsr      les_ogr
6.12 c                   when      vdldor <> 0
6.12 c                   eval      w_ldor = vdldor
6.12 c                   exsr      les_ldor
     c                   endsl
      *
     c     vsdtl6_key    reade     vsdtl6
     c                   enddo
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser tom ugr på vareregister kaller behandling av vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_ugr       begsr
      *
     c                   eval      vvarl8_ogrp = w_ogrp
     c                   eval      vvarl8_hgrp = w_hgrp
     c                   eval      vvarl8_ugrp = w_ugrp
     c     vvarl8_key    setll     vvarl8
     c     vvarl8_key    reade     vvarl8
     c                   dow       not %eof(vvarl8)
     c                   exsr      beh_vare
     c     vvarl8_key    reade     vvarl8
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser tom hgr på vareregister kaller behandling av vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_hgr       begsr
      *
     c                   eval      vvarlx_ogrp = w_ogrp
     c                   eval      vvarlx_hgrp = w_hgrp
     c     vvarlx_key    setll     vvarl8
     c     vvarlx_key    reade     vvarl8
     c                   dow       not %eof(vvarl8)
     c                   exsr      beh_vare
     c     vvarlx_key    reade     vvarl8
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser tom ogr på vareregister kaller oppdatering av lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_ogr       begsr
      *
     c                   eval      vvarly_ogrp = w_ogrp
     c     vvarly_key    setll     vvarl8
     c     vvarly_key    reade     vvarl8
     c                   dow       not %eof(vvarl8)
     c                   exsr      beh_vare
     c     vvarly_key    reade     vvarl8
     c                   enddo
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser tom ugr på vareregister kaller behandling av vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_ldor      begsr
      *
6.12 c                   eval      vvarla_ldor = w_ldor
     c     vvarla_key    setll     vvarla
     c     vvarla_key    reade     vvarla
     c                   dow       not %eof(vvarla)
     c                   exsr      beh_vare
     c     vvarla_key    reade     vvarla
     c                   enddo
      *
     c                   endsr
      *
      *
      * --------------------------------------------------------------
      *  behandler lest vare
      * --------------------------------------------------------------
      *
     c     beh_vare      begsr
      *
     c                   exsr      rec_ktrl
     c                   if        b_ok   = *on
      *
     c                   if        d_etprm = 1
     c                   exsr      les_1_parm
     c                   else
     c                   exsr      les_parm
     c                   endif
     c                   if        mmlagr   <> 0
     c                   move      mmlagr        d_lagr
     c                   endif
      *
5.54  * Fikser hvis lager-array ikke er initiert
5.54 c                   if        d_la01c = *blanks
5.54 c                   eval      d_la01 = mmlage
5.54 c                   endif
5.54 c                   if        d_la02c = *blanks
5.54 c                   eval      d_la02 = 0
5.54 c                   endif
      *
     c                   if        b_ok   = *on and
     c                             b_matr = *on
      *
      * Sjekk om riktig leverandør - hvis leverandør er valgt
      *
     c                   if        mmldor <> *zero
     c                             and mmldor <> vvldor
     c                   leavesr
     c                   endif
      *
     c                   eval      w_tot_dagsF = 0
     c                   eval      w_tot_dagsS = 0
     c                   eval      w_tot_dagsL = 0
      *
      * behandler for hvert lager
      * - har med seg leveringstid fra lager, er med i beregning av
      *   bestillingspunkt
      *
     c                   eval      vlagl1_vare = vvvare
     c     vlagl1_key    setll     vlagl1
     c     vlagl1_key    reade     vlagl1
     c                   dow       not %eof(vlagl1)
      *
      * Sjekk først om mange lager er utfylt
      *
     c                   if        mmlagr   <> 0
     c                   if        vllage <> d_la01 and
     c                             vllage <> d_la02 and
     c                             vllage <> d_la03 and
     c                             vllage <> d_la04 and
     c                             vllage <> d_la05 and
     c                             vllage <> d_la06 and
     c                             vllage <> d_la07 and
     c                             vllage <> d_la08 and
     c                             vllage <> d_la09 and
     c                             vllage <> d_la10 and
     c                             vllage <> mmlage
     c                   goto      nxt_vlag
     c                   endif
     c                   else
     c                   if        mmlage  <> 0 and
     c                             vllage <> mmlage
     c                   goto      nxt_vlag
     c                   endif
     c                   endif
6.20  *   Hent utg.dato fra subprogram
6.20 c                   call      'VL720R'
6.20 c                   parm                    w_firm
6.20 c                   parm                    vvvare
6.20 c                   parm                    vllage
6.20 c                   parm                    w_udat
6.20 c                   parm                    b_inlr
6.20  *
6.20 c                   if        w_udat <> *loval and
6.20 c                             w_udat < w_ddat
6.20 c                   goto      nxt_vlag
6.20 c                   endif
5.61  * hvis lager har abckode - så velges denne
5.61 c                   if        vlkabc <> *blank
5.61 c                   eval      w_kabc = vlkabc
5.61 c                   endif
5.61 c                   if        w_kabc = *blank
5.61 c                   goto      nxt_vlag
5.61 c                   endif
      *
     c                   exsr      finn_uke_F
      *
      * hvis antall dager er 0, dvs. varen ikke har ant.best. pr. år
     c                   if        w_90 = 0
5.52 c                   exsr      dann_prog0
     c                   goto      nxt_vlag
     c                   endif
     c                   eval      w_dags_F = 0
     c                   eval      w_dags_S = 0
     c                   eval      w_dags_L = 0
      *
      * Finner dagsbehov for bestillingsmengde (forbrukstid)
      *
     c                   exsr      dags_F
      *
      * Finner dagsbehov for Sikkerhetstid
      *
     c                   if        w_stid <> 0
     c                   exsr      finn_uke_S
     c                   exsr      dags_S
     c                   endif
      *
      * Finner dagsbehov for Ledetid
      *
     c                   if        vlltid <> 0
     c                   exsr      finn_uke_L
     c                   exsr      dags_L
     c                   endif
      *
     c                   if        mmlagt = 0
      * hvis "lager som skal oppdateres" er 0, så oppdater her
     c                   eval      w_lage  = vllage
     c                   exsr      dann_prog
     c                   else
     c                   eval      w_tot_dagsF = w_tot_dagsF + w_dags_F
     c                   eval      w_tot_dagsS = w_tot_dagsS + w_dags_S
     c                   eval      w_tot_dagsL = w_tot_dagsL + w_dags_L
     c                   endif
      *
     c     nxt_vlag      tag
     c     vlagl1_key    reade     vlagl1
     c                   enddo
      *
     c                   if        mmlagt <> 0
      * hvis "lager som skal oppdateres" er et lager, så oppdater her
     c                   eval      w_dags_F = w_tot_dagsF
     c                   eval      w_dags_S = w_tot_dagsS
     c                   eval      w_dags_L = w_tot_dagsL
     c                   eval      w_lage  = mmlagt
6.10 c                   if        w_dags_F <> 0 or
6.10 c                             w_dags_S <> 0 or
6.10 c                             w_dags_L <> 0
     c                   exsr      dann_prog
6.10 c                   else
6.10 c                   exsr      dann_prog0
6.10 c                   endif
     c                   endif
      *
     c                   endif
     c                   endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for kontroll mot parameter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     rec_ktrl      begsr
      *
     c                   eval      b_ok = *off
      *
     c                   if        vvfirm <> l_firm
     c                   leavesr
     c                   endif
      *
5.70 c*                  if        vvvtyp <> 'L' and
5.70 c*                            vvvtyp <> 'S'
5.70 c*                  leavesr
5.70 c*                  endif
      * hvis utgått dato er passert skal den ikke med
6.20  * fjernet herfra, kontroll skjer pr. lager
6.20 c*                  if        vvudat <> *loval and
6.20 c*                            vvudat < w_ddat
6.20 c*                  leavesr
6.20 c*                  endif
      *
     c                   eval      w_kabc = vvkabc
      *
     c                   if        w_kabc = *blank
5.53  * Testen flyttet til beh_vare
5.53 c*                  leavesr
     c                   endif
      * varegruppe
     c                   if        d_pogrp <> 0 and
     c                             vvogrp <> d_pogrp
     c                   leavesr
     c                   endif
     c                   if        d_phgrp <> 0 and
     c                             vvhgrp <> d_phgrp
     c                   leavesr
     c                   endif
     c                   if        d_pugrp <> 0 and
     c                             vvugrp <> d_pugrp
     c                   leavesr
     c                   endif
      * leverandør
     c                   if        d_pldor <> 0 and
     c                             vvldor <> d_pldor
     c                   leavesr
     c                   endif
      *
      *
      * Sett OK-indikator - record skal være med
      *
     c                   eval      b_ok = *on
      *
     c     end_ktrl      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for les av parameter på forskjellige kombinasjoner
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_parm      begsr
      *
     c                   eval      b_matr = *off
      *  vare  - crossdocket
     c                   eval      mmatlr_vsor  = *blank
     c                   eval      mmatlr_ogrp  = 0
     c                   eval      mmatlr_hgrp  = 0
     c                   eval      mmatlr_ugrp  = 0
     c                   eval      mmatlr_ldor  = 0
     c                   eval      mmatlr_vare  = vvvare
     c                   eval      mmatlr_crdo  = d_crdo
     c     mmatlr_key    chain     mmatlr
     c                   if        %found and
     c                             mmakti = 0
     c                   eval      b_matr   = *on
     c                   leavesr
     c                   endif
      *  vare  -
     c                   eval      mmatlr_vsor  = *blank
     c                   eval      mmatlr_ogrp  = 0
     c                   eval      mmatlr_hgrp  = 0
     c                   eval      mmatlr_ugrp  = 0
     c                   eval      mmatlr_ldor  = 0
     c                   eval      mmatlr_vare  = vvvare
     c                   eval      mmatlr_crdo  = 0
     c     mmatlr_key    chain     mmatlr
     c                   if        %found and
     c                             mmakti = 0
     c                   eval      b_matr   = *on
     c                   leavesr
     c                   endif
5.63  *
5.63 c                   exsr      sjekk_sort
5.63 c                   if        b_sort = *on
5.63  *varesortiment - crossdocket
5.63 c                   eval      mmatlr_vsor  = vdvsor
5.63 c                   eval      mmatlr_ogrp  = 0
5.63 c                   eval      mmatlr_hgrp  = 0
5.63 c                   eval      mmatlr_ugrp  = 0
5.63 c                   eval      mmatlr_ldor  = 0
5.63 c                   eval      mmatlr_vare  = *blank
5.63 c                   eval      mmatlr_crdo  = 1
5.63 c     mmatlr_key    chain     mmatlr
5.63 c                   if        %found  and
5.63 c                             mmakti  = 0
5.63 c                   eval      b_matr  = *on
5.63 c                   leavesr
5.63 c                   endif
5.63  *varesortiment -
5.63 c                   eval      mmatlr_crdo  = 0
5.63 c     mmatlr_key    chain     mmatlr
5.63 c                   if        %found  and
5.63 c                             mmakti  = 0
5.63 c                   eval      b_matr  = *on
5.63 c                   leavesr
5.63 c                   endif
5.63  *
5.63 c                   endif
      *
      *  varegrupper m/leverandør ogr, hgr, ugr
     c                   eval      mmatlr_vsor  = *blank
     c                   eval      mmatlr_ogrp  = vvogrp
     c                   eval      mmatlr_hgrp  = vvhgrp
     c                   eval      mmatlr_ugrp  = vvugrp
     c                   eval      mmatlr_ldor  = vvldor
     c                   eval      mmatlr_vare  = *blank
     c     mmatlr_key    chain     mmatlr
     c                   if        %found  and
     c                             mmakti  = 0
     c                   eval      b_matr   = *on
     c                   leavesr
     c                   endif
5.62  * varegrupper  m /lev ogr, hgr
5.62 c                   eval      mmatlr_ugrp  = 0
5.62 c     mmatlr_key    chain     mmatlr
5.62 c                   if        %found  and
5.62 c                             mmakti  = 0
5.62 c                   eval      b_matr   = *on
5.62 c                   leavesr
5.62 c                   endif
5.62  * varegrupper  m /lev ogr
5.62 c                   eval      mmatlr_hgrp  = 0
5.62 c     mmatlr_key    chain     mmatlr
5.62 c                   if        %found  and
5.62 c                             mmakti  = 0
5.62 c                   eval      b_matr   = *on
5.62 c                   leavesr
5.62 c                   endif
      *
      * varegrupper
5.62 c                   eval      mmatlr_ogrp  = vvogrp
5.62 c                   eval      mmatlr_hgrp  = vvhgrp
5.62 c                   eval      mmatlr_ugrp  = vvugrp
5.62 c                   eval      mmatlr_ldor  = 0
5.62 c     mmatlr_key    chain     mmatlr
5.62 c                   if        %found  and
5.62 c                             mmakti  = 0
5.62 c                   eval      b_matr   = *on
5.62 c                   leavesr
5.62 c                   endif
5.62  *
5.62 c                   eval      mmatlr_ugrp  = 0
5.62 c     mmatlr_key    chain     mmatlr
5.62 c                   if        %found  and
5.62 c                             mmakti  = 0
5.62 c                   eval      b_matr  = *on
5.62 c                   leavesr
5.62 c                   endif
5.62  *
5.62 c                   eval      mmatlr_hgrp  = 0
5.62 c     mmatlr_key    chain     mmatlr
5.62 c                   if        %found  and
5.62 c                             mmakti  = 0
5.62 c                   eval      b_matr  = *on
5.62 c                   leavesr
5.62 c                   endif
5.62  *
5.62 c*                  eval      mmatlr_ogrp  = 0
5.62 c*    mmatlr_key    chain     mmatlr
5.62 c*                  if        %found  and
5.62 c*                            mmakti  = 0
5.62 c*                  eval      b_matr  = *on
5.62 c*                  leavesr
5.62 c*                  endif
      * bare leverandør
5.62 c                   eval      mmatlr_ogrp  = 0
5.62 c                   eval      mmatlr_hgrp  = 0
5.62 c                   eval      mmatlr_ugrp  = 0
     c                   eval      mmatlr_ldor  = vvldor
     c     mmatlr_key    chain     mmatlr
     c                   if        %found  and
     c                             mmakti  = 0
     c                   eval      b_matr  = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for les av parameter på forskjellige kombinasjoner
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_1_parm    begsr
      *
     c                   eval      b_matr = *off
     c                   eval      mmatlr_vsor  = d_pvsor
     c                   eval      mmatlr_ogrp  = d_pogrp
     c                   eval      mmatlr_hgrp  = d_phgrp
     c                   eval      mmatlr_ugrp  = d_pugrp
     c                   eval      mmatlr_ldor  = d_pldor
     c                   if        d_pvare <> 0
     c                   movel     d_pvare       mmatlr_vare
     c                   else
     c                   eval      mmatlr_vare  = *blank
     c                   endif
     c                   eval      mmatlr_crdo  = d_crdo
     c     mmatlr_key    chain     mmatlr
     c                   if        %found and
     c                             mmakti = 0
     c                   eval      b_matr   = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
5.52  *
5.52  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.52  *  Subrutine for å danne prognose for null bestillinger/år
5.52  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.52 c     dann_prog0    begsr
5.52 c                   eval      p_vare = vvvare
5.52 c                   clear                   mprolur
5.52 c                   eval      mprolu_vare = p_vare
5.52 c                   eval      mprolu_lage = w_lage
5.52 c     mprolu_key    chain     mprolu
5.52 c                   eval      movsor = d_pvsor
5.52 c                   eval      moogrp = d_pogrp
5.52 c                   eval      mohgrp = d_phgrp
5.52 c                   eval      mougrp = d_pugrp
5.52 c                   eval      moldor = d_pldor
5.52 c                   if        d_pvare <> 0
5.52 c                   movel     d_pvare       movark
5.52 c                   else
5.52 c                   eval      movark = *blank
5.52 c                   endif
5.52 c                   eval      mokabc = w_kabc
5.52 c     *dmy          move      UDATE         mokdat
5.52 c                   eval      mobmen = 0
5.52 c                   eval      mobpun = 0
5.52 c                   eval      momini = 0
5.52 c                   if        %found(mprolu)
6.10 c                   time                    moedat
6.10 c                   time                    moetim
6.10 c                   eval      moeusr = l_user
5.52 c                   update    mprolur
5.52 c                   else
5.52 c                   eval      mofirm = w_firm
5.52 c                   eval      movare = p_vare
5.52 c                   eval      molage = w_lage
6.10 c                   time                    moodat
6.10 c                   time                    mootim
6.10 c                   eval      moousr = l_user
6.10 c                   time                    moedat
6.10 c                   time                    moetim
6.10 c                   eval      moeusr = l_user
5.52 c                   write     mprolur
5.52 c                   endif
5.52  *
5.52 c                   endsr
5.52  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å danne prognose
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     dann_prog     begsr
     c                   clear                   mprolur
     c                   eval      mprolu_vare = p_vare
     c                   eval      mprolu_lage = w_lage
     c     mprolu_key    chain     mprolu
     c                   eval      movsor = d_pvsor
     c                   eval      moogrp = d_pogrp
     c                   eval      mohgrp = d_phgrp
     c                   eval      mougrp = d_pugrp
     c                   eval      moldor = d_pldor
     c                   if        d_pvare <> 0
     c                   movel     d_pvare       movark
     c                   else
     c                   eval      movark = *blank
     c                   endif
5.53 c                   eval      mokabc = w_kabc
     c     *dmy          move      UDATE         mokdat
     c                   if        w_dags_F >= 0 and
     c                             w_dags_S >= 0 and
     c                             w_dags_L >= 0
     c                   eval(h)   mobmen = w_bmen * w_dags_F
     c                   eval(h)   mobpun = w_stid * w_dags_S +
     c                                      vlltid * w_dags_L
     c                   eval(h)   momini = w_stid * w_dags_S
     c                   else
     c                   eval      mobmen = 0
     c                   eval      mobpun = 0
     c                   eval      momini = 0
     c                   endif
     c                   if        %found(mprolu)
6.10 c                   time                    moedat
6.10 c                   time                    moetim
6.10 c                   eval      moeusr = l_user
     c                   update    mprolur
     c                   else
     c                   eval      mofirm = w_firm
     c                   eval      movare = p_vare
     c                   eval      molage = w_lage
6.10 c                   time                    moodat
6.10 c                   time                    mootim
6.10 c                   eval      moousr = l_user
6.10 c                   time                    moedat
6.10 c                   time                    moetim
6.10 c                   eval      moeusr = l_user
     c                   write     mprolur
     c                   endif
      *
     c                   exsr      sjekk_avvik
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å behandle datoer og uker
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_uke_F    begsr
      *
      * finner fra uke
      *
     c     *dmy          move      w_stdat       w_dato
     c                   adddur    vlltid:*D     w_dato
      *
     c                   eval      w_pdat = w_dato
     c                   call      'AX711R  '
     c                   parm                    w_pdat
     c                   parm                    w_paar
     c                   parm                    w_puke
     c                   parm                    w_psts
      *
     c                   if        w_psts = *blank
     c                   eval      w_faar   = w_paar
     c                   eval      w_fuke   = w_puke
     c                   endif
      *
      * finne startdato i fra uke
      *
     c                   call      'AX710R  '
     c                   parm                    w_faar
     c                   parm                    w_fuke
     c                   parm                    w_fdat
     c                   parm                    w_tdat
     c                   parm                    w_psts
      * finner  antall dager
     c                   select
5.53 c                   when      w_kabc = 'AX'
     c                   if        mmdbax <> 0
     c                   eval      w_90    = 365 / mmdbax
     c                   else
     c                   eval      w_90 = 0
     c                   endif
     c                   eval      w_stid  = mmdsax
5.53 c                   when      w_kabc = 'AY'
     c                   if        mmdbay <> 0
     c                   eval      w_90    = 365 / mmdbay
     c                   else
     c                   eval      w_90 = 0
     c                   endif
     c                   eval      w_stid  = mmdsay
5.53 c                   when      w_kabc = 'AZ'
     c                   if        mmdbaz <> 0
     c                   eval      w_90    = 365 / mmdbaz
     c                   else
     c                   eval      w_90 = 0
     c                   endif
     c                   eval      w_stid  = mmdsaz
5.53 c                   when      w_kabc = 'BX'
     c                   if        mmdbbx <> 0
     c                   eval      w_90    = 365 / mmdbbx
     c                   else
     c                   eval      w_90 = 0
     c                   endif
     c                   eval      w_stid  = mmdsbx
5.53 c                   when      w_kabc = 'BY'
     c                   if        mmdbby <> 0
     c                   eval      w_90    = 365 / mmdbby
     c                   else
     c                   eval      w_90 = 0
     c                   endif
     c                   eval      w_stid  = mmdsby
5.53 c                   when      w_kabc = 'BZ'
     c                   if        mmdbbz <> 0
     c                   eval      w_90    = 365 / mmdbbz
     c                   else
     c                   eval      w_90 = 0
     c                   endif
     c                   eval      w_stid  = mmdsbz
5.53 c                   when      w_kabc = 'CX'
     c                   if        mmdbcx <> 0
     c                   eval      w_90    = 365 / mmdbcx
     c                   else
     c                   eval      w_90 = 0
     c                   endif
     c                   eval      w_stid  = mmdscx
5.53 c                   when      w_kabc = 'CY'
     c                   if        mmdbcy <> 0
     c                   eval      w_90    = 365 / mmdbcy
     c                   else
     c                   eval      w_90 = 0
     c                   endif
     c                   eval      w_stid  = mmdscy
5.53 c                   when      w_kabc = 'CZ'
     c                   if        mmdbcz <> 0
     c                   eval      w_90    = 365 / mmdbcz
     c                   else
     c                   eval      w_90 = 0
     c                   endif
     c                   eval      w_stid  = mmdscz
     c                   endsl
      *
      * hvis antall dager er 0
     c                   if        w_90 = 0
     c                   leavesr
     c                   endif
      *
      * finner til antall hele uker
      *
     c                   eval      w_112   = w_90 / 7
     c                   eval      p_helu  = w_112
     c                   eval      w_antd  = p_helu *7
      * finner til dato hele uker
      * (tar vare på denne datoen - brukes også i dagsbehov S)
     c                   eval      w_fdat_F = w_fdat
     c                   eval      w_dato   = w_fdat
     c                   adddur    w_antd:*D     w_dato
      * må trekke fra en dag..... ellers kommer man over i ny uke
     c                   subdur    1:*D          w_dato
      * finner til uke (hele uker)
     c                   eval      w_pdat  = w_dato
     c                   call      'AX711R  '
     c                   parm                    w_pdat
     c                   parm                    w_paar
     c                   parm                    w_puke
     c                   parm                    w_psts
      *
     c                   if        w_psts = *blank
     c                   eval      w_taar   = w_paar
     c                   eval      w_tuke   = w_puke
     c                   endif
      * finner til dato hele totalt
     c                   eval      w_dato = w_fdat
     c                   adddur    w_90:*D       w_dato
      * må trekke fra en dag.....
     c                   subdur    1:*D          w_dato
     c                   eval      w_tdatt = w_dato
      * finner overskytende dager over hel uke
     c                   move      w_112         w_22
     c                   select
     c                   when      w_22 < 0,05
     c                   eval      p_dage = 0
     c                   when      w_22 < 0,15
     c                   eval      p_dage = 1
     c                   when      w_22 > 0,15  and w_22 < 0,30
     c                   eval      p_dage = 2
     c                   when      w_22 > 0,30  and w_22 < 0,45
     c                   eval      p_dage = 3
     c                   when      w_22 > 0,45  and w_22 < 0,60
     c                   eval      p_dage = 4
     c                   when      w_22 > 0,60  and w_22 < 0,75
     c                   eval      p_dage = 5
     c                   when      w_22 > 0,75  and w_22 < 0,90
     c                   eval      p_dage = 6
     c                   when      w_22 > 0,90
     c                   eval      p_dage = 7
     c                   endsl
     c                   eval      w_bmen  = w_antd + p_dage
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne dagsbehov for å beregne bestillingsmengde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dags_F        begsr
     c                   eval      p_vare = vvvare
     c                   eval      p_lage = vllage
     c                   eval      p_fuke = w_fuke
     c                   eval      p_faar = w_faar
     c                   eval      p_tuke = w_tuke
     c                   eval      p_taar = w_taar
     c                   if        w_tdatt <> *loval
     c     *dmy          move      w_tdatt       p_tdtt
     c                   else
     c                   eval      p_tdtt = 0
     c                   endif
     c                   eval      p_brkg = mmbrkg
     c                   eval      p_lr   = *blank
     c                   eval      p_list = d_list
     c                   call      'MM735R  '
     c                   parm                    p_list
     c                   eval      d_list = p_list
     c                   eval      w_dags_F = p_dagsb
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å behandle datoer og uker - Sikkerhetstid
      *                  - brukes for å finne Minimumslager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_uke_S    begsr
      * Fra dato (dags.dato + ledetid) beregnet før (mandag i fra uke)
     c                   eval      w_dato   = w_fdat_F
      *
      * Beregner tildato - legger til sikkerhetstid
      * finner til dato  og  antall hele uker
      *
     c                   eval      w_112   = w_stid / 7
     c                   eval      p_helu  = w_112
     c                   eval      w_antd  = p_helu *7
     c                   adddur    w_antd:*D     w_dato
      * må trekke fra en dag..... ellers kommer man over i ny uke
     c                   subdur    1:*D          w_dato
      *
      * finner til uke (hele uker)
     c                   eval      w_pdat  = w_dato
     c                   call      'AX711R  '
     c                   parm                    w_pdat
     c                   parm                    w_paar
     c                   parm                    w_puke
     c                   parm                    w_psts
      *
     c                   if        w_psts = *blank
     c                   eval      w_taar   = w_paar
     c                   eval      w_tuke   = w_puke
     c                   else
     c                   eval      w_taar   = w_faar
     c                   eval      w_tuke   = w_fuke
     c                   endif
      * finner til dato hele totalt
     c                   eval      w_dato = w_fdat_F
     c                   adddur    w_stid:*D     w_dato
      * må trekke fra en dag.....
     c                   subdur    1:*D          w_dato
     c                   eval      w_tdatt = w_dato
      * finner overskytende dager over hel uke
     c                   move      w_112         w_22
     c                   select
     c                   when      w_22 < 0,05
     c                   eval      p_dage = 0
     c                   when      w_22 < 0,15
     c                   eval      p_dage = 1
     c                   when      w_22 > 0,15  and w_22 < 0,30
     c                   eval      p_dage = 2
     c                   when      w_22 > 0,30  and w_22 < 0,45
     c                   eval      p_dage = 3
     c                   when      w_22 > 0,45  and w_22 < 0,60
     c                   eval      p_dage = 4
     c                   when      w_22 > 0,60  and w_22 < 0,75
     c                   eval      p_dage = 5
     c                   when      w_22 > 0,75  and w_22 < 0,90
     c                   eval      p_dage = 6
     c                   when      w_22 > 0,90
     c                   eval      p_dage = 7
     c                   endsl
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne dagsbehov for å beregne bestillingsmengde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dags_S        begsr
     c                   eval      p_vare = vvvare
     c                   eval      p_lage = vllage
     c                   eval      p_fuke = w_fuke
     c                   eval      p_faar = w_faar
     c                   eval      p_tuke = w_tuke
     c                   eval      p_taar = w_taar
     c                   if        w_tdatt <> *loval
     c     *dmy          move      w_tdatt       p_tdtt
     c                   else
     c                   eval      p_tdtt = 0
     c                   endif
     c                   eval      p_brkg = mmbrkg
     c                   eval      p_lr   = *blank
     c                   eval      p_list = d_list
     c                   eval      p_list = d_list
     c                   call      'MM735R  '
     c                   parm                    p_list
     c                   eval      d_list = p_list
     c                   eval      w_dags_S = p_dagsb
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å behandle datoer og uker - Ledetid
      *                  - brukes for å finne Bestillingspunkt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_uke_L    begsr
      *
     c     *dmy          move      w_stdat       w_dato
      * finner fra uke
     c                   eval      w_pdat = w_dato
     c                   call      'AX711R  '
     c                   parm                    w_pdat
     c                   parm                    w_paar
     c                   parm                    w_puke
     c                   parm                    w_psts
      *
     c                   if        w_psts = *blank
     c                   eval      w_faar   = w_paar
     c                   eval      w_fuke   = w_puke
     c                   endif
      *
      * finne startdato i fra uke
      *
     c                   call      'AX710R  '
     c                   parm                    w_faar
     c                   parm                    w_fuke
     c                   parm                    w_fdat
     c                   parm                    w_tdat
     c                   parm                    w_psts
      *
     c                   eval      w_dato = w_fdat
      *
      * Beregner tildato - legger til leveringstid
      * finner til dato  og  antall hele uker
      *
     c                   eval      w_112   = vlltid / 7
     c                   eval      p_helu  = w_112
     c                   eval      w_antd  = p_helu *7
     c                   adddur    w_antd:*D     w_dato
      * må trekke fra en dag..... ellers kommer man over i ny uke
     c                   subdur    1:*D          w_dato
      *
      * finner til uke (hele uker)
     c                   eval      w_pdat  = w_dato
     c                   call      'AX711R  '
     c                   parm                    w_pdat
     c                   parm                    w_paar
     c                   parm                    w_puke
     c                   parm                    w_psts
      *
     c                   if        w_psts = *blank
     c                   eval      w_taar   = w_paar
     c                   eval      w_tuke   = w_puke
     c                   else
     c                   eval      w_taar   = w_faar
     c                   eval      w_tuke   = w_fuke
     c                   endif
      * finner til dato hele totalt
     c                   eval      w_dato = w_fdat
     c                   adddur    vlltid:*D     w_dato
      * må trekke fra en dag.....
     c                   subdur    1:*D          w_dato
     c                   eval      w_tdatt = w_dato
      * finner overskytende dager over hel uke
     c                   move      w_112         w_22
     c                   select
     c                   when      w_22 < 0,05
     c                   eval      p_dage = 0
     c                   when      w_22 < 0,15
     c                   eval      p_dage = 1
     c                   when      w_22 > 0,15  and w_22 < 0,30
     c                   eval      p_dage = 2
     c                   when      w_22 > 0,30  and w_22 < 0,45
     c                   eval      p_dage = 3
     c                   when      w_22 > 0,45  and w_22 < 0,60
     c                   eval      p_dage = 4
     c                   when      w_22 > 0,60  and w_22 < 0,75
     c                   eval      p_dage = 5
     c                   when      w_22 > 0,75  and w_22 < 0,90
     c                   eval      p_dage = 6
     c                   when      w_22 > 0,90
     c                   eval      p_dage = 7
     c                   endsl
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne dagsbehov for å beregne bestillingsmengde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dags_L        begsr
     c                   eval      p_vare = vvvare
     c                   eval      p_lage = vllage
     c                   eval      p_fuke = w_fuke
     c                   eval      p_faar = w_faar
     c                   eval      p_tuke = w_tuke
     c                   eval      p_taar = w_taar
     c                   if        w_tdatt <> *loval
     c     *dmy          move      w_tdatt       p_tdtt
     c                   else
     c                   eval      p_tdtt = 0
     c                   endif
     c                   eval      p_brkg = mmbrkg
     c                   eval      p_lr   = *blank
     c                   eval      p_list = d_list
     c                   eval      p_list = d_list
     c                   call      'MM735R  '
     c                   parm                    p_list
     c                   eval      d_list = p_list
     c                   eval      w_dags_L = p_dagsb
     c                   endsr
      * --------------------------------------------------------------
      *  sjekker om det er større avvik enn 100 %
      * --------------------------------------------------------------
      *
     c     sjekk_avvik   begsr
      *
      *
     c                   if        vlbmen <> 0 and
     c                             mobmen <> 0
     c                   eval(h)   w_avvik = ((mobmen - vlbmen)/mobmen)*100
     c                   if        w_avvik < 0
     c                   eval      w_avvik = w_avvik * -1
     c                   endif
     c                   if        w_avvik >  100,0
     c                   clear                   mpralur
     c                   eval      mpralu_vare = movare
     c                   eval      mpralu_lage = molage
     c     mpralu_key    chain     mpralu
     c                   eval      mqbmeg = vlbmen
     c                   eval      mqbmen = mobmen
     c                   if        %found(mpralu)
     c                   update    mpralur
     c                   else
     c                   eval      mqfirm = mofirm
     c                   eval      mqvare = movare
     c                   eval      mqlage = molage
     c                   write     mpralur
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c                   endsr
      *
      * --------------------------------------------------------------
      *  Oppdaterer parameter for sist kjørt
      * --------------------------------------------------------------
      *
     c     upd_parm      begsr
      *
     c     mmatlu_key    setll     mmatlu
     c     mmatlu_key    reade     mmatlu
      *
     c                   dow       not %eof(mmatlu)
     c                   if        mmakti = 0
     c                   time                    mmkdat
6.10 c                   time                    mmedat
6.10 c                   time                    mmetim
6.10 c                   eval      mmeusr = l_user
     c                   update    mmatlur
     c                   endif
     c     mmatlu_key    reade     mmatlu
     c                   enddo
      *
     c                   endsr
5.63  *
5.63  * --------------------------------------------------------------
5.63  *  Sjekker om vare inngår i sortiment på alle sortiment
5.63  *  som finnes på prognose parameter
5.63  * --------------------------------------------------------------
5.63  *
5.63 c     sjekk_sort    begsr
5.63  *
5.63 c                   eval      b_sort = *off
5.63  *
5.63 c     mmatl2_key    reade     mmatl2
5.63 c                   dow       not %eof(mmatl2)
5.63  *
5.63 c                   if        mmvsor <> *blank
5.63 c                   eval      vsdtl6_vsor = mmvsor
6.20 c                   eval      vsdtl6_lage = 0
5.63 c                   eval      vsdtl1_vsor = mmvsor
6.20 c                   eval      vsdtl1_lage = 0
5.63  *
5.63 c                   eval      vsdtl6_vare = vvvare
5.63 c     vsdtl6_key2   chain     vsdtl6
5.63 c                   if        %found
5.63 c                   eval      b_sort = *on
5.63 c                   leavesr
5.63 c                   endif
5.63  *
6.20 c                   eval      vsdtl1_lage = 0
5.63 c                   eval      vsdtl1_ldor = vvldor
5.63 c                   eval      vsdtl1_ogrp = vvogrp
5.63 c                   eval      vsdtl1_hgrp = vvhgrp
5.63 c                   eval      vsdtl1_ugrp = vvugrp
5.63 c                   eval      vsdtl1_modn = vvnmod
5.63 c                   eval      vsdtl1_vare = *blank
5.63 c     vsdtl1_key    chain     vsdtl1
5.63 c                   if        not %found
5.63 c                   eval      vsdtl1_modn = 0
5.63 c     vsdtl1_key    chain     vsdtl1
5.63 c                   if        not %found
5.63 c                   eval      vsdtl1_ugrp = 0
5.63 c     vsdtl1_key    chain     vsdtl1
5.63 c                   if        not %found
5.63 c                   eval      vsdtl1_hgrp = 0
5.63 c     vsdtl1_key    chain     vsdtl1
5.63 c                   if        not %found
5.63 c                   eval      vsdtl1_ogrp = 0
5.63 c     vsdtl1_key    chain     vsdtl1
5.63 c                   endif
5.63 c                   endif
5.63 c                   endif
5.63 c                   endif
5.63  *
5.63 c                   if        not %found and
5.63 c                             vvldor <> 0
5.63 c                   eval      vsdtl1_ldor = 0
5.63 c                   eval      vsdtl1_ogrp = vvogrp
5.63 c                   eval      vsdtl1_hgrp = vvhgrp
5.63 c                   eval      vsdtl1_ugrp = vvugrp
5.63 c                   eval      vsdtl1_modn = vvnmod
5.63 c                   eval      vsdtl1_vare = *blank
5.63 c     vsdtl1_key    chain     vsdtl1
5.63 c                   if        not %found
5.63 c                   eval      vsdtl1_modn = 0
5.63 c     vsdtl1_key    chain     vsdtl1
5.63 c                   if        not %found
5.63 c                   eval      vsdtl1_ugrp = 0
5.63 c     vsdtl1_key    chain     vsdtl1
5.63 c                   if        not %found
5.63 c                   eval      vsdtl1_hgrp = 0
5.63 c     vsdtl1_key    chain     vsdtl1
5.63 c                   endif
5.63 c                   endif
5.63 c                   endif
5.63 c                   endif
5.63  *
5.63 c                   if        %found
5.63 c                   eval      b_sort = *on
5.63 c                   leavesr
5.63 c                   endif
5.63  *
5.63 c                   endif
5.63  *
5.63 c     mmatl2_key    reade     mmatl2
5.63 c                   enddo
5.63*
5.63 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av vareregister
      *
     c     vvarlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlr_vare
      *
      *  Key for les av varesortiment
      *
     c     vsdtl6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vsdtl6_vsor
6.20 c                   kfld                    vsdtl6_lage
5.63  *
5.63  * Key for les/oppslag på varesortiment-linje
5.63  *
5.63 c     vsdtl1_key    klist
5.63 c                   kfld                    w_firm
5.63 c                   kfld                    vsdtl1_vsor
6.20 c                   kfld                    vsdtl1_lage
5.63 c                   kfld                    vsdtl1_ldor
5.63 c                   kfld                    vsdtl1_ogrp
5.63 c                   kfld                    vsdtl1_hgrp
5.63 c                   kfld                    vsdtl1_ugrp
5.63 c                   kfld                    vsdtl1_modn
5.63 c                   kfld                    vsdtl1_vare
5.63  *
5.63  *  Key for les av varesortiment
5.63  *
5.63 c     vsdtl6_key2   klist
5.63 c                   kfld                    w_firm
5.63 c                   kfld                    vsdtl6_vsor
6.20 c                   kfld                    vsdtl6_lage
5.63 c                   kfld                    vsdtl6_vare
      *
      *  Key for les av vareregister
      *
     c     vvarl8_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl8_ogrp
     c                   kfld                    vvarl8_hgrp
     c                   kfld                    vvarl8_ugrp
      *
      *  Key for les av vareregister
      *
     c     vvarlx_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlx_ogrp
     c                   kfld                    vvarlx_hgrp
      *
      *  Key for les av vareregister
      *
     c     vvarly_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarly_ogrp
      *
      *  Key for les av vareregister  pr. lev.
      *
     c     vvarla_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarla_ldor
      *
5.63  *
5.63  * Key for les av prognose matrise
5.63  *
5.63 c     mmatl2_key    klist
5.63 c                   kfld                    w_firm
      *
      * Key for les av prognose matrise
      *
     c     mmatlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mmatlr_vsor
     c                   kfld                    mmatlr_ogrp
     c                   kfld                    mmatlr_hgrp
     c                   kfld                    mmatlr_ugrp
     c                   kfld                    mmatlr_ldor
     c                   kfld                    mmatlr_vare
     c                   kfld                    mmatlr_crdo
      *
      * Key for oppdater av parameter
      *
     c     mmatlu_key    klist
     c                   kfld                    w_firm
      *
      * Key for les av lager
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
5.53  *
5.53 c     vlagl1_key2   klist
5.53 c                   kfld                    w_firm
5.53 c                   kfld                    vlagl1_vare
5.53 c                   kfld                    vlagl1_lage
      *
     c     mprolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mprolu_vare
     c                   kfld                    mprolu_lage
      *
      * Key for oppdater av stort avvik for best.mengde
      *
     c     mpralu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mpralu_vare
     c                   kfld                    mpralu_lage
      *
     c     *entry        plist
     c                   parm                    p_listp
      *
     c                   eval      d_listp = p_listp
      *
     c                   if        d_dato <> 0
     c                   eval      w_stdat = d_dato
     c                   else
     c                   move      UDATE         w_stdat
     c                   endif
     c                   eval      w_firm = l_firm
     c                   time                    w_ddat
      *
     c                   endsr
