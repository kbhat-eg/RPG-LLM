     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VV601R
      * Beskrivelse . . : Vare, utskrift - utplukk
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       280999 HKO  Nytt program
      *       161103 HKO  Fjernet behandling av rabattgruppe
      *       180204 HKO  Lagt inn valg av minimun dekningsgrad
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvawpf    o    e             disk
      *
      * Definering av datastruktur
      *
      *  parameterliste - inn
      *
     d                 ds
     d d_list                        80
     d  d_firm                        3  0 overlay(d_list:1)
     d  d_fvgr                        7  0 overlay(d_list:4)
     d   d_fogr                       2  0 overlay(d_list:4)
     d   d_fhgr                       2  0 overlay(d_list:6)
     d   d_fugr                       3  0 overlay(d_list:8)
     d  d_tvgr                        7  0 overlay(d_list:11)
     d   d_togr                       2  0 overlay(d_list:11)
     d   d_thgr                       2  0 overlay(d_list:13)
     d   d_tugr                       3  0 overlay(d_list:15)
     d  d_flev                        6  0 overlay(d_list:18)
     d  d_tlev                        6  0 overlay(d_list:24)
     d  d_fvar                       15    overlay(d_list:30)
     d  d_tvar                       15    overlay(d_list:45)
     d  d_dekn                        4  2 overlay(d_list:60)
     d  d_utsk                        1  0 overlay(d_list:64)
     d  d_srt1                        1  0 overlay(d_list:65)
     d  d_srt2                        1  0 overlay(d_list:66)
     d  d_srt3                        1  0 overlay(d_list:67)
     d  d_srt4                        1  0 overlay(d_list:68)
      *
      *  varegruppe
      *
     d                 ds
     d d_vgrp                         7  0
     d  d_ogrp                        2  0 overlay(d_vgrp)
     d  d_hgrp                        2  0 overlay(d_vgrp:3)
     d  d_ugrp                        3  0 overlay(d_vgrp:5)
      *
      *  sorteringsnøkkel
      *
     d                 ds
     d d_vakey                       64
     d  d_vsrt1                      21    overlay(d_vakey)
     d  d_vsrt2                      21    overlay(d_vakey:22)
     d  d_vsrt3                      21    overlay(d_vakey:43)
      *
      * Definering av parametre
      *
     d p_list          s             80
      *
      * Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vvfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      vvarl1_vare = d_fvar
     c     vvarl1_key    setll     vvarl1
     c                   read      vvarl1                                 90
     c                   dow       *in90 = *off
      *
      * Sjekk firma
      *
     c                   if        vvfirm <> d_firm
     c                   leave
     c                   endif
      *
      * Sjekk varenummer fra - til
      *
     c                   if        d_tvar <> *blank and
     c                             vvvare > d_tvar
     c                   leave
     c                   endif
      *
      * Sjekk varegruppe fra - til
      *
     c                   if        d_fvgr <> 0 or
     c                             d_tvgr <> 0
     c                   eval      d_ogrp = vvogrp
     c                   eval      d_hgrp = vvhgrp
     c                   eval      d_ugrp = vvugrp
     c                   if        d_vgrp < d_fvgr or
     c                             d_vgrp > d_tvgr
     c                   goto      les_neste
     c                   endif
     c                   endif
      *
      * Sjekk leverandør fra - til
      *
     c                   if        d_flev <> 0 or
     c                             d_tlev <> 0
     c                   if        vvldor < d_flev or
     c                             vvldor > d_tlev
     c                   goto      les_neste
     c                   endif
     c                   endif
      *
      * Bygg opp sorterings-nøkkel
      *
     c                   eval      d_ogrp = vvogrp
     c                   eval      d_hgrp = vvhgrp
     c                   eval      d_ugrp = vvugrp
      *
     c                   eval      d_vsrt1 = *blank
     c                   eval      d_vsrt2 = *blank
     c                   eval      d_vsrt3 = *blank
      *
     c                   if        d_srt1 = 1
     c                   movel     d_vgrp        d_vsrt1
     c                   endif
      *
     c                   if        d_srt1 = 2
     c                   movel     d_vgrp        d_vsrt2
     c                   endif
      *
     c                   if        d_srt1 = 3
     c                   movel     d_vgrp        d_vsrt3
     c                   endif
      *
     c                   if        d_srt2 = 1
     c                   movel     vvldor        d_vsrt1
     c                   endif
      *
     c                   if        d_srt2 = 2
     c                   movel     vvldor        d_vsrt2
     c                   endif
      *
     c                   if        d_srt2 = 3
     c                   movel     vvldor        d_vsrt3
     c                   endif
      *
     c                   if        d_srt3 = 1
     c                   movel     vvtek1        d_vsrt1
     c                   endif
      *
     c                   if        d_srt3 = 2
     c                   movel     vvtek1        d_vsrt2
     c                   endif
      *
     c                   if        d_srt3 = 3
     c                   movel     vvtek1        d_vsrt3
     c                   endif
      *
     c                   if        d_srt4 = 1
     c                   movel     vvvare        d_vsrt1
     c                   endif
      *
     c                   if        d_srt4 = 2
     c                   movel     vvvare        d_vsrt2
     c                   endif
      *
     c                   if        d_srt4 = 3
     c                   movel     vvvare        d_vsrt3
     c                   endif
      *
     c                   eval      vwakey = d_vakey
      *
      * Skriver til arbeidsfil
      *
     c                   write     vvawpfr
      *
     c     les_neste     tag
      *
     c                   read      vvarl1                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for les på vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *
      * Kopierer parameterliste til datastruktur
      *
     c                   eval      d_list = p_list
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = d_firm
      *
     c                   endsr
