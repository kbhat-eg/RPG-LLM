     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RH501R
      * Beskrivelse . . : Kontoplan, spørring
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       070402 KOB  Nytt program
T5.40 * V5.40 040805 HFL  Utvidet parameterliste for RF020R
6.20  *       021210 sst  Endret prosjekt til alfanum m.m.
7.xx  * 7.xx  040516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frhovl1    if   e           k disk    rename(rhovpfr:rhovl1r)
     frhovl2    if   e           k disk    rename(rhovpfr:rhovl2r)
     frhovl3    if   e           k disk    rename(rhovpfr:rhovl3r)
     frhovl4    if   e           k disk    rename(rhovpfr:rhovl4r)
     frhovlr    if   e           k disk    rename(rhovpfr:rhovlrr)
T5.40frklal2    if   e           k disk    rename(rklapfr:rklal2r)
     frh501d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
     d p_kont          s                   like(rhkont)
     d p_avde          s                   like(rhavde)
     d p_spes          s                   like(rhspes)
     d p_text          s                   like(rhtext)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d rhovl1_kont     s                   like(rhkont)
     d rhovl1_spes     s                   like(rhspes)
     d rhovl1_avde     s                   like(rhavde)
     d rhovl2_alfa     s                   like(rhalfa)
     d rhovl2_spes     s                   like(rhspes)
     d rhovl2_avde     s                   like(rhavde)
     d rhovl3_kont     s                   like(rhkont)
     d rhovl3_spes     s                   like(rhspes)
     d rhovl3_avde     s                   like(rhavde)
     d rhovl4_kont     s                   like(rhkont)
     d rhovl4_spes     s                   like(rhspes)
     d rhovl4_avde     s                   like(rhavde)
     d rhovlr_kont     s                   like(rhkont)
     d rhovlr_spes     s                   like(rhspes)
     d rhovlr_avde     s                   like(rhavde)
      *
T5.40d rklal2_syst     s                   like(rxsyst)
T5.40d rklal2_pros     s                   like(rxpros)
T5.40d rklal2_akti     s                   like(rxakti)
T5.40d rklal2_kont     s                   like(rxkont)
T5.40d rklal2_spes     s                   like(rxspes)
T5.40d rklal2_avde     s                   like(rxavde)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_eof           s              1    inz(*off)
     d b_valg_ok       s              1    inz(*off)
      *
     d w_firm          s                   like(rhfirm)
     d w_kont_gml      s                   like(rhkont)
     d w_spes_gml      s                   like(rhspes)
     d w_avde_gml      s                   like(rhavde)
      *
     d p_stat          s              1
     d p_ekod          s                   like(rhmvak)
     d p_mkod          s                   like(rhbnØk)
     d p_okod          s                   like(rhmngd)
     d p_pkod          s                   like(rhvalk)
     d p_tx20          s             20
     d p_tx30          s             30
      *
     d wpkont          s                   like(rhkont)
     d wpspes          s                   like(rhspes)
     d wpavde          s                   like(rhavde)
     d wpraar          s              4  0
     d wprper          s              2  0
     d wpsude          s              1
      *
      * Parameter - program RF020R
6.20 i/COPY QCPYLESRC,RF020PAR
6.20 d*                ds
6.20 d*rxulda                       175
6.20 d* rxstat                        1    overlay(rxulda)
6.20 d* rxfirm                        3  0 overlay(rxulda:2)
6.20 d* rxsyst                        1    overlay(rxulda:5)
6.20 d* rxpros                        6  0 overlay(rxulda:6)
6.20 d* rxakti                        3    overlay(rxulda:12)
6.20 d* rxf001                        3    overlay(rxulda:15)
6.20 d* rxkont                        6  0 overlay(rxulda:18)
6.20 d* rxspes                        4  0 overlay(rxulda:24)
6.20 d* rxavde                        4  0 overlay(rxulda:28)
6.20 d* rxlinj                        4  0 overlay(rxulda:32)
6.20 d* rxtgrp                        3    overlay(rxulda:36)
6.20 d* rxtext                       78    overlay(rxulda:39)
6.20 d* rxf002                       13    overlay(rxulda:117)
6.20 d* rxvalg                        1  0 overlay(rxulda:130)
6.20 d* rxtist                         z   overlay(rxulda:131)
6.20 d* rxbiln                        6  0 overlay(rxulda:158)
6.20 d* rxrper                        2  0 overlay(rxulda:164)
6.20 d* rxraar                        4  0 overlay(rxulda:166)
6.20 d* rxonr                         6  0 overlay(rxulda:170)
     d rxupar          s                   like(rxulda)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(12)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Vis den side som inneholder denne record.
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2kont <> 0 or
     c                             b2spes <> 0 or
     c                             b2avde <> 0 or
     c                             b2alfa <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   if        b_valg_ok = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'K'
     c     rhovl1_key    setll     rhovl1
     c                   when      w_seqe = 'A'
     c     rhovl3_key    setll     rhovl3
     c                   when      w_seqe = 'B'
     c     rhovl4_key    setll     rhovl4
     c                   when      w_seqe = 'N'
     c     rhovl2_key    setll     rhovl2
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på konto
      *
     c                   if        b2kont <> 0
     c                   eval      w_seqe = 'K'
     c                   eval      rhovl1_kont = b2kont
     c                   eval      rhovl1_spes = b2spes
     c                   eval      rhovl1_avde = b2avde
     c     rhovl1_key    setll     rhovl1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på avdeling
      *
     c                   if        b2avde <> 0
     c                   eval      w_seqe = 'A'
     c                   eval      rhovl3_kont = *zeros
     c                   eval      rhovl3_spes = *zeros
     c                   eval      rhovl3_avde = b2avde
     c     rhovl3_key    setll     rhovl3
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på kostnadsbærer
      *
     c                   if        b2spes <> 0
     c                   eval      w_seqe = 'B'
     c                   eval      rhovl4_kont = *zeros
     c                   eval      rhovl4_spes = b2spes
     c                   eval      rhovl4_avde = *zeros
     c     rhovl4_key    setll     rhovl4
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på alfabetisk nøkkel
      *
     c                   if        b2alfa <> *blank
     c                   eval      w_seqe = 'N'
     c                   eval      rhovl2_alfa = b2alfa
     c     rhovl2_key    setll     rhovl2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2kont = 0
     c                   eval      b2spes = 0
     c                   eval      b2avde = 0
     c                   eval      b2alfa = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vise post
      *
     c                   if        b1valg = 5
     c                   eval      c1kont = b1kont
     c                   eval      c1spes = b1spes
     c                   eval      c1avde = b1avde
     c                   exsr      xc1bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *                                                                 .
      * Vise posteringer
      *                                                                 .
     c                   if        b1valg = 8
     c                   move      *year         wpraar
     c                   move      *month        wprper
     c                   move      b1kont        wpkont
     c                   move      b1spes        wpspes
     c                   move      b1avde        wpavde
     c                   call      'RH510R'
     c                   parm                    wpsude
     c                   parm                    wpraar
     c                   parm                    wprper
     c                   parm                    wpkont
     c                   parm                    wpspes
     c                   parm                    wpavde
      *                                                                 .
     c                   eval      b1valg = 0
     c                   update    b1sfl
      *                                                                 .
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for vise (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      rhovlr_kont = c1kont
     c                   eval      rhovlr_spes = c1spes
     c                   eval      rhovlr_avde = c1avde
     c     rhovlr_key    chain     rhovlr                             90
      *
     c                   if        *in90 = *off
     c                   eval      c1text = rhtext
     c                   eval      c1txt2 = rhtxt2
     c                   eval      c1alfa = rhalfa
     c                   eval      c1mvak = rhmvak
     c                   eval      c1mvao = rhmvao
     c                   eval      c1mvag = rhmvag
     c                   eval      c1bnØk = rhbnØk
     c                   eval      c1valk = rhvalk
     c                   eval      c1mngd = rhmngd
     c                   eval      c1proj = rhproj
     c                   eval      c1bÆrk = rhbÆrk
     c                   eval      c1avdk = rhavdk
     c                   eval      c1nØkk = rhnØkk
     c                   eval      c1sprk = rhsprk
     c                   eval      c1eusr = rheusr
     c     *dmy          move      rhedat        c1edat
     c                   exsr      kontroll
     c                   endif
      *
     c     c1tagb        tag
      *
      *
T5.40 * Finnes notatark på kontoen?
      *
T5.40c                   eval      rklal2_syst = 'H'
T5.40c                   eval      rklal2_pros = *zeros
T5.40c                   eval      rklal2_akti = *blanks
T5.40c                   eval      rklal2_kont = c1kont
T5.40c                   eval      rklal2_spes = c1spes
T5.40c                   eval      rklal2_avde = c1avde
T5.40c     rklal2_key    setll     rklal2                                 94
      *
T5.40c                   if        *in94 = *on
T5.40c                   eval      C2notb = 'NOTATARK'
T5.40c                   endif
      *
     c                   exfmt     c1bld
      *
     c                   if        *inkc or *inkl
     c                   goto      end_c1bld
     c                   endif
      *
      * Oppkall av notatark.
      *
     c                   if        *inkb = *on
     c                   eval      rxfirm = w_firm
     c                   eval      rxsyst = 'H'
E5.40c                   eval      rxpros = *zeros
     c                   eval      rxakti = *blank
E5.40c                   eval      rxkont = b1kont
E5.40c                   eval      rxspes = b1spes
E5.40c                   eval      rxavde = b1avde
     c                   eval      rxlinj = *zeros
     c                   eval      rxtext = *blank
6.20 c                   move      b1valg        rxvalg
T5.40c                   move      *loval        rxtist
T5.40c                   eval      rxbiln = *zero
T5.40c                   eval      rxrper = *zero
T5.40c                   eval      rxraar = *zero
T5.40c                   eval      rxonr  = *zero
     c                   move      rxulda        rxupar
     c                   call      'RF020R'
     c                   parm                    rxupar
     c                   move      rxupar        rxulda
     c                   goto      c1tagb
     c                   endif
      *
     c     end_c1bld     tag
      *
     c                   endsr
      *
     c/eject
      *
      * Subrutine for kontroll av inputfelt
      *
     c     kontroll      begsr
      *
      * Sjekke om mva-kode er gyldig
      *
     c                   move      c1mvak        p_ekod
     c                   call      'RS705R'
     c                   parm                    p_stat
     c                   parm                    p_ekod
     c                   parm                    p_tx30
      *
     c                   eval      c1etxt = p_tx30
      *
      * Sjekke om budsjettnøkkel
      * er gyldig - default = 99
      *
     c                   if        c1bnØk = *zero
     c                   move      99            c1bnØk
     c                   endif
      *
     c                   if        c1bnØk = 99
     c                   eval      c1mtxt = 'Jevn fordeling'
     c                   else
     c                   move      c1bnØk        p_mkod
     c                   call      'RS713R'
     c                   parm                    p_stat
     c                   parm                    p_mkod
     c                   parm                    p_tx30
      *
     c                   eval      c1mtxt = p_tx30
      *
     c                   endif
      *
      * Sjekke om mengde er gyldig
      *
     c                   if        c1mngd <> *zero
     c                   move      c1mngd        p_okod
     c                   call      'RS715R'
     c                   parm                    p_stat
     c                   parm                    p_okod
     c                   parm                    p_tx30
      *
     c                   eval      c1otxt = p_tx30
     c                   endif
      *
      * Sjekke om valuta er gyldig
      *
     c                   if        c1valk <> *blank
     c                   move      c1valk        p_pkod
     c                   call      'RS716R'
     c                   parm                    p_stat
     c                   parm                    p_pkod
     c                   parm                    p_tx20
      *
     c                   eval      c1pmna =p_tx20
      *
     c                   endif
      *
     c     kont_end      endsr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'K'
     c                   read      rhovl1                                 15
     c                   when      w_seqe = 'A'
     c                   read      rhovl3                                 15
     c                   when      w_seqe = 'B'
     c                   read      rhovl4                                 15
     c                   when      w_seqe = 'N'
     c                   read      rhovl2                                 15
     c                   endsl
      *
     c                   if        %eof or
     c                             rhfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1kont = rhkont
     c                   eval      b1spes = rhspes
     c                   eval      b1avde = rhavde
     c                   eval      b1text = rhtext
     c                   eval      b1sprk = rhsprk
     c                   eval      b1mvak = rhmvak
     c                   eval      b1mvao = rhmvao
     c                   eval      b1mvag = rhmvag
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   select
     c                   when      w_seqe = 'K'
     c     rhovl1_key    setll     rhovl1
     c                   when      w_seqe = 'A'
     c     rhovl3_key    setll     rhovl3
     c                   when      w_seqe = 'B'
     c     rhovl4_key    setll     rhovl4
     c                   when      w_seqe = 'N'
     c     rhovl2_key    setll     rhovl2
     c                   endsl
      *
      * Leser tilbake en side + posisjoneringsrad
      *
     c                   eval      b_eof  = *off
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = c_sfil + 1
      *
     c                   select
     c                   when      w_seqe = 'K'
     c                   readp     rhovl1                                 15
     c                   when      w_seqe = 'A'
     c                   readp     rhovl3                                 15
     c                   when      w_seqe = 'B'
     c                   readp     rhovl4                                 15
     c                   when      w_seqe = 'N'
     c                   readp     rhovl2                                 15
     c                   endsl
      *
     c                   if        %eof or
     c                             rhfirm <> w_firm
     c                   eval      b_eof = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   if        w_stel <= c_sfil
     c                   select
     c                   when      w_seqe = 'K'
     c                   eval      rhovl1_kont = rhkont
     c                   when      w_seqe = 'A'
     c                   eval      rhovl3_avde = rhavde
     c                   when      w_seqe = 'B'
     c                   eval      rhovl4_spes = rhspes
     c                   when      w_seqe = 'N'
     c                   eval      rhovl2_alfa = rhalfa
     c                   endsl
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_eof = *on
     c                   select
     c                   when      w_seqe = 'K'
     c     rhovl1_key    setll     rhovl1
     c                   when      w_seqe = 'A'
     c     rhovl3_key    setll     rhovl3
     c                   when      w_seqe = 'B'
     c     rhovl4_key    setll     rhovl4
     c                   when      w_seqe = 'N'
     c     rhovl2_key    setll     rhovl2
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på konto
      *
     c     rhovl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovl1_kont
     c                   kfld                    rhovl1_spes
     c                   kfld                    rhovl1_avde
      *
      * Key for posisjonering på avdeling
      *
     c     rhovl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovl3_avde
     c                   kfld                    rhovl3_kont
     c                   kfld                    rhovl3_spes
      *
      * Key for posisjonering på kosnadsbærer
      *
     c     rhovl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovl4_spes
     c                   kfld                    rhovl4_kont
     c                   kfld                    rhovl4_avde
      *
      * Key for posisjonering på beskrivelse
      *
     c     rhovl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovl2_alfa
      *
      * Key for les på konto
      *
     c     rhovlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovlr_kont
     c                   kfld                    rhovlr_spes
     c                   kfld                    rhovlr_avde
      *
T5.40 * Key for notatark
      *
T5.40c     rklal2_key    klist
T5.40c                   kfld                    w_firm
T5.40c                   kfld                    rklal2_syst
T5.40c                   kfld                    rklal2_pros
T5.40c                   kfld                    rklal2_akti
T5.40c                   kfld                    rklal2_kont
T5.40c                   kfld                    rklal2_spes
T5.40c                   kfld                    rklal2_avde
      *
      * Henter firmanummer fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      w_seqe = 'K'
     c                   eval      rhovl1_kont = 0
     c                   eval      rhovl1_spes = 0
     c                   eval      rhovl1_avde = 0
     c     rhovl1_key    setll     rhovl1
     c                   exsr      crt_subfile
      *
     c                   endsr
