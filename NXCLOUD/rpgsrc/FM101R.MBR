     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FM101R
      * Beskrivelse . . : Kort/fakt.avtaler, vedlikehold (param.)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       041105 GRO  Nytt program
7.01  *       220120 bsa  Håndtere lange kortnummer. Droppes hvis for langt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffmkal1    if   e           k disk    rename(fmkapfr:fmkal1r)
     ffmkalu    uf a e           k disk    rename(fmkapfr:fmkalur)
     frukrl1    if   e           k disk    rename(rukrpfr:rukrl1r)
      *
      * Definering av parametre
      *
     d p_ofir          s              3  0
     d p_okun          s              6  0
     d p_opro          s              6  0
     d p_okpn          s              6  0
     d p_otyp          s              2  0
     d p_otsq          s              4  0
     d p_oper          s              1
      *
      * Definering av local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d fmkal1_kund     s                   like(fmkund)
     d fmkal1_kpro     s                   like(fmkpro)
     d fmkal1_kftp     s                   like(fmkftp)
     d fmkal1_ktsq     s                   like(fmktsq)
     d fmkalu_kund     s                   like(fmkund)
     d fmkalu_kpro     s                   like(fmkpro)
     d fmkalu_kftp     s                   like(fmkftp)
     d fmkalu_ktsq     s                   like(fmktsq)
     d rukrl1_kund     s                   like(rukkun)
     d rukrl1_kpro     s                   like(rukpro)
     d rukrl1_ktyp     s                   like(ruktyp)
     d rukrl1_ktsq     s                   like(ruktsq)
      *
      * Definering av variable
      *
     d w_firm          s                   like(fmfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   if        p_oper = 'D'
     c                   exsr      slett_kort
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      rukrl1_kund = p_okun
     c                   eval      rukrl1_kpro = p_opro
     c                   eval      rukrl1_ktyp = p_otyp
     c                   eval      rukrl1_ktsq = p_otsq
     c     rukrl1_key    chain     rukrl1
      *
     c                   if        %found
     c                   if        p_oper = 'W'
     c                   exsr      skriv_kort
     c                   endif
      *
     c                   if        p_oper = 'U'
     c                   exsr      oppd_kort
     c                   endif
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for skriv av ny kortpost
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_kort    begsr
      *
     c                   eval      fmkal1_kund = p_okun
     c                   eval      fmkal1_kpro = p_opro
     c                   eval      fmkal1_kftp = p_otyp
     c                   eval      fmkal1_ktsq = p_otsq
     c     fmkal1_key    chain     fmkal1
     c                   if        %found
     c                   leavesr
     c                   endif
      *
     c                   eval      fmfirm = p_ofir
     c                   eval      fmkund = p_okun
     c                   eval      fmkpro = p_opro
     c                   eval      fmkftp = p_otyp
     c                   eval      fmktsq = p_otsq
      *
7.01 c                   if        rukknr < 10000000000000000
     c                   eval      fmkrnr = rukknr
7.01 c                   else
7.01 c                   eval      fmkrnr = 9999999999999999
7.01 c                   endif
     c                   eval      fmexpd = rukexp
     c                   eval      fmoblg = rukobl
     c                   eval      fmhldr = rukhld
      *
     c                   eval      fmeusr = l_user
     c                   time                    fmetim
     c                   time                    fmedat
      *
     c                   write     fmkalur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av eksisterende kortpost
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_kort     begsr
      *
     c                   eval      fmkalu_kund = p_okun
     c                   eval      fmkalu_kpro = p_opro
     c                   eval      fmkalu_kftp = p_otyp
     c                   eval      fmkalu_ktsq = p_otsq
     c     fmkalu_key    chain     fmkalu
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
     c                   eval      fmkrnr = rukknr
     c                   eval      fmexpd = rukexp
     c                   eval      fmoblg = rukobl
     c                   eval      fmhldr = rukhld
      *
     c                   eval      fmeusr = l_user
     c                   time                    fmetim
     c                   time                    fmedat
      *
     c                   update    fmkalur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sletting av kortpost
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slett_kort    begsr
      *
     c                   eval      fmkalu_kund = p_okun
     c                   eval      fmkalu_kpro = p_opro
     c                   eval      fmkalu_kftp = p_otyp
     c                   eval      fmkalu_ktsq = p_otsq
     c     fmkalu_key    chain     fmkalu
     c                   if        %found
     c                   delete    fmkalur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_ofir
     c                   parm                    p_okun
     c                   parm                    p_opro
     c                   parm                    p_okpn
     c                   parm                    p_otyp
     c                   parm                    p_otsq
     c                   parm                    p_oper
      *
      * Key for oppslag på kort/fakt.avtale-register (OFAK)
      *
     c     fmkal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fmkal1_kund
     c                   kfld                    fmkal1_kpro
     c                   kfld                    fmkal1_kftp
     c                   kfld                    fmkal1_ktsq
      *
      * Key for oppdat. av kort/fakt.avtale-register (OFAK)
      *
     c     fmkalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fmkalu_kund
     c                   kfld                    fmkalu_kpro
     c                   kfld                    fmkalu_kftp
     c                   kfld                    fmkalu_ktsq
      *
      * Key for oppslag på kort/fakt.avtale-register (OKON)
      *
     c     rukrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rukrl1_kund
     c                   kfld                    rukrl1_kpro
     c                   kfld                    rukrl1_ktyp
     c                   kfld                    rukrl1_ktsq
      *
     c                   eval      w_firm = p_ofir
      *
     c                   endsr
