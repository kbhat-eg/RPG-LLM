      /title  ASOFAK Lev.vare, henter vareregister - Smith
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FW716R
      * Beskrivelse . . : Danning av leverandør vareregister,
      *                   Smith
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       090101 HKO  Nytt program
      * 6.10  230609 BSA  Oppdatert med sporingsinformasjon
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflvwpf    if   e           k disk
     ffenkl1    if   e           k disk    rename(fenkpfr:fenkl1r)
     fflvapf    o  a e             disk
      *
6.10  * Local data area
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av datastruktur
      *
      *  Recordbeskrivelse - Smith
      *
     d                 ds
     d d_inpu                       417
     d  d_vare                       15    overlay(d_inpu:1)                    Varenr
     d  d_vtxt                       70    overlay(d_inpu:17)                   Varetekst
     d  d_vgrp                        7    overlay(d_inpu:88)                   Varegruppe
     d  d_gbes                       40    overlay(d_inpu:96)                   Varegruppetekst
     d  d_pgrp                        5    overlay(d_inpu:137)                  Produktgruppe
     d  d_pbes                       40    overlay(d_inpu:143)                  Produktgruppetekst
     d  d_lage                        1    overlay(d_inpu:184)                  Lagerføres
     d  d_vekt                        8  3 overlay(d_inpu:186)                  Vekt pr. grunnenhet
     d  d_volu                        8  3 overlay(d_inpu:195)                  Vol. pr. grunnenhet
     d  d_enhe                        3    overlay(d_inpu:204)                  Grunnenhet
     d  d_enhp                        3    overlay(d_inpu:208)                  Prisenhet
     d  d_enhs                        3    overlay(d_inpu:212)                  Salgsenhet
     d  d_omrp                        8  3 overlay(d_inpu:216)                  Omr. gr.enh.-prisenh
     d  d_omrs                        8  3 overlay(d_inpu:225)                  Omr. gr.enh.-salgsen
     d  d_antp                        5  2 overlay(d_inpu:234)                  Pris pr. antall
     d  d_lipr                        9  2 overlay(d_inpu:240)                  Listepris eksl.
     d  d_liim                        9  2 overlay(d_inpu:250)                  Listepris inkl.
     d  d_gvar                        8    overlay(d_inpu:260)                  Gml. varenr
     d  d_eann                       13    overlay(d_inpu:269)                  EAN-nr
     d  d_stat                        2    overlay(d_inpu:283)                  Status
     d  d_fdat                        8    overlay(d_inpu:286)                  Dato gyldig fra
     d  d_tdat                        8    overlay(d_inpu:295)                  Dato gyldig til
     d  d_plas                       20    overlay(d_inpu:304)                  Lagerføres hvor
     d  d_kopr                        9  2 overlay(d_inpu:325)                  Kalk. kostpris
     d  d_inpr                        9  2 overlay(d_inpu:335)                  Innkjøpspris
     d  d_sapr                        9  2 overlay(d_inpu:345)                  Salgspris eksl.
     d  d_saim                        9  2 overlay(d_inpu:355)                  Salgspris inkl.
     d  d_sort                        1    overlay(d_inpu:365)                  Sortimentskode
     d  d_enhl                        3    overlay(d_inpu:367)                  Anbef. lagerenhet
     d  d_anta                        5  0 overlay(d_inpu:371)                  Pakningsantall
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_ldor          s              6  0
      *
      * Definering av variable i nøkler
      *
     d fenkl1_enhf     s                   like(feenhf)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_dato          s              6  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser input-fil, og legger data ut på leverandør vareregister
      *
     c                   read      flvwpf                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      d_inpu = fwirad
      *
      * Skriver til leverandør vareregister
      *
     c                   eval      fwfirm = p_firm
     c                   eval      fwldor = p_ldor
     c                   eval      fwekod = *blank
     c                   eval      fwtek1 = %trim(%subst(d_vtxt:1:35))
     c                   eval      fwtek2 = %trim(%subst(d_vtxt:36:35))
     c                   eval      fwlvar = d_vare
     c                   eval      fweann = 0
     c                   eval      fwenhe = d_enhe
     c                   eval(h)   fwinpr = d_inpr * d_omrp
     c                   eval(h)   fwsapr = d_sapr * d_omrp
     c                   eval      fwdato = *loval
     c                   eval      fwvolu = d_volu
     c                   eval      fwvekt = d_vekt
     c                   eval      fwbmen = 0
     c                   eval      fwhkod = 1
      *
     c                   eval      fwline = fwline + 1
      *
     c                   move      d_eann        fweann
      *
     c                   eval      fenkl1_enhf = d_enhe
     c     fenkl1_key    chain     fenkl1                             91
     c                   if        *in91 = *off
     c                   eval      fwenhe = feenht
     c                   endif
      *
     c                   if        d_fdat <> *blank
     c                   move      d_fdat        w_dato
     c     *ymd          move      w_dato        fwdato
     c                   endif
      *
     c                   if        d_anta <> 0 and
     c                             d_anta <> 1 and
     c                             d_enhe <> 'PAK'
     c                   eval      fwbmen = d_anta
     c                   endif
      *
6.10 c                   time                    fwodat
6.10 c                   time                    fwotim
6.10 c                   eval      fwousr = l_user
6.10 c                   time                    fwedat
6.10 c                   time                    fwetim
6.10 c                   eval      fweusr = l_user
     c                   write     flvapfr
      *
     c                   read      flvwpf                                 90
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
      *
      * Key for oppslag på enhet-konvertering
      *
     c     fenkl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fenkl1_enhf
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
