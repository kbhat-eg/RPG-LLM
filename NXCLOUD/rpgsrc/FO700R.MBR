      /TITLE  ASOFAK Ordrehode, Oppdatering av totaler fra linjer
     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO700R
      * Beskrivelse . . : Ordrehode, Oppdatering av totaler fra linjer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       081099 AMD  Nytt program
      * 6.10  240609 BSA  Oppdatert med sporingsinformasjon
6.nu  * R6.30 030614 bsa  Programmet gjennomgått mtp nummerutvidelser.
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvltyl1    if a e           k disk    rename(vltypfr:vltyl1r)
     ffodtl1    if a e           k disk    rename(fodtpfr:fodtl1r)
     ffohelu    uf a e           k disk    rename(fohepfr:fohelur)
      *
6.10  * Definering av Local data area
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av parametere
      *
     d p_firm          s                   like(fofirm)
     d p_numm          s                   like(fonumm)
     d p_suff          s                   like(fosuff)
      *
      * Definering av variable i nøkler
      *
     d vltyl1_ltyp     s                   like(valtyp)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d fohelu_numm     s                   like(fonumm)
     d fohelu_suff     s                   like(fosuff)
      *
      * Definering av variable
      *
     d w_firm          s                   like(fofirm)
     d w_tots          s                   like(fotots)
     d w_totk          s                   like(fototk)
     d w_totr          s                   like(fototr)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Lese ordrelinjer og summer opp
      *
     c                   eval      fodtl1_numm = p_numm
     c                   eval      fodtl1_suff = p_suff
     c     fodtl1_key    setll     fodtl1
     c     fodtl1_key    reade     fodtl1                                 90
      *
     c                   dow       *in90  = *off
      *
      * Hente linjetype
      *
     c                   eval      vltyl1_ltyp = fdltyp
     c     vltyl1_key    chain     vltyl1r                            91
      *
      * Summer opp dersom det ikke er tekstlinjer
      *
     c                   if        valktx = 0
     c                   eval      w_tots = w_tots + fdsums
     c                   eval      w_totk = w_totk + fdsumk
     c                   eval      w_totr = w_totr + fdsumr
     c                   endif
     c     fodtl1_key    reade     fodtl1                                 90
      *
     c                   enddo
      *
     c                   exsr      oppd_fohe
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatere ordrehode med verdier fra ordrelinjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_fohe     begsr
      *
      * Gjør oppslag mot ordrehode
      *
     c                   eval      fohelu_numm = p_numm
     c                   eval      fohelu_suff = p_suff
     c     fohelu_key    chain     fohelur                            90
      *
      * Oppdaterer ordrehode med ny informasjon
      *
     c                   eval      fotots = w_tots
     c                   eval      fototk = w_totk
     c                   eval      fototr = w_totr
     c                   if        *in90  = *off
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   update    fohelur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_firm
6.nu c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Key for oppslag på linjetype
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
      * Key for oppslag på ordrehode
      *
     c     fohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelu_numm
     c                   kfld                    fohelu_suff
      *
      * Key for oppslag på ordrelinje
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
      *
      * Legg inn firmanummer
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
