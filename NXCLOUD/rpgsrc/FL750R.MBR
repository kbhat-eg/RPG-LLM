      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FL750R
      * Beskrivelse . . : Kundeprosjekt, oppdaterer søketekst
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       120499 HKO  Nytt program
3.11  * R3.11 220900 AMD  Selgernr er utvidet fra 2 til 4 posisjoner
      *       241105 HKO  Lagt inn eksternt prosjektnr i søkefelt
      * 6.10  230609 BSA  Oppdatert med sporingsinformasjon
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fapospf    if   e           k disk    rename(apospfr:apospfr)
     ffkpslu    uf a e           k disk    rename(fkpspfr:fkpslur)
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av parametre
      *
     d p_firm          s                   like(flfirm)
     d p_kund          s                   like(flkund)
     d p_kpro          s                   like(flkpro)
      *
      * Definering av variable i nøkler
      *
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d rkunl1_kund     s                   like(rkkund)
     d apospf_ponr     s                   like(apponr)
     d fkpslu_skun     s                   like(flskun)
     d fkpslu_skpr     s                   like(flskpr)
      *
      * Definering av variable
      *
     d w_ponr_alf      s              4
3.11 d w_selg_alf      s              4
     d w_kund_alf      s              6
     d w_kpro_alf      s              6
      *
     d w_firm          s                   like(flfirm)
     d w_stek          s                   like(flstek)
      *
      * Definering av konstanter
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      fkprl1_kund = p_kund
     c                   eval      fkprl1_kpro = p_kpro
     c     fkprl1_key    chain     fkprl1
     c                   if        %found
     c                   exsr      oppdater
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer søketekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
      * Navn
      *
     c     Lo:Up         xlate     flnavn        flnavn
      *
     c                   eval      w_stek = flnavn
      *
      * Kundenavn
      *
     c                   eval      rkunl1_kund = flkund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c     Lo:Up         xlate     rknavn        rknavn
     c                   eval      w_stek = %trimr(w_stek) + ' ' +
     c                                      %trim(rknavn)
     c                   endif
      *
      * Adresse-1
      *
     c                   if        fladr1 <> *blank
     c     Lo:Up         xlate     fladr1        fladr1
     c                   eval      w_stek = %trimr(w_stek) + ' ' +
     c                                      %trim(fladr1)
     c                   endif
      *
      * Adresse-2
      *
     c                   if        fladr2 <> *blank
     c     Lo:Up         xlate     fladr2        fladr2
     c                   eval      w_stek = %trimr(w_stek) + ' ' +
     c                                      %trim(fladr2)
     c                   endif
      *
      * Postnummer/poststed
      *
     c                   if        flponr <> 0
      *
     c                   move      flponr        w_ponr_alf
     c                   eval      w_stek = %trimr(w_stek) + ' ' +
     c                                      w_ponr_alf
      *
     c                   eval      apospf_ponr = flponr
     c     apospf_key    chain     apospf
     c                   if        %found
     c     Lo:Up         xlate     apsted        apsted
     c                   eval      w_stek = %trimr(w_stek) + ' ' +
     c                                      %trim(apsted)
     c                   endif
      *
     c                   endif
      *
      * Kontaktaktperson (fra kunde)
      *
     c                   if        flkprs <> *blank
     c     Lo:Up         xlate     flkprs        flkprs
     c                   eval      w_stek = %trimr(w_stek) + ' ' +
     c                                      %trim(flkprs)
     c                   endif
      *
      * Selgernummer
      *
     c                   if        flselg <> 0
     c                   move      flselg        w_selg_alf
     c                   eval      w_stek = %trimr(w_stek) + ' ' +
     c                                      w_selg_alf
     c                   endif
      *
      * Kundenummer
      *
     c                   move      flkund        w_kund_alf
      *
     c                   eval      w_stek = %trimr(w_stek) + ' ' +
     c                                      w_kund_alf
      *
      * Prosjektnummer
      *
     c                   move      flkpro        w_kpro_alf
      *
     c                   eval      w_stek = %trimr(w_stek) + ' ' +
     c                                      w_kpro_alf
      *
      * Eksternt kundeprosjektnavn
      *
     c                   if        flepro <> *blank
     c                   eval      w_stek = %trimr(w_stek) + ' ' +
     c                                      %trim(flepro)
     c                   endif
      *
      * Oppdaterer/oppretter søketekst
      *
     c                   eval      fkpslu_skun = flkund
     c                   eval      fkpslu_skpr = flkpro
     c     fkpslu_key    chain     fkpslur
      *
     c                   eval      flstek = w_stek
      *
     c                   if        %found(fkpslu)
6.10 c                   time                    flseda
6.10 c                   time                    flseti
6.10 c                   eval      flseus = l_user
     c                   update    fkpslur
     c                   else
     c                   eval      flsfir = w_firm
     c                   eval      flskun = flkund
     c                   eval      flskpr = flkpro
6.10 c                   time                    flsoda
6.10 c                   time                    flsoti
6.10 c                   eval      flsous = l_user
6.10 c                   time                    flseda
6.10 c                   time                    flseti
6.10 c                   eval      flseus = l_user
     c                   write     fkpslur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kund
     c                   parm                    p_kpro
      *
      * Key for oppslag på kunde-prosjekt-register
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag på kunde-register
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på poststed-register
      *
     c     apospf_key    klist
     c                   kfld                    apospf_ponr
      *
      * Key for oppdatering av søketekst-register
      *
     c     fkpslu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkpslu_skun
     c                   kfld                    fkpslu_skpr
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
