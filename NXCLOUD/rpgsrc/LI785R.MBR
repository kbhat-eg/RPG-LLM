     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LI785R
      * Beskrivelse . . : Sjekker antall EDI-OBKR på aktuell bruker
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       140306 ASK  Nytt program
6.nu  *       100614 bof  sjekket mtp. nummerutvidelse
      *
8.01  *K0000  240621 BSA  Utvidet meldingsnummer fra 6-8 siffer
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fledil1    if   e           k disk    rename(ledipfr:ledil1r)
     flohelr    if   e           k disk    rename(lohepfr:lohelrr)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
      *
      * Definering av parametre
      *
     d p_anta          s              6  0
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d ledil1_type     s                   like(litype)
     d lohelr_numm     s                   like(lonumm)
     d lohelr_suff     s                   like(losuff)
     d fusrl1_user     s                   like(fbuser)
      *
      * Definering av variable
      *
     d w_firm          s                   like(lofirm)
      *
     d w_dato          s               d   datfmt(*iso)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   if        l_user = *blank
     c                   goto      avslutt
     c                   endif
      *
      * Initierer output-parametre
      *
     c                   eval      p_anta = 0
      *
      * Finner selgernr for bruker
      *
     c                   eval      fusrl1_user = l_user
     c     fusrl1_key    chain     fusrl1
     c                   if        not %found or
     c                             fbselg = 0
     c                   goto      avslutt
     c                   endif
      *
      * Leser alle EDI-ordrebekreftelser og sjekker om den er ubehandlet
      * Leser matchende bestilling og sjekker selgernummer på denne.
      *
     c                   eval      ledil1_type = 'OBKR'
     c     ledil1_key    setll     ledil1
     c     ledil1_key    reade     ledil1
     c                   dow       not %eof
      *
     c                   if        listat <> '7' and
     c                             listat <> '8' and
     c                             listat <> '9'
      *
     c                   eval      lohelr_numm = linumm
     c                   eval      lohelr_suff = lisuff
     c     lohelr_key    chain     lohelr
     c                   if        %found and
     c                             loinnk = fbselg
     c                   eval      p_anta = p_anta + 1
     c                   endif
      *
     c                   endif
      *
     c     ledil1_key    reade     ledil1
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_anta
      *
      * Key for les på EDI-melding
      *
     c     ledil1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ledil1_type
      *
      * Key for les på bestilling-hode
      *
     c     lohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelr_numm
     c                   kfld                    lohelr_suff
      *
      * Key for oppslag på ofak bruker
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrl1_user
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
