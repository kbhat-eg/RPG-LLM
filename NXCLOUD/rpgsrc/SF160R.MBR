     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASSTAT
      * Program . . . . : SF560R
      * Beskrivelse . . : Salgsstatistikk, registrering fra ordre - param.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       141005 HKO  Nytt program
6.NU  * R6.30 230514 bsa  Programmet gjennomgått mtp nummerutvidelser.
7.xx  * 7.00  220116 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner - COMP
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsstapf    if   e           k disk
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
      *
     fsf160d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_kode          s              1
     d p_firm          s                   like(sffirm)
     d p_vkun          s                   like(sfvkun)
     d p_kpro          s                   like(sfkpro)
6.NU d p_numm          s                   like(sfornr)
     d p_suff          s                   like(sforsu)
     d p_line          s                   like(sforli)
     d p_ltyp          s              2
     d p_lety          s                   like(sflety)
     d p_kost          s              1
      *
      * Definering av variabler i nøkler
      *
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_dato          s               d   datfmt(*iso)
     d w_dato_alf      s             10
      *
     d w_firm          s                   like(sffirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer bildet
      *
     c                   time                    w_dato
     c                   move      w_dato        w_dato_alf
     c                   movel     w_dato_alf    b1paar
      *
      * Behandler bildet
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   eval      b_feil = *off
     c                   goto      b1taga
     c                   endif
      *
      * Kaller spørreprogram
      *
     c                   eval      w_dato = *loval
     c                   if        b1bida <> 0
     c     *dmy          move      b1bida        w_dato
     c                   endif
      *
     c                   call      'SF161R'
     c                   parm                    p_kode
     c                   parm                    p_firm
     c                   parm                    b1paar
     c                   parm                    b1vkun
     c                   parm                    b1kpro
     c                   parm                    w_dato
6.NU c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_line
     c                   parm                    p_ltyp
     c                   parm                    p_lety
     c                   parm                    p_kost
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Kunde
      *
     c                   if        wactfe = 'B1VKUN'
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    b1vkun
     c                   parm                    b1knav
     c                   leavesr
     c                   endif
      *
      * Kundeprosjekt
      *
     c                   if        wactfe = 'B1KPRO'
     c                   CALL      'FL500R'
     c                   parm                    w_firm
     c                   parm                    b1vkun
     c                   parm                    b1kpro
     c                   parm                    b1knav
     c                   parm                    b1pnav
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
     c                   eval      b1knav = *blank
     c                   eval      b1pnav = *blank
      *
      * År
      *
     c                   if        b1bida <> 0
     c     *dmy          move      b1bida        w_dato
     c                   move      w_dato        w_dato_alf
     c                   movel     w_dato_alf    b1paar
     c                   endif
      *
     c                   if        b1paar = 0
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Dato
      *
     c                   if        b1bida <> 0
     c     *dmy          test(d)                 b1bida                 32
     c                   if        *in32
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Kunde
      *
     c                   if        b1vkun = 0
     c                   eval      b1vkun = p_vkun
     c                   endif
      *
     c                   eval      rkunl1_kund = b1vkun
     c     rkunl1_key    chain     rkunl1
     c                   if        not %found
     c                   eval      *in33  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      b1knav = rknavn
      *
      * Kundeprosjekt
      *
     c                   if        b1kpro <> 0
     c                   eval      fkprl1_kund = b1vkun
     c                   eval      fkprl1_kpro = b1kpro
     c     fkprl1_key    chain     fkprl1
     c                   if        not %found
     c                   eval      *in34  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1pnav = flnavn
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_kode
     c                   parm                    p_firm
     c                   parm                    p_vkun
     c                   parm                    p_kpro
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_line
     c                   parm                    p_ltyp
     c                   parm                    p_lety
     c                   parm                    p_kost
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kunde/kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      *
      * Henter firma fra parameter og initierer skjermbilde
      *
     c                   eval      w_firm = p_firm
      *
     c                   eval      b1vkun = p_vkun
     c                   eval      b1kpro = p_kpro
      *
     c                   eval      rkunl1_kund = b1vkun
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      b1knav = rknavn
     c                   endif
      *
     c                   if        b1kpro <> 0
     c                   eval      fkprl1_kund = b1vkun
     c                   eval      fkprl1_kpro = b1kpro
     c     fkprl1_key    chain     fkprl1
     c                   if        %found
     c                   eval      b1pnav = flnavn
     c                   endif
     c                   endif
      *
     c                   endsr
