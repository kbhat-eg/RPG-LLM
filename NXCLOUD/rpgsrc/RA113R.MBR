     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RA113R
      * Beskrivelse . . : Budsjettnøkler, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       260302 KOB  Nytt program
7.xx  * 7.00  020316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra13l1    if   e           k disk    rename(ra13pfr:ra13l1r)
     fra13l2    if   e           k disk    rename(ra13pfr:ra13l2r)
     fra13lr    if   e           k disk    rename(ra13pfr:ra13lrr)
     fra13lu    uf a e           k disk    rename(ra13pfr:ra13lur)
      *
     fra113d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d ra13l1_mkod     s                   like(ramkod)
     d ra13l2_mtxt     s                   like(ramtxt)
     d ra13lr_mkod     s                   like(ramkod)
     d ra13lu_mkod     s                   like(ramkod)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(ramfir)
      *
     d s_mkod          s                   like(ramkod)
     d w_pro           s              5  2
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Siden som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2mkod <> 0 or
     c                             b2mtxt <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ra13l2_mtxt = b1mtxt
     c     ra13l2_key    setll     ra13l2
     c                   other
     c                   eval      ra13l1_mkod = b1mkod
     c     ra13l1_key    setll     ra13l1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Budsjettnøkkel
      *
     c                   if        b2mkod <> 0
     c                   eval      w_seqe = *blank
     c                   eval      ra13l1_mkod = b2mkod
     c     ra13l1_key    setll     ra13l1
     c                   goto      end_pos
     c                   endif
      *
      * Beskrivelse
      *
     c                   if        b2mtxt <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      ra13l2_mtxt = b2mtxt
     c     ra13l2_key    setll     ra13l2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2mkod = 0
     c                   eval      b2mtxt = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1mkod = b1mkod
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1mkod = b1mkod
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1mkod = b1mkod
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1mkod = b1mkod
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1mkod = b2mkod
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1mkod = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      ra13lr_mkod = c1mkod
     c     ra13lr_key    chain     ra13lr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1mkod = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2mkod = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      ra13lr_mkod = c1mkod
     c     ra13lr_key    chain     ra13lr
      *
     c                   if        %found
     c                   eval      c2mtxt = ramtxt
     c                   eval      c2mp01 = ramp01                                           .
     c                   eval      c2mp02 = ramp02                                           .
     c                   eval      c2mp03 = ramp03                                           .
     c                   eval      c2mp04 = ramp04                                           .
     c                   eval      c2mp05 = ramp05                                           .
     c                   eval      c2mp06 = ramp06                                           .
     c                   eval      c2mp07 = ramp07                                           .
     c                   eval      c2mp08 = ramp08                                           .
     c                   eval      c2mp09 = ramp09                                           .
     c                   eval      c2mp10 = ramp10                                           .
     c                   eval      c2mp11 = ramp11                                           .
     c                   eval      c2mp12 = ramp12                                           .
     c                   eval      c2mp13 = ramp13                                           .
     c                   eval      c2eusr = ramusr
     c     *dmy          move      ramdat        c2edat
     c                   else
     c                   eval      c2mtxt = *blank
     c                   eval      c2mp01 = *zeros                                           .
     c                   eval      c2mp02 = *zeros                                           .
     c                   eval      c2mp03 = *zeros                                           .
     c                   eval      c2mp04 = *zeros                                           .
     c                   eval      c2mp05 = *zeros                                           .
     c                   eval      c2mp06 = *zeros                                           .
     c                   eval      c2mp07 = *zeros                                           .
     c                   eval      c2mp08 = *zeros                                           .
     c                   eval      c2mp09 = *zeros                                           .
     c                   eval      c2mp10 = *zeros                                           .
     c                   eval      c2mp11 = *zeros                                           .
     c                   eval      c2mp12 = *zeros                                           .
     c                   eval      c2mp13 = *zeros                                           .
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1mkod = k1mkod
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2mtxt = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
      * Sjekk prosent
      *
     c                   eval      w_pro = 0
     c                   eval      w_pro = w_pro + c2mp01
     c                   eval      w_pro = w_pro + c2mp02
     c                   eval      w_pro = w_pro + c2mp03
     c                   eval      w_pro = w_pro + c2mp04
     c                   eval      w_pro = w_pro + c2mp05
     c                   eval      w_pro = w_pro + c2mp06
     c                   eval      w_pro = w_pro + c2mp07
     c                   eval      w_pro = w_pro + c2mp08
     c                   eval      w_pro = w_pro + c2mp09
     c                   eval      w_pro = w_pro + c2mp10
     c                   eval      w_pro = w_pro + c2mp11
     c                   eval      w_pro = w_pro + c2mp12
     c                   eval      w_pro = w_pro + c2mp13
      *
     c                   if        w_pro <> 100
     c                   move      *on           *in33
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   ra13lur
      *
     c                   eval      ra13lu_mkod = c1mkod
     c     ra13lu_key    chain     ra13lur
      *
     c                   eval      ramtxt = c2mtxt
     c                   eval      ramp01 = c2mp01                                           .
     c                   eval      ramp02 = c2mp02                                           .
     c                   eval      ramp03 = c2mp03                                           .
     c                   eval      ramp04 = c2mp04                                           .
     c                   eval      ramp05 = c2mp05                                           .
     c                   eval      ramp06 = c2mp06                                           .
     c                   eval      ramp07 = c2mp07                                           .
     c                   eval      ramp08 = c2mp08                                           .
     c                   eval      ramp09 = c2mp09                                           .
     c                   eval      ramp10 = c2mp10                                           .
     c                   eval      ramp11 = c2mp11                                           .
     c                   eval      ramp12 = c2mp12                                           .
     c                   eval      ramp13 = c2mp13                                           .
      *
     c                   time                    ramdat
     c                   time                    ramtim
     c                   eval      ramusr = l_user
      *
     c                   if        %found
     c                   update    ra13lur
     c                   else
     c                   eval      ramfir = w_firm
     c                   eval      ramkod = c1mkod
     c                   write     ra13lur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1mtxt = ramtxt
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      ra13lr_mkod = d1mkod
     c     ra13lr_key    chain     ra13lr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      ra13lu_mkod = d1mkod
     c     ra13lu_key    chain     ra13lur
      *
     c                   delete    ra13lur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_mkod = k1mkod
      *
      * Fyller kopierings-bildet
      *
     c                   eval      ra13lr_mkod = k1mkod
     c     ra13lr_key    chain     ra13lr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1mkod = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      ra13lr_mkod = k1mkod
     c     ra13lr_key    chain     ra13lr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1mkod = s_mkod
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      ra13l2
     c                   other
     c                   read      ra13l1
     c                   endsl
      *
     c                   if        %eof or
     c                             ramfir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ra13l2_mtxt = ramtxt
     c                   other
     c                   eval      ra13l1_mkod = ramkod
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1mkod = ramkod
     c                   eval      b1mtxt = ramtxt
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     ra13l2_key    setgt     ra13l2
     c                   readp     ra13l2
     c                   other
     c     ra13l1_key    setgt     ra13l1
     c                   readp     ra13l1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     ra13l2
     c                   other
     c                   readp     ra13l1
     c                   endsl
      *
     c                   if        %eof or
     c                             ramfir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      ra13l2_mtxt = ramtxt
     c                   other
     c                   eval      ra13l1_mkod = ramkod
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     ra13l2_key    setll     ra13l2
     c                   other
     c     ra13l1_key    setll     ra13l1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på budsjettnøkkel
      *
     c     ra13l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra13l1_mkod
      *
      * Key for posisjonering på beskrivelse
      *
     c     ra13l2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra13l2_mtxt
      *
      * Key for les på budsjettnøkkel
      *
     c     ra13lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra13lr_mkod
      *
      * Key for oppdatering av budsjettnøkkel
      *
     c     ra13lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra13lu_mkod
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      ra13l1_mkod = 0
     c     ra13l1_key    setll     ra13l1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
