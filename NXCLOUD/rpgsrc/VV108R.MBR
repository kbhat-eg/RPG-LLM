     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VV108R
      * Beskrivelse . . : Vare, vedlikehold - endre lagerenhet
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       091199 HKO  Nytt program
      * 6.10  290609 BSA  Oppdatert med sporingsinformasjon
6.30  *       270116 bof  Sjekk om vare ligger på LBDTPF/LTELPF på aktiv batch
6.31  *       210317 bof  Vise hvilke tellinger som er aktive.
7.xx  * 7.00  030216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fvvarlu    uf   e           k disk    rename(vvarpfr:vvarlur)
6.30 flbdtl3    if   e           k disk    rename(lbdtpfr:lbdtl3r)
6.30 fltell3    if   e           k disk    rename(ltelpfr:ltell3r)
6.30 flbchl1    if   e           k disk    rename(lbchpfr:lbchl1r)
     fvv108d    cf   e             workstn
      *
6.10  * Definering av Local data area
6.10  *
6.10 d                uds
6.10 d l_user                911    920
6.31  * Array
6.31 d a_txt           s             40    dim(15)
      *
      *
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vvfirm)
     d p_vare          s                   like(vvvare)
      *
      * Definering av variabler i nøkler
      *
     d vvenl1_enhe     s                   like(veenhe)
6.30 d lbdtl3_vare     s                   like(lvvare)
6.30 d ltell3_vare     s                   like(levare)
6.30 d lbchl1_batc     s                   like(lcbatc)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
     d w_etxt          s             35
6.31 d i               s              3  0
      *
     d w_firm          s                   like(vvfirm)
     d w_vare          s                   like(vvvare)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer skjerm-felter
      *
     c     vvarl1_key    chain     vvarl1                             90
     c                   if        *in90 = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      b1enlg = vvenhl
      *
      * Behandler bildet
      *
     c     b1taga        tag
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
      * Oppdaterer lagerenhet på vare
      *
     c     vvarlu_key    chain     vvarlur                            90
     c                   if        *in90 = *off
     c                   eval      vvenhl = b1enhl
6.10 c                   time                    vvedat
6.10 c                   time                    vvetim
6.10 c                   eval      vveusr = l_user
     c                   update    vvarlur
     c                   endif
     c
      * Kaller program for bytting av enhet i innkjøpsstatistikk
      *
     c                   call      'LL750R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    b1enhl
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørringer (F4)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
     c                   if        w_afld = 'B1ENHL'
     c                   call      'VE550R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    b1enhl
     c                   parm                    w_etxt
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
6.31 c                   eval      i = 0
6.31 c                   eval      a_txt(*) = *blank
6.30  *
6.30  * sjekker om vare ligger på en telling (LBDTPF/LTELPF) på aktiv batch
6.30  *
6.30 c                   eval      ltell3_vare = p_vare
6.30 c     ltell3_key    setll     ltell3
6.30 c     ltell3_key    reade     ltell3
6.31 c                   dow       not %eof(ltell3)  and
6.31 c                             i < 15
6.30 c                   eval      lbchl1_batc = lebatc
6.30 c     lbchl1_key    chain     lbchl1
6.30 c                   if        %found(lbchl1) and
6.30 c                             lcsper = 0
6.31 c                   eval      i = i + 1
6.31 c                   eval      a_txt(i)  = 'Batch : ' +
6.31 c                                      %trim(lebatc)
6.30 c                   endif
6.30 c     ltell3_key    reade     ltell3
6.30 c                   enddo
6.31  *flytter fra matrise til bilde
6.31 c                   if        i > 0
6.31 c                   eval      n1tx01 = a_txt(1)
6.31 c                   eval      n1tx02 = a_txt(2)
6.31 c                   eval      n1tx03 = a_txt(3)
6.31 c                   eval      n1tx04 = a_txt(4)
6.31 c                   eval      n1tx05 = a_txt(5)
6.31 c                   eval      n1tx06 = a_txt(6)
6.31 c                   eval      n1tx07 = a_txt(7)
6.31 c                   eval      n1tx08 = a_txt(8)
6.31 c                   eval      n1tx09 = a_txt(9)
6.31 c                   eval      n1tx10 = a_txt(10)
6.31 c                   eval      n1tx11 = a_txt(11)
6.31 c                   eval      n1tx12 = a_txt(12)
6.31 c                   eval      n1tx13 = a_txt(13)
6.31 c                   exfmt     n1win
6.31 c                   eval      *in37  = *on
6.31 c                   eval      b_feil = *on
6.31 c                   leavesr
6.31 c                   endif
6.30  *
6.30 c                   eval      lbdtl3_vare = p_vare
6.30 c     lbdtl3_key    setll     lbdtl3
6.30 c     lbdtl3_key    reade     lbdtl3
6.30 c                   dow       not %eof(lbdtl3)
6.30 c                   eval      lbchl1_batc = lvbatc
6.30 c     lbchl1_key    chain     lbchl1
6.30 c                   if        %found(lbchl1) and
6.30 c                             lcsper = 0
6.30 c                   eval      *in37  = *on
6.30 c                   eval      b_feil = *on
6.30 c                   goto      end_sjekk
6.30 c                   endif
6.30 c     lbdtl3_key    reade     lbdtl3
6.30 c                   enddo
      *
     c                   if        b1enhl = b1enlg
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   eval      vvenl1_enhe = b1enhl
     c     vvenl1_key    chain     vvenl1                             32
     c                   if        *in32 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      * Key for oppslag på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
     c                   kfld                    vvenl1_enhe
      *
      * Key for oppdatering av vare
      *
     c     vvarlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
6.30  *
6.30  * Key for les av telling
6.30  *
6.30 c     ltell3_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    ltell3_vare
6.30  *
6.30  * Key for les av telling
6.30  *
6.30 c     lbdtl3_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    lbdtl3_vare
6.30  *
6.30  * Key for les av batch
6.30  *
6.30 c     lbchl1_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    lbchl1_batc
      *
      *
      * Henter firma og vare fra parmeter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_vare = p_vare
      *
     c                   endsr
