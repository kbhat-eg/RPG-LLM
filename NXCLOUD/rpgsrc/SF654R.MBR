     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASSTAT                                              *
      * Program . . . . : SF654R                                        *
      * Beskrivelse . . : Utskrift av bruksrettytelser                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       170304 KOB  Nytt program
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner - COMP
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frmknl1    if   e           k disk    rename(rmknpfr:rmknl1r)
     frmknl2    if   e           k disk    rename(rmknpfr:rmknl2r)
     fsstal7    if   e           k disk    rename(sstapfr:sstal7r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fralml1    if   e           k disk    rename(ralmpfr:ralml1r)
     fsf654p    o    e             printer oflind(*in30)
      *
      * Definering av variable i nøkler
      *
     d rmknl1_xalm     s                   like(rmxalm)
     d rmknl2_xknr     s                   like(rmxknr)
     d sstal7_paar     s                   like(sfpaar)
     d sstal7_vkun     s                   like(sfvkun)
     d rkunl1_kund     s                   like(rkkund)
     d votyl1_otyp     s                   like(sfotyp)
     d ralml1_alme     s                   like(rmalme)
      *
     d                 ds
      *                                                      Parameter-inn
     d ldprec                        64
     d  ldalme                        4  0 overlay(ldprec)
     d  ldpaar                        4  0 overlay(ldprec:5)
     d  ldfdat                       10d   datfmt(*iso)
     d                                     overlay(ldprec:09)
     d  ldtdat                       10d   datfmt(*iso)
     d                                     overlay(ldprec:19)
     d  ldvari                        1  0 overlay(ldprec:35)
      *
      * Definering av Local data area
      *
     d                uds
      *
     d l_wsid                901    910
     d l_user                911    920
     d l_file                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d wwparm          s             64
     d w_firm          s                   like(rkfirm)
     d w_kund_gml      s                   like(rkkund)
     d w_funnet        s              1  0
     d w_tell          s              3  0
      *
      * Les eiendomsknytningsregister
      *
     c                   eval      rmknl1_xalm = ldalme
     c     rmknl1_key    setll     rmknl1                                                    .
     c     rmknl1_key    reade     rmknl1                                 90
      *
     c                   dow       *in90 = *off
      *
     c                   if        w_kund_gml = rmxknr
     c                   goto      neste_knr
     c                   endif
      *
     c                   exsr      hent_gbr
      *
     c                   eval      w_funnet = 0
      *
     c                   eval      t1brk1 = 0
     c                   eval      t1brk2 = 0
      *
     c                   eval      rkunl1_kund = rmxknr
     c     rkunl1_key    chain     rkunl1
      *
     c                   eval      a1kund = rmxknr
     c                   eval      a1navn = rknavn
     c                   eval      a1adr1 = rkgate
     c                   eval      a1adr2 = rkadr2
     c                   eval      a1sted = rksted
      *
     c                   exsr      les_stat
      *
     c                   if        w_funnet = 1                                              .
     c                   write(e)  t1tot
     c                   endif
      *
      * Nullstill g.nr./b.nr.
      *
     c                   eval      a1gnr1 = 0
     c                   eval      a1bnr1 = 0
     c                   eval      a1gnr2 = 0
     c                   eval      a1bnr2 = 0
     c                   eval      a1gnr3 = 0
     c                   eval      a1bnr3 = 0
     c                   eval      a1gnr4 = 0
     c                   eval      a1bnr4 = 0
     c                   eval      w_tell = 0
      *
     c     neste_knr     tag
      *
     c                   eval      w_kund_gml = rmxknr
      *
     c     rmknl1_key    reade     rmknl1                                 90
     c                   enddo
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
      *
     c                   write(e)  a1hdr
     c                   write(e)  t2tot
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine som leser statistikkposter                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     les_stat      begsr
      *
     c                   eval      sstal7_paar = ldpaar
     c                   eval      sstal7_vkun = rmxknr
     c     sstal7_key    setll     sstal7                                                    .
     c     sstal7_key    reade     sstal7                                 90
      *
     c                   dow       *in90 = *off
      *
      * Test om dato ligger innenfor valgt interval
      *
     c                   if        sfbida < ldfdat or                                        .
     c                             sfbida > ldtdat                                           .
     c                   goto      neste
     c                   endif
      *
      * Test om bruksrettrabatt = 0
      *
     c                   if        sfbrk1 = 0 and                                            .
     c                             sfbrk2 = 0                                                .
     c                   goto      neste
     c                   endif
      *
      * Privatkjøp skal ikke med
      *
     c                   if        sfpriv = 1                                                .
     c                   goto      neste
     c                   endif
      *
      * Skrive rapporthode etter første gyldige post
      *
     c                   if        w_funnet = 0                                              .
      *
     c                   write(e)  a1hdr
      *
     c                   eval      w_funnet = 1
     c                   endif
      *
     c                   eval      d1binr = sfbinr
     c                   eval      d1otyp = sfotyp
     c                   eval      d1brk1 = sfbrk1
     c                   eval      d1brk2 = sfbrk2
     c     *dmy          move      sfbida        d1bida
      *
      * Slår opp ordretype
      *
     c                   eval      votyl1_otyp = sfotyp
     c     votyl1_key    chain     votyl1r
      *
     c                   if        %found                                                    .
     c                   eval      d1otxt = vaotxt
     c                   endif
      *
     c                   write(e)  d1det
      *
      * Sjekker om overflow
      *
     c                   if        *in30 = *on                                               .
     c                   write(e)  a1hdr                                                     .
     c                   eval      *in30 = *off                                              .
     c                   endif                                                           .....
      *
      * Summer til total pr kunde
      *
     c                   eval      t1brk1 = t1brk1 + sfbrk1
     c                   eval      t1brk2 = t1brk2 + sfbrk2
      *
      * Summer til total
      *
     c                   eval      t2brk1 = t2brk1 + sfbrk1
     c                   eval      t2brk2 = t2brk2 + sfbrk2
      *
     c     neste         tag
      *
     c     sstal7_key    reade     sstal7                                 90
     c                   enddo
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine som leser flere eiendommer                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     hent_gbr      begsr
      *
     c                   eval      rmknl2_xknr = rmxknr
     c     rmknl2_key    setll     rmknl2                                                    .
     c     rmknl2_key    reade     rmknl2                                 91
      *
     c                   dow       *in91 = *off
      *
     c                   eval      w_tell = w_tell + 1
      *
      * Ta vare på g.nr./b.nr. (max 4)
      *
      *
     c                   select
     c                   when      w_tell = 1
     c                   eval      a1gnr1 = rmxgnr
     c                   eval      a1bnr1 = rmxbnr
     c                   when      w_tell = 2
     c                   eval      a1gnr2 = rmxgnr
     c                   eval      a1bnr2 = rmxbnr
     c                   when      w_tell = 3
     c                   eval      a1gnr3 = rmxgnr
     c                   eval      a1bnr3 = rmxbnr
     c                   when      w_tell = 4
     c                   eval      a1gnr4 = rmxgnr
     c                   eval      a1bnr4 = rmxbnr
     c                   endsl
      *
     c     rmknl2_key    reade     rmknl2                                 91
     c                   enddo
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    wwparm
      *
     c                   move      wwparm        ldprec
      *
      * Key for eiendomsknytningsregister
      *
     c     rmknl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rmknl1_xalm
      *
      * Key for eiendomsknytningsregister
      *
     c     rmknl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rmknl2_xknr
      *
      * Key for statistikkregister
      *
     c     sstal7_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sstal7_paar
     c                   kfld                    sstal7_vkun
      *
      * Key for kunderegister
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for ordretype
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for almenningsregister
      *
     c     ralml1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ralml1_alme
      *
     c                   eval      w_firm = l_firm
      *
     c     *dmy          move      ldfdat        h1fdat
     c     *dmy          move      ldtdat        h1tdat
      *
      * Slår opp almenning
      *
     c                   eval      ralml1_alme = ldalme
     c     ralml1_key    chain     ralml1r
      *
     c                   if        %found                                                    .
     c                   eval      h1alme = rmnavn
     c                   endif
      *
     c                   endsr
      *
