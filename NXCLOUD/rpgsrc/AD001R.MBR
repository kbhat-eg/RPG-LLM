      /TITLE  ASSERV Vedlikehold av Gruppe-tekster (Hm)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : Adgangskontroll
      * Program . . . . : AD001R
      * Beskrivelse . . : Vedlikehold av Rutiner
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      *  Dato  Sign Endringbeskrivelse
      * ------ ---- ---------------------------------------------      ----
      * 280906 sst  Nytt program
7.xx  * 160817 bsa  Skjermfila er tilpasset ny GUI
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Product av felter ved vise
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer
      *    60-69 = Fileindikatorer chain etc.
      *    70-79 = Arbeidsindikatorer i sub-rutiner
      *    80-89 = Arbeidsindikatorer ordinære
      *    90-99 = Benyttes ved std. sub-rutiner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fiadrl1    if   e           k disk    rename(iadrpfr:iadrl1r)
     fiadrl2    if   e           k disk    rename(iadrpfr:iadrl2r)
     fiadrl3    if   e           k disk    rename(iadrpfr:iadrl3r)
     fiadrlr    if   e           k disk    rename(iadrpfr:iadrlrr)
     fiadrlu    uf a e           k disk    rename(iadrpfr:iadrlur)
     fiadtl2    uf   e           k disk    rename(iadtpfr:iadtl2r)
      *
     fad001d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåÆØÅ'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÆØÅ'
      *
      * Definering av array
      *
     d a_meld          s             11    dim(3) ctdata perrcd(1)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av parametre
      *
      *
      * Definering av variabler i nøkler
      *
     d iadrl1_frm      s                   like(iadfrm)
     d iadrl1_rut      s                   like(iadrut)
     d iadrl2_frm      s                   like(iadfrm)
     d iadrl2_rtx      s                   like(iadrtx)
     d iadrl3_frm      s                   like(iadfrm)
     d iadrl3_rty      s                   like(iadrty)
     d iadrl3_rut      s                   like(iadrut)
     d iadrlr_frm      s                   like(iadfrm)
     d iadrlr_rut      s                   like(iadrut)
     d iadrlu_frm      s                   like(iadfrm)
     d iadrlu_rut      s                   like(iadrut)
     d iadtl2_frm      s                   like(iadtfr)
     d iadtl2_rut      s                   like(iadtru)
      *
      * Definering av variable
      *
     d w_spge          s              3  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d w_forn          s              1    inz(*off)
     d w_anul          s              1    inz(*off)
     d w_feil          s              1    inz(*off)
     d w_frm           s                   like(iadfrm) inz('000')
     d w_rut           s                   like(iadrut)
ad   d w_tilgko        s             10
ad   d w_rutine        s             10
ad   d w_adv           s              3
ad   d w_status        s             10
      *
     d scan1           s             20
     d scan2           s             20
     d scan3           s             20
     d l1              s              3  0
     d l2              s              3  0
     d l3              s              3  0
     d i               s              3  0
     d j               s              3  0
     d wtekst          s                   like(iadrtx)
     d p_firm          s              3
     d p_ruti          s             10
     d p_text          s             50
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(10)
      *
      /EJECT
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * wvisrn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      * S1WIN  - S1 Standardvindu som inneholder definisjon av
      *             størrelse og utseende.  Bildene D1WIN og K1WIN
      *             er referert til dette.
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
ad    *
ad    * Sjekk om denne bruker har tilgang til denne F-tast
ad    *
ad   c                   select
ad   c                   when      *inkc
ad   c                   eval      w_adv    = 'F03'
ad   c                   when      *inke
ad   c                   eval      w_adv    = 'F05'
ad   c                   when      *inkf
ad   c                   eval      w_adv    = 'F06'
ad   c                   other
ad   c                   goto      f_tast_ok
ad   c                   endsl
ad   c                   eval      w_tilgko = l_user
ad   c                   eval      w_rutine = 'ADGANG'
ad   c                   eval      w_status = *blank
ad   c                   call      'AD005R'
ad   c                   parm                    w_tilgko
ad   c                   parm                    w_rutine
ad   c                   parm                    w_adv
ad   c                   parm                    w_status
ad   c                   if        w_status = 'Sperret'
ad   c                   goto      b2tagb
ad   c                   endif
ad   c     F_tast_ok     tag
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2valg = *blank
     c                   if        b2ruti <> *blank or
     c                             b2type <> *blank or
     c                             b2text <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
     c                   endif
      *
      * Behandling av subfile control
      *
     c                   if        b2valg <> *blank
     c                   exsr      control
     c                   if        w_feil = *on
     c                   goto      b2tagb
     c                   else
     c                   goto      b2taga
     c                   endif
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   select
     c                   when      w_seqe = 'A'
     c     iadrl3_key    setll     iadrl3
     c                   when      w_seqe = 'B'
     c     iadrl2_key    setll     iadrl2
     c                   other
     c     iadrl1_key    setll     iadrl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på recordformat
      *
     c                   if        b2ruti <> *blank
     c                   eval      w_seqe = ' '
     c                   eval      iadrl1_rut  = b2ruti
     c     iadrl1_key    setll     iadrl1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på beskrivelse
      *
     c                   if        b2text <> *blank
     c                   eval      w_seqe = 'B'
     c*                  eval      iadrl2_rtx  = b2text
     c                   eval      iadrl2_rtx  = *blank
     c     iadrl2_key    setll     iadrl2
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på type
      *
     c                   if        b2type <> *blank
     c                   eval      w_seqe = 'A'
     c                   eval      iadrl3_rty  = b2type
     c                   eval      iadrl3_rut  = *blank
     c     iadrl3_key    setll     iadrl3
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2ruti = *blank
     c                   eval      b2type = *blank
     c                   eval      b2text = *blank
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behanding av subfile-control
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     control       begsr
      *
     c                   eval      w_forn = *off
     c                   eval      w_feil = *off
      *
      * Opprette
      *
     c                   if        b2valg = '1'
     c                   eval      c1ruti = b2ruti
     c                   exsr      xc1bld
     c                   goto      end_ctl
     c                   endif
      *
      * Validering av nøkkel
      *
     c                   if        b2ruti = *blank
     c                   eval      w_feil = *on
     c                   eval      *in31  = *on
     c                   goto      end_ctl
     c                   endif
      *
     c                   eval      iadrlr_frm  = w_frm
     c                   eval      iadrlr_rut  = b2ruti
     c     iadrlr_key    chain     iadrlr                             60
     c                   if        *in60
     c                   eval      w_feil = *on
     c                   eval      *in32  = *on
     c                   goto      end_ctl
     c                   endif
      *
      * Endre
      *
     c                   if        b2valg = '2'
     c                   eval      c1ruti = b2ruti
     c                   exsr      xc2bld
     c                   goto      end_ctl
     c                   endif
      *
      * Kopiere
      *
     c                   if        b2valg = '3'
     c                   eval      k1ruti = b2ruti
     c                   exsr      xk1win
     c                   goto      end_ctl
     c                   endif
      *
      * Slette
      *
     c                   if        b2valg = '4'
     c                   eval      d1ruti = b2ruti
     c                   exsr      xd1win
     c                   goto      end_ctl
     c                   endif
      *
      * Vise
      *
     c                   if        b2valg = '5'
     c                   eval      c1ruti = b2ruti
     c                   exsr      xc2bld
     c                   goto      end_ctl
     c                   endif
      *
     c     end_ctl       tag
      *
     c                   eval      b2valg = *blank
     c                   eval      b2ruti = *blank
     c                   eval      b2type = *blank
     c                   eval      b2text = *blank
      *
     c                   if        w_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        wcurrn > 0
     c                   eval      wvisrn =  wcurrn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   eval      w_forn = *off
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
ad    *
ad    * Sjekk om denne bruker har tilgang til dette valg
ad    *
ad   c                   eval      w_adv    = b1valg
ad   c                   eval      w_tilgko = l_user
ad   c                   eval      w_rutine = 'ADGANG'
ad   c                   eval      w_status = *blank
ad   c                   call      'AD005R'
ad   c                   parm                    w_tilgko
ad   c                   parm                    w_rutine
ad   c                   parm                    w_adv
ad   c                   parm                    w_status
ad   c                   if        w_status = 'Sperret'
ad   c                   eval      b1valg   = *blank
     c                   update    b1sfl
ad   c                   iter
ad   c                   endif
      *
      * Endre post
      *
     c                   if        b1valg = '2'
     c                   eval      c1ruti = b1ruti
     c                   exsr      xc2bld
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopiere post
      *
     c                   if        b1valg = '3'
     c                   eval      k1ruti = b1ruti
     c                   exsr      xk1win
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slette post
      *
     c                   if        b1valg = '4'
     c                   eval      d1ruti = b1ruti
     c                   exsr      xd1win
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise post
      *
     c                   if        b1valg = '5'
     c                   eval      c1ruti = b1ruti
     c                   exsr      xc2bld
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Jobbe med tilganger på denne rutine
      *
     c                   if        b1valg = '8'
     c                   move      l_firm        p_firm
     c                   eval      p_ruti = b1ruti
     c                   eval      p_text = b1text
     c                   call      'AD004R'
     c                   parm                    p_firm
     c                   parm                    p_ruti
     c                   parm                    p_text
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        w_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   goto      c1taga
     c                   endsl
      *
      * Validering
      *
     c                   if        c1ruti = *blank
     c                   eval      *in31  = *on
     c                   goto      c1taga
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      iadrlr_frm  = w_frm
     c                   eval      iadrlr_rut  = c1ruti
     c     iadrlr_key    chain     iadrlr                             60
     c                   if        *in60 = *off
     c                   exsr      xc1msg
     c                   if        w_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
      *                  eval      c2kode = c1kode
     c                   exsr      xc2bld
      *
     c                   goto      c1taga
      *
     c     end_c1bld     endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      w_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      w_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      iadrlr_frm  = w_frm
     c                   eval      iadrlr_rut  = c1ruti
     c     iadrlr_key    chain     iadrlr                             60
      *
     c                   if        *in60 = *off
     c                   eval      c2nyen = a_meld(2)
 001 C                   eval      c2type = iadrty
 001 C                   eval      c2text = iadrtx
 001  *                  eval      c2ruti = iadrut
     c                   else
     c                   eval      c2nyen = a_meld(1)
E001 c                   endif
      *
     c                   if        b1valg = '3' or b2valg = '3'
     c                   eval      c2nyen = a_meld(1)
     c                   eval      c1ruti = k1ruti
     c                   eval      iadrlr_rut  = k1ruti
     c                   endif
      *
     c                   if        b1valg = '5' or b2valg = '5'
     c                   eval      *in30  = *on
     c                   eval      c2nyen = a_meld(3)
     c                   endif
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2type <> 'PROG' and
     c                             c2type <> 'FELT'
     c                   eval      *in32 = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        c2text = *blank
     c                   eval      *in31 = *on
     c                   goto      c2tagb
     c                   endif
      *
      * Oppdatering av gruppe-tekster
      *
     c                   clear                   iadrlur
      *
     c                   eval      iadrlu_rut  = c1ruti
     c     iadrlu_key    chain     iadrlur                            60
      *
 001 c                   eval      iadrty = c2type
 001 c                   eval      iadrtx = c2text
4.61 c                   eval      iadfrm = w_frm
 001 C                   eval      iadrut = c1ruti
      *
     c                   if        *in60 = *off
     c                   update    iadrlur
     c                   else
     c                   eval      iadfrm = w_frm
     c                   eval      iadrut = iadrlr_rut
     c                   write     iadrlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = '2'
     c                   eval      b1text = iadrtx
     c                   eval      b1type = iadrty
     c                   endif
      *
     c                   if        b1valg = '1' or b2valg = '1' or
     c                             b1valg = '3' or b2valg = '3'
     c                   eval      w_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
 001 C                   eval      c2type = *blank
 001 C                   eval      c2text = *blank
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      iadrlr_rut  = d1ruti
     c     iadrlr_key    chain     iadrlr                             60
      *
 001 C                   eval      d1ruti = iadrut
 001 C*                  eval      d1type = iadrty
 001 C                   eval      d1text = iadrtx
      *
     c                   write     s1win
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *  Sjekk om denne rutina har tilganger og ? om disse også skal slettes
     c                   eval      iadtl2_frm  = w_frm
     c                   eval      iadtl2_rut  = d1ruti
     c     iadtl2_key    setll     iadtl2
     c     iadtl2_key    reade     iadtl2
     c                   if        not %eof(iadtl2)
      *
 001 c                   eval      d2ruti = d1ruti
 001 c                   eval      d2text = d1text
      *
     c                   write     s1win
     c                   exfmt     d2win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   endif
      *
     c                   eval      w_forn = *on
      *
      * Sletter recordformat
      *
     c                   eval      iadrlu_rut  = d1ruti
     c     iadrlu_key    chain     iadrlur                            60
5.51 c                   delete    iadrlur
      *
      * Sletter ev. tilgangslinjer
      *
     c                   dow       not %eof(iadtl2)
     c                   eval      iadtru = iadtru
     c                   eval      iadtko = iadtko
     c                   delete    iadtl2r
5.51 c     iadtl2_key    reade     iadtl2
     c                   enddo
      *
     c     end_d1win     endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      w_rut  = k1ruti
      *
      * Fyller kopierings-bildet
      *
     c                   eval      iadrlr_rut  = k1ruti
     c     iadrlr_key    chain     iadrlr                             60
 001 C                   eval      k2ruti = iadrut
 001 C*                  eval      k2type = iadrty
 001 C                   eval      k2text = iadrtx
      *
     c                   write     s1win
      *
     c     k1tagb        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1ruti = *blank
     c                   eval      *in32 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      iadrlr_rut  = k1ruti
     c     iadrlr_key    chain     iadrlr                             60
     c                   if        *in60 = *off
     c                   eval      *in31 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1ruti = w_rut
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *   Del opp frisøk i inntil 3 ord
     c                   if        b2text <> *blank
     c     ' '           scan      b2text        i
     c                   if        i > 0
     c                   eval      scan1 = %subst(b2text:1:i-1)
     c                   eval      l1 = i - 1
     c                   eval      j = i + 1
     c     ' '           scan      b2text:j      i
     c                   if        i > 0
     c                   eval      scan2 = %subst(b2text:j:i)
     c                   eval      l2 = i - j
     c                   eval      j = i + 1
     c     ' '           scan      b2text:j      i
     c                   if        i > 0
     c                   eval      scan3 = %subst(b2text:j:i)
     c                   eval      l3 = i - j
     c                   endif
     c                   endif
     c                   endif
     c                   else
     c                   eval      scan1 = *blank
     c                   eval      scan2 = *blank
     c                   eval      scan3 = *blank
     c                   endif
     c     lo:up         xlate     scan1         scan1
     c     lo:up         xlate     scan2         scan2
     c     lo:up         xlate     scan3         scan3
      *
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   read      iadrl3                                 15
     c                   when      w_seqe = 'B'
     c                   read      iadrl2                                 15
     c                   other
     c                   read      iadrl1                                 15
     c                   endsl
      *
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
     c                   if        iadfrm <> w_frm
     c                   goto      end_crt
     c                   endif
      *
      *  Utvalgte Friteskt?
     c                   if        scan1 <> *blank
     c     lo:up         xlate     iadrtx        wtekst
     c     scan1:l1      scan      wtekst        i
     c                   if        not %found
     c                   iter
     c                   endif
     c                   endif
     c                   if        scan2 <> *blank
     c     scan2:l2      scan      wtekst        i
     c                   if        not %found
     c                   iter
     c                   endif
     c                   endif
     c                   if        scan3 <> *blank
     c     scan3:l3      scan      wtekst        i
     c                   if        not %found
     c                   iter
     c                   endif
     c                   endif
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = *blank
     c                   eval      b1ruti = iadrut
     c                   eval      b1type = iadrty
     c                   eval      b1text = iadrtx
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      wvisrn = w_sfrn
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på recordformat
      *
     c     iadrl1_key    klist
     c                   kfld                    iadrl1_frm
     c                   kfld                    iadrl1_rut
      *
      * Key for posisjonering på beskrivelse
      *
     c     iadrl2_key    klist
     c                   kfld                    iadrl2_frm
     c                   kfld                    iadrl2_rtx
      *
      * Key for posisjonering på type
      *
     c     iadrl3_key    klist
     c                   kfld                    iadrl3_frm
     c                   kfld                    iadrl3_rty
     c                   kfld                    iadrl3_rut
      *
      * Key for les på rcordformat
      *
     c     iadrlr_key    klist
     c                   kfld                    iadrlr_frm
     c                   kfld                    iadrlr_rut
      *
      * Key for oppdatering av recordformat
      *
     c     iadrlu_key    klist
     c                   kfld                    iadrlu_frm
     c                   kfld                    iadrlu_rut
      *
      * Key for sjekk om rutine har tilgangslinjer
      *
     c     iadtl2_key    klist
     c                   kfld                    iadtl2_frm
     c                   kfld                    iadtl2_rut
      *
      *
      * Henter firmakode fra LDA
      *
4.50 c*                  eval      w_frm  = *zero
     c                   eval      *in22 = *on
      *
      * Sjekk om denne bruker har tilgang til denne rutina
ad    *
ad   c                   eval      w_adv    = *blank
ad   c                   eval      w_tilgko = l_user
ad   c                   eval      w_rutine = 'ADGANG'
ad   c                   eval      w_status = *blank
ad   c                   call      'AD005R'
ad   c                   parm                    w_tilgko
ad   c                   parm                    w_rutine
ad   c                   parm                    w_adv
ad   c                   parm                    w_status
ad   c                   if        w_status = 'Sperret'
ad   c                   goto      avslutt
ad   c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      iadrl1_frm  = w_frm
     c                   eval      iadrl2_frm  = w_frm
     c                   eval      iadrl3_frm  = w_frm
     c                   eval      iadrlr_frm  = w_frm
     c                   eval      iadrlu_frm  = w_frm
     c                   eval      iadrl1_rut  = *blank
     c     iadrl1_key    setll     iadrl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
**
  Ny post                  001
  Endring                  002
  Vise                     003
