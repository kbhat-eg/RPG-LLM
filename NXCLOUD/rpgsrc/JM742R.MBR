     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JM740R
      * Beskrivelse . . : Kampanje-vare, oppdater fra csv format
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       250311 bof  Nytt program
6.21  *       131013 bof  Justert slik at den takler om tildato er siste kolonne
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fpcintpf   if   e           k disk
      *
     fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
     fjkahlu    uf a e           k disk    rename(jkahpfr:jkahlur)
     fjkavlu    uf a e           k disk    rename(jkavpfr:jkavlur)
      *
      * Definering av Local Data Area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Datastrukturer
      *
     d                 ds
     d d_vrec                       300
     d  d_n_nobb                      8  0 overlay(d_vrec:3)
     d  d_ldor                        6    overlay(d_vrec:22)
     d  d_v_vldo                      6  0 overlay(d_ldor:1)
     d  d_v_fil2                     94    overlay(d_vrec:28)
     d  d_v_vkai                      9  2 overlay(d_vrec:122)
     d  d_v_fil3                      1    overlay(d_vrec:131)
     d  d_v_ifda                      8  0 overlay(d_vrec:132)
     d  d_v_fil4                      1    overlay(d_vrec:140)
     d  d_v_itda                      8  0 overlay(d_vrec:141)
     d  d_v_fil5                      1    overlay(d_vrec:149)
     d  d_v_kpri                      9  2 overlay(d_vrec:150)
     d  d_v_fil6                      1    overlay(d_vrec:159)
     d  d_v_kfda                      8  0 overlay(d_vrec:160)
     d  d_v_fil7                      1    overlay(d_vrec:168)
     d  d_v_ktda                      8  0 overlay(d_vrec:169)
     d  d_v_fil8                      1    overlay(d_vrec:177)
     d  d_v_vkaa                      9  2 overlay(d_vrec:178)
     d  d_v_fil9                      1    overlay(d_vrec:187)
     d  d_v_efda                      8  0 overlay(d_vrec:188)
     d  d_v_fil10                     1    overlay(d_vrec:196)
     d  d_v_etda                      8  0 overlay(d_vrec:197)
     d  d_inpr                       10    overlay(d_vrec:210)
     d   d_inpr_kr                    7  0 overlay(d_inpr:1)
     d   d_inpr_Øre                   2  2 overlay(d_inpr:9)
     d  d_sapr                       10    overlay(d_vrec:220)
     d   d_sapr_kr                    7  0 overlay(d_sapr:1)
     d   d_sapr_Øre                   2  2 overlay(d_sapr:9)
     d  d_fdat                       10    overlay(d_vrec:230)
     d  d_tdat                       10    overlay(d_vrec:240)
     d  d_vare                        8    overlay(d_vrec:250)
     d                 ds
     d d_dato                        10
     d  d_dd                          2    overlay(d_dato:1)
     d  d_mm                          2    overlay(d_dato:4)
     d  d_aa                          2    overlay(d_dato:9)
     d                 ds
     d w_dat6                         6
     d  w_dd                          2    overlay(w_dat6:1)
     d  w_mm                          2    overlay(w_dat6:3)
     d  w_aa                          2    overlay(w_dat6:5)
      * Definering av variable i nøkler
      *
     d jkahlu_kamp     s                   like(jmkamp)
     d jkavlu_vkam     s                   like(jmvkam)
     d jvarl3_nobb     s                   like(jvnobb)
      *
      * Definering av variable
      *
     d w_rect          s              1
     d w_dato          s              6  0
     d w_fdat          s                   like(jmfdat)
     d w_tdat          s                   like(jmfdat)
     d len             s              3  0
     d w_leng          s              4  0
     d w_posi          s              4  0
     d st              s              3  0
     d w_6x            s              6
      *
     d w_firm          s                   like(jmfirm)
      *
     d b_nobb          s              1    inz(*off)
     d b_peri          s              1    inz(*off)
     d b_ok            s              1    inz(*off)
      *
     d c_digit         c                   ' 0123456789'
     d c_null          s             15    inz('000000000000000')
      *
     d p_kamp          s                   like(jmkamp)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   exsr      hode
      * Leser kampanjefil
      *
     c                   read      pcintpf
     c                   dow       not %eof
      *
     c                   eval      d_vrec = *blank
     c                   exsr      utled_ds
      *
     c                   if        b_ok = *on
     c                   exsr      nobb
     c                   if        b_nobb = *on
     c                   exsr      varelinje
     c                   endif
     c                   eval      b_nobb = *off
     c                   endif
      *
     c                   read      pcintpf
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av hoderecord
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode          begsr
      *
      * Sletter eventuell eksisterende kampanje på varenivå
      *
     c                   eval      jkahlu_kamp = p_kamp
     c     jkahlu_key    chain     jkahlur
     c                   if        %found
      * hvis kampanje finnes fra før så slettes detaljene
     c                   eval      jkavlu_vkam = p_kamp
     c     jkavlu_key    setll     jkavlu
     c     jkavlu_key    reade     jkavlur
     c                   dow       not %eof
     c                   delete    jkavlur
     c     jkavlu_key    reade     jkavlur
     c                   enddo
     c                   else
      *
      * Hvis kampanje ikke finnes fra før, så opprettes heading
      *
     c                   clear                   jkahlur
      *
     c                   eval      jmfirm = w_firm
     c                   eval      jmkamp = p_kamp
     c                   eval      jmbesk = 'Automatisk opprettet'
     c                   eval      jmansv = *blank
     c                   eval      jmodat = *loval
     c                   eval      jmeusr = l_user
      *
     c                   eval      jmfdat = *loval
     c                   eval      jmtdat = *loval
      *
     c                   time                    jmedat
     c                   time                    jmetim
      *
     c                   write     jkahlur
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av varelinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     nobb          begsr
      *
      *
     c                   eval      jvarl3_nobb = d_n_nobb
     c     jvarl3_key    chain     jvarl3
     c                   if        not %found
     c                   movel     d_n_nobb      jvvare
     c                   endif
      *
     c                   eval      b_nobb = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av varelinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     varelinje     begsr
      *
     c                   if        b_peri = *off
     c                   exsr      upd_kamp
     c                   endif
      *
      * Finner varenr på bakgrunn av nobbnr
      *
      * Skriver til kampanje-vare
      *
     c                   clear                   jkavlur
      *
     c                   eval      jmvfir = w_firm
     c                   eval      jmvkam = p_kamp
     c                   eval      jmvvar = jvvare
     c                   eval      jmvldo = d_v_vldo
     c                   eval      jmvkai = d_inpr_kr + d_inpr_Øre
     c                   eval      jmvkpr = d_inpr_kr + d_inpr_Øre
     c                   eval      jmvkaa = d_sapr_kr + d_sapr_Øre
     c                   eval      jmvkau = 0
     c                   eval      jmvans = *blank
     c                   eval      jmveus = l_user
      *
     c                   time                    jmveda
     c                   time                    jmveti
      *
     c                   write     jkavlur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å oppdatere kampanje hode med fra - til dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     upd_kamp      begsr
      *
      *
     c                   eval      jkahlu_kamp = p_kamp
     c     jkahlu_key    chain     jkahlur
     c                   if        %found(jkahlu)
     c                   eval      jmfdat   = w_fdat
     c                   eval      jmtdat   = w_tdat
     c                   update    jkahlur
     c                   eval      b_peri = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utleding av datastruktur
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utled_ds      begsr
      *
     c                   eval      b_ok = *off
     c     '"':' '       xlate     pcint         pcint
      *
      * Ubrukte kolonner
     c                   eval      w_posi = %scan(';':pcint)
     c                   eval      pcint  = %subst(pcint:w_posi+1:298-w_posi)
     c                   eval      w_posi = %scan(';':pcint)
      * nobb lev.nr
     c                   eval      pcint  = %subst(pcint:w_posi+1:298-w_posi)
     c                   eval      w_posi = %scan(';':pcint)
     c                   eval      d_ldor = %trim(%subst(pcint:1:w_posi-1))
     c                   if        d_ldor = *blank
     c                   leavesr
     c                   endif
     c                   eval      len     = %len(%trim(d_ldor))
     c                   if        len     > 0
     c                   eval      st = 7 - len
     c                   eval      w_6x  = *blank
     c                   eval      %subst(w_6x:st:len) = %subst(d_ldor:1:len)
     c                   eval      d_ldor = w_6x
     c                   endif
     c                   eval      d_ldor = %xlate(' ':'0':d_ldor)
      *
      *
     c                   if        %check(c_digit: d_ldor) <>0
     c                   leavesr
     c                   endif
      * nobb-nr
     c                   eval      pcint  = %subst(pcint:w_posi+1:298-w_posi)
     c                   eval      w_posi = %scan(';':pcint)
     c                   eval      d_vare = %trim(%subst(pcint:1:w_posi-1))
     c                   if        d_vare = *blank
     c                   leavesr
     c                   endif
      *
     c                   if        %check(c_digit: d_vare) <>0
     c                   leavesr
     c                   endif
      *
     c                   if        %len(%trim(d_vare)) <>  8
     c                   leavesr
     c                   endif
     c                   move      d_vare        d_n_nobb
      *
      * Innpris
      *
     c                   eval      pcint  = %subst(pcint :w_posi+1:298-w_posi)
     c                   eval      w_posi = %scan(';':pcint )
     c                   eval      d_inpr= %trim(%subst(pcint :1:w_posi-1))
     c                   if        d_inpr = *blank
     c                   leavesr
     c                   endif
      *
      * ubrukt kolonne
      *
     c                   eval      pcint  = %subst(pcint :w_posi+1:298-w_posi)
     c                   eval      w_posi = %scan(';':pcint )
     c
      *
      * Kampanjepris u.mva
      *
     c                   eval      pcint  = %subst(pcint :w_posi+1:298-w_posi)
     c                   eval      w_posi = %scan(';':pcint )
     c                   eval      d_sapr = %trim(%subst(pcint :1:w_posi-1))
     c                   if        d_sapr = *blank
     c                   leavesr
     c                   endif
      *
      * ubrukt kolonne
      *
     c                   eval      pcint  = %subst(pcint :w_posi+1:298-w_posi)
     c                   eval      w_posi = %scan(';':pcint )
      *
     c                   eval      pcint  = %subst(pcint :w_posi+1:298-w_posi)
     c                   eval      w_posi = %scan(';':pcint )
      *
     c                   eval      pcint  = %subst(pcint :w_posi+1:298-w_posi)
     c                   eval      w_posi = %scan(';':pcint )
      *
     c                   eval      pcint  = %subst(pcint :w_posi+1:298-w_posi)
     c                   eval      w_posi = %scan(';':pcint )
      *
      * Fra dato
      *
     c                   eval      pcint  = %subst(pcint :w_posi+1:298-w_posi)
     c                   eval      w_posi = %scan(';':pcint )
     c                   eval      d_fdat = %trim(%subst(pcint :1:w_posi-1))
     c                   eval      d_dato = d_fdat
     c                   eval      w_dd = d_dd
     c                   eval      w_mm = d_mm
     c                   eval      w_aa = d_aa
     c     c_digit       check     w_dat6                                 70
     c                   if        *in70 = *on
     c                   leavesr
     c                   endif
     c                   move      w_dat6        w_dato
     c     *dmy          test(d)                 w_dato                 32
     c                   if        *in32 = *on
     c                   leavesr
     c                   endif
     c     *dmy          move      w_dato        w_fdat
      *
      * til dato
      *
     c                   eval      pcint  = %subst(pcint :w_posi+1:298-w_posi)
     c                   eval      w_posi = %scan(';':pcint )
6.21 c                   if        w_posi > 0
     c                   eval      d_tdat = %trim(%subst(pcint :1:w_posi-1))
6.21 c                   else
6.21 c                   eval      d_tdat = %trim(%subst(pcint :1:10))
6.21 c                   endif
     c                   eval      d_dato = d_tdat
     c                   eval      w_dd = d_dd
     c                   eval      w_mm = d_mm
     c                   eval      w_aa = d_aa
     c     c_digit       check     w_dat6                                 70
     c                   if        *in70 = *on
     c                   leavesr
     c                   endif
     c                   move      w_dat6        w_dato
     c     *dmy          test(d)                 w_dato                 32
     c                   if        *in32 = *on
     c                   leavesr
     c                   endif
     c     *dmy          move      w_dato        w_tdat
      *
      *
     c                   eval      b_ok = *on
      *
      * Utleder innkjøps
      *
     c                   if        d_inpr <> *blank
      *
     c                   eval      d_inpr = %trim(d_inpr)
     c                   eval      w_posi = %scan(',':d_inpr)
     c                   eval      w_leng = %len(%trim(d_inpr))
      *
     c                   if        w_posi = 0
     c                   eval      w_posi = w_leng + 1
     c                   eval      d_inpr = %trim(d_inpr) + '.00'
     c                   endif
      *
     c     ' ':'0'       xlate     d_inpr        d_inpr
      *
     c                   if        (w_posi-1) < 7
     c                   eval      d_inpr = %subst(c_null:1:7-w_posi+1) +
     c                                      %subst(d_inpr:1:w_posi-1) +
     c                                      '.' +
     c                                      %subst(d_inpr:w_posi+1:2)
     c                   endif
      *
     c                   else
     c                   eval      d_inpr = '0000000.00'
     c                   endif
      *
      * Utleder salgspris
      *
     c                   if        d_sapr <> *blank
      *
     c                   eval      d_sapr = %trim(d_sapr)
     c                   eval      w_posi = %scan(',':d_sapr)
     c                   eval      w_leng = %len(%trim(d_sapr))
      *
     c                   if        w_posi = 0
     c                   eval      w_posi = w_leng + 1
     c                   eval      d_sapr = %trim(d_sapr) + '.00'
     c                   endif
      *
     c     ' ':'0'       xlate     d_sapr        d_sapr
      *
     c                   if        (w_posi-1) < 7
     c                   eval      d_sapr = %subst(c_null:1:7-w_posi+1) +
     c                                      %subst(d_sapr:1:w_posi-1) +
     c                                      '.' +
     c                                      %subst(d_sapr:w_posi+1:2)
     c                   endif
      *
     c                   else
     c                   eval      d_sapr = '0000000.00'
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å initiere
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *
     c     *entry        plist
     c                   parm                    p_kamp
      *
      * Key for oppslag på vare
      *
     c     jvarl3_key    klist
     c                   kfld                    jvarl3_nobb
      *
      * Key for oppdatering av kampanje-hode
      *
     c     jkahlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jkahlu_kamp
      *
      * Key for sletting av kampanje-vare
      *
     c     jkavlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jkavlu_vkam
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
