     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : XP990R
      * Beskrivelse . . : Fellesprogram for ordrebehandling 2DPOINT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.
      * --------- ------  --------------------------------------------------
      *           300308  Nytt program                             ASK
 6.11 * 6.10      110610  Justert i utvidelse av parm VL001R       BOF
 6.12 * 6.10      071010  Lagt inn nye felter til VL001R           BOF
 6.13 * 6.10      070311  Lagt inn merking "Lager oppdatert"       ASK
6.20  * R6.20 140112 bof  Mulighet til å legge ut ByggDok innsamling
6.21  * R6.20 281112 amd  Korrigert feilsituasjon ved update before
6.21  *                   read.
6.22  * R6.20 040113 bof  Henter byggdok - kode fra FKPRPF
6.24  * R6.20 050115 bof  Legger inn regnskapsprosjekt og evt. mvakode
6.25  * R6.20 030815 bsa  Sjekker mot om det er krav til leveringsmåte
6.25x * R6.20 130116 bof  Legger inn koder vedr. fakt.geb. fra kunde
6.nu  * R6.30 030614 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.30  * 6.30  221014 BKV  Søtte for cobuilder
6.31  * 6.30  040615 bkv  Ordre økt fra 6 til 8
6.32  * 6.30  290616 bof  Rettelse vedr. Cobuilder
6.33  * 6.30  050716 bof  Byggdok/Cob. skal evt. ta mailadr. fra kundeprosjekt
6.34  * 6.30  220916 bsa  Sjekker om kunden har kort, og legger inn info
6.34  *                   om kort, og sperrekode på ordren.
6.35  * 6.30  280317 bkv  Fjernet automatisk bestilling
6.36  * 6.30  061017 bof  Sjekke berikelse fra kunde om det ligger på prosj.
      *
7.01  * 6.30  090119 bsa  Legger ut tabeller for å logge priser osv
7.01  *                   Må testes fra 2DP.
7.01s *                   Justert kallav VP900R
7.02  * 6.30  180619 sst  Ordretype = PP(FAAOT6) fra Håndholdt v/prosjektkunde
      *
8.01  * 800   070322 ask  Snur fortegn på ordrelinjer ved negativ internordre
8.02  *PRG2   140622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
8.04  *       140524 ask  Fjernet skriv til ByggDok
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffohelu    uf   e           k disk    rename(fohepfr:fohelur)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffodtlu    uf   e           k disk    rename(fodtpfr:fodtlur)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
6.13 fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
6.20 fafpslr    if   e           k disk    rename(afpspfr:afpslrr)
6.22 ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
6.20 famodl1    if   e           k disk    rename(amodpfr:amodl1r)
6.20 frukpl6    if   e           k disk    rename(rukppfr:rukpl6r)
6.20 frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
6.25 favall1    if   e           k disk    rename(avalpfr:avall1r)
6.34 frukrlr    if   e           k disk    rename(rukrpfr:rukrlrr)
6.34 fruksl1    if   e           k disk    rename(rukspfr:ruksl1r)
7.01 ffodmiu    uf a e           k disk    rename(fodmst:fodmiur)
7.01 f                                     prefix(xx:2)
6.20 ffbdopf    o  a e           k disk
7.02 ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
      *
      * Local Data Area
      *
     d                uds
     d l_wsid                901    910
     d l_user                        10
6.20 d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
7.01  *
7.01  * Definering av datastrukturer
7.01  *
7.01  * Parametre for henting av pris -inn
7.01  *
7.01 d                 ds
7.01 d hirec                        120
7.01 d  hifirm                        3  0 overlay(hirec)
7.01 d  hirkat                        3  0 overlay(hirec:4)
7.01 d  hikund                        6  0 overlay(hirec:7)
7.01 d  hikpro                        6  0 overlay(hirec:13)
7.01 d  hivare                       15    overlay(hirec:19)
7.01 d  hilety                        1  0 overlay(hirec:34)
7.01 d  hienhe                        3    overlay(hirec:35)
7.01 d  hidato                         d   overlay(hirec:38) datfmt(*iso)
7.01 d  hitime                         t   overlay(hirec:48) timfmt(*iso)
7.01 d  hinumm                        8  0 overlay(hirec:56)
7.01 d  hikode                        1    overlay(hirec:64)
7.01 d  hidekn                        5  2 overlay(hirec:65)
7.01 d  hiavgf                        1  0 overlay(hirec:70)
7.01 d  hipriv                        1  0 overlay(hirec:71)
7.01 d  hitilb                        1    overlay(hirec:72)
7.01 d  hiskaf                        1    overlay(hirec:73)
7.01 d  hildor                        6  0 overlay(hirec:74)
7.01 d  hisapr                        9  2 overlay(hirec:80)
7.01 d  hikopr                        9  2 overlay(hirec:89)
8.02 d  hiprgr                        2    overlay(hirec:98)
7.01 d  hiinlr                        1    overlay(hirec:120)
7.01  *
7.01  * Parametre for henting av pris -ut
7.01  *
7.01 d                 ds
7.01 d horec                         80
7.01 d  holety                        1  0 overlay(horec)
7.01 d  hokpri                        1    overlay(horec:2)
7.01 d  hooppr                        9  2 overlay(horec:3)
7.01 d  hosapr                        9  2 overlay(horec:12)
7.01 d  hokopr                        9  2 overlay(horec:21)
7.01 d  horab1                        5  2 overlay(horec:30)
7.01 d  horab2                        5  2 overlay(horec:35)
7.01 d  hodekn                        5  2 overlay(horec:40)
7.01 d  hopros                        5  2 overlay(horec:45)
7.01 d  hokamp                        5    overlay(horec:50)
7.01 d  hoalme                        4  0 overlay(horec:55)
7.01 d  hobrty                        1  0 overlay(horec:59)
7.01 d  hobrr1                        5  2 overlay(horec:60)
7.01 d  hobrr2                        5  2 overlay(horec:65)
7.01 d  hoomva                        1    overlay(horec:70)
8.02 d  hoprgr                        2    overlay(horec:71)
      *
      * Datastruktur for lageroppdatering
      *
     d                 ds
6.11 d hlrec                        128
     d  hlvare                       15    overlay(hlrec)
     d  hllage                        2  0 overlay(hlrec:16)
     d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
     d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
     d  hlotyp                        2    overlay(hlrec:36)
     d  hlltyp                        2    overlay(hlrec:38)
     d  hlanta                       11  3 overlay(hlrec:40)
     d  hlkopr                        9  2 overlay(hlrec:51)
6.11 d  hlrefr                       20    overlay(hlrec:60)
6.11 d  hlsyst                        1    overlay(hlrec:80)
6.11 d  hlaltk                        2    overlay(hlrec:81)
6.11 d  hlrest                        1    overlay(hlrec:83)
6.11 d  hltype                        1    overlay(hlrec:84)
6.11 d  hlenhe                        3    overlay(hlrec:85)
6.11 d  hlinlr                        1    overlay(hlrec:88)
6.11 d  hllety                        1    overlay(hlrec:89)
6.11 d  hlonum                        8  0 overlay(hlrec:90)
6.11 d  hlolin                        4  0 overlay(hlrec:98)
      *
      * Definering av variable i nøkler
      *
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fohelu_numm     s                   like(fonumm)
     d fohelu_suff     s                   like(fosuff)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d fodtlu_numm     s                   like(fdnumm)
     d fodtlu_suff     s                   like(fdsuff)
     d vvarl1_vare     s                   like(fdvare)
6.13 d votyl1_otyp     s                   like(vaotyp)
6.22 d fkprl1_kund     s                   like(flkund)
6.22 d fkprl1_kpro     s                   like(flkpro)
6.20 d amodl1_modu     s                   like(axmodu)
6.20 d afpslr_ufil     s                   like(l_filg)
6.20 d afpslr_uide     s                   like(afuide)
6.20 d rkunl1_kund     s                   like(rkkund)
6.20 d rukpl6_kund     s                   like(rukund)
6.25 d avall1_apgm     s                   like(avapgm)
6.34 d rukrlr_kkun     s                   like(rukkun)
6.34 d ruksl1_skty     s                   like(ruskty)
6.34 d ruksl1_slag     s                   like(ruslag)
7.01 d fodmiu_numm     s                   like(fdnumm)
7.01 d fodmiu_suff     s                   like(fdsuff)
7.01 d fodmiu_line     s                   like(fdline)
      *
      *  Parameter-variable
      *
     d p_firm          s              3  0
6.nu d p_numm          s              8  0
     d p_suff          s              2  0
     d p_stat          s              1
7.01 d p_inlr          s              1
      *
      * Definering av variable
      *
     d w_firm          s                   like(p_firm)
     d w_numm          s                   like(fonumm)
     d w_suff          s                   like(fosuff)
     d w_stat          s              1
     d w_totr          s                   like(fdsumr)
     d w_orab          s                   like(fdsumr)
     d w_rab1          s                   like(fdsumr)
     d w_rab2          s                   like(fdsumr)
     d w_tots          s                   like(fdsums)
     d w_totk          s                   like(fdsumk)
     d w_sald          s                   like(fdsums)
     d w_sal2          s                   like(fdsums)
6.31 d w_numa          s              8
6.12 d w_lina          s              4
6.20 d w_btms          s               Z
6.20 d w_ti26          s             26
6.20 d w_bnum          s              6
6.25 d w_retk          s              1
6.25 d w_qtxt          s             20
6.34 d w_ityp          s              2
6.34 d w_psuf          s              2  0
7.01 d w_time          s               t   timfmt(*iso)
7.01 d w_prgr          s                   like(fdprgr)
7.01 d w_line          s                   like(fdline)
7.01 d w_rabt          s             15  4
     d w_rut           s             50
8.01 d w_okre          s                   like(vaokre)
8.01 d w_enor          s              1    inz(' ')
      *
     d b_feil          s              1    inz(*off)
     d b_init          s              1    inz(*on)
     d b_best          s              1    inz(*off)
6.20 d b_bdo1          s              1    inz(*off)
6.20 d b_bdo2          s              1    inz(*off)
6.20 d b_bdo3          s              1    inz(*off)
6.20 d b_bdo4          s              1    inz(*off)
6.20 d b_kper          s              1    inz(*off)
8.01 d b_into          s              1    inz(*off)
8.04 d b_bygd          s              1    inz(*off)
6.20  *
6.25 d u_lmat          s              1    inz(*off)
      *
7.01 d s_osap          s                   like(xxosap)
7.01 d s_otip          s                   like(xxotip)
7.01 d s_okop          s                   like(xxokop)
7.01 d s_oinp          s                   like(xxoinp)
7.01 d s_oppk          s                   like(xxoppk)
7.01 d s_okpr          s                   like(xxokpr)
7.01 d s_okr1          s                   like(xxokr1)
7.01 d s_okr2          s                   like(xxokr2)
7.01 d s_okpk          s                   like(xxokpk)
      *
7.01 d x_sapr          s                   like(fdsapr)
7.01 d x_bupr          s                   like(fdsapr)
7.01 d x_tipr          s                   like(fdsapr)
7.01 d x_kopr          s                   like(fdsapr)
7.02 d x_inpr          s                   like(fdsapr)
6.20  *
6.20  * Definering av konstanter
6.20  *
6.20 d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
6.20 d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedbehandling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      w_rut = 'Start XP990R'
     c**                 call      'DEBUGCL'
     c**                 parm                    w_rut
      *
      * Sjekker om gyldig ordre hode
      *
     c                   eval      fohel1_numm = p_numm
     c                   eval      fohel1_suff = p_suff
     c     fohel1_key    chain     fohel1r
     c                   if        not %found
     c                   eval      p_stat = '1'
     c                   goto      end_pgm
     c                   endif
7.01  *
7.01  * Hent prigruppe
7.01  *
7.01 c                   exsr      hent_prisgr
      *
6.13  * Sjekker ordretype
6.13  *
6.13 c                   eval      votyl1_otyp = footyp
6.13 c     votyl1_key    chain     votyl1r
8.01 c                   eval      w_okre = vaokre
6.20  *
6.20  * Sjekker om kundeprosjekt skal legge ut ByggDok
6.20  *
6.20 c                   eval      b_bdo2 = *off
6.20 c                   eval      b_bdo4 = *off
6.20 c                   if        b_bdo1 = *on
6.22 c                   eval      fkprl1_kund = fokund
6.22 c                   eval      fkprl1_kpro = fokpro
6.22 c     fkprl1_key    chain     fkprl1r
6.32 c                   if        %found(fkprl1)
6.32 c                   if        flbdok = 1 or
6.32 c                             flcobu = 1
6.20 c                   time                    w_btms
6.20 c                   eval      b_bdo2 = *on
6.32 c                   endif
6.20 c                   endif
6.20 c                   eval      b_bdo3 = *on
6.20 c                   endif
6.24  *
6.24  * Leser kunde her for å bruke info videre
6.24 c                   eval      rkunl1_kund = fokund
6.24 c     rkunl1_key    chain     rkunl1
      *
      * Behandling i subrutiner
      *
     c                   exsr      lager
     c*                  exsr      bestill
     c                   exsr      status
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppdaterer lager fra ordrelinjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lager         begsr
      *
     c                   eval      fodtl1_numm = p_numm
     c                   eval      fodtl1_suff = p_suff
     c     fodtl1_key    setll     fodtl1
     c     fodtl1_key    reade     fodtl1
     c                   dow       not %eof
      *
      * Legger over verdier i overførings-felter
      *
     c                   move      fdvare        hlvare
5.31 c                   move      fdlage        hllage
     c                   move      fdenh1        hlenhe
     c                   move      *loval        hldato
     c                   move      *loval        hltime
     c                   move      footyp        hlotyp
     c                   move      fdltyp        hlltyp
      *
     c                   eval      hlanta = fdanta
     c                   eval      hlkopr = fdkopr
      *
     c                   eval      hlrefr = *blank
     c                   move      'F'           hlsyst
     c                   eval      hlaltk = *blank
     c                   eval      hlrest = *blank
      *
6.12 c*                  movel     fdnumm        hlrefr
6.12 c*                  move      fdline        hlrefr
6.12 c*                  move      fdlety        hllety
6.12  *
6.12 c                   move      fdnumm        w_numa
6.12 c                   move      fdline        w_lina
6.12 c                   eval      hlrefr = 'O:' + w_numa +
6.12 c                                      ' L:' + w_lina
6.12 c                   eval      hlonum = fdnumm
6.12 c                   eval      hlolin = fdline
6.12 c                   move      fdlety        hllety
      *
      * Kaller opp lageroppdaterings-program
      *
     c                   if        fdlvre = 0
     c                   eval      w_stat = *blank
      *
     c                   call      'VL001R'
     c                   parm                    w_stat
     c                   parm                    hlrec
     c                   endif
6.20  * Oppdaterer evt. byggdok
6.20  *
6 2x c                   if        vaoakk = 2 and
6 2x c                             vaokre = *blank
6.20 c                   exsr      byggdok
6.20 c                   endif
      *
     c     fodtl1_key    reade     fodtl1
     c                   enddo
      *
     c     end_lager     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekker om bestillingsbehov og genererer bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.35 c*     bestill       begsr
6.35  **
6.35  ** Setter b_best=1 dersom ordren inneholder varer som må bestilles
6.35  **
6.35 c*                   call      'FO743R'
6.35 c*                   parm                    w_firm
6.nu c*                   parm                    p_numm
6.35 c*                   parm                    p_suff
6.35 c*                   parm                    b_best
6.35  **
6.35  ** Dersom b_best=1 startes program for automatisk bestilling
6.35  **
6.35 c*                   if        b_best = *on
6.35 c*                   call      'FO745R'
6.35 c*                   parm                    w_firm
6.nu c*                   parm                    p_numm
6.35 c*                   parm                    p_suff
6.35 c*                   parm                    w_stat
6.35 c*                   endif
6.35  **
6.35 c*     end_bestill   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppdaterer status på ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     status        begsr
     c                   eval      w_rut = 'Start Status'
     c**                 call      'DEBUGCL'
     c**                 parm                    w_rut
      *
      * Henter ordretotal
      *
     c                   eval      w_tots = 0
     c                   eval      w_totr = 0
     c                   eval      w_totk = 0
      *
     c                   eval      fodtlu_numm = fonumm
     c                   eval      fodtlu_suff = fosuff
     c     fodtlu_key    setll     fodtlu
     c     fodtlu_key    reade     fodtlu
     c                   dow       not %eof
      *
      * Rekalkulerer linjerabatt totaler
      *
     c                   eval(h)   w_rab1 = (fdsums * fdrab1) / 100
     c                   eval(h)   w_rab2 = ((fdsums - w_rab1)
     c                                        * fdrab2) / 100
      *
      * Sjekker om vare skal inngå i ordrerabatt
      *
     c                   eval      vvarl1_vare = fdvare
     c     vvarl1_key    chain     vvarl1r
     c                   if        vvkord = 0
     c                   eval(h)   w_orab = ((fdsums - w_rab1 - w_rab2)
     c                                        * forabp) / 100
     c                   else
     c                   eval      w_orab = 0
     c                   endif
      *
     c                   eval      fdsumr = w_rab1 + w_rab2 + w_orab
     c                   update    fodtlur
      *
     c                   eval      w_tots = w_tots + fdsums
     c                   eval      w_totr = w_totr + fdsumr
     c                   eval      w_totk = w_totk + fdsumk
7.01  *
7.01  * Oppretter skyggetabell for logging av priser
7.01  *
     c                   eval      w_rut = 'lag_skygge'
     c**                 call      'DEBUGCL'
     c**                 parm                    w_rut
7.01 c                   exsr      lag_skygge
7.01  *
     c     fodtlu_key    reade     fodtlu
     c                   enddo
      *
      * Beregner justeringsbeløp, og oppdaterer memosaldo
      *
     c                   eval      w_sald = w_tots - w_totr
     c                   eval      w_sal2 = 0
      *
     c                   eval      w_rut = 'FA922R'
     c**                 call      'DEBUGCL'
     c**                 parm                    w_rut
     c                   call      'FA922R'
     c                   parm                    w_firm
     c                   parm                    footyp
     c                   parm                    fokund
     c                   parm                    w_sald
     c                   parm                    w_sal2
6.34  *
6.34  * Sjekk om kunden har kreditkort
6.34  *
6.34 c                   eval      w_ityp = *blanks
6.34 c                   eval      w_psuf = 0
6.34 c                   eval      rukrlr_kkun = fokund
6.34 c     rukrlr_key    setll     rukrlr
6.34 c     rukrlr_key    reade     rukrlr
6.34 c                   dow       not %eof
6.34 c                   if        rukobl = 1 and
6.34 c                             w_psuf = 0
6.34 c                   eval      ruksl1_skty = ruktyp
6.34 c                   eval      ruksl1_slag = folage
6.34 c     ruksl1_key    chain     ruksl1
6.34 c                   if        %found   and
6.34 c                             rustyp = 'K'
6.34 c                   eval      w_psuf = ruskty
6.34 c                   eval      w_ityp = rusity
6.34 c                   goto      videre
6.34 c                   else
6.34  * Prøv med lager 0
6.34 c                   eval      ruksl1_slag = 0
6.34 c     ruksl1_key    chain     ruksl1
6.34 c                   if        %found   and
6.34 c                             rustyp = 'K'
6.34 c                   eval      w_psuf = ruskty
6.34 c                   eval      w_ityp = rusity
6.34 c                   goto      videre
6.34 c                   endif
6.34 c                   endif
6.34 c                   endif
6.34 c     rukrlr_key    reade     rukrlr
6.34 c                   enddo
6.34  *
6.34 c     videre        tag
     c                   eval      w_rut = 'Start Videre'
     c**                 call      'DEBUGCL'
     c**                 parm                    w_rut
      *
      * Oppdaterer ordre-hode med ny saldo
      *
     c                   eval      w_rut = 'Start Ordre'
     c**                 call      'DEBUGCL'
     c**                 parm                    w_rut
     c                   eval      fohelu_numm = fonumm
     c                   eval      fohelu_suff = fosuff
     c     fohelu_key    chain     fohelur
      *
6.21 c                   if        %found(fohelu)
     c                   eval      fotots = w_tots
     c                   eval      fototr = w_totr
     c                   eval      fototk = w_totk
6.13 c                   if        vaoakk = 2 or
6.13 c                             vaoakk = 3
6.13 c                   eval      foolag = 1
6.13 c                   endif
6.24 c                   eval      fkprl1_kund = fokund
6.24 c                   eval      fkprl1_kpro = fokpro
6.24 c     fkprl1_key    chain     fkprl1r
6.36 c                   if        %found(fkprl1)
6.36 c                   eval      rkmkod = flmkod
6.36 c                   eval      rkfagb = flfagb
6.36 c                   eval      rkfaty = flfaty
6.36 c                   eval      rkbetb = flbetb
6.36 c                   eval      rkneto = flneto
6.36 c                   if        flproj <> *blank
6.24 c                   eval      foproj = flproj
6.24 c                   endif
6.36 c                   endif
6.36 c                   eval      fobetb = rkbetb
6.36 c                   eval      foneto = rkneto
6.36 c                   eval      fofaty = rkfaty
6.36 c                   eval      fomkod = rkmkod
6.25xc                   eval      fokgfa = rkfagb
6.25xc                   eval      fokgfr = rkfrgb
6.34 c                   eval      fopsuf = w_psuf
6.34  * NB Kun hvis ikke annen kode finnes fra før
6.34 c                   if        foityp = *blanks
6.34 c                   eval      foityp = w_ityp
6.34 c                   endif
6.25  * Sjekk om det er krav til utfylt leveringsmåte
6.25 c                   if        u_lmat = *on and
6.25 c                             folmat = 0
6.25 c                   eval      folmat = 99
6.25 c                   exsr      sbform
6.25 c                   endif
7.02  *
7.02  * Tildeling av ordretype på prosjektkunder
7.02  *
7.02  *   Ordretype PP fra håndholdt
7.02  *
      *
     c                   eval      w_rut = %CHAR(RKSPFA) + '  >' +
     c                                     %char(rkmkod) + '  >' +
     c                                     %char(footyp) + '  >' +
     c                                     %char(faaot6)
7.02 c                   if            rkspfa <> *zero
7.02 c                             and rkmkod <> *zero
7.02 c                   eval      votyl1_otyp = faaot6
7.02 c     votyl1_key    chain     votyl1
7.02 c                   if        %found(votyl1)
     c                   eval      w_rut = %CHAR(RKSPFA) + '  >' +
     c                                     %char(rkmkod) + '  >' +
     c                                     %char(footyp) + '  >' +
     c                                     %char(vaonxt) + '  OTYP'
7.02 c                   eval      footyp = vaonxt
8.01 c                   eval      b_into = *on
7.02 c                   endif
7.02 c                   endif
      *
     c                   update    fohelur
8.01  *
8.01  * Sjekker om opprinnelig ordre er negativ (PN) - snur fortegn på ordrelinjer
8.01  *
8.01 c                   if        b_into = *on and
8.01 c                             w_okre = '-'
8.01 c                   exsr      neg_intord
8.01 c                   endif
8.01  *
6.21 c                   endif
      *
      * Kaller opp subprogram
      *
     c                   call      'FO700R'
     c                   parm                    w_firm
     c                   parm                    fonumm
     c                   parm                    fosuff
      *
     c     end_status    endsr
      *
6.25  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.25  * Sjekk/Hent tekst for leveringsmåte                RS717R
6.25  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.25  *
6.25 c     sbform        begsr
6.25  *
6.25 c                   eval      folmtx = *blank
6.25 c                   call      'RS717R'
6.25 c                   parm                    w_retk
6.25 c                   parm                    folmat
6.25 c                   parm                    w_qtxt
6.25  *
6.25 c                   if        w_retk = *blank
6.25 c                   movel     w_qtxt        folmtx
6.25 c                   endif
6.25  *
6.25 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      w_rut = 'Start *pssr'
     c**                 call      'DEBUGCL'
     c**                 parm                    w_rut
     c                   eval      p_stat = '9'
     c*                  goto      end_pgm
      *
     c                   endsr
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  * Subrutine for å legge ut i Byggdok
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     byggdok       begsr
6.20  *
6.20  * legger evt. linjer til BygDok
6.20  *
6.20 c                   if        b_bdo1 = *on and
6.20 c                             b_bdo2 = *on and
6.20 c                             b_bdo3 = *on and
6.20 c                             fdnobb <> 0
6.20 c                   eval      rkunl1_kund = fokund
6.20 c     rkunl1_key    chain     rkunl1
6.20  * finner kontaktperson for kunde som har rolle BYGGDOK
6.20 c                   eval      b_kper  = *off
6.20 c                   eval      rukpl6_kund = fokund
6.20 c     rukpl6_key    setll     rukpl6
6.20 c     rukpl6_key    reade     rukpl6
6.20 c                   dow       not %eof
6.20 c     lo:up         xlate     rukrol        rukrol
6.30 c                   if        rukrol  = 'BYGGDOK' or rukrol = 'COBUILDER'
6.20 c                   eval      b_kper  = *on
6.20 c                   goto      fbdo_tag
6.20 c                   endif
6.20 c     rukpl6_key    reade     rukpl6
6.20 c                   enddo
6.20 c     fbdo_tag      tag
6.20 c                   eval      fwbfir = w_firm
6.20 c                   eval      fwbkun = fokund
6.20 c                   eval      fwbkpr = fokpro
6.20 c                   movel     fonumm        fwbnum
6.20 c                   eval      fwbnav = rknavn
6.20 c                   if        b_kper  = *on and
6.20 c                             ruenav <> *blank and
6.20 c                             rumail <> *blank
6.20 c                   eval      fwbper = %trim(ruenav) + ',' +
6.20 c                                      %trim(rufnav)
6.20 c                   eval      fwbmai = rumail
6.20 c                   else
6.20 c                   eval      fwbper = rknavn
6.20 c                   eval      fwbmai = rkemal
6.20 c                   endif
6.33  * hvis mail på kunde-prosjekt overstyrer det alt
6.33 c                   if        flmail <> *blank
6.33 c                   eval      fwbmai = flmail
6.33 c                   if        flkprs <> *blank
6.33 c                   eval      fwbper = flkprs
6.33 c                   endif
6.33 c                   endif
6.20 c                   eval      fwbftn = rkftnr
6.20 c                   move      w_btms        w_ti26
6.20 c                   eval      fwbtms = w_ti26
6.20 c                   eval      fwbnob = fdnobb
6.20 c                   time                    fwboda
6.20 c                   time                    fwboti
6.20 c                   time                    fwbeda
6.20 c                   time                    fwbeti
6.20 c                   eval      fwbous = l_user
6.20 c                   eval      fwbeus = l_user
8.04 c                   if        b_bygd = *on
8.04 c                   write     fbdopfr
8.04 c                   endif
6.2c c                   move      fwbnum        w_bnum
6.20 c                   eval      b_bdo4 = *on
6.20 c                   endif
6.20  *
6.20 c                   endsr
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Lager skyggetabell for senere oppdatering ved fakturering
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01 c     lag_skygge    begsr
7.01  *
7.01 c                   eval      s_osap = 0
7.01 c                   eval      s_otip = 0
7.01 c                   eval      s_okop = 0
7.01 c                   eval      s_oinp = 0
7.01 c                   eval      s_oppk = *blanks
7.01 c                   eval      s_okpr = 0
7.01 c                   eval      s_okr1 = 0
7.01 c                   eval      s_okr2 = 0
7.01 c                   eval      s_okpk = *blanks
7.01  *
7.01  * Hent pris
7.01  *
7.01 c                   exsr      hent_pris
7.01  *
7.01 c                   eval      p_inlr = *blanks
7.01  *
7.01  * Hent inn std priser
7.01  *
7.01 c                   call      'VP700R'
7.01 c                   parm                    w_firm
7.01 c                   parm                    fdvare
7.01 c                   parm                    w_prgr
7.01 c                   parm                    fdenh1
7.01 c                   parm                    fdlety
7.01 c                   parm                    fobdat
7.01 c                   parm                    w_time
7.01 c                   parm                    p_inlr
7.01 c                   parm                    x_sapr
7.01 c                   parm                    x_bupr
7.01 c                   parm                    x_tipr
7.01 c                   parm                    x_kopr
7.01  *
7.01 c                   eval      s_osap = x_sapr
7.01 c                   eval      s_otip = x_tipr
7.01 c                   eval      s_okop = x_kopr
7.01 c                   if        x_tipr <> 0
7.01 c                   eval      s_oppk = 'T'
7.01 c                   else
7.01 c                   eval      s_oppk = ' '
7.01 c                   endif
7.01  * Hent inn innkjøpspris
7.01 c                   call      'VP703R'
7.01 c                   parm                    w_firm
7.01 c                   parm                    fdvare
7.01 c                   parm                    w_prgr
7.01 c                   parm                    fdenh1
7.01 c                   parm                    fdlety
7.01 c                   parm                    fobdat
7.01 c                   parm                    p_inlr
7.01 c                   parm                    x_sapr
7.01 c                   parm                    x_bupr
7.01 c                   parm                    x_kopr
7.01 c                   parm                    x_inpr
7.01  *
7.01 c                   eval      s_oinp = x_inpr
7.01  *
7.01 c     fodtlu_key    chain     fodmiu
7.01 c                   if        not %found(fodmiu)
7.01 c                   clear                   fodmiur
7.01 c                   eval      xxfirm = w_firm
7.01 c                   eval      xxnumm = fdnumm
7.01 c                   eval      xxsuff = fdsuff
7.01 c                   eval      xxline = fdline
7.01 c                   time                    xxodat
7.01 c                   time                    xxotim
7.01 c                   eval      xxousr = l_user
7.01 c**                 endif
7.01  * Beregn nettopris - kun hvis
7.01 c                   if        s_okpr <> 0
7.01  *
7.01 c                   if        s_okr1 <> 0
7.01 c                   eval      w_rabt = (s_okpr * s_okr1) / 100
7.01 c                   eval      s_okpr = s_okpr - w_rabt
7.01 c                   endif
7.01 c                   if        s_okr2 <> 0
7.01 c                   eval      w_rabt = (s_okpr * s_okr2) / 100
7.01 c                   eval      s_okpr = s_okpr - w_rabt
7.01 c                   endif
7.01  *
7.01 c                   endif
7.01 c                   eval      xxokpr = s_okpr
7.01 c                   eval      xxokr1 = s_okr1
7.01 c                   eval      xxokr2 = s_okr2
7.01 c                   eval      xxokpk = s_okpk
7.01 c                   endif
7.01  *
7.01 c                   eval      xxosap = s_osap
7.01 c                   eval      xxotip = s_otip
7.01 c                   eval      xxokop = s_okop
7.01 c                   eval      xxoinp = s_oinp
7.01 c                   eval      xxoppk = s_oppk
7.01 c                   time                    xxtmst
7.01 c                   time                    xxedat
7.01 c                   time                    xxetim
7.01 c                   eval      xxeusr = l_user
7.01  *
7.01 c                   if        not %found(fodmiu)
7.01 c                   write     fodmiur
7.01 c                   else
7.01 c                   update    fodmiur
7.01 c                   endif
7.01  *
7.01 c                   endsr
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Henter kalkulert pris på varen
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01 c     hent_pris     begsr
7.01  *
7.01 c                   eval      hifirm = w_firm
7.01 c                   eval      hirkat = 0
7.01  *
7.01 c                   eval      hikund = fokund
7.01 c                   if        folkun <> 0
7.01 c                   eval      hikund = folkun
7.01 c                   endif
7.01  *
7.01 c                   eval      hikpro = fokpro
7.01 c                   eval      hivare = vvvare
7.01 c                   eval      hidato = fobdat
7.01 c                   eval      hitime = *loval
7.01 c                   eval      hinumm = 0
7.01 c                   eval      hikode = fopask
7.01 c                   eval      hiskaf = *off
7.01 c                   eval      hildor = 0
7.01 c                   eval      hisapr = 0
7.01 c                   eval      hikopr = 0
7.01sc                   eval      hiavgf = 0
7.01sc                   eval      hipriv = 0
7.01sc                   eval      hilety = 0
7.01sc                   eval      hidekn = 0
7.01  *
7.01 c                   call      'VP900R'
7.01 c                   parm                    hirec
7.01 c                   parm                    horec
7.01  *
7.01 c                   endsr
7.01  *
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Henter prisgruppe
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01 c     hent_prisgr   begsr
7.01  *
7.01 c                   eval      w_prgr = *blanks
7.01  *
7.01 c                   call      'VL712R   '
7.01 c                   parm                    w_firm
7.01 c                   parm                    folage
7.01 c                   parm                    foavde
7.01 c                   parm                    w_prgr
7.01  *
7.01 c                   endsr
8.01  *
8.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.01  * Snur fortegn på ordrelinjer ved negativ ordre fra HH
8.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.01  *
8.01 c     neg_intord    begsr
8.01  *
8.01 c                   eval      fodtlu_numm = fonumm
8.01 c                   eval      fodtlu_suff = fosuff
8.01 c     fodtlu_key    setll     fodtlu
8.01 c     fodtlu_key    reade     fodtlu
8.01 c                   dow       not %eof
8.01  *
8.01  * Sjekker fortegn og snur
8.01  *
8.01 c                   if        fdltyp = ' '
8.01 c                   eval      fdltyp = '- '
8.01 c                   else
8.01 c                   if        fdltyp = '- '
8.01 c                   eval      fdltyp = '  '
8.01 c                   endif
8.01 c                   endif
8.01 c                   update    fodtlur
8.01  *
8.01  *
8.01 c     fodtlu_key    reade     fodtlu
8.01 c                   enddo
8.01  *
8.01  * Rekalkulerer ordre
8.01  *
8.01 c                   call      'FO730R'
8.01 c                   parm                    w_enor
8.01 c                   parm                    w_firm
8.01 c                   parm                    fohelu_numm
8.01 c                   parm                    fohelu_suff
8.01  *
8.01 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les av Ordre-hode-tabell
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for oppdatering av Ordre-hode-tabell
      *
     c     fohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelu_numm
     c                   kfld                    fohelu_suff
      *
      * Key for les av Ordre-linje-tabell
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
      *
      * Key for oppdatering av Ordre-linje-tabell
      *
     c     fodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlu_numm
     c                   kfld                    fodtlu_suff
      *
      * Key for file : Vare-register
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
6.13  *
6.13  * Key for file : ORDRE-TYPE-REGISTER
6.13  *
6.13 c     votyl1_key    klist
6.13 c                   kfld                    w_firm
6.13 c                   kfld                    votyl1_otyp
6.20  *
6.20  * Key for les av moduler Nexstep
6.20  *
6.20 c     amodl1_key    klist
6.20 c                   kfld                    amodl1_modu
6.20  *
6.20  * Key for les av kundeprosjekt
6.20  *
6.22 c     fkprl1_key    klist
6.22 c                   kfld                    w_firm
6.22 c                   kfld                    fkprl1_kund
6.22 c                   kfld                    fkprl1_kpro
6.20  *
6.20  * Key for propertiesfil
6.20  *
6.20 c     afpslr_key    klist
6.20 c                   kfld                    afpslr_ufil
6.20 c                   kfld                    afpslr_uide
6.20  *
6.20  * Key for les av kunde
6.20  *
6.20 c     rkunl1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    rkunl1_kund
6.20  *
6.20  * Key for oppslag på kunde - kontaktperson
6.20  *
6.20 c     rukpl6_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    rukpl6_kund
6.25  *
6.25  * Key for file : Firmastyrt validering
6.25  *
6.25 c     avall1_key    klist
6.25 c                   kfld                    w_firm
6.25 c                   kfld                    avall1_apgm
6.34  *
6.34  * Key for file : kundekort
6.34  *
6.34 c     rukrlr_key    klist
6.34 c                   kfld                    w_firm
6.34 c                   kfld                    rukrlr_kkun
6.34  *
6.34  * Key for file : kort
6.34  *
6.34 c     ruksl1_key    klist
6.34 c                   kfld                    w_firm
6.34 c                   kfld                    ruksl1_skty
6.34 c                   kfld                    ruksl1_slag
7.01  *
7.01  * Key for oppslag på skygge tabell
7.01  *
7.01 c     fodmiu_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    fodmiu_numm
7.01 c                   kfld                    fodmiu_suff
7.01 c                   kfld                    fodmiu_line
7.02  *
7.02  * Key for oppslag på intern ordre
7.02  *
7.02 c     fstsl1_key    klist
7.02 c                   kfld                    w_firm
7.02  *
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_firm
6.nu c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_stat
     c                   eval      w_firm = p_firm
      *
6.20  *
6.20  * Sjekker om Byggdok skal kjøres forutsetter at
6.20  * modul er på plass og at
6.20  *
6.20 c                   eval      b_bdo1 = *off
6.20 c                   eval      amodl1_modu = 'BYGGDOK'
6.20 c     amodl1_key    chain     amodl1
6.20 c                   if        %found(amodl1)
6.20 c                   eval      afpslr_ufil = l_filg
6.20 c                   eval      afpslr_uide = 'BYGGDOK   '
6.20  *
6.30  * Sjekk om byggdok eller cobuilder aktivering er på/av
6.20  *
6.20 c     afpslr_key    chain     afpslrr
6.20 c                   if        %found(afpslr)
6.20 c                   eval      b_bdo1 = *on
6.30 c                   else
6.30 c                   eval      afpslr_uide = 'COBUILDER '
6.30 c     afpslr_key    chain     afpslrr
6.30 c                   if        %found(afpslr)
6.30 c                   eval      b_bdo1 = *on
6.30 c                   endif
6.20 c                   endif
6.20 c                   endif
6.25  *
6.25  * Sjekk om det er lagt opp krav til leveringsmåte
6.25  *
6.25 c                   eval      avall1_apgm = 'FO101R'
6.25 c     avall1_key    setll     avall1
6.25 c     avall1_key    reade     avall1
6.25 c                   dow       not %eof(avall1)
6.25 c                   if        avaflt = 'FOLMAT'  and
6.25 c                             avaval = 1
6.25 c                   eval      u_lmat = *on
6.25 c                   endif
6.25 c     avall1_key    reade     avall1
6.25 c                   enddo
6.25  *
6.25  * Sjekk om det er lagt opp krav til leveringsmåte
6.25  *
6.25 c                   eval      avall1_apgm = 'FO102R'
6.25 c     avall1_key    setll     avall1
6.25 c     avall1_key    reade     avall1
6.25 c                   dow       not %eof(avall1)
6.25 c                   if        avaflt = 'FOLMAT'  and
6.25 c                             avaval = 1
6.25 c                   eval      u_lmat = *on
6.25 c                   endif
6.25 c     avall1_key    reade     avall1
6.25 c                   enddo
6.25  * Sjekk om det er lagt opp krav til leveringsmåte
6.25  *
6.25 c                   eval      avall1_apgm = 'FO103R'
6.25 c     avall1_key    setll     avall1
6.25 c     avall1_key    reade     avall1
6.25 c                   dow       not %eof(avall1)
6.25 c                   if        avaflt = 'FOLMAT'  and
6.25 c                             avaval = 1
6.25 c                   eval      u_lmat = *on
6.25 c                   endif
6.25 c     avall1_key    reade     avall1
6.25 c                   enddo
7.02 c     fstsl1_key    chain     fstsl1
      *
7.01 c                   time                    w_time
      *
     c                   endsr
