     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASPNIB
      * Program . . . . : EK110R
      * Beskrivelse . . : Kunde bonuselement, vise
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       261005 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fekbei1    if   e           k disk    rename(ekbest:ekbeir1)
     fekbeir    if   e           k disk    rename(ekbest:ekbeirr)
     fra06l1    if   e           k disk    rename(ra06pfr:ra06l1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvenhl1    if   e           k disk    rename(venhpfr:venhl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
     fvbotl1    if   e           k disk    rename(vbotpfr:vbotl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
      *
     fek510d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(ekefir)
     d p_bkun          s                   like(ekebku)
     d p_rkat          s                   like(ekerka)
     d p_kund          s                   like(ekekun)
     d p_kpro          s                   like(ekekpr)
     d p_ogrp          s                   like(ekeogr)
     d p_hgrp          s                   like(ekehgr)
     d p_ugrp          s                   like(ekeugr)
     d p_ldor          s                   like(ekeldo)
     d p_modn          s                   like(ekemod)
     d p_vare          s                   like(ekevar)
     d p_enhe          s                   like(ekeenh)
     d p_lety          s                   like(ekelet)
     d p_boty          s                   like(ekebot)
     d p_fdat          s                   like(ekefda)
     d p_tdat          s                   like(eketda)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d ekbei1_ebgf     s                   like(ekebgf)
     d ekbei1_ebgt     s                   like(ekebgt)
     d ekbeir_ebgf     s                   like(ekebgf)
     d ekbeir_ebgt     s                   like(ekebgt)
     d ra06l1_fkod     s                   like(rafkod)
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d rlevl1_levr     s                   like(rllevr)
     d vvarl1_vare     s                   like(vvvare)
     d venhl1_enhe     s                   like(vaeenh)
     d vltpl1_lety     s                   like(vylety)
     d vbotl1_tbot     s                   like(vatbot)
     d jvarl1_vare     s                   like(jvvare)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_subf          s              1    inz(*off)
      *
     d w_firm          s                   like(ekefir)
     d w_bkun          s                   like(ekebku)
     d w_rkat          s                   like(ekerka)
     d w_kund          s                   like(ekekun)
     d w_kpro          s                   like(ekekpr)
     d w_ogrp          s                   like(ekeogr)
     d w_hgrp          s                   like(ekehgr)
     d w_ugrp          s                   like(ekeugr)
     d w_ldor          s                   like(ekeldo)
     d w_modn          s                   like(ekemod)
     d w_vare          s                   like(ekevar)
     d w_enhe          s                   like(ekeenh)
     d w_lety          s                   like(ekelet)
     d w_boty          s                   like(ekebot)
     d w_fdat          s                   like(ekefda)
     d w_tdat          s                   like(eketda)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(7)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile                                  CRT SFL
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   if        b_subf = *on
     c                   goto      b2taga
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c     ekbei1_key    setll     ekbei1
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_subf = *off
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      b_subf = *on
     c                   eval      w_virn = w_srrn
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1ebgf = b1ebgf
     c                   eval      c1ebgt = b1ebgt
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle skjermbilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      ekbeir_ebgf = c1ebgf
     c                   eval      ekbeir_ebgt = c1ebgt
     c     ekbeir_key    chain     ekbeir
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
     c                   eval      c2ebpr = ekebpr
     c                   eval      c2emrk = ekemrk
     c                   eval      c2eeus = ekeeus
     c     *dmy          move      ekeoda        c2eoda
     c     *dmy          move      ekeeda        c2eeda
     c                   eval      c2esbo = ekesbo
     c                   eval      c2ebda = 0
     c                   eval      c2ebti = 0
     c                   if        ekebda <> *loval
     c     *dmy          move      ekebda        c2ebda
     c                   endif
     c                   if        ekebti <> *loval
     c     *hms          move      ekebti        c2ebti
     c                   endif
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      ekbei1                                 15
      *
     c                   if        *in15                                        EOF
     c                   leave
     c                   endif
      *
     c                   if        ekebku <> p_bkun or
     c                             ekerka <> p_rkat or
     c                             ekekun <> p_kund or
     c                             ekekpr <> p_kpro or
     c                             ekeogr <> p_ogrp or
     c                             ekehgr <> p_hgrp or
     c                             ekeugr <> p_ugrp or
     c                             ekeldo <> p_ldor or
     c                             ekemod <> p_modn or
     c                             ekevar <> p_vare or
     c                             ekeenh <> p_enhe or
     c                             ekelet <> p_lety or
     c                             ekebot <> p_boty or
     c                             ekefda <> p_fdat or
     c                             eketda <> p_tdat
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1ebgf = ekebgf
     c                   eval      b1ebgt = ekebgt
     c                   eval      b1ebpr = ekebpr
     c                   eval      b1emrk = ekemrk
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_bkun
     c                   parm                    p_rkat
     c                   parm                    p_kund
     c                   parm                    p_kpro
     c                   parm                    p_ogrp
     c                   parm                    p_hgrp
     c                   parm                    p_ugrp
     c                   parm                    p_ldor
     c                   parm                    p_modn
     c                   parm                    p_vare
     c                   parm                    p_enhe
     c                   parm                    p_lety
     c                   parm                    p_boty
     c                   parm                    p_fdat
     c                   parm                    p_tdat
      *
      * Key for oppslag på kunde bonuselement
      *
     c     ekbei1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_bkun
     c                   kfld                    w_rkat
     c                   kfld                    w_kund
     c                   kfld                    w_kpro
     c                   kfld                    w_ogrp
     c                   kfld                    w_hgrp
     c                   kfld                    w_ugrp
     c                   kfld                    w_ldor
     c                   kfld                    w_modn
     c                   kfld                    w_vare
     c                   kfld                    w_enhe
     c                   kfld                    w_lety
     c                   kfld                    w_boty
     c                   kfld                    w_fdat
     c                   kfld                    w_tdat
     c                   kfld                    ekbei1_ebgf
     c                   kfld                    ekbei1_ebgt
      *
      * Key for oppslag på kunde bonuselement
      *
     c     ekbeir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_bkun
     c                   kfld                    w_rkat
     c                   kfld                    w_kund
     c                   kfld                    w_kpro
     c                   kfld                    w_ogrp
     c                   kfld                    w_hgrp
     c                   kfld                    w_ugrp
     c                   kfld                    w_ldor
     c                   kfld                    w_modn
     c                   kfld                    w_vare
     c                   kfld                    w_enhe
     c                   kfld                    w_lety
     c                   kfld                    w_boty
     c                   kfld                    w_fdat
     c                   kfld                    w_tdat
     c                   kfld                    ekbeir_ebgf
     c                   kfld                    ekbeir_ebgt
      *
      * Key for oppslag på rabattkategori
      *
     c     ra06l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra06l1_fkod
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på enhet
      *
     c     venhl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    venhl1_enhe
      *
      * Key for oppslag på leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
      *
      * Key for oppslag på bonustype
      *
     c     vbotl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vbotl1_tbot
      *
      * Key for oppslag på vare (VA)
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      *
      * Henter parametre, og fyller opp skjermbilde og subfile
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_bkun = p_bkun
     c                   eval      w_rkat = p_rkat
     c                   eval      w_kund = p_kund
     c                   eval      w_kpro = p_kpro
     c                   eval      w_ogrp = p_ogrp
     c                   eval      w_hgrp = p_hgrp
     c                   eval      w_ugrp = p_ugrp
     c                   eval      w_ldor = p_ldor
     c                   eval      w_modn = p_modn
     c                   eval      w_vare = p_vare
     c                   eval      w_enhe = p_enhe
     c                   eval      w_lety = p_lety
     c                   eval      w_boty = p_boty
     c                   eval      w_fdat = p_fdat
     c                   eval      w_tdat = p_tdat
      *
     c                   eval      b2rkat = p_rkat
     c                   eval      b2kund = p_kund
     c                   eval      b2kpro = p_kpro
     c                   eval      b2ogrp = p_ogrp
     c                   eval      b2hgrp = p_hgrp
     c                   eval      b2ugrp = p_ugrp
     c                   eval      b2ldor = p_ldor
     c                   eval      b2modn = p_modn
     c                   eval      b2vare = p_vare
     c                   eval      b2enhe = p_enhe
     c                   eval      b2lety = p_lety
     c                   eval      b2boty = p_boty
     c                   eval      b2fdat = 0
     c                   eval      b2tdat = 0
      *
      * Rabattkategori
      *
     c                   if        b2rkat <> 0
     c                   eval      ra06l1_fkod = b2rkat
     c     ra06l1_key    chain     ra06l1
     c                   if        %found
     c                   eval      b2rtxt = raftxt
     c                   endif
     c                   endif
      *
      * Kunde
      *
     c                   if        b2kund <> 0
     c                   eval      rkunl1_kund = b2kund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      b2knav = rknavn
     c                   endif
     c                   endif
      *
      * Kundeprosjekt
      *
     c                   if        b2kpro <> 0
     c                   eval      fkprl1_kund = b2kund
     c                   eval      fkprl1_kpro = b2kpro
     c     fkprl1_key    chain     fkprl1
     c                   if        %found
     c                   eval      b2pnav = flnavn
     c                   endif
     c                   endif
      *
      * Overgruppe
      *
     c                   if        b2ogrp <> 0 and
     c                             b2hgrp = 0
     c                   eval      vogrl1_oogr = b2ogrp
     c     vogrl1_key    chain     vogrl1
     c                   if        %found
     c                   eval      b2gtxt = vgotxt
     c                   endif
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        b2hgrp <> 0 and
     c                             b2ugrp = 0
     c                   eval      vhgrl1_hogr = b2ogrp
     c                   eval      vhgrl1_hhgr = b2hgrp
     c     vhgrl1_key    chain     vhgrl1
     c                   if        %found
     c                   eval      b2gtxt = vghtxt
     c                   endif
     c                   endif
      *
      * Undergruppe
      *
     c                   if        b2ugrp <> 0
     c                   eval      vugrl1_uogr = b2ogrp
     c                   eval      vugrl1_uhgr = b2hgrp
     c                   eval      vugrl1_uugr = b2ugrp
     c     vugrl1_key    chain     vugrl1
     c                   if        %found
     c                   eval      b2gtxt = vgutxt
     c                   endif
     c                   endif
      *
      * Leverandør
      *
     c                   if        b2ldor <> 0
     c                   eval      rlevl1_levr = b2ldor
     c     rlevl1_key    chain     rlevl1
     c                   if        %found
     c                   eval      b2lnav = rlnavn
     c                   endif
     c                   endif
      *
      * Modul
      *
     c                   if        b2modn <> 0
     c                   call      'FO714C'
     c                   parm                    w_firm
     c                   parm                    b2modn
     c                   parm                    b2mtxt
     c                   endif
      *
      * Varenummer
      *
     c                   if        b2vare <> *blank
     c                   eval      vvarl1_vare = b2vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      b2vtxt = vvtek1
     c                   else
     c                   eval      jvarl1_vare = b2vare
     c     jvarl1_key    chain     jvarl1
     c                   if        %found
     c                   eval      b2vtxt = jvtek1
     c                   endif
     c                   endif
     c                   endif
      *
      * Enhet
      *
     c                   if        b2enhe <> *blank
     c                   eval      venhl1_enhe = b2enhe
     c     venhl1_key    chain     venhl1
     c                   if        %found
     c                   eval      b2etxt = vaetxt
     c                   endif
     c                   endif
      *
      * Leveringstype
      *
     c                   if        b2lety <> 0
     c                   eval      vltpl1_lety = b2lety
     c     vltpl1_key    chain     vltpl1
     c                   if        %found
     c                   eval      b2ltxt = vybesk
     c                   endif
     c                   endif
      *
      * Bonustype
      *
     c                   if        b2boty <> *blank
     c                   eval      vbotl1_tbot = b2boty
     c     vbotl1_key    chain     vbotl1
     c                   if        %found
     c                   eval      b2btxt = vatbes
     c                   endif
     c                   endif
      *
     c                   if        p_fdat <> *loval
     c     *dmy          move      p_fdat        b2fdat
     c                   endif
     c                   if        p_tdat <> *loval
     c     *dmy          move      p_tdat        b2tdat
     c                   endif
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     ekbei1_key    setll     ekbei1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
