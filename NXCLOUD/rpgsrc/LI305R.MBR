     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  System. . . . . : ASLAGR                                       *
      *  Program . . . . : LI305R                                       *
      *  Beskrivelse . . : Plukke enkeltkolli til godkjenning           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  Spesialversjon. :                                              *
      *  Tilpasset for . :                                              *
      *                                                                 *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.*
      *  --------- ------  ---------------------------------------------*
      *  7.00     131219  Nytt program                                 *
7.01  *  7.00     090120  Skal kun slå sammen IP'er hvis alle kolli    *
7.01  *                   på pakkseddelen er scannet.                  *
7.02  *  7.00     140120  Endret kodesetting.                          *
7.03  *  7.00     300120  Fjernet bruken av LWVAPF, legg ut basert     *
7.03  *                   på LBLIST.                                   *
7.04  *  7.00     300120  Feil i bruken av restkoder.                  *
7.05  *  7.00     310120  Håndtering av tekstlinjer.                   *
7.06  *  K000     260620  Parameter for å akseptere resting etter      *
7.06  *                   oppdatering av pakkseddel.                   *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
      *
     flbhoi2    if   e           k disk    rename(lbhost:lbhoi2r)
     flpkoi2    uf   e           k disk    rename(lpkost:lpkoi2r)
     flpkoir    if   e           k disk    rename(lpkost:lpkoirr)
     flpkli2    uf   e           k disk    rename(lpklst:lpkli2r)
     flbhoiu    uf   e           k disk    rename(lbhost:lbhoiur)
     flbliir    if   e           k disk    rename(lblist:lbliirr)
     flbliiu    uf   e           k disk    rename(lblist:lbliiur)
     flphoi1    if   e           k disk    rename(lphost:lphoi1r)
     flphoiu    uf   e           k disk    rename(lphost:lphoiur)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flohelu    uf   e           k disk    rename(lohepfr:lohelur)
     flohelr    if   e           k disk    rename(lohepfr:lohelrr)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
7.05 flodtlr    if   e           k disk    rename(lodtpfr:lodtlrr)
      *
     flplolu    uf a e           k disk    rename(lplopfr:lplolur)
     flplol1    if   e           k disk    rename(lplopfr:lplol1r)
     flplllu    uf a e           k disk    rename(lpllpfr:lplllur)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     flusrl1    if   e           k disk    rename(lusrpfr:lusrl1r)
7.06 frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
7.06 flbreiu    uf a e           k disk    rename(lbrest:lbreiur)
7.06 flodtlu    uf   e           k disk    rename(lodtpfr:lodtlur)
      *
     fli300d    cf   e             workstn
      *
      * Definering av array's
      *
     d bnr             s              8  0 dim(20)
     d bsu             s              2  0 dim(20)
     d oty             s              2    dim(20)
     d ssc             s             18  0 dim(20)
      *
      *
      * Definering av variable i parametre
      *
     d p_type          s              4
     d p_mnum          s              8  0
     d p_aarr          s              4  0
     d p_paid          s             17
     d p_pver          s              3  0
     d p_orgi          s              5
7.06 d p_levr          s                   like(rllevr)
      *
      * Definering av variabler i nøkler
      *
     d lpkoi2_type     s                   like(potype)
     d lpkoi2_sscc     s                   like(posscc)
     d lpkoi2_aarr     s                   like(poaarr)
     d lpkoi2_paid     s                   like(popaid)
      *
     d lpkoir_type     s                   like(phtype)
     d lpkoir_aarr     s                   like(phaarr)
     d lpkoir_mnum     s                   like(phmnum)
     d lpkoir_paid     s                   like(phpaid)
     d lpkoir_pver     s                   like(phpver)
      *
     d lbhoiu_hssc     s                   like(pbhssc)
     d lbhoiu_hnum     s                   like(pbhnum)
     d lbhoiu_hsuf     s                   like(pbhsuf)
      *
     d lbhoi2_hpid     s                   like(pbhpid)
     d lohelu_numm     s                   like(lonumm)
     d lohelu_suff     s                   like(losuff)
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d lohelr_numm     s                   like(lonumm)
     d lohelr_suff     s                   like(losuff)
      *
     d lplolu_numm     s                   like(lkponr)
     d lplolu_suff     s                   like(lkpsuf)
     d lusrl1_user     s                   like(lbuser)
     d lusrl1_firm     s                   like(lbfirm)
      *
     d lphoi1_type     s                   like(phtype)
     d lphoi1_aarr     s                   like(phaarr)
     d lphoi1_mnum     s                   like(phmnum)
     d lphoi1_paid     s                   like(phpaid)
     d lphoi1_pver     s                   like(phpver)
      *
     d lbliir_lssc     s                   like(pblssc)
      *
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d lodtl1_line     s                   like(ldline)
      *
7.05 d lodtlr_numm     s                   like(ldnumm)
7.05 d lodtlr_suff     s                   like(ldsuff)
7.05 d lodtlr_line     s                   like(ldline)
      *
7.06 d lodtlu_numm     s                   like(ldnumm)
7.06 d lodtlu_suff     s                   like(ldsuff)
7.06 d lodtlu_line     s                   like(ldline)
      *
     d lplllu_lonr     s                   like(lklonr)
     d lplllu_lsuf     s                   like(lklsuf)
     d lplllu_llin     s                   like(lkllin)
      *
     d lplol1_lonr     s                   like(lkponr)
     d lplol1_lsuf     s                   like(lkpsuf)
     d votyl1_otyp     s                   like(vaotyp)
7.06 d rlevl1_levr     s                   like(rllevr)
7.06 d lbreiu_rnum     s                   like(pbrnum)
7.06 d lbreiu_rsuf     s                   like(pbrsuf)
7.06 d lbreiu_rlin     s                   like(pbrlin)
      *
      * Definering av øvrige variable
      *
     d w_on01          s              2
     d w_on02          s              2
     d w_on03          s              2
     d w_on04          s              2
     d w_on05          s              2
     d w_on06          s              2
     d w_firm          s                   like(lofirm)
     d w_valg          s              1  0
     d w_ipor          s              1
     d w_bsuf          s              2  0
7.06 d w_rest          s              1  0
7.06 d w_stat          s              1
7.06 d w_numa          s              8
7.06 d w_lina          s              4
      *
     d x               s              2  0
      *
     d b_best          s              1    inz(*off)
     d b_alle          s              1    inz(*off)
7.05 d b_text          s              1    inz(*off)
7.05 d b_flin          s              1    inz(*off)
      *
7.06 d                 ds
7.06 d hlrec                        128
7.06 d  hlvare                       15    overlay(hlrec)
7.06 d  hllage                        2  0 overlay(hlrec:16)
7.06 d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
7.06 d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
7.06 d  hlotyp                        2    overlay(hlrec:36)
7.06 d  hlltyp                        2    overlay(hlrec:38)
7.06 d  hlanta                       11  3 overlay(hlrec:40)
7.06 d  hlkopr                        9  2 overlay(hlrec:51)
7.06 d  hlrefr                       20    overlay(hlrec:60)
7.06 d  hlsyst                        1    overlay(hlrec:80)
7.06 d  hlaltk                        2    overlay(hlrec:81)
7.06 d  hlrest                        1    overlay(hlrec:83)
7.06 d  hltype                        1    overlay(hlrec:84)
7.06 d  hlenhe                        3    overlay(hlrec:85)
7.06 d  hlinlr                        1    overlay(hlrec:88)
7.06 d  hllety                        1    overlay(hlrec:89)
7.06 d  hlonum                        8  0 overlay(hlrec:90)
7.06 d  hlolin                        4  0 overlay(hlrec:98)
      *
     d                UDS
     d  l_wsid               901    910
     d  l_user               911    920
     d  l_firm               944    946  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  A1 Behandler plukking av EDI pakksedler                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c                   eval      x = 0
     c                   eval      bnr = *zeros
     c                   eval      bsu = *zeros
     c                   eval      oty = *blanks
     c                   eval      ssc = *zeros
     c                   eval      a1paid = p_paid
     c                   eval      a1pver = p_pver
      *
     c     a1tag1        tag
      *
      * Finner om det er en bestillingsreferanse - minst en av dem må være gyldig
      *
     c                   eval      b_best = *off
     c                   eval      lbhoi2_hpid = p_paid
     c     lbhoi2_key    setll     lbhoi2
     c     lbhoi2_key    reade     lbhoi2
     c                   dow       not %eof
     c                   if        pbhnum <> 0     and
     c                             pbhaar = p_aarr and
     c                             pbhsta = '50'
      *
      *  Sjekk om bestilling er gyldig
      *
     c                   eval      lohel1_numm = pbhnum
     c                   eval      lohel1_suff = pbhsuf
     c     lohel1_key    chain     lohel1
     c                   if        %found
      *
      *  Hent opplysninger fra ORDRETYPE-REGISTER
      *
     c                   eval      votyl1_otyp = lootyp
     c     votyl1_key    chain     votyl1
     c                   if        %found
     c                   if        vaoakk <> 1
     c                   eval      b_best = *off
     c                   else
     c                   if        %lookup(pbhssc:ssc) = 0
     c                   eval      b_best = *on
     c                   eval      x = x +1
     c                   eval      bnr(x) = pbhnum
     c                   eval      bsu(x) = pbhsuf
     c                   eval      ssc(x) = pbhssc
     c                   eval      oty(x) = vaonxt
     c                   endif
      *
      * Lagrer informasjon for "neste ordretype" sjekk
      * (Brukes ikke for øyeblikket)
      *
     c                   eval      w_on01 = vaon01
     c                   eval      w_on02 = vaon02
     c                   eval      w_on03 = vaon03
     c                   eval      w_on04 = vaon04
     c                   eval      w_on05 = vaon05
     c                   eval      w_on06 = vaon06
     c                   endif
     c                   endif
      *
     c                   endif
     c                   endif
     c     lbhoi2_key    reade     lbhoi2
     c                   enddo
      *
      *  Legger inn opplysningene i skjermbildefeltene
      *
     c**                 eval      a1otyp = vaonxt
      *
     c                   if        b_best = *off
     c                   eval      *in31 = *on
     c                   endif
      *
      *  Send Alt
      *
     c                   write     a1bld
      *
      *  Bare data
      *
     c     a1tagb        tag
     c                   exfmt     a1bld
      *
      *  F3=Avslutt program
      *
     c                   if        *inkc = *on
     c                   goto      xslutt
     c                   endif
      *
      * Kan ikke gjøre noe hvis ikke gyldig bestilling
      *
     c                   if        b_best = *off
     c                   goto      a1tag1
     c                   endif
      *
      *  F4=Spørring
      *
     c                   if        *inkd = *on
      * Ordretype
     c*                  if        wactre = 'A1BLD' and
     c*                            wactfe = 'A1OTYP'
     c*                  call      'VA550R'
     c*                  parm                    w_firm
     c*                  parm                    a1otyp
     c*                  goto      a1tagb
     c*                  endif
      *
     c                   endif
      *
      * Sjekk om det skal testes på lov til utplukk
      *
     c     lusrl1_key    chain     lusrl1
     c                   if        %found
     c                   if        lbko10 = 0 and
     c                             vaoakk > 2
     c                   eval      *in35 = *on
     c                   goto      a1tagb
     c                   endif
     c                   endif
     c
      *
      * Sett sperre på EDI pakkseddelen, så den ikke kan plukkes flere ganger.
      *
     c     lphoi1_key    chain     lphoiur
     c                   if        %found
     c                   eval      phstat = '30'
     c                   time                    phedat
     c                   time                    phetim
     c                   eval      pheusr = l_user
     c                   update    lphoiur
     c                   endif
      *
      * Så looper vi de bestillingene som er gyldige, og skal plukkes/delplukkes.
      *
     c     1             do        20            x
     c                   if        ssc(x) <> 0
     c                   eval      lohel1_numm = bnr(x)
     c                   eval      lohel1_suff = bsu(x)
     c                   eval      lohelu_numm = bnr(x)
     c                   eval      lohelu_suff = bsu(x)
     c                   eval      lonumm = bnr(x)
     c                   eval      losuff = bsu(x)
      *
      *  Oppdater BESTILLING-HODE-REGISTER, Bestillingen er til behandling
      *
     c                   call      'LO709R'
     c                   parm                    lonumm
     c                   parm                    losuff
      *
      * Sett "sperrekode" på bestilling
      *
     c     lohelu_key    chain     lohelur
     c                   if        %found
     c                   eval      lokode = 3
     c                   time                    loedat
     c                   time                    loetim
     c                   eval      loeusr = l_user
     c                   update    lohelur
     c                   endif
      *
      * Kjør utplukk avhengig av hvor vi kommer fra
      *
     c                   exsr      scan_li210
      *
      *  Kaller opp utskrift-program for å sette inn alternativ
      *  og om direktevalg skal benyttes
      *
     c                   eval      w_valg = 1
      *
     c                   call      'LI301R'
     c                   parm                    w_ipor
     c                   parm                    w_firm
     c                   parm                    lohelu_numm
     c                   parm                    lohelu_suff
     c                   parm                    w_valg
      *
      * Sette tilbake status
      *
     c     lohelu_key    chain     lohelur
     c                   if        %found
     c                   eval      lokode = 0
     c                   time                    loedat
     c                   time                    loetim
     c                   eval      loeusr = l_user
     c                   update    lohelur
     c                   endif
      *
     c                   endif
     c                   enddo
      *
      * Så looper vi de bestillingene som er behqndlet og setter statuser
      *
     c     1             do        20            x
     c                   if        ssc(x) <> 0
     c                   eval      lbhoiu_hssc = ssc(x)
     c                   eval      lbhoiu_hnum = bnr(x)
     c                   eval      lbhoiu_hsuf = bsu(x)
     c                   eval      lpkoi2_sscc = ssc(x)
      *
      * Setter statusen til ferdig varemottatt på knytning bestilling SSCC kode  - hode
      *
     c     lbhoiu_key    chain     lbhoiu
     c                   if        %found
     c                   eval      pbhsta = '90'
     c                   time                    pbheda
     c                   time                    pbheti
     c                   eval      pbheus = l_user
     c                   update    lbhoiur
     c                   endif
      *
      * Setter statusen til ferdig varemottatt på knytning bestilling SSCC kode - linje
      *
     c     lbhoiu_key    setll     lbliiu
     c     lbhoiu_key    reade     lbliiu
     c                   dow       not %eof
     c                   eval      pblsta = '90'
     c                   time                    pbleda
     c                   time                    pbleti
     c                   eval      pbleus = l_user
     c                   update    lbliiur
     c     lbhoiu_key    reade     lbliiu
     c                   enddo
      *
      * Setter statusen til ferdig varemottatt på pakkseddel - kolli
      *
     c     lpkoi2_key    setll     lpkoi2
     c     lpkoi2_key    reade     lpkoi2
     c                   dow       not %eof
     c                   eval      postat = '90'
     c                   time                    poedat
     c                   time                    poetim
     c                   eval      poeusr = l_user
     c                   update    lpkoi2r
      *
      * Sett sperre på alle underliggende kollilinjer, så de ikke kan plukkes fra andre.
      *
     c     lpkoi2_key    setll     lpkli2
     c     lpkoi2_key    reade     lpkli2
     c                   dow       not %eof
     c                   eval      plstat = '90'
     c                   time                    pledat
     c                   time                    pletim
     c                   eval      pleusr = l_user
     c                   update    lpkli2r
     c     lpkoi2_key    reade     lpkli2
     c                   enddo
      *
     c     lpkoi2_key    reade     lpkoi2
     c                   enddo
      *
     c                   endif
     c                   enddo
      *
      * Sjekk om hele pakkkseddelen er scannet ?? Forutsetter at alle kolli er oppdatert
      *
     c                   eval      b_alle = *on
      *
     c     lpkoir_key    setll     lpkoir
     c     lpkoir_key    reade     lpkoir
     c                   dow       not %eof
     c                   if        postat <> '90'
     c                   eval      b_alle = *off
     c                   endif
     c     lpkoir_key    reade     lpkoir
     c                   enddo
      * Hvis alle har status 90, er alle scannet
     c                   if        b_alle = *on
     c     lphoi1_key    chain     lphoiur
     c                   if        %found
     c                   eval      phstat = '90'
     c                   time                    phedat
     c                   time                    phetim
     c                   eval      pheusr = l_user
     c                   update    lphoiur
     c                   endif
     c                   endif
      *
      * Slå sammen alle evnt IP'er
7.01  * NB kun hvis alle kolli er scannet
      *
      * Så looper vi de bestillingene som er behandlet og setter statuser
      *
7.01 c                   if        b_alle = *on
      *
     c     1             do        20            x
     c                   if        bnr(x) <> 0
      * Basisordre
     c                   eval      w_bsuf = 0
     c                   eval      lohelr_numm = bnr(x)
     c                   eval      lohelr_suff = 00
     c     lohelr_key    setll     lohelr
     c     lohelr_key2   reade     lohelr
     c                   dow       not %eof
     c                   if        losuff > 0 and
     c                             w_bsuf = 0
     c                   eval      w_bsuf = losuff
     c                   endif
     c     lohelr_key2   reade     lohelr
     c                   enddo
      *
     c                   eval      lohelr_numm = bnr(x)
     c                   eval      lohelr_suff = 99
      *
      * Les fra siste suffiks og slå sammen med basisbestillingen
      *
      *
     c     lohelr_key    setll     lohelr
     c     lohelr_key2   readpe    lohelr
     c                   dow       not %eof
     c
     c                   if        losuff <> bsu(x) and
     c                             losuff > 1
      * Så kaller vi program for å slå sammen IP'er
     c                   call      'LI306R'
     c                   parm                    lohelr_numm
     c                   parm                    w_bsuf
     c                   parm                    losuff
     c                   endif
      *
     c     lohelr_key2   readpe    lohelr
     c                   enddo
7.06  *
7.06  * Hvis alle er slått sammen, ligger rest igjen på suffiks 0.
7.06  *
7.06 c                   eval      lbreiu_rnum = bnr(x)
7.06 c                   eval      lbreiu_rsuf = 00
7.06 c     lbreiu_key    setll     lbreiu
7.06 c     lbreiu_key2   reade     lbreiu
7.06 c                   dow       not %eof
7.06 c                   eval      lodtlu_numm = lbreiu_rnum
7.06 c                   eval      lodtlu_suff = 0
7.06 c                   eval      lodtlu_line = lbreiu_rlin
7.06 c     lodtlu_key    chain     lodtlu
7.06 c                   if        %found
7.06 c                   delete    lodtlur
7.06 c                   if        laalag <> 0
7.06 c                   eval      ldanta = ldanta * -1
7.06 c                   exsr      oppdat_lager
7.06 c                   endif
7.06 c                   delete    lbreiur
7.06 c                   endif
7.06 c     lbreiu_key2   reade     lbreiu
7.06 c                   enddo
7.06  *
     c                   endif
     c                   enddo
      *
7.06 c                   else
7.06  *
7.06  * Vi må lese fra loggfil, men rest vil ligge på suffiks 0.
7.06 c     1             do        20            x
7.06 c                   if        bnr(x) <> 0
7.06  *
7.06  * Rest ligger igjen på suffiks 0.
7.06  *
7.06 c                   eval      lbreiu_rnum = bnr(x)
7.06 c                   eval      lbreiu_rsuf = 00
7.06 c     lbreiu_key    setll     lbreiu
7.06 c     lbreiu_key2   reade     lbreiu
7.06 c                   dow       not %eof
7.06 c                   eval      lodtlu_numm = lbreiu_rnum
7.06 c                   eval      lodtlu_suff = 0
7.06 c                   eval      lodtlu_line = lbreiu_rlin
7.06 c     lodtlu_key    chain     lodtlu
7.06 c                   if        %found
7.06 c                   delete    lodtlur
7.06 c                   if        laalag <> 0
7.06 c                   eval      ldanta = ldanta * -1
7.06 c                   exsr      oppdat_lager
7.06 c                   endif
7.06 c                   delete    lbreiur
7.06 c                   endif
7.06 c     lbreiu_key2   reade     lbreiu
7.06 c                   enddo
7.06  *
     c                   endif
     c                   enddo
7.06  *
7.01 c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for utplukk fra scanning av kolli                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     scan_li210    begsr
      *
      * Les scannede kollilinjer
      *
      *
     c                   eval      lbliir_lssc = ssc(x)
     c     lbliir_key    setll     lbliir
     c     lbliir_key    reade     lbliir
      *
     c                   dow       not %eof
      *
     c                   eval      lodtl1_numm = lohel1_numm
     c                   eval      lodtl1_suff = lohel1_suff
     c                   eval      lodtl1_line = pbllin
     c     lodtl1_key    chain     lodtl1
     c                   if        %found
7.06  *
7.06  * Legg ut arbeidsregister for pakkseddel, vis resting ikke skal skje
7.06  *
7.06 c                   if        a1rest = 0
7.06 c                   eval      lbreiu_rnum = pblnum
7.06 c                   eval      lbreiu_rsuf = pblsuf
7.06 c                   eval      lbreiu_rlin = pbllin
7.06 c     lbreiu_key    chain     lbreiu
7.06 c                   if        not %found
7.06 c                   eval      pbrfir = ldfirm
7.06 c                   eval      pbrnum = ldnumm
7.06 c                   eval      pbrsuf = ldsuff
7.06 c                   eval      pbrlin = ldline
7.06  *
7.06 c                   eval      pbrsta = '50'
7.06  *
7.06 c                   time                    pbroda
7.06 c                   time                    pbroti
7.06 c                   eval      pbrous = l_user
7.06 c                   time                    pbreda
7.06 c                   time                    pbreti
7.06 c                   eval      pbreus = l_user
7.06 c                   write     lbreiur
7.06 c                   endif
7.06 c                   endif
      *
      * Leser innmeldt og skriver plukklinje
      *
      *  Tekstlinje plukket?
      *
     c                   if        ldvare = *blank
     c                   eval      pblpan = 1
     c                   endif
      *
     c                   eval      lklfir = w_firm
     c                   eval      lklonr = lohelu_numm
     c                   eval      lklsuf = lohelu_suff
     c                   eval      lkllin = ldline
     c                   eval      lklant = pblpan
7.02 c                   eval      lklkod = '1'
      *
     c                   if        ldvare = *blank
     c                   eval      lklant = 0
7.02 c**                 eval      lklkod = '1'
     c                   endif
      *
     c                   write     lplllur
7.05  *
7.05  * Sjekk om dette er første varelinje, da skal evnt tekstlinjer over plukkes samtidig.
7.05  *
7.05 c                   eval      b_flin = *on
7.05 c                   eval      b_text = *on
7.05 c                   eval      lodtlr_numm = lohel1_numm
7.05 c                   eval      lodtlr_suff = lohel1_suff
7.05 c                   eval      lodtlr_line = pbllin
7.05 c     lodtlr_key    setll     lodtlr
7.05 c     lodtlr_key2   readpe    lodtlr
7.05  *
7.05 c                   dow       not %eof
7.05 c                   if        ldline < pbllin
7.05 c                   if        ldltyp <> 'TX'
7.05 c                   eval      b_flin = *off
7.05 c                   endif
7.05 c                   endif
7.05  *
7.05 c     lodtlr_key2   readpe    lodtlr
7.05 c                   enddo
7.05  * Tekstlinjer over skal følge første varelinje
7.05 c                   if        b_flin = *on
7.05 c                   eval      lodtlr_numm = lohel1_numm
7.05 c                   eval      lodtlr_suff = lohel1_suff
7.05 c                   eval      lodtlr_line = pbllin
7.05 c     lodtlr_key    setll     lodtlr
7.05 c     lodtlr_key2   readpe    lodtlr
7.05  *
7.05 c                   dow       not %eof
7.05 c                   if        ldline < pbllin
7.05 c                   if        ldltyp = 'TX'
7.05  * Skriv tekstlinje som skal plukkes
7.05 c                   eval      lklfir = w_firm
7.05 c                   eval      lklonr = lohelu_numm
7.05 c                   eval      lklsuf = lohelu_suff
7.05 c                   eval      lkllin = ldline
7.05 c                   eval      lklkod = '1'
7.05 c                   eval      lklant = 0
7.05 c                   write     lplllur
7.05 c                   endif
7.05 c                   endif
7.05  *
7.05 c     lodtlr_key2   readpe    lodtlr
7.05 c                   enddo
7.05  *
7.05 c                   endif
7.05  *
7.05  * Sjekk om neste linje(r) er tekstlinjer. Da skal disse også plukkes
7.05  *
7.05 c                   eval      b_text = *on
7.05 c                   eval      lodtlr_numm = lohel1_numm
7.05 c                   eval      lodtlr_suff = lohel1_suff
7.05 c                   eval      lodtlr_line = pbllin
7.05 c     lodtlr_key    setll     lodtlr
7.05 c     lodtlr_key2   reade     lodtlr
7.05  *
7.05 c                   dow       not %eof
7.05 c                   if        ldline > pbllin
7.05 c                   if        b_text = *on and
7.05 c                             ldltyp = 'TX'
7.05  * Skriv tekstlinje som skal plukkes
7.05 c                   eval      lklfir = w_firm
7.05 c                   eval      lklonr = lohelu_numm
7.05 c                   eval      lklsuf = lohelu_suff
7.05 c                   eval      lkllin = ldline
7.05 c                   eval      lklkod = '1'
7.05 c                   eval      lklant = 0
7.05 c                   write     lplllur
7.05 c                   else
7.05 c                   eval      b_text = *off
7.05 c                   endif
7.05 c                   endif
7.05  *
7.05 c     lodtlr_key2   reade     lodtlr
7.05 c                   enddo
7.05  *
     c                   endif
      *
     c     lbliir_key    reade     lbliir
     c                   enddo
      *
      * Skriv plukk heading
      *
     c                   eval      lkpfir = w_firm
     c                   eval      lkponr = lohelu_numm
     c                   eval      lkpsuf = lohelu_suff
     c                   eval      lkpoty = oty(x)
     c                   eval      lkplag = lolage
     c                   eval      lkpwsd = l_wsid
     c                   eval      lkpusr = l_user
     c                   eval      lkpdir = 1
     c                   eval      lkpres = 1
      *
     c                   write     lplolur
      *
     c                   endsr
      *
7.06  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.06  * Oppdatering av Lager
7.06  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.06  *
7.06 c     oppdat_lager  begsr
7.06  *
7.06  * Legger over verdier i overførings-felter
7.06  *
7.06 c                   move      ldvare        hlvare
7.06 c                   move      lolage        hllage
7.06 c                   move      ldenh1        hlenhe
7.06 c                   move      *loval        hldato
7.06 c                   move      *loval        hltime
7.06 c                   move      lootyp        hlotyp
7.06 c                   move      ldltyp        hlltyp
7.06  *
7.06 c                   eval      hlanta = ldanta
7.06 c                   eval      hlkopr = ldkpny
7.06  *
7.06 c                   eval      hlrefr = *blank
7.06 c                   move      'L'           hlsyst
7.06 c                   eval      hlaltk = *blank
7.06 c                   eval      hlrest = *blank
7.06 c                   move      ldnumm        w_numa
7.06 c                   move      ldline        w_lina
7.06 c                   eval      hlrefr = 'B:' + w_numa +
7.06 c                                      'L:' + w_lina
7.06 c                   eval      hlonum = ldnumm
7.06 c                   eval      hlolin = ldline
7.06 c                   move      ldlety        hllety
7.06  *
7.06  * Kaller opp lageroppdaterings-program
7.06  * dersom dette ikke er en vare hentet inn fra LVR
7.06  *
7.06 c                   if        ldlvre = 0
7.06  *
7.06 c                   eval      w_stat = *blank
7.06  *
7.06 c                   call      'VL001R'
7.06 c                   parm                    w_stat
7.06 c                   parm                    hlrec
7.06 c                   endif
7.06  *
7.06 c                   endsr
7.06  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_type
     c                   parm                    p_mnum
     c                   parm                    p_aarr
     c                   parm                    p_paid
     c                   parm                    p_pver
     c                   parm                    p_orgi
7.06 c                   parm                    p_levr
      *
      *  Key for les av bestillingshode pr. kolli
      *
     c     lbhoi2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbhoi2_hpid
      *
      *  Key for les av bestillingshode pr. kolli
      *
     c     lbhoiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbhoiu_hssc
     c                   kfld                    lbhoiu_hnum
     c                   kfld                    lbhoiu_hsuf
      *
      *  Key for les av bestillingshode pr. kolli linjer
      *
     c     lbliir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbliir_lssc
      *
      *  Key for les av pakksedddel linjer
      *
     c     lusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lusrl1_user
      *
      *  Key for file : ORDRETYPE-REGISTER
      *
     c     votyl1_key    KLIST
     c                   KFLD                    w_firm
     c                   KFLD                    votyl1_otyp
      *
      *  Key for file : BESTILLING-HODE-REGISTER
      *
     c     lohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelu_numm
     c                   kfld                    lohelu_suff
      *
      *  Key for file : Kollihode
      *
     c     lpkoi2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lpkoi2_type
     c                   kfld                    lpkoi2_sscc
     c                   kfld                    lpkoi2_aarr
     c                   kfld                    lpkoi2_paid
      *
      *  Key for file : Kollihoder
      *
     c     lpkoir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lpkoir_type
     c                   kfld                    lpkoir_mnum
     c                   kfld                    lpkoir_aarr
     c                   kfld                    lpkoir_paid
     c                   kfld                    lpkoir_pver
      *
      *  Key for file : Pakkseddelhode
      *
     c     lphoi1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lphoi1_type
     c                   kfld                    lphoi1_mnum
     c                   kfld                    lphoi1_aarr
     c                   kfld                    lphoi1_paid
     c                   kfld                    lphoi1_pver
      *
      *  Key for file : UTPLUKK-PARAM-REGISTER
      *
     c     lplol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lplol1_lonr
     c                   kfld                    lplol1_lsuf
      *
      * Key for les STATUS
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les av bestillingshode
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      * Key for les av bestillingshode
      *
     c     lohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelr_numm
     c                   kfld                    lohelr_suff
      *
      * Key for les av bestillingshode
      *
     c     lohelr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    lohelr_numm
      *
      * Key for les av bestillingslinjer
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
     c                   kfld                    lodtl1_line
7.05  *
7.05  * Key for les av bestillingslinjer
7.05  *
7.05 c     lodtlr_key    klist
7.05 c                   kfld                    w_firm
7.05 c                   kfld                    lodtlr_numm
7.05 c                   kfld                    lodtlr_suff
7.05 c                   kfld                    lodtlr_line
7.05  *
7.05  * Key for les av bestillingslinjer
7.05  *
7.05 c     lodtlr_key2   klist
7.05 c                   kfld                    w_firm
7.05 c                   kfld                    lodtlr_numm
7.05 c                   kfld                    lodtlr_suff
7.06  *
7.06  * Key for les av bestillingslinjer
7.06  *
7.06 c     lodtlu_key    klist
7.06 c                   kfld                    w_firm
7.06 c                   kfld                    lodtlu_numm
7.06 c                   kfld                    lodtlu_suff
7.06 c                   kfld                    lodtlu_line
      *
      * Key for les/oppd. av utplukksfil/hode
      *
     c     lplolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lplolu_numm
     c                   kfld                    lplolu_suff
      *
      * Key for les av plukkede linjer
      *
     c     lplllu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lplllu_lonr
     c                   kfld                    lplllu_lsuf
     c                   kfld                    lplllu_llin
7.06  *
7.06  * Key for les av leverandører
7.06  *
7.06 c     rlevl1_key    klist
7.06 c                   kfld                    w_firm
7.06 c                   kfld                    rlevl1_levr
7.06  *
7.06  *  Key for file : Pakkseddel arbeidsregister
7.06  *
7.06 c     lbreiu_key    klist
7.06 c                   kfld                    w_firm
7.06 c                   kfld                    lbreiu_rnum
7.06 c                   kfld                    lbreiu_rsuf
7.06 c                   kfld                    lbreiu_rlin
7.06  *
7.06  *  Key for file : Pakkseddel arbeidsregister
7.06  *
7.06 c     lbreiu_key2   klist
7.06 c                   kfld                    w_firm
7.06 c                   kfld                    lbreiu_rnum
      *
     c                   eval      lusrl1_user = l_user
     c                   eval      w_firm = l_firm
7.06 c                   eval      rlevl1_levr = p_levr
      *
     c                   eval      lpkoi2_type = p_type
     c                   eval      lpkoi2_aarr = p_aarr
     c                   eval      lpkoi2_paid = p_paid
      *
     c                   eval      lpkoir_type = p_type
     c                   eval      lpkoir_mnum = p_mnum
     c                   eval      lpkoir_aarr = p_aarr
     c                   eval      lpkoir_paid = p_paid
     c                   eval      lpkoir_pver = p_pver
      *
     c                   eval      lphoi1_type = p_type
     c                   eval      lphoi1_mnum = p_mnum
     c                   eval      lphoi1_aarr = p_aarr
     c                   eval      lphoi1_paid = p_paid
     c                   eval      lphoi1_pver = p_pver
     c     lphoi1_key    chain     lphoi1
      *
      *  Nullstiller felter som alltid skal være blanke/null
      *
7.06 c     rlevl1_key    chain     rlevl1
7.06 c                   if        rlrest = 1
7.06 c                   eval      a1rest = 1
7.06 c                   endif
      *
      * Hent opplysninger fra LAGER-STATUS-KODER
      *
     c     lstsl1_key    chain     lstsl1r                            90
      *
     c                   endsr
