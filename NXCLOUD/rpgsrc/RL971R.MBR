     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RL971R                                              *
      * Beskrivelse . . : Leverandører, oppdaterer saldo/kjøp i år
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Ref.  Dato   Sign Endringsbeskrivelse                                 *
      * ----- ------ ---- --------------------------------------------------- *
6.10  * V6.10 210909 BSA  Lagt inn sporingsfelter
6.11  * V6.10 070510 BSA  Endret i sporingsfelter
6.12  * V6.10 190111 sst  Rettet oppdatering saldo fra beløp til Rest
6.20  *       140214 nho  Fjernet endring 6.12, ref Mariann
      *                   Skal bruker ROBELØ og ikke ROREST                   *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frltrl1    if   e           k disk    rename(rltrpfr:rltrl1r)
     frlevl1    uf   e           k disk    rename(rlevpfr:rlevl1r)
     frlevlu    uf   e           k disk    rename(rlevpfr:rlevlur)
     fra01l1    if   e           k disk    rename(ra01pfr:ra01l1r)
      *
     d                uds
     d rpraar                300    303  0
     d rpsald                         1
     d rpkjØp                         1
     d rpkjØf                         1
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d w_akod          s                   like(raakod)
     d w_faar          s                   like(rpraar)
     d w_firm          s                   like(rlfirm)
     d w_kjØf          s                   like(rlkjØf)
     d w_kjØp          s                   like(rlkjØp)
     d w_levr          s                   like(rllevr)
     d w_saif          s                   like(rlsaif)
     d w_sald          s                   like(rlsald)
     d w_levr_gml      s                   like(rolevr)
      *
     d rlevlu_levr     s                   like(rllevr)
     d ra01l1_akod     s                   like(raakod)
      *
     c     dummy         tag
      *
      * Les leverandørposteringer
      *
     c     rltrl1_key    setll     rltrl1                                 61
     c     rltrl1_key    reade     rltrl1                                 61
      *
     c                   dow       *in61 = *off
      *
      * Oppdater leverandør ved brudd
      *
     c                   if        rolevr <> w_levr_gml
     c                   exsr      opdlev
     c                   eval      w_sald = 0
     c                   eval      w_saif = 0
     c                   eval      w_kjØp = 0
     c                   eval      w_kjØf = 0
     c                   eval      w_levr_gml = rolevr
     c                   endif
      *
      * Oppdater saldo/saldo i fjor
      *
     c                   if        rpsald = 'J'
6.12 c**                 eval      w_sald = w_sald + rorest
6.20 c                   eval      w_sald = w_sald + robelØ
      *
     c                   if        roraar < rpraar
6.12 c**                 eval      w_saif = w_saif + rorest
6.20 c                   eval      w_saif = w_saif + robelØ
     c                   endif
      *
     c                   endif
      *
      * Sjekk om bilagskode skal
      * oppdatere kjøp
      *
     c                   move      robilk        ra01l1_akod
     c     ra01l1_key    chain     ra01l1                             61
      *
     c                   if        raakjØ = '1'
      *
      * Oppdater kjøp i år
      *
     c                   if        rpkjØp = 'J' and
     c                             roraar = rpraar
     c                   eval      w_kjØp = w_kjØp + robelØ
     c                   endif
      *
      * Oppdater kjøp i fjor
      *
     c                   if        rpkjØf = 'J' and
     c                             roraar = w_faar
     c                   eval      w_kjØf = w_kjØf + robelØ
     c                   endif
      *
     c                   endif
      *
     c     rltrl1_key    reade     rltrl1                                 61
      *
     c                   enddo
      *
     c                   exsr      opdlev
      *
     c                   seton                                        lr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for nullstilling av leverandør                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     nullev        begsr
      *
     c     rlevl1_key    setll     rlevl1                                 60
     c     rlevl1_key    reade     rlevl1                                 60
      *
     c                   dow       *in60 = *off
      *
      * Nullstill felt
      *
     c                   if        rpsald = 'J'
     c                   eval      rlsald = *zeros
     c                   eval      rlsaif = *zeros
     c                   endif
      *
     c                   if        rpkjØp = 'J'
     c                   eval      rlkjØp = *zeros
     c                   endif
      *
     c                   if        rpkjØf = 'J'
     c                   eval      rlkjØf = *zeros
     c                   endif
      *
     c                   update    rlevl1r
      *
     c     rlevl1_key    reade     rlevl1                                 60
      *
     c                   enddo
      *
     c                   endsr
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for oppdatering av leverandør                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     opdlev        begsr
      *
     c                   eval      rlevlu_levr = w_levr_gml
     c     rlevlu_key    chain     rlevlu                             62
      *
     c                   if        *in62 = *off
      *
      * Oppdater saldo
      *
     c                   if        rpsald = 'J'
     c                   eval      rlsald = w_sald
     c                   eval      rlsaif = w_saif
     c                   endif
      *
      * Oppdater kjøp i år
      *
     c                   if        rpkjØp = 'J'
     c                   eval      rlkjØp = w_kjØp
     c                   endif
      *
      * Oppdater kjøp i fjor
      *
     c                   if        rpkjØf = 'J'
     c                   eval      rlkjØf = w_kjØf
     c                   endif
      *
6.10 c*                  time                    rledat
6.10 c*                  time                    rletim
6.10 c*                  eval      rlesig = l_user
6.11 c                   time                    rl2dpt
6.11 c                   eval      rlepgm = 'RL971R    '
     c                   update    rlevlur
     c                   endif
      *
     c                   endsr
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   move      l_firm        w_firm
     c                   eval      w_levr_gml = *hival
      *
      * Beregn fjorår
      *
     c                   eval      w_faar = rpraar - 1
      *
      * Nullstill leverandørregister
      *
     c                   exsr      nullev
      *
      * Key for leverandørposteringer
      *
     c     rltrl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for leverandør
      *
     c     rlevlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevlu_levr
      *
      * Key for bilagskode
      *
     c     ra01l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra01l1_akod
      *
     c                   endsr
