     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK684R                                              *
      * Beskrivelse . . : Legger ut posteringer til regnskap                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * V5.40     120405  Nytt program                                   KOB
      * V5.40     270306  Fjernet unødvendige definisjoner i local       KOB
6.20  * V6.20     180412  Flytter blank til RTPROS                       NHO
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frkwbl2    if   e           k disk    rename(rkwbpfr:rkwbl2r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fraa4l1    if   e           k disk    rename(raa4pfr:raa4l1r)
     fraa4lu    uf a e           k disk    rename(raa4pfr:raa4lur)
     fra01l1    if   e           k disk    rename(ra01pfr:ra01l1r)
     fraa9l1    if   e           k disk    rename(raa9pfr:raa9l1r)
     frbunlu    uf a e           k disk    rename(rbunpfr:rbunlur)
     frtrapf    o  a e           k disk    rename(rtrapfr:rtrapfr)
     frkw5pf    o    e           k disk    rename(rkw5pfr:rkw5pfr)
     frktrlu    uf   e           k disk    rename(rktrpfr:rktrlur)
      *
      *  LDA området
      *
     d                uds
     d  pfirm                300    302  0
     d  pnavn                303    332
     d  puser                333    342
     d  pkdat                343    348  0
     d  praar                349    352  0
     d  prper                353    354  0
     d  pbskr                469    469
     d  palfa                475    475
     d  pbilk                476    477  0
     d  pparam               300    490
      *
     d l_ruti                800    809
     d l_oflw                860    862  0
     d l_kidk                883    883  0
     d l_ocrk                885    885  0
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
610  d rbunlu_nivo     s                   like(rsnivo)
610  d rbunlu_memb     s                   like(rsmemb)
610  d w_memb          s                   like(rsmemb)
     d w_firm          s                   like(rnfirm)
     d w_kund          s                   like(rnkund)
     d w_rest          s                   like(rnrest)
     d w_biln          s                   like(rnbiln)
     d w_atxt          s                   like(raatxt)
     d w_kont          s              4  0
     d w_avde          s              4  0
     d w_spes          s              4  0
     d w_pkdat         s               d   datfmt(*iso)
     d wlinje          s              5  0
      *
     d raa4l1_4aar     s                   like(ra4aar)
     d raa4lu_4aar     s                   like(ra4aar)
     d ra01l1_akod     s                   like(raakod)
     d rbunlu_bunt     s                   like(rsbunt)
     d rktrlu_kund     s                   like(rnkund)
     d rktrlu_bdat     s                   like(rnbdat)
     d rktrlu_tist     s                   like(rntist)
      *
     c/eject
      *
     c     dummy         tag
      *
     c     rkwbl2_key    setll     rkwbl2                                 60
     c     rkwbl2_key    reade     rkwbl2                                 60
      *
     c                   dow       *in60 = *off
      *
      * Klargjør motpost til kundereskontro
      *
     c                   eval      wlinje = wlinje + 1                          Linjenr
     c                   eval      rtreid = 'T'                                 Rec -id
     c                   eval      rtfirm = rnfirm                              Firma
     c                   eval      rtbunt = rsbunt                              Bunt-nr
     c                   eval      rtlinj = wlinje                              Linjenr
     c                   eval      rtstat = *blank                              Buntstatus
     c     *dmy          move      pkdat         rtbdat                         Bilag-dato
     c                   move      praar         rtrper                         Regn år
     c                   movel     prper         rtrper                         Periode
     c                   eval      rtbiln = w_biln                              Bilag-nr
     c                   eval      rtdavd = *zeros                              Debet-avd
     c                   eval      rtdsps = *zeros                              Debet-spes
     c                   eval      rtdmva = *zeros                              Debet-mva
      *
     c                   eval      rtcavd = *zeros                              Kredit-avd
     c                   eval      rtcsps = *zeros                              Kredit-spes
     c                   eval      rtcmva = *zeros                              Kredit-mva
      *
     c                   if        rnrest > 0
     c                   eval      rtckto = rnkund                              Kredit-kto
     c                   eval      rtdkto = *zeros                              Debet-kto
     c                   eval      rtbelØ = rnrest                              Beløp
     c                   else
     c                   eval      rtdkto = rnkund                              Debet-kto
     c                   eval      rtckto = *zeros                              Kredit-kto
     c                   eval      rtbelØ = rnrest * -1                         Beløp
     c                   endif
      *
6.20 c**                 eval      rtpros = *zeros                              Prosjekt
6.20 c                   eval      rtpros = *blank                              Prosjekt
     c                   eval      rtrefn = *zeros                              Referanse
     c     *dmy          move      pkdat         rtfdat                         Forfall
      *
      *
     c                   eval      rtvalk = *blank                              Valuta-kode
      *
     c                   eval      rtakti = *blank                              Aktivitet
     c                   eval      rtmngd = *zeros                              Mengde
     c                   eval      rtmngk = *zeros                              Mengde-kode
     c                   eval      rtresn = rnkund                              Reskontronr
     c                   eval      rtrefn = rnbiln                              Reskontronr
     c                   eval      rtvirk = *blank                              Virksomhet
     c                   eval      rtatte = *zeros                              Attestert
     c                   eval      rtkrys = *blank                              Avkryssing
     c                   eval      rtkidi = *blank                              KID-referanse
     c                   eval      rtbilk = ra9bil                              Bilagskode
     c                   eval      rttext = w_atxt                              Bilagstekst
     c                   eval      rtbetb = 0                                   Betalingbet
     c                   eval      rtbref = *blank                              Bank-referanse
      *
610  c                   eval      rtnivo = *blank
610  c                   eval      rtmemb = w_memb
610  c                   time                    rtedat
610  c                   time                    rtetim
610  c                   eval      rteusr = l_user
     c                   write     rtrapfr
      *
      * Oppdater kundepostering
      *
     c                   eval      rktrlu_kund = rnkund
     c                   eval      rktrlu_bdat = rnbdat
     c                   eval      rktrlu_tist = rntist
     c     rktrlu_key    chain     rktrlu
      *
     c                   if        %found
     c     *dmy          move      pkdat         rnfada                         Bilag-dato
6.11 c                   time                    rnedat
6.11 c                   time                    rnetim
6.11 c                   eval      rneusr = l_user
     c                   update    rktrlur
     c                   endif
      *
      * Summer totalt beløp
      *
     c                   eval      w_rest = w_rest + rnrest
      *
     c     rkwbl2_key    reade     rkwbl2                                 60
     c                   enddo
      *
     c/space 3
      *
     c     slutt         tag
      *
      * Klargjør reskontropost til factoringselskapet
      *
     c                   eval      wlinje = wlinje + 1                          Linjenr
     c                   eval      rtreid = 'T'                                 Rec -id
     c                   eval      rtfirm = rnfirm                              Firma
     c                   eval      rtbunt = rsbunt                              Bunt-nr
     c                   eval      rtlinj = wlinje                              Linjenr
     c                   eval      rtstat = *blank                              Buntstatus
     c     *dmy          move      pkdat         rtbdat                         Bilag-dato
     c                   move      praar         rtrper                         Regn år
     c                   movel     prper         rtrper                         Periode
     c                   eval      rtbiln = w_biln                              Bilag-nr
     c                   eval      rtdavd = *zeros                              Debet-avd
     c                   eval      rtdkto = *zeros                              Debet-kto
     c                   eval      rtdsps = *zeros                              Debet-spes
     c                   eval      rtdmva = *zeros                              Debet-mva
      *
     c                   eval      rtcavd = *zeros                              Kredit-avd
     c                   eval      rtcsps = *zeros                              Kredit-spes
     c                   eval      rtcmva = *zeros                              Kredit-mva
6.20 c**                 eval      rtpros = *zeros                              Prosjekt
6.20 c                   eval      rtpros = *blank                              Prosjekt
     c                   eval      rtrefn = *zeros                              Referanse
     c     *dmy          move      pkdat         rtfdat                         Forfall
      *
     c                   if        w_rest > 0
     c                   eval      rtdkto = ra9knr                              Kredit-kto
     c                   eval      rtckto = *zeros                              Debet-kto
     c                   eval      rtbelØ = w_rest                              Beløp
     c                   else
     c                   eval      rtdkto = *zeros                              Kredit-kto
     c                   eval      rtckto = ra9knr                              Debet-kto
     c                   eval      rtbelØ = w_rest * -1                         Beløp
     c                   endif
      *
     c                   eval      rtvalk = *blank                              Valuta-kode
      *
     c                   eval      rtakti = *blank                              Aktivitet
     c                   eval      rtmngd = *zeros                              Mengde
     c                   eval      rtmngk = *zeros                              Mengde-kode
     c                   eval      rtresn = rnkund                              Reskontronr
     c                   eval      rtvirk = *blank                              Virksomhet
     c                   eval      rtatte = *zeros                              Attestert
     c                   eval      rtkrys = *blank                              Avkryssing
     c                   eval      rtkidi = *blank                              KID-referanse
     c                   eval      rtbilk = ra9bil                              Bilagskode
     c                   eval      rttext = w_atxt                              Bilagstekst
     c                   eval      rtbetb = 0                                   Betalingbet
     c                   eval      rtbref = *blank                              Bank-referanse
      *
610  c                   eval      rtnivo = *blank
610  c                   eval      rtmemb = w_memb
610  c                   time                    rtedat
610  c                   time                    rtetim
610  c                   eval      rteusr = l_user
     c                   write     rtrapfr
      *
      * Legg ut buntsum
      *
     c                   eval      rtbelØ = 0
E5.20c                   eval      rsfirm = w_firm                              Firma
     c                   eval      rsstat = *blank
     c                   eval      rsfØrs = 1
     c                   eval      rsbsum = *zeros
     c                   eval      rspsum = *zeros
     c                   eval      rspdeb = *zeros
     c                   eval      rspkre = *zeros
      *
610  c                   eval      rsnivo = *blank
610  c                   eval      rsmemb = w_memb
610  c                   time                    rsedat
610  c                   time                    rsetim
610  c                   eval      rseusr = l_user
     c                   write     rbunlur
      *
      * Oppdater sist brukte bilagsnummer
      *
     c                   eval      raa4lu_4aar = praar
     c     raa4lu_key    chain     raa4lu
      *
     c                   eval      ra4sgb = w_biln
      *
     c                   if        %found
     c                   update    raa4lur
     c                   else
     c                   eval      ra4fir = w_firm
     c                   eval      ra4aar = praar
     c                   write     raa4lur
     c                   endif
      *
     c                   seton                                        lr
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
610  c     *entry        plist
610  c                   parm                    w_memb                         user
      *
      * Key for transaksjonsregister
      *
     c     rkwbl2_key    klist
     c                   kfld                    w_firm
      *
      * Key for statuskode 4
      *
     c     raa4l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    raa4l1_4aar
      *
      * Key for statuskode 4
      *
     c     raa4lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    raa4lu_4aar
      *
      * Key for statuskode 9
      *
     c     raa9l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for bilagskoder
      *
     c     ra01l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra01l1_akod
      *
      * Key for buntregister
      *
     c     rbunlu_key    klist
610  c                   kfld                    rbunlu_nivo
610  c                   kfld                    rbunlu_memb
     c                   kfld                    w_firm
     c                   kfld                    rbunlu_bunt
      *
      * Key for kundeposteringer
      *
     c     rktrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrlu_kund
     c                   kfld                    rktrlu_bdat
     c                   kfld                    rktrlu_tist
      *
     c                   eval      w_firm = l_firm
      *
      * Hent statusinformasjon
      *
     c     raa9l1_key    chain     raa9l1
     c                   if        %found and
     c                             ra9reg = ' '
     c                   goto      slutt
     c                   endif
      *
      * Hent bilagstekst
      *
     c                   eval      ra01l1_akod = ra9bil
     c     ra01l1_key    chain     ra01l1
      *
     c                   if        %found
     c                   eval      w_atxt = raatxt
     c                   endif
      *
      * Behandler statusrecord nr. 04
      *
     c                   eval      raa4l1_4aar = praar
     c     raa4l1_key    chain     raa4l1
     c                   if        %found
     c                   eval      w_biln = ra4sgb
     c                   else
     c                   eval      w_biln = 0
     c                   endif
      *
     c                   eval      w_biln = w_biln + 1
      *
      *  Finn ledig buntnr. for poster
      *
610  c                   eval      rbunlu_nivo = *blank
610  c                   eval      rbunlu_memb = w_memb
     c                   eval      rbunlu_bunt = 99999
     c     rbunlu_key    setgt     rbunlu
     c                   readp     rbunlu                                 61
      *
     c   61              eval      rsbunt = 1
     c  n61              eval      rsbunt = rsbunt + 1
      *
     c                   move      praar         rsraar
     c                   movel     prper         rsrper
     c                   move      wlinje        rssist
     c                   move      udate         rspdat
      *
     c                   endsr
