     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RS203R                                              *
      * Beskrivelse . . : Kontroll av gyldig betalingsbetingelser             *
      *                   Beregner forfallsdato
      *                                                                     - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                     - *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       020100 HKO  Nytt program                                        *
      *       090703 KOB  Skrevet om etter modell av tilsvarende i Teko       *
R5.60 *       190807 KOB  Lagt inn behandling av "fri 15"                     *
      *                                                                       *
      * ----- ------ ---- -------------------------------------------------
      *                                                                       *
      * Bruk av returkode:                                                    *
      *                                                                       *
      *  Kode i statusfelt(p_stat = N) : post finnes ikke                     *
      *                                                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fra03l1    if   e           k disk    rename(ra03pfr:ra03l1r)
      *
      * Definering av parametre
      *
     d p_stat          s              1
     d p_ckod          s                   like(rackod)
     d p_ctxt          s                   like(ractxt)
     d p_ctx2          s                   like(ractx2)
     d p_ffda          s               d   datfmt(*iso)
     d p_fkda          s               d   datfmt(*iso)
     d p_rda1          s               d   datfmt(*iso)
     d p_rda2          s               d   datfmt(*iso)
     d p_rda3          s               d   datfmt(*iso)
      *
23063 * Datastruktur for dato - 8 siffer
      *
     d                 ds
     d w_dato                         8  0
     d  w_aar                         4  0 overlay(w_dato)
     d  w_mnd                         2  0 overlay(w_dato:5)
     d  w_dag                         2  0 overlay(w_dato:7)
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable i nøkler
      *
     d ra03l1_ckod     s                   like(rackod)
      *
      * Definering av variabler
      *
     d w_dagr          s              3  0
      *
     d w_firm          s                   like(racfir)
      *
      * Sjekke kode
      *
     c                   eval      ra03l1_ckod = p_ckod                                      .
     c     ra03l1_key    chain     ra03l1r
      *
     c                   if        not%found                                             .....
     c                   eval      p_stat = 'N'                                              .
     c                   eval      p_ctxt = 'Koden er ugyldig'                               .
     c                   eval      p_ctx2 = *blank                                           .
     c                   eval      w_dagr = 0
     c                   else                                                            .....
     c                   eval      p_stat = *blank                                           .
     c                   eval      p_ctxt = ractxt                                           .
     c                   eval      p_ctx2 = ractx2                                           .
     c                   eval      w_dagr =  racdag
     c                   endif                                                           .....
      *
      * Finner forfallsdato
      *
      *
      * Forklaring på beregning :
      * -------------------------
      * Dersom det er fri leveringsmåned, skal man først finne siste
      * dag i måneden, før eventuelle dager legges til.  Dette blir
      * gjort med å sette dato = 1, og legge til 1 på måned.  Deretter
      * trekkes 1 ifra på datofeltet, for å få siste dag i foregående måned.
      * --------------------------------------------------------------------
      *
      * Hvis dato ikke utfylt settes den dagens dato
      *
     c                   if        p_fkda = *loval
     c                   time                    p_fkda
     c                   endif
      *
      * Flytte over dato til et felt med 8 pos
      *
     c                   move      p_fkda        w_dato
      *
      * Sett dag = 01 dersom fri leveringsmåned
      *
     c                   if        racjan = 'JA ' or
     c                             racjan = ' JA'
     c                   movel     01            w_dag
     c                   endif
R5.60 *
R5.60 * Sett dag = 15 dersom fri 15 og dag < 15
R5.60 *
R5.60c                   if        racjan = '15 ' or
R5.60c                             racjan = ' 15'
R5.60c                   if        w_dag <= 15
R5.60c                   movel     15            w_dag
R5.60c                   else
R5.60c                   movel     01            w_dag
R5.60c                   endif
R5.60c                   endif
      *
      * Flytte over dato til forfallsdat
      *
     c                   move      w_dato        p_ffda
      *
      * Finn første dag i ny måned
      *
     c                   if        racjan = 'JA ' or
     c                             racjan = ' JA'
     c                   adddur    1:*months     p_ffda
     c                   subdur    1:*days       p_ffda
     c                   endif
R5.60 *
R5.60 * Finn første dag i ny måned hvis fri 15
R5.60 *
R5.60c                   if        racjan = '15 ' or
R5.60c                             racjan = ' 15'
R5.60c                   if        w_dag = 01
R5.60c                   adddur    1:*months     p_ffda
R5.60c                   subdur    1:*days       p_ffda
R5.60c                   endif
R5.60c                   endif
      *
      * Beregne forfallsdato
      *
     c                   adddur    w_dagr: *d    p_ffda
      *
      * Finner forfallsdato for rabatt 1, dersom rabatt-% er utfylt.
      *
     c                   if        racrab <> 0
     c                   eval      w_dagr = racda1 - racdag
     c                   move      p_ffda        p_rda1
     c     p_rda1        adddur    w_dagr : *d   p_rda1
     c                   endif
      *
      * Finner forfallsdato for rabatt 2, dersom rabatt-% er utfylt.
      *
     c                   if        racra2 <> 0
     c                   eval      w_dagr = racda2 - racdag
     c                   move      p_ffda        p_rda2
     c     p_rda2        adddur    w_dagr : *d   p_rda2
     c                   endif
      *
      * Finner forfallsdato for rabatt 3, dersom rabatt-% er utfylt.
      *
     c                   if        racra3 <> 0
     c                   eval      w_dagr = racda3 - racdag
     c                   move      p_ffda        p_rda3
     c     p_rda3        adddur    w_dagr : *d   p_rda3
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   eval      w_firm = l_firm
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_stat
     c                   parm                    p_ckod
     c                   parm                    p_ctxt
     c                   parm                    p_ctx2
     c                   parm                    p_fkda
     c                   parm                    p_ffda
     c                   parm                    p_rda1
     c                   parm                    p_rda2
     c                   parm                    p_rda3
      *
      * Key for kode
      *
     c     ra03l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra03l1_ckod
      *
     c                   endsr
