     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JP100R
      * Beskrivelse . . : Vedlikehold av betingelser, param
      *
      *                   Velger leverandør fra liste
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       190598 HKO  Nytt program
      *       150905 HKO  Lagt inn frisøk på leverandør
6.30  *       030714 bof  Utvidet med fraktbeløp
6.31  * 6.30  201014 bsa  Jacada justeringer i skjermbildet
6.3q  *       260515 bof  Datoformat sql
7.xx  * 7.00  280716 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
8.01  *PRG2   200622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjlevl2    if   e           k disk    rename(jlevpfr:jlevl2r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fjbetl1    if   e           k disk    rename(jbetpfr:jbetl1r)
6.30 fjfbel1    if   e           k disk    rename(jfbepfr:jfbel1r)
      *
     fjp100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d jlevl2_navn     s                   like(jlnavn)
     d jlevl1_ldor     s                   like(jlldor)
     d jbetl1_ldor     s                   like(jpldor)
6.30 d jfbel1_fldo     s                   like(jpfldo)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_open_sok      s              1    inz(*off)
      *
     d w_ste1          s             35
     d w_ste2          s             35
     d w_ste3          s             35
     d w_ste4          s             35
     d w_ste5          s             35
      *
     d s_stek          s             35
      *
     d w_firm          s                   like(jpfirm)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
6.3q C/End-Exec
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile                                  CRT SFL
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * F9=Gjenta søk
      *
     c                   if        *inki = *on
     c                   eval      *in22 = *on
     c                   eval      b2stek = s_stek
     c                   goto      b2tagb
     c                   endif
      *
      * Søking
      *
     c                   if        b2stek <> *blank
     c                   exsr      sØk
     c                   goto      b2tagb
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2navn <> *blank or
     c                             b2ldor <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'S'
     c                   eval      b_open_sok = *on
     c                   when      w_seqe = 'L'
     c                   eval      jlevl1_ldor = b1ldor
     c     jlevl1_key    setll     jlevl1
     c                   other
     c                   eval      jlevl2_navn = b1navn
     c     jlevl2_key    setll     jlevl2
     c                   endsl
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for søking i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sØk           begsr
      *
     c                   eval      w_seqe = 'S'
     c                   eval      b_open_sok = *on
      *
     c                   eval      s_stek = b2stek
      *
      * Utleder søketekster ut av søketekst
      *
     c                   call      'AS701R'
     c                   parm                    b2stek
     c                   parm                    w_ste1
     c                   parm                    w_ste2
     c                   parm                    w_ste3
     c                   parm                    w_ste4
     c                   parm                    w_ste5
      *
      * Blanker subfile, og fyller opp i.h.t. søk
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker søkefelt
      *
     c                   eval      b2stek = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Navn
      *
     c                   if        b2navn <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      jlevl2_navn = b2navn
     c     jlevl2_key    setll     jlevl2
     c                   goto      end_pos
     c                   endif
      *
      * Leverandør
      *
     c                   if        b2ldor <> 0
     c                   eval      w_seqe = 'L'
     c                   eval      jlevl1_ldor = b2ldor
     c     jlevl1_key    setll     jlevl1
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2navn = *blank
     c                   eval      b2ldor = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Betingelser
      *
     c                   if        b1valg = 7
     c                   call      'JP102R'
     c                   parm                    w_firm
     c                   parm                    b1ldor
     c                   eval      b_forn = *on
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
6.30  *
6.30  * Frakt betingelser
6.30  *
6.30 c                   if        b1valg = 9
6.30 c                   call      'JP120R'
6.30 c                   parm                    w_firm
6.30 c                   parm                    b1ldor
6.30 c                   eval      b_forn = *on
6.30 c                   eval      b1valg = 0
6.30 c                   update    b1sfl
6.30 c                   iter
6.30 c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
      *
      * Initierer SQL for søk
      *
     c                   if        w_seqe = 'S' and
     c                             b_open_sok = *on
     c/exec sql
     c+                  declare sok cursor for
     c+                  select jlldor,
     c+                         jlnavn,
     c+                         jlkvav,
     c+                         jlkmod,
     c+                         jlkkal,
     c+                         jlenum
     c+                  from   jlevpf
     c+                  where  upper(char(jlldor)
     c+                          concat(jlnavn) concat(jladr1)
     c+                          concat(jladr2) concat(jlponr)
     c+                          concat(jlsted))
     c+                         like :w_ste1 and
     c+                         upper(char(jlldor)
     c+                          concat(jlnavn) concat(jladr1)
     c+                          concat(jladr2) concat(jlponr)
     c+                          concat(jlsted))
     c+                         like :w_ste2 and
     c+                         upper(char(jlldor)
     c+                          concat(jlnavn) concat(jladr1)
     c+                          concat(jladr2) concat(jlponr)
     c+                          concat(jlsted))
     c+                         like :w_ste3 and
     c+                         upper(char(jlldor)
     c+                          concat(jlnavn) concat(jladr1)
     c+                          concat(jladr2) concat(jlponr)
     c+                          concat(jlsted))
     c+                         like :w_ste4 and
     c+                         upper(char(jlldor)
     c+                          concat(jlnavn) concat(jladr1)
     c+                          concat(jladr2) concat(jlponr)
     c+                          concat(jlsted))
     c+                         like :w_ste5
     c+                  order by jlnavn, jlldor
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok
     c/end-exec
     c/exec sql
     c+                  open sok
     c/end-exec
     c                   endif
      *
      * Fyller opp subfile
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'S'
     c/exec sql
     c+                  fetch next
     c+                  from  sok
     c+                  into  :jlldor,
     c+                        :jlnavn,
     c+                        :jlkvav,
     c+                        :jlkmod,
     c+                        :jlkkal,
     c+                        :jlenum
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'L'
     c                   read      jlevl1                                 15
     c                   other
     c                   read      jlevl2                                 15
     c                   endsl
      *
     c                   if        *in15                                        EOF
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = *blank
     c                   eval      jlevl2_navn = jlnavn
     c                   when      w_seqe = 'L'
     c                   eval      jlevl1_ldor = jlldor
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1navn = jlnavn
     c                   eval      b1ldor = jlldor
     c                   eval      b1bet  = *blank
      *
     c                   eval      jbetl1_ldor = b1ldor
     c     jbetl1_key    chain     jbetl1
     c                   if        %found
     c                   eval      b1bet  = 'X'
     c                   endif
6.30  *
6.30 c                   eval      b1fbe  = *blank
6.30 c                   eval      jfbel1_fldo = b1ldor
6.30 c     jfbel1_key    chain     jfbel1
6.30 c                   if        %found
6.30 c                   eval      b1fbe  = 'X'
6.30 c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   eval      b_open_sok = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        w_seqe = 'S'
     c                   exsr      forny
     c                   leavesr
     c                   endif
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'L'
     c     jlevl1_key    setgt     jlevl1
     c                   readp     jlevl1
     c                   other
     c     jlevl2_key    setgt     jlevl2
     c                   readp     jlevl2
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   readp     jlevl1
     c                   other
     c                   readp     jlevl2
     c                   endsl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   eval      jlevl1_ldor = jlldor
     c                   other
     c                   eval      jlevl2_navn = jlnavn
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'L'
     c     jlevl1_key    setll     jlevl1
     c                   other
     c     jlevl2_key    setll     jlevl2
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på navn
      *
     c     jlevl2_key    klist
     c                   kfld                    jlevl2_navn
      *
      * Key for posisjonering på leverandørnummer
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på betingelse
      *
     c     jbetl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jbetl1_ldor
6.30  *
6.30  * Key for oppslag på frakt betingelse
6.30  *
6.30 c     jfbel1_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    jfbel1_fldo
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      jlevl2_navn = *blank
     c     jlevl2_key    setll     jlevl2
      *
     c                   eval      *in22 = *on
     c                   exsr      crt_subfile
      *
     c                   endsr
