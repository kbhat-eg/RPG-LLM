      /TITLE  Oppslag mot rutine-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : AP608R
      * Beskrivelse . . : Oppslag mot PDF parametre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- - - - - - - - - - - - - - - - - - - - - - - - - -
      *       010910 BSA  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     fafopl1    if   e           k disk    rename(afoppfr:afopl1r)
     fafapl1    if   e           k disk    rename(afappfr:afapl1r)
     fafpslr    if   e           k disk    rename(afpspfr:afpslrr)
      *
     d                uds
     d l_filg                931    933
     d l_firm                944    946  0
     d l_avde                947    950  0
      *
      * Definering av parametre
      *
     d p_ruti          s                   like(afprut)
     d p_pdf           s              1
      *
      * Definering av variabler i nøkler
      *
     d afopl1_ruti     s                   like(afprut)
     d afapl1_arut     s                   like(afarut)
     d afapl1_afir     s                   like(afafir)
     d afapl1_aavd     s                   like(afaavd)
     d afpslr_ufil     s                   like(l_filg)
     d afpslr_uide     s                   like(afuide)
      *
      * Definering av variable
      *
     d d_pdf           ds
     d  d_mail                        1  0
     d  d_exlp                        1  0
     d  d_arki                        1  0
     d  d_skje                        1  0
     d  d_land                        1  0
     d  d_xmlm                       75
     d  d_jasm                       75
     d  d_logo                       75
     d  d_emal                       50
      *
      *
     d w_pdf_ds        ds
     d  w_xmlm                       75
     d  w_jasm                       75
     d  w_logo                       75
     d  w_path                      100
     d  w_emal                       50
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Hent inn post fra formularregister                              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c                   eval      p_pdf = '0'
      * Sjekk først om PROA er aktivert
     c                   eval      afpslr_ufil = l_filg
     c                   eval      afpslr_uide = 'STATUS    '
     c     afpslr_key    chain     afpslrr
     c                   if        not %found
     c                   eval      p_pdf = '0'
     c                   else
     c                   movel     afudss        p_pdf
     c                   endif
      *
      * Hvis aktiv, sjekk at også rutina er aktiv
      *
     c                   if        p_pdf = '1'
      *
     c                   eval      afopl1_ruti = p_ruti
     c     afopl1_key    chain     afopl1r
      *
     c                   if        %found
     c                   if        afppdf = 1
     c                   eval      p_pdf = '1'
     c                   elseif    afppdf = 2
     c                   eval      p_pdf = '1'
     c                   elseif    afppdf = 0
     c                   eval      p_pdf = '0'
     c                   endif
     c                   else
     c                   eval      p_pdf = '0'
     c                   endif
      *
     c                   endif
      *
     c                   if        p_pdf = '1' and
     c                             afppdf = 2
      * Sjekk til slutt om den er deaktivert på avdelingsnivå.
     c                   eval      afapl1_arut = p_ruti
     c                   eval      afapl1_afir = l_firm
     c                   eval      afapl1_aavd = l_avde
     c     afapl1_key    chain     afapl1r
     c                   if        %found
     c                   if        afapdf = 0
     c                   eval      p_pdf = '0'
     c                   endif
     c                   else
     c                   eval      p_pdf = '0'
     c                   endif
      *
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_ruti
     c                   parm                    p_pdf
      *
      * Key for rutineregister
      *
     c     afopl1_key    klist
     c                   kfld                    afopl1_ruti
      *
      * Key for avvikende pdf informasjon
      *
     c     afapl1_key    klist
     c                   kfld                    afapl1_arut
     c                   kfld                    afapl1_afir
     c                   kfld                    afapl1_aavd
      *
      * Key for propertiesfil
      *
     c     afpslr_key    klist
     c                   kfld                    afpslr_ufil
     c                   kfld                    afpslr_uide
      *
     c                   endsr
      *
