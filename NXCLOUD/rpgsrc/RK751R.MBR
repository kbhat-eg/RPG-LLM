      /title  ASOKON Kunde, oppdaterer søketekst på alle kunder
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON
      * Program . . . . : RK751R
      * Beskrivelse . . : Kunde, oppdaterer søketekst på alle kunder
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       300399 HKO  Nytt program
      *       020500 HKO  Fjernet blanke fra telefonnummer før det legges
      *                   ut som søketekst.
      *                   Lagt mobilnummer ut som søketekst
      * R5.10 100603 KOB  Lagt inn alfafelt først i søketekst
      * R5.20 101103 KOB  Lagt inn foretaksnummer i søketekst
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frksolu    uf a e           k disk    rename(rksopfr:rksolur)
      *
      * Definering av LDA
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d rksolu_skun     s                   like(rkskun)
      *
      * Definering av variable
      *
     d w_l             s              2  0
     d w_leng          s              2  0
     d w_kund_alf      s              6
     d w_ftnr_alf      s              9
      *
     d w_firm          s                   like(rkfirm)
     d w_tlfn          s                   like(rktlfn)
     d w_mobn          s                   like(rkmobn)
      *
      * Definering av konstanter
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøå'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅ'
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     rkunl1_key    setll     rkunl1
     c     rkunl1_key    reade     rkunl1                                 90
     c                   dow       *in90 = *off
      *
     c                   exsr      oppdater
      *
     c     rkunl1_key    reade     rkunl1                                 90
     c                   enddo
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer søketekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
      * Sletter eksisterende søketekst
      *
     c                   eval      rksolu_skun = rkkund
     c     rksolu_key    chain     rksolur                            91
     c                   if        *in91 = *off
     c                   delete    rksolur
     c                   endif
      *
      * Oppretter ny søketekst....
      *
     c                   eval      rksfir = w_firm
     c                   eval      rkskun = rkkund
      *
      * Alfa-felt
      *
     c                   eval      rkstek = rkalfa
      *
      * Navn
      *
     c     Lo:Up         xlate     rknavn        rknavn
     c                   eval      rkstek = %trimr(rkstek) + ' ' +
     c                                      %trim(rknavn)
      *
      * Adresse-1
      *
     c                   if        rkgate <> *blank
     c     Lo:Up         xlate     rkgate        rkgate
     c                   eval      rkstek = %trimr(rkstek) + ' ' +
     c                                      %trim(rkgate)
     c                   endif
      *
      * Adresse-2
      *
     c                   if        rkadr2 <> *blank
     c     Lo:Up         xlate     rkadr2        rkadr2
     c                   eval      rkstek = %trimr(rkstek) + ' ' +
     c                                      %trim(rkadr2)
     c                   endif
      *
      * Poststed
      *
     c                   if        rksted <> *blank
     c     Lo:Up         xlate     rksted        rksted
     c                   eval      rkstek = %trimr(rkstek) + ' ' +
     c                                      %trim(rksted)
     c                   endif
      *
      * Telefonnummer
      *
     c                   if        rktlfn <> *blank
      *
     c                   eval      w_tlfn = %trim(rktlfn)
     c                   eval      w_leng = %len(%trim(rktlfn))
      *
     c     ' '           scan      w_tlfn        w_l
     c                   dow       w_l <> 0 and
     c                             w_l <  w_leng
     c                   eval      w_tlfn = %subst(w_tlfn:1:w_l-1) +
     c                                      %subst(w_tlfn:w_l+1:11-w_l)
     c                   eval      w_tlfn = %trim(w_tlfn)
     c                   eval      w_leng = %len(%trim(w_tlfn))
     c     ' '           scan      w_tlfn        w_l
     c                   enddo
      *
     c                   eval      rkstek = %trimr(rkstek) + ' ' +
     c                                      %trim(w_tlfn)
      *
     c                   endif
      *
      * Mobiltelefon-nummer
      *
     c                   if        rkmobn <> *blank
      *
     c                   eval      w_mobn = %trim(rkmobn)
     c                   eval      w_leng = %len(%trim(rkmobn))
      *
     c     ' '           scan      w_mobn        w_l
     c                   dow       w_l <> 0 and
     c                             w_l <  w_leng
     c                   eval      w_mobn = %subst(w_mobn:1:w_l-1) +
     c                                      %subst(w_mobn:w_l+1:11-w_l)
     c                   eval      w_mobn = %trim(w_mobn)
     c                   eval      w_leng = %len(%trim(w_mobn))
     c     ' '           scan      w_mobn        w_l
     c                   enddo
      *
     c                   eval      rkstek = %trimr(rkstek) + ' ' +
     c                                      %trim(w_mobn)
      *
     c                   endif
      *
      * Kundenummer
      *
     c                   move      rkkund        w_kund_alf
      *
     c                   eval      rkstek = %trimr(rkstek) + ' ' +
     c                                      w_kund_alf
      *
      * Foretaksnummer
      *
     c                   if        rkftnr <> 0
     c                   move      rkftnr        w_ftnr_alf
     c                   eval      rkstek = %trimr(rkstek) + ' ' +
     c                                      w_ftnr_alf
     c                   endif
      *
      * Oppretter søketekst
      *
     c                   write     rksolur
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les på kunderegister (sortert alfabetisk)
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppdatering av søketekst-register
      *
     c     rksolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rksolu_skun
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
