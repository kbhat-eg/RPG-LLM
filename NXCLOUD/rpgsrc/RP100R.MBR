     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RP100R
      * Beskrivelse . . : Prosjekt, vedlikehold 2013 -
      *
      * Spesialversjon. :             Prosj.leder
      * Tilpasset for . :             opprettet dato
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       150213 sst  Nytt program
      *       190213 sst  NB!!!!!  Ev. kontroll av prosjketleder gjøres i
      *                   klienten mot ILONPF.  Bør gjøres på samme måte her
      *                   og at denne oppdateres 'automatisk' fra H&L Lønn
6.21  *       160514 sst  Justert parameter til utskrift.
6.22  *       250215 bof  Div. justeringer ihht. spec.
6.30  *       250216 bsa  F taster flyttet i skjermbildet. Linjenr 24 skal ALDRI
6.30  *                   brukes til annet enn meldinger.
6.31  *       210416 nho  Ikke mulig å taste blank i prosj.nr
6.32  *       150917 sst  Lagt inn valg av underprosjekt og aktivitet
6.33  *       301117 sst  Ikke legge ut selgernr i utvalgsfeltet
6.34  *       301117 sst  Hent pr.leder fra pr.leder(P) eller Selger(S)
6.35  *       011217 sst  Ikke reg budsjett dersom delt inn i Upr/akt
6.36  *       310518 sst  Fjernet nullstilling av underprosjekt og akt.
6.36  *       310518 sst  Overgang til underprosjekt etter oppretting
6.37  *       240418 bkv  Endret dato format (6.32 i std)
7.xx  * 7.00  020316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
9.01  *       290319 sst  Nytt felt for Prosjketkategori (Kl&Kl)
9.01  *                   Feilmelding via AA007R
9.02  *       020419      Spørring på Prosjektkategori
9.03  *       220819 sst  Prosjektregnskap Fase 2,  August 2019
      *_____________________________________________________________________________________________
Vers. *Kxx _Dato_ Usr Ref.____ Beskrivelse
      *_____________________________________________________________________________________________
7.00  *K00 061020 sst NXO-1234 Frisøk på Prosjekt
8.01  * 800   300125 ask  Lagt inn kontroll mot ikke utfylt startdato på prosjekt
      *_____________________________________________________________________________________________

      *  Bruk av indikatorer:
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frp1pl1    if   e           k disk    rename(rp1ppfr:rp1pl1r)
     frp1pl2    if   e           k disk    rename(rp1ppfr:rp1pl2r)
     frp1pl3    if   e           k disk    rename(rp1ppfr:rp1pl3r)
     frp1pl4    if   e           k disk    rename(rp1ppfr:rp1pl4r)
     frp1plr    if   e           k disk    rename(rp1ppfr:rp1plrr)
     frp1plu    uf a e           k disk    rename(rp1ppfr:rp1plur)
     frp2ul1    if   e           k disk    rename(rp2upfr:rp2ulr1)
     frp3al1    if   e           k disk    rename(rp3apfr:rp3alr1)
     frp4olu    uf a e           k disk    rename(rp4opfr:rp4olur)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     filonl1    if   e           k disk    rename(ilonpfr:ilonl1r)
     fra07l1    if   e           k disk    rename(ra07pfr:ra07l1r)
6.34 fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
     frprrl1    if   e           k disk    rename(rprrpfr:rprrl1r)
6.22 frhtrlc    if   e           k disk    rename(rhtrpfr:rhtrlcr)
6.22 fausrl1    if   e           k disk    rename(ausrpfr:ausrl1r)
6.22 ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
9.02 frpkol1    if   e           k disk    rename(rpkopfr:rpkol1r)
9.03 fanumlu    uf a e           k disk    rename(anumpfr:anumlur)
      *
     frp100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
4.44 d lo              c                   'abcdefghijklmnopqrstuvwxyzæøå'
4.44 d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅ'
      *
      * Definering av Local data area
      *
     d                udS
     d l_user                911    920
     d l_filg                931    933
     d l_firm                944    946  0
     d l_avde                947    950  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d rp1pl1_pros     s                   like(rp1pro)
     d rp1pl2_besk     s                   like(rp1alf)
     d rp1pl2_pros     s                   like(rp1pro)
     d rp1pl3_avd      s                   like(rp1avd)
     d rp1pl3_pros     s                   like(rp1pro)
     d rp1pl4_prl      s                   like(rp1prl)
     d rp1pl4_pros     s                   like(rp1pro)
     d rp1plr_pros     s                   like(rp1pro)
     d rp1plu_pros     s                   like(rp1pro)
     d rp2ul1_pros     s                   like(rp2pro)
     d rp3al1_pros     s                   like(rp3pro)
     d rp4olu_arbo     s                   like(rp4ord)
     d rkunl1_kund     s                   like(rkkund)
     d ilonl1_kli      s                   like(ilrkli)
     d ilonl1_lnr      s                   like(ilrlnr)
6.34 d ra09l1_fir      s                   like(raifir)
6.34 d ra09l1_kod      s                   like(raikod)
     d ra07l1_frm      s                   like(ragfir)
     d ra07l1_kod      s                   like(ragkod)
     d rprrl1_fir      s                   like(rphfir)
     d rprrl1_rap      s                   like(rphrap)
     d rprrl1_lin      s                   like(rphlin)
     d rprrl1_uli      s                   like(rphuli)
6.22 d rhtrlc_fir      s                   like(rbfirm)
6.22 d rhtrlc_pro      s                   like(rbhpro)
6.22 d fusrl1_fir      s                   like(fbfirm)
6.22 d fusrl1_usr      s                   like(fbuser)
6.22 d ausrl1_usr      s                   like(abuser)
9.02 d rpkol1_firm     s                   like(rpkfir)
9.02 d rpkol1_type     s                   like(rpktyp)
9.02 d rpkol1_kode     s                   like(rpkkod)
9.03 d anumlu_fell     s                   like(anfell)
9.03 d anumlu_type     s                   like(antype)
9.03 d anumlu_fast     s                   like(anfast)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_oppr          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_subf          s              1    inz(*off)
      *
     d w_firm          s              3  0
     d s_pros          s                   like(rp1pro)
     d s_aktiv         s                   like(b2aktiv)
     d s_avd           s                   like(b2vavd)
     d s_prl           s                   like(b2vprl)
      *
     d w_pknr          s                   like(rkkund)
     d w_pkna          s                   like(rknavn)
     d w_dato          s             10
6.37 d*w_datd          s                   like(rp1fsd)
6.22 d w_1alf          s                   like(rp1alf)
     d w_datx          s             10
     d w_datn          s              6  0
     d w_lnr           s                   like(ilrlnr)
6.34 d w_lnr4          s              4  0
     d w_lnav          s                   like(ilrnav)
     d p_avd           s                   like(ilravd)
     d p_tx30          s             30
     d p_rapx          s              4
9.03 d p_fdat          s              6  0
9.03 d p_tdat          s              6  0
9.03 d p_knav          s             30
9.03 d p_selg          s              4  0
9.03 d p_fkun          s              6  0
9.03 d p_pros          s              6
9.03 d p_upro          s              6     inz(' ')
9.03 d p_akti          s              6     inz(' ')
9.03 d p_kpro          s              6  0
     d w_fper          s              6  0
     d p_rapt          s             30
     d p_firm          s              3
     d p_kont          s              5  0
     d p_text          s             30
     d p_txt1          s             30
     d w3avde          s              4
     d w3prol          s              5
6.22 d w_sumi          s              8  0
6.22 d w_sumix         s             11  0
6.22 d w_sumk          s              8  0
6.22 d w_sumkx         s             11  0
6.22 d w_buo           s              8  0
6.22 d w_but           s              8  0
     d w_rest          s              8  0
     d w_pros          s              8  0
6.22 d w_mld1          s             50
6.22 d w_stat          s              1
6.22 d w_spor          s              1    inz('5')
6.22 d w_endr          s              1    inz('2')
     d w_a51           s              1  0 inz(0)
     d w_tilgko        s             10
     d w_rutine        s             10
     d w_adv           s              3
     d w_status        s             10
     d w_vare          s              3    inz('Mgr')
     d w_upro          s                   like(rp2upr)
     d                                      inz(' ')
     d w_akti          s                   like(rp3akt)
     d                                      inz(' ')
     d w_blank         s                   like(rp1pro)
     d                                      inz(' ')
9.01 d w_m1            s             50
9.01 d w_m2            s             50
9.01 d w_sv            s              1
9.01 d w_in12          s              1  0
9.01 d w_pkod          s              4
9.01 d w_ptxt          s             30
9.01 d w_type          s              4
9.01 d w_kost          s              9  0
     d x_upro          s                   like(rp1pro)
     d x_akti          s                   like(rp1pro)
     d x_kont          s              5  0
7.00 d scan1           s             20
7.00 d scan2           s             20
7.00 d scan3           s             20
7.00 d scan4           s             20
7.00 d scan5           s             20
7.00 d l1              s              3  0
7.00 d l2              s              3  0
7.00 d l3              s              3  0
7.00 d l4              s              3  0
7.00 d l5              s              3  0
7.00 d i               s              3  0
7.00 d w_txt           s             70
7.00 d wtxt            s             70
7.00 d w_frit          s             70
      *
6.34  * u_prlslg styr om Pr.leder hentes fra prosj.leder(P) eller selger(S)
6.34 d u_prlslg        s              1    inz('S')
9.03  * u_kravprk styrer om det er krav om prosjekt kategori  'PROSJ_KRAV_PRK'
9.03 d u_kravprk       s              1    inz(*off)
9.03  *
9.03  * Definering av eksterne prg
9.03  * BKV
9.03   dcl-s co402_verdi1  char(1);
9.03   dcl-s co402_verdi2  char(2048);
9.03   DCL-PR CO402R EXTPGM('CO402R');
9.03     p_filgrp            char(3)     const;
9.03     p_firm              packed(3:0) const;
9.03     p_lager             packed(2:0) const;
9.03     p_nokkel            char(50)    const;
9.03     p_verdi1            char(1);
9.03     p_verdi2            char(2048);
9.03   END-PR;
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(12)
7.xx d c_sepw          c                   const(X'22')
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Derwsom man kommer fra Spørreprogram RP600R hoppes direkte til oppretting av prosjekt
      *
     c                   if        p_fdat > *zero
     c                   exsr      xc1bld
6.36  *  Gå ev videre til underprosjekt
6.36 c                   if        c2rup = 'J'
6.36 c                   call      'RP110R'
6.36 c*****              parm                    w_endr
9.03 c                   parm                    p_fdat
9.03 c                   parm                    p_tdat
     c                   parm                    p_txt1
9.03 c                   parm                    p_selg
9.03 c                   parm                    p_fkun
6.36 c                   parm                    c1pros
9.03 c                   parm                    p_kpro
9.03 c                   parm                    p_knav
6.36  *  Gå ev videre til aktivitet
6.36 c                   else
6.36 c                   if        c2rak = 'J'
6.36 c                   call      'RP120R'
6.36 c                   parm                    c1pros
6.36 c                   parm                    w_upro
6.36 c                   parm                    b1valg
6.36 c                   endif
6.36 c                   endif
     c                   goto      avslutt
     c                   endif
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
6.36  *  Gå ev videre til underprosjekt
6.36 c                   if        c2rup = 'J'
6.36 c                   call      'RP110R'
6.36 c                   parm                    p_fdat
6.36 c                   parm                    p_tdat
9.03 c                   parm                    p_txt1
9.03 c                   parm                    p_selg
9.03 c                   parm                    p_fkun
9.03 c                   parm                    c1pros
9.03 c                   parm                    p_kpro
9.03 c                   parm                    p_knav
6.36  *  Gå ev videre til aktivitet
6.36 c                   else
6.36 c                   if        c2rak = 'J'
6.36 c                   call      'RP120R'
6.36 c                   parm                    c1pros
6.36 c                   parm                    w_upro
6.36 c                   parm                    b1valg
6.36 c                   endif
6.36 c                   endif
     c                   goto      b2taga
6.22 c                   when      *inkg
6.22 c                   eval      c3frap = *blank
6.22 c                   eval      c3tilp = *blank
6.22 c                   eval      c3hit  = *blank
6.22 c                   exsr      xc3bld
6.22 c                   goto      b2taga
     c                   when      *inkj
     c                   eval      c3frap = *blank
     c                   exsr      xc3bld
     c                   goto      b2taga
     c                   when      *inkk
     c                   select
     c                   when      w_a51 = 0
7.xx c                   eval      b2hea1 = 'Budsj.Innt.'
7.xx c                   eval      b2hea2 = 'Budsj.Kost.'
7.xx c                   eval      b2hea3 = '   Inntekt'
7.xx c                   eval      b2hea4 = '   Kostnad'
     c                   eval      w_a51 = 1
     c                   when      w_a51 = 1
7.xx c                   eval      b2hea1 = 'Budsj.Innt.'
7.xx c                   eval      b2hea2 = 'Virk.Innt.'
7.xx c                   eval      b2hea3 = ' Gjennstår'
7.xx c                   eval      b2hea4 = '  Utført %'
     c                   eval      w_a51 = 2
     c                   when      w_a51 = 2
7.xx c                   eval      b2hea1 = 'Budsj.Kost.'
7.xx c                   eval      b2hea2 = 'Virk.Kost.'
7.xx c                   eval      b2hea3 = ' Gjennstår'
7.xx c                   eval      b2hea4 = '  Utført %'
     c                   eval      w_a51 = 3
     c                   when      w_a51 = 3
7.xx c*                   eval      b2head = 'Fra dato    Til dato     -
7.xx c*                             Avd.     Pr.leder'
7.xx c                   eval      b2hea1 = 'Fra dato'
7.xx c                   eval      b2hea2 = 'Til dato'
7.xx c                   eval      b2hea3 = 'Avd.'
7.xx c                   eval      b2hea4 = 'Pr.leder'
     c                   eval      w_a51 = 0
     c                   endsl
6.22 c                   exsr      forny
6.22 c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Utvalg   Aktiv/alle
      *
     c                   if        b2aktiv <> s_aktiv
     c                   eval      s_aktiv  = b2aktiv
     c                   eval      s_avd    = b2vavd
     c                   eval      s_prl    = b2vprl
     c                   exsr      forny
     c                   goto      b2tagb
     c                   endif
      *
      * Utvalg   avdeling
      *
     c                   if        b2vavd  <> s_avd
     c                   eval      s_avd    = b2vavd
     c                   eval      s_prl    = b2vprl
     c                   exsr      forny
     c                   goto      b2tagb
     c                   endif
      *
      * Utvalg   prosjektleder
      *
     c                   if        b2vprl  <> s_prl
     c                   eval      s_prl    = b2vprl
     c                   exsr      forny
     c                   goto      b2tagb
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2pros <> *blank or
     c                             b2besk <> *blank or
     c                             b2avd  <> *zero  or
     c                             b2prl  <> *zero
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   if        b_subf = *on
     c                   goto      b2taga
     c                   endif
      *
      * Retur til første bilde
      *
     c                   goto      b2tagb
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   select
     c                   when      w_seqe = 'B'
     c     rp1pl2_key    setll     rp1pl2
     c                   when      w_seqe = 'C'
     c     rp1pl3_key    setll     rp1pl3
     c                   when      w_seqe = 'D'
     c     rp1pl4_key    setll     rp1pl4
     c                   other
     c     rp1pl1_key    setll     rp1pl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på prosjekt
      *
     c                   if        b2pros <> *blank
     c                   eval      w_seqe = ' '
     c                   eval      rp1pl1_pros = b2pros
     c     rp1pl1_key    setll     rp1pl1
     c                   endif
      *
      * Posisjoner startverdi på teskt
      *
     c                   if        b2besk <> *blank
7.01 c                   eval      w_seqe = ' '
7.01 c                   eval      rp1pl1_pros = *blank
7.01 c     rp1pl1_key    setll     rp1pl1
     c                   endif
      *
      * Posisjoner startverdi på avdeling
      *
     c                   if        b2avd  <> *zero
     c                   eval      w_seqe = 'C'
     c                   eval      rp1pl3_avd = b2avd
     c                   eval      rp1pl3_pros = *blank
     c     rp1pl3_key    setll     rp1pl3
     c                   endif
      *
      * Posisjoner startverdi på prosj.leder
      *
     c                   if        b2prl  <> *zero
     c                   eval      w_seqe = 'D'
     c                   eval      rp1pl4_prl = b2prl
     c                   eval      rp1pl4_pros = *blank
     c     rp1pl4_key    setll     rp1pl4
     c                   endif
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2pros = *blank
     c                   eval      b2besk = *blank
     c                   eval      b2avd  = *zero
     c                   eval      b2prl  = *zero
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
     c                   eval      b_subf = *off
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      b_subf = *on
     c                   eval      w_virn = w_srrn
      *
      * Spøør på Status
      *
     c                   if        b1valg = 1
     c                   eval      w_upro = *blank
     c                   eval      w_akti = *blank
     c                   call      'RP151R'
     c                   parm                    w_firm
     c                   parm                    b1pros
9.03 c                   parm                    w_upro
9.03 c                   parm                    w_akti
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Endre post
      *
     c                   if        b1valg = 2
     c                   eval      c1pros = b1pros
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
      *  Gå ev videre til underprosjekt
     c                   if        not *inkc
     c                   if        c2rup = 'J'
     c                   call      'RP110R'
     c                   parm                    p_fdat
     c                   parm                    p_tdat
9.03 c                   parm                    p_txt1
9.03 c                   parm                    p_selg
9.03 c                   parm                    p_fkun
9.03 c                   parm                    c1pros
9.03 c                   parm                    p_kpro
9.03 c                   parm                    p_knav
      *  Gå ev videre til aktivitet
     c                   else
     c                   if        c2rak = 'J'
     c                             and not *inkc
     c                   call      'RP120R'
     c                   parm                    c1pros
     c                   parm                    w_upro
     c                   parm                    b1valg
     c                   endif
     c                   endif
     c                   endif
     c                   exsr      forny
     c                   iter
     c                   endif
      *
      * Kopiere post
      *
     c                   if        b1valg = 3
     c                   eval      k1pros = b1pros
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slette post
      *
     c                   if        b1valg = 4
     c                   eval      d1pros = b1pros
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise post
      *
     c                   if        b1valg = 5
     c                   eval      c1pros = b1pros
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
      *  Gå ev videre til underprosjekt
     c                   if        not *inkc
     c                   if        c2rup = 'J'
     c                   call      'RP110R'
     c                   parm                    p_fdat
     c                   parm                    p_tdat
9.03 c                   parm                    p_txt1
9.03 c                   parm                    p_selg
9.03 c                   parm                    p_fkun
9.03 c                   parm                    c1pros
9.03 c                   parm                    p_kpro
9.03 c                   parm                    p_knav
      *  Gå ev videre til aktivitet
     c                   else
     c                   if        c2rak = 'J'
     c                             and not *inkc
     c                   call      'RP120R'
     c                   parm                    c1pros
     c                   parm                    w_upro
     c                   parm                    b1valg
     c                   endif
     c                   endif
     c                   endif
     c                   exsr      forny
     c                   iter
     c                   endif
      *  Gå ev videre til underprosjekt
     c*                  if        c2rup = 'J'
     c*                  call      'RP110R'
     c*                  parm                    p_fdat
     c*                  parm                    p_tdat
9.03 c*                  parm                    p_txt1
9.03 c*                  parm                    p_selg
9.03 c*                  parm                    p_fkun
9.03 c*                  parm                    c1pros
9.03 c*                  parm                    p_kpro
9.03 c*                  parm                    p_knav
      *  Gå ev videre til aktivitet
     c*                  else
     c*                  call      'RP120R'
     c*                  parm                    c1pros
     c*                  parm                    w_spor
     c*                  endif
     c*                  iter
     c*                  endif
      *
      * Skrive rapport
      *
     c                   if        b1valg = 6
     c                   eval      c3frap = b1pros
6.21 c                   eval      c3tilp = b1pros
     c                   exsr      xc3bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
6.22  *
6.22  * Se på varelinjer ordre
6.22  *
6.22 c                   if        b1valg = 7
      *
     c                   if        w_vare = 'Mgr'
     c                   eval      p_kont = *zero
     c                   eval      p_text = *blank
     c                   call      'RP145R  '
     c                   parm                    b1pros
     c                   parm                    w_blank
     c                   parm                    w_blank
     c                   parm                    p_kont
     c                   parm                    p_text
      *
     c                   else
6.22 c                   call      'RP555R  '
6.22 c                   parm                    w_firm
6.22 c                   parm                    b1pros
6.22 c                   parm                    w_blank
6.22 c                   parm                    w_blank
     c                   endif
      *
6.22 c                   eval      b1valg = 0
6.22 c                   update    b1sfl
6.22 c                   iter
6.22 c                   endif
6.22  *
6.22  * Vise poster
6.22  *
6.22 c                   if        b1valg = 8
6.22 c                   call      'RP510R  '
6.22 c                   parm                    w_firm
6.22 c                   parm                    b1pros
6.22 c                   eval      b1valg = 0
6.22 c                   update    b1sfl
6.22 c                   iter
6.22 c                   endif
6.22  *
6.22  * Se på bestillinger
6.22  *
6.22 c                   if        b1valg = 9
6.22 c                   call      'RP550R  '
6.22 c                   parm                    w_firm
6.22 c                   parm                    b1pros
6.22 c                   eval      b1valg = 0
6.22 c                   update    b1sfl
6.22 c                   iter
6.22 c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1pros = *blank
9.03  *
9.03  *  Sjekk om automatisk tildeling av teller
9.03  *
9.03 c                   eval      anumlu_fell = 'RPROSJ'
9.03 c                   eval      anumlu_type = *blank
9.03 c                   eval      anumlu_fast = *blank
9.03 c     anumlu_key    chain     anumlu
9.03 c                   if        %found(anumlu)
9.03 c     next_rprosj   tag
9.03 c                   eval      ansist = ansist + 1
9.03 c                   move      ansist        c1pros
9.03 c                   eval      rp1plr_pros = c1pros
9.03 c     rp1plr_key    chain     rp1plr
9.03 c                   if        %found(rp1plr)
9.03 c                   goto      next_rprosj
9.03 c                   endif
9.03 c                   update    anumlur
9.03 c                   endif
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
6.31 c                   if        c1pros = *blank
6.31 c                   goto      end_c1bld
6.31 c                   endif
      *
      * Validering
      *
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      rp1plr_pros = c1pros
     c     rp1plr_key    chain     rp1plr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      *in23 = *on
     c                   exsr      xc2bld
      *
     c     end_c1bld     tag
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   eval      b_oppr = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      rp1plr_pros = c1pros
     c     rp1plr_key    chain     rp1plr
      *
     c                   if        %found
     c                   eval      c2besk = rp1txt
9.01 c                   eval      c2prka = %subst(rp1tx2:1:4)
     c*                  eval      c2fsd  = rp1fsd
6.37 c*                  move      rp1fsd        c2fsd
8.01 c                   if        rp1fsd <> *loval
6.37 c     *dmy          move      rp1fsd        c2fsd
8.01 c                   else
8.01 c                   eval      c2fsd = *zero
8.01 c                   endif
6.37 c*                  eval      c2fed  = rp1fed
7.01 c                   if        rp1fed <> *loval
6.32 c     *dmy          move      rp1fed        c2fed
7.01 c                   else
7.01 c                   eval      c2fed = *zero
7.01 c                   endif
     c                   eval      c2prl  = rp1prl
6.34 c                   if        u_prlslg = 'P'
     c                   eval      ilonl1_kli  = l_firm
     c                   eval      ilonl1_lnr  = rp1prl
     c     ilonl1_key    chain     ilonl1
     c                   if        %found(ilonl1)
     c                   eval      c2prx  = ilrnav
     c                   else
     c                   eval      c2prx  = *blank
     c                   endif
6.34 c                   else
6.34 c                   eval      ra09l1_fir  = l_firm
6.34 c                   eval      ra09l1_kod  = rp1prl
6.34 c     ra09l1_key    chain     ra09l1
6.34 c                   if        %found(ra09l1)
6.34 c                   eval      c2prx  = raitxt
6.34 c                   else
6.34 c                   eval      c2prx  = *blank
6.34 c                   endif
6.34 c                   endif
     c                   eval      c2avd  = rp1avd
     c                   eval      ra07l1_kod  = rp1avd
     c     ra07l1_key    chain     ra07l1
     c                   if        %found(ra07l1)
     c                   eval      c2avx  = ragtxt
     c                   else
     c                   eval      c2avx  = *blank
     c                   endif
     c                   eval      c2knr  = rp1knr
     c                   eval      rkunl1_kund = rp1knr
     c     rkunl1_key    chain     rkunl1
     c                   if        %found(rkunl1)
     c                   eval      c2kna  = rknavn
     c                   else
     c                   eval      c2kna  = *blank
     c                   endif
     c                   eval      c2buo  = rp1buo
6.22 c                   eval      c2but  = rp1but
6.35 c*                  if           rp1rak = 'J'
6.35 c*                            or rp1rup = 'J'
6.35 c*                  exsr      getbudsj
6.35 c*                  endif
     c                   eval      c2pty  = rp1pty
     c                   eval      c2sts  = rp1sts
6.32 c                   eval      c2rup  = rp1rup
6.32 c                   eval      c2rak  = rp1rak
     c*                  eval      c2alf  = rp1alf
     c                   eval      c2eusr = rp1eav
     c     *dmy          move      rp1eda        c2edat
     c                   else
9.03 c                   eval      c2besk = p_knav
6.37 c*                  eval      c2fsd = *blank
6.37 c                   eval      c2fsd  = 0
6.37 c*                  eval      c2fed = *blank
6.37 c                   eval      c2fed  = 0
9.03 c                   move      p_fdat        c2fsd
9.03 c                   move      p_tdat        c2fed
9.03 c                   eval      c2prl  = p_selg
      *  Mestergruppen vedr c2avd
     c                   eval      c2avd  = l_avde
9.03 c                   eval      c2knr  = p_fkun
     c                   eval      c2pty  = *blank
     c                   if        c2knr <> *zero
     c                   eval      c2pty = 'K'
     c                   endif
     c                   eval      c2sts  = *blank
6.32 c                   eval      c2rup  = *blank
6.32 c                   eval      c2rak  = *blank
     c                   eval      c2buo  = *zero
6.22 c                   eval      c2but  = *zero
     c                   eval      c2kna  = *blank
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   eval      c2eusr = *blank
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1pros = k1pros
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   setoff                                       232425
     c                   setoff                                       262728
9.01 c                   setoff                                       41
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      *   Overgang til varelinjer sven
      *
     c                   if        *inki
     c                   eval      x_upro = *blank
     c                   eval      x_akti = *blank
     c                   eval      x_kont = *zero
     c                   call      'RP145R'
     c                   parm                    c1pros
     c                   parm                    x_upro
     c                   parm                    x_akti
     c                   parm                    x_kont
     c                   parm                    c2besk
     c                   goto      c2tagb
     c                   endif
      *
      * Spørring
      *
      *  Prosjektleder
     c                   if        *inkd = *on and
5.33 c                             w_afld = 'C2PRL '
     c                   eval      w_lnr       = *zero
     c                   eval      w_lnav      = *blank
6.34 c                   if        u_prlslg = 'P'
4.88 c                   call      'RP603R  '
4.55 c                   PARM                    w_firm
     c                   PARM                    w_lnr
     c                   PARM                    w_lnav
6.34 c                   else
6.34 c                   eval      w_lnr4 = *zero
6.34 c                   call      'RA509R  '
6.34 c                   PARM                    w_firm
6.34 c                   PARM                    w_lnr4
6.34 c                   PARM                    w_lnav
6.34 c                   eval      w_lnr = w_lnr4
6.34 c                   endif
     c                   if        w_lnr <> *zero
     c                   eval      c2prl  = w_lnr
     c                   eval      c2prx  = w_lnav
     c                   endif
     c                   goto      c2tagb
     c                   endif
      *  Avdeling
     c                   if        *inkd = *on and
5.33 c                             w_afld = 'C2AVD '
     c                   eval      p_avd       = *zero
     c                   eval      p_tx30      = *blank
4.88 c                   call      'RA507R  '
4.55 c                   PARM                    w_firm
     c                   PARM                    p_avd
     c                   PARM                    p_tx30
     c                   if        p_avd <> *zero
     c                   eval      c2avd  = p_avd
     c                   eval      c2avx  = p_tx30
     c                   endif
     c                   goto      c2tagb
     c                   endif
      *  Kunde
     c                   if        *inkd = *on and
5.33 c                             w_afld = 'C2KNR '
     c                   eval      w_pknr      = *zero
     c                   eval      w_pkna      = *blank
4.88 c                   call      'RK500R  '
4.55 c                   PARM                    w_firm
     c                   PARM                    w_pknr
     c                   PARM                    w_pkna
     c                   eval      c2knr  = w_pknr
     c                   eval      c2kna  = w_pkna
     c                   goto      c2tagb
     c                   endif
9.02  *  Prosjekt type
9.02 c                   if        *inkd = *on and
9.02 c                             w_afld = 'C2PRKA'
9.02 c                   eval      w_type      = 'PRKA'
9.02 c                   eval      w_pkod      = *blank
9.02 c                   eval      w_ptxt      = *blank
9.02 c                   call      'RP002R  '
9.02 c                   parm                    w_firm
9.02 c                   parm                    w_type
9.02 c                   parm                    w_pkod
9.02 c                   parm                    w_ptxt
9.02 c                   eval      c2prka = w_pkod
9.02 c*                  eval      c2prkt = w_ptxt
9.02 c                   goto      c2tagb
9.02 c                   endif
9.02  *  Budsjettert kost
9.02 c                   if        *inkd = *on and
9.02 c                             w_afld = 'C2BUT'
9.03 c                   eval      w_kost = c2but
9.02 c                   call      'RP140R  '
9.02 c                   parm                    w_firm
9.02 c                   parm                    c1pros
9.02 c                   parm                    p_upro
9.02 c                   parm                    p_akti
9.02 c                   parm                    p_kpro
9.02 c                   parm                    w_kost
     c                   if        w_kost <> 0
9.02 c                   eval      c2but  = w_kost
     c                   endif
9.02 c                   goto      c2tagb
9.02 c                   endif
6.22  *  prosjekt raport.
6.22 c                   if        *inkf = *on
     c                   eval      c3frap = c1pros
6.21 c                   eval      c3tilp = c1pros
6.22 c                   exsr      xc3bld
6.22 c                   goto      c2tagb
6.22 c                   endif
6.22  *  Spørring prosjektposter
6.22 c                   if        *inkg = *on
6.22 c                   call      'RP510R  '
6.22 c                   parm                    w_firm
6.22 c                   parm                    c1pros
6.22 c                   goto      c2tagb
6.22 c                   endif
6.22  *  Spørring ordre /Bestilling
6.22 c                   if        *inkh = *on
6.22 c                   call      'RP550R  '
6.22 c                   parm                    w_firm
6.22 c                   parm                    c1pros
6.22 c                   goto      c2tagb
6.22 c                   endif
      *
      * Validering av input
      *
     c                   if        c2besk = *blank
     c                   eval      *in23 = *on
     c                   goto      c2tagb
     c                   endif
      *  Start Dato
6.37 c*                  if        c2fsd = *blank
6.37 c                   if        c2fsd = 0
     c                   eval      *in24 = *on
     c                   goto      c2tagb
     c                   endif
6.37 c*                  eval      w_dato = c2fsd
6.37 c                   eval      w_datn = c2fsd
6.37 c*                  eval      w_datn = *zero
6.37 c*                  exsr      reddato
      *
     c                   if        w_datn > *zero
     c     *dmy          test (d)                w_datn                 25
     c                   if        *in25 = *on
     c                   goto      c2tagb
     c                   else
6.37 c*    *dmy          move      w_datn        w_datd
6.37 c*                  move      w_datd        c2fsd
     c                   endif
     c                   endif
      *  Slutt Dato
6.37 c*                  if        c2fed = *blank
6.37 c                   if        c2fed = 0
     c                   eval      *in26 = *on
     c                   goto      c2tagb
     c                   endif
6.37 c*                  eval      w_dato = c2fed
6.37 c                   eval      w_datn = c2fed
6.37 c*                  eval      w_datn = *zero
6.37 c*                  exsr      reddato
      *
     c                   if        w_datn > *zero
     c     *dmy          test (d)                w_datn                 27
     c                   if        *in27 = *on
     c                   goto      c2tagb
     c                   else
6.37 c*    *dmy          move      w_datn        w_datd
6.37 c*                  move      w_datd        c2fed
     c                   endif
     c                   endif
6.22  *  Prosjektleder
6.22 c                   if        c2prl  <> 0
6.34 c                   if        u_prlslg = 'P'
6.22 c                   eval      ilonl1_kli  = l_firm
6.22 c                   eval      ilonl1_lnr  = c2prl
6.22 c     ilonl1_key    chain     ilonl1
6.22 c                   if        not %found(ilonl1)
6.22 c                   eval      c2prx  = ilrnav
6.22 c                   eval      *in37 = *on
6.22 c                   goto      c2tagb
6.22 c                   endif
6.34 c                   else
6.34 c                   eval      ra09l1_fir  = l_firm
6.34 c                   eval      ra09l1_kod  = c2prl
6.34 c     ra09l1_key    chain     ra09l1
6.34 c                   if        %found(ra09l1)
6.34 c                   eval      c2prx  = raitxt
6.34 c                   else
6.34 c                   eval      *in37 = *on
6.34 c                   goto      c2tagb
6.34 c                   endif
6.34 c                   endif
6.22 c                   endif
      *  Kunde
     c                   if        c2knr  = *zero and
     c                             c2pty  = 'K'
     c                   eval      *in28 = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        c2knr  > *zero
     c                   eval      rkunl1_kund = c2knr
     c     rkunl1_key    chain     rkunl1
     c                   if        not %found(rkunl1)
     c                   eval      *in29 = *on
     c                   goto      c2tagb
     c                   endif
     c                   endif
      *  Prosjkettype
     c                   if        c2pty <> 'I ' and
     c                             c2pty <> 'K '
     c                   eval      *in35 = *on
     c                   goto      c2tagb
     c                   endif
      *  Prosjektkategori
     c                   if        c2prka = *blank
     c                             and u_kravprk = *on
     c                   eval      *in49 = *on
     c                   goto      c2tagb
     c                   endif
      *  Prosjketstatus
     c                   if        c2sts <> 'A' and
     c                             c2sts <> 'O' and
     c                             c2sts <> ' '
     c                   eval      *in36 = *on
     c                   goto      c2tagb
     c                   endif
6.32  *  Bruk av underprosjekt
6.32 c                   if        c2rup <> 'J' and
6.32 c                             c2rup <> ' '
6.32 c                   eval      *in38 = *on
6.32 c                   goto      c2tagb
6.32 c                   endif
6.32  *  Bruk av aktivitet
6.32 c                   if        c2rak <> 'J' and
6.32 c                             c2rak <> ' '
6.32 c                   eval      *in39 = *on
6.32 c                   goto      c2tagb
6.32 c                   endif
6.35  *
6.35  *  Ikke Budjsett dersom inndleing i underprosjekt eller aktivitet
6.35 c                   if          (c2rup = 'J'
6.35 c                             or c2rak = 'J')
6.35 c                             and (c2buo <> 0
6.35 c                             or   c2but <> 0)
6.35 c                   eval      *in40 = *on
6.35 c                   goto      c2tagb
6.35 c                   endif
9.01  *
9.01  *  Prosjekt kategori:  foreløpig gylødige er KK = Klappet & Klart
9.01 c                   if            c2prka <> *blank
9.01 c                   eval      rpkol1_firm = w_firm
9.01 c                   eval      rpkol1_type = 'PRKA'
9.01 c                   eval      rpkol1_kode = c2prka
     c     rpkol1_key    chain     rpkol1
     c                   if        not %found(rpkol1)
9.01 c                   eval      *in12 = *off
9.01 c                   eval      w_sv  = *blank
9.01 c                   eval      w_m1  = 'Prosjekt Kategori finnes ikke.'
9.01 c                   eval      w_m2  = 'Trykk F4 for å se gyldige koder'
9.01 c                   call      'AA007R'
9.01 c                   parm                    w_m1
9.01 c                   parm                    w_m2
9.01 c                   parm                    w_sv
9.01 c                   parm                    w_in12
9.01 c                   eval      *in41 = *on
9.01 c                   goto      c2tagb
9.01 c                   endif
9.01 c                   endif
      *
      * Oppdatering
      *
     c                   clear                   rp1plur
     c                   clear                   rp4olur
      *
     c                   eval      rp1plu_pros = c1pros
     c     rp1plu_key    chain     rp1plur
      *
     c                   eval      rp1txt = c2besk
5.4d c     lo:up         xlate     c2besk        rp1alf
     c*                  eval      rp1fsd = c2fsd
6.37 c     *dmy          move      c2fsd         rp1fsd
     c*                  eval      rp1fed = c2fed
6.37 c     *dmy          move      c2fed         rp1fed
     c                   eval      rp1prl = c2prl
     c                   eval      rp1avd = c2avd
     c                   eval      rp1knr = c2knr
     c                   eval      rp1buo = c2buo
6.22 c                   eval      rp1but = c2but
     c                   eval      rp1pty = c2pty
     c                   eval      rp1sts = c2sts
6.32 c                   eval      rp1rup = c2rup
6.32 c                   eval      rp1rak = c2rak
9.01 c                   eval      %subst(rp1tx2:1:4) = c2prka
      *
     c                   time                    rp1eda
     c                   time                    rp1eti
     c                   eval      rp1eav = l_user
      *
     c                   if        %found
     c                   update    rp1plur
     c                   else
     c                   eval      rp1frm = w_firm
     c                   eval      rp1pro = c1pros
9.03 c                   eval      p_pros = c1pros
6.36 c**                 eval      rp1rup = ' '
6.36 c**                 eval      rp1rak = ' '
     c                   eval      rp1rao = 'N'
     c                   time                    rp1oda
     c                   time                    rp1oti
     c                   eval      rp1oav = l_user
     c                   write     rp1plur
     c                   endif
      *
     c                   eval      rp4olu_arbo = c1pros
     c     rp4olu_key    chain     rp4olur
     c                   eval      rp4txt = c2besk
     c*                  eval      rp4fsd = c2fsd
6.37 c     *dmy          move      c2fsd         rp4fsd
     c*                  eval      rp4fed = c2fed
6.37 c     *dmy          move      c2fed         rp4fed
     c                   eval      rp4prl = c2prl
      *
     c                   time                    rp4eda
     c                   time                    rp4eti
     c                   eval      rp4eav = l_user
      *
     c                   if        %found
     c                   update    rp4olur
     c                   else
     c                   eval      rp4frm = w_firm
     c                   eval      rp4pro = c1pros
     c                   eval      rp4ord = c1pros
     c                   time                    rp4eda
     c                   time                    rp4eti
     c                   time                    rp4oda
     c                   time                    rp4oti
     c                   eval      rp4oav = l_user
     c                   write     rp4olur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1besk = rp1txt
     c                   eval      b1fsd = rp1fsd
     c                   eval      b1fed = rp1fed
     c                   eval      b1avd  = rp1avd
     c                   eval      b1prl  = rp1prl
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
6.35  * __________________________________________________________
6.35  *    Hente budjsett fra Underprosjekt eller aktivitet
6.35  *
6.35 c     getbudsj      begsr
6.35  *
6.35 c                   eval      rp2ul1_pros = rp1pro
6.35 c                   eval      rp3al1_pros = rp1pro
6.35 c                   eval      c2buo = *zero
6.35 c                   eval      c2but = *zero
6.35 c                   if        rp1rak = 'J'
6.35 c     rp3al1_key    setll     rp3al1
6.35 c     rp3al1_key    reade     rp3al1
6.35 c                   dow       not %eof(rp3al1)
6.35 c                   eval      c2buo = c2buo + rp3buo
6.35 c                   eval      c2but = c2but + rp3but
6.35 c     rp3al1_key    reade     rp3al1
6.35 c                   enddo
6.35 c                   else
6.35 c     rp2ul1_key    setll     rp2ul1
6.35 c     rp2ul1_key    reade     rp2ul1
6.35 c                   dow       not %eof(rp2ul1)
6.35 c                   eval      c2buo = c2buo + rp2buo
6.35 c                   eval      c2but = c2but + rp2but
6.35 c     rp2ul1_key    reade     rp2ul1
6.35 c                   enddo
6.35 c                   endif
6.35  *
6.35 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle utskrift av prosjekt  (C3BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc3bld        begsr
      *
     c                   eval      w_arow = 3
     c                   eval      w_acol = 5
     c                   eval      c3fper = *blank
     c                   eval      w_fper = 200000 + uyear*100 + umonth
     c                   move      w_fper        c3tper
     c                   eval      c3avde = *zero
     c                   eval      c3prol = *zero
     c                   eval      c3rapp = *zero
     c                   eval      c3aktiv = '1'
      *
     c     c3tagb        tag
      *
     c                   exfmt     c3bld
      *
     c                   if        *inkc or *inkl
     c                   goto      end_c3bld
     c                   endif
      *
      * Spørring
      *
6.22  *  Prosjekt fra
6.22 c                   if        *inkd = *on and
6.22 c                             w_afld = 'C3FRAP'
6.22 c                   call      'RP500R  '
6.22 c                   PARM                    w_firm
6.22 c                   PARM                    c3frap
6.22 c                   PARM                    w_1alf
6.22 c                   goto      c3tagb
6.22 c                   endif
6.22  *  Prosjekt til
6.22 c                   if        *inkd = *on and
6.22 c                             w_afld = 'C3TILP'
6.22 c                   call      'RP500R  '
6.22 c                   PARM                    w_firm
6.22 c                   PARM                    c3tilp
6.22 c                   PARM                    w_1alf
6.22 c                   goto      c3tagb
6.22 c                   endif
      *  Prosjektleder
     c                   if        *inkd = *on and
5.33 c                             w_afld = 'C3PROL'
     c                   eval      w_lnr       = *zero
     c                   eval      w_lnav      = *blank
6.34 c                   if        u_prlslg = 'P'
4.88 c                   call      'RP603R  '
4.55 c                   PARM                    w_firm
     c                   PARM                    w_lnr
     c                   PARM                    w_lnav
6.34 c                   else
6.34 c                   eval      w_lnr4 = *zero
6.34 c                   call      'RA509R  '
6.34 c                   PARM                    w_firm
6.34 c                   PARM                    w_lnr4
6.34 c                   PARM                    w_lnav
6.34 c                   eval      w_lnr = w_lnr4
6.34 c                   endif
     c                   if        w_lnr <> *zero
     c                   move      w_lnr         c3prol
     c                   eval      c3prox = w_lnav
     c                   endif
     c                   goto      c3tagb
     c                   endif
      *  Avdeling
     c                   if        *inkd = *on and
5.33 c                             w_afld = 'C3AVDE'
     c                   eval      p_avd       = *zero
     c                   eval      p_tx30      = *blank
4.88 c                   call      'RA507R  '
4.55 c                   PARM                    w_firm
     c                   PARM                    p_avd
     c                   PARM                    p_tx30
     c                   if        p_avd <> *zero
     c                   move      p_avd         c3avde
     c                   eval      c3avdx = p_tx30
     c                   endif
     c                   goto      c3tagb
     c                   endif
      *  Rapport
     c                   if        *inkd = *on and
5.33 c                             w_afld = 'C3RAPP'
     c                   move      w_firm        p_firm
     c                   eval      p_rapx      = *zero
     c                   eval      p_rapt      = *blank
4.88 c                   call      'RP604R  '
     c                   PARM                    p_firm
     c                   PARM                    p_rapx
     c                   PARM                    p_rapt
     c                   if        p_rapx <> *zero
     c                   move      p_rapx        c3rapp
     c                   movel     p_rapt        c3rapt
     c                   endif
     c                   goto      c3tagb
     c                   endif
      *
     c                   move      c3avde        w3avde
     c                   move      c3prol        w3prol
      *   blank = alle, '1' = aktive '2' = sluttmeldte '3'=Aktive m/poster
     c                   eval      w_stat = c3aktiv
      *
      *   Rapport
     c                   move      *zero         p_rapx
     c                   move      c3rapp        p_rapx
     c                   eval      rprrl1_fir = w_firm
     c                   eval      rprrl1_rap = c3rapp
     c                   eval      rprrl1_lin = *zero
     c                   eval      rprrl1_uli = *zero
     c     rprrl1_key    chain     rprrl1
     c                   if        %found(rprrl1) and
     c                             rphfun = 'K'
     c                   call      'RP802C '
     c                   parm                    c3fper
     c                   parm                    c3tper
     c                   parm                    c3frap
6.22 c                   parm                    c3tilp
     c                   parm                    p_rapx
     c                   parm                    w3avde
     c                   parm                    w3prol
6.22 c                   parm                    w_stat
6.22 c                   parm                    c3hit
     c                   else
     c                   call      'RP801C '
     c                   parm                    c3fper
     c                   parm                    c3tper
     c                   parm                    c3frap
     c                   parm                    c3tilp
     c                   parm                    p_rapx
     c                   parm                    w3avde
     c                   parm                    w3prol
6.22 c                   parm                    w_stat
6.22 c                   parm                    c3hit
     c                   endif
      *
      *
     c     end_c3bld     tag
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å redigere inntastet dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     reddato       begsr
      *
     c                   eval      w_datx = w_dato
     c                   if        %subst(w_dato:5:1) = '-' and
     c                             %subst(w_dato:8:1) = '-'
     c                   goto      redend
     c                   endif
      *
     c                   if        w_dato = *blank
     c                   movel     udate         w_datx
     c                   else
     c                   dow       %subst(w_datx:1:1) = ' '
     c                   eval      %subst(w_datx:1:9) = %subst(w_datx:2:9)
     c                   eval      %subst(w_datx:10:1) = ' '
     c                   enddo
     c                   endif
     c                   if        %subst(w_dato:2:9) = *blank
     c                   eval      %subst(w_datx:01:1) = '0'
     c                   eval      %subst(w_datx:02:1) = %subst(w_dato:1:1)
     c                   elseif    %subst(w_dato:3:8) = *blank
     c                   eval      %subst(w_datx:01:2) = %subst(w_dato:1:2)
     c                   elseif    %subst(w_dato:5:6) = *blank
     c                   eval      %subst(w_datx:01:2) = %subst(w_dato:1:2)
     c                   eval      %subst(w_datx:03:2) = %subst(w_dato:3:2)
     c                   elseif    %subst(w_dato:7:4) = *blank
     c                   eval      %subst(w_datx:01:2) = %subst(w_dato:1:2)
     c                   eval      %subst(w_datx:03:2) = %subst(w_dato:3:2)
     c                   eval      %subst(w_datx:05:2) = %subst(w_dato:5:2)
     c                   endif
     c                   movel     w_datx        w_datn
     c     redend        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      rp1plr_pros = d1pros
     c     rp1plr_key    chain     rp1plr
      *
6.22  * sjekker om det finnes poster, da kan man ikke slette
6.22  *
6.22 c                   eval      rhtrlc_fir = w_firm
6.22 c                   eval      rhtrlc_pro = d1pros
6.22 c     rhtrlc_key    setll     rhtrlc
6.22 c     rhtrlc_key    reade     rhtrlc
6.22 c                   dow       not %eof(rhtrlc)
6.22 c                   eval      w_mld1 = 'Kan ikke slettes, de+
6.22 c                                       t finnes transaksjone+
6.22 c                                       r '
6.22 c                   call      'AA005R'
6.22 c                   parm                    w_mld1
6.22 c                   leavesr
6.22 c                   enddo
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      rp1plu_pros = d1pros
     c     rp1plu_key    chain     rp1plur
     c                   if        %found(rp1plu)
     c                   delete    rp1plur
     c                   endif
     c                   eval      rp4olu_arbo = d1pros
     c     rp4olu_key    chain     rp4olur
     c                   if        %found(rp4olu)
     c                   delete    rp4olur
     c                   endif
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_pros = k1pros
      *
      * Fyller kopierings-bildet
      *
     c                   eval      rp1plr_pros = k1pros
     c     rp1plr_key    chain     rp1plr
      *
     c     k1tagb        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1pros = *blank
     c                   eval      *in31 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      rp1plr_pros = k1pros
     c     rp1plr_key    chain     rp1plr
     c                   if        %found
     c                   eval      *in33 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1pros = s_pros
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *    debug
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
7.00  *
7.00  *   Del opp frisøk i inntil 5 ord
7.00 c                   eval      scan1 = *blank
7.00 c                   eval      scan2 = *blank
7.00 c                   eval      scan3 = *blank
7.00 c                   eval      scan4 = *blank
7.00 c                   eval      scan5 = *blank
7.00 c                   if        b2besk <> *blank
7.00 c                   eval      i = %scan(' ':b2besk)
7.00 c                   eval      scan1 = %trim(%subst(b2besk:1:i-1))
7.00 c                   eval      b2besk = %subst(b2besk:i+1:25-i)
7.00 c                   if        b2besk <> *blank
7.00 c                   eval      i = %scan(' ':b2besk)
7.00 c                   eval      scan2 = %trim(%subst(b2besk:1:i-1))
7.00 c                   eval      b2besk = %subst(b2besk:i+1:25-i)
7.00 c                   if        b2besk <> *blank
7.00 c                   eval      i = %scan(' ':b2besk)
7.00 c                   eval      scan3 = %trim(%subst(b2besk:1:i-1))
7.00 c                   eval      b2besk = %subst(b2besk:i+1:25-i)
7.00 c                   if        b2besk <> *blank
7.00 c                   eval      i = %scan(' ':b2besk)
7.00 c                   eval      scan4 = %trim(%subst(b2besk:1:i-1))
7.00 c                   eval      b2besk = %subst(b2besk:i+1:25-i)
7.00 c                   if        b2besk <> *blank
7.00 c                   eval      i = %scan(' ':b2besk)
7.00 c                   eval      scan5 = %trim(%subst(b2besk:1:i-1))
7.00 c                   eval      b2besk = %subst(b2besk:i+1:25-i)
7.00 c                   endif
7.00 c                   endif
7.00 c                   endif
7.00 c                   endif
7.00 c                   endif
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      rp1pl2                                 15
     c                   when      w_seqe = 'C'
     c                   read      rp1pl3                                 15
     c                   when      w_seqe = 'D'
     c                   read      rp1pl4                                 15
     c                   other
     c                   read      rp1pl1                                 15
     c                   endsl
      *
5.61 c                   if        *in15  or
5.61 c                             rp1frm <> w_firm
     c                   leave
     c                   endif
      *
5.61 c                   if        b2aktiv = 'J' and
     c                             rp1sts  = 'A'
     c                   iter
     c                   endif
      *        Valgt avdeling
5.61 c                   if        b2vavd <> *zero and
     c                             rp1avd <> b2vavd
     c                   iter
     c                   endif
      *        Valgt prosjektleder
5.61 c                   if        b2vprl <> *zero and
     c                             rp1prl <> b2vprl
     c                   iter
     c                   endif
7.00  *  Søk på Fritekst
7.00 c                   if        scan1 <> *blank
7.00 c                   eval      rkunl1_kund = rp1knr
7.00 c     rkunl1_key    chain     rkunl1
7.00 c                   if        not %found(rkunl1)
7.00 c                   eval      rknavn = *blank
7.00 c                   endif
7.00 c                   eval      ilonl1_kli  = l_firm
7.00 c                   eval      ilonl1_lnr  = rp1prl
7.00 c     ilonl1_key    chain     ilonl1
7.00 c                   if        not %found(ilonl1)
7.00 c                   eval      ilrnav = *blank
7.00 c                   endif
7.00  *
7.00 c                   eval      w_txt = %trim(rp1txt)
7.00 c                   if        rp1pro <> *blank
7.00 c                   eval      w_txt = %trim(w_txt) + ' ' + %trim(rp1pro)
7.00 c                   endif
7.00 c                   if        rp1tx2 <> *blank
7.00 c                   eval      w_txt = %trim(w_txt) + ' ' + %trim(rp1tx2)
7.00 c                   endif
7.00 c                   if        rp1krf <> *blank
7.00 c                   eval      w_txt = %trim(w_txt) + ' ' + %trim(rp1krf)
7.00 c                   endif
7.00 c                   if        rp1krt <> *blank
7.00 c                   eval      w_txt = %trim(w_txt) + ' ' + %trim(rp1krt)
7.00 c                   endif
7.00 c                   if        rp1krm <> *blank
7.00 c                   eval      w_txt = %trim(w_txt) + ' ' + %trim(rp1krm)
7.00 c                   endif
7.00 c                   if        rknavn <> *blank
7.00 c                   eval      w_txt = %trim(w_txt) + ' ' + %trim(rknavn)
7.00 c                   endif
7.00 c                   if        ilrnav <> *blank
7.00 c                   eval      w_txt = %trim(w_txt) + ' ' + %trim(ilrnav)
7.00 c                   endif
7.00 c     lo:up         xlate     w_txt         wtxt
7.00  *
7.00 c                   if        %scan(%trim(scan1):wtxt) = 0
7.00 c                   iter
7.00 c                   endif
7.00  *
7.00 c                   if        scan2 <> *blank
7.00 c                             and %scan(%trim(scan2):wtxt) = 0
7.00 c                   iter
7.00 c                   endif
7.00  *
7.00 c                   if        scan3 <> *blank
7.00 c                             and %scan(%trim(scan3):wtxt) = 0
7.00 c                   iter
7.00 c                   endif
7.00  *
7.00 c                   if        scan4 <> *blank
7.00 c                             and %scan(%trim(scan4):wtxt) = 0
7.00 c                   iter
7.00 c                   endif
7.00  *
7.00 c                   if        scan5 <> *blank
7.00 c                             and %scan(%trim(scan5):wtxt) = 0
7.00 c                   iter
7.00 c                   endif
7.00  *
7.00 c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1pros = rp1pro
     c                   eval      b1besk = rp1txt
     c                   eval      b1fsd  = rp1fsd
     c                   eval      b1fed  = rp1fed
     c                   eval      b1avd  = rp1avd
     c                   eval      b1prl  = rp1prl
6.22 c                   eval      b1buo  = rp1buo
6.22 c                   eval      b1but  = rp1but
     c                   if        rp1rak = 'J'
     c                             or rp1rup = 'J'
     c                   exsr      getbudsj
     c                   eval      b1buo = c2buo
     c                   eval      b1but = c2but
     c                   endif
6.22  *
     c                   select
     c                   when      w_a51  = 0
6.22  * dato informasjon
6.22 c                   eval      b1flt1 = *blank
6.22 c                   eval      b1flt2 = *blank
6.22 c                   eval      b1flt3 = *blank
6.22 c                   eval      b1flt4 = *blank
6.22 c                   if        b1fsd <> *loval
6.22 c     *dmy          move      b1fsd         w_datn
6.22 c                   eval      b1flt1 = %editw(w_datn : '  .  .  ')
6.22 c                   endif
6.22 c                   if        b1fed <> *loval
6.22 c     *dmy          move      b1fed         w_datn
6.22 c                   eval      b1flt2 = %editw(w_datn : '  .  .  ')
6,22 c                   endif
     c                   eval      b1flt3 = %editw(b1avd: '    0')
     c                   eval      b1flt4 = %editw(b1prl: '    0')
      *
     c                   when      w_a51 = 1
6.22  * beløpsinformasjon
6.22 c                   eval      b1flt1 = *blank
6.22 c                   eval      b1flt2 = *blank
6.22 c                   eval      b1flt3 = *blank
6.22 c                   eval      b1flt4 = *blank
6.22 c                   exsr      finn_sum
6.22 c                   eval      w_buo  = b1buo
6.22 c                   eval      w_but  = b1but
6.22 c                   eval      b1flt1 = %editw(w_buo :'  .   .   -')
6.22 c                   eval      b1flt2 = %editw(w_but :'  .   .   -')
6.22 c                   eval      b1flt3 = %editw(w_sumi:'  .   .   -')
6.22 c                   eval      b1flt4 = %editw(w_sumk:'  .   .   -')
     c                   if        w_sumk > w_sumi
     c                   eval      *in41 = *on
     c                   else
     c                   eval      *in41 = *off
     c                   endif
      *
     c                   when      w_a51 = 2
      * Inntekts informasjon
     c                   eval      b1flt1 = *blank
     c                   eval      b1flt2 = *blank
     c                   eval      b1flt3 = *blank
     c                   eval      b1flt4 = *blank
     c                   exsr      finn_sum
     c                   eval      w_buo  = b1buo
     c                   eval      w_but  = b1but
     c                   eval      w_rest = w_buo - w_sumi
     c                   if        w_buo <> 0
     c                   eval(h)   w_pros = w_sumi * 100 / w_buo
     c                   else
     c                   eval      w_pros = 0
     c                   endif
     c                   eval      b1flt1 = %editw(w_buo :'  .   .   -')
     c                   eval      b1flt2 = %editw(w_sumi:'  .   .   -')
     c                   eval      b1flt3 = %editw(w_rest:'  .   .   -')
     c                   eval      b1flt4 = %editw(w_pros:'  .   .   -')
     c                   if        w_rest < 0
     c                   eval      *in41 = *on
     c                   else
     c                   eval      *in41 = *off
     c                   endif
      *
     c                   when      w_a51 = 3
      * Kostnads informasjon
     c                   eval      b1flt1 = *blank
     c                   eval      b1flt2 = *blank
     c                   eval      b1flt3 = *blank
     c                   eval      b1flt4 = *blank
     c                   exsr      finn_sum
     c                   eval      w_buo  = b1buo
     c                   eval      w_but  = b1but
     c                   eval      w_rest = w_but - w_sumk
     c                   if        w_but <> 0
     c                   eval(h)   w_pros = w_sumk * 100 / w_but
     c                   else
     c                   eval      w_pros = 0
     c                   endif
     c                   eval      b1flt1 = %editw(w_but :'  .   .   -')
     c                   eval      b1flt2 = %editw(w_sumk:'  .   .   -')
     c                   eval      b1flt3 = %editw(w_rest:'  .   .   -')
     c                   eval      b1flt4 = %editw(w_pros:'  .   .   -')
     c                   if        w_rest < 0
     c                   eval      *in41 = *on
     c                   else
     c                   eval      *in41 = *off
     c                   endif
     c                   endsl
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
6.22  *
6.22  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.22  * Subrutine for å hente summer av prosjekt
6.22  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.22  *
6.22 c     finn_sum      begsr
6.22  *
6.22 c                   eval      w_sumi = 0
6.22 c                   eval      w_sumk = 0
6.22 c                   eval      w_sumix = 0
6.22 c                   eval      w_sumkx = 0
6.22  * inntekter
6.22 c                   eval      rprrl1_fir = w_firm
6.22 c                   eval      rprrl1_rap = 9700
6.22 c                   eval      rprrl1_lin = 10
6.22 c                   eval      rprrl1_uli = *zero
     c                   dow       rprrl1_lin < 20
6.22 c     rprrl1_key    chain     rprrl1
6.22 c                   if        %found(rprrl1)
6.22  * inntekter
6.22 c                   eval      rhtrlc_fir = w_firm
6.22 c                   eval      rhtrlc_pro = b1pros
6.22 c     rhtrlc_key    setll     rhtrlc
6.22 c     rhtrlc_key    reade     rhtrlc
6.22 c                   dow       not %eof(rhtrlc) and
6.22 c                             rbhpro = b1pros
6.22 c                   if        rbkont >= rphktf and
6.22 c                             rbkont <= rphktt
     c                   if        rphfun = '-'
6.22 c                   eval(h)   w_sumix = w_sumix - rbneto
6.22 c                   else
6.22 c                   eval(h)   w_sumix = w_sumix + rbneto
6.22 c                   endif
6.22 c                   endif
6.22 c     rhtrlc_key    reade     rhtrlc
6.22 c                   enddo
6.22 c                   endif
6.22  *
     c                   eval      rprrl1_lin = rprrl1_lin + 1
     c                   enddo
6.22  *
6.22  *
6.22  * utgifter varekjøp
6.22 c                   eval      rprrl1_fir = w_firm
6.22 c                   eval      rprrl1_rap = 9700
6.22 c                   eval      rprrl1_lin = 20
6.22 c                   eval      rprrl1_uli = *zero
     c                   dow       rprrl1_lin < 30
6.22 c     rprrl1_key    chain     rprrl1
6.22 c                   if        %found(rprrl1)
6.22 c                   eval      rhtrlc_fir = w_firm
6.22 c                   eval      rhtrlc_pro = b1pros
6.22 c     rhtrlc_key    setll     rhtrlc
6.22 c     rhtrlc_key    reade     rhtrlc
6.22 c                   dow       not %eof(rhtrlc)
6.22 c                   if        rbkont >= rphktf and
6.22 c                             rbkont <= rphktt
     c                   if        rphfun = '-'
6.22 c                   eval(h)   w_sumkx = w_sumkx - rbneto
6.22 c                   else
6.22 c                   eval(h)   w_sumkx = w_sumkx + rbneto
6.22 c                   endif
6.22 c                   endif
6.22 c     rhtrlc_key    reade     rhtrlc
6.22 c                   enddo
6.22 c                   endif
6.22  *
     c                   eval      rprrl1_lin = rprrl1_lin + 1
     c                   enddo
6.22  *
6.22  * utgifter lønn
6.22 c                   eval      rprrl1_fir = w_firm
6.22 c                   eval      rprrl1_rap = 9700
6.22 c                   eval      rprrl1_lin = 30
6.22 c                   eval      rprrl1_uli = *zero
     c                   dow       rprrl1_lin < 40
6.22 c     rprrl1_key    chain     rprrl1
6.22 c                   if        %found(rprrl1)
6.22 c                   eval      rhtrlc_fir = w_firm
6.22 c                   eval      rhtrlc_pro = b1pros
6.22 c     rhtrlc_key    setll     rhtrlc
6.22 c     rhtrlc_key    reade     rhtrlc
6.22 c                   dow       not %eof(rhtrlc)
6.22 c                   if        rbkont >= rphktf and
6.22 c                             rbkont <= rphktt
     c                   if        rphfun = '-'
6.22 c                   eval(h)   w_sumkx = w_sumkx - rbneto
6.22 c                   else
6.22 c                   eval(h)   w_sumkx = w_sumkx + rbneto
6.22 c                   endif
6.22 c                   endif
6.22  *
6.22 c     rhtrlc_key    reade     rhtrlc
6.22 c                   enddo
6.22 c                   endif
6.22  *
     c                   eval      rprrl1_lin = rprrl1_lin + 1
     c                   enddo
6.22  * andre kostnader
6.22 c                   eval      rprrl1_fir = w_firm
6.22 c                   eval      rprrl1_rap = 9700
6.22 c                   eval      rprrl1_lin = 50
6.22 c                   eval      rprrl1_uli = *zero
     c                   dow       rprrl1_lin < 60
6.22 c     rprrl1_key    chain     rprrl1
6.22 c                   if        %found(rprrl1)
6.22  * utgifter
6.22 c                   eval      rhtrlc_fir = w_firm
6.22 c                   eval      rhtrlc_pro = b1pros
6.22 c     rhtrlc_key    setll     rhtrlc
6.22 c     rhtrlc_key    reade     rhtrlc
6.22 c                   dow       not %eof(rhtrlc)
6.22 c                   if        rbkont >= rphktf and
6.22 c                             rbkont <= rphktt
     c                   if        rphfun = '-'
6.22 c                   eval(h)   w_sumkx = w_sumkx - rbneto
6.22 c                   else
6.22 c                   eval(h)   w_sumkx = w_sumkx + rbneto
6.22 c                   endif
6.22 c                   endif
6.22 c     rhtrlc_key    reade     rhtrlc
6.22 c                   enddo
6.22 c                   endif
6.22  *
     c                   eval      rprrl1_lin = rprrl1_lin + 1
     c                   enddo
6.22  *
6.22  * finans kostnader
6.22 c                   eval      rprrl1_fir = w_firm
6.22 c                   eval      rprrl1_rap = 9700
6.22 c                   eval      rprrl1_lin = 60
6.22 c                   eval      rprrl1_uli = *zero
     c                   dow       rprrl1_lin < 70
6.22 c     rprrl1_key    chain     rprrl1
6.22 c                   if        %found(rprrl1)
6.22  * utgifter
6.22 c                   eval      rhtrlc_fir = w_firm
6.22 c                   eval      rhtrlc_pro = b1pros
6.22 c     rhtrlc_key    setll     rhtrlc
6.22 c     rhtrlc_key    reade     rhtrlc
6.22 c                   dow       not %eof(rhtrlc)
6.22 c                   if        rbkont >= rphktf and
6.22 c                             rbkont <= rphktt
     c                   if        rphfun = '-'
6.22 c                   eval(h)   w_sumkx = w_sumkx - rbneto
6.22 c                   else
6.22 c                   eval(h)   w_sumkx = w_sumkx + rbneto
6.22 c                   endif
6.22 c                   endif
6.22 c     rhtrlc_key    reade     rhtrlc
6.22 c                   enddo
6.22  *
6.22 c                   endif
6.22  *
     c                   eval      rprrl1_lin = rprrl1_lin + 1
     c                   enddo
      *    ---------------------------------------------------------
     c                   if        %abs(w_sumix) <= 9999999
     c                   eval      w_sumi = w_sumix
     c                   else
     c                   eval      w_sumi = 9999999
     c                   endif
      *
     c                   if        %abs(w_sumkx) <= 9999999
     c                   eval      w_sumk = w_sumkx
     c                   else
     c                   eval      w_sumk = 9999999
     c                   endif
6.22  *
6.22 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
9.03 c                   parm                    p_fdat
9.03 c                   parm                    p_tdat
9.03 c                   parm                    p_knav
9.03 c                   parm                    p_selg
9.03 c                   parm                    p_fkun
9.03 c                   parm                    p_pros
9.03 c                   parm                    p_kpro
      *
      * Key for posisjonering på prosjekt
      *
     c     rp1pl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rp1pl1_pros
      *
      * Key for posisjonering på tekst
      *
     c     rp1pl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rp1pl2_besk
     c                   kfld                    rp1pl2_pros
      *
      * Key for posisjonering på avdeling
      *
     c     rp1pl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rp1pl3_avd
     c                   kfld                    rp1pl3_pros
      *
      * Key for posisjonering på prosjektleder
      *
     c     rp1pl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rp1pl4_prl
     c                   kfld                    rp1pl4_pros
      *
      * Key for les på prosjekt
      *
     c     rp1plr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rp1plr_pros
      *
      * Key for oppdatering av prosjket
      *
     c     rp1plu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rp1plu_pros
6.35  *
6.35  * Key for lesing av underprosjekt
6.35  *
6.35 c     rp2ul1_key    klist
6.35 c                   kfld                    w_firm
6.35 c                   kfld                    rp2ul1_pros
6.35  *
6.35  * Key for lesing av aktivitet
6.35  *
6.35 c     rp3al1_key    klist
6.35 c                   kfld                    w_firm
6.35 c                   kfld                    rp3al1_pros
      *
      * Key for oppdatering av arbeidsordre
      *
     c     rp4olu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rp4olu_arbo
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på Prosjektleder
      *
     c     ilonl1_key    klist
     c                   kfld                    ilonl1_kli
     c                   kfld                    ilonl1_lnr
6.34  *
6.34  * Key for oppslag på Selger
6.34  *
6.34 c     ra09l1_key    klist
6.34 c                   kfld                    ra09l1_fir
6.34 c                   kfld                    ra09l1_kod
      *
      * Key for oppslag på avdeling
      *
     c     ra07l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra07l1_kod
      *
      * Key for posisjonering på prosjekt-rapport
      *
     c     rprrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rprrl1_rap
     c                   kfld                    rprrl1_lin
     c                   kfld                    rprrl1_uli
6.22  *
6.22  * Key for les på posteringer
6.22  *
6.22 c     rhtrlc_key    klist
6.22 c                   kfld                    w_firm
6.22 c                   kfld                    rhtrlc_pro
6.22  *
6.22  * Key for les på bruker(FUSR)
6.22  *
6.22 c     fusrl1_key    klist
6.22 c                   kfld                    w_firm
6.22 c                   kfld                    fusrl1_usr
6.22  *
6.22  * Key for les på bruker(AUSR)
6.22  *
6.22 c     ausrl1_key    klist
6.22 c                   kfld                    ausrl1_usr
9.02  *
9.02  * Key for les på Koder
9.02  *
9.02 c     rpkol1_key    klist
9.02 c                   kfld                    rpkol1_firm
9.02 c                   kfld                    rpkol1_type
9.02 c                   kfld                    rpkol1_kode
9.03  *
9.03  * Key for les av nummerteller
9.03  *
9.03 c     anumlu_key    klist
9.03 c                   kfld                    w_firm
9.03 c                   kfld                    anumlu_fell
9.03 c                   kfld                    anumlu_type
9.03 c                   kfld                    anumlu_fast
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjermbildet
      *
     c                   eval      *in22 = *on
      *
      * Hent opp brukerens selgernummer
      *
     c                   eval      fusrl1_usr = l_user
     c     fusrl1_key    chain     fusrl1
     c                   if        %found(fusrl1)
6.33 c**                 eval      b2vprl = fbselg
     c                   else
     c                   eval      b2vprl = *zero
     c                   endif
     c                   eval      ausrl1_usr = l_user
     c     ausrl1_key    chain     ausrl1
     c                   if        %found(ausrl1)
     c                   eval      b2vavd = absavd
     c                   else
     c                   eval      b2vavd = *zero
     c                   endif
      *
      * Skal bruker kunnde overtyre valgt avdeling
      *
     c                   eval      w_tilgko = l_user
     c                   eval      w_rutine = 'RP1_B2VAVD'
     c                   eval      w_adv    = *blank
     c                   eval      w_status = 'Uten'
     c                   call      'AD005R'
     c                   parm                    w_tilgko
     c                   parm                    w_rutine
     c                   parm                    w_adv
     c                   parm                    w_status
     c                   if        w_status = 'Sperret'
     c                   eval      *in32 = *on
     c                   endif
      *
      * Skal bruker kunnde overtyre valgt prosjektleder
      *
     c                   eval      w_tilgko = l_user
     c                   eval      w_rutine = 'RP1_B2VPRL'
     c                   eval      w_adv    = *blank
     c                   eval      w_status = 'Uten'
     c                   call      'AD005R'
     c                   parm                    w_tilgko
     c                   parm                    w_rutine
     c                   parm                    w_adv
     c                   parm                    w_status
     c                   if        w_status = 'Sperret'
     c                   eval      *in33 = *on
     c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      b2aktiv     = 'J'
     c                   eval      s_aktiv     = 'J'
     c                   eval      w_seqe      = *blank
     c                   eval      s_prl       = b2vprl
     c                   eval      s_avd       = b2vavd
     c                   eval      rp1pl1_pros = *blank
     c     rp1pl1_key    setll     rp1pl1
      *
7.xx c*                  eval      b2head = 'Fra dato    Til dato     -
7.xx c*                            Avd.     Pr.leder'
7.xx c                   eval      b2hea1 = 'Fra dato'
7.xx c                   eval      b2hea2 = 'Til dato'
7.xx c                   eval      b2hea3 = 'Avd.'
7.xx c                   eval      b2hea4 = 'Pr.leder'
     c                   exsr      crt_subfile
9.03  *
9.03  * Skal det være krav om prosjekt kategori
9.03  *
9.03   CallP CO402R(l_filg:w_firm:0:'PROSJ_KRAV_PRK':
9.03                    co402_verdi1:co402_verdi2);
9.03   if co402_verdi1 = '1';
9.03     u_kravprk = *on;
9.03   endif;
9.03  *
      *
     c                   endsr
