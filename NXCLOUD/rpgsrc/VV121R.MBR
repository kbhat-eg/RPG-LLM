     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASPNIB
      * Program . . . . : VV121R
      * Beskrivelse . . : Alternativ vare, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       090405 HKO  Alternativ vare, vedlikehold
      * 6.10  290609 BSA  Oppdatert med sporingsinformasjon
7.xx  * 7.00  110316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.00  * 7.00  110316 bsa  Må ta med seg firmakoden i oppdatering.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvaal1    if   e           k disk    rename(vvaapfr:vvaal1r)
     fvvaalr    if   e           k disk    rename(vvaapfr:vvaalrr)
     fvvaalu    uf a e           k disk    rename(vvaapfr:vvaalur)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
     fvv121d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vvfirm)
     d p_nobb          s                   like(vvnobb)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d vvaalr_aava     s                   like(vvaava)
     d vvaalu_aava     s                   like(vvaava)
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_srrn_w        s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_firm          s                   like(vvfirm)
     d w_nobb          s                   like(vvnobb)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(7)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile                                  CRT SFL
     c                   goto      b2tagb
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c     vvaal1_key    setll     vvaal1
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler alle rader i subfile
      *
     c                   eval      w_srrn_w = 0
      *
     c                   do        w_ssrn
      *
     c                   eval      w_srrn_w = w_srrn_w + 1
     c     w_srrn_w      chain     b1sfl
      *
     c                   if        not %found
     c                   leave
     c                   endif
      *
     c                   if        b1avar <> 0
     c                   movel     b1avar        b1aava
     c                   else
     c                   eval      b1aava = *blank
     c                   endif
      *
     c                   if        b1aava = b1work
     c                   iter
     c                   endif
      *
      * Sletting av alternativ vare
      *
     c                   if        b1aava = *blank
     c                   eval      vvaalu_aava = b1work
     c     vvaalu_key    chain     vvaalur
     c                   if        %found
     c                   delete    vvaalur
     c                   endif
     c                   eval      b1work = *blank
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Sjekker om gyldig vare
      *
     c                   if        b1aava <> *blank
     c                   eval      vvarl1_vare = b1aava
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   eval      *in32 = *on
     c                   update    b1sfl
     c                   iter
     c                   endif
     c                   endif
      *
      * Oppretting eller endring av alternativ vare
      *
      * Dersom knytingen er ny eller endret sjekkes det om knytningen
      * finnes fra før
      *
     c                   eval      vvaalr_aava = b1aava
     c     vvaalr_key    chain     vvaalr
     c                   if        %found
     c                   eval      *in31 = *on
     c                   iter
     c                   endif
      *
      * Sletter eventuelt endret alternativ vare
      *
     c                   if        b1work <> *blank
     c                   eval      vvaalu_aava = b1work
     c     vvaalu_key    chain     vvaalur
     c                   if        %found
     c                   delete    vvaalur
     c                   endif
     c                   endif
      *
      * Oppretter alternativ vare
      *
7.00 c                   eval      vvafir = w_firm
     c                   eval      vvanob = w_nobb
     c                   eval      vvaava = b1aava
      *
     c                   time                    vvaoda
6.10 c                   time                    vvaoti
6.10 c                   eval      vvaous = l_user
     c                   time                    vvaeda
     c                   time                    vvaeti
     c                   eval      vvaeus = l_user
      *
     c                   write     vvaalur
      *
     c                   iter
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c     vvaal1_key    reade     vvaal1                                 15
      *
     c                   if        *in15 = *off                                 EOF
     c                   eval      b1work = vvaava
     c                   eval      b1aava = vvaava
     c                   movel     vvaava        b1avar
     c                   else
     c                   eval      b1work = *blank
     c                   eval      b1aava = *blank
     c                   eval      b1avar = 0
     c                   endif
      *
     c                   eval      b1tek1 = *blank
      *
     c                   if        b1aava <> *blank
     c                   eval      vvarl1_vare = b1aava
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      b1tek1 = vvtek1
     c                   endif
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørringer (F4)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Spørring i subfile
      *
     c                   if        w_afld = 'B1AVAR'
     c     w_curn        chain     b1sfl
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    b1aava
     c                   parm                    b1tek1
     c                   movel     b1aava        b1avar
     c                   update    b1sfl
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_nobb
      *
      * Key for les på alternativ vare
      *
     c     vvaal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_nobb
      *
      * Key for oppslag på alternativ vare
      *
     c     vvaalr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_nobb
     c                   kfld                    vvaalr_aava
      *
      * Key for oppdatering av alternativ vare
      *
     c     vvaalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_nobb
     c                   kfld                    vvaalu_aava
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *
      * Henter firma og nobbnr fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_nobb = p_nobb
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     vvaal1_key    setll     vvaal1                                                    .
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
