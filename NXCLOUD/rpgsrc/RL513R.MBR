     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RL513R
      * Beskrivelse . . : Leverandør, spørring på motpost
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       121103 KOB  Nytt program
      * V5.41 280805 KOB  Lagt inn ny funksjon for å vise arkivpost
      * R5.60 200308 KOB  Endret fra l8 til lf index slik at alle poster
      *                   med samme referansenr. vises
pdf   * R6.10     041010  Henter fra ASP arkiv. Mulighet for valg mellom BSA  *
pdf   *                   ASP og Multiarkiv gjøres i AP622C. Det finnes  BSA  *
pdf   *                   to varianter av dette programmet.              BSA  *
6.20  * R6.20     070611  Starter arkiv, hvis ikke faktura               BSA  *
6.21  * R6.20     210711  Legger ut firmanummer i skjultfelt for         bsa  *
6.21  *                   spørring mot ASP-arkiv fra Jacada              bsa  *
6.22  * R6.20     061212  Endret metode for oppslag mot arkiv            bsa  *
6.23  * R6.20     040113  Endret oppbygging av refno                     bsa  *
6.24  * R6.20     270213  Egen kode for forskjellige bilag               bsa  *
6.25  * R6.20     260313  Henter dokumenttype basert på bilagskode       bsa  *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frltrlf    if   e           k disk    rename(rltrpfr:rltrlfr)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frltrl2    if   e           k disk    rename(rltrpfr:rltrl2r)
     fraa1l1    if   e           k disk    rename(raa1pfr:raa1l1r)
      *
     frl513d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
      *
     d rltrlf_levr     s                   like(rolevr)
     d rltrlf_biln     s                   like(robiln)
     d rlevl1_levr     s                   like(rolevr)
     d rltrl2_levr     s                   like(rolevr)
     d rltrl2_bdat     s                   like(robdat)
     d rltrl2_tist     s                   like(rotist)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
6.22 d w_type          s             50
pdf  d w_modu          s             10    inz('PROA    ')
pdf  d*w_modu          s             10    inz('MSA     ')
     d w_disp          s              1    inz('1')
     d w_tilg          s              1
6.22 d w_refn          s             50
6.25 d w_dtyp          s             50
6.25 d w_year          s              1
6.25 d w_ok            s              1
      *
      * Definering av parametre MultiArcive
      *
     d w_pgrp          s              2
     d w_aara          s              4
     d w_bila          s              6
     d w_afir          s              3
      *
     d w_seqe          s              1
     d b_valg_ok       s              1    inz(*off)
      *
     d p_kont          s              6  0
     d p_biln          s                   like(robiln)
     d p_bida          s                   like(robdat)
     d p_rper          s                   like(rorper)
     d p_raar          s                   like(roraar)
      *
     d p_stat          s              1
     d p_gkod          s                   like(rlavde)
     d p_gtxt          s             20
     d p_itxt          s             20
      *
     d w_firm          s                   like(rofirm)
     d w_levr          s                   like(rolevr)
     d w_raar          s                   like(roraar)
      *
     d p_text          s             30
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * d_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        d_fcrn <> 0
     c     d_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      rltrlf_biln = p_biln
     c     rltrlf_key    setll     rltrlf
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = d_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   exsr      xc2win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis arkiv
      *
     c                   if        b1valg = 9
      *
      * Kontrollerer gyldig tilgang
      *
     c                   call      'AX700R'
     c                   parm                    w_modu
     c                   parm                    w_disp
     c                   parm                    w_tilg
      *
     c                   if        w_tilg = '0'
6.25  *
6.25  * Hent dokumenttype basert på bilagskoden
6.25  *
6.25 c                   eval      w_dtyp = *blanks
6.25 c                   eval      w_year = *blanks
6.25 c                   eval      w_ok = '0'
6.25  *
6.25 c                   call      'AP632R'
6.25 c                   parm                    w_firm
6.25 c                   parm                    b1bilk
6.25 c                   parm                    w_dtyp
6.25 c                   parm                    w_year
6.25 c                   parm                    w_ok
6.25  *
6.25 c                   if        w_ok = '1'
6.25 c                   eval      w_type = w_dtyp
6.25 c                   if        w_year = 'J'
6.25 c                   eval      w_refn = %char(b1raar)+%trim(%char(b1biln))
6.25 c                   else
6.25 c                   eval      w_refn = %trim(%char(b1biln))
6.25 c                   endif
6.25 c                   else
6.25  * Gammel logikk hvis den ikke finnes
      *
6.24 c                   if        b1bilk = 1 or
6.24 c                             b1bilk = 3
pdf  c                   eval      w_type = 'FAKTURA'
6.23 c                   eval      w_refn = %char(b1raar)+%trim(%char(b1biln))
6.24 c                   elseif    b1bilk = 2 or
6.24 c                             b1bilk = 4
6.22 c                   eval      w_type = 'INNGÅENDE%20FAKTURA'
6.23 c                   eval      w_refn = %trim(%char(b1biln))
6.24 c                   else
6.24 c                   eval      w_type = 'DIVERSEBILAG'
6.24 c                   eval      w_refn = %char(b1raar)+%trim(%char(b1biln))
pdf  c                   endif
6.25 c                   endif
      *
     c                   move      w_firm        w_afir
     c                   eval      w_pgrp = '30'
     c                   eval      w_aara = %char(b1raar)
     c                   eval      w_bila = %char(b1biln)
      *
pdf  c                   call      'AP622C'
pdf  c                   parm                    w_type
6.22 c                   parm                    w_refn
6.22 c*                  parm                    w_aara
6.22 c*                  parm                    w_bila
6.22 c*                  parm                    w_pgrp
pdf  c**                 call      'AH720C'
pdf  c**                 parm                    w_pgrp
pdf  c**                 parm                    w_afir
pdf  c**                 parm                    w_aara
pdf  c**                 parm                    w_bila
      *
     c                   endif
      *
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
      * Les leverandørposter
      *
     c     rltrlf_key    reade     rltrlf                                 62
      *
     c                   if        *in62
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1biln = robiln
     c                   eval      b1bdat = robdat
     c     *dmy          move      robdat        b1bida
     c     *dmy          move      rofdat        b1fdat
     c                   eval      b1refn = rorefn
     c                   eval      b1tist = rotist
     c                   eval      b1bilk = robilk
     c                   eval      b1text = rotext
     c                   eval      b1belØ = robelØ
     c                   eval      b1rest = rorest
     c                   eval      b1raar = roraar
     c                   eval      b1rper = rorper
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     rltrlf_key    setgt     rltrlf
     c                   readp     rltrlf
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     rltrlf
      *
     c                   if        %eof or
     c                             rofirm <> w_firm or
     c                             robiln <> p_biln
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      rltrlf_biln = robiln
      *
     c                   enddo
      *
     c                   if        %eof
     c     rltrlf_key    setll     rltrlf
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * C2WIN Behandle bilde for å vise leverandørposter                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xc2win        begsr
      *
     c                   eval      rltrl2_levr = p_kont
     c                   eval      rltrl2_bdat = b1bdat
     c                   eval      rltrl2_tist = b1tist
     c     rltrl2_key    chain     rltrl2                             60
      *
     c                   if        not%found
     c                   goto      end_c2win
     c                   endif
      *
     c                   move      robiln        c2biln
     c                   move      robilk        c2bilk
     c                   move      rotext        c2text
     c     *dmy          move      robdat        c2bdat
     c     *dmy          move      rofdat        c2fdat
     c                   move      rorper        c2rper
     c                   move      roraar        c2raar
     c                   move      rojour        c2jour
     c                   move      rorefn        c2refn
     c                   move      robiln        c2biln
     c                   move      robelØ        c2belØ
     c                   move      rorest        c2rest
     c                   move      rovalk        c2valk
     c                   move      roavde        c2avde
     c                   move      rovalb        c2valb
     c                   move      rovalr        c2valr
      *
T5.00c                   move      rorefn        c2refn
T5.00c                   move      robfnr        c2bfnr
T5.00c                   move      robfli        c2bfli
T5.00c                   move      roatts        c2atts
T5.00c                   if        roatts = '1'
T5.00c                   move      'J'           c2atts
T5.00c                   endif
T5.00c                   move      rokidi        c2kidi
T5.20c                   move      rolfak        c2lfak
      *
      * Hent avdelingstekst
      *
     c                   if        roavde <> *zero
     c                   move      roavde        p_gkod
     c                   call      'RS707R'
     c                   parm                    p_stat
     c                   parm                    p_gkod
     c                   parm                    p_gtxt
      *
     c                   movel     p_gtxt        c2gtxt
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_c2win
     c                   endif
      *
     c     end_c2win     tag
      *
     c                   endsr
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_kont
     c                   parm                    p_biln
     c                   parm                    p_bida
     c                   parm                    p_rper
     c                   parm                    p_raar
      *
      * Key for posisjonering på bilagsnummer
      *
     c     rltrlf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rltrlf_levr
     c                   kfld                    rltrlf_biln
      *
      * Key for leverandørregister via leverandørnummer
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for lesing av postering
      *
     c     rltrl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rltrl2_levr
     c                   kfld                    rltrl2_bdat
     c                   kfld                    rltrl2_tist
      *
      * Key for statusrecord
      *
     c     raa1l1_key    klist
     c                   kfld                    w_firm
      *
      * Henter parametre, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
6.21 c                   eval      b2firm = w_firm
     c                   eval      b2rper = p_rper
     c                   eval      b2raar = p_raar
      *
     c                   eval      *in22 = *on
      *
      * Hent leverandør
      *
     c                   eval      rlevl1_levr = p_kont
     c     rlevl1_key    chain     rlevl1
     c                   if        %found
     c                   eval      b2levr = rllevr
     c                   eval      b2navn = rlnavn
     c                   eval      b2sald = rlsald
     c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rltrlf_levr = p_kont
     c                   eval      rltrlf_biln = p_biln
     c     rltrlf_key    setll     rltrlf
      *
     c     raa1l1_key    chain     raa1l1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
