      /TITLE  ASOFAK Faktura, spørring - parameter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : SO500R
      * Beskrivelse . . : Faktura, spørring - parameter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       220699 HKO  Nytt program
7.xx  * 7.00  010216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsohepf    if   e           k disk
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
      *
     fso500d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d votyl1_otyp     s                   like(vaotyp)
     d rkunl1_kund     s                   like(rkkund)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_dato          s               d   datfmt(*iso)
     d w_dato_alf      s             10
      *
     d w_firm          s                   like(sofirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer bildet
      *
     c                   time                    w_dato
     c                   move      w_dato        w_dato_alf
     c                   movel     w_dato_alf    b1aarr
      *
      * Behandler bildet
      *
     c     b1taga        tag
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
      * Kaller spørreprogram
      *
     c                   call      'SO501R'
     c                   parm                    w_firm
     c                   parm                    b1aarr
     c                   parm                    b1otyp
     c                   parm                    b1kund
      *
     c                   goto      b1taga
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av spørringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Ordretype
      *
     c                   if        w_afld = 'B1OTYP'
     c                   call      'VA550R'
     c                   parm                    w_firm
     c                   parm                    b1otyp
     c                   goto      end_spØrr
     c                   endif
      *
      * Kunde
      *
     c                   if        w_afld = 'B1KUND'
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    b1kund
     c                   parm                    b1knav
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
     c                   eval      b1knav = *blank
      *
      * År
      *
     c                   if        b1aarr = 0
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Ordretype
      *
     c                   if        b1otyp <> *blank
     c                   eval      votyl1_otyp = b1otyp
     c     votyl1_key    chain     votyl1                             90
     c                   if        *in90
     c                   eval      *in32  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
      * Kunde
      *
     c                   if        b1kund <> 0
     c                   eval      rkunl1_kund = b1kund
     c     rkunl1_key    chain     rkunl1                             90
     c                   if        *in90
     c                   eval      *in33  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1knav = rknavn
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på ordretype-register
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      *
      * Henter firmakode fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
