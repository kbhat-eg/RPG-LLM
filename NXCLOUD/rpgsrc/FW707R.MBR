      /TITLE  ASOFAK Lev.vare, henter vareregister - ESAB
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FW707R
      * Beskrivelse . . : Danning av leverandør vareregister, ESAB
      *
      *
      * --- !!! --- !!! --- !!!
      *
      * NB !!  Venter på nytt recordformat som skal være klart før 01.01.99
      *        Gro Øksenholdt skal gi tilbakemelding før denne dato !!!
      *        33 12 10 00
      *
      * --- !!! --- !!! --- !!!
      *
      *
      * Salgspris settes lik innkjøpspris
      * Prisdato settes lik dagens dato
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       060699 HKO  Nytt program
      * 6.10  230609 BSA  Oppdatert med sporingsinformasjon
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflvwpf    if   e           k disk
     ffenkl1    if   e           k disk    rename(fenkpfr:fenkl1r)
     fflvapf    o  a e             disk
      *
6.10  * Local data area
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av datastruktur
      *
      *  Recordbeskrivelse - ESAB
      *
     d                 ds
     d d_inpu                       128
     d  d_vare                       10    overlay(d_inpu:1)                    Varenr
     d  d_eann                       13    overlay(d_inpu:28)                   EAN-nr
     d  d_tek1                       20    overlay(d_inpu:15)                   Varetekst
     d  d_tek2                       10    overlay(d_inpu:36)                   Varetekst
     d  d_enhe                        2    overlay(d_inpu:54)                   Enhet
     d  d_inpr_kr                     9  0 overlay(d_inpu:59)                   Innkjøpspris, kroner
     d  d_inpr_Øre                    2  2 overlay(d_inpu:69)                   Innkjøpspris, ører
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_ldor          s              6  0
      *
      * Definering av variable i nøkler
      *
     d fenkl1_enhf     s                   like(feenhf)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_tek1_15       s             15
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser input-fil, og legger data ut på leverandør vareregister
      *
     c                   read      flvwpf                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      d_inpu = fwirad
      *
      * Skriver til leverandør vareregister
      *
     c                   eval      fwfirm = p_firm
     c                   eval      fwldor = p_ldor
     c                   eval      fwtek2 = *blank
     c                   eval      fwlvar = d_vare
     c                   eval      fweann = 0
     c                   eval      fwnobb = 0
     c                   eval      fwenhe = d_enhe
     c                   eval      fwinpr = d_inpr_kr + d_inpr_Øre
     c                   eval      fwsapr = fwinpr
     c                   eval      fwdato = *loval
     c                   eval      fwhkod = 1
      *
     c                   eval      fwline = fwline + 1
      *
     c                   movel     d_tek1        fwtek1
     c                   movel     d_tek2        w_tek1_15
     c                   move      w_tek1_15     fwtek1
      *
     c                   move      d_eann        fweann
      *
     c                   eval      fenkl1_enhf = d_enhe
     c     fenkl1_key    chain     fenkl1                             91
     c                   if        *in91 = *off
     c                   eval      fwenhe = feenht
     c                   endif
      *
     c                   time                    fwdato
      *
6.10 c                   time                    fwodat
6.10 c                   time                    fwotim
6.10 c                   eval      fwousr = l_user
6.10 c                   time                    fwedat
6.10 c                   time                    fwetim
6.10 c                   eval      fweusr = l_user
     c                   write     flvapfr
      *
     c                   read      flvwpf                                 90
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
      *
      * Key for oppslag på enhet-konvertering
      *
     c     fenkl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fenkl1_enhf
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
