     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : QO737R
      * Beskrivelse . . : Plukke ordrenummer til utlevering - pakkseddel
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
7.xx  * 7.00  050419 bsa  Nytt program
7.xx  *                   Vi kjører samme rutiner som vanlig plukking, men
7.xx  *                   vi ønsker ikke vise skjermbilder for valg. Kun
7.xx  *                   hvis det er feil eller mangler.
7.xx  * 7.00  050419 bsa  Bredere skjerm
8.01  *PRG2   220622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     fqo737d    cf   e             workstn
     f                                     sfile(b1sfl:srrn01)
     f                                     sfile(d1sfl:srrn02)
      *
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     ffohelu    uf   e           k disk    rename(fohepfr:fohelur)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffplolu    uf a e           k disk    rename(fplopfr:fplolur)
     ffplol1    if   e           k disk    rename(fplopfr:fplol1r)
     ffplolo    if   e           k disk    rename(fplopfr:fplolor)
     ffplllu    uf a e           k disk    rename(fpllpfr:fplllur)
     ffplll1    if   e           k disk    rename(fpllpfr:fplll1r)
     fvinfl1    if   e           k disk    rename(vinfpfr:vinfl1r)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     flwpll2    if   e           k disk    rename(lwplpfr:lwpll2r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
      *
      * Definering av variable i nøkler
      *
     d vinfl1_ityp     s                   like(vaityp)
     d fusrl1_user     s                   like(fbuser)
     d lwpll2_numm     s                   like(mknumm)
     d lwpll2_suff     s                   like(mksuff)
     d rkunl1_kund     s                   like(rkkund)
      *
      * Definering av variable
      *
     d w_alf1          s              1
     d w_firm          s                   like(fofirm)
     d w_on01          s                   like(vaon01)
     d w_on02          s                   like(vaon02)
     d w_on03          s                   like(vaon03)
     d w_on04          s                   like(vaon04)
     d w_on05          s                   like(vaon05)
     d w_on06          s                   like(vaon06)
     d w_onxt          s                   like(vaonxt)
     d w_aarr          s              4  0
     d p_kode          s              1
     d w_ldat          s                   like(foldat)
     d w_stat          s              1
7.xx d w_acti          s              1
      *
     d p_urec          s             80
     d p_stat          s              1
      *
7.xx d b_noshow        s              1    inz(*off)
      *
     d                 ds
     d d_urec                        80
     d  d_ufir                        3  0 overlay(d_urec)
     d  d_aarr                        4  0 overlay(d_urec:4)
     d  d_unum                        8  0 overlay(d_urec:8)
     d  d_usuf                        2  0 overlay(d_urec:16)
     d  d_ukod                        1    overlay(d_urec:18)
     d  d_date                         d   overlay(d_urec:19) datfmt(*iso)
     d  d_time                         t   overlay(d_urec:29) timfmt(*iso)
     d  d_user                       10    overlay(d_urec:37)
     d  d_wsid                       10    overlay(d_urec:47)
      *
      *
     d                uds
     d dswsid                901    910
     d dsuser                911    920
     d dssfir                944    946  0
     d l_fnav                951    980
     d dssyst                999    999
      *---------------------------------------------------------------------
      * Stand Alone Fields - TOP
      *---------------------------------------------------------------------
     d hedfir          s                   like(fofirm)
     d hednum          s                   like(fonumm)
     d hedsuf          s                   like(fosuff)
     d linfir          s                   like(fdfirm)
     d linlin          s                   like(fdline)
     d linnum          s                   like(fdnumm)
     d linsuf          s                   like(fdsuff)
     d otyfir          s                   like(vaofir)
     d otytyp          s                   like(vaotyp)
     d pllfir          s                   like(fklfir)
     d plllin          s                   like(fkllin)
     d pllnum          s                   like(fklonr)
     d pllsuf          s                   like(fklsuf)
     d plofir          s                   like(fkpfir)
     d plonum          s                   like(fkponr)
     d plosuf          s                   like(fkpsuf)
     d potfir          s                   like(fofirm)
     d potnum          s                   like(fonumm)
     d pototy          s                   like(footyp)
     d potsuf          s                   like(fosuff)
     d sfrn01          s                   like(srrn01)
     d sfrn02          s                   like(srrn02)
     d spge01          s              3  0
     d spge02          s              3  0
     d srrn01          s              4  0
     d srrn02          s              4  0
     d ssrn01          s                   like(srrn01)
     d ssrn02          s                   like(srrn02)
     d wpfirm          s                   like(fofirm)
     d wpftor          s              1
     d wpipor          s              1
     d w_jkod          s              2  0
     d wpnumm          s                   like(fonumm)
     d wpplor          s                   like(w_alf1)
     d w_retk          s              1
     d w_jtxt          s             20
     d wpsuff          s                   like(fosuff)
     d wseqen          s              1
     d wwoutq          s             10
     d wxnumm          s                   like(fkponr)
     d wxotyp          s                   like(fkpoty)
     d wxpdir          s              1  0
     d wxsuff          s                   like(fkpsuf)
      *
      *---------------------------------------------------------------------
      * Stand Alone Fields - BOTTOM
      *---------------------------------------------------------------------
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Velg om det skal plukkes eller vises                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     wpnumm        cabeq     *zero         b2taga
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  A1 Behandler plukking av ordrenummer                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     a1taga        tag
      *
      *  Hent opplysninger fra ORDRE-HODE-REGISTER
      *
     c                   move      wpnumm        hednum
     c                   move      wpsuff        hedsuf
     c     hedkey        chain     fohel1r                            90
      *
      *  Hent opplysninger fra ORDREYTYPE-REGISTER
      *
     c                   move      footyp        otytyp
     c     otykey        chain     votyl1r                            90
      *
      *  Hent opplysninger fra Kunderegister
      *
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1r
      *
      *  Ta vare på verdier fra ordretype-register
      *
     c                   move      vaon01        w_on01
     c                   move      vaon02        w_on02
     c                   move      vaon03        w_on03
     c                   move      vaon04        w_on04
     c                   move      vaon05        w_on05
     c                   move      vaon06        w_on06
     c                   move      vaonxt        w_onxt
      *
      *  Legger inn opplysningene i skjermbildefeltene
      *
     c                   move      fonumm        a1numm
     c                   move      fosuff        a1suff
     c                   move      footyp        a1foty
     c                   move      folage        a1lage
     c                   move      fokuna        a1kuna
     c                   move      vaotxt        a1ftxt
      *
     c                   move      vaonxt        a1otyp
7.xx  * Ingen spesifisering av varer skal skje
7.xx c                   eval      a1vlin = *zero
     c                   eval      a1till = *zero
     c                   eval      a1pluk = *zero
     c                   eval      a1ptxt = *zero
      *
      * Sjekk om det er foretatt plukking på Radioterminal
      *
     c                   exsr      sjk_rt
      *
      *  Lager skal bare vises i utplukksbilde
      *  dersom akkumulatortype er = 1
      *
     c                   move      *off          *in49
      *
      *  Hent, Tekst for lager
      *
     c                   exsr      sblagr
      *
      *  Send Alt
      *
7.xx c                   if        b_noshow = *off
7.xx c     a1tagx        tag
     c                   write     a1bld
      *
      *  Bare data
      *
     c     a1tagb        tag
     c                   exfmt     a1bld
7.xx c                   endif
      *
      *  F3=Avslutt program
      *
     c                   if        *inkc = *on
     c                   move      '9'           wpplor
     c                   goto      xslutt
     c                   endif
      *
      *  F4=Spørring ordretype
      *
     c                   if        *inkd = *on
     c                   if        w_arcd = 'A1BLD'
     c                   if        w_afld = 'A1OTYP'
     c                   call      'VA550R'
     c                   parm                    hedfir
     c                   parm                    a1otyp
     c                   goto      a1tagb
     c                   endif
     c                   endif
     c                   endif
      *
      *  F4=Spørring lager
      *
     c                   if        dssyst = 'A'
     c                   if        *inkd = *on
     c                   if        w_arcd = 'A1BLD'
     c                   if        w_afld = 'A1LAGE'
     c                   call      'RA510R'
     c                   parm                    wpfirm
     c                   parm                    w_jkod
     c                   parm                    w_jtxt
     c                   move      w_jkod        a1lage
     c                   move      w_jtxt        a1latx
     c                   goto      a1tagb
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
      *  Sjekk,  Finnes ordretype i ORDRETYPE-REGISTER
      *
     c                   move      a1otyp        otytyp
     c     otykey        chain     votyl1r                            90
     c                   if        *in90 = *on
     c                   move      *on           *in32
7.xx c                   if        b_noshow = *on
7.xx c                   eval      b_noshow = *off
7.xx c                   goto      a1tagx
7.xx c                   endif
     c                   goto      a1tagb
     c                   endif
      *
      *  Sjekk,  Er oppgitt til-ordretype gyldig ?
      *
     c                   if        faanxt =  1
     c                   if        a1otyp <> w_on01 and
     c                             a1otyp <> w_on02 and
     c                             a1otyp <> w_on03 and
     c                             a1otyp <> w_on04 and
     c                             a1otyp <> w_on05 and
     c                             a1otyp <> w_on06 and
     c                             a1otyp <> w_onxt
     c                   move      *on           *in33
7.xx c                   if        b_noshow = *on
7.xx c                   eval      b_noshow = *off
7.xx c                   goto      a1tagx
7.xx c                   endif
     c                   goto      a1tagb
     c                   endif
     c                   endif
      *
      *  Sjekk,  Kontantkunde kan ikke plukkes til pakkseddel
      *
     c                   if        vaoakk >  1 and
     c                             rkkres =  2
     c                   eval      *in36  =  *on
7.xx c                   if        b_noshow = *on
7.xx c                   eval      b_noshow = *off
7.xx c                   goto      a1tagx
7.xx c                   endif
     c                   goto      a1tagb
     c                   endif
      *
      *  Sjekk,  Kunde med kredittsprerre kan ikke plukkes til pakkseddel
      *
     c                   if        vaoakk >  1 and
     c                             rkkres =  1
     c                   exfmt     a1msg1
7.xx c                   if        b_noshow = *on
7.xx c                   eval      b_noshow = *off
7.xx c                   goto      a1tagx
7.xx c                   endif
     c                   goto      a1tagb
     c                   endif
      *
      *  Sjekk,  Finnes lager i KODE-REGISTER
      *
     c                   eval      a1latx = *blank
     c                   exsr      sblagr
     c                   if        w_retk <> *blank
     c                   move      *on           *in34
7.xx c                   if        b_noshow = *on
7.xx c                   eval      b_noshow = *off
7.xx c                   goto      a1tagx
7.xx c                   endif
     c                   goto      a1tagb
     c                   endif
      *
      *  Dersom noen valg er endret, sendt bilde pånytt
      *
     c                   if        a1lage <> folage
     c                   move      a1lage        folage
     c                   goto      a1tagb
     c                   endif
      *
      * Hent inn ordre-info-kode i inforegister
      *
     c                   eval      vinfl1_ityp = foityp
     c     vinfl1_key    chain     vinfl1r                            98
     c                   if        *in98  = *on
     c                   eval      vaifak = *blank
     c                   endif
      *
      *  Dersom ordre har en infokode, og dette er fakturering,
      *  sjekkes det om fakturering skal stoppes.
      *
     c                   if        foityp <> *blank
     c                   if        vaoakk =  3
     c                   if        vaifak = '1'
     c                   eval      wpplor = '9'
     c                   goto      xslutt
     c                   endif
     c                   endif
     c                   endif
      *
      *  Kontroll om bruker har lov til å sette igang fakturering.
      *
     c                   if        fbko08 <> 1
     c                   if        vaoakk =  3
     c                   eval      wpplor = '9'
     c                   goto      xslutt
     c                   endif
     c                   endif
      *
      *  Oppdater ORDRE-HODE-REGISTER, Ordren er til behandling
      *
     c                   call      'FO709R'
     c                   parm                    hednum
     c                   parm                    hedsuf
      *
     c     hedkey        chain     fohelur                            90
     c                   if        *in90 = *off
     c                   move      3             fokode
     c                   time                    foedat
     c                   time                    foetim
     c                   eval      foeusr = dsuser
     c                   update    fohelur
     c                   endif
      *
      *  Oppdater UTPLUKK-PARAM-REGISTER med ny ordre
      *
     c                   move      wpnumm        plonum
     c                   move      wpsuff        plosuf
     c     plokey        chain     fplolur                            90
     c                   if        *in90 = *on
     c                   move      plofir        fkpfir
     c                   move      plonum        fkponr
     c                   move      plosuf        fkpsuf
     c                   move      a1otyp        fkpoty
     c                   move      a1lage        fkplag
     c                   move      dswsid        fkpwsd
     c                   move      dsuser        fkpusr
     c                   eval      fkpalt = *zero
     c                   eval      fkpdir = *zero
     c                   move      a1vlin        fkpres
     c                   move      a1ptxt        fkptxt
     c                   eval      fkpkol = *zero
     c                   eval      fkpnet = *zero
     c                   eval      fkpbru = *zero
     c                   eval      fkplmt = *blank
     c                   eval      fkpfpr = *zero
     c                   eval      fkpfte = *blank
     c                   eval      fkpepr = *zero
     c                   eval      fkpete = *blank
     c                   write     fplolur
     c                   else
     c                   update    fplolur
     c                   endif
      *
      *  Subrutinen for resting av varelinjer skal hentes opp
      *
     c                   if        a1vlin = '1'
     c                             or a1vlin = '2'
     c                   exsr      xd1bld
     c                   endif
      *
      *  Subrutinen for tilleggsopplysninger av varelinjer skal hentes opp
      *
     c                   if        a1till = '1'
     c                   exsr      xb7win
     c                   endif
      *
      *  Subrutinen for leveringsdato og tilleggsinfo 1 og 2
      *
     c                   if        a1pluk = '1'
     c                   exsr      o1subr
     c                   endif
      *
      *  Kaller opp utskrift-program for å sette inn alternativ
      *  og om direktevalg skal benyttes
      *
7.xx c                   eval      w_acti = 'U'
7.xx c                   call      'QO600R'
     c                   parm                    wpipor
     c                   parm                    plofir
     c                   parm                    plonum
     c                   parm                    plosuf
7.xx c                   parm                    w_acti
      *
     c                   move      wpipor        wpplor
      *
     c                   goto      xslutt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  B2 Behandler åpningsbildet samt funksjonstaster                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      *  Bare data
      *
     c     b2tagb        tag
     c                   exsr      xdsp01
      *
      *  F1=Vise bla-alternativer
      *
     c                   if        *inka = *on
     c                   exsr      xh1win
     c                   endif
      *
      *  F3=Avslutt program
      *
     c                   if        *inkc = *on
     c                   move      '9'           wpplor
     c                   goto      xslutt
     c                   endif
      *
      *  F5=Forny
      *
     c                   if        *inke = *on
     c                   exsr      xlsp01
     c                   goto      b2tagb
     c                   endif
      *
      *  F15=Starter utskrift
      *
     c                   if        *inkp = *on
     c                   call      'FO604R'
     c                   parm                    wpftor
     c                   parm                    wpfirm
     c                   parm                    wxnumm
     c                   parm                    wxsuff
     c                   parm                    wxotyp
     c                   parm                    wxpdir
     c                   parm                    wwoutq
     c                   goto      b2tagb
     c                   endif
      *
      *  Roll
      *
     c                   if        *in10 = *on
     c                   exsr      xcrt01
     c                   goto      b2tagb
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandling av startverdier for start f.o.m.                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Posisjoner
      *  startverdi
      *  på ordrenummer, men rutinen blar bakfra
      *
     c                   if        b2numm <> *zero
     c                   if        wseqen = 'B'
     c                   move      b2numm        plonum
     c                   move      99            plosuf
     c     plokey        setll     fplol1
     c                   exsr      xclr01
     c                   exsr      xcrt01
     c                   eval      b2numm = *zero
     c                   goto      b2tagb
     c                   endif
     c                   endif
      *  Posisjoner
      *  startverdi
      *  på ordretype + ordrenummer
      *
     c                   if        b2numm <> *zero
     c                   if        wseqen = 'O'
     c                   move      b2numm        potnum
     c                   eval      potsuf = *zero
     c     potkey        setll     fplolo
     c                   exsr      xclr01
     c                   exsr      xcrt01
     c                   eval      b2numm = *zero
     c                   goto      b2tagb
     c                   endif
     c                   endif
      *
      *  Posisjoner
      *  startverdi
      *  på ordrenummer
      *
     c                   if        b2numm <> *zero
     c                   move      ' '           wseqen
     c                   move      b2numm        plonum
     c                   eval      plosuf = *zero
     c     plokey        setll     fplol1
     c                   exsr      xclr01
     c                   exsr      xcrt01
     c                   eval      b2numm = *zero
     c                   goto      b2tagb
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandling av subfile, Foretar valgene                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Sjekk om valg
      *  er foretatt
      *
     c                   do        ssrn01
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
      *  Ved valg 2 eller 7, Sjekk om dette er en utskrift
      *  Hvis dette er en utskrift skal den ikke kunne vedlikeholdes
      *
     c                   if        b1valg = '2 '
     c                             or b1valg = '7 '
      *
      *  Hent opplysninger fra ORDRE-HODE-REGISTER
      *
     c                   move      b1numm        hednum
     c                   move      b1suff        hedsuf
     c     hedkey        chain     fohel1r                            90
      *
     c                   if        fokode <> 3
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   endif
      *
      *  Registrering/Endring av resting
      *
     c                   if        b1valg = '2 '
     c                   move      b1numm        hednum
     c                   move      b1suff        hedsuf
     c                   exsr      xd1bld
     c                   endif
      *
      *  Slette utplukk/Fjerne merke for at ordren er til behandling
      *
     c                   if        b1valg = '4 '
     c                   move      b1numm        plonum
     c                   move      b1suff        plosuf
     c                   exsr      xb4win
     c                   iter
     c                   endif
      *
      *  Registre/Endre tilleggsopplysninger
      *
     c                   if        b1valg = '7 '
     c                   move      b1numm        plonum
     c                   move      b1suff        plosuf
     c                   exsr      xb7win
     c                   iter
     c                   endif
      *
     c                   enddo
      *
      *  Nullstiller/blanker feltet for start f.o.m.
      *
     c                   eval      b2numm = *zero
      *
     c                   goto      b2taga
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  B4 Behandle bilde for sletting av utvalg                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xb4win        begsr
      *
     c                   move      plonum        b4numm
      *
     c                   write     b4bld
     c                   exfmt     b4bld
      *
      *  F3=Avslutt
      *
     c     *inkc         cabeq     *on           b4tag1
      *
      *  Kaller opp subprogram for å slette utvalg og blanke merking
      *
     c                   call      'FO412R'
     c                   parm                    wpipor
     c                   parm                    plofir
     c                   parm                    plonum
     c                   parm                    plosuf
      *
      *  Oppdater subfile
      *
     c                   eval      b1wsid = *blank
     c                   movel     '*Slettet'    b1wsid
      *
     c     b4tag1        tag
      *
     c                   eval      b1valg = *blank
     c                   update    b1sfl
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  B7 Behandle bilde for innlegging av tilleggsopplysninger       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xb7win        begsr
      *
      *  Henter inn opplysninger fra UTPLUKK-PARAM-REGISTER
      *
     c     plokey        chain     fplol1                             90
     c                   if        *in90 = *off
     c                   move      fkponr        b7numm
     c                   move      fkpkol        b7koli
     c                   move      fkpnet        b7nett
     c                   move      fkpbru        b7brut
     c                   move      fkplmt        b7lmtx
     c                   move      fkpfpr        b7frpr
     c                   move      fkpfte        b7frte
     c                   move      fkpepr        b7empr
     c                   move      fkpete        b7emte
     c                   endif
      *
     c                   write     b7bld
     c                   exfmt     b7bld
      *
      *  F3=Avslutt
      *
     c     *inkc         cabeq     *on           xb7slt
      *
      *  Oppdater UTPLUKK-PARAM-REGISTER
      *
     c     plokey        chain     fplolur                            90
     c                   if        *in90 = *off
     c                   move      b7koli        fkpkol
     c                   move      b7nett        fkpnet
     c                   move      b7brut        fkpbru
     c                   move      b7lmtx        fkplmt
     c                   move      b7frpr        fkpfpr
     c                   move      b7frte        fkpfte
     c                   move      b7empr        fkpepr
     c                   move      b7emte        fkpete
     c                   update    fplolur
     c                   endif
      *
      *  Oppdater subfile
      *
     c     xb7slt        tag
      *
     c                   if        b1valg <> *blank
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatering av tilleggsinformasjon ved kjørekontorløsning
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     o1subr        begsr
      *
      * Initierer skjermbilde
      *
     c                   eval      o1ldat = 0
     c                   eval      o1inf1 = foinf1
     c                   eval      o1inf2 = foinf2
      *
     c                   if        foldat <> *loval
     c     *dmy          move      foldat        o1ldat
     c                   endif
      *
      * Send bilde o1win
      *
     c     o1tagb        tag
      *
     c                   exfmt     o1win
      *
      * F3=Avslutt
      *
     c                   if        *inkc or *inkl
     c                   eval      *inkc = *on
     c                   leavesr
     c                   endif
      *
      * F4=Spørring
      *
      *
      * Validering
      *
      *
      * Leveringsdato må oppgis
      *
     c                   if        o1ldat = 0
     c                   eval      *in33 = *on
     c                   goto      o1tagb
     c                   endif
      *
      * Leveringsdato må være en gyldig dato
      *
     c     *dmy          test(d)                 o1ldat                 34
     c                   if        *in34 = *on
     c                   goto      o1tagb
     c                   endif
      *
      * Leveringsdato må være større enn/lik ordre-dato
      *
     c     *dmy          move      o1ldat        w_ldat
     c                   if        w_ldat < fobdat
     c                   eval      *in35 = *on
     c                   goto      o1tagb
     c                   endif
      *
      * Oppdaterer ordre
      *
     c     hedkey        chain     fohelur
     c                   if        %found(fohelu)
     c                   eval      foldat = %date(o1ldat:*dmy)
     c                   eval      foinf1 = o1inf1
     c                   eval      foinf2 = o1inf2
     c                   time                    foedat
     c                   time                    foetim
     c                   eval      foeusr = dsuser
     c                   update    fohelur
     c                   endif
      * oppdat. status
     c                   eval      d_ufir = fofirm
     c                   eval      d_unum = fonumm
     c                   eval      d_usuf = fosuff
     c                   eval      d_ukod = '0'
     c                   exsr      upd_logg
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  D1 Behandle bilde for PLUKKING AV LINJER                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xd1bld        begsr
      *
     c                   move      *on           *in21
      *
      *  Legger inn ordrenummer og suffix i nøkklene
      *
     c                   move      hednum        linnum
     c                   move      hedsuf        linsuf
     c                   move      hednum        pllnum
     c                   move      hedsuf        pllsuf
      *
      *  Hent opplysninger fra ORDRE-HODE-REGISTER
      *
     c     hedkey        chain     fohel1r                            90
     c                   move      fonumm        d2numm
     c                   move      fosuff        d2suff
     c                   move      fokund        d2kund
     c                   move      fokuna        d2kuna
      *
      *  Hent opplysninger fra ORDRE-LINJE-REGISTER
      *
     c                   eval      linlin = *zero
     c                   eval      plllin = *zero
     c     linkey        setll     fodtl1
     c                   exsr      xclr02
     c                   exsr      xcrt02
      *
      *  Send Alt
      *
     c     d2taga        tag
     c                   write     d2cmd
      *
      *  Bare data
      *
     c     d2tagb        tag
     c                   exsr      xdsp02
      *
      *  F3=Avslutt
      *
     c     *inkc         cabeq     *on           xd1slt
      *
      *  F5=Forny
      *
     c                   if        *inke = *on
     c                   exsr      xlsp02
     c                   goto      d2tagb
     c                   endif
      *
      *  Roll
      *
     c                   if        *in10 = *on
     c                   exsr      xcrt02
     c                   goto      d2tagb
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandling av startverdier for start f.o.m.                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Posisjoner
      *  startverdi
      *  på linjenummer
      *
     c                   if        d2line <> *zero
     c                   move      d2line        linlin
     c     linkey        setll     fodtl1r
     c                   exsr      xclr02
     c                   exsr      xcrt02
     c                   eval      d2line = *zero
     c                   goto      d2tagb
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandling av subfile, Foretar valgene                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Sjekk om valg
      *  er foretatt
      *
     c                   do        ssrn02
     c                   readc     d1sfl
     c                   if        %eof
     c                   leave
     c                   endif
      *
      *  Sjekk alle linjer
      *
     c                   move      d1line        plllin
     c                   exsr      xd7win
      *
     c                   enddo
      *
      *  Nullstiller/blanker feltet for start f.o.m.
      *
     c                   eval      d2line = *zero
      *
     c                   exsr      xlsp02
     c                   goto      d2taga
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutter behandlingen av utplukk/resting                      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xd1slt        tag
      *
      *  Oppdaterer subfile i forrige bilde (B1) før retur
      *
     c                   if        b1valg <> *blank
     c     srrn01        chain     b1sfl                              90
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  D7 Behandle bilde for OPPDATERING AV PLUKK-REGISTER            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xd7win        begsr
      *
      *  Benytter Z-ADD på antall inn i nytt felt fordi feltene
      *  ikke er like store, benytter kortere feltlengde i bildet
      *
     c     pllkey        chain     fplllur                            96
      *
      *  Hverken antall eller kode er tastet, hopp ut, Slett evnt. record
      *
     c                   if        d1kode = '9'
     c                   eval      d1kode = *blank
     c                   endif
     c                   if        d1leve = *zero
     c                   if        d1kode = *blank
     c                   if        *in96 = *off
     c                   delete    fplllur
     c                   endif
     c                   goto      xd7slt
     c                   endif
     c                   endif
      *
      *  Legg inn verdier i UTPLUKK-LINJE-REGISTER
      *
     c                   if        d1vare = *blank
     c                   if        d1leve = 1
     c                   move      '1'           d1kode
     c                   endif
     c                   if        d1leve = 3
     c                   move      '3'           d1kode
     c                   endif
     c                   if        d1leve = 4
     c                   move      '4'           d1kode
     c                   endif
     c                   eval      d1leve = *zero
     c                   endif
      *
     c                   eval      fklant = d1leve
     c                   move      d1kode        fklkod
      *
     c                   if        *in96 = *off
     c                   update    fplllur
     c                   else
     c                   move      pllfir        fklfir
     c                   move      pllnum        fklonr
     c                   move      pllsuf        fklsuf
     c                   move      plllin        fkllin
     c                   write     fplllur
     c                   endif
      *
      *  Oppdater subfile
      *
     c     xd7slt        tag
      *
      * Setter farge
     c                   eval      *in55 = *off
     c                   eval      *in56 = *off
     c                   eval      *in57 = *off
     c                   eval      *in58 = *off
     c                   eval      *in59 = *on
     c                   select
     c                   when      d1kode = '1'
     c                   eval      *in57 = *on
     c                   eval      *in59 = *off
     c                   when      d1kode = '3'
     c                   eval      *in56 = *on
     c                   eval      *in59 = *off
     c                   when      d1kode = '4'
     c                   eval      *in55 = *on
     c                   eval      *in59 = *off
     c                   when      d1kode = '5'
     c                   eval      *in58 = *on
     c                   eval      *in59 = *off
     c                   endsl
      *
     c                   update    d1sfl
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  H1 Behandle bilde for bla-alternativer                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xh1win        begsr
      *
      *  Send Alt
      *
     c                   write     h1bld
     c                   exfmt     h1bld
      *
     c     *inkc         cabeq     *on           xh1slt
      *
      *  Kun en bestemt ordre-type
      *
     c                   if        h1otyp <> *blank
     c                   move      'O'           wseqen
     c                   eval      potnum = *zero
     c                   eval      potsuf = *zero
     c                   move      h1otyp        pototy
     c     potkey        setll     fplolo
     c                   exsr      xclr01
     c                   exsr      xcrt01
     c                   eval      b2numm = *zero
     c                   eval      h1otyp = *blank
     c                   goto      xh1slt
     c                   endif
      *
      *  Ved blaing i ordre-register skal det blas bakfra
      *
     c                   if        h1valg = '1'
     c                   move      'B'           wseqen
     c                   move      999999        plonum
     c                   move      99            plosuf
     c     plokey        setll     fplol1
     c                   exsr      xclr01
     c                   exsr      xcrt01
     c                   eval      b2numm = *zero
     c                   eval      h1valg = *blank
     c                   goto      xh1slt
     c                   endif
      *
      *  Dersom ingen er valgt, men ENTER er trykket
      *  skal startverdi settes til ordrenummer lik null
      *
     c                   move      ' '           wseqen
     c                   eval      plonum = *zero
     c                   eval      plosuf = *zero
     c     plokey        setll     fplol1
     c                   exsr      xclr01
     c                   exsr      xcrt01
     c                   eval      b2numm = *zero
      *
     c     xh1slt        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Sjekk/Hent tekst for lager                        RS710R       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     sblagr        begsr
      *
     c                   call      'RS710R'
     c                   parm                    w_retk
     c                   parm                    a1lage
     c                   parm                    w_jtxt
      *
     c                   eval      a1latx = *blank
      *
     c                   if        w_retk = *blank
     c                   movel     w_jtxt        a1latx
     c                   endif
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Fornyer subfilen (dvs. loader subfilen pånytt)                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xlsp01        begsr
      *
     c                   if        sfrn01 <> *zero
     c     sfrn01        chain     b1sfl                              60
     c                   endif
      *
     c                   move      b1numm        plonum
     c                   move      b1suff        plosuf
      *
     c                   if        wseqen = 'B'
     c     plokey        setll     fplol1
     c                   endif
     c                   if        wseqen = 'O'
     c     potkey        setll     fplolo
     c                   endif
     c                   if        wseqen = *blank
     c     plokey        setll     fplol1
     c                   endif
      *
     c                   exsr      xclr01
     c                   exsr      xcrt01
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Display subfile                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xdsp01        begsr
      *
     c                   if        srrn01 <> 0
     c                   move      *on           *in12
     c                   endif
      *
     c                   move      *on           *in13
     c                   exfmt     b2ctl
     c                   move      *off          *in12
     c                   move      *off          *in13
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tøm subfile                                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xclr01        begsr
     c                   move      *on           *in14
     c                   write     b2ctl
     c                   move      *off          *in14
      *
     c                   move      *off          *in15
     c                   eval      srrn01 = 0
     c                   eval      sfrn01 = 0
     c                   eval      ssrn01 = 0
     c                   eval      spge01 = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Fyll opp subfile                                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xcrt01        begsr
     c                   eval      spge01 = spge01 + 13
     c                   eval      srrn01 = ssrn01
      *
     c                   dou       srrn01 = spge01
      *
     c                   if        wseqen = 'B'
     c                   readp     fplol1                                 15
     c                   endif
     c                   if        wseqen = 'O'
     c                   read      fplolo                                 15
     c                   if        *in15 = *off
     c                   if        fkpoty <> pototy
     c                   move      *on           *in15
     c                   endif
     c                   endif
     c                   endif
     c                   if        wseqen = *blank
     c                   read      fplol1                                 15
     c                   endif
      *
     c     *in15         cabeq     *on           xend01
      *
     c                   if        fkpfir <> w_firm
     c                   goto      xend01
     c                   endif
      *
     c                   eval      srrn01 = srrn01 + 1
     c                   move      fkponr        b1numm
     c                   move      fkpsuf        b1suff
     c                   move      fkpoty        b1otyp
     c                   move      fkpwsd        b1wsid
     c                   move      fkpusr        b1user
     c                   move      fkpalt        b1palt
     c                   move      fkptxt        b1ptxt
     c                   write     b1sfl
     c                   enddo
      *
     c     xend01        tag
      *
     c                   if        srrn01 > ssrn01
     c                   eval      sfrn01 = ssrn01 + 1
     c                   endif
      *
     c                   eval      ssrn01 = srrn01
     c                   eval      w_virn = sfrn01
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Fornyer subfilen (dvs. loader subfilen pånytt)                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xlsp02        begsr
      *
     c                   if        sfrn02 <> *zero
     c     sfrn02        chain     d1sfl                              90
     c                   endif
      *
     c                   if        *in11 = *on
     c                   eval      linlin = d1line
     c                   eval      plllin = *zero
     c     linkey        setll     fodtl1
     c*
     c                   z-add     0             tell              2 0
     c     tell          doueq     14
     c                   readp     fodtl1                                 15
     c                   add       1             tell
     c                   if        *in15 = *off
     c                   if        fdnumm <> linnum
     c                   move      *on           *in15
     c                   eval      tell = 14
     c                   endif
     c                   endif
     c                   if        *in15 = *off
     c                   if        fdsuff <> linsuf
     c                   move      *on           *in15
     c                   eval      tell = 14
     c                   endif
     c                   endif
     c*
     c                   enddo
     c                   endif
      *
     c                   move      d1line        linlin
     c                   if        tell = 14
     c                   move      fdline        linlin
     c                   z-add     0             tell
     c                   endif
      *
     c     linkey        setll     fodtl1
      *
     c                   exsr      xclr02
     c                   exsr      xcrt02
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Display subfile                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xdsp02        begsr
      *
     c                   if        srrn02 <> 0
     c                   move      *on           *in12
     c                   endif
      *
     c                   move      *on           *in13
     c                   exfmt     d2ctl
     c                   move      *off          *in12
     c                   move      *off          *in13
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tøm subfile                                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xclr02        begsr
     c                   move      *on           *in14
     c                   write     d2ctl
     c                   move      *off          *in14
      *
     c                   move      *off          *in15
     c                   eval      srrn02 = 0
     c                   eval      sfrn02 = 0
     c                   eval      ssrn02 = 0
     c                   eval      spge02 = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Fyll opp subfile                                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xcrt02        begsr
7.xx c                   eval      spge02 = spge02 + 13
     c                   eval      srrn02 = ssrn02
      *
     c                   dou       srrn02 = spge02
      *
     c                   read      fodtl1                                 15
     c                   if        *in15 = *off
     c                   if        fdnumm <> linnum
     c                   move      *on           *in15
     c                   endif
     c                   endif
     c                   if        *in15 = *off
     c                   if        fdsuff <> linsuf
     c                   move      *on           *in15
     c                   endif
     c                   endif
      *
     c     *in15         cabeq     *on           xend02
      *
     c                   eval      srrn02 = srrn02 + 1
      *
     c                   eval      d1leve = 0
     c                   eval      d1kode = ' '
     c                   move      fdline        plllin
     c     pllkey        chain     fplll1                             96
     c                   if        *in96 = *off
     c                   eval      d1leve = fklant
     c                   move      fklkod        d1kode
     c                   endif
      *
     c                   move      fdline        d1line
     c                   move      fdanta        d1anta
     c                   movel     fdvare        d1vare
     c                   move      fdtek1        d1tek1
     c                   move      fdenh1        d1enh1
      *
      * Lassnummer blir lagt inn
      * i de to siste posisjoner
      * i varenummer dersom > 0.
      *
      *    FDLASS        IFGT      0
      *                  MOVE      FDLASS        D1VARE
      *                  ENDIF
      *
      * Sette farge
     c                   eval      *in55 = *off
     c                   eval      *in56 = *off
     c                   eval      *in57 = *off
     c                   eval      *in58 = *off
     c                   eval      *in59 = *on
     c                   select
     c                   when      d1kode = '1'
     c                   eval      *in57 = *on
     c                   eval      *in59 = *off
     c                   when      d1kode = '3'
     c                   eval      *in56 = *on
     c                   eval      *in59 = *off
     c                   when      d1kode = '4'
     c                   eval      *in55 = *on
     c                   eval      *in59 = *off
     c                   when      d1kode = '5'
     c                   eval      *in58 = *on
     c                   eval      *in59 = *off
     c                   endsl
      *
     c                   write     d1sfl
     c                   enddo
      *
     c     xend02        tag
      *
     c                   if        srrn02 > ssrn02
     c                   eval      sfrn02 = ssrn02 + 1
     c                   endif
      *
     c                   eval      ssrn02 = srrn02
     c                   eval      svrn02 = sfrn02
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke om det er plukket noe på radioterminal
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_rt        begsr
      *
      *  Sjekker siste status - bare status 7 skal behandles
      *
     c                   eval      p_kode = *blank
     c                   eval      w_aarr = %subdt(fobdat:*years)
     c                   call      'FU706R '
     c                   parm                    w_aarr
     c                   parm                    wpnumm
     c                   parm                    wpsuff
     c                   parm                    p_kode
      *
     c                   if        p_kode <> '7'
     c                   goto      end_rt
     c                   endif
      *
     c                   eval      a1vlin = '1'
      *
      * Leser plukkfil på radio og oppdaterer man. plukkefil
      *
     c                   eval      pllnum = wpnumm
     c                   eval      pllsuf = wpsuff
      *
     c                   eval      lwpll2_numm = wpnumm
     c                   eval      lwpll2_suff = wpsuff
     c     lwpll2_key    setll     lwpll2
     c     lwpll2_key    reade     lwpll2
      *
     c                   dow       not %eof
     c                   eval      plllin = mkline
     c     pllkey        chain     fplllur                            96
     c                   if        *in96 = *off
     c                   eval      fklant = mkantp
     c                   update    fplllur
     c                   else
     c                   move      pllfir        fklfir
     c                   move      pllnum        fklonr
     c                   move      pllsuf        fklsuf
     c                   move      plllin        fkllin
     c                   eval      fklant = mkantp
     c                   write     fplllur
     c                   endif
     c     lwpll2_key    reade     lwpll2
     c                   enddo
      *
     c     end_rt        endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for slette plukk-reg. på RadioTerm.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     upd_logg      begsr
      *
      *
     c                   time                    d_date
     c                   time                    d_time
     c                   eval      d_user = dsuser
     c                   eval      d_wsid = dswsid
     c                   eval      d_aarr = %subdt(fobdat:*years)
     c                   eval      p_urec = d_urec
     c                   call      'FU900R  '
     c                   parm                    p_stat
     c                   parm                    p_urec
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     c                   eval      spge01 = 0
     c                   eval      srrn01 = 0
     c                   eval      wseqen = *blank
     c                   eval      w_retk = *blank
      *
     c                   eval      spge02 = 0
     c                   eval      srrn02 = 0
      *
     c                   eval      wwoutq = *blank
      *
     c                   eval      wpipor = *blank
     c                   eval      wpftor = *blank
     c                   eval      wxpdir = *zero
      *
      *  Redefinering av felter
      *
      *  Redefinering av parameterfeltene
      *
      *
      *  Redefinering av nøkler
      *
      *  Key for file : ORDRETYPE-REGISTER
      *
     c     otykey        klist
     c                   kfld                    otyfir
     c                   kfld                    otytyp
      *
      *  Key for file : ORDRE-HODE-REGISTER
      *
     c     hedkey        klist
     c                   kfld                    hedfir
     c                   kfld                    hednum
     c                   kfld                    hedsuf
      *
      *  Key for file : ORDRE-LINJE-REGISTER
      *
     c     linkey        klist
     c                   kfld                    linfir
     c                   kfld                    linnum
     c                   kfld                    linsuf
     c                   kfld                    linlin
      *
      *  Key for file : UTPLUKK-PARAM-REGISTER
      *
     c     plokey        klist
     c                   kfld                    plofir
     c                   kfld                    plonum
     c                   kfld                    plosuf
      *
      *  Key for file : UTPLUKK-PARAM-ORDRETYPE-REGISTER
      *
     c     potkey        klist
     c                   kfld                    potfir
     c                   kfld                    pototy
     c                   kfld                    potnum
     c                   kfld                    potsuf
      *
      *  Key for file : UTPLUKK-LINJE-REGISTER
      *
     c     pllkey        klist
     c                   kfld                    pllfir
     c                   kfld                    pllnum
     c                   kfld                    pllsuf
     c                   kfld                    plllin
      *
      * Key for file : KUNDE-REGISTER
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på ordre-info-type
      *
     c     vinfl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vinfl1_ityp
      *
      * Key for brukerregister
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrl1_user
      *
      * Key for ofak kode-register
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les på radioterminal-trans
      *
     c     lwpll2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwpll2_numm
     c                   kfld                    lwpll2_suff
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     *entry        plist
     c                   parm                    wpplor
     c                   parm                    wpfirm
     c                   parm                    wpnumm
     c                   parm                    wpsuff
      *
      *  Legger inn firma i nøkklene
      *
     c                   move      wpfirm        otyfir
     c                   move      wpfirm        hedfir
     c                   move      wpfirm        linfir
     c                   move      wpfirm        plofir
     c                   move      wpfirm        potfir
     c                   move      wpfirm        pllfir
     c                   move      wpfirm        w_firm
      *
      *  Nullstiller felter som alltid skal være blanke/null
      *
     c                   eval      plonum = *zero
     c                   eval      plosuf = *zero
     c                   eval      potnum = *zero
     c                   eval      potsuf = *zero
     c                   eval      pllnum = *zero
     c                   eval      pllsuf = *zero
     c                   eval      plllin = *zero
      *
     c                   eval      wxnumm = *zero
     c                   eval      wxsuff = *zero
     c                   eval      wxotyp = *blank
7.xx c                   eval      b_noshow = *on
      *
      *  Fyller opp skjermbildet for første visning
      *
     c     plokey        setll     fplol1
     c                   exsr      xcrt01
      *
      * Hente inn brukerregister
      *
     c                   eval      fusrl1_user = dsuser
     c     fusrl1_key    chain     fusrl1r                            90
      *
      * Hente inn ofak kode-register
      *
     c     fstsl1_key    chain     fstsl1r                            90
      *
     c                   endsr
