     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : AN015R
      * Beskrivelse . . : Generering av nattrutine, kontroll tape
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * R1.08 251097 AMD  Nytt program for kontroll av tape
      *
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fqcllesrc  o  a f  096        disk
      *
      * Definering av tabeller
      *
     d rec             s             80    dim(52) ctdata perrcd(1)             Programlinjer
     d fra             s              1    dim(10)                              Hjelpe-array
     d til             s              1    dim(84)                              Hjelpe-array
      *
      * Definering av data-strukturer
      *
     d                 ds
     d d_dato                         6  0
     d  d_aaar                        2  0 overlay(d_dato)
     d  d_mmnd                        2  0 overlay(d_dato:3)
     d  d_ddag                        2  0 overlay(d_dato:5)
      *
     d                 ds
     d d_valg                       128
     d  d_time                        6  0 overlay(d_valg)
     d  d_dens                       11    overlay(d_valg:7)
     d  d_rorg                        1    overlay(d_valg:18)
     d  d_back                        1    overlay(d_valg:19)
     d  d_ssbs                        1    overlay(d_valg:20)
     d  d_prc1                        1    overlay(d_valg:21)
     d  d_prc2                        1    overlay(d_valg:22)
     d  d_genp                        1    overlay(d_valg:23)
     d  d_savf                        1    overlay(d_valg:24)
     d  d_smap                        1    overlay(d_valg:25)
      *                                                                     -
      * Definering av parametere
      *                                                                     -
     d p_valg          s            128
      *                                                                     -
      * Definering av variable
      *                                                                     -
     d n               s              2  0
     d x               s              2  0
     d y               s              2  0
     d z               s              2  0
     d w_dato          s              6  0
     d w_recc          s             84
     d w_tell          s              6  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Legg ut records til Cl-program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Legger ut starten på CL-programmet
      *
     c                   for       n = 1 to 5
     c                   add       100           w_tell
     c                   movel     rec(n)        w_recc
     c                   except    xrecut
     c                   endfor
      *
      * Legger inn riktig density i parameter i CL-programmet
      *
     c                   exsr      xdensi
      *
      * Legger ut siste del av CL-programmet
      *
     c                   for       n = 10 to 52
     c                   add       100           w_tell
     c                   movel     rec(n)        w_recc
     c                   except    xrecut
     c                   endfor
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Legger inn riktig DENSITY
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xdensi        begsr
      *
      * Redigerer CL-linje hvor DENSITY legges inn (CHGVAR)
      *
     c                   movea     d_dens        fra
     c                   eval      z = *zero
      *
     c                   for       y = 1 to 10
     c                   if        fra(y) <> *blank
     c                   add       1             z
     c                   endif
     c                   endfor
      *
     c                   if        z = 5
     c                   movel     rec(6)        w_recc
     c                   endif
     c                   if        z = 6
     c                   movel     rec(7)        w_recc
     c                   endif
     c                   if        z = 7
     c                   movel     rec(8)        w_recc
     c                   endif
     c                   if        z = 8
     c                   movel     rec(9)        w_recc
     c                   endif
     c                   eval      til = *blank
     c                   movea     w_recc        til
      *
     c                   z-add     41            z
      *
     c                   for       y = 1 to 10
     c                   if        fra(y) <> *blank
     c                   add       1             z
     c                   eval      til(z) = fra(y)
     c                   endif
     c                   endfor
      *
     c                   eval      w_recc = *blank
     c                   movea     til           w_recc
     c                   add       100           w_tell
     c                   except    xrecut
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c                   eval      w_recc = *blank
     c                   eval      w_tell = *zero
     c                   eval      w_dato = *zero
     c                   eval      n = *zero
     c                   eval      x = *zero
     c                   eval      y = *zero
     c                   eval      z = *zero
      *
     c                   eval      d_aaar = uyear
     c                   eval      d_mmnd = umonth
     c                   eval      d_ddag = uday
     c                   eval      w_dato = d_dato
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_valg
      *
     c                   eval      d_valg = p_valg
      *
     c                   endsr
      *
     oqcllesrc  eadd         xrecut
     o                       w_recc              96
     o                       w_tell               6
     o                       w_dato              12
**
             PGM       PARM(&TAPE)                                /*1*/
             DCL       VAR(&KODE) TYPE(*DEC)  LEN(2 0)            /*2*/
             DCL       VAR(&TAPE) TYPE(*DEC)  LEN(1 0)            /*3*/
             DCL       VAR(&DENS) TYPE(*CHAR) LEN(8)              /*4*/
             DCL       VAR(&BIBL) TYPE(*CHAR) LEN(10)             /*5*/
             CHGVAR    VAR(&DENS) VALUE('     ')                  /*6*/
             CHGVAR    VAR(&DENS) VALUE('      ')                 /*7*/
             CHGVAR    VAR(&DENS) VALUE('       ')                /*8*/
             CHGVAR    VAR(&DENS) VALUE('        ')               /*9*/
 /*                                                                10*/
 /* Kontroll av tape                                               11*/
 /*                                                                12*/
             CHKTAP    DEV(TAP01) VOL(ASBACK) ENDOPT(*REWIND)    /*13*/
 /*                                                                14*/
 /* Er det satt inn tape ?                                         15*/
 /*                                                                16*/
             MONMSG    MSGID(CPF6760) EXEC(DO)                   /*17*/
             CHGVAR    VAR(&KODE)     VALUE(11)                  /*18*/
             CALL      PGM(AN050R)    PARM(&KODE)                /*19*/
             DLYJOB    DLY(5)                                    /*20*/
             CHGVAR    VAR(&KODE)     VALUE(13)                  /*21*/
             CALL      PGM(AN050R)    PARM(&KODE)                /*22*/
             CHGVAR    VAR(&TAPE)     VALUE(1)                   /*23*/
             GOTO      CMDLBL(SLUTT)                             /*24*/
             ENDDO                                               /*25*/
 /*                                                                26*/
 /* Er det ASBACK som står i ?                                     27*/
 /*                                                                28*/
             MONMSG    MSGID(CPF6720) EXEC(DO)                   /*29*/
             CHGVAR    VAR(&KODE)     VALUE(12)                  /*30*/
             CALL      PGM(AN050R)    PARM(&KODE)                /*31*/
             DLYJOB    DLY(5)                                    /*32*/
             CHGVAR    VAR(&KODE)     VALUE(13)                  /*33*/
             CALL      PGM(AN050R)    PARM(&KODE)                /*34*/
             CHGVAR    VAR(&TAPE)     VALUE(1)                   /*35*/
             GOTO      CMDLBL(SLUTT)                             /*36*/
             ENDDO                                               /*37*/
 /*                                                                38*/
             INZTAP    DEV(TAP01) NEWVOL(ASBACK) CHECK(*NO) +
                                  DENSITY(&DENS)
             MONMSG    MSGID(CPF6708 CPF6762) EXEC(DO)           /*41*/
             CHGVAR    VAR(&KODE)  VALUE(14)                     /*42*/
             CALL      PGM(AN050R) PARM(&KODE)                   /*43*/
             DLYJOB    DLY(5)                                    /*44*/
             CHGVAR    VAR(&KODE)  VALUE(13)                     /*45*/
             CALL      PGM(AN050R) PARM(&KODE)                   /*46*/
             CHGVAR    VAR(&TAPE)  VALUE(1)                      /*47*/
             GOTO      CMDLBL(SLUTT)                             /*48*/
             ENDDO                                               /*49*/
 /*                                                                50*/
SLUTT:       ENDPGM                                              /*51*/
 /*                                                                52*/
