     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VV490R
      * Beskrivelse . . : Vare, sjekk om vare kan slettes
      *
      * NB! Dersom det legges inn sjekk mot flere tabeller, må også
      *     jobben for kopiering fra filgruppe til filgruppe endres
      *     m.h.p. overriding av tabeller mot databibliotek !!!
      *
      *     LR-flagg må være lik '1' for at *inLR ikke skal settes
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       031005 HKO  Nytt program
5.60  *       180908 bof  Må sjekke mot ordretype på hode
5.61  *       060109 bof  Må sjekke mot spes.varenr i butikk/lager/ofak
6.20  *       060614 BKV  Må sjekke mot vare i bestilling
6.nu  * 6.30  230614 bof  sjekket mtp. nummerutvidelse
8.01  *PRG2   200622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffodtlv    if   e           k disk    rename(fodtpfr:fodtlvr)
5.60 ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
5.61 fbstsl1    if   e           k disk    rename(bstspfr:bstsl1r)
6.20 flodtlv    if   e           k disk    rename(lodtpfr:lodtlvr)
6.20 flohel1    if   e           k disk    rename(lohepfr:lohel1r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(fdfirm)
     d p_vare          s                   like(fdvare)
     d p_inlr          s              1
     d p_ordr          s              1
     d p_lagr          s              1
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av variable i nøkler
      *
     d fodtlv_vare     s                   like(fdvare)
5.60 d fohel1_numm     s                   like(fonumm)
5.60 d fohel1_suff     s                   like(fosuff)
     d votyl1_otyp     s                   like(vaotyp)
6.20 d lohel1_numm     s                   like(lonumm)
6.20 d lohel1_suff     s                   like(losuff)
      *
      * Definering av variable
      *
     d w_firm          s                   like(fdfirm)
     d w_vare          s                   like(fdvare)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter firma og vare fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_vare = p_vare
      *
      * Initierer output-parametre
      *
     c                   eval      p_ordr = *off
     c                   eval      p_lagr = *off
      *
      * Test på at vare ikke finnes i ordre som annet en tilbud
      * Tilbud identifiseres som ordretype med behandlingskode lik 0
      *
     c     fodtlv_key    setll     fodtlv
     c     fodtlv_key    reade     fodtlv
     c                   dow       not %eof
5.60 c                   eval      fohel1_numm = fdnumm
5.60 c                   eval      fohel1_suff = fdsuff
5.60 c     fohel1_key    chain     fohel1
5.60  *
5.60 c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1
     c                   if        %found and
     c                             vaoakk <> 0
     c                   eval      p_ordr = *on
     c                   leave
     c                   endif
     c     fodtlv_key    reade     fodtlv
     c                   enddo
      *
      * Dersom det kjøres med 'oppdater lager' kalles program for å
      * sjekke om varen har lagerbeholding
      *
     c     fstsl1_key    chain     fstsl1
     c                   if        %found and
     c                             faalag = 1
      *
     c                   call      'LL765R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    p_inlr
     c                   parm                    p_lagr
      *
     c                   endif
5.61  *
5.61  * sjekker om øreavr.
5.61 c                   if         w_vare = faavnr
5.61 c                   eval      p_ordr = *on
5.61 c                   endif
5.61  *
5.61  * sjekker om varenr på butikk status-reg
5.61  *
5.61 c     bstsl1_key    chain     bstsl1
5.61 c                   if        %found(bstsl1)
5.61 c                   if         w_vare = baavar or
5.61 c                              w_vare = baagva or
5.61 c                              w_vare = baadva
5.61 c                   eval      p_ordr = *on
5.61 c                   endif
5.61 c                   endif
6.20  *
6.20  * sjekker om varenr i bestilling
6.20  *
6.20 c     lodtlv_key    setll     lodtlv
6.20 c     lodtlv_key    reade     lodtlv
6.20 c                   dow       not %eof
6.20 c                   eval      lohel1_numm = ldnumm
6.20 c                   eval      lohel1_suff = ldsuff
6.20 c     lohel1_key    chain     lohel1
6.20 c                   if        %found(lohel1)
6.20 c                   eval      p_ordr = *on
6.20 c                   endif
6.20 c     lodtlv_key    reade     lodtlv
6.20 c                   enddo
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   if        p_inlr <> *on
     c                   eval      *inlr = *on
     c                   endif
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_inlr
     c                   parm                    p_ordr
     c                   parm                    p_lagr
      *
      * Key for les på ordre-linje
      *
     c     fodtlv_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
5.60  *
5.60  * Key for les på ordre-hode
5.60  *
5.60 c     fohel1_key    klist
5.60 c                   kfld                    w_firm
5.60 c                   kfld                    fohel1_numm
5.60 c                   kfld                    fohel1_suff
      *
      * Key for oppslag på kode/status
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på ordretype
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
6.20  *
6.20  * Key for les på best.hode
6.20  *
6.20 c     lohel1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    lohel1_numm
6.20 c                   kfld                    lohel1_suff
6.20  *
6.20  * Key for les på best.-linje
6.20  *
6.20 c     lodtlv_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    w_vare
5.61  *
5.61  * Key for oppslag på kode/status for butikk
5.61  *
5.61 c     bstsl1_key    klist
5.61 c                   kfld                    w_firm
      *
     c                   endsr
