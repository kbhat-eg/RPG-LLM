     h decedit ('0,') datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LL852R
      *  Beskrivelse . . : Uttrekk innkjøp pr. leverandør til excel
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       110910 bof  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsihel1    if   e           k disk    rename(sihepfr:sihel1r)
     fsidtl1    if   e           k disk    rename(sidtpfr:sidtl1r)
     flwexpf    o  a e           k disk    rename(lexcpfr:lexcpfr)
      *
      *
      *  Local Data Area
      *
     d                uds
     d l_firm                944    946  0
      *
      *  Parameter-record
      *
     d                 ds
     d p_prec                        55
     d  p_ldor                        6  0 overlay(p_prec)
     d  p_fdat                        6  0 overlay(p_prec:7)
     d  p_tdat                        6  0 overlay(p_prec:13)
      *
      * Definering av variable i nøkler
      *
     d sidtl1_aarr     s                   like(slaarr)
     d sidtl1_binr     s                   like(slbinr)
     d sidtl1_numm     s                   like(slnumm)
     d sidtl1_suff     s                   like(slsuff)
      *
      * Definering av outputrecord
      *
     d d_orec          s           1024
      *
      *   Definering av øvrige variable
      *
     d w_parm          s             55
     d w_firm          s                   like(slfirm)
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_faar          s                   like(slaarr)
     d w_taar          s                   like(slaarr)
     d w_numm          s                   like(slnumm)
     d w_suff          s                   like(slsuff)
     d w_ldor          s                   like(shldor)
     d w_fdat          s                   like(shbdat)
     d w_tdat          s                   like(shbdat)
      *
     d ew112           c                   '         ,  -'
     d ew113           c                   '        ,   -'
      *
     d b_open_sok      s              1    inz(*off)
      *
      *  Konstanter
     d c_fsep          c                   X'05'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * danne heading
     c                   eval      d_orec = *blank
     c                   eval      d_orec = 'Leverandør' + c_fsep +
     c                                      %char(w_ldor)  + c_fsep
     c                   eval      exclin = d_orec
     c                   write     lexcpfr
     c                   eval      d_orec = *blank
     c                   eval      d_orec = 'Bilagsnr' + c_fsep +
     c                                      'Varenr' + c_fsep +
     c                                      'V.navn' + c_fsep +
     c                                      'Enhet' + c_fsep +
     c                                      'Antall' + c_fsep +
     c                                      'Kostpris' + c_fsep +
     c                                      'Innkjøpspris' + c_fsep +
     c                                      'Rabatt 1' + c_fsep +
     c                                      'Rabatt 2' + c_fsep
     c                   eval      exclin = d_orec
     c                   write     lexcpfr
      *
     c                   eval      b_open_sok = *on
      * pr leverandør
      *
     c/exec sql
     c+                  declare sok_v cursor for
     c+                  select shaarr,
     c+                         shbinr,
     c+                         shnumm,
     c+                         shsuff,
     c+                         shbdat,
     c+                         shldor
     c+                  from   sihepf
     c+                  where  shfirm = :w_firm and
     c+                         shaarr >= :w_faar and
     c+                         shaarr <= :w_taar and
     c+                         shldor = :w_ldor and
     c+                         shbdat >=:w_fdat and
     c+                         shbdat <=:w_tdat
     c+                  order by shaarr,shbinr
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_v
     c/end-exec
     c/exec sql
     c+                  open sok_v
     c/end-exec
      *
     c                   eval      *in15 = *off
      *
     c                   dow       *in15 = *off
      * leverandør
     c/exec sql
     c+                  fetch next
     c+                  from  sok_v
     c+                  into  :shaarr,
     c+                        :shbinr,
     c+                        :shnumm,
     c+                        :shsuff,
     c+                        :shbdat,
     c+                        :shldor
     c/end-exec
      *
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   if        *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      sidtl1_aarr = shaarr
     c                   eval      sidtl1_binr = shbinr
     c                   eval      sidtl1_numm = shnumm
     c                   eval      sidtl1_suff = shsuff
     c     sidtl1_key    setll     sidtl1
     c     sidtl1_key    reade     sidtl1
     c                   dow       not %eof(sidtl1)
     c                   if        slvare <> *blank
     c                   exsr      skriv_vlin
     c                   endif
     c     sidtl1_key    reade     sidtl1
     c                   enddo
      *
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for skriving av output-record
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_vlin    begsr
      *
     c                   eval      d_orec = %char(shbinr) + c_fsep +
     c                                      %trim(slvare) + c_fsep +
     c                                      %trim(sltek1) + c_fsep +
     c                                      %trim(slenh1) + c_fsep +
     c                                      %editw(slanta : ew113) + c_fsep +
     c                                      %editw(slkpny : ew112) + c_fsep +
     c                                      %editw(slipny : ew112) + c_fsep +
     c                                      %editw(slrab1 : ew112) + c_fsep +
     c                                      %editw(slrab2 : ew112) + c_fsep
     c                   eval      exclin = d_orec
     c                   write     lexcpfr
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av innkjøp det.
      *
     c     sidtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sidtl1_aarr
     c                   kfld                    sidtl1_binr
     c                   kfld                    sidtl1_numm
     c                   kfld                    sidtl1_suff
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    w_parm
     c                   eval      p_prec = w_parm
      *
      *  Legge inn firmanummer i nøklene
      *
     c                   eval      w_firm = l_firm
     c                   time                    w_date
     c                   time                    w_time
     c     *dmy          move      p_fdat        w_fdat
     c     *dmy          move      p_tdat        w_tdat
     c                   extrct    w_fdat:*y     w_faar
     c                   extrct    w_tdat:*y     w_taar
     c                   eval      w_ldor = p_ldor
      *
     c                   endsr
