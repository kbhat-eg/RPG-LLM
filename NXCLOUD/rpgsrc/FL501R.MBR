     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FL501R
      * Beskrivelse . . : Kundeprosjekt, spørring på kunde/dato
      *
      *                   Firma, kunde og dato er input-parametre
      *                   Rutine for spørring på ikke utgåtte og utgåtte
      *                   prosjekt for en kunde
      *                   Egen funksjonstast for spørring på utgåtte
      *                   prosjekt
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       120499 HKO  Nytt program
5.31  * R5.31 221004 AMD  Lagt inn kundens eksterne prosjektnummer
6.3q  *       260515 bof  Datoformat sql
7.xx  * 7.00  230119 bkv  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  230119 bkv  Vasker vekk probl Newlook tegn
7.02  * 6.30  270917 sst  Henter ev underprosjekt og aktivitet
7.03  * K000  210121 bsa  Feil i parameterlista til RP601.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffkpspf    if   e           k disk
     ffkprl4    if   e           k disk    rename(fkprpfr:fkprl4r)
     ffkprlr    if   e           k disk    rename(fkprpfr:fkprlrr)
     fapospf    if   e           k disk    rename(apospfr:apospfr)
7.01 frp1pl1    if   e           k disk    rename(rp1ppfr:rp1pl1r)
7.01 frp2ul1    if   e           k disk    rename(rp2upfr:rp2ul1r)
      *
     ffl501d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(flfirm)
     d p_kund          s                   like(flkund)
     d p_dato          s                   like(fltdat)
     d p_kpro          s                   like(flkpro)
     d p_navn          s                   like(flnavn)
     d p_adr1          s                   like(fladr1)
     d p_adr2          s                   like(fladr2)
     d p_kprs          s                   like(flkprs)
     d p_tlfa          s                   like(fltlfa)
     d p_tlfp          s                   like(fltlfp)
     d p_tlfm          s                   like(fltlfm)
     d p_mail          s                   like(flmail)
     d p_orka          s                   like(florka)
     d p_okun          s                   like(flokun)
     d p_okpr          s                   like(flokpr)
     d p_opda          s                   like(flopda)
     d p_proj          s                   like(flproj)
     d p_kjor          s                   like(flkjor)
     d p_kjnr          s                   like(flkjnr)
     d p_sted          s             30
7.01 d p_upro          s                   like(rp2upr)
7.01 d p_akti          s              6
7.01 d p_txt1          s                   like(rp2txt)
7.01 d p_txt2          s                   like(rp2tx2)
7.01 d p_uprs          s                   like(rp2upr)
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d fkprlr_kpro     s                   like(flkpro)
     d apospf_ponr     s                   like(apponr)
7.01 d rp1pl1_pros     s                   like(rp1pro)
7.01 d rp2ul1_pros     s                   like(rp2pro)
7.01 d rp2ul1_upro     s                   like(rp2upr)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_valg_ok       s              1    inz(*off)
     d b_open_sok      s              1    inz(*off)
      *
     d w_ste1          s             35
     d w_ste2          s             35
     d w_ste3          s             35
     d w_ste4          s             35
     d w_ste5          s             35
     d w_posi          s              3  0
     d s_stek          s             35
      *
     d w_firm          s                   like(flfirm)
     d w_kund          s                   like(flkund)
7.01 d w_len           s              3  0
7.01 d w_teller        s              3  0
7.03 d w_fdat          s              6  0
7.03 d w_tdat          s              6  0
7.03 d w_kunf          s              6  0
7.03 d w_knav          s             30
7.03 d w_kpro          s              6  0
7.03 d w_selg          s              4  0
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(8)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
6.3q C/End-Exec
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * F9=Gjenta søk
      *
     c                   if        *inki = *on
     c                   eval      *in22 = *on
     c                   eval      b2stek = s_stek
     c                   goto      b2tagb
     c                   endif
      *
      * F23=Vis utgåtte/ikke aktive kundeprosjekt
      *
     c                   if        *inkx = *on
     c                   if        *in80 = *off
     c                   eval      *in80 = *on
     c                   else
     c                   eval      *in80 = *off
     c                   endif
     c                   exsr      forny
     c                   goto      b2taga
     c                   endif
      *
      * Søking
      *
     c                   if        b2stek <> *blank
     c                   exsr      sØk
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   if        b_valg_ok = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   if        w_seqe = 'S'
     c                   eval      b_open_sok = *on
     c                   else
     c     fkprl4_key    setll     fkprl4
     c                   endif
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for søking i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sØk           begsr
      *
     c                   eval      w_seqe = 'S'
     c                   eval      b_open_sok = *on
      *
     c                   eval      s_stek = b2stek
      *
      * Utleder søketekster ut av søketekst
      *
     c                   call      'AS700R'
     c                   parm                    b2stek
     c                   parm                    w_ste1
     c                   parm                    w_ste2
     c                   parm                    w_ste3
     c                   parm                    w_ste4
     c                   parm                    w_ste5
      *
      * Blanker subfile, og fyller opp i.h.t. søk
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker søkefelt
      *
     c                   eval      b2stek = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Velg
      *
     c                   if        b1valg = 1
      *
     c                   eval      fkprlr_kpro = b1kpro
     c     fkprlr_key    chain     fkprlr                             91
     c                   if        *in91 = *off
      *
     c                   eval      p_kpro = b1kpro
     c                   eval      p_navn = flnavn
     c                   eval      p_adr1 = fladr1
     c                   eval      p_adr2 = fladr2
     c                   eval      p_kprs = flkprs
     c                   eval      p_tlfa = fltlfa
     c                   eval      p_tlfp = fltlfp
     c                   eval      p_tlfm = fltlfm
     c                   eval      p_mail = flmail
     c                   eval      p_orka = florka
     c                   eval      p_okun = flokun
     c                   eval      p_okpr = flokpr
     c                   eval      p_opda = flopda
     c                   eval      p_proj = flproj
     c                   eval      p_kjor = flkjor
     c                   eval      p_kjnr = flkjnr
      *
      * Legge sammen postnummer og
      * poststed til et felt
      *
     c                   eval      p_sted =  *blank
     c                   if        flponr <> *zero
     c                   eval      apospf_ponr = flponr
     c     apospf_key    chain     apospfr                            92
     c                   if        *in92 = *off
     c                   movel     flponr        p_sted
     c                   move      apsted        p_sted
     c                   endif
     c                   endif
7.01  *
7.01  *  Henter ev prosjekt og spør ev om underprosjekt
7.01  *                  NBNB      NBNBNBNB <<<<<<<<<<<<<<<<<<<<<<<<<<<<<
7.01 c                   goto      noprosj
7.01 c                   if        flproj <> *blank
7.01 c                   eval      rp1pl1_pros = flproj
7.01 c     rp1pl1_key    chain     rp1pl1
7.01 c                   if        not %found(rp1pl1)
7.01 c                   goto      noprosj
7.01 c                   else
7.01 c                   if        rp1rup = 'J'
9.0S c                   if        p_upro = *blank
7.01 c                   eval      p_upro = *blank
7.01 c                   eval      p_uprs = *blank
7.03 c                   eval      w_kunf = 0
7.03 c                   eval      w_fdat = 0
7.03 c                   eval      w_tdat = 0
7.03 c                   eval      w_selg = 0
7.03 c                   eval      w_kpro = 0
7.01 c                   call      'RP601R'
7.01 c                   parm                    w_firm
7.01 c                   parm                    flproj
7.01 c                   parm                    p_upro
7.03 c                   parm                    p_uprs
7.01 c                   parm                    p_txt1
7.01 c                   parm                    p_txt2
7.01 c                   parm                    p_kund
7.03 c                   parm                    w_fdat
7.03 c                   parm                    w_tdat
7.03 c                   parm                    w_knav
7.03 c                   parm                    w_selg
7.03 c                   parm                    w_kunf
7.03 c                   parm                    w_kpro
9.0s c                   endif
9.0s c                   endif
7.01 c                   if        p_upro <> *blank
7.01 c                   eval      rp2ul1_pros = flproj
7.01 c                   eval      rp2ul1_upro = p_upro
7.01 c     rp2ul1_key    chain     rp2ul1
7.01 c                   endif
7.01 c                   if        (%found(rp2ul1)
7.01 c                             and rp2rak = 'J'
7.01 c                             and rp1rup = 'J')
7.01 c                             or (rp1rup <> 'J'
7.01 c                             and rp1rak = 'J')
7.01 c                   call      'RP605R'
7.01 c                   parm                    w_firm
7.01 c                   parm                    flproj
7.01 c                   parm                    p_upro
7.01 c                   parm                    p_txt1
7.01 c                   parm                    p_txt2
7.01 c                   parm                    p_kund
7.01 c                   parm                    p_akti
7.01 c                   endif
7.01 c                   endif
7.01 c                   endif
7.01  *
7.01 c     noprosj       tag
      *
     c                   eval      b_valg_ok = *on
     c                   leave
      *
     c                   endif
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
      * Initierer SQL for søk
      *
     c                   if        w_seqe = 'S' and
     c                             b_open_sok = *on
     c/exec sql
     c+                  declare sok cursor for
     c+                  select flsfir,
     c+                         flskun,
     c+                         flstek,
     c+                         flskpr
     c+                  from   fkpspf
     c+                  where  flsfir = :w_firm and
     c+                         flskun = :w_kund and
     c+                         flstek like :w_ste1
     c+                  order by flsfir, flskun, flstek
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok
     c/end-exec
     c/exec sql
     c+                  open sok
     c/end-exec
     c                   endif
      *
      * Fyller opp subfile
      *
     c                   dou       w_srrn = w_spge
      *
     c                   if        w_seqe = 'S'
     c/exec sql
     c+                  fetch next
     c+                  from  sok
     c+                  into  :flsfir,
     c+                        :flskun,
     c+                        :flstek,
     c+                        :flskpr
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   else
     c     fkprl4_key    reade     fkprl4                                 15
     c                   endif
      *
     c                   if        *in15
     c                   leave
     c                   endif
      *
     c                   if        w_seqe = 'S'
      *
     c                   if        w_ste2 <> *blank
      *
     c                   eval      w_posi = %scan(%trim(w_ste2):%trim(flstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
      *
     c                   if        w_ste3 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste3):%trim(flstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste4 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste4):%trim(flstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste5 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste5):%trim(flstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c                   eval      fkprlr_kpro = flskpr
     c     fkprlr_key    chain     fkprlr                             91
     c                   if        *in91 = *on
     c                   iter
     c                   endif
      *
     c                   endif
      *
     c                   if        *in80 = *off
     c                   if        flfdat <> *loval and
     c                             flfdat > p_dato
     c                   iter
     c                   endif
     c                   if        fltdat <> *loval and
     c                             fltdat < p_dato
     c                   iter
     c                   endif
     c                   endif
      *
      * Skriver til subfile
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1navn = flnavn
     c                   eval      b1adr1 = fladr1
     c                   eval      b1kpro = flkpro
5.31 c                   eval      b1epro = flepro
5.31  *                  eval      b1fdat = 0
     c                   eval      b1tdat = 0
      *
5.31  *                  if        flfdat <> *loval
5.31  *    *dmy          move      flfdat        b1fdat
5.31  *                  endif
      *
     c                   if        fltdat <> *loval
     c     *dmy          move      fltdat        b1tdat
     c                   endif
      *
7.01 c                   exsr      newlo_vask
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   eval      b_open_sok = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c     end_bck       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Newlook vask av data
7.01  *  Fjerner tegn som gjør at Newlook feiltolker
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01   begsr newlo_vask;
7.01     for w_teller = 1 to 5 ;
7.01       w_len = %len(%trim(b1navn));
7.01       if (w_len > 0);
7.01       b1navn = %ScanRpl('.':'':b1navn:w_len); // newlook tåler ikke . på slutten
7.01       endif;
7.01       w_len = %len(%trim(b1navn));
7.01       if (w_len > 0);
7.01       b1navn = %ScanRpl(':':'':b1navn:w_len); // newlook tåler ikke : på slutten
7.01       endif;
7.01       w_len = %len(%trim(b1adr1));
7.01       if (w_len > 0);
7.01       b1adr1 = %ScanRpl('.':'':b1adr1:w_len); // newlook tåler ikke . på slutten
7.01       endif;
7.01       w_len = %len(%trim(b1adr1));
7.01       if (w_len > 0);
7.01       b1adr1 = %ScanRpl(':':'':b1adr1:w_len); // newlook tåler ikke : på slutten
7.01       endif;
7.01     endfor ;
7.01   endsr;
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kund
     c                   parm                    p_dato
     c                   parm                    p_kpro
     c                   parm                    p_navn
     c                   parm                    p_adr1
     c                   parm                    p_adr2
     c                   parm                    p_sted
     c                   parm                    p_kprs
     c                   parm                    p_tlfa
     c                   parm                    p_tlfp
     c                   parm                    p_tlfm
     c                   parm                    p_mail
     c                   parm                    p_orka
     c                   parm                    p_okun
     c                   parm                    p_okpr
     c                   parm                    p_opda
     c                   parm                    p_proj
     c                   parm                    p_kjor
     c                   parm                    p_kjnr
      *
      * Key for posisjonering på navn
      *
     c     fkprl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_kund
      *
      * Key for oppslag på kunde/kundeprosjekt
      *
     c     fkprlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_kund
     c                   kfld                    fkprlr_kpro
      *
      * Key for oppslag på post-sted
      *
     c     apospf_key    klist
     c                   kfld                    apospf_ponr
7.01  * Key for oppslag på prosjekt
7.01  *
7.01 c     rp1pl1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    rp1pl1_pros
7.01  *
7.01  * Key for oppslag på under prosjekt
7.01  *
7.01 c     rp2ul1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    rp2ul1_pros
7.01 c                   kfld                    rp2ul1_upro
      *
      *
      * Henter parameter, og initierer skjermbildet
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_kund = p_kund
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     fkprl4_key    setll     fkprl4
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
