     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VT510R
      * Beskrivelse . . : Tilbudspriser, vise for vare
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       111199 HKO  Nytt program
5.70  * PRGR  301108 bof  Utvidet med prisgruppe
6.20  * 6.20  110511 NHO  Endring key på filer
      *
8.01  * 8.xx  101122 bsa  Skrevet om til å vises i cloud
8.02  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvtillr    if   e           k disk    rename(vtilpfr:vtillrr)
     fvtill1    if   e           k disk    rename(vtilpfr:vtill1r)
     fvtill2    if   e           k disk    rename(vtilpfr:vtill2r)
     fvtill3    if   e           k disk    rename(vtilpfr:vtill3r)
     fvtill4    if   e           k disk    rename(vtilpfr:vtill4r)
     fvtill5    if   e           k disk    rename(vtilpfr:vtill5r)
     fvtill6    if   e           k disk    rename(vtilpfr:vtill6r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
     fra30l1    if   e           k disk    rename(ra30pfr:ra30l1r)
      *
     fvt510d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vtfirm)
     d p_vare          s                   like(vtvare)
      *
      * Definering av Local data area
      *
     d                uds
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variable i nøkler
      *
6.20 d vtillr_ogrp     s                   like(vtogrp)
6.20 d vtillr_hgrp     s                   like(vthgrp)
6.20 d vtillr_ugrp     s                   like(vtugrp)
6.20 d vtillr_ldor     s                   like(vtldor)
     d vtillr_vare     s                   like(vtvare)
     d vtillr_prgr     s                   like(vtprgr)
     d vtillr_lety     s                   like(vtlety)
     d vtillr_fdat     s                   like(vtfdat)
     d vtillr_ftim     s                   like(vtftim)
     d vtillr_tdat     s                   like(vttdat)
     d vtillr_ttim     s                   like(vtttim)
6.20 d vtill1_ogrp     s                   like(vtogrp)
6.20 d vtill1_hgrp     s                   like(vthgrp)
6.20 d vtill1_ugrp     s                   like(vtugrp)
6.20 d vtill1_ldor     s                   like(vtldor)
     d vtill1_vare     s                   like(vtvare)
     d vtill2_lety     s                   like(vtlety)
     d vtill3_fdat     s                   like(vtfdat)
     d vtill4_tdat     s                   like(vttdat)
     d vtill5_kamp     s                   like(vtkamp)
     d vtill6_prgr     s                   like(vtprgr)
     d vvarl1_vare     s                   like(vvvare)
     d vltpl1_lety     s                   like(vylety)
     d ra30l1_dkod     s                   like(redkod)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
      *
     d w_ftim          s              6  0
     d w_ttim          s              6  0
      *
     d w_firm          s                   like(vtfirm)
      *
      * Definering av konstanter
      *
8.01 d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for vise
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2lety <> 0 or
     c                             b2prgr <> *blank or
     c                             b2fdat <> 0 or
     c                             b2tdat <> 0 or
     c                             b2kamp <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   eval      vtill2_lety = b1lety
     c     vtill2_key    setll     vtill2
     c                   when      w_seqe = 'F'
     c     *dmy          move      b1fdat        vtill3_fdat
     c     vtill3_key    setll     vtill3
     c                   when      w_seqe = 'T'
     c     *dmy          move      b1tdat        vtill4_tdat
     c     vtill4_key    setll     vtill4
     c                   when      w_seqe = 'K'
     c                   eval      vtill5_kamp = b1kamp
     c     vtill5_key    setll     vtill5
     c                   when      w_seqe = 'P'
     c                   eval      vtill6_prgr = b1prgr
     c     vtill6_key    setll     vtill6
     c                   other
     c     vtill1_key    setll     vtill1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på prisgruppe
      *
     c                   if        b2prgr <> *blank
     c                   eval      w_seqe = 'P'
     c                   eval      vtill6_prgr = b2prgr
     c     vtill6_key    setll     vtill6
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på leveringstype
      *
     c                   if        b2lety <> 0
     c                   eval      w_seqe = 'L'
     c                   eval      vtill2_lety = b2lety
     c     vtill2_key    setll     vtill2
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på fra-dato
      *
     c                   if        b2fdat <> 0
     c                   eval      w_seqe = 'F'
     c     *dmy          test(d)                 b2fdat                 80
     c                   if        *in80 = *on
     c                   eval      vtill3_fdat = *hival
     c                   else
     c     *dmy          move      b2fdat        vtill3_fdat
     c                   endif
     c     vtill3_key    setll     vtill3
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på til-dato
      *
     c                   if        b2tdat <> 0
     c                   eval      w_seqe = 'T'
     c     *dmy          test(d)                 b2tdat                 80
     c                   if        *in80 = *on
     c                   eval      vtill4_tdat = *hival
     c                   else
     c     *dmy          move      b2tdat        vtill4_tdat
     c                   endif
     c     vtill4_key    setll     vtill4
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på kampanje
      *
     c                   if        b2kamp <> *blank
     c                   eval      w_seqe = 'K'
     c                   eval      vtill5_kamp = b2kamp
     c     vtill5_key    setll     vtill5
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2lety = 0
     c                   eval      b2prgr = *blank
     c                   eval      b2fdat = 0
     c                   eval      b2tdat = 0
     c                   eval      b2kamp = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1lety = b1lety
     c                   eval      c1prgr = b1prgr
     c                   eval      c1fdat = b1fdat
     c                   eval      c1ftim = b1ftim
     c                   eval      c1tdat = b1tdat
     c                   eval      c1ttim = b1ttim
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * C2BLD Behandle bilde for ny - endring - vise
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      vtillr_vare = p_vare
     c                   eval      vtillr_lety = c1lety
     c                   eval      vtillr_prgr = c1prgr
     c     *dmy          move      c1fdat        vtillr_fdat
     c     *dmy          move      c1tdat        vtillr_tdat
     c                   eval      w_ftim = c1ftim * 100
     c     *hms          move      w_ftim        vtillr_ftim
     c                   eval      w_ttim = c1ttim * 100
     c     *hms          move      w_ttim        vtillr_ttim
     c     vtillr_key    chain     vtillr                             90
      *
     c                   if        *in90 = *on
     c                   goto      end_c2bld
     c                   endif
      *
     c                   eval      *in80  = *off
     c                   eval      c2kamp = vtkamp
     c                   eval      c2enh1 = vtenh1
     c                   eval      c2tip1 = vttip1
     c                   eval      c2kop1 = vtkop1
     c                   eval      c2enh2 = vtenh2
     c                   eval      c2tip2 = vttip2
     c                   eval      c2kop2 = vtkop2
     c                   eval      c2enh3 = vtenh3
     c                   eval      c2tip3 = vttip3
     c                   eval      c2kop3 = vtkop3
     c                   eval      c2eusr = vteusr
      *
     c     *dmy          move      vtedat        c2edat
      *
      * Henter beskrivelse av nivå
      *
     c                   exsr      hent_info
      *
      * Behandler bildet
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl
     c                   goto      end_c2bld
     c                   endif
      *
     c     end_c2bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Fyll opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   read      vtill2
     c                   when      w_seqe = 'F'
     c                   read      vtill3
     c                   when      w_seqe = 'T'
     c                   read      vtill4
     c                   when      w_seqe = 'K'
     c                   read      vtill5
     c                   when      w_seqe = 'P'
     c                   read      vtill6
     c                   other
     c     vtill1_key    reade     vtill1
     c                   endsl
      *
     c                   if        %eof or
     c                             vtfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        vtvare <> p_vare
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   eval      vtill2_lety = vtlety
     c                   when      w_seqe = 'F'
     c                   eval      vtill3_fdat = vtfdat
     c                   when      w_seqe = 'T'
     c                   eval      vtill4_tdat = vttdat
     c                   when      w_seqe = 'K'
     c                   eval      vtill5_kamp = vtkamp
     c                   when      w_seqe = 'P'
     c                   eval      vtill6_prgr = vtprgr
     c                   endsl
      *
     c                   eval      b1lety = vtlety
     c                   eval      b1prgr = vtprgr
     c                   eval      b1kamp = vtkamp
     c                   eval      b1enh1 = vtenh1
     c                   eval      b1tip1 = vttip1
     c                   eval      b1kop1 = vtkop1
     c                   eval      b1enh2 = vtenh2
     c                   eval      b1enh3 = vtenh3
      *
     c     *dmy          move      vtfdat        b1fdat
     c     *dmy          move      vttdat        b1tdat
      *
     c     *hms          move      vtftim        w_ftim
     c                   movel     w_ftim        b1ftim
     c     *hms          move      vtttim        w_ttim
     c                   movel     w_ttim        b1ttim
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'L'
     c     vtill2_key    setgt     vtill2
     c                   readp     vtill2
     c                   when      w_seqe = 'F'
     c     vtill3_key    setgt     vtill3
     c                   readp     vtill3
     c                   when      w_seqe = 'T'
     c     vtill4_key    setgt     vtill4
     c                   readp     vtill4
     c                   when      w_seqe = 'K'
     c     vtill5_key    setgt     vtill5
     c                   readp     vtill5
     c                   when      w_seqe = 'P'
     c     vtill6_key    setgt     vtill6
     c                   readp     vtill6
     c                   other
     c     vtill1_key    setgt     vtill1
     c                   readp     vtill1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   readp     vtill2
     c                   when      w_seqe = 'F'
     c                   readp     vtill3
     c                   when      w_seqe = 'T'
     c                   readp     vtill4
     c                   when      w_seqe = 'K'
     c                   readp     vtill5
     c                   when      w_seqe = 'P'
     c                   readp     vtill6
     c                   other
     c                   readp     vtill1
     c                   endsl
      *
     c                   if        %eof or
     c                             vtfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   if        vtvare <> p_vare
     c                   iter
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   eval      vtill2_lety = vtlety
     c                   when      w_seqe = 'F'
     c                   eval      vtill3_fdat = vtfdat
     c                   when      w_seqe = 'T'
     c                   eval      vtill4_tdat = vttdat
     c                   when      w_seqe = 'K'
     c                   eval      vtill5_kamp = vtkamp
     c                   when      w_seqe = 'P'
     c                   eval      vtill6_prgr = vtprgr
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'L'
     c     vtill2_key    setll     vtill2
     c                   when      w_seqe = 'F'
     c     vtill3_key    setll     vtill3
     c                   when      w_seqe = 'T'
     c     vtill4_key    setll     vtill4
     c                   when      w_seqe = 'K'
     c     vtill5_key    setll     vtill5
     c                   when      w_seqe = 'P'
     c     vtill6_key    setll     vtill6
     c                   other
     c     vtill1_key    setll     vtill1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter beskrivelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_info     begsr
      *
     c                   eval      c1vare = p_vare
     c                   eval      c1tek1 = b2tek1
     c                   eval      c1ltxt = *blank
     c                   eval      c1ptxt = *blank
      *
      * Prisgruppe
      *
     c                   if        c1prgr <> *blank
     c                   eval      ra30l1_dkod = c1prgr
     c     ra30l1_key    chain     ra30l1                             91
     c                   if        *in91 = *off
     c                   eval      c1ptxt = redtxt
     c                   endif
     c                   endif
      *
      *
      * Leveringstype
      *
     c                   if        c1lety <> 0
     c                   eval      vltpl1_lety = c1lety
     c     vltpl1_key    chain     vltpl1                             91
     c                   if        *in91 = *off
     c                   eval      c1ltxt = vybesk
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
      *
      * Key for oppslag på tilbudspriser
      *
     c     vtillr_key    klist
     c                   kfld                    w_firm
6.20 c                   kfld                    vtillr_ogrp
6.20 c                   kfld                    vtillr_hgrp
6.20 c                   kfld                    vtillr_ugrp
6.20 c                   kfld                    vtillr_ldor
     c                   kfld                    vtillr_vare
     c                   kfld                    vtillr_prgr
     c                   kfld                    vtillr_lety
     c                   kfld                    vtillr_fdat
     c                   kfld                    vtillr_ftim
     c                   kfld                    vtillr_tdat
     c                   kfld                    vtillr_ttim
      *
      * Key for posisjonering på vare
      *
     c     vtill1_key    klist
     c                   kfld                    w_firm
6.20 c                   kfld                    vtill1_ogrp
6.20 c                   kfld                    vtill1_hgrp
6.20 c                   kfld                    vtill1_ugrp
6.20 c                   kfld                    vtill1_ldor
     c                   kfld                    vtill1_vare
      *
      * Key for posisjonering på leveringstype
      *
     c     vtill2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vtill2_lety
      *
      * Key for posisjonering på fra-dato
      *
     c     vtill3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vtill3_fdat
      *
      * Key for posisjonering på til-dato
      *
     c     vtill4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vtill4_tdat
      *
      * Key for posisjonering på kampanje
      *
     c     vtill5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vtill5_kamp
      *
      * Key for posisjonering på prisgruppe
      *
     c     vtill6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vtill6_prgr
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
      *
      * Key for oppslag på prisgrupe
      *
     c     ra30l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra30l1_dkod
      *
      *
      * Henter firma fra parametre
      *
     c                   eval      w_firm = p_firm
      *
      * Initierer skjermbilde
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1                             90
     c                   if        *in90 = *off
     c                   eval      b2vare = vvvare
     c                   eval      b2tek1 = vvtek1
     c                   endif
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      vtill1_vare = p_vare
     c     vtill1_key    setll     vtill1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
