     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VP612R
      * Beskrivelse . . : Vare-pris, utskrift av prisbok kunder
      *
      *                   NB! Programmet må kjøres i batch
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       270599 HKO  Nytt program
      *       280200 HKO  Utvidet lengde på overskrift
      *       230101 HKO  Leser varesortiment via utplukksfil
      *                   Dersom leverandør er valgt utelatt ved varesort-
      *                   iment tas heller ikke leverandør med i sorteringen
      *       161203 HKO  Endret parameter ved kall til VP900R
      *       120804 HKO  Endret parameter ved kall til VP900R
      *       041104 HKO  Endret parameter ved kall til VP900R
      *       070205 HKO  Utelater utgåtte varer
      *       020805 HKO  Endret les mot vare-enhet for å finne prisbok-
      *                   enheter og salgsenheter
      *       270606 HKO  Henter pris på bakgrunn av valgt leveringstype
5.70  * PRGR  311008 bof  Utvidet med prisgruppe
      * 6.10  230609 BSA  Oppdatert med sporingsinformasjon
6.20  * 6.20  021110 bof  Utvidet med utg.dato pr. lager
6.21  * 6.20  190511 bø   Utvidet vl710r med div. info
6.nu  *       160614 bof  Endret ihht. nummerutvidelse
6.30  * 6.30  170918 bkv  Utvidet med trelast sortiment
7.01  *k00  030620 BOF NXS-1195  kompilert pga tab.utvidelse
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fapospf    if   e           k disk    rename(apospfr:apospfr)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fvvarl9    if   e           k disk    rename(vvarpfr:vvarl9r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvvenl3    if   e           k disk    rename(vvenpfr:vvenl3r)
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
     fvpkol1    if   e           k disk    rename(vpkopfr:vpkol1r)
     ffkdilu    uf   e           k disk    rename(fkdipfr:fkdilur)
     fvvawpf    if   e           k disk
6.30 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
     fvp612p    o    e             printer infds(d_infds)
      *
      * Array
      *
     d a_index         s            100    dim(5000) ctdata perrcd(1)            Array søketekst
      *
      * Definering av Local data area
      *
     d                uds
     d l_ruti                800    809
     d l_alin                856    858  0
     d l_wsid                901    910
     d l_user                911    920
5.70 d l_lage               1001   1002  0
      *
      * Definering av datastrukturer
      *
      * Informasjon fra printer-file
      *
     d d_infds         ds
     d  d_pagnbr             369    372B 0
      *
      * Innhold i index-array
      *
     d                 ds
     d d_index                      100
     d  d_text                       96    overlay(d_index)
     d  d_side                        4  0 overlay(d_index:97)
      *
      * Datastruktur - parametre inn til VP900R
      *
     d                 ds
     d hirec                        120
     d  hifirm                        3  0 overlay(hirec)
     d  hirkat                        3  0 overlay(hirec:4)
     d  hikund                        6  0 overlay(hirec:7)
     d  hikpro                        6  0 overlay(hirec:13)
     d  hivare                       15    overlay(hirec:19)
     d  hilety                        1  0 overlay(hirec:34)
     d  hienhe                        3    overlay(hirec:35)
     d  hidato                         d   overlay(hirec:38) datfmt(*iso)
     d  hitime                         t   overlay(hirec:48) timfmt(*iso)
6.nu d  hinumm                        8  0 overlay(hirec:56)
6.nu d  hikode                        1    overlay(hirec:64)
6.nu d  hidekn                        5  2 overlay(hirec:65)
6.nu d  hiavgf                        1  0 overlay(hirec:70)
6.nu d  hipriv                        1  0 overlay(hirec:71)
6.nu d  hitilb                        1    overlay(hirec:72)
6.nu d  hiskaf                        1    overlay(hirec:73)
6.nu d  hildor                        6  0 overlay(hirec:74)
6.nu d  hisapr                        9  2 overlay(hirec:80)
6.nu d  hikopr                        9  2 overlay(hirec:89)
8.01 d  hiprgr                        2    overlay(hirec:98)
     d  hiinlr                        1    overlay(hirec:120)
      *
      * Datastruktur - parametre ut fra VP900R
      *
     d                 ds
     d d_orec                        80
     d  d_kpri                        1    overlay(d_orec:2)
     d  d_oppr                        9  2 overlay(d_orec:3)
     d  d_sapr                        9  2 overlay(d_orec:12)
     d  d_rab1                        5  2 overlay(d_orec:30)
     d  d_rab2                        5  2 overlay(d_orec:35)
8.01 d  hoprgr                        2    overlay(d_orec:71)
      *
      * Definering av parametre
      *
     d p_vsor          s             10
     d p_sval          s              1  0
     d p_tval          s              1  0
     d p_2val          s              1  0
     d p_rval          s              1  0
     d p_lval          s              1  0
     d p_lety          s              1  0
     d p_dato          s               d   datfmt(*iso)
5.70 d p_lage          s              2  0
      *
     d p_firm          s                   like(vvfirm)
     d p_kund          s                   like(rkkund)
     d p_kpro          s                   like(flkpro)
     d p_ogrp          s                   like(vvogrp)
     d p_hgrp          s                   like(vvhgrp)
     d p_ugrp          s                   like(vvugrp)
     d p_ldor          s                   like(vvldor)
     d p_btis          s                   like(fhbtis)
      *
      * Definering av variabler i nøkler
      *
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d apospf_ponr     s                   like(apponr)
     d vvarl9_ogrp     s                   like(vvogrp)
     d vvarl9_hgrp     s                   like(vvhgrp)
     d vvarl9_ugrp     s                   like(vvugrp)
     d vvarl9_ldor     s                   like(vvldor)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d rlevl1_levr     s                   like(rllevr)
     d vvenl3_vare     s                   like(vevare)
     d vvenl3_pben     s                   like(vepben)
     d vvenl2_vare     s                   like(vevare)
     d vvenl2_saen     s                   like(vesaen)
     d vpkol1_pkod     s                   like(vapkod)
     d fkdilu_kund     s                   like(fhkund)
     d fkdilu_kpro     s                   like(fhkpro)
      *
      * Definering av variable
      *
     d b_frst          s              1    inz(*off)
     d b_svgr          s              1
     d b_sldo          s              1
     d b_stek          s              1
6.20 d b_inlr          s              1    inz(*off)
      *
6.20 d w_ddat          s               d   datfmt(*iso)
6.20 d w_udat          s               d   datfmt(*iso)
6.21 d w_ldor          s                   like(vvldor)
6.30 d w_lv_nobb       s                   like(vvlnob)
6.30 d w_lv_modn       s                   like(vvlmod)
6.30 d w_lv_lldo       s                   like(vvlnle)
6.30 d w_lv_llva       s                   like(vvllva)
     d w_timestamp     s             12  0
     d w_ogrp_alf      s              2
     d w_hgrp_alf      s              2
     d w_ugrp_alf      s              3
     d w_ldor_alf      s              6
     d w_i1            s              4  0
     d w_i2            s              4  0
     d x               s              1  0
      *
     d w_firm          s                   like(vvfirm)
     d w_enh1          s                   like(veenhe)
     d w_enh2          s                   like(veenhe)
     d w_enh3          s                   like(veenhe)
     d w_otxt          s                   like(vgotxt)
     d w_htxt          s                   like(vghtxt)
     d w_utxt          s                   like(vgutxt)
     d w_lnav          s                   like(rlnavn)
     d w_ogrp_gml      s                   like(vvogrp)
     d w_hgrp_gml      s                   like(vvhgrp)
     d w_ugrp_gml      s                   like(vvugrp)
     d w_ldor_gml      s                   like(vvldor)
     d w_vare_gml      s                   like(vvvare)
8.01 d w_prgr          s              2
5.70 d w_lage          s              2  0
5.70 d w_avde          s              4  0
5.21 d w_vtyp          s                   like(vvvtyp)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Dersom det er valgt å ikke skrive ut rabatt, settes indikator.
      *
     c                   if        p_rval = 1
     c                   eval      *in81 = *on
     c                   endif
      *
      * Initiering og utskrift av hode
      *
     c                   exsr      hode
      *
     c                   eval      b_frst = *on
      *
      * Behandler utskift
      *
     c                   select
     c                   when      p_ogrp <> 0
     c                   exsr      behandl_vgrp
     c                   when      p_ldor <> 0
     c                   exsr      behandl_ldor
     c                   when      p_vsor <> *blank
     c                   exsr      behandl_vsor
     c                   other
     c                   exsr      behandl_tot
     c                   endsl
      *
      * Skiver innholdsfortegnelse / index
      *
     c                   exsr      index
      *
      * Oppdaterer kunde distribusjonsliste med unik nøkkel for
      * forsendelsen.
      *
     c                   eval      fkdilu_kund = p_kund
     c                   eval      fkdilu_kpro = p_kpro
     c     fkdilu_key    chain     fkdilur                            92
     c                   if        *in92 = *off
     c                   eval      fhbtis = p_btis
6.10 c                   time                    fhedat
6.10 c                   time                    fhetim
6.10 c                   eval      fheusr = l_user
     c                   update    fkdilur
     c                   endif
      *
      * Avslutt
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av hode-opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode          begsr
      *
      * Legger inn arb.stasjon og bruker i faste felter
      *
     c                   time                    w_timestamp
     c                   move      w_timestamp   a1date
      *
      * Henter firmaopplysninger
      *
     c     afirl1_key    chain     afirl1r                            90
     c                   eval      a1fna1 = aafnav
     c                   eval      a1fna2 = aafnav
      *
      * Prisdato
      *
     c     *dmy          move      p_dato        a1dato
      *
      * Henter kunde- og kundeprosjekt-informasjon
      *
     c                   eval      a1kund = p_kund
     c                   eval      rkunl1_kund = p_kund
     c     rkunl1_key    chain     rkunl1                             90
     c                   if        *in90 = *off
     c                   eval      a1knav = rknavn
     c                   eval      a1gate = rkgate
     c                   eval      a1kpos = rksted
     c                   endif
      *
     c                   if        p_kpro <> 0
      *
     c                   eval      a1kpro = p_kpro
     c                   eval      fkprl1_kund = p_kund
     c                   eval      fkprl1_kpro = p_kpro
     c     fkprl1_key    chain     fkprl1                             90
     c                   if        *in90 = *off
     c                   eval      a1pnav = flnavn
     c                   eval      a1adr1 = fladr1
     c                   eval      a1ppon = flponr
     c                   if        flponr <> 0
     c                   eval      apospf_ponr = flponr
     c     apospf_key    chain     apospf                             90
     c                   if        *in90 = *off
     c                   eval      a1ppos = apsted
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Dersom det er valgt å fjerne rabatter, skal denne utelates fra
      * overskriften
      *
     c***                if        p_rval = 0
     c***                eval      a2rtxt = 'Rab'
     c***                endif
      *
      * Skriver header
      *
     c                   write     a1hdr
     c                   write     a2hdr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av utskrift - varegruppe og leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_vgrp  begsr
      *
      * Posisjonerer og leser i vareregister i.h.t. utplukk
      *
     c                   eval      vvarl9_ogrp = p_ogrp
     c                   eval      vvarl9_hgrp = p_hgrp
     c                   eval      vvarl9_ugrp = p_ugrp
     c                   eval      vvarl9_ldor = p_ldor
      *
     c                   select
     c                   when      p_ldor <> 0
     c     vvarl9_key1   setll     vvarl9
     c     vvarl9_key1   reade     vvarl9                                 90
     c                   when      p_ugrp <> 0
     c     vvarl9_key2   setll     vvarl9
     c     vvarl9_key2   reade     vvarl9                                 90
     c                   when      p_hgrp <> 0
     c     vvarl9_key3   setll     vvarl9
     c     vvarl9_key3   reade     vvarl9                                 90
     c                   other
     c     vvarl9_key4   setll     vvarl9
     c     vvarl9_key4   reade     vvarl9                                 90
     c                   endsl
      *
     c                   dow       *in90 = *off
      *
     c                   exsr      behandling
      *
     c                   select
     c                   when      p_ldor <> 0
     c     vvarl9_key1   reade     vvarl9                                 90
     c                   when      p_ugrp <> 0
     c     vvarl9_key2   reade     vvarl9                                 90
     c                   when      p_hgrp <> 0
     c     vvarl9_key3   reade     vvarl9                                 90
     c                   other
     c     vvarl9_key4   reade     vvarl9                                 90
     c                   endsl
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av utskrift - kun leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_ldor  begsr
      *
      * Leser hele vareregisteret, og plukker varer tilhørende leverandør
      *
     c     vvarl9_key5   setll     vvarl9
     c     vvarl9_key5   reade     vvarl9                                 90
      *
     c                   dow       *in90 = *off
      *
     c                   if        vvldor <> p_ldor
     c                   goto      les_neste
     c                   endif
      *
     c                   exsr      behandling
      *
     c     les_neste     tag
      *
     c     vvarl9_key5   reade     vvarl9                                 90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av utskrift - utplukk på varesortiment
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_vsor  begsr
      *
      * Kaller utplukksprogram
      *
     c                   eval      b_svgr = *on
     c                   eval      b_sldo = *on
     c                   eval      b_stek = *on
      *
     c                   if        p_lval = 1
     c                   eval      b_sldo = *off
     c                   endif
      *
     c                   call      'VD700R'
     c                   parm                    w_firm
     c                   parm                    p_vsor
     c                   parm                    b_svgr
     c                   parm                    b_sldo
     c                   parm                    b_stek
6.20 c                   parm                    w_lage
      *
      * Leser utplukksfil som inneholder alle varer i sortimentet
      *
     c                   eval      w_vare_gml = *blank
      *
     c                   read      vvawpf                                 90
     c                   dow       *in90 = *off
      *
     c                   if        vvvare <> w_vare_gml
     c                   exsr      behandling
     c                   endif
      *
     c                   eval      w_vare_gml = vvvare
      *
     c                   read      vvawpf                                 90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av utskrift - hele vareregisteret
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_tot   begsr
      *
     c     vvarl9_key5   setll     vvarl9
     c     vvarl9_key5   reade     vvarl9                                 90
      *
     c                   dow       *in90 = *off
      *
     c                   exsr      behandling
      *
     c     vvarl9_key5   reade     vvarl9                                 90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
      * Siling av varer.
      * Diversevarer utelates
      * Skaffevarer skal kun være med dersom dette er valgt.
      * Varer merket 'ikke prisdistribusjon' og utgåtte varer utelates.
      *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    w_lage
6.21 c                   parm                    w_vtyp
6.21 c                   parm                    w_udat
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
      *
6.21 c                   if        w_vtyp = 'D'
     c                   leavesr
     c                   endif
      *
     c                   if        p_sval = 0 and
6.21 c                             w_vtyp = 'S'
     c                   leavesr
     c                   endif
      *
     c                   if        vvkpdi = 1
     c                   leavesr
     c                   endif
      *
6.20 c                   if        w_udat <> *loval and
6.20 c                             w_udat <= w_ddat
     c                   leavesr
     c                   endif
      *
      * Behandler brudd
      *
     c                   if        vvogrp <> w_ogrp_gml
     c                   exsr      hode_ogrp
     c                   endif
      *
     c                   if        vvogrp <> w_ogrp_gml or
     c                             vvhgrp <> w_hgrp_gml
     c                   exsr      hode_hgrp
     c                   endif
      *
     c                   if        vvogrp <> w_ogrp_gml or
     c                             vvhgrp <> w_hgrp_gml or
     c                             vvugrp <> w_ugrp_gml
     c                   exsr      hode_ugrp
     c                   endif
      *
     c                   if        vvogrp <> w_ogrp_gml or
     c                             vvhgrp <> w_hgrp_gml or
     c                             vvugrp <> w_ugrp_gml or
     c                             vvldor <> w_ldor_gml
     c                   exsr      hode_ldor
     c                   endif
      *
      * Henter enheter fra vare
      *
     c                   exsr      enheter
      *
      * Skriver detaljlinje
      *
     c                   exsr      detalj
      *
      * Dersom varetekst-2 eller enhet-2 finnes på vare, kalles subrutine
      * for utskrift av varetekst-2 og priser for enhet-2
      *
     c                   if        vvtek2 <> *blank and p_2val = 1 or
     c                             w_enh2 <> *blank
     c                   exsr      detalj_enh2
     c                   endif
      *
      * Dersom enhet-3 finnes på vare, kalles subrutine for utskrift av
      * priser for enhet-3
      *
     c                   if        w_enh3 <> *blank
     c                   exsr      detalj_enh3
     c                   endif
      *
      * Dersom det er valgt å ta med varetekst-2 på utskriften, skrives
      * blank linje for å få bedre oversikt i prisboken
      *
     c                   if        p_2val = 1
     c                   write     space                                30
     c                   exsr      overflow
     c                   endif
      *
      * Tar vare på brudd-variable
      *
     c                   eval      w_ogrp_gml = vvogrp
     c                   eval      w_hgrp_gml = vvhgrp
     c                   eval      w_ugrp_gml = vvugrp
     c                   eval      w_ldor_gml = vvldor
      *
     c                   eval      b_frst = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av enheter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     enheter       begsr
      *
     c                   eval      w_enh1 = *blank
     c                   eval      w_enh2 = *blank
     c                   eval      w_enh3 = *blank
      *
      * Leser prisbok-enheter på vare, og henter prisinformasjon
      *
     c                   eval      x = 0
      *
     c                   eval      vvenl3_vare = vvvare
     c                   eval      vvenl3_pben = 1
     c     vvenl3_key    setll     vvenl3
     c     vvenl3_key2   reade     vvenl3                                 92
     c                   dow       *in92 = *off and
     c                             x < 3
      *
     c                   eval      x = x + 1
      *
     c                   select
     c                   when      x = 1
     c                   eval      w_enh1 = veenhe
     c                   when      x = 2
     c                   eval      w_enh2 = veenhe
     c                   when      x = 3
     c                   eval      w_enh3 = veenhe
     c                   endsl
      *
     c     vvenl3_key2   reade     vvenl3                                 92
     c                   enddo
      *
      * Dersom prisbokenheter ikke ble funnet, eller dersom priser ikke
      * ble funnet på prisbokenheter, hentes priser på salgsenheter
      *
     c                   if        x = 0
      *
     c                   eval      vvenl2_vare = vvvare
     c                   eval      vvenl2_saen = 1
     c     vvenl2_key    setll     vvenl2
     c     vvenl2_key2   reade     vvenl2                                 92
     c                   dow       *in92 = *off and
     c                             x < 3
      *
     c                   eval      x = x + 1
      *
     c                   select
     c                   when      x = 1
     c                   eval      w_enh1 = veenhe
     c                   when      x = 2
     c                   eval      w_enh2 = veenhe
     c                   when      x = 3
     c                   eval      w_enh3 = veenhe
     c                   endsl
      *
     c     vvenl2_key2   reade     vvenl2                                 92
     c                   enddo
      *
     c                   endif
      *
     c     enhienheter   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift ved brudd på overgruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode_ogrp     begsr
      *
     c                   eval      w_otxt = *blank
      *
     c                   eval      vogrl1_oogr = vvogrp
     c     vogrl1_key    chain     vogrl1                             91
     c                   if        *in91 = *off
     c                   eval      w_otxt = vgotxt
     c                   endif
      *
     c                   if        b_frst = *off
     c                   write     a1hdr
     c                   write     a2hdr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift ved brudd på hovedgruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode_hgrp     begsr
      *
     c                   eval      w_htxt = *blank
      *
     c                   eval      vhgrl1_hogr = vvogrp
     c                   eval      vhgrl1_hhgr = vvhgrp
     c     vhgrl1_key    chain     vhgrl1                             91
     c                   if        *in91 = *off
     c                   eval      w_htxt = vghtxt
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift ved brudd på undergruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode_ugrp     begsr
      *
     c                   eval      w_utxt = *blank
      *
     c                   eval      vugrl1_uogr = vvogrp
     c                   eval      vugrl1_uhgr = vvhgrp
     c                   eval      vugrl1_uugr = vvugrp
     c     vugrl1_key    chain     vugrl1                             91
     c                   if        *in91 = *off
     c                   eval      w_utxt = vgutxt
     c                   endif
      *
     c                   move      vvogrp        w_ogrp_alf
     c                   move      vvhgrp        w_hgrp_alf
     c                   move      vvugrp        w_ugrp_alf
      *
     c                   eval      a3txt1 = %trim( %trim(w_otxt) + ' - ' +
     c                                             %trim(w_htxt) + ' - ' +
     c                                             %trim(w_utxt) + ' (' +
     c                                             w_ogrp_alf + ' ' +
     c                                             w_hgrp_alf + ' ' +
     c                                             w_ugrp_alf + ')')
      *
     c                   eval      a3txt2 = a3txt1
      *
     c                   write     a3hdr                                30
     c                   exsr      overflow
      *
      * Tar vare på sidenummer i index-array
      *
     c                   eval      d_text = a3txt2
     c                   eval      d_side = d_pagnbr
      *
     c                   eval      w_i1 = w_i1 + 1
     c                   eval      a_index(w_i1) = d_index
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift ved brudd på leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode_ldor     begsr
      *
      * Dersom leverandør er utelatt eller mangler på vare, skrives blank
      * linje
      *
     c                   if        p_lval = 1 or
     c                             vvldor = 0
     c                   write     space                                30
     c                   exsr      overflow
     c                   goto      end_hode_ldor
     c                   endif
      *
      * Dersom leverandør ikke er utelatt, skrives leverandørnavn
      *
     c                   eval      w_lnav = *blank
      *
     c                   eval      rlevl1_levr = vvldor
     c     rlevl1_key    chain     rlevl1                             91
     c                   if        *in91 = *off
     c                   eval      w_lnav = rlnavn
     c                   endif
      *
     c                   move      vvldor        w_ldor_alf
      *
     c                   eval      a4txt1 = %trim( %trim(w_lnav) + ' (' +
     c                                      w_ldor_alf + ')')
      *
     c                   eval      a4txt2 = a4txt1
      *
     c                   write     a4hdr                                30
     c                   exsr      overflow
      *
     c     end_hode_ldor endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine utskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalj        begsr
      *
     c                   eval      d1tek1 = vvtek1
     c                   eval      d1vare = vvvare
6.30 c                   if        w_lv_nobb > 0
6.30 c                   eval      d1nobb = w_lv_nobb
6.30 c                   else
     c                   eval      d1nobb = vvnobb
6.30 c                   endif
     c                   eval      d1vtyp = *blank
     c                   eval      d1ptxt = *blank
     c                   eval      d1raba = 0
     c                   eval      d1enh1 = w_enh1
     c                   eval      d1sabr = 0
     c                   eval      d1sant = 0
      *
6.21 c                   if        w_vtyp <> 'L'
6.21 c                   eval      d1vtyp = w_vtyp
     c                   endif
      *
      * Kaller prisberegningsprogram for enhet-1, og finner salgspris,
      * rabatt og priskode
      *
      *
     c                   eval      hifirm = p_firm
     c                   eval      hirkat = 0
     c                   eval      hikund = p_kund
     c                   eval      hikpro = p_kpro
     c                   eval      hivare = vvvare
     c                   eval      hilety = p_lety
     c                   eval      hienhe = w_enh1
     c                   eval      hidato = p_dato
     c                   eval      hitime = *loval
5.70 c                   eval      hiprgr = w_prgr
     c                   eval      hinumm = 0
     c                   eval      hikode = *blank
     c                   eval      hidekn = 0
     c                   eval      hitilb = *off
     c                   eval      hiavgf = 0
     c                   eval      hipriv = 0
     c                   eval      hiskaf = *off
     c                   eval      hildor = 0
     c                   eval      hisapr = 0
     c                   eval      hikopr = 0
     c                   eval      hiinlr = *on
      *
     c                   if        p_tval = 0
     c                   eval      hitilb = *on
     c                   endif
      *
     c                   call      'VP900R'
     c                   parm                    hirec
     c                   parm                    d_orec
      *
     c                   eval      d1sabr = d_oppr
     c                   eval      d1sant = d_sapr
      *
     c                   if        d_rab1 > 0
     c                   eval(h)   d1sant = d1sant - (d1sant * d_rab1 / 100)
     c                   endif
     c                   if        d_rab2 > 0
     c                   eval(h)   d1sant = d1sant - (d1sant * d_rab2 / 100)
     c                   endif
      *
      * Beregner rabatt
      *
     c                   if        p_rval = 0
      *
     c                   if        d_rab2 = 0
     c                   eval      d1raba = d_rab1
     c                   else
     C                   if        d1sabr <> 0
     c                   eval(h)   d1raba = 100 * (d1sabr - d1sant) / d1sabr
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Henter beskrivelse av priskode
      *
     c                   if        d_kpri <> *blank
     c                   eval      vpkol1_pkod = d_kpri
     c     vpkol1_key    chain     vpkol1                             92
     c     *in92         ifeq      *off
     c                   eval      d1ptxt = vaptxt
     c                   endif
     c                   endif
      *
      * Skriv detaljlinje
      *
     c                   write     d1dtl                                30
     c                   exsr      overflow
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine utskrift av detaljlinje for enhet-2
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalj_enh2   begsr
      *
     c                   eval      d2tek2 = *blank
     c                   eval      d2ptxt = *blank
     c                   eval      d2raba = 0
     c                   eval      d2enh2 = w_enh2
     c                   eval      d2sabr = 0
     c                   eval      d2sant = 0
      *
      * Varetekst-2
      *
     c                   if        p_2val = 1
     c                   eval      d2tek2 = vvtek2
     c                   endif
      *
      * Kaller prisberegningsprogram for enhet-2
      *
     c                   if        w_enh2 <> *blank
      *
     c                   eval      hienhe = w_enh2
      *
     c                   call      'VP900R'
     c                   parm                    hirec
     c                   parm                    d_orec
      *
     c                   eval      d2sabr = d_oppr
     c                   eval      d2sant = d_sapr
      *
     c                   if        d_rab1 > 0
     c                   eval(h)   d2sant = d2sant - (d2sant * d_rab1 / 100)
     c                   endif
     c                   if        d_rab2 > 0
     c                   eval(h)   d2sant = d2sant - (d2sant * d_rab2 / 100)
     c                   endif
      *
      * Beregner rabatt
      *
     c                   if        p_rval = 0
      *
     c                   if        d_rab2 = 0
     c                   eval      d2raba = d_rab1
     c                   else
     C                   if        d2sabr <> 0
     c                   eval(h)   d2raba = 100 * (d2sabr - d2sant) / d2sabr
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Henter beskrivelse av priskode
      *
     c                   if        d_kpri <> *blank
     c                   eval      vpkol1_pkod = d_kpri
     c     vpkol1_key    chain     vpkol1                             92
     c     *in92         ifeq      *off
     c                   eval      d2ptxt = vaptxt
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Skriv detaljlinje
      *
     c                   write     d2dtl                                30
     c                   exsr      overflow
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine utskrift av detaljlinje for enhet-3
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalj_enh3   begsr
      *
     c                   eval      d3ptxt = *blank
     c                   eval      d3raba = 0
     c                   eval      d3enh3 = w_enh3
     c                   eval      d3sabr = 0
     c                   eval      d3sant = 0
      *
      * Kaller prisberegningsprogram for enhet-3
      *
     c                   eval      hienhe = w_enh3
      *
     c                   call      'VP900R'
     c                   parm                    hirec
     c                   parm                    d_orec
      *
     c                   eval      d3sabr = d_oppr
     c                   eval      d3sant = d_sapr
      *
     c                   if        d_rab1 > 0
     c                   eval(h)   d3sant = d3sant - (d3sant * d_rab1 / 100)
     c                   endif
     c                   if        d_rab2 > 0
     c                   eval(h)   d3sant = d3sant - (d3sant * d_rab2 / 100)
     c                   endif
      *
      * Beregner rabatt
      *
     c                   if        p_rval = 0
      *
     c                   if        d_rab2 = 0
     c                   eval      d3raba = d_rab1
     c                   else
     C                   if        d3sabr <> 0
     c                   eval(h)   d3raba = 100 * (d3sabr - d3sant) / d3sabr
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Henter beskrivelse av priskode
      *
     c                   if        d_kpri <> *blank
     c                   eval      vpkol1_pkod = d_kpri
     c     vpkol1_key    chain     vpkol1                             92
     c     *in92         ifeq      *off
     c                   eval      d3ptxt = vaptxt
     c                   endif
     c                   endif
      *
      * Skriv detaljlinje
      *
     c                   write     d3dtl                                30
     c                   exsr      overflow
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for håndtering av overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overflow      begsr
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   write     a2hdr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift av innholdsfortegnelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     index         begsr
      *
      * Fjerner sidenummer fra innholdsfortegnelse
      *
     c                   eval      *in80 = *on
      *
      * Utskrift av parametre
      *
     c                   write     a1hdr
      *
      * Utskrift av innholdsfortegnelse
      *
     c                   write     a5hdr
      *
      * Skriver linjer i innholdsfortegnelsen
      *
     c                   dow       w_i2 < w_i1
      *
     c                   eval      w_i2 = w_i2 + 1
      *
     c                   eval      d_index = a_index(w_i2)
      *
     c                   eval      d4text = %trim(d_text) + '   ' +
     c                                     '..............................'+
     c                                     '..............................'+
     c                                     '..............................'+
     c                                     '..............................'
     c                   eval      d4side = d_side
      *
     c                   write     d4dtl                                30
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   write     a5hdr
     c                   endif
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kund
     c                   parm                    p_kpro
     c                   parm                    p_vsor
     c                   parm                    p_ogrp
     c                   parm                    p_hgrp
     c                   parm                    p_ugrp
     c                   parm                    p_ldor
     c                   parm                    p_sval
     c                   parm                    p_tval
     c                   parm                    p_2val
     c                   parm                    p_rval
     c                   parm                    p_lval
     c                   parm                    p_lety
     c                   parm                    p_dato
     c                   parm                    p_btis
      *
      * Key for oppslag på firmaregister
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på kunderegister
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kundeprosjekt-register
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag på poststed-register
      *
     c     apospf_key    klist
     c                   kfld                    apospf_ponr
      *
      * Key for les på vareregister med varegruppe og leverandør
      *
     c     vvarl9_key1   klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl9_ogrp
     c                   kfld                    vvarl9_hgrp
     c                   kfld                    vvarl9_ugrp
     c                   kfld                    vvarl9_ldor
      *
     c     vvarl9_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl9_ogrp
     c                   kfld                    vvarl9_hgrp
     c                   kfld                    vvarl9_ugrp
      *
     c     vvarl9_key3   klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl9_ogrp
     c                   kfld                    vvarl9_hgrp
      *
     c     vvarl9_key4   klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl9_ogrp
      *
     c     vvarl9_key5   klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på overgruppe-register
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe-register
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe-register
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på leverandør-register
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for les på vare-enhet-register m/prisbok-enhet
      *
     c     vvenl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl3_vare
     c                   kfld                    vvenl3_pben
      *
     c     vvenl3_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl3_vare
      *
      * Key for les på vare-enhet-register m/salgsenhet
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
     c                   kfld                    vvenl2_saen
      *
     c     vvenl2_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
      *
      * Key for oppslag på priskode-register
      *
     c     vpkol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vpkol1_pkod
      *
      * Key for oppdatering av kunde-distribusjonsliste
      *
     c     fkdilu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkdilu_kund
     c                   kfld                    fkdilu_kpro
      *
      *
      * Splitter datastruktur i variable
      *
     c                   eval      w_firm = p_firm
      *
      * Henter dagens dato
      *
6.20 c                   time                    w_ddat
5.70  *
5.70  * Henter prisgruppe
5.70  *
5.70 c                   eval      w_lage = p_lage
5.70 c                   eval      w_avde = 0
5.70  *
5.70 c                   call      'VL712R   '
5.70 c                   parm                    w_firm
5.70 c                   parm                    w_lage
5.70 c                   parm                    w_avde
5.70 c                   parm                    w_prgr
      *
     c                   endsr
