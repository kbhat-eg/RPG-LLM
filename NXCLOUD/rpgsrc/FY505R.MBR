     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FY505R
      * Beskrivelse . . : Vise slettede ordrelinjer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *  6.10 300710 BSA  Nytt program
6.nu  * R6.30 270514 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.30  * R6.30 161117 nho  Ved spørring på år/ordrenr så ble det listet
6.30  *                   feil da samme ordrenr lå på forskjellige år,
6.30  *                   og år ligger ikke på linjenivå
7.01  *       060819 sst  Mulighet til å hente slettet ordre til tilbud
7.02  *       041219 sst  Bare hente linjer som ikke er slettet enkeltvis
      *
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
8.02  *K0000  120824 bsa  Valg for å innhente nytt tilbudsnummer
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffdell1    if   e           k disk    rename(fdelpfr:fdell1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffdesl1    if   e           k disk    rename(fdespfr:fdesl1r)
7.01 ffdeslr    uf   e           k disk    rename(fdespfr:fdeslrr)
     fvpkol1    if   e           k disk    rename(vpkopfr:vpkol1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
7.01 ffodtlu    uf a e           k disk    rename(fodtpfr:fodtlur)
8.02 fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
8.02 ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
      *
     ffy505d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
8.02 d l_wsid                901    910
7.01 d l_user                911    920
7.01 d l_filg                931    933
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av datastrukturer
      *
      *  Datastruktur for detaljlinje
      *
     d                 ds
7.01 d d_text                        73
     d  d_vare                        8    overlay(d_text)
     d  d_tek1                       30    overlay(d_text:10)
     d  d_anta                       13    overlay(d_text:37)
     d  d_enh1                        3    overlay(d_text:50)
     d  d_raba                        6    overlay(d_text:53)
     d  d_salg                       13    overlay(d_text:58)
7.01 d  d_suff                        2    overlay(d_text:72)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(fyfirm)
     d p_numm          s                   like(fynumm)
     d p_suff          s                   like(fysuff)
     d p_aarr          s              4  0
7.01 d p_nysu          s                   like(fysuff)
8.02 d p_tnum          s                   like(fynumm)
      *
      * Definering av variabler i nøkler
      *
     d rkunl1_kund     s                   like(rkkund)
     d vpkol1_pkod     s                   like(vapkod)
     d vltyl1_ltyp     s                   like(valtyp)
7.01 d fodtlu_numm     s                   like(fdnumm)
7.01 d fodtlu_suff     s                   like(fdsuff)
7.01 d fodtlu_line     s                   like(fdline)
7.01 d fdeslr_numm     s                   like(fsnumm)
7.01 d fdeslr_suff     s                   like(fssuff)
7.01 d fdeslr_line     s                   like(fsline)
8.02 d votyl1_otyp     s                   like(vaotyp)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_dato          s               d   datfmt(*iso)
     d w_raba          s              5  2
     d w_csts          s              1
     d w_ctx2          s             20
      *
     d w_firm          s                   like(fyfirm)
     d w_kund          s                   like(fykund)
     d w_numm          s                   like(fynumm)
     d w_suff          s                   like(fysuff)
     d w_vare          s                   like(fsvare)
     d w_aarr          s                   like(fyaarr)
     d w_eror          s              1
6.30 d x_dat4          s             10
7.01 d w_antl          s              5  0 inz(*zero)
8.02 d w_fast          s             10
8.02 d w_fell          s              6
8.02 d w_kode          s              1
8.02 d w_tnum          s                   like(fdnumm)
8.02 d w_type          s              2
8.02 d w_onum          s                   like(vaonum)
8.02 d w_otyp          s                   like(vaotyp)
8.02 d w_mld1          s             50
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(8)
7.01 d u_sot           s              1    inz(*off)
      *
7.01   dcl-s co402_verdi1  char(1);
7.01   dcl-s co402_verdi2  char(2048);
7.01   DCL-PR CO402R EXTPGM('CO402R');
7.01     p_filgrp            char(3)     const;
7.01     p_firm              packed(3:0) const;
7.01     p_lager             packed(2:0) const;
7.01     p_nokkel            char(50)    const;
7.01     p_verdi1            char(1);
7.01     p_verdi2            char(2048);
7.01   END-PR;
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Fyller control-bildet
      *
     c                   exsr      control_init
      *
      * Fyller subfile
      *
     c     fdesl1_key    setll     fdesl1
     c                   exsr      crt_subfile
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
7.01 c                   when      *inkj
7.01 c                             and u_sot  = *on
8.02 c                   eval      votyl1_otyp = w_otyp
8.02 c     votyl1_key    chain     votyl1
8.02 c                   if        %found and
8.02 c                             vaoakk > 0
8.02 c                   eval      w_mld1 = 'Ordretype er ikke av type Tilbud'
8.02 c                   call      'AA005R'
8.02 c                   parm                    w_mld1
8.02 c                   goto      avslutt
8.02 c                   endif
7.01 c                   exsr      bygg_tilbud
7.01 c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
7.01  *
7.01  *    Dersom det ikke er hentet slettede ordrelinjer til tilbud
7.01  *           settes p_nysu = 0 for ikke å bygge ordre i FY500R
7.01  *
7.01 c                   if        w_antl = *zero
7.01 c                   eval      p_nysu = *zero
8.02 c                   eval      p_tnum = 0
8.02 c                   else
8.02 c                   eval      p_tnum = w_tnum
7.01 c                   endif
7.01  *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å initierer/fylle opp control-bildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     control_init  begsr
      *
      * Finner ordre-hode
      *
     c     fdell1_key    setll     fdell1
     c     fdell1_key    reade     fdell1                                 90
     c                   if        *in90 = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   dow       *in90 = *off
     c     fdell1_key    reade     fdell1                                 90
     c                   enddo
      *
     c                   eval      b2kund = fykund
     c                   eval      b2navn = fynavn
     c                   eval      b2adr1 = fyadr1
     c                   eval      b2sted = fysted
     c                   eval      b2numm = fynumm
     c                   eval      b2vref = fyvref
     c                   eval      b2dref = fydref
     c                   eval      b2inf1 = fykom1
     c                   eval      b2inf2 = fykom2
8.02 c                   eval      w_otyp = fyotyp
      *
     c                   if        fydate <> *loval
     c     *dmy          move      fydate        b2ddat
     c                   endif
      *
     c     *hms          move      fytime        b2dtim
      *
     c*                  if        fytdat <> *loval
     c*    *dmy          move      fytdat        b2tdat
     c*                  endif
      *
     c*                  if        fyldat <> *loval
     c*    *dmy          move      fyldat        b2ldat
     c*                  endif
      *
      * Henter kundenavn
      *
     c                   if        fykund <> 0
     c                   eval      rkunl1_kund = fykund
     c     rkunl1_key    chain     rkunl1                             90
     c                   if        *in90 = *off
     c                   eval      b2knav = rknavn
     c                   eval      b2kgat = rkgate
     c                   eval      b2kste = rksted
     c                   endif
     c                   endif
5.21  *
5.21  * Viser mobilnummer
5.21  *
5.21 c                   eval      b2motx = *blank
5.21 c                   eval      b2mobi = rkmobn
5.21  *
5.21 c*                  if        fymobi <> *blank
5.21 c*                  eval      b2mobi = fymobi
5.21 c*                  endif
5.21  *
5.21 c                   if        b2mobi <> *blank
5.21 c                   eval      b2motx = 'Mob:'
5.21 c                   endif
      *
      * Finner ordretotal
      *
     c                   eval      b2salg = (fytots - fytotr)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c     fdesl1_key    setll     fdesl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vise
      *
     c                   if        b1valg = 5
     c                   call      'FD506R'
     c                   parm                    w_firm
6.nu c                   parm                    b1numm
     c                   parm                    b1suff
     c                   parm                    b1line
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
7.01  *
7.01  * Hente linjer til tilbud
7.01  *
7.01 c                   if        b1valg = 1
7.01 c                             and u_sot  = *on
8.02 c                   eval      votyl1_otyp = w_otyp
8.02 c     votyl1_key    chain     votyl1
8.02 c                   if        %found and
8.02 c                             vaoakk > 0
8.02 c                   eval      w_mld1 = 'Ordretype er ikke av type Tilbud'
8.02 c                   call      'AA005R'
8.02 c                   parm                    w_mld1
8.02 c                   eval      b1valg = 0
8.02 c                   update    b1sfl
8.02 c                   iter
8.02 c                   endif
7.01 c                   exsr      bygg_tilbud
7.01 c                   eval      b1valg = 0
7.01 c                   update    b1sfl
7.01 c                   iter
7.01 c                   endif
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
6.30  *
6.30 c     nyles         tag
      *
     c     fdesl1_key    reade     fdesl1                                 15
      *
     c                   if        *in15
     c                   leave
     c                   endif
      *
6.30  * Sjekker om det er riktig år på linjenivå
6.30 c                   movel     fsslda        x_dat4
6.30 c                   movel     x_dat4        w_aar4            4 0
6.30 c                   if        w_aar4 <> p_aarr
6.30 c                   goto      nyles
6.30 c                   endif
6.30  *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1numm = fsnumm
     c                   eval      b1suff = fssuff
     c                   eval      b1lety = fslety
     c                   eval      b1line = fsline
     c                   eval      b1text = *blank
      *
     c                   eval      w_raba = 0
      *
      * Finner linjetype
      *
     c                   eval      vltyl1_ltyp = fsltyp
     c     vltyl1_key    chain     vltyl1                             92
      *
      * Tekstlinje
      *
     c                   if        valktx = 1
     c                   eval      b1text = fstek1 + fstek2
     c                   eval      b1lety = *zero
      *
      * Varelinje
      *
     c                   else
      *
     c                   if        fsrab2 = 0 and
     c                             fsbrr1 = 0 and
     c                             fsbrr2 = 0
     c                   eval      w_raba = fsrab1
     c                   else
     c                   if        fssums <> 0
     c                   eval(h)   w_raba = fssumr * 100 / fssums
     c                   endif
     c                   endif
      *
     c                   eval(h)   fssums = fssums - (fssums * w_raba / 100)
      *
     c                   eval      d_anta = %editc(fsanta : 'M')
     c                   eval      d_salg = %editc(fssums : 'M')
      *
     c                   eval      d_vare = fsvare
     c                   eval      d_tek1 = fstek1
     c                   eval      d_enh1 = fsenh1
     c                   eval      d_raba = *blank
      *  ssdebug
7.01 c*                  eval      d_suff = %subst(%char(fsalme):3:2)
7.01 c                   if        fsalme > 0
7.01 c                   move      fsalme        d_suff
7.01 c                   else
7.01 c                   eval      d_suff = *blank
7.01 c                   endif
      *
     c                   if        w_raba <> 0
     c                   eval      d_raba = %editc(w_raba : '4')
     c                   endif
      *
     c                   if        w_raba = 0
     c                   eval      vpkol1_pkod = fskpri
     c     vpkol1_key    chain     vpkol1                             91
     c                   if        *in91 = *off
     c                   move      vaptxt        d_raba
     c                   endif
     c                   endif
      *
     c                   eval      b1text = d_text
      *
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
7.01  *
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Subrutine for å bygge tilbud fra Slettet ordre
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01 c     bygg_tilbud   begsr
7.01  *                            En linje er valgt
7.01  *                            _________________
7.01 c                   if        b1valg = 1
7.01 c                   eval      fdeslr_numm = b1numm
7.01 c                   eval      fdeslr_suff = b1suff
7.01 c                   eval      fdeslr_line = b1line
7.01 c     fdeslr_key    chain     fdeslr
7.01 c                   if        %found(fdeslr)
7.01 c                   exsr      hent_linje
7.01 c                   endif
7.01 c                   else
7.01  *                            Alle linjer er valgt
7.01  *                            ____________________
7.01 c                   if        *inkj = *on
7.01  *   Skal alle linjer være med?
7.01 c     h1taga        tag
7.01 c                   write     h1win
7.01 c                   exfmt     h1win
7.01  *
7.01  * Sjekk svar
7.01  *
7.01 c                   if        *inkc
     c                   eval      p_nysu = *zero
7.01 c                   goto      end_bygg
7.01 c                   endif
7.01 c                   if        h1svar <> ' '
7.01 c                             and h1svar <> 'J'
7.01 c                   goto      h1taga
7.01 c                   endif
8.02 c                   if        h1tilb <> ' '
8.02 c                             and h1tilb <> 'J'
8.02 c                   goto      h1taga
8.02 c                   endif
7.01s *s  Skal vel ikke med men kan ligge foreløpig
7.01s *sEtter at man har vært inne i dette vindu (H1WIN)
7.01s *svelges logisk fil tilbake til ordrerekkefølge
7.01s *sog sub-file fylles på nytt.
7.01s *s
7.01sc*s                 eval      w_seqe = ' '
7.01sc*s                 eval      fdell1_aarr = 0
7.01sc*S                 eval      fdell1_numm = 0
7.01sc*S                 eval      fdell1_suff = 0
8.02  *
8.02 c                   if        h1tilb = 'J'
8.02 c                   eval      w_tnum = 0
8.02 c                   exsr      hente_numm
8.02 c                   eval      p_nysu = 0
8.02 c                   endif
7.01  *
7.01 c                   eval      fdeslr_numm = b1numm
7.01 c                   eval      fdeslr_suff = b1suff
7.01 c                   eval      fdeslr_line = *zero
7.01 c     fdeslr_key    setll     fdeslr
7.01 c                   read      fdeslr
7.01 c                   dow       not %eof(fdeslr)
7.01 c                             and fsnumm = b1numm
7.01 c                             and fssuff = b1suff
7.01 c                   eval      b1line = fsline
7.02 c                   if        (h1svar = 'J'
8.02 c                              or fsalme = 0
8.02 c                              or h1tilb = 'J')
7.02 c                             and fsspgm <> 'FD100R'
7.01 c                   exsr      hent_linje
7.01 c                   endif
7.01 c                   read      fdeslr
7.01 c                   enddo
7.01 c                   endif
7.01 c                   endif
7.01  *
7.01 c     end_bygg      tag
7.01 c                   endsr
7.01  *
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Subrutine for å hente en linje fra FDESPF til FODTPF
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01 c     hent_linje    begsr
8.02  *
8.02 c                   if        w_tnum <> 0
8.02 c                   eval      fodtlu_numm = w_tnum
8.02 c                   else
7.01 c                   eval      fodtlu_numm = b1numm
8.02 c                   endif
7.01 c                   eval      fodtlu_suff = p_nysu
7.01 c                   eval      fodtlu_line = b1line
7.01 c     fodtlu_key    chain     fodtlu
7.01 c                   if        not %found(fodtlu)
7.01 c                   eval      fdfirm = w_firm
8.02 c                   if        w_tnum <> 0
8.02 c                   eval      fdnumm = w_tnum
8.02 c                   else
7.01 c                   eval      fdnumm = b1numm
8.02 c                   endif
7.01 c                   eval      fdsuff = p_nysu
7.01 c                   eval      fdline = fsline
7.01 c                   eval      fdotyp = 'T'
7.01 c                   eval      fdltyp = fsltyp
7.01 c                   eval      fdlety = fslety
7.01 c                   eval      fdvare = fsvare
7.01 c                   eval      fdenh1 = fsenh1
7.01 c                   eval      fdlage = fslage
7.01 c                   eval      fdkakk = fskakk
7.01 c                   eval      fdkpri = fskpri
7.01 c                   eval      fdkund = fskund
7.01 c                   eval      fdldor = fsldor
7.01 c                   time                    fdldat
7.01 c                   eval      fdavde = fsavde
7.01 c                   eval      fdkont = fskont
7.01 c                   eval      fdspes = fsspes
7.01 c                   eval      fdproj = fsproj
7.01 c                   eval      fdakti = fsakti
7.01 c                   eval      fdlvar = fslvar
7.01 c                   eval      fdnobb = fsnobb
7.01 c                   eval      fdtek1 = fstek1
7.01 c                   eval      fdtek2 = fstek2
7.01 c                   eval      fdanta = fsanta
7.01 c                   eval      fdoppr = fsoppr
7.01 c                   eval      fdsapr = fssapr
7.01 c                   eval      fdkopr = fskopr
7.01 c                   eval      fdinpr = fsinpr
7.01 c                   eval      fdrab1 = fsrab1
7.01 c                   eval      fdrab2 = fsrab2
7.01 c                   eval      fdrab3 = fsrab3
7.01 c                   eval      fdrab4 = fsrab4
7.01 c                   eval      fdrab5 = fsrab5
7.01 c                   eval      fdpasl = fspasl
7.01 c                   eval      fdrkr1 = fsrkr1
7.01 c                   eval      fdrkr2 = fsrkr2
7.01 c                   eval      fdomva = fsomva
7.01 c                   eval      fdlvre = fslvre
7.01 c                   eval      fdolag = fsolag
7.01 c                   eval      fdogrp = fsogrp
7.01 c                   eval      fdhgrp = fshgrp
7.01 c                   eval      fdugrp = fsugrp
7.01 c                   eval      fdvari = fsvari
7.01 c                   eval      fdkamp = fskamp
7.01 c                   eval      fdkbnr = fskbnr
7.01 c                   eval      fdkbli = fskbli
7.01 c                   eval      fdeann = fseann
7.01 c                   eval      fdprgr = fsprgr
7.01 c                   eval      fdprtx = fsprtx
7.01 c                   eval      fdwsid = fswsid
7.01 c                   eval      fduser = fsuser
7.01 c                   eval      fdodat = fsodat
7.01 c                   eval      fdotim = fsotim
7.01 c                   eval      fdoous = fsousr
7.01 c                   time                    fdedat
7.01 c                   time                    fdetim
7.01 c                   eval      fdeusr = l_user
7.01 c                   eval      w_antl = w_antl + 1
7.01 c                   write     fodtlur
7.01  *
7.01  *   Oppdatere linje i FDES hvilken Suffix denne er kopiert til
7.01  *
7.01 c                   eval      fsalme = p_nysu
7.01 c                   update    fdeslrr
7.01  *
7.01 c                   endif
7.01  *
7.01 c                   endsr
      *
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02  * Henter/Oppdatering av NUMMER-REGISTER
8.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.02  *
8.02 c     hente_numm    begsr
8.02  *
8.02 c                   eval      w_kode = '0'
8.02 c                   eval      w_fell = faaork
8.02 c                   eval      w_type = w_otyp
8.02 c                   eval      w_fast = *blanks
8.02  *
8.02  * Hente inn eventuell overstyrt
8.02  * ordretype for nummerserie
8.02  *
8.02 c                   call      'VA752R'
8.02 c                   parm                    w_firm
8.02 c                   parm                    w_otyp
8.02 c                   parm                    w_onum
8.02  *
8.02 c                   if        w_onum <> *blank
8.02 c                   eval      w_type = w_onum
8.02 c                   endif
8.02  *
8.02  * Kaller opp subprogram for henting av nummer fra NUMMER-REGISTER
8.02  *
8.02 c                   call      'AS100R'
8.02 c                   parm                    w_kode
8.02 c                   parm                    w_firm
8.02 c                   parm                    w_fell
8.02 c                   parm                    w_type
8.02 c                   parm                    w_fast
8.02 c                   parm                    w_tnum
8.02  *
8.02 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_aarr
     c                   parm                    p_numm
     c                   parm                    p_suff
7.01 c                   parm                    p_nysu
8.02 c                   parm                    p_tnum
7.01  *
7.01  * Key for oppslag på ordre-hode-register
7.01  *
7.01 c     fdell1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    w_aarr
7.01 c                   kfld                    w_numm
7.01 c                   kfld                    w_suff
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for les på ordre-linje-register
      *
     c     fdesl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
7.01  *
7.01  * Key for les på ordre-linje-register
7.01  *
7.01 c     fdeslr_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    fdeslr_numm
7.01 c                   kfld                    fdeslr_suff
7.01 c                   kfld                    fdeslr_line
      *
      * Key for oppslag på priskode-register
      *
     c     vpkol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vpkol1_pkod
      *
      * Key for oppslag på linje-type-register
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
7.01  *
7.01  * Key for les på ordre-linje-register
7.01  *
7.01 c     fodtlu_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    fodtlu_numm
7.01 c                   kfld                    fodtlu_suff
7.01 c                   kfld                    fodtlu_line
8.02  *
8.02  * Key for oppslag på ordretype-register
8.02  *
8.02 c     votyl1_key    klist
8.02 c                   kfld                    w_firm
8.02 c                   kfld                    votyl1_otyp
8.02  *
8.02  * Key for oppslag på status-register
8.02  *
8.02 c     fstsl1_key    klist
8.02 c                   kfld                    w_firm
      *
      * Henter parameter-verdier
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_numm = p_numm
     c                   eval      w_suff = p_suff
     c                   eval      w_aarr = p_aarr
      *
     c                   eval      *in22 = *on
8.02 c     fstsl1_key    chain     fstsl1
7.01  *
7.01  * Mulighet til å hente ordre til tilbud
7.01  *
7.01  * Sjekk om gen av tilbud fra slettede ordre benyttes
7.01  *                  skal hentes fra ny løsning
7.01   CallP CO402R(l_filg:w_firm:0:'SlettOrdreTilTilbud':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01      u_sot = *on;
7.01   endif;
7.01  *
7.01  *  Skal erstattes av ny løsning
7.01 c                   if        u_sot  = *on
7.01 c*                  eval      b2valg = '1 - Hent linje til tilbud'
7.01 c                   eval      b2funk = 'F10 - Hent alt til tilbud'
7.01 c                   eval      *in41  = *off
7.01 c                   else
7.01 c*                  eval      b2valg = *blank
7.01 c                   eval      b2funk = *blank
7.01 c                   eval      *in41  = *on
7.01 c                   endif
7.01 c                   endsr
