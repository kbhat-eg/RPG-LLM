      /TITLE  Kontroll av gyldig konto
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RS740R                                              *
      * Beskrivelse . . : Kontroll av gyldig konto                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      *           020100  Nytt program                                   KOB  *
      * V5.00     020802  Fjernet firma fra parameterliste                    *
      *                                                                       *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                                                       *
      *  BRUK AV INDIKATORER                                                  *
      *                                                                       *
      *    60-61 = Fileindikatorer chain etc.                                 *
      *                                                                       *
      *  BRUK AV RETURKODE                                                    *
      *                                                                       *
      *  Kode i statusfelt(w_stat = N) : post finnes ikke                     *
      *                                                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frhovl1    if   e           k disk    rename(rhovpfr:rhovl1r)
     fra07l1    if   e           k disk    rename(ra07pfr:ra07l1r)
      *
     d p_stat          s              1
     d p_firm          s                   like(rhfirm)
     d p_kont          s                   like(rhkont)
     d p_spes          s                   like(rhspes)
     d p_avde          s                   like(rhavde)
     d p_text          s                   like(rhtext)
      *
     d w_firm          s                   like(rhfirm)
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d ra07l1_gkod     s                   like(ragkod)
     d rhovl1_kont     s                   like(rhkont)
     d rhovl1_avde     s                   like(rhavde)
     d rhovl1_spes     s                   like(rhspes)
      *
      * Sjekke avdeling mot koderegister
      *
     c                   if        p_avde <> *zero                                       .....
     c                   move      p_avde        ra07l1_gkod                                 .
     c     ra07l1_key    chain     ra07l1                             61                     .
      *
     c                   if        *in61 = *on                                               .
     c                   move      'A'           p_stat                                      .
     c                   eval      p_text = 'Avdeling ikke i koderegisteret'                 .
     c                   endif                                                               .
      *
     c                   endif                                                           .....
      *
      * Hvis avdeling utfylt, må også konto være utfylt
      *
     c                   if        p_avde <> *zero and                                   .....
     c                             p_kont = *zero                                            .
     c                   move      'M'           p_stat                                      .
     c                   eval      p_text = 'Bare avdeling utfylt'                           .
     c                   goto      slutt                                                     .
     c                   endif                                                           .....
      *
      * Hvis spesifikasjon utfylt, må også konto være utfylt
      *
     c                   if        p_spes <> *zero and                                   .....
     c                             p_kont = *zero                                            .
     c                   move      'K'           p_stat                                      .
     c                   eval      p_text = 'Bare spesifikasjon utfylt'                      .
     c                   goto      slutt                                                     .
     c                   endif                                                           .....
      *
      * Sjekk konto
      * Konto-spes-avd
      *
     c                   eval      rhovl1_kont = p_kont                                      .
     c                   eval      rhovl1_spes = p_spes                                      .
     c                   eval      rhovl1_avde = p_avde                                      .
     c     rhovl1_key    chain     rhovl1r                            60
      *
      * Konto-spes
      *
     c                   if        *in60 = *on                                           .....
     c                   eval      rhovl1_avde = *zeros                                      .
     c     rhovl1_key    chain     rhovl1r                            60                     .
     c                   endif                                                           .....
      *
      * Konto
      *
     c                   if        *in60 = *on                                           .....
     c                   eval      rhovl1_spes = *zeros                                      .
     c     rhovl1_key    chain     rhovl1r                            60                     .
     c                   endif                                                           .....
      *
     c                   if        *in60 = *on                                           .....
     c                   move      'N'           p_stat                                      .
     c                   eval      p_text = 'Kontonummer er ugyldig'                         .
     c                   goto      slutt                                                     .
     c                   endif                                                           .....
      *
      * Er konto sperret?
      *
     c                   if        rhsprk = 'J'                                          .....
     c                   move      'S'           p_stat                                      .
     c                   eval      p_text = 'Konto er sperret'                               .
     c                   goto      slutt                                                     .
     c                   endif                                                           .....
      *
     c                   eval      p_text = rhtext                                           .
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
     c                   return
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_stat
S500 c***                parm                    p_firm
     c                   parm                    p_kont
     c                   parm                    p_spes
     c                   parm                    p_avde
     c                   parm                    p_text
      *
     c                   eval      w_firm = l_firm
     c                   eval      p_stat = *blank
     c                   eval      p_text = *blank
      *
      * Key for avdeling
      *
     c     ra07l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra07l1_gkod
      *
      * Key for kontoplan
      *
     c     rhovl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovl1_kont
     c                   kfld                    rhovl1_spes
     c                   kfld                    rhovl1_avde
      *
     c                   endsr
