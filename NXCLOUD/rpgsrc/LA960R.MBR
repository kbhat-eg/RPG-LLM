     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LA960R
      *  Beskrivelse . . : Oppdatering av lager-register utfra lagerbok
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
prg   * 5.70  290908 sst  Prisgruppe
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign
      * --------- ------  -------------------------------------------------
5.70  * PRGR      121208  Utvidet med prisgruppe / varetype
6.21  * 6.20      190511  Utvidet vl710r med div. info             bof
6.30  * 6.30  170918 bkv  Utvidet med trelast sortiment
8.01  *PRG2   140622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
6.30 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
     fla960d    cf   e             workstn
      *
      * Parameter-inn
      *
     d                 ds
     d d_prec                        64
     d  d_lage                        2  0 overlay(d_prec)
     d  d_dato                       10d   datfmt(*iso)
     d                                     overlay(d_prec:3)
     d  d_vare                        8  0 overlay(d_prec:13)
      *
      * Local Data Area
      *
     d                uds
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_vare          s                   like(vvvare)
     d w_firm          s              3  0
     d w_adrs          s             30
     d w_ponr          s              4  0
     d w_pste          s             30
     d w_avde          s              4  0
     d w_prec          s             64
5.70 d w_vtyp          s                   like(vvvtyp)
     d b_feil          s              1    inz(*off)
6.21 d w_udat          s                   like(vvudat)
6.21 d w_ldor          s                   like(vvldor)
6.21 d b_inlr          s              1    inz(*off)
6.30 d w_lv_nobb       s                   like(vvlnob)
6.30 d w_lv_modn       s                   like(vvlmod)
6.30 d w_lv_lldo       s                   like(vvlnle)
6.30 d w_lv_llva       s                   like(vvllva)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     b1tag         tag
      *
      *  Behandler bildet
      *
     c     b1taga        tag
     c                   exfmt     b1bld
      *
      *  F3/F12=Avslutt
      *
     c                   if        *inkc = *on or
     c                             *inkl = *on
     c                   goto      end_pgm
     c                   endif
      *
      * F4 = Søking
      *
     c                   if        *inkd = *on
     c                   if        w_afld = 'B1LAGE'
     c                   exsr      finn_lager
     c                   goto      b1taga
     c                   endif
     c                   if        w_afld = 'B1VARE'
     c                   exsr      finn_vare
     c                   goto      b1taga
     c                   endif
     c                   endif
      *
      *  Sjekker input-felter
      *
     c                   exsr      sjekk_lager
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
     c                   if        b1vare <> 0
     c                   exsr      sjekk_vare
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
     c                   endif
      *
     c                   exsr      sjekk_dato
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
      *  Kaller program for oppdatering
      *
     c                   eval      d_lage = b1lage
     c     *dmy          move      b1date        d_dato
     c                   eval      d_vare = b1vare
     c                   eval      w_prec = d_prec
      *
     c                   call      'LA962R'
     c                   parm                    w_prec
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for spørring på lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_lager    begsr
      *
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    b1lage
     c                   parm                    b1latx
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for spørring på vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_vare     begsr
      *
5.52 c                   call      'VV500R'
5.52 c                   parm                    w_firm
5.52 c                   parm                    w_vare
5.52 c                   parm                    b1vatx
5.52 c                   movel     w_vare        b1vare
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kontroll av lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_lager   begsr
      *
     c                   eval      b_feil = *off
     c                   call      'FÅ720R'
     c                   parm                    w_firm
     c                   parm                    b1lage
     c                   parm                    b1latx
     c                   parm                    w_adrs
     c                   parm                    w_ponr
     c                   parm                    w_pste
     c                   parm                    w_avde
     c                   parm                    b_feil
      *
     c                   if        b_feil = *on
     c                   eval      *in31 = *on
     c                   eval      b1latx = *blank
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kontroll av vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_vare    begsr
      *
     c                   movel     b1vare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1
5.70  *  Hent vvtyp fra vare og lager
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    b1lage
5.70 c                   parm                    w_vtyp
6.21 c                   parm                    w_udat
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
     c                   if        not %found or
5.70 c                             w_vtyp <> 'L'
     c                   eval      b_feil = *on
     c                   else
     c                   eval      b1vatx = vvtek1
     c                   endif
      *
     c                   if        b_feil = *on
     c                   eval      *in33 = *on
     c                   eval      b1vatx = *blank
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kontroll av dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_dato    begsr
      *
     c                   eval      b_feil = *off
      *
     c                   if        b1date = 0
     c                   eval      b_feil = *on
     c                   eval      *in32 = *on
     c                   goto      end_dato
     c                   endif
      *
     c     *dmy          test(d)                 b1date                 32
     c                   if        *in32 = *on
     c                   eval      b_feil = *on
     c                   endif
      *
     c     end_dato      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
