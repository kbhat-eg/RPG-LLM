     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VP710R
      * Beskrivelse . . : Vare-pris, henter salgsenhet-1 m/priser
      *
      *                   Input:  Firma, vare, prisgruppe, lev.type,
      *                            dato og tidspunkt
      *                   Output: Enhet, salgspris, butikkpris og
      *                           tilbudspris
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       260499 HKO  Nytt program
      *       020805 HKO  Endret les mot vare-enhet for å finne salgsenheter
5.70  * PRGR  231008 BOF  Utvidet med prisgruppe
6.20  *       220311 BOF  Utvidet med prisgiver på vare-pris
6.21  *       240411 BOF  Utvidet med tilbudspris med varegruppe og leverandør
6.22  *       070312 BOF  Justering på hente tilbudspris vareruppe og leverandør
7.01  *       120718 BOF  Henter beste tilbudspris
8.01  *       070623 bof  NXS- 17077 Prisforslag skal sjekkes
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
     fvtill1    if   e           k disk    rename(vtilpfr:vtill1r)
6.20 fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
8.01 fvpfoi1    if   e           k disk    rename(vpfost:vpfoi1r)
      *
      * Definering av pamametre
      *
     d p_firm          s                   like(vpfirm)
     d p_vare          s                   like(vpvare)
5.70 d p_prgr          s                   like(vpprgr)
     d p_lety          s                   like(vplety)
     d p_dato          s                   like(vppdat)
     d p_time          s                   like(vtftim)
     d p_enhe          s                   like(vpenhe)
     d p_sapr          s                   like(vpsapr)
     d p_bupr          s                   like(vpbupr)
     d p_tipr          s                   like(vttip1)
      *
      * Definering av variable i nøkler
      *
     d vvprl1_vare     s                   like(vpvare)
6.20 d vvprl1_ldor     s                   like(vpldor)
5.70 d vvprl1_prgr     s                   like(vpprgr)
     d vvprl1_enhe     s                   like(vpenhe)
     d vvprl1_lety     s                   like(vplety)
     d vvenl2_vare     s                   like(vevare)
     d vvenl2_saen     s                   like(vesaen)
     d vtill1_vare     s                   like(vtvare)
5.70 d vtill1_prgr     s                   like(vtprgr)
     d vtill1_lety     s                   like(vtlety)
6.21 d vtill1_ogrp     s                   like(vtogrp)
6.21 d vtill1_hgrp     s                   like(vthgrp)
6.21 d vtill1_ugrp     s                   like(vtugrp)
6.21 d vtill1_ldor     s                   like(vtldor)
6.20 d vvarl1_vare     s                   like(vvvare)
8.01 d vpfoi1_fprg     s                   like(vpfprg)
8.01 d vpfoi1_fvar     s                   like(vpfvar)
      *
      * Definering av variable
      *
     d b_pris          s              1    inz(*off)
6.20 d b_inlr          s              1    inz(*off)
8.01 d b_prisf         s              1    inz(*off)
      *
     d w_firm          s                   like(vpfirm)
     d w_dato          s                   like(vppdat)
     d w_time          s                   like(vtftim)
6.20 d w_ldor          s                   like(vvldor)
6.20 d w_lage          s              2  0
6.21 d w_lety          s                   like(vplety)
6.21 d w_prgr          s                   like(vpprgr)
6.21 d w_pldo          s                   like(vpldor)
7.01 d s_tipr          s                   like(vttip1)
8.01 d w_pfor          s                   like(vpfor1)
8.01 d w_pfko          s                   like(vpfko1)
8.01 d w_pfin          s                   like(vpfin1)
8.01 d w_fsko          s                   like(vpfsko)
8.01 d w_anta          s              6  0
      *
      *
6.20 d                uds
7.01 d l_filg                931    933
6.20 d l_lage               1001   1002  0
      *
7.01 d u_s701          s              1    inz(*off)                            Finne beste pris
8.01 d u_801           s              1    inz(*off)
7.01  *
7.01  * Definering av eksterne prg
7.01  *
7.01   dcl-s co402_verdi1  char(1);
7.01   dcl-s co402_verdi2  char(2048);
7.01   DCL-PR CO402R EXTPGM('CO402R');
7.01     p_filgrp            char(3)     const;
7.01     p_firm              packed(3:0) const;
7.01     p_lager             packed(2:0) const;
7.01     p_nokkel            char(50)    const;
7.02     p_verdi1            char(1);
7.01     p_verdi2            char(2048);
7.01   END-PR;
7.01  *
8.01 C/Exec SQL
8.01 C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
8.01 C/End-Exec
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20 c                   eval      w_lage = l_lage
6.20  *
6.20  * Henter leverandør for vare og lager
6.20  *
6.20 c                   call      'VL721R  '
6.20 c                   parm                    w_firm
6.20 c                   parm                    p_vare
6.20 c                   parm                    w_lage
6.20 c                   parm                    w_ldor
6.20 c                   parm                    b_inlr
      *
6.20 c                   eval      vvarl1_vare = p_vare
6.20 c     vvarl1_key    chain     vvarl1
      *
      * Leser salgsenheter på vare, og henter prisinformasjon
      *
     c                   eval      b_pris = *off
      *
     c                   eval      vvenl2_vare = p_vare
     c                   eval      vvenl2_saen = 1
     c     vvenl2_key    setll     vvenl2
     c     vvenl2_key2   reade     vvenl2                                 90
     c                   dow       *in90 = *off and
     c                             b_pris = *off
      *
6.20  * 1. leverandør for lageret
6.20 c                   eval      vvprl1_ldor = w_ldor
6.20 c                   exsr      hent_pris
6.20 c                   if        b_pris = *off  and
6.20 c                             w_ldor <> vvldor
6.20  * 2. hovedleverandør
6.20 c                   eval      vvprl1_ldor = vvldor
6.20 c                   exsr      hent_pris
6.20 c                   if        b_pris = *off
6.20  * siste mulighet...
6.20 c                   eval      vvprl1_ldor = 0
6.20 c                   exsr      hent_pris
6.20 c                   endif
6.20 c                   endif
      *
     c     vvenl2_key2   reade     vvenl2                                 90
     c                   enddo
      *
8.01  * hvis prisforslag er aktiv så sjekkes dette først
8.01 c                   if        u_801 = *on
8.01 c                   exsr      sjekk_prisf
8.01 c                   endif
8.01  * sjekker evt. prisforslag
8.01 c                   if        w_pfor <> 0 and
8.01 c                             w_pfor < p_sapr
8.01 c                   eval      p_sapr = w_pfor
8.01 c                   endif
      *
      * Henter eventuell tilbudspris
      *
7.01 c                   eval      s_tipr = *hival
6.21 c                   eval      vtill1_ogrp = 0
6.21 c                   eval      vtill1_hgrp = 0
6.21 c                   eval      vtill1_ugrp = 0
6.21 c                   eval      vtill1_ldor = 0
     c                   eval      vtill1_vare = p_vare
5.70 c                   eval      vtill1_prgr = p_prgr
     c                   eval      vtill1_lety = p_lety
     c     vtill1_key    setll     vtill1
     c     vtill1_key    reade     vtill1                                 90
     c                   dow       *in90 = *off
     c                   if        w_dato >= vtfdat and
     c                             w_dato <= vttdat
     c                   if        w_dato <> vtfdat or
     c                             w_dato = vtfdat and
     c                             w_time >= vtftim
     c                   if        w_dato <> vttdat or
     c                             w_dato = vttdat and
     c                             w_time <= vtttim
      *
     c                   select
     c                   when      p_enhe = vtenh1
     c                   eval      p_tipr = vttip1
     c                   when      p_enhe = vtenh2
     c                   eval      p_tipr = vttip2
     c                   when      p_enhe = vtenh3
     c                   eval      p_tipr = vttip3
     c                   endsl
      *
7.01 c                   if        u_s701 = *on
7.01 c                   if        p_tipr < s_tipr
7.01 c                   eval      s_tipr = p_tipr
7.01 c                   endif
7.01 c                   else
     c                   leave
7.01 c                   endif
      *
     c                   endif
     c                   endif
     c                   endif
     c     vtill1_key    reade     vtill1                                 90
     c                   enddo
      *
      * Dersom tilbud ikke ble funnet på oppgitt leveringstype, letes det
      * etter tilbud på leveringstype 0
      *
     c                   if        p_tipr = 0 and
     c                             p_lety <> 0
      *
7.01 c                   eval      s_tipr = *hival
7.01 c                   eval      vtill1_vare = p_vare
5.70 c                   eval      vtill1_prgr = p_prgr
     c                   eval      vtill1_lety = 0
     c     vtill1_key    setll     vtill1
     c     vtill1_key    reade     vtill1                                 90
     c                   dow       *in90 = *off
     c                   if        w_dato >= vtfdat and
     c                             w_dato <= vttdat
     c                   if        w_dato <> vtfdat or
     c                             w_dato = vtfdat and
     c                             w_time >= vtftim
     c                   if        w_dato <> vttdat or
     c                             w_dato = vttdat and
     c                             w_time <= vtttim
      *
     c                   select
     c                   when      p_enhe = vtenh1
     c                   eval      p_tipr = vttip1
     c                   when      p_enhe = vtenh2
     c                   eval      p_tipr = vttip2
     c                   when      p_enhe = vtenh3
     c                   eval      p_tipr = vttip3
     c                   endsl
      *
7.01 c                   if        u_s701 = *on
7.01 c                   if        p_tipr < s_tipr
7.01 c                   eval      s_tipr = p_tipr
7.01 c                   endif
7.01 c                   else
     c                   leave
7.01 c                   endif
      *
     c                   endif
     c                   endif
     c                   endif
     c     vtill1_key    reade     vtill1                                 90
     c                   enddo
      *
     c                   endif
5.70  *
5.70  * Dersom tilbud ikke ble funnet på oppgitt prisgruppe, letes det
5.70  * etter tilbud på prisgruppe = blank
5.70  *
5.70 c                   if        p_tipr = 0 and
5.70 c                             p_prgr <> *blank
7.01 c                   eval      s_tipr = *hival
5.70 c                   eval      vtill1_vare = p_vare
5.70 c                   eval      vtill1_prgr = *blank
5.70 c                   eval      vtill1_lety = p_lety
5.70 c     vtill1_key    setll     vtill1
5.70 c     vtill1_key    reade     vtill1                                 90
5.70 c                   dow       *in90 = *off
5.70 c                   if        w_dato >= vtfdat and
5.70 c                             w_dato <= vttdat
5.70 c                   if        w_dato <> vtfdat or
5.70 c                             w_dato = vtfdat and
5.70 c                             w_time >= vtftim
5.70 c                   if        w_dato <> vttdat or
5.70 c                             w_dato = vttdat and
5.70 c                             w_time <= vtttim
5.70  *
5.70 c                   select
5.70 c                   when      p_enhe = vtenh1
5.70 c                   eval      p_tipr = vttip1
5.70 c                   when      p_enhe = vtenh2
5.70 c                   eval      p_tipr = vttip2
5.70 c                   when      p_enhe = vtenh3
5.70 c                   eval      p_tipr = vttip3
5.70 c                   endsl
5.70  *
7.01 c                   if        u_s701 = *on
7.01 c                   if        p_tipr < s_tipr
7.01 c                   eval      s_tipr = p_tipr
7.01 c                   endif
7.01 c                   else
     c                   leave
7.01 c                   endif
5.70  *
5.70 c                   endif
5.70 c                   endif
5.70 c                   endif
5.70 c     vtill1_key    reade     vtill1                                 90
5.70 c                   enddo
5.70  *
5.70  * Dersom tilbud ikke ble funnet på oppgitt leveringstype, letes det
5.70  * etter tilbud på leveringstype 0
5.70  *
5.70 c                   if        p_tipr = 0 and
5.70 c                             p_lety <> 0
5.70  *
7.01 c                   eval      s_tipr = *hival
5.70 c                   eval      vtill1_vare = p_vare
5.70 c                   eval      vtill1_prgr = *blank
5.70 c                   eval      vtill1_lety = 0
5.70 c     vtill1_key    setll     vtill1
5.70 c     vtill1_key    reade     vtill1                                 90
5.70 c                   dow       *in90 = *off
5.70 c                   if        w_dato >= vtfdat and
5.70 c                             w_dato <= vttdat
5.70 c                   if        w_dato <> vtfdat or
5.70 c                             w_dato = vtfdat and
5.70 c                             w_time >= vtftim
5.70 c                   if        w_dato <> vttdat or
5.70 c                             w_dato = vttdat and
5.70 c                             w_time <= vtttim
5.70  *
5.70 c                   select
5.70 c                   when      p_enhe = vtenh1
5.70 c                   eval      p_tipr = vttip1
5.70 c                   when      p_enhe = vtenh2
5.70 c                   eval      p_tipr = vttip2
5.70 c                   when      p_enhe = vtenh3
5.70 c                   eval      p_tipr = vttip3
5.70 c                   endsl
5.70  *
7.01 c                   if        u_s701 = *on
7.01 c                   if        p_tipr < s_tipr
7.01 c                   eval      s_tipr = p_tipr
7.01 c                   endif
7.01 c                   else
     c                   leave
7.01 c                   endif
5.70  *
5.70 c                   endif
5.70 c                   endif
5.70 c                   endif
5.70 c     vtill1_key    reade     vtill1                                 90
5.70 c                   enddo
5.70  *
5.70 c                   endif
5.70  *
5.70 c                   endif
      *
7.01 c                   if        u_s701 = *on
7.01 c                   if        s_tipr <> *hival
7.01 c                   eval      p_tipr = s_tipr
7.01 c                   endif
7.01 c                   endif
6.21  *
6.21  * Dersom tilbud ikke ble funnet på oppgitt vare/prisgruppe/leveringstupe
6.21  * sjekkes varegruppe / leverandør
6.22  *
6.22 c                   if        p_tipr = 0
6.22 c                   eval      vtill1_prgr = p_prgr
6.22 c                   exsr      tilb_sjekk
6.22 c                   if        p_tipr = 0 and
6.22 c                             p_prgr <> *blank
6.22 c                   eval      vtill1_prgr = *blank
6.22 c                   exsr      tilb_sjekk
6.22 c                   endif
6.22 c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for hendting av prisinformasjon for salgsenhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_pris     begsr
      *
     c                   eval      vvprl1_vare = p_vare
5.70 c                   eval      vvprl1_prgr = p_prgr
     c                   eval      vvprl1_enhe = veenhe
     c                   eval      vvprl1_lety = p_lety
     c     vvprl1_key    setll     vvprl1
     c     vvprl1_key    reade     vvprl1                                 91
     c                   dow       *in91 = *off
      *
     c                   eval      b_pris = *on
     c                   eval      p_enhe = vpenhe
     c                   eval      p_sapr = vpsapr
     c                   eval      p_bupr = vpbupr
      *
     c                   if        w_dato >= vppdat
     c                   leave
     c                   endif
      *
     c     vvprl1_key    reade     vvprl1                                 91
     c                   enddo
      *
      * Dersom pris ikke finnes for oppgitt leveringstype, hentes pris fra
      * leveringstype 0
      *
     c                   if        b_pris = *off and
     c                             p_lety <> 0
      *
     c                   eval      vvprl1_vare = p_vare
5.70 c                   eval      vvprl1_prgr = p_prgr
     c                   eval      vvprl1_enhe = veenhe
     c                   eval      vvprl1_lety = 0
     c     vvprl1_key    setll     vvprl1
     c     vvprl1_key    reade     vvprl1                                 91
     c                   dow       *in91 = *off
      *
     c                   eval      b_pris = *on
     c                   eval      p_enhe = vpenhe
     c                   eval      p_sapr = vpsapr
     c                   eval      p_bupr = vpbupr
      *
     c                   if        w_dato >= vppdat
     c                   leave
     c                   endif
      *
     c     vvprl1_key    reade     vvprl1                                 91
     c                   enddo
      *
     c                   endif
5.70  *
5.70  * Dersom pris ikke finnes for oppgitt prisgruppe, hentes pris fra
5.70  * prisgruppe = blank
5.70  *
5.70 c                   if        b_pris = *off and
5.70 c                             p_prgr <> *blank
5.70 c                   eval      vvprl1_vare = p_vare
5.70 c                   eval      vvprl1_prgr = *blank
5.70 c                   eval      vvprl1_enhe = veenhe
5.70 c                   eval      vvprl1_lety = p_lety
5.70 c     vvprl1_key    setll     vvprl1
5.70 c     vvprl1_key    reade     vvprl1                                 91
5.70 c                   dow       *in91 = *off
5.70  *
5.70 c                   eval      b_pris = *on
5.70 c                   eval      p_enhe = vpenhe
5.70 c                   eval      p_sapr = vpsapr
5.70 c                   eval      p_bupr = vpbupr
5.70  *
5.70 c                   if        w_dato >= vppdat
5.70 c                   leave
5.70 c                   endif
5.70  *
5.70 c     vvprl1_key    reade     vvprl1                                 91
5.70 c                   enddo
5.70  *
5.70  * Dersom pris ikke finnes for oppgitt leveringstype, hentes pris fra
5.70  * leveringstype 0
5.70  *
5.70 c                   if        b_pris = *off and
5.70 c                             p_lety <> 0
5.70  *
5.70 c                   eval      vvprl1_vare = p_vare
5.70 c                   eval      vvprl1_prgr = *blank
5.70 c                   eval      vvprl1_enhe = veenhe
5.70 c                   eval      vvprl1_lety = 0
5.70 c     vvprl1_key    setll     vvprl1
5.70 c     vvprl1_key    reade     vvprl1                                 91
5.70 c                   dow       *in91 = *off
5.70  *
5.70 c                   eval      b_pris = *on
5.70 c                   eval      p_enhe = vpenhe
5.70 c                   eval      p_sapr = vpsapr
5.70 c                   eval      p_bupr = vpbupr
5.70  *
5.70 c                   if        w_dato >= vppdat
5.70 c                   leave
5.70 c                   endif
5.70  *
5.70 c     vvprl1_key    reade     vvprl1                                 91
5.70 c                   enddo
5.70  *
5.70 c                   endif
5.70  *
5.70 c                   endif
6.12  *
6.21 c                   eval      w_lety = vplety
6.21 c                   eval      w_prgr = vpprgr
6.21 c                   eval      w_pldo = vpldor
5.70  *
     c                   endsr
6.20  *
6.22  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.22  * Subrutine for å hente sjekke om det finnes tilbud på varegr.nivå
6.22  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.22  *
6.22 c     tilb_sjekk    begsr
6.22  *
6.22 c                   if        p_tipr = 0
6.22  * 1. lety = p_lety, prgr = p_prgr
6.22 c                   eval      vtill1_ogrp = vvogrp
6.22 c                   eval      vtill1_hgrp = vvhgrp
6.22 c                   eval      vtill1_ugrp = vvugrp
6.22 c                   eval      vtill1_ldor = 0
6.22 c                   eval      vtill1_lety = p_lety
6.22 c                   eval      vtill1_vare = *blank
6.22 c                   exsr      hent_tilb
6.22 c                   endif
6.22 c                   if        p_tipr = 0  and
6.22 c                             p_lety <> 0
6.22  * 2. lety = 0       prgr = p_prgr
6.22 c                   eval      vtill1_lety = 0
6.22 c                   exsr      hent_tilb
6.22 c                   endif
6.22  *  overgruppe,hovedgruppe
6.22 c                   if        p_tipr = 0
c.24  * 1. lety = p_lety, prgr = p_prgr
6.22 c                   eval      vtill1_ugrp = 0
6.22 c                   eval      vtill1_lety = p_lety
6.22 c                   exsr      hent_tilb
6.22 c                   endif
6.22 c                   if        p_tipr = 0  and
6.22 c                             p_lety <> 0
6.22  * 2. lety = 0       prgr = p_prgr
6.22 c                   eval      vtill1_lety = 0
6.22 c                   exsr      hent_tilb
6.22 c                   endif
6.22  *  overgruppe
6.22 c                   if        p_tipr = 0
c.24  * 1. lety = p_lety, prgr = p_prgr
6.22 c                   eval      vtill1_hgrp = 0
6.22 c                   eval      vtill1_lety = p_lety
6.22 c                   exsr      hent_tilb
6.22 c                   endif
6.22 c                   if        p_tipr = 0  and
6.22 c                             p_lety <> 0
6.22  * 2. lety = 0       prgr = p_prgr
6.22 c                   eval      vtill1_lety = 0
6.22 c                   exsr      hent_tilb
6.22 c                   endif
6.22  *  overgruppe,hovedgruppe,undergruppe, leverandør
6.22 c                   if        p_tipr = 0
c.24  * 1. lety = p_lety, prgr = p_prgr
6.22 c                   eval      vtill1_ogrp = vvogrp
6.22 c                   eval      vtill1_hgrp = vvhgrp
6.22 c                   eval      vtill1_ugrp = vvugrp
6.22 c                   eval      vtill1_ldor = w_pldo
6.22 c                   eval      vtill1_lety = p_lety
6.22 c                   exsr      hent_tilb
6.22 c                   endif
6.22 c                   if        p_tipr = 0  and
6.22 c                             p_lety <> 0
6.22  * 2. lety = 0       prgr = p_prgr
6.22 c                   eval      vtill1_lety = 0
6.22 c                   exsr      hent_tilb
6.22 c                   endif
6.22  *  overgruppe,hovedgruppe              leverandør
6.22 c                   if        p_tipr = 0
c.24  * 1. lety = p_lety, prgr = p_prgr
6.22 c                   eval      vtill1_ugrp = 0
6.22 c                   eval      vtill1_lety = p_lety
6.22 c                   exsr      hent_tilb
6.22 c                   endif
6.22 c                   if        p_tipr = 0  and
6.22 c                             p_lety <> 0
6.22  * 2. lety = 0       prgr = p_prgr
6.22 c                   eval      vtill1_lety = 0
6.22 c                   exsr      hent_tilb
6.22 c                   endif
6.22  *  overgruppe                          leverandør
6.22 c                   if        p_tipr = 0
c.24  * 1. lety = p_lety, prgr = p_prgr
6.22 c                   eval      vtill1_hgrp = 0
6.22 c                   eval      vtill1_lety = p_lety
6.22 c                   exsr      hent_tilb
6.22 c                   endif
6.22 c                   if        p_tipr = 0   and
6.22 c                             p_lety <> 0
6.22  * 2. lety = 0       prgr = p_prgr
6.22 c                   eval      vtill1_lety = 0
6.22 c                   exsr      hent_tilb
6.22 c                   endif
6.22  *  leverandør
6.22 c                   if        p_tipr = 0
c.24  * 1. lety = p_lety, prgr = p_prgr
6.22 c                   eval      vtill1_ogrp = 0
6.22 c                   eval      vtill1_lety = p_lety
6.22 c                   exsr      hent_tilb
6.22 c                   endif
6.22 c                   if        p_tipr = 0   and
6.22 c                             p_lety <> 0
6.22  * 2. lety = 0       prgr = p_prgr
6.22 c                   eval      vtill1_lety = 0
6.22 c                   exsr      hent_tilb
6.22 c                   endif
6.22 c                   endsr
6.22  *
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  * Subrutine for å hente tilbudspris på varegruppe/leverandørnivå
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     hent_tilb     begsr
6.20  *
6.21 c     vtill1_key    setll     vtill1
6.21 c     vtill1_key    reade     vtill1                                 90
6.21 c                   dow       *in90 = *off
6.21 c                   if        w_dato >= vtfdat and
6.21 c                             w_dato <= vttdat
6.21 c                   if        w_dato <> vtfdat or
6.21 c                             w_dato = vtfdat and
6.21 c                             w_time >= vtftim
6.21 c                   if        w_dato <> vttdat or
6.21 c                             w_dato = vttdat and
6.21 c                             w_time <= vtttim
6.21  *
6.21 c                   eval(h)   p_tipr = p_sapr -
6.21 c                                      (p_sapr * vtpro1)/100
6.21  *
6.21 c                   leave
6.21  *
6.21 c                   endif
6.21 c                   endif
6.21 c                   endif
6.21 c     vtill1_key    reade     vtill1                                 90
6.21 c                   enddo
6.21  *
6.21 c                   endsr
6.21  *
8.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.01  * Subrutine for å sjekke om det finnes prisforslag
8.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.01  *
8.01 c     sjekk_prisf   begsr
8.01 c                   eval      b_prisf = *off
8.01 c                   eval      w_pfor = 0
8.01 c                   eval      w_pfko = 0
8.01 c                   eval      w_pfin = 0
8.01  * sjekk om en forekomst finnes
8.01 c                   eval      w_anta = 0
8.01 c/exec sql
8.01 c+ select count(*)
8.01 i+ into   :w_anta
8.01 c+ from   vpfost
8.01 c+ where  vpfvar = :p_vare and
8.01 c+        vpffir = :w_firm
8.01 c/end-exec
8.01 c                   if        w_anta = 0
8.01 c                   leavesr
8.01 c                   endif
8.01  *
8.01 c                   eval      vpfoi1_fvar = p_vare
8.01 c                   eval      vpfoi1_fprg = p_prgr
8.01 c                   exsr      prisf_matr
8.01  *
8.01  * Dersom pris ikke finnes for oppgitt prisgruppe, hentes pris fra
8.01  * prisgruppe blank
8.01  *
8.01 c                   if        w_pfor = 0 and
8.01 c                             p_prgr <> *blank
8.01 c                   eval      vpfoi1_fvar = p_vare
8.01 c                   eval      vpfoi1_fprg = *blank
8.01 c                   exsr      prisf_matr
8.01 c                   endif
8.01  *
8.01  *  Indikere om vi fant pris på kundeklubb matrisen eller ikke
8.01 c                   if        w_pfor <> 0
8.01 c                   eval      b_prisf = *on
8.01 c                   endif
8.01 c
8.01 c                   endsr
8.01  *---------------------------------------------------------------------
8.01  * Subrutine for å sjekke om nivået har  prisforslag
8.01  *---------------------------------------------------------------------
8.01 c     prisf_matr    begsr
8.01  *
8.01 c     vpfoi1_key    setll     vpfoi1
8.01 c     vpfoi1_key    reade     vpfoi1
8.01 c                   dow       not %eof
8.01 c                   if        w_dato >= vpffda and
8.01 c                             w_dato <= vpftda
8.01 c                   if        w_dato <> vpffda or
8.01 c                             w_dato = vpffda
8.01 c                   if        w_dato <> vpftda or
8.01 c                             w_dato = vpftda
8.01  *
8.01 c                   eval      w_fsko = vpfsko
8.01 c                   select
8.01 c                   when      p_enhe = vpfen1
8.01 c                   eval      w_pfor = vpfor1
8.01 c                   if        vpfko1 <> 0
8.01 c                   eval      w_pfko = vpfko1
8.01 c                   endif
8.01 c                   if        vpfin1 <> 0
8.01 c                   eval      w_pfin = vpfin1
8.01 c                   endif
8.01 c                   when      p_enhe = vpfen2
8.01 c                   eval      w_pfor = vpfor2
8.01 c                   if        vpfko2 <> 0
8.01 c                   eval      w_pfko = vpfko2
8.01 c                   endif
8.01 c                   if        vpfin2 <> 0
8.01 c                   eval      w_pfin = vpfin2
8.01 c                   endif
8.01 c                   when      p_enhe = vpfen3
8.01 c                   eval      w_pfor = vpfor3
8.01 c                   if        vpfko3 <> 0
8.01 c                   eval      w_pfko = vpfko3
8.01 c                   endif
8.01 c                   if        vpfin3 <> 0
8.01 c                   eval      w_pfin = vpfin3
8.01 c                   endif
8.01 c                   endsl
8.01  *
8.01 c                   leavesr
8.01  *
8.01 c                   endif
8.01 c                   endif
8.01 c                   endif
8.01 c     vpfoi1_key    reade     vpfoi1
8.01 c                   enddo
8.01  *
8.01 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
5.70 c                   parm                    p_prgr
     c                   parm                    p_lety
     c                   parm                    p_dato
     c                   parm                    p_time
     c                   parm                    p_enhe
     c                   parm                    p_sapr
     c                   parm                    p_bupr
     c                   parm                    p_tipr
      *
      * Key for les på vare-pris-register
      *
     c     vvprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprl1_vare
6.20 c                   kfld                    vvprl1_ldor
5.70 c                   kfld                    vvprl1_prgr
     c                   kfld                    vvprl1_enhe
     c                   kfld                    vvprl1_lety
      *
      * Key for les på vare-enhet-register
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
     c                   kfld                    vvenl2_saen
      *
     c     vvenl2_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
      *
      * Key for les på tilbuds-register
      *
     c     vtill1_key    klist
     c                   kfld                    w_firm
6.21 c                   kfld                    vtill1_ogrp
6.21 c                   kfld                    vtill1_hgrp
6.21 c                   kfld                    vtill1_ugrp
6.21 c                   kfld                    vtill1_ldor
     c                   kfld                    vtill1_vare
5.70 c                   kfld                    vtill1_prgr
     c                   kfld                    vtill1_lety
6.20  *
6.20  * Key for les på vare
6.20  *
6.20 c     vvarl1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    vvarl1_vare
8.01  *
8.01  * Key for les prisforslag
8.01  *
8.01 c     vpfoi1_key    klist
8.01 c                   kfld                    w_firm
8.01 c                   kfld                    vpfoi1_fprg
8.01 c                   kfld                    vpfoi1_fvar
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
      * Initierer dato og tidspunkt
      *
     c                   eval      w_dato = p_dato
     c                   eval      w_time = p_time
      *
     c                   if        w_dato = *loval
     c                   time                    w_dato
     c                   endif
      *
     c                   if        w_time = *loval
     c                   time                    w_time
     c                   endif
      *
      * Initierer output-parametre
      *
     c                   eval      p_enhe = *blank
     c                   eval      p_sapr = 0
     c                   eval      p_bupr = 0
     c                   eval      p_tipr = 0
7.01  *
7.01  * Bruke beste pris uansett tilbud
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'BESTE_PRIS':
7.01   co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_s701 = *on;
7.01   endif;
7.02  *
8.01  * Endring 8.01 Prisforslag
8.01  *
8.01   CallP CO402R(l_filg:w_firm:0:'PRISFORSLAG':
8.01                    co402_verdi1:co402_verdi2);
8.01   if co402_verdi1 = '1';
8.01     u_801 = *on;
8.01   endif;
      *
     c                   endsr
