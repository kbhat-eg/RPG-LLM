     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : jl110R
      * Beskrivelse . . : Leverandør, kontaktpersoner -  vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringbeskrrivelse
      * ----- ------ ---- -------------------------------------------------
      *       200809 bof  Nytt program
6.30  *       120918 BKV  Byttet felt i visning
7.xx  * 7.00  040316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fjlkpl1    if   e           k disk    rename(jlkppfr:jlkpl1r)
     fjlkplr    if   e           k disk    rename(jlkppfr:jlkplrr)
     fjlkplu    uf a e           k disk    rename(jlkppfr:jlkplur)
      *
     fjl110d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d jlevl1_ldor     s                   like(jlldor)
     d jlkpl1_ptel     s                   like(jkptel)
     d jlkplr_ptel     s                   like(jkptel)
     d jlkplu_ptel     s                   like(jkptel)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_ldor          s                   like(jlldor)
      *
     d p_ldor          s                   like(jlldor)
     d p_valg          s              1  0
      *
     d s_ptel          s                   like(jkptel)
      *
      * Definering av konstanter
      *
7.xx d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   if        p_valg <> 5
     c                   exsr      xc1bld
     c                   endif
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2ptel <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      jlkpl1_ptel = b1ptel
     c     jlkpl1_key    setll     jlkpl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Type
      *
     c                   if        b2ptel <> 0
     c                   eval      w_seqe = *blank
     c                   eval      jlkpl1_ptel = b2ptel
     c     jlkpl1_key    setll     jlkpl1
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2ptel = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1ptel = b1ptel
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   if        p_valg <> 5
     c                   eval      k1ptel = b1ptel
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   if        p_valg <> 5
     c                   eval      d1ptel = b1ptel
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1ptel = b1ptel
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1ptel = b2ptel
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1ptel = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      jlkplr_ptel = c1ptel
     c     jlkplr_key    chain     jlkplr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1ptel = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2ptel = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (c2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      jlkplr_ptel = c1ptel
     c     jlkplr_key    chain     jlkplr
      *
     c                   if        %found
     c                   eval      c2pnav = jkpnav
     c                   eval      c2pttl = jkpttl
     c                   eval      c2ptlf = jkptlf
     c                   eval      c2pmob = jkpmob
     c                   eval      c2pfax = jkpfax
     c                   eval      c2pmai = jkpmai
     c                   eval      c2prol = jkprol
     c                   eval      c2pusr = jkpusr
     c                   if        jkpoda <> *loval
     c     *dmy          move      jkpoda        c2poda
     c                   else
     c                   eval      c2poda = 0
     c                   endif
     c                   if        jkpeda <> *loval
     c     *dmy          move      jkpeda        c2peda
     c                   else
     c                   eval      c2peda = 0
     c                   endif
     c                   else
     c                   eval      c2pnav = *blank
     c                   eval      c2pttl = *blank
     c                   eval      c2ptlf = *blank
     c                   eval      c2pmob = *blank
     c                   eval      c2pfax = *blank
     c                   eval      c2pmai = *blank
     c                   eval      c2prol = *blank
     c                   eval      c2pusr = *blank
     c                   eval      c2poda = 0
     c                   eval      c2peda = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1ptel = k1ptel
     c                   endif
      *
     c                   if        b1valg = 5 or p_valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2pld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2pnav = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   jlkplur
      *
     c                   eval      jlkplu_ptel = c1ptel
     c     jlkplu_key    chain     jlkplur
      *
     c                   eval      jkpnav = c2pnav
     c                   eval      jkpttl = c2pttl
     c                   eval      jkptlf = c2ptlf
     c                   eval      jkpmob = c2pmob
     c                   eval      jkpfax = c2pfax
     c                   eval      jkpmai = c2pmai
     c                   eval      jkprol = c2prol
      *
     c                   time                    jkpeda
     c                   time                    jkpeti
     c                   eval      jkpusr = l_user
      *
     c                   if        %found
     c                   update    jlkplur
     c                   else
     c                   eval      jkpldo = w_ldor
     c                   eval      jkptel = c1ptel
     c                   time                    jkpoda
     c                   write     jlkplur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1pnav = jkpnav
6.30 c*                   eval      b1pttl = jkpttl
6.30 c*                   eval      b1ptlf = jkptlf
     c                   eval      b1pmob = jkpmob
6.30 c                   eval      b1prol = jkprol
6.30 c                   eval      b1pmai = jkpmai
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2pld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      jlkplr_ptel = d1ptel
     c     jlkplr_key    chain     jlkplr
      *
     c                   eval      d1pnav = jkpnav
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      jlkplu_ptel = d1ptel
     c     jlkplu_key    chain     jlkplur
      *
     c                   delete    jlkplur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_ptel = k1ptel
      *
      * Fyller kopierings-bildet
      *
     c                   eval      jlkplr_ptel = k1ptel
     c     jlkplr_key    chain     jlkplr
      *
     c     k1taga        tag
      *
     c                   eval      k2pnav = jkpnav
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Validering
      *
     c                   if        k1ptel = 0
     c                   eval      *in31  = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      jlkplr_ptel = k1ptel
     c     jlkplr_key    chain     jlkplr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1ptel = s_ptel
     c                   exsr      xc2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      jlkpl1
      *
     c                   if        %eof or
     c                             jkpldo <> w_ldor
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      jlkpl1_ptel = jkptel
      *
     c                   eval      b1valg = 0
     c                   eval      b1ptel = jkptel
     c                   eval      b1pnav = jkpnav
6.30 c*                   eval      b1pttl = jkpttl
6.30 c*                   eval      b1ptlf = jkptlf
     c                   eval      b1pmob = jkpmob
6.30 c                   eval      b1prol = jkprol
6.30 c                   eval      b1pmai = jkpmai
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     jlkpl1_key    setgt     jlkpl1
     c                   readp     jlkpl1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     jlkpl1
      *
     c                   if        %eof or
     c                             jkpldo <> w_ldor
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      jlkpl1_ptel = jkptel
      *
     c                   enddo
      *
     c                   if        %eof
     c     jlkpl1_key    setll     jlkpl1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_ldor
     c                   parm                    p_valg
      * Key for posisjonering på rabattgruppe
      *
     c     jlevl1_key    klist
     c                   kfld                    w_ldor
      *
      * Key for posisjonering på rabattgruppe
      *
     c     jlkpl1_key    klist
     c                   kfld                    w_ldor
     c                   kfld                    jlkpl1_ptel
      *
      * Key for les på rabattgruppe
      *
     c     jlkplr_key    klist
     c                   kfld                    w_ldor
     c                   kfld                    jlkplr_ptel
      *
      * Key for oppdatering av rabattgruppe
      *
     c     jlkplu_key    klist
     c                   kfld                    w_ldor
     c                   kfld                    jlkplu_ptel
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_ldor = p_ldor
      *
     c                   eval      *in22 = *on
      *
     c     jlevl1_key    chain     jlevl1
      *
     c                   eval      b2ldor = jlldor
     c                   eval      b2lnav = jlnavn
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      jlkpl1_ptel = 0
     c     jlkpl1_key    setll     jlkpl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
