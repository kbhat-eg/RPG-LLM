     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RL400R                                              *
      * Beskrivelse . . : Sletting av leverandører - parameter                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ----------------------------------------------------*
T5.00 * V5.00     230403  Lev. lagt inn etter xx.xx.xx blir ikke slettet HFL  *
      *                                                                       *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fausrl1    if   e           k disk
     frl400d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d s               s              1  0
     d w_firm          s              3  0
     d w_in03          s              1
     d w_type          s              1
     d w_edat          s               d   datfmt(*iso)
T5.00d w_ndat          s               d   datfmt(*iso)
      *
     c     dummy         tag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av felter                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * A2BLD  - A2 Bilde for valg av parametre
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A2BLD Behandle bilde for valg av utskriftskriteria              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     a2taga        tag
      *
      * Bare data
      *
     c                   exfmt     a2bld
      *
     c                   eval      *in31 = *off
     c                   eval      *in32 = *off
      *
      * Gå ut
      *
     c                   if        *inkc = *on                                  .....
     c                   move      '1'           w_in03                             .
     c                   goto      xslutt                                           .
     c                   endif                                                  .....
      *
      * Sjekke om "Eldre enn dato" er gyldig
      *
      *
     c     *dmy          test(d)                 a2edat                 31
     c                   if        *in31 = *on                                           .....
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
     c     *dmy          move      a2edat        w_edat                                      .
      *
T5.00 * Sjekke om "Leverandører lagt inn etter" er gyldig dato
      *
T5.00c                   eval      *in33 = *off                                          .....
T5.00c                   if        a2ndat = *zeros
T5.00c                   move      *hival        w_ndat                                      .
T5.00c                   else                                                                .
T5.00c     *dmy          test(d)                 a2ndat                 33
T5.00c                   if        *in33 = *on                                           .....
T5.00c                   goto      a2taga                                                    .
T5.00c                   endif                                                           .....
      *
T5.00c     *dmy          move      a2ndat        w_ndat                                      .
T5.00c                   endif                                                           .....
      *
      *  Kontroll type utskrift
      *
     c                   if        a2type <> 'J'                                         .....
     c                             and a2type <> 'N'                                         .
     c                   move      *on           *in32                          Pos.cur.     .
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
     c                   move      a2type        w_type                                      .
      *
      * Call sletterutine
      *
     c                   call      'RL400C'
     c                   parm                    w_in03
     c                   parm                    w_type
     c                   parm                    w_edat
T5.00c                   parm                    w_ndat
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      /space 3
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      *
      * Legge inn firmanummer
      *
     c                   move      l_firm        w_firm
     c                   move      'J'           a2type
     c                   eval      w_in03 = ' '
      *
     c                   endsr
