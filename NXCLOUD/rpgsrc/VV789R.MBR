     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VV789R
      * Beskrivelse . . : Vare, kopiering til annen filgruppe - param.
      *                   Ut fra csv file
      * NB! d_dumm har som oppgave å forhindre "forsøpling" av datastruktur
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       040708 bof  Nytt program
6.31  *       251018 bof  Lagt på valg av varetype
7.xx  * 7.00  280219 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     fvv789d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_fgrp                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Datastruktur for parameterliste -ut
      *
     d                 ds
     d d_list                       260
     d  d_fgrp                        3    overlay(d_list:1)
     d  d_firm                        3  0 overlay(d_list:4)
     d  d_ldor                        6  0 overlay(d_list:7)
     d  d_ogrp                        2  0 overlay(d_list:13)
     d  d_hgrp                        2  0 overlay(d_list:15)
     d  d_ugrp                        3  0 overlay(d_list:17)
     d  d_var0                       15    overlay(d_list:20)
     d  d_var1                       15    overlay(d_list:35)
     d  d_var2                       15    overlay(d_list:50)
     d  d_var3                       15    overlay(d_list:65)
     d  d_var4                       15    overlay(d_list:80)
     d  d_var5                       15    overlay(d_list:95)
     d  d_var6                       15    overlay(d_list:110)
     d  d_var7                       15    overlay(d_list:125)
     d  d_var8                       15    overlay(d_list:140)
     d  d_var9                       15    overlay(d_list:155)
     d  d_odat                         d   overlay(d_list:170) datfmt(*iso)
     d  d_vtyp                        1    overlay(d_list:180)
     d  d_fgt0                        3    overlay(d_list:181)
     d  d_fit0                        3  0 overlay(d_list:184)
     d  d_fgt1                        3    overlay(d_list:187)
     d  d_fit1                        3  0 overlay(d_list:190)
     d  d_fgt2                        3    overlay(d_list:193)
     d  d_fit2                        3  0 overlay(d_list:196)
     d  d_fgt3                        3    overlay(d_list:199)
     d  d_fit3                        3  0 overlay(d_list:202)
     d  d_fgt4                        3    overlay(d_list:205)
     d  d_fit4                        3  0 overlay(d_list:208)
     d  d_fgt5                        3    overlay(d_list:211)
     d  d_fit5                        3  0 overlay(d_list:214)
     d  d_fgt6                        3    overlay(d_list:217)
     d  d_fit6                        3  0 overlay(d_list:220)
     d  d_fgt7                        3    overlay(d_list:223)
     d  d_fit7                        3  0 overlay(d_list:226)
     d  d_fgt8                        3    overlay(d_list:229)
     d  d_fit8                        3  0 overlay(d_list:232)
     d  d_fgt9                        3    overlay(d_list:235)
     d  d_fit9                        3  0 overlay(d_list:238)
6.32 d  d_file                        1    overlay(d_list:241)
     d  d_dumm                        1    overlay(d_list:260)
      *
      * Definering av variable i nøkler
      *
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s              3  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc = *on or *inkl = *on
     c                   goto      avslutt
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
      * Kaller program for henting av data, avbryter dersom feil
      *
     c                   eval      b_feil = *off
     c                   call      'VV789BC'
     c                   parm                    b1file
     c                   parm                    b1mapp
     c                   parm                    b_feil
      *
     c                   if        b_feil = *on
     c                   exfmt     b1msg
     c                   goto      b1taga
     c                   endif
      *
      * Flytter skjermfelter til datastruktur -ut
      *
     c                   eval      d_fgrp = l_fgrp
     c                   eval      d_firm = l_firm
     c                   eval      d_ldor = 0
     c                   eval      d_ogrp = 0
     c                   eval      d_hgrp = 0
     c                   eval      d_ugrp = 0
     c                   eval      d_var0 = *blank
     c                   eval      d_var1 = *blank
     c                   eval      d_var2 = *blank
     c                   eval      d_var3 = *blank
     c                   eval      d_var4 = *blank
     c                   eval      d_var5 = *blank
     c                   eval      d_var6 = *blank
     c                   eval      d_var7 = *blank
     c                   eval      d_var8 = *blank
     c                   eval      d_var9 = *blank
     c                   eval      d_odat = *loval
6.31 c                   eval      d_vtyp = b1vtyp
     c                   eval      d_fgt0 = b1fgt0
     c                   eval      d_fit0 = b1fit0
     c                   eval      d_fgt1 = b1fgt1
     c                   eval      d_fit1 = b1fit1
     c                   eval      d_fgt2 = b1fgt2
     c                   eval      d_fit2 = b1fit2
     c                   eval      d_fgt3 = b1fgt3
     c                   eval      d_fit3 = b1fit3
     c                   eval      d_fgt4 = b1fgt4
     c                   eval      d_fit4 = b1fit4
     c                   eval      d_fgt5 = b1fgt5
     c                   eval      d_fit5 = b1fit5
     c                   eval      d_fgt6 = b1fgt6
     c                   eval      d_fit6 = b1fit6
     c                   eval      d_fgt7 = b1fgt7
     c                   eval      d_fit7 = b1fit7
     c                   eval      d_fgt8 = b1fgt8
     c                   eval      d_fit8 = b1fit8
     c                   eval      d_fgt9 = b1fgt9
     c                   eval      d_fit9 = b1fit9
     c                   eval      d_dumm = '*'
     c                   eval      d_file = '1'
      *
      *
      * Kaller overføringsprogram
      *
     c                   call      'VV789C'
     c                   parm                    d_list
      *
     c                   exfmt     b1sbm
      *
     c                   eval      b1file = *blank
     c                   eval      b1mapp = *Blank
      *
     c                   goto      b1taga
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   move      *on           *inlr
     c                   return
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
      *
      * file må oppgis
      *
     c                   if        b1file = *blank
     c                   eval      *in31 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * mappe må oppgis
      *
     c                   if        b1mapp= *blank
     c                   eval      *in32 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
6.31  *
6.31 c                   if        b1vtyp <> *blank and
6.31 c                             b1vtyp <> 'L' and
6.31 c                             b1vtyp <> 'S'
6.31 c                   eval      *in48 = *on
6.31 c                   eval      b_feil = *on
6.31 c                   leavesr
6.31 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Henter firma fra LDA
      *
     c                   move      l_firm        w_firm
      *
     c                   endsr
