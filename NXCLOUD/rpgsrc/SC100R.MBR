      /title  ASSTAT Vedlikehold av budsjett-nivå-register
     h datedit(*dmy.) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASSTAT
      * Program . . . . : SC100R
      * Beskrivelse . . : Vedlikehold av budsjett-nivå-register
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * V5.40 070405 KOB  Nytt program
      * V5.50 301006 KOB  Kode 13 endret fra ordre- til leveringstype
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     fsc100d    cf   e             workstn sfile(b1sfl:srrn01)
      *
     fslnil1    if   e           k disk    rename(slnipfr:slnil1r)
     fslnilu    uf a e           k disk    rename(slnipfr:slnilur)
      *
     d mld             s             11    dim(4) ctdata perrcd(1)
     d rpt             s             15    dim(60) ctdata perrcd(1)
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d wwkode          ds            12
     d wwkode_11                      1  0
     d wwkode_12                      1  0
     d wwkode_13                      1  0
     d wwkode_21                      1  0
     d wwkode_22                      1  0
     d wwkode_23                      1  0
     d wwkode_24                      1  0
     d wwkode_25                      1  0
     d wwkode_31                      1  0
     d wwkode_32                      1  0
     d wwkode_33                      1  0
     d wwkode_34                      1  0
      *
     d                 ds
     d wsparm                        64
     d  wsfirm                        3  0 overlay(wsparm:01)
     d  wshhaa                        4  0 overlay(wsparm:04)
     d  wsvers                        2    overlay(wsparm:08)
     d  wsakod                       12    overlay(wsparm:10)
     d  wsaabb                        1    overlay(wsparm:22)
     d  wskode                       12    overlay(wsparm:23)
     d  wsfile                       10    overlay(wsparm:35)
     d  wsbibl                       10    overlay(wsparm:45)
      *
     d spge01          s              3  0
     d srrn01          s              4  0
     d sfrn01          s                   like(srrn01)
     d ssrn01          s                   like(srrn01)
      *
     d wwfirm          s                   like(smfirm)
      *
     d r1              s              2  0
     d r2              s                   like(r1)
     d r3              s                   like(r1)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * B2BLD Behandle åpningsbildet med subfile                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      xdsp01
      *
      * Avslutt program
      *
     c     *inkc         cabeq     *on           xslutt
      *
      * Forny
      *
     c                   if        *inke = *on
     c                   if        sfrn01 <> *zero
     c     sfrn01        chain     b1sfl                              60
     c                   endif
      *
     c                   move      b1kode        wwkode
     c     key01         setll     slnil1r
     c                   exsr      xclr01
     c                   exsr      xcrt01
     c                   goto      b2tagb
     c                   endif
      *
      * Opprette ny record
      *
     c                   if        *inkf = *on
     c                   goto      c1taga
     c                   endif
      *
      * Genering av akkumulator-register
      *
     c                   if        *inkk = *on
     c                   move      *blank        u0kode
     c                   exsr      xu0win
     c                   endif
      *
      * Roll
      *
     c                   if        *in10 = *on
     c                   exsr      xcrt01
     c                   goto      b2tagb
     c                   endif
      *
      * Home
      *
     c                   if        *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endif
      *
      * Posisjoner
      * startverdi
      *
     c                   if        b2kode <> *zero
     c                   move      b2kode        wwkode
     c     key01         setll     slnil1r
     c                   exsr      xclr01
     c                   exsr      xcrt01
     c                   eval      b2kode = *zero
     c                   goto      b2tagb
     c                   endif
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Behandling av subfile                                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      * Sjekk om valg er foretatt i subfile
      *
     c                   do        ssrn01
     c                   readc     b1sfl                                  16
     c                   if        *in16 = *on
     c                   leave
     c                   endif
      *
      * Endre post
      *
     c                   if        b1valg = ' 2'
     c                             or b1valg = '2 '
     c                   move      b1kode        c2kode
     c                   move      b1kode        wwkode
     c                   exsr      xc2bld
     c                   iter
     c                   endif
      *
      * Kopiere
      *
     c                   if        b1valg = ' 3'
     c                             or b1valg = '3 '
     c                   move      b1kode        wwkode
     c                   exsr      xk1win
     c                   iter
     c                   endif
      *
      * Slette post
      *
     c                   if        b1valg = ' 4'
     c                             or b1valg = '4 '
     c                   if        b1endr = *blank
     c                   move      b1kode        wwkode
     c                   exsr      xd1win
     c                   iter
     c                   endif
     c                   endif
      *
      * Vise
      *
     c                   if        b1valg = ' 5'
     c                             or b1valg = '5 '
     c                   move      b1kode        f0kode
     c                   move      b1kode        wwkode
     c                   exsr      xf0win
     c                   iter
     c                   endif
      *
      * Utskrift
      *
     c                   if        b1valg = ' 6'
     c                             or b1valg = '6 '
     c                   move      b1kode        p0kode
     c                   move      b1kode        wwkode
     c                   exsr      xp0win
     c                   iter
     c                   endif
      *
      * Budsjettering
      *
     c                   if        b1valg = ' 8'
     c                             or b1valg = '8 '
     c                   move      b1kode        r0kode
     c                   move      b1kode        wwkode
     c                   exsr      xr0win
     c                   iter
     c                   endif
      *
      * Generering
      *
     c                   if        b1valg = '60'
     c                             or b1valg = '60'
     c                   move      b1kode        t0kode
     c                   move      b1kode        wwkode
     c                   exsr      xt0win
     c                   iter
     c                   endif
      *
      * Hent fra fil
      *
     c                   if        b1valg = '62'
     c                             or b1valg = '62'
     c                   move      b1kode        v0kode
     c                   move      b1kode        wwkode
     c                   exsr      xv0win
     c                   iter
     c                   endif
      *
      * Overfør til økonomi
      *
     c                   if        b1valg = '78'
     c                             or b1valg = '78'
     c                   move      b1kode        v1kode
     c                   move      b1kode        wwkode
     c                   exsr      xv1win
     c                   iter
     c                   endif
      *
      * Akkumlering
      *
     c                   if        b1valg = '99'
     c                             or b1valg = '99'
     c                   move      b1kode        u0kode
     c                   move      b1kode        wwkode
     c                   exsr      xu0win
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   goto      b2taga
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * C1BLD Behandle åpningsbildet for ny post                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     c1taga        tag
      *
      * Nullstiller felter som benyttes i bilde
      *
     c                   eval      c1sr11 = *zero
     c                   eval      c1sr12 = *zero
     c                   eval      c1sr13 = *zero
     c                   eval      c1sr21 = *zero
     c                   eval      c1sr22 = *zero
     c                   eval      c1sr23 = *zero
     c                   eval      c1sr24 = *zero
     c                   eval      c1sr25 = *zero
     c                   eval      c1sr31 = *zero
     c                   eval      c1sr32 = *zero
     c                   eval      c1sr33 = *zero
     c                   eval      c1sr34 = *zero
      *
      * Vis skjermbilde
      *
     c                   exfmt     c1bld
      *
     c     *inkc         cabeq     *on           b2taga
     c     *inkl         cabeq     *on           b2taga
      *
     c                   move      c1sr11        wwkode_11
     c                   move      c1sr12        wwkode_12
     c                   move      c1sr13        wwkode_13
     c                   move      c1sr21        wwkode_21
     c                   move      c1sr22        wwkode_22
     c                   move      c1sr23        wwkode_23
     c                   move      c1sr24        wwkode_24
     c                   move      c1sr25        wwkode_25
     c                   move      c1sr31        wwkode_31
     c                   move      c1sr32        wwkode_32
     c                   move      c1sr33        wwkode_33
     c                   move      c1sr34        wwkode_34
      *
     c                   exsr      xc2bld
      *
     c                   goto      b2taga
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * C2BLD Behandle bilde for ny - endring - vise                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xc2bld        begsr
      *
      * Flytter inn informasjon i bilde-felter
      *
     c                   eval      c2kode = wwkode
      *
      * Nullstiller felter som benyttes i bilde
      *
     c                   eval      c2komm = *blank
     c                   eval      c2endr = *blank
     c                   eval      c2akkk = 'N'
     c                   eval      c2akod = *blank
     c                   eval      c2stx1 = *blank
     c                   eval      c2stx2 = *blank
     c                   eval      c2stx3 = *blank
      *
      * Hent informasjon fra BUDSJETT-NIVÅ-REGISTER
      *
     c     key01         chain     slnil1r                            60
     c                   if        *in60 = *off
     c                   movel     mld(2)        c2nyen
     c                   move      smkomm        c2komm
     c                   move      smendr        c2endr
     c                   move      smakkk        c2akkk
     c                   move      smakod        c2akod
      *
     c                   else
      *
     c                   movel     mld(1)        c2nyen
     c                   endif
      *
      * Hent tekster fra tabell
      *
     c                   exsr      hntarr
      *
     c                   if        r1 > *zeros
     c                   movel     rpt(r1)       c2stx1
     c                   endif
      *
     c                   if        r2 > *zeros
     c                   movel     rpt(r2)       c2stx2
     c                   endif
      *
     c                   if        r3 > *zeros
     c                   movel     rpt(r3)       c2stx3
     c                   endif
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c     *inkc         cabeq     *on           c2tag1
     c     *inkl         cabeq     *on           c2tag1
      *
     c     key01         chain     slnilur                            60
      *
     c                   move      c2komm        smkomm
     c                   move      c2endr        smendr
     c                   move      c2akkk        smakkk
     c                   move      c2akod        smakod
      *
     c                   if        *in60 = *off
     c                   update    slnilur
      *
     c                   else
      *
     c                   move      wwfirm        smfirm
     c                   movel     wwkode        smkode
     c                   write     slnilur
      *
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = ' 2'
     c                             or b1valg = '2 '
     c                   move      c2kode        b1kode
     c                   endif
      *
     c     c2tag1        tag
      *
     c                   if        b1valg = ' 2'
     c                             or b1valg = '2 '
     c                   move      srrn01        svrn01
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   endif
      *
     c                   eval      b2kode = *zero
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * D1WIN Behandle bilde for slette                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xd1win        begsr
      *
     c     key01         chain     slnil1r                            60
      *
     c                   write     s1win
      *
     c                   move      *blank        d1valg
     c                   move      *zero         d1hhaa
     c                   move      *zero         d1paar
     c                   move      'B'           d1aabb
     c                   move      'O '          d1vers
     c                   move      'O '          d1pers
      *
     c                   move      smkode        d1kode
     c                   move      smkomm        d1text
      *
     c                   exfmt     d1win
      *
     c     *inkc         cabeq     *on           d1end1
     c     *inkl         cabeq     *on           d1end1
      *
      *  Kaller opp subprogram for sletting av budsjett
      *
     c                   if        d1hhaa <> 0  or d1paar <> 0
     C                   call      'SCDELR'
     C                   parm                    wwfirm
     C                   parm                    d1kode
     C                   parm                    d1aabb
     C                   parm                    d1hhaa
     C                   parm                    d1paar
     C                   parm                    d1vers
     C                   parm                    d1pers
     c                   endif
      *
      * Slett parametre
      *
     c                   if        d1valg = 'J'
     c     key02         reade     slnilur                                61
     c     key02         setll     slnilur
     c     key02         reade     slnilur                                61
     c                   dow       *in61 = *off
     c                   delete    slnilur
     c     key02         reade     slnilur                                61
     c                   enddo
     c                   endif
      *
      * Oppdater subfile
      *
     c     d1tag1        tag
      *
     c                   if        b1valg = ' 4'
     c                             or b1valg = '4 '
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   endif
      *
     c                   eval      b2kode = *zero
      *
     c     d1end1        endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * F0WIN Behandle bilde for budsjettering                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xf0win        begsr
      *
      * Hent informasjon fra BUDSJETT-NIVÅ-REGISTER
      *
     c     key01         chain     slnil1r                            60
      *
      * Legg inn faste startverdier
      *
     c                   move      *year         f0hhaa
     c                   move      'O '          f0vers
     c                   move      'B'           f0aabb
      *
     c                   write     s1win
      *
     c                   exfmt     f0win
      *
     c     *inkc         cabeq     *on           f0tag1
     c     *inkl         cabeq     *on           f0tag1
      *
     c                   call      'SC210R'
     c                   parm                    f0hhaa
     c                   parm                    f0vers
     c                   parm                    f0kode
     c                   parm                    f0aabb
      *
     c     f0tag1        tag
      *
      * Oppdater subfile
      *
     c                   if        b1valg = ' 5'
     c                             or b1valg = '5 '
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   endif
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * K1WIN Behandle bilde for kopiere                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xk1win        begsr
      *
     c     key01         chain     slnil1r                            60
      *
     c                   move      'B'           k1aabb
     c                   move      'O '          k1fers
     c                   move      'O '          k1ters
      *
     c                   move      smkode        k2kode
     c                   move      smkomm        k1text
      *
     c                   write     s1win
      *
     c     k1tagb        tag
      *
     c                   exfmt     k1win
      *
     c     *inkc         cabeq     *on           k1end1
     c     *inkl         cabeq     *on           k1end1
      *
     c     k1tag1        tag
      *
      *  Kaller opp subprogram for kopiering av budsjett
      *
     C                   call      'SCCOLR'
     C                   parm                    wwfirm
     C                   parm                    k2kode
     C                   parm                    k1aabb
     C                   parm                    k1fhaa
     C                   parm                    k1thaa
     C                   parm                    k1fers
     C                   parm                    k1ters
      *
      * Oppdater subfile
      *
     c                   if        b1valg = ' 3'
     c                             or b1valg = '3 '
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   endif
      *
     c                   eval      b2kode = *zero
      *
     c     k1end1        endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * P0WIN Behandle bilde for utskrift                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xp0win        begsr
      *
     c                   move      'O '          p0vers
     c                   move      'B'           p0aabb
      *
     c                   write     s1win
      *
     c                   exfmt     p0win
      *
     c     *inkc         cabeq     *on           p0tag1
     c     *inkl         cabeq     *on           p0tag1
      *
      * Kaller opp program for utskrift av budsjett
      *
     c                   move      *blank        wsparm
     c                   move      l_firm        wsfirm
     c                   move      p0hhaa        wshhaa
     c                   move      p0vers        wsvers
     c                   move      p0kode        wskode
     c                   move      p0aabb        wsaabb
      *
     c                   call      'SC610C'
     c                   parm                    wsparm
      *
     c     p0tag1        tag
      *
      * Oppdater subfile
      *
     c                   if        b1valg = ' 6'
     c                             or b1valg = '6 '
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   endif
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * R0WIN Behandle bilde for budsjettering                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xr0win        begsr
      *
      * Hent informasjon fra BUDSJETT-NIVÅ-REGISTER
      *
     c     key01         chain     slnil1r                            60
      *
      * Hopp ut dersom denne ikke kan oppdateres
      *
     c                   if        smakod <> *blank
     c                   goto      r0tag1
     c                   endif
      *
      * Legg inn faste startverdier
      *
     c                   move      'O '          r0vers
     c                   move      'B'           r0aabb
      *
     c                   write     s1win
      *
     c                   exfmt     r0win
      *
     c     *inkc         cabeq     *on           r0tag1
     c     *inkl         cabeq     *on           r0tag1
      *
     c                   call      'SC110R'
     c                   parm                    r0hhaa
     c                   parm                    r0vers
     c                   parm                    r0kode
     c                   parm                    r0aabb
      *
     c     r0tag1        tag
      *
      * Oppdater subfile
      *
     c                   if        b1valg = ' 8'
     c                             or b1valg = '8 '
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   endif
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * T0WIN Behandle bilde for budsjettering                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xt0win        begsr
      *
      * Hent informasjon fra BUDSJETT-NIVÅ-REGISTER
      *
     c     key01         chain     slnil1r                            60
      *
      * Hopp ut dersom denne ikke kan oppdateres
      *
     c                   if        smakod = *blank
     c                   goto      t0tag1
     c                   endif
      *
      * Legg inn faste startverdier
      *
     c                   move      'O '          t0vers
     c                   move      'B'           t0aabb
      *
     c                   write     s1win
      *
     c                   exfmt     t0win
      *
     c     *inkc         cabeq     *on           t0tag1
     c     *inkl         cabeq     *on           t0tag1
      *
      * Kaller opp program for genering av nytt budsjett
      *
     c                   move      l_firm        wsfirm
     c                   move      t0hhaa        wshhaa
     c                   move      t0vers        wsvers
     c                   move      t0kode        wskode
     c                   move      t0aabb        wsaabb
     c                   move      smakod        wsakod
      *
     c                   call      'SCGENR'
     c                   parm                    wsparm
      *
     c     t0tag1        tag
      *
      * Oppdater subfile
      *
     c                   if        b1valg = '60'
     c                             or b1valg = '60'
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   endif
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * U0WIN Behandle bilde for budsjettering                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xu0win        begsr
      *
      * Hopp ut til start, dersom alle
      *
     c                   if        u0kode = *blank
     c                   movel     'Alle'        u0kode
     c                   goto      u0tag1
     c                   endif
      *
      * Hent informasjon fra BUDSJETT-NIVÅ-REGISTER
      *
     c     key01         chain     slnil1r                            60
      *
      * Hopp ut dersom denne ikke kan oppdateres
      *
     c                   if        smakkk <> *blank
     c                   goto      u0tag9
     c                   endif
      *
     c     u0tag1        tag
      *
     c                   write     s1win
      *
     c                   exfmt     u0win
      *
     c     *inkc         cabeq     *on           u0tag9
     c     *inkl         cabeq     *on           u0tag9
      *
      * Kaller opp program for genering av nytt budsjett
      *
     c                   if        u0kode = 'Alle'
     c                   movel     *blank        u0kode
     c                   endif
      *
     c                   move      l_firm        wsfirm
     c                   move      u0hhaa        wshhaa
     c                   move      u0kode        wskode
      *
     c                   call      'SCANYR'
     c                   parm                    wsparm
      *
     c     u0tag9        tag
      *
      * Oppdater subfile
      *
     c                   if        b1valg = '99'
     c                             or b1valg = '99'
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   endif
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * V0WIN Behandle bilde for budsjettering                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xv0win        begsr
      *
      * Hent informasjon fra BUDSJETT-NIVÅ-REGISTER
      *
     c     key01         chain     slnil1r                            60
      *
      * Hopp ut dersom denne ikke kan oppdateres
      *
     c                   if        smakod <> *blank
     c                   goto      v0tag1
     c                   endif
      *
      * Hopp ut dersom denne ikke kan oppdateres
      *
     c                   if        v0kode <> '000000000001'
     c                   goto      v0tag1
     c                   endif
      *
      * Legg inn faste startverdier
      *
     c                   move      'O '          v0vers
     c                   move      'B'           v0aabb
      *
     c                   write     s1win
      *
     c                   exfmt     v0win
      *
     c     *inkc         cabeq     *on           v0tag1
     c     *inkl         cabeq     *on           v0tag1
      *
      * Kaller opp program for genering av nytt budsjett
      *
     c                   move      l_firm        wsfirm
     c                   move      v0hhaa        wshhaa
     c                   move      v0vers        wsvers
     c                   move      v0kode        wskode
     c                   move      v0aabb        wsaabb
     c                   move      *blank        wsakod
     c                   move      v0file        wsfile
     c                   move      v0bibl        wsbibl
      *
     c                   call      'SCFILC'
     c                   parm                    wsparm
      *
     c     v0tag1        tag
      *
      * Oppdater subfile
      *
     c                   if        b1valg = '62'
     c                             or b1valg = '62'
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   endif
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * V1WIN Behandle bilde for budsjettering                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xv1win        begsr
      *
      * Hent informasjon fra BUDSJETT-NIVÅ-REGISTER
      *
     c     key01         chain     slnil1r                            60
      *
      * Hopp ut dersom denne ikke kan oppdateres
      *
     c                   if        v1kode <> '000000001230' and
     c                             v1kode <> '000000001200' and
     c                             v1kode <> '000000001000'
     c                   goto      v1tag1
     c                   endif
      *
      * Legg inn faste startverdier
      *
     c                   move      'O '          v1vers
      *
     c                   write     s1win
      *
     c                   exfmt     v1win
      *
     c     *inkc         cabeq     *on           v1tag1
     c     *inkl         cabeq     *on           v1tag1
      *
      * Kaller opp program for genering av nytt budsjett
      *
     c                   move      l_firm        wsfirm
     c                   move      v1hhaa        wshhaa
     c                   move      v1vers        wsvers
     c                   move      v1kode        wskode
     c                   move      'B'           wsaabb
     c                   move      *blank        wsakod
      *
     c                   call      'SCOVFC'
     c                   parm                    wsparm
      *
     c     v1tag1        tag
      *
      * Oppdater subfile
      *
     c                   if        b1valg = '78'
     c                             or b1valg = '78'
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   endif
      *
     c                   endsr
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Tøm subfile                                                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xclr01        begsr
      *
     c                   move      *on           *in14
     c                   write     b2ctl
     c                   move      *off          *in14
      *
     c                   move      *off          *in15
     c                   eval      srrn01 = 0
     c                   eval      sfrn01 = 0
     c                   eval      ssrn01 = 0
     c                   eval      spge01 = 0
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Fyll opp subfile                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xcrt01        begsr
      *
     c                   eval      spge01 = spge01 + 10
     c                   eval      srrn01 = ssrn01
      *
     c                   dou       srrn01 = spge01
      *
     c     les01         tag
      *
     c                   read      slnil1r                                15
      *
     c     *in15         cabeq     *on           xend01
      *
     c                   eval      srrn01 = srrn01 + 1
     c                   move      smkode        b1kode
      *
      * Hent tekster fra tabell
      *
     c                   move      smendr        b1endr
     c                   move      *blank        b1vis1
     c                   move      *blank        b1vis2
     c                   move      *blank        b1vis3
      *
     c                   move      smkode        wwkode
     c                   exsr      hntarr
      *
     c                   if        r1 > *zeros
     c                   movel     rpt(r1)       b1vis1
     c                   endif
      *
     c                   if        r2 > *zeros
     c                   movel     rpt(r2)       b1vis2
     c                   endif
      *
     c                   if        r3 > *zeros
     c                   movel     rpt(r3)       b1vis3
     c                   endif
      *
     c                   write     b1sfl
     c                   enddo
      *
     c     xend01        tag
      *
     c                   if        srrn01 > ssrn01
     c                   eval      sfrn01 = ssrn01 + 1
     c                   endif
      *
     c                   eval      ssrn01 = srrn01
     c                   eval      svrn01 = sfrn01
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Display subfile                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xdsp01        begsr
      *
     c                   if        srrn01 <> 0
     c                   move      *on           *in12
     c                   endif
      *
     c                   move      *on           *in13
     c                   exfmt     b2ctl
     c                   move      *off          *in12
     c                   move      *off          *in13
     c                   move      *off          *in22
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Henter nummer verdier fra array                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     hntarr        begsr
      *
     c                   move      *zero         r1
     c                   move      *zero         r2
     c                   move      *zero         r3
      *
      * (11)
      *
     c     wwkode_11     ifeq      1
     c                   move      11            r1
     c                   endif
     c     wwkode_11     ifeq      2
     c                   move      11            r2
     c                   endif
     c     wwkode_11     ifeq      3
     c                   move      11            r3
     c                   endif
      *
      * (12)
      *
     c     wwkode_12     ifeq      1
     c                   move      12            r1
     c                   endif
     c     wwkode_12     ifeq      2
     c                   move      12            r2
     c                   endif
     c     wwkode_12     ifeq      3
     c                   move      12            r3
     c                   endif
      *
      * (13)
      *
     c     wwkode_13     ifeq      1
     c                   move      13            r1
     c                   endif
     c     wwkode_13     ifeq      2
     c                   move      13            r2
     c                   endif
     c     wwkode_13     ifeq      3
     c                   move      13            r3
     c                   endif
      *
      * (21)
      *
     c     wwkode_21     ifeq      1
     c                   move      21            r1
     c                   endif
     c     wwkode_21     ifeq      2
     c                   move      21            r2
     c                   endif
     c     wwkode_21     ifeq      3
     c                   move      21            r3
     c                   endif
      *
      * (22)
      *
     c     wwkode_22     ifeq      1
     c                   move      22            r1
     c                   endif
     c     wwkode_22     ifeq      2
     c                   move      22            r2
     c                   endif
     c     wwkode_22     ifeq      3
     c                   move      22            r3
     c                   endif
      *
      * (23)
      *
     c     wwkode_23     ifeq      1
     c                   move      23            r1
     c                   endif
     c     wwkode_23     ifeq      2
     c                   move      23            r2
     c                   endif
     c     wwkode_23     ifeq      3
     c                   move      23            r3
     c                   endif
      *
      * (24)
      *
     c     wwkode_24     ifeq      1
     c                   move      24            r1
     c                   endif
     c     wwkode_24     ifeq      2
     c                   move      24            r2
     c                   endif
     c     wwkode_24     ifeq      3
     c                   move      24            r3
     c                   endif
      *
      * (25)
      *
     c     wwkode_25     ifeq      1
     c                   move      25            r1
     c                   endif
     c     wwkode_25     ifeq      2
     c                   move      25            r2
     c                   endif
     c     wwkode_25     ifeq      3
     c                   move      25            r3
     c                   endif
      *
      * (31)
      *
     c     wwkode_31     ifeq      1
     c                   move      31            r1
     c                   endif
     c     wwkode_31     ifeq      2
     c                   move      31            r2
     c                   endif
     c     wwkode_31     ifeq      3
     c                   move      31            r3
     c                   endif
      *
      * (32)
      *
     c     wwkode_32     ifeq      1
     c                   move      32            r1
     c                   endif
     c     wwkode_32     ifeq      2
     c                   move      32            r2
     c                   endif
     c     wwkode_32     ifeq      3
     c                   move      32            r3
     c                   endif
      *
      * (33)
      *
     c     wwkode_33     ifeq      1
     c                   move      33            r1
     c                   endif
     c     wwkode_33     ifeq      2
     c                   move      33            r2
     c                   endif
     c     wwkode_33     ifeq      3
     c                   move      33            r3
     c                   endif
      *
      * (34)
      *
     c     wwkode_34     ifeq      1
     c                   move      34            r1
     c                   endif
     c     wwkode_34     ifeq      2
     c                   move      34            r2
     c                   endif
     c     wwkode_34     ifeq      3
     c                   move      34            r3
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     *inzsr        begsr
      *
      * Key for file nummerisk
      *
     c     key01         klist
     c                   kfld                    wwfirm
     c                   kfld                    wwkode
      *
      * Key for sleting
      *
     c     key02         klist
     c                   kfld                    wwfirm
     c                   kfld                    wwkode
      *
     c                   eval      f0hhaa =  *year
      *
     c                   eval      wwfirm =  l_firm
     c                   eval      wwkode = *zero
      *
     c                   eval      *in21 = *on
     c                   eval      *in22 = *on
      *
     c     key01         setll     slnil1r
      *
     c                   exsr      xcrt01
      *
     c                   endsr
      *
      **********************  T A B E L L E R  ***************************
**
  Ny post                  001
  Endring                  002
  Vise                     003
* Slettet *                004
**  SORTERINGSREKKEFØLGE
Ledig                      01
Ledig                      02
Ledig                      03
Ledig                      04
Ledig                      05
Ledig                      06
Ledig                      07
Ledig                      08
Ledig                      09
Ledig                      10
Avdeling                   11
Lager                      12
Leveringstype              13
Ledig                      14
Ledig                      15
Ledig                      16
Ledig                      17
Ledig                      18
Ledig                      19
Ledig                      20
Land                       21
Distrikt                   22
Selger                     23
Kundekategori              24
Kundenummer                25
Ledig                      26
Ledig                      27
Ledig                      28
Ledig                      29
Ledig                      30
Overgruppe                 31
Hovedgruppe                32
Undergruppe                33
Varenummer                 34
Ledig                      35
Ledig                      36
Ledig                      37
Ledig                      38
Ledig                      39
Ledig                      40
Ledig                      41
Ledig                      42
Ledig                      43
Ledig                      44
Ledig                      45
Ledig                      46
Ledig                      47
Ledig                      48
Ledig                      49
Ledig                      50
Ledig                      51
Ledig                      52
Ledig                      53
Ledig                      54
Ledig                      55
Ledig                      56
Ledig                      57
Ledig                      58
Ledig                      59
Ledig                      60
