     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSHOP
      * Program . . . . : BU726R
      * Beskrivelse . . : Butikk, eksport av vareliste
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       210404 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvshel3    if   e           k disk    rename(vshepfr:vshel3r)
      *
     fbolhst    uf a e           k disk    rename(bolhst:bolhstr)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vcfirm)
      *
      * Definering av variabler i nøkler
      *
     d vshel3_type     s                   like(vctype)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vcfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer fil
      *
     c                   clear                   bolhstr
      *
     c                   eval      firm = w_firm
      *
      * Behandler varesortiment-hode
      *
     c                   eval      vshel3_type = 'V'
     c     vshel3_key    setll     vshel3
     c     vshel3_key    reade     vshel3
     c                   dow       %eof = *off
      *
     c                   eval      list = vcvsor
     c                   eval      besk = vcbesk
     c                   eval      odat = vcodat
     c                   eval      edat = vcedat
     c                   eval      etim = vcetim
     c                   eval      eusr = vceusr
      *
     c                   write     bolhstr
      *
      * Skiver varelinjer
      *
     c                   call      'BU727C'
     c                   parm                    w_firm
     c                   parm                    vcvsor
      *
     c     vshel3_key    reade     vshel3
     c                   enddo
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameter
      *
     c     *entry        plist
     c                   parm                    p_firm
      *
      * Key for les på varesortiment-hode
      *
     c     vshel3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vshel3_type
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
