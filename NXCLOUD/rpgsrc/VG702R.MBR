     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : VG702R
      * Beskrivelse . . : Beregner ny glidende gjennomsnittlig kostpris
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       231118 ASK  Nytt program
      * 8.00  121021 ask  Programmet er kraftig omskrevet og endringer er ikke merket
      *                   Ses på som nytt program
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flohelr    if   e           k disk    rename(lohepfr:lohelrr)
     flodtlr    if   e           k disk    rename(lodtpfr:lodtlrr)
     fvgkpiu    uf a e           k disk    rename(vgkpst:vgkpiur)
     fvgkpi1    if   e           k disk    rename(vgkpst:vgkpi1r)
     fvvarl1    if   e           K disk    rename(vvarpfr:vvarl1r)
     fvlaglr    if   e           k disk    rename(vlagpfr:vlaglrr)
     fvvenlr    if   e           k disk    rename(vvenpfr:vvenlrr)
     fvgkkir    if   e           k disk    rename(vgkkst:vgkkirr)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
      *
      * Definering av parametre
      *
     d p_numm          s              8  0
     d p_suff          s              2  0
      *
     d p_cost          s                   like(vgcost)
8.01 d p_prgr          s              2
     d p_lety          s              1  0
     d p_kopr          s              9  2
     d p_inpr          s              9  2
     d p_aogr          s              2  0
     d p_ahgr          s              2  0
     d p_augr          s              3  0
     d p_aldo          s              6  0
     d p_amod          s              8  0
     d p_aart          s              8  0
     d p_apro          s              3  1
     d p_dato          s              8  0
     d p_inlr          s              1
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_user                911    920
      *
      * Datastruktur for kostprisfaktorer
      *
     d d_frec          ds
     d  d_fty1                       30
     d  d_fsi1                        1
     d  d_fak1                        8  6
     d  d_kto1                        6  0
     d  d_mot1                        6  0
     d  d_fty2                       30
     d  d_fsi2                        1
     d  d_fak2                        8  6
     d  d_kto2                        6  0
     d  d_mot2                        6  0
     d  d_fty3                       30
     d  d_fsi3                        1
     d  d_fak3                        8  6
     d  d_kto3                        6  0
     d  d_mot3                        6  0
     d  d_fty4                       30
     d  d_fsi4                        1
     d  d_fak4                        8  6
     d  d_kto4                        6  0
     d  d_mot4                        6  0
     d  d_fty5                       30
     d  d_fsi5                        1
     d  d_fak5                        8  6
     d  d_kto5                        6  0
     d  d_mot5                        6  0
      *
      * Definering av variabler i nøkler
      *
     d lohelr_numm     s                   like(lonumm)
     d lohelr_suff     s                   like(losuff)
     d lodtlr_numm     s                   like(ldnumm)
     d lodtlr_suff     s                   like(ldsuff)
     d lodtlr_line     s                   like(ldline)
     d vgkpiu_lage     s              2  0
     d vgkpiu_hkod     s                   like(vghkod)
     d vgkpiu_vare     s                   like(vgvare)
     d vgkpi1_lage     s              2  0
     d vgkpi1_hkod     s                   like(vghkod)
     d vgkpi1_vare     s                   like(vgvare)
     d vgkpi1_gdat     s                   like(vggdat)
     d vgkpi1_gtim     s                   like(vggtim)
     d vvarl1_vare     s                   like(vvvare)
     d vlaglr_vare     s                   like(vlvare)
     d vlaglr_lage     s                   like(vllage)
     d vvenlr_vare     s                   like(vevare)
     d vvenlr_enhe     s                   like(veenhe)
     d vltyl1_ltyp     s                   like(valtyp)
      *
      * Definering av variable
      *
     d w_firm          s                   like(ldfirm)
     d w_numm          s                   like(ldnumm)
     d w_suff          s                   like(ldsuff)
     d w_line          s                   like(ldline)
     d w_vare          s                   like(ldvare)
     d s_vare          s                   like(ldvare)
     d w_antb          s                   like(ldanta)
     d w_anta          s                   like(ldanta)
     d w_sumk          s                   like(ldsumk)
     d w_sumb          s                   like(ldsumi)
     d w_ldor          s                   like(ldldor)
     d w_anth          s                   like(ldanta)
     d w_frec          s            300
     d w_ipny          s              9  2
     d w_kpny          s              9  2
     d w_inkp          s              9  2
     d w_avpv          s             12  2
     d w_avpp          s             12  2
     d w_omrf          s              4  2
     d w_behl          s             12  3
     d w_gbeh          s             12  3
     d w_sign          s              1  0
     d w_feik          s              2  0
     d w_feil          s            100
     d w_til1          s              9  2
     d w_til2          s              9  2
     d w_til3          s              9  2
     d w_til4          s              9  2
     d w_til5          s              9  2
     d w_nykp          s              9  2
     d w_hdat          s               d   datfmt(*iso)
     d w_htim          s               t   timfmt(*iso)
     d w_nylv          s              9  2
     d w_gmlv          s              9  2
     d w_gjkp          s              9  2
     d w_ofak          s             12  6
     d b_feil          s              1    inz(*off)
     d b_avvik         s              1    inz(*off)

       DCL-PR VG707R EXTPGM('VG707R');
         p_aogr      packed(2:0);
         p_ahgr      packed(2:0);
         p_augr      packed(3:0);
         p_aldo      packed(6:0);
         p_amod      packed(8:0);
         p_aart      packed(8:0);
         p_apro      packed(3:1);
         p_inlr      char(1);
       END-PR;

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedrutine - beregn ny glidende gj
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Les bestillingshode
      *
     c                   eval      lohelr_numm = p_numm
     c                   eval      lohelr_suff = p_suff
     c     lohelr_key    chain     lohelrr
     c                   if        not %found(lohelr)
     c                   goto      avslutt
     c                   endif
      *
      * Henter ut bestillingsliner til cursor sortert på varenummer
      *
     c/Exec SQL
     c+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR,
     c+             commit=*none
     c/End-Exec
     c                   eval      s_vare = *blank
      *
     c/exec sql
     c+                  declare beslin cursor for
     c+                  select ldfirm,
     c+                         ldnumm,
     c+                         ldsuff,
     c+                         ldline,
     c+                         ldvare
     c+                  from   lodtpf
     c+                  where  ldfirm = :w_firm and
     c+                         ldnumm = :p_numm and
     c+                         ldsuff = :p_suff and
     c+                         ldvare <> '  '
     c+                  order by ldfirm, ldnumm, ldsuff, ldvare
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close beslin
     c/end-exec
      *
      * Leser bestillingsliner fra cursor for beregning av gj.sn kostpris
      *
     c/exec sql
     c+                  open beslin
     c/end-exec
      *
     c     les_neste     tag
      *
     c/exec sql
     c+                  fetch next
     c+                  from  beslin
     c+                  into  :w_firm,
     c+                        :w_numm,
     c+                        :w_suff,
     c+                        :w_line,
     c+                        :w_vare
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   goto      avslutt
     c                   endif
      *
      * Sjekk om vare er behandlet før
      *
     c                   if        w_vare = s_vare
     c                   goto      les_neste
     c                   else
     c                   eval      s_vare = w_vare
     c                   endif
      *
      * Les bestillingslinje
      *
     c                   eval      lodtlr_numm = w_numm
     c                   eval      lodtlr_suff = w_suff
     c                   eval      lodtlr_line = w_line
     c     lodtlr_key    chain     lodtlrr
     c                   if        not %found(lodtlr)
     c                   goto      les_neste
     c                   endif
      *
      * Direktelevert skal ikke oppdatere
      *
     c                   if        ldlety = 1
     c                   goto      les_neste
     c                   endif
      *
      * Hvis 0 eller negativt i antall, skal vi ikke oppdatere kostpris
      *
     c                   if        ldanta < 0,001
     c                   goto      les_neste
     c                   endif
      *
      * Sjekker om gyldig linjetype og behandler
      *
     c                   eval      vltyl1_ltyp = ldltyp
     c     vltyl1_key    chain     vltyl1
     c                   if        not %found(vltyl1) or
     c                             vallag <> 1
     c                   goto      les_neste
     c                   endif
      *
      * Sjekk om gyldig vare / lagervare
      *
     c                   exsr      les_vare
     c                   if        b_feil = *on
     c                   goto      les_neste
     c                   endif
      *
     c                   exsr      les_lager
     c                   if        b_feil = *on
     c                   goto      les_neste
     c                   endif
      *
      * Summerer linjer på bestilling med dette varenummer og beregn priser
      *
     c                   exsr      sum_best
     c                   if        w_antb < 0.001
     c                   eval      w_feik = 01
     c                   eval      w_feil = 'Sum innkjøp er 0 eller negativt'
     c                   exsr      logg_feil
     c                   goto      les_neste
     c                   endif
      *
     c                   eval(h)   w_ipny = w_sumb/w_antb
     c                   eval      w_kpny = w_sumk/w_antb
      *
      * Beregn sum i lagerbok på bestilling med dette varenummer og beregn priser
      *
     c                   exsr      sum_hist
     c                   if        w_anth < 0.001
     c                   eval      w_feik = 02
     c                   eval      w_feil = 'Sum lagerbok er 0 eller negativt'
     c                   exsr      logg_feil
     c                   goto      les_neste
     c                   endif
      *
      * Sjekker om varen er bestilt i lagerenhet - hvis ikke, regn om til lagerenhet
      *
     c                   eval      w_ofak = 0
     c                   if        ldenh1 <> vvenhl
     c                   eval      w_anta = w_antb
     c                   exsr      beregn_pris
     c                   eval      w_antb = w_anta
     c                   endif
      *
     c                   eval(h)   w_avpv = %abs(((w_antb-w_anth)*100)/w_anth)
     c                   if        w_avpv > 1
     c                   eval      w_feik = 03
     c                   eval      w_feil = 'Avvik mellom innkjøps transaksjon +
     c                                       og lagerbok'
     c                   exsr      logg_feil
     c                   goto      les_neste
     c                   endif
      *
      *
      * Sjekker om innkjøpspris eller kostpris skal brukes
      *
     c                   if        vgkprk = 'I'
     c                   eval      w_inkp = w_ipny
     c                   else
     c                   eval      w_inkp = w_kpny
     c                   endif
      *
      * Hent eventuelle kostprisfaktorer og beregn tillegg/fradrag
      *
     c                   eval      w_ldor = ldldor
     c                   call      'VG703R'
     c                   parm                    s_vare
     c                   parm                    ldlage
     c                   parm                    ldldor
     c                   parm                    w_frec
     c                   eval       d_frec = w_frec
      *
     c                   exsr      finn_kostt
     c                   eval      w_nykp = w_inkp + w_til1 + w_til2 + w_til3
     c                                             + w_til4 + w_til5
      *
      * Sjekk om innpris er %-vis usansynlig sammenlignet mot kalkulert kostpris.
      *
     c                   eval      b_avvik = *off
     c                   exsr      kost_kontroll
      *
     c                   if        b_avvik = *off
     c                   if        vgkprk = 'I'
     c                   eval      w_nykp = w_nykp * w_omrf
     c                   endif
     c                   else
     c                   eval      w_feik = 04
     c                   eval      w_feil = 'Avvik mellom transaksjon pris +
     c                                       og kalk. kostpris for stort'
     c                   exsr      logg_feil
     c                   goto      les_neste
     c                   endif
      *
      * Beregn lagerverdi på nytt innkjøp
      *
     c                   eval(h)   w_nylv = w_inkp * w_antb
      *
      * Hent gjeldende gjennomsnittlig kostpris og beregn eksisterende lagerverdi
      *
     c                   eval      p_cost = 0
     c                   eval      p_dato = %dec(lovida:*iso)
     c                   call      'VG701R'
     c                   parm                    ldvare
     c                   parm                    ldlage
     c                   parm                    p_dato
     c                   parm                    p_cost
      *
      * Beregn lagerverdi på beholdning før  innkjøp
      *
     c                   exsr      finn_behl
     c                   eval(h)   w_gmlv = p_cost * w_gbeh
      *
      * Bergn ny glidende gjennomsnittlig kostpris
      *
     c                   if        w_antb > 0 and
     c                             w_gmlv > 0
     c                   eval(h)   w_gjkp = (w_nylv + w_gmlv)
     c                                       / w_behl
     c                   else
     c                   eval      w_gjkp = w_nykp
     c                   endif
      *
      * Sjekk om kostpris er 0 eller negativ
      *
     c                   if        w_gjkp <= 0
     c                   goto      les_neste
     c                   endif
      *
      * Oppdatering
      *
     c                   eval      vgkpiu_hkod = *blank
     c                   movel     ldvare        vgkpiu_vare
     c                   eval      vgkpiu_lage = ldlage
     c     vgkpiu_key    chain     vgkpiu
      *
     c                   if        %found
     c                   exsr      pris_hist
     c                   end
      *
     c                   clear                   vgkpiur
     c                   eval      vgfirm = w_firm
     c                   eval      vghkod = ' '
     c                   eval      vgvare = ldvare
     c                   eval      vglage = ldlage
     c     *ymd          move      p_dato        vggdat
     c                   time                    vggtim
     c                   eval      vgenhe = vvenhl
     c                   eval      vgtype = 'A'
     c                   eval      vgcost = w_gjkp
      *
     c                   eval      vgbest = p_numm
     c                   eval      vgsuff = p_suff
     c                   eval      vgline = ldline
     c                   eval      vggmkp = p_cost
     c                   eval      vginkp = w_inkp
     c                   eval      vgpkod = vgkprk
     c                   eval      vganta = w_antb
     c                   eval      vgbehl = w_gbeh
      *
     c                   time                    vgttim
     c                   time                    vgbdat
     c                   eval      vgbusr = l_user
      *
     c                   write     vgkpiur
     c                   goto      les_neste
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppslag på vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_vare      begsr
      *
     c                   eval      vvarl1_vare = ldvare
     c     vvarl1_key    chain     vvarl1r
     c                   if        %found
     c                   eval      b_feil = *off
     c                   else
     c                   eval      b_feil = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppslag på lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_lager     begsr
      *
     c                   eval      b_feil = *off
     c                   eval      vlaglr_vare = ldvare
     c                   eval      vlaglr_lage = ldlage
     c     vlaglr_key    chain     vlaglr
     c                   if        not %found(vlaglr) or
     c                             vlvtyp <> 'L'
     c                   eval      b_feil = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å summere alle linjer på bestilling/suffiks/vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sum_best      begsr
      *
      /FREE
          w_antb = 0;
          w_sumk = 0;
          w_sumb = 0;

            exec sql
            select
              sum(ldanta),
              sum(ldsumk - ldsumr),
              sum(ldsumi - ldsumr)
             into :w_antb, :w_sumk, :w_sumb
            from lodtpf
             where ldfirm = :w_firm and ldnumm = :p_numm and
                   ldsuff = :p_suff and ldvare = :s_vare;

      /END-FREE
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å summere alle linjer i historisk lagerbok
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sum_hist      begsr
      *
      /FREE

          w_anth = 0;

            exec sql
            select sum(vhanta) into :w_anth
            from vhispf
             where vhfirm = :w_firm and vhonum = :lonumm and
                   vhdato = :lovida and vhvare = :s_vare and
                   vhlage = :ldlage;

      /END-FREE
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne gammel beholdning fra historisk lagerbok
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_behl     begsr
      *
      /FREE

          w_behl = 0;
          w_gbeh = 0;

            exec sql
            select vhbehl, vhdato, vhtime
             into :w_behl, :w_hdat, :w_htim
            from vhispf
             where vhfirm = :w_firm and vhonum = :lonumm and
                   vhdato = :lovida and vhvare = :s_vare and
                   vhlage = :ldlage
             order by vhdato, vhtime desc, vhsekv desc
              fetch first rows only;

          w_gbeh = w_behl - w_antb;

      /END-FREE
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for avvikskontroll mot kalkulert kostpris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      /FREE
       begsr kost_kontroll;

          p_prgr = *blanks;
          p_lety = 0;
          p_kopr = 0;
          p_inpr = 0;
          w_omrf = 1;

       // Finner avviksprosent i egen tabell

          p_aogr = ldogrp;
          p_ahgr = ldhgrp;
          p_augr = ldugrp;
          p_aldo = ldldor;
          p_amod = 0;
          p_aart = 0;
          p_apro = 0;
          p_inlr = *blank;

            VG707R(p_aogr:p_ahgr:p_augr:p_aldo:p_amod:p_aart:p_apro:p_inlr);

          if p_apro = 0;
             p_apro = 20;
          endif;

       // Finner prisgruppe på lager

          exec SQL
           select vpgprg into :p_prgr
             from vpgrpf
             where vpglag = :ldlage;

       // Finner kalkulert kostpris
       //  - prøver leverandør fra innkjøp, preferert leverandør på lager, leverandør fra varekort

          exsr finn_kost;
          if p_kopr = 0 or p_inpr = 0;
             w_ldor = vlbldo;
             exsr finn_kost;
             if p_kopr = 0 or p_inpr = 0;
               w_ldor = vvldor;
             exsr finn_kost;
          endif;
          endif;

       // Sjekker ny innpris mot kalkulert kostpris/innkjøpspris

          if p_kopr > 0 and p_inpr > 0;
           w_avpp = %abs(((w_nykp-p_kopr)*100)/p_kopr);
             if w_avpp > p_apro;
               w_avpp = %abs(((w_nykp-p_inpr)*100)/p_inpr);
               if w_avpp > p_apro;
                 b_avvik = *on;
               endif;
             endif;
           if p_kopr > p_inpr and
              vgkprk = 'I';
             w_omrf = p_kopr / p_inpr;
            else;
             w_omrf = 1;
           endif;
          endif;

       endsr;
      /END-FREE
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne kalkulertkostpris på valgt leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      /FREE
        begsr finn_kost;

          exec SQL
            select vpkopr, vpinpr
             into :p_kopr, :p_inpr
              from vvprpf where
               vpfirm = :w_firm and
               vpvare = :s_vare and
               vpprgr = :p_prgr and
               vplety = :p_lety and
               vpldor = :w_ldor and
               vpenhe = :vvenhl and
               vppdat <= :lovida
              order by vppdat desc
               fetch first row only;

        endsr;
      /END-FREE
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å beregne kostpris tillegg og fradrag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_kostt    begsr
      *
     c                   if        d_fty1 <> *blank
     c                   if        d_fsi1 = '-'
     c                   eval      w_sign = -1
     c                   else
     c                   eval      w_sign = 1
     c                   endif
     c                   eval(h)   w_til1  = ((ldipny * d_fak1)/100) * w_sign
     c                   endif
      *
     c                   if        d_fty2 <> *blank
     c                   if        d_fsi2 = '-'
     c                   eval      w_sign = -1
     c                   else
     c                   eval      w_sign = 1
     c                   endif
     c                   eval(h)   w_til2  = ((ldipny * d_fak2)/100) * w_sign
     c                   endif
      *
     c                   if        d_fty3 <> *blank
     c                   if        d_fsi3 = '-'
     c                   eval      w_sign = -1
     c                   else
     c                   eval      w_sign = 1
     c                   endif
     c                   eval(h)   w_til3  = ((ldipny * d_fak3)/100) * w_sign
     c                   endif
      *
     c                   if        d_fty4 <> *blank
     c                   if        d_fsi4 = '-'
     c                   eval      w_sign = -1
     c                   else
     c                   eval      w_sign = 1
     c                   endif
     c                   eval(h)   w_til4  = ((ldipny * d_fak4)/100) * w_sign
     c                   endif
      *
     c                   if        d_fty5 <> *blank
     c                   if        d_fsi5 = '-'
     c                   eval      w_sign = -1
     c                   else
     c                   eval      w_sign = 1
     c                   endif
     c                   eval(h)   w_til5  = ((ldipny * d_fak5)/100) * w_sign
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for beregning av kost- og innkjøps-pris i lagerenhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beregn_pris   begsr
      *
      * Finn omregningsfaktor på innkjøpsenhet og beregn priser i forhold til basisenhet
      *
     c                   eval      vvenlr_vare = ldvare
     c                   eval      vvenlr_enhe = ldenh1
     c     vvenlr_key    chain     vvenlr
     c                   if        %found(vvenlr)
     c                   eval(h)   w_ipny = w_ipny / veomre
     c                   eval(h)   w_kpny = w_kpny / veomre
     c                   eval      w_anta = w_anta * veomre
     c                   endif
      *
      * Finn omregningsfaktor på lagerenhet og beregn priser dersom lagerenhet er ulik basisenhet
      *
     c                   if        vvenhe <> vvenhl
     c                   eval      vvenlr_enhe = vvenhl
     c     vvenlr_key    chain     vvenlr
     c                   if        %found(vvenlr)
     c                   eval(h)   w_ipny = w_ipny * veomre
     c                   eval(h)   w_kpny = w_kpny * veomre
     c                   eval      w_anta = w_anta / veomre
     c                   eval      w_ofak = veomre
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kostpris historikk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     pris_hist     begsr
      *
      * Sletter opprinnelig post
      *
     c                   delete    vgkpiur
      *
      * Oppretter historikk post - sjekker om historikk post allerede finnes
      *
     c                   eval      vgkpi1_lage = vglage
     c                   eval      vgkpi1_hkod = 'H'
     c                   eval      vgkpi1_vare = vgvare
     c                   eval      vgkpi1_gdat = vggdat
     c                   eval      vgkpi1_gtim = vggtim
     c     vgkpi1_key    chain     vgkpi1
      *
     c                   if        %found
     c                   goto      end_hist
     c                   end
     c                   eval      vghkod = 'H'
      *
     c                   write     vgkpiur
      *
     c     end_hist      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for logging av avvik i transaksjoner
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      /FREE
       begsr logg_feil;

      // Nullstiller verdier avhengig av avbruddspunkt

         if w_feik = 1;
          w_anth = 0;
          w_gbeh = 0;
          w_avpv = 0;
          w_avpp = 0;
          p_apro = 0;
          p_kopr = 0;
          w_nykp = 0;
          p_cost = 0;
          w_ofak = 0;
         endif;

         if w_feik = 2;
          w_gbeh = 0;
          w_avpv = 0;
          w_avpp = 0;
          p_apro = 0;
          p_kopr = 0;
          w_nykp = 0;
          p_cost = 0;
          w_ofak = 0;
         endif;

         if w_feik = 3;
          w_gbeh = 0;
          w_avpp = 0;
          p_apro = 0;
          p_kopr = 0;
          w_nykp = 0;
          p_cost = 0;
         endif;

         if w_feik = 4;
          w_nykp = 0;
          p_cost = 0;
         endif;

             exec sql
            insert into vgkest
              (vefirm,velage,vevare,veenhe,vetdat,
               vebest,vesuff,veinkp,vesumb,vesumh,
               vebehl,veavpv,veavpp,veavpt,vepkod,
               vegkpr,venkpr,vefeik,vefeil,vetype,
               vebusr,vekalp,veldor,veenhb,veomrf,
               veantb)
            values(:w_firm,:ldlage,:ldvare,:vvenhl,:lovida,
                   :p_numm,:p_suff,:w_inkp,:w_sumb,:w_anth,
                   :w_gbeh,:w_avpv,:w_avpp,:p_apro,:vgkprk,
                   :p_kopr,:w_nykp,:w_feik,:w_feil,'A',
                   :l_user,:p_cost,:w_ldor,:ldenh1,:w_ofak,
                   :w_antb);
       endsr;
      /END-FREE

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      w_feik = 99
     c                   eval      w_feil = 'VG702R PSSR subrutine !!!'
     c                   exsr      logg_feil
     c                   eval      *inlr = *on
     c                   return
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Key for kostpris parameter
      *
     c     vgkkir_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på bestilling-hode
      *
     c     lohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelr_numm
     c                   kfld                    lohelr_suff
      *
      * Key for oppslag på bestilling-linje
      *
     c     lodtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlr_numm
     c                   kfld                    lodtlr_suff
     c                   kfld                    lodtlr_line
      *
      * Key for oppslag av kostpris
      *
     c     vgkpiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vgkpiu_lage
     c                   kfld                    vgkpiu_hkod
     c                   kfld                    vgkpiu_vare
      *
     c     vgkpi1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vgkpi1_lage
     c                   kfld                    vgkpi1_hkod
     c                   kfld                    vgkpi1_vare
     c                   kfld                    vgkpi1_gdat
     c                   kfld                    vgkpi1_gtim
      *
      *  Key for file les av vareregister
      *
     c     vvarl1_key    KLIST
     c                   KFLD                    w_firm
     c                   KFLD                    vvarl1_vare
      *
      * Key for oppslag på vare/lager
      *
     c     vlaglr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglr_vare
     c                   kfld                    vlaglr_lage
      *
      *  Key for oppdslag på vare/enhet
      *
     c     vvenlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenlr_vare
     c                   kfld                    vvenlr_enhe
      *
      * Key for oppslag på linjetype
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
     c                   eval      w_firm = l_firm
      *
      * Henter kode informasjon
      *
     c     vgkkir_key    chain     vgkkir
      *
     c                   endsr
