     h option(*nodebugio) datedit(*dmy) decedit('0,')
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVARE
      * Program . . . . : JV602R
      * Beskrivelse . . : Utskrift av vareregister, vare-/søketekster
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       130596 HKO  Nytt program
6.11  *       201109 bof  Endret parameterlisten
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     Fjvarl9    IF   E           K DISK    RENAME(JVARPFR:jvarl9R)
     FJSOKL1    IF   E           K DISK    RENAME(JSOKPFR:JSOKL1R)
     FAFIRL1    IF   E           K DISK    RENAME(AFIRPFR:AFIRL1R)
     FJLEVL1    IF   E           K DISK    RENAME(JLEVPFR:JLEVL1R)
     FAUSRL1    IF   E           K DISK    RENAME(AUSRPFR:AUSRL1R)
      *
     FJV602P    O    E             PRINTER
      *
     D WSTEK           S             30    DIM(3) CTDATA PERRCD(1)               MELDINGER
      *
      * Variabler
      *
6.11 d p_parm          s            120
      *
      * Local data area
      *
     D                UDS
     D  DSRUTI               800    809
     D  DSALIN               856    858  0
     D  DSWSID               901    910
     D  DSUSER               911    920
     D  DSSFIR               944    946  0
      *
     D                 DS
      *
      * Datastruktur for parameterliste -inn
6.11 d                 ds
6.11 d d_list                       120
6.11 d  wpfirm                        3  0 overlay(d_list)
6.11 d  wpvalg                        1  0 overlay(d_list:4)
6.11 d  wpldor                        6  0 overlay(d_list:5)
6.11 d  wpeusr                       10    overlay(d_list:11)
6.11 d  wpogrf                        2  0 overlay(d_list:21)
6.11 d  wphgrf                        2  0 overlay(d_list:23)
6.11 d  wpugrf                        3  0 overlay(d_list:25)
6.11 d  wpogrt                        2  0 overlay(d_list:28)
6.11 d  wphgrt                        2  0 overlay(d_list:30)
6.11 d  wpugrt                        3  0 overlay(d_list:32)
6.11 d  wpvarf                       15    overlay(d_list:35)
6.11 d  wpvart                       15    overlay(d_list:50)
6.11 d  wpedaf                         d   overlay(d_list:65) datfmt(*iso)
6.11 d  wpedat                         d   overlay(d_list:75) datfmt(*iso)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Hovedprogram                                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Header
      *
     C                   EXSR      HODE
      *
      * Detalj
      *
     C                   EXSR      DETALJ
      *
      * Avslutter prorgram
      *
     C                   MOVE      *ON           *INLR
     C                   RETURN
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering og utskrift av hode-opplysninger       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     HODE          BEGSR
      *
      * Legger inn arb.stasjon og bruker i faste felter
      *
     C                   MOVE      DSSFIR        A1FIRM
     C                   MOVE      DSUSER        A1USER
     C                   MOVE      DSWSID        A1WSID
     C                   MOVE      UDATE         A1DATE
     C                   TIME                    UTIME             6 0
     C                   MOVE      UTIME         A1TIME
      *
      * Henter firmaopplysninger
      *
     C     KEY02         CHAIN     AFIRL1R                            60
     C                   MOVE      AAFNAV        A1FNAV
      *
      * Henter parametre med beskrivelse
      *
     C                   MOVE      WPLDOR        A2LDOR
     C                   MOVE      WPeusr        A2eusr
     C                   MOVE      WPHGRF        A2HGRF
     C                   MOVE      WPUGRF        A2UGRF
     C                   MOVE      WPHGRT        A2HGRT
     C                   MOVE      WPUGRT        A2UGRT
     C                   MOVE      WPVARF        A2VARF
     C                   MOVE      WPVART        A2VART
      *
     C     WPLDOR        IFNE      *ZERO
     C                   MOVE      WPLDOR        wwldor
     C     KEY04         CHAIN     JLEVL1                             60
     C     *IN60         IFEQ      *OFF
     C                   MOVEL     JLNAVN        A2LNAV
     C                   ENDIF
     C                   ENDIF
      *
     C     WPeusr        IFNE      *BLANK
     C                   MOVE      WPeusr        WWUSER
     C     KEY05         CHAIN     AUSRL1                             60
     C     *IN60         IFEQ      *OFF
     C                   MOVEL     ABNAVN        A2UNAV
     C                   ENDIF
     C                   ENDIF
      *
     C                   IF        WPEDAF <> *LOVAL
     C     *DMY          MOVE      WPEDAF        A2EDAF
     C                   ENDIF
      *
     C                   IF        WPEDAT <> *LOVAL
     C     *DMY          MOVE      WPEDAT        A2EDAT
     C                   ENDIF
      *
      * Skriver heading
      *
     C                   WRITE     A1HDR
     C                   WRITE     A2HDR
     C                   WRITE     A3HDR
      *
     C                   ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for utplukk til - og utkskrift av detaljlinje         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     DETALJ        BEGSR
      *
     C                   MOVE      WPLDOR        WWLDOR
     C                   MOVE      WPHGRF        WWHGRP
     C                   MOVE      WPUGRF        WWUGRP
     C     jvarl9_key    SETLL     jvarl9
     C                   READ      jvarl9                                 60
      *
     C     *IN60         DOWEQ     *OFF
      *
      * Test på leverandør
      *
     C     WPLDOR        IFNE      0
     C     JVLDOR        ANDNE     WPLDOR
     C                   LEAVE
     C                   ENDIF
      *
      * Test på oppdateringskode. Varer som er ikke er behandlet skal ikke
      * skrives ut
      *
     C***  JVKODE        IFGE      2
     C***                GOTO      NESTE_VARE
     C***                ENDIF
      *
      * Test på signatur
      *
     C     WPeusr        IFNE      *BLANK
     C     JVeusr        ANDNE     WPeusr
     C                   GOTO      NESTE_VARE
     C                   ENDIF
      *
      * Test på hovedgruppe/undergruppe
      *
     C     WPHGRF        IFNE      0
     C     JVHGRP        IFLT      WPHGRF
     C     JVUGRP        ORLT      WPUGRF
     C     JVHGRP        ORGT      WPHGRT
     C     JVUGRP        ORGT      WPUGRT
     C                   GOTO      NESTE_VARE
     C                   ENDIF
     C                   ENDIF
      *
      * Test på varenummer
      *
     C     WPVARF        IFNE      *BLANK
     C     JVVARE        IFLT      WPVARF
     C     JVVARE        ORGT      WPVART
     C                   GOTO      NESTE_VARE
     C                   ENDIF
     C                   ENDIF
      *
      * Test på endringsdato
      *
     C     WPEDAF        IFNE      *LOVAL
     C     JVEDAT        IFLT      WPEDAF
     C     JVEDAT        ORGT      WPEDAT
     C                   GOTO      NESTE_VARE
     C                   ENDIF
     C                   ENDIF
      *
      * Behandling ved brudd på leverandør
      *
     C     WPLDOR        IFEQ      0
     C     JVLDOR        ANDNE     A4LDOR
     C                   MOVE      JVLDOR        A4LDOR
     C                   MOVE      JVLDOR        wwldor
     C     KEY04         CHAIN     JLEVL1                             60
     C     *IN60         IFEQ      *OFF
     C                   MOVEL     JLNAVN        A4LNAV
     C                   ELSE
     C                   MOVE      *BLANK        A4LNAV
     C                   ENDIF
     C     WFØRSTE       IFEQ      *ON
     C                   MOVE      *OFF          WFØRSTE
     C                   ELSE
     C                   EXSR      OVERFLOW
     C                   ENDIF
     C                   WRITE     A4HDR
     C                   ENDIF
      *
      * Flytter verdier til detaljlinje
      *
     C                   MOVE      JVHGRP        D1HGRP
     C                   MOVE      JVUGRP        D1UGRP
     C                   MOVE      JVVARE        D1VARE
     C                   MOVE      JVTEK1        D1TEK1
     C                   MOVE      JVTEK2        D1TEK2
     C                   MOVEL     JVETE1        D1ETE1
      *
      * Henter søketekster
      *
     C                   CLEAR                   WSTEK
      *
     c                   eval      i = 0
      *
     C                   MOVE      JVVARE        WWVARE
     C     JSOKL1_KEY    SETLL     JSOKL1
     C     JSOKL1_KEY    READE     JSOKL1                                 61
     c                   dow       *in61 = *off and
     c                             i < 3
      *
     c                   if        jsokod = 'L'
     c                   eval      i = i + 1
     C                   MOVEL     JSSTEK        WSTEK(I)
     c                   endif
      *
     C     JSOKL1_KEY    READE     JSOKL1                                 61
     C                   ENDDO
      *
     C                   MOVEL     WSTEK(1)      D1STE1
     C                   MOVEL     WSTEK(2)      D1STE2
     C                   MOVEL     WSTEK(3)      D1STE3
      *
      * Skriv detaljlinje
      *
     C                   WRITE     D1DTL                                30
      *
      * Er det overflow ?
      *
     C     *IN30         CASEQ     *ON           OVERFLOW
     C                   ENDCS
      *
     C     NESTE_VARE    TAG
      *
     C                   READ      jvarl9                                 60
      *
     C                   ENDDO
      *
     C                   ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for overflow                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     OVERFLOW      BEGSR
      *
     C                   WRITE     A1HDR
     C                   WRITE     A3HDR
      *
     C                   ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     *INZSR        BEGSR
      *
     C                   MOVE      *ZERO         I                 1 0
     C                   MOVE      *ZERO         WWfirm            3 0
     C                   MOVE      *ON           WFØRSTE           1
      *
     C     *LIKE         DEFINE    JVLDOR        WWLDOR
     C     *LIKE         DEFINE    JVOGRP        WWOGRP
     C     *LIKE         DEFINE    JVHGRP        WWHGRP
     C     *LIKE         DEFINE    JVUGRP        WWUGRP
     C     *LIKE         DEFINE    JVVARE        WWVARE
     C     *LIKE         DEFINE    jlldor        wwldor
     C     *LIKE         DEFINE    ABUSER        WWUSER
      *
      * Paramtre
      *
     C     *ENTRY        PLIST
6.11 c                   parm                    p_parm
      *
      * Key for vare
      *
     C     jvarl9_key    KLIST
     C                   KFLD                    WWLDOR
     C                   KFLD                    WWOGRP
     C                   KFLD                    WWHGRP
     C                   KFLD                    WWUGRP
      *
      * Key for oppslag firmaregister
      *
     C     KEY02         KLIST
     C                   KFLD                    WWFIRM
      *
      * Key for oppslag søketekst-register
      *
     C     JSOKL1_KEY    KLIST
     C                   KFLD                    WWVARE
      *
      * Key for oppslag leverandør-register
      *
     C     KEY04         KLIST
     C                   KFLD                    wwldor
      *
      * Key for oppslag bruker-register
      *
     C     KEY05         KLIST
     C                   KFLD                    WWUSER
      *
      * Flytter parameter til datasturuktur
      *
6.11 c                   eval      d_list = p_parm
      *
     C                   MOVE      WPFIRM        WWFIRM
     C                   MOVE      999999        A4LDOR
      *
     C                   ENDSR
