     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JV862R
      * Beskrivelse . . : Oppdater fra NOBB, pakning
      *
      *                   Oppdaterer/oppretter pakninger fra
      *                   NOBB 80 (XML-format)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       040414 BKV
6.30  * R6.30 230414 BKV  Støtte for NIB nr
6.31  * R6.30 240414 BKV  Bugfix. Lagt inn const for dim variabler
6.32  * R6.30 270514 BKV  JWOFAP = 0, var = 1
6.33  * R6.30 160714 BKV  Utvidket log record
6.34  * R6.30 160714 BKV  Leverandør sin utgår dato settes ikke på pakning og pris
6.35  * R6.30 101014 BKV  Fjernet oppdatering av jwodat ved update 000
6.36  * R6.30 071114 BKV  Bug fix. Manglet clear av jvpklur + jwofap=0 for write
6.37  * R6.30 071114 BKV  Ikke sett utgårdato for egne leverandører > 900000
6.38  * R6.30 020216 bof  Skal ikke sette tildato på egne pakninger
6.39  * R6.30 290316 bkv  Sette alle priser til utgått
6.3A  * R6.30 070416 bkv  Setter vare-ldor JVLEPF, utgått, istdet for pakining/pris
6.3B  * R6.30 070416 BKV  Logge utgåtte vare-leverandør
6.3C  * R6.30 280317 BKV  Rettet feil: oppdaterer ikke prisenhet 000
6.3D  * R6.30 190917 BKV  ldor = 9999 er egen leverandor
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvpklu    uf a e           k disk    rename(jvpkpfr:jvpklur)
     f                                     infsr(FileErr)
     fjvpglu    uf a e           k disk    rename(jvpgpfr:jvpglur)
     f                                     infsr(FileErr)
     fjvarlu    uf a e           k disk    rename(jvarpfr:jvarlur)
     f                                     infsr(FileErr)
     fjvprlu    uf a e           k disk    rename(jvprpfr:jvprlur)
     f                                     infsr(FileErr)
6.3A fjvlelu    uf a e           k disk    rename(jvlepfr:jvlelur)
6.3A f                                     infsr(FileErr)
     fjjltlu    uf a e           k disk    rename(jjltpfr:jjltlur)
     f                                     infsr(FileErr)
      * Definering av tabell med meldinger
     d a_meld          s            100    dim(4) ctdata perrcd(1)
      *
      * Definering av variable i nøkler
      *
     d jvpglu_vare     s                   like(jwvare)
     d jvpglu_pakn     s                   like(jwpakn)
     d jvpklu_vare     s                   like(jwvare)
     d jvpklu_pakn     s                   like(jwpakn)
     d jvpklu_leve     s                   like(jwldor)
     d jvpklr_vare     s                   like(jvvare)
     d jvpklr_leve     s                   like(jwldor)
     d jvprlu_vare     s                   like(jxvare)
     d jvprlu_ldor     s                   like(jxldor)
6.3A d jvlelu_lvnr     s                   like(jvlvnr)
6.3A d jvlel1_lvnr     s                   like(jvlvnr)
6.3A d jvlel1_lldo     s                   like(jvlldo)
      *
      *
      * Definering av parametre
      *
     d p_stat          s              1
     d b_enhe          s              1    inz(*off)
      *
      * Brukes som parametre til jobblogging
      *
     d p_jbid          s             41
     d p_jmld          s            200
     d p_jsts          s              2  0
      *
      * const
      *
6.31 d c_ant_pak       c                   50
6.31 d c_ant_lev       c                   50
      *
      * Parameter
      *
     d p_prec          s            500    Dim(50)
6.30 d p_sist_var      s                   like(jwvare)
     d p_levLest       s                   like(jwldor) Dim(50)
6.3B d p_job_log       s            150
     d p_inlr          s              1
      *
      * Job log out
      *
     d                 ds
6.3B d d_job_log                    150
     d  w_vare                        8    overlay(d_job_log:2)
     d  w_ojva                        8    overlay(d_job_log:10)
     d   w_ojva_num                   8  0 overlay(w_ojva:1)
     d  w_jvar                        8    overlay(d_job_log:18)
     d   w_jvar_num                   8  0 overlay(w_jvar:1)
     d  w_jvdt                        8    overlay(d_job_log:26)
     d   w_jvdt_num                   8  0 overlay(w_jvdt:1)
     d  w_jvpk                        8    overlay(d_job_log:34)
     d   w_jvpk_num                   8  0 overlay(w_jvpk:1)
     d  w_jvpg                        8    overlay(d_job_log:42)
     d   w_jvpg_num                   8  0 overlay(w_jvpg:1)
     d  w_jvpke                       8    overlay(d_job_log:50)
     d   w_jvpke_num                  8  0 overlay(w_jvpke:1)
     d  w_jvpku                       8    overlay(d_job_log:58)
     d   w_jvpku_num                  8  0 overlay(w_jvpku:1)
     d  w_jvpru                       8    overlay(d_job_log:66)
     d   w_jvpru_num                  8  0 overlay(w_jvpru:1)
6.33 d  w_jvlevn                      8    overlay(d_job_log:74)
6.33 d   w_jvlevn_num                 8  0 overlay(w_jvlevn:1)
6.33 d  w_jvleve                      8    overlay(d_job_log:82)
6.33 d   w_jvleve_num                 8  0 overlay(w_jvleve:1)
6.3B d  w_jvlevu                      8    overlay(d_job_log:90)
6.3B d   w_jvlevu_num                 8  0 overlay(w_jvlevu:1)
6.3B d  w_lnr                         5  0 overlay(d_job_log:98)
6.3B d  w_jbid                       41    overlay(d_job_log:103)

      *
6.31 d d_sist_var      s              8
     d d_levLest       s                   like(jwldor) Dim(50)

      *
      * Definering av variable
      *
     d n               s              2  0 Inz(0)
     d n1              s              2  0 Inz(0)
     d d_pakLest       s                   like(jwpakn) dim(50)
      *
      *  Dataelementer pakning
      *
     d                 ds
     d d_prec                       500
     d  d_pakn                        3    overlay(d_prec:2)
     d  d_pakn_num                    3  0 overlay(d_pakn:1)
     d  d_pkla1                       1    overlay(d_prec:5)
     d  d_pkla2                       1    overlay(d_prec:6)
     d  d_pkla3                       1    overlay(d_prec:7)
     d  d_enhe                        3    overlay(d_prec:8)
     d  d_epeh                        1    overlay(d_prec:11)
     d   d_erpeh_num                  1  0 overlay(d_epeh:1)
     d  d_ePSE                        1    overlay(d_prec:12)
     d   d_pse_num                    1  0 overlay(d_ePSE:1)
     d  d_ofak                       15    overlay(d_prec:13)
     d   d_ofak_num                  14  6 overlay(d_ofak:1)
     d  d_antp                       15    overlay(d_prec:28)
     d   d_betAnt_num                14  6 overlay(d_antp:1)
     d  d_bestarAvEht                 5    overlay(d_prec:43)
     d  d_GTIN                       20    overlay(d_prec:48)
     d   d_GTIN_num                  14  0 overlay(d_GTIN:1)
     d  d_GTINType                   10    overlay(d_prec:68)
     d  d_lfop                        5    overlay(d_prec:78)
     d   d_lagp_num                   1  0 overlay(d_lfop:1)
     d  d_best                        5    overlay(d_prec:83)
     d   d_best_num                   1  0 overlay(d_best:1)
     d  d_fdat                       15    overlay(d_prec:88)
     d   d_fraD_iso                    d   overlay(d_fdat:1) datfmt(*iso)
     d  d_tdat                       15    overlay(d_prec:103)
     d   d_tilD_iso                    d   overlay(d_tdat:1) datfmt(*iso)
     d  d_nbre                       15    overlay(d_prec:118)
     d   d_BredN_num                 10  4 overlay(d_nbre:1)
     d  d_nlen                       15    overlay(d_prec:133)
     d   d_LengN_num                 10  4 overlay(d_nlen:1)
     d  d_nhoy                       15    overlay(d_prec:148)
     d   d_HoyN_num                  10  4 overlay(d_nhoy:1)
     d  d_nvek                       15    overlay(d_prec:163)
     d   d_VektN_num                 13  4 overlay(d_nvek:1)
     d  d_nvol                       15    overlay(d_prec:178)
     d   d_VolN_num                   9  5 overlay(d_nvol:1)
     d  d_bred                       15    overlay(d_prec:193)
     d   d_BredB_num                 10  4 overlay(d_bred:1)
     d  d_leng                       15    overlay(d_prec:208)
     d   d_LengB_num                 10  4 overlay(d_leng:1)
     d  d_hoyd                       15    overlay(d_prec:223)
     d   d_HoyB_num                  10  4 overlay(d_hoyd:1)
     d  d_vekt                       15    overlay(d_prec:238)
     d   d_VektB_num                 13  4 overlay(d_vekt:1)
     d  d_volu                       15    overlay(d_prec:253)
     d   d_VolB_num                   9  5 overlay(d_volu:1)
     d  d_papp                        6    overlay(d_prec:268)
     d   d_papp_num                   6  0 overlay(d_papp:1)
     d  d_pmsb                       15    overlay(d_prec:274)
     d   d_pmsb_num                  14  4 overlay(d_pmsb:1)
6.30 d  d_varp                        8    overlay(d_prec:289)
6.30 d   d_vare_numP                  8  0 overlay(d_varp:1)
     d  d_delp                      100    overlay(d_prec:297)
     d   d_del_numP                   6  0 overlay(d_delp:1)
     d  d_prehP                       3    overlay(d_prec:397)
     d  d_noda                       25    overlay(d_prec:400)
     d   d_creD_iso                    d   overlay(d_noda:1) datfmt(*iso)
     d  d_neda                       25    overlay(d_prec:425)
     d   d_updD_iso                    d   overlay(d_neda:1) datfmt(*iso)
     d  d_udat                       10    overlay(d_prec:450)
     d   d_udat_iso                    d   overlay(d_udat:1) datfmt(*iso)
      *
      *
      *
      * Definering av variable
      *
     d b_vfel          s              1    inz(*off)
     d d_idag          s                   like(jwedat)
     d d_utgdat        s                   like(jwedat)
     d* w_stat          s                   like(jvstat)
     d w_ttxt          s                   like(jjttxt)
     d w_ttyp          s                   like(jjttyp)
     d w_tnob          s                   like(jjtnob)
     d w_tldo          s                   like(jjtldo)
      *
      *
      *
      * Definering av Local Data Area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      *
      * Datastruktur for informasjon ved exceptions
      *
     d PgmError       sds
     d  proc_name        *proc
     d  pgm_status       *STATUS
     d  prv_status            16     20s 0
     d  line_numm             21     28
     d  routine          *ROUTINE
     d  parms            *PARMS
     d  excp_type             40     42
     d  excp_numm             43     46
     d  pgm_lib               81     90
     d  excp_data             91    170
     d  excp_id              171    174
     d  date                 191    198
     d  year                 199    200s 0
     d  last_file            201    208
     d  file_info            209    243
     d  job_name             244    253
     d  job_user             254    263
     d  job_numb             264    269s 0
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   time                    d_idag
      *
     c                   eval      d_job_log = p_job_log
     c                   eval      p_jbid = w_jbid
     c                   if        p_inlr = *on
     c
      * hvis d_sist_vare har en verdi, så er alle pakninger lest for varen
      * Da skal det evt ryddes i pakninger, hvis noen leverandør mangler i XML
      * p_levLest inneholder leverandører lest. Sjekkes mot leve i DB
6.31 c                   eval      d_sist_var = *blank
6.30 c                   eval      d_sist_var = p_sist_var
6.31 c                   if        (%Trim(d_sist_var) <> '')
6.31 c                   eval      n1 = 0
6.31 c                   for       n1 = 1 to c_ant_lev
6.31 c                   eval      d_levLest(n1) = *zero
6.31 c                   endfor
6.31 c                   eval      n1 = 0
6.31 c                   eval      d_levLest = p_levLest
     c                   exsr      utg_levd
     c                   else
     c
     c                   eval      n1 = 0
6.31 c                   for       n1 = 1 to c_ant_pak
     c                   eval      d_pakLest(n1) = *blank
     c                   endfor
     c                   eval      n1 = 0
     c
     c                   exsr      skr_pakn
     c                   exsr      utg_pakn
6.34 c*                  exsr      utg_levd2
     c
     c                   endif
     c
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     avslutt       tag
     c
     c                   eval      p_job_log = d_job_log
     c
     c                   if        p_inlr <> *on
     c                   eval      *inlr = *on
     c                   endif
     c
     c                   return
     c
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer vare-pakning
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_pakn      begsr
      *
      * Henter første pakning og sjekker om vare nr er satt
      *
     c                   eval      d_prec = p_prec(1)
6.30 c                   if        d_vare_numP = *zero
     c                   leavesr
     c                   endif
      *
6.36 c                   clear                   jvpklur
      *
6.30 c                   eval      jvpklu_vare = d_varp
     c                   eval      jvpklu_leve = d_del_numP
     c                   eval      jvpklu_pakn = '000'
     c     jvpklu_key    chain     jvpklu
     c                   if        not %found(jvpklu)
     c                   clear                   jvpklur
      *
6.30 c                   eval      jwvare = d_varp
     c                   eval      jwldor = d_del_numP
     c                   eval      jwpakn = '000'
     c                   eval      jwenhe = d_prehP
     c                   eval      jwofak = 1
6.32 c                   eval      jwofap = 0
     c                   eval      jweusr = l_user
      *
     c                   time                    jwodat
     c                   time                    jwedat
     c                   time                    jwetim
      *
     c                   write     jvpklur
6.3C c                   elseif    (jwenhe <> d_prehP)
     c                   eval      jwenhe = d_prehP
     c                   eval      jwofak = 1
6.32 c                   eval      jwofap = 0
     c                   eval      jweusr = l_user
     c                   eval      jwtdat = *loval
      *
6.35 c*                  time                    jwodat
     c                   time                    jwedat
     c                   time                    jwetim
      *
     c                   update    jvpklur
     c
     c                   endif
      *
6.31 c                   for       n = 1 to c_ant_pak
     c                   eval      d_prec = p_prec(n)
6.30 c                   if        d_vare_numP = *zero
     c                   leave
     c                   endif
      *
6.36 c                   clear                   jvpklur
      *
6.30 c                   eval      jvpklu_vare = d_varp
     c                   eval      jvpklu_leve = d_del_numP
     c                   eval      jvpklu_pakn = d_pakn
     c     jvpklu_key    chain     jvpklu
      *
     c                   eval      jwpkla = d_pkla1
     c                   eval      jwenhe = d_enhe
     c                   eval      jwofak = d_ofak_num
     c                   eval      jwbred = d_BredB_num
     c                   eval      jwleng = d_LengB_num
     c                   eval      jwhoyd = d_HoyB_num
     c                   eval      jwvekt = d_VektB_num
     c                   eval      jwvolu = d_VolB_num
     c                   eval      jwnbre = d_BredN_num
     c                   eval      jwnlen = d_LengN_num
     c                   eval      jwnhoy = d_HoyN_num
     c                   eval      jwnvek = d_VektN_num
     c                   eval      jwnvol = d_VolN_num
     c                   eval      jwgtin = d_GTIN_num
     c                   eval      jwbest = d_best_num
     c                   eval      jwpsen = d_pse_num
     c                   eval      jwpaks = d_bestarAvEht
     c                   eval      jwantp = d_betAnt_num
     c                   eval      jwpapp = d_papp_num
     c                   eval      jwpmsb = d_pmsb_num
     c                   eval      jwlfor = d_lagp_num
     c                   eval      jwgtyp = d_GTINType
     c                   eval      jwepse = d_erpeh_num
     c                   eval      jwfdat = d_fraD_iso
     c                   eval      jwtdat = d_tilD_iso
     c                   eval      jwnoda = d_creD_iso
     c                   eval      jwneda = d_updD_iso
     c                   eval      jwnsda = *loval
     c                   time                    jwedat
     c                   time                    jwetim
     c                   eval      jweusr = l_user
      *
     c                   if        %found(jvpklu)
      *
      * Sjekk om gamle og nye enheter stemmer oversens.
     c                   eval      b_enhe = *off
      * Ser det samme enhet og omregningsfaktor ?
     c                   if        jwenhe <> d_enhe
     c                   eval      b_enhe = *on
     c                   endif
     c                   if        jwofak <> d_ofak_num
     c                   eval      b_enhe = *on
     c                   endif
      * Legg ut på g register ?
     c                   if        b_enhe = *on
6.30 c                   eval      jvpglu_vare = d_varp
     c                   eval      jvpglu_pakn = d_enhe
     c     jvpglu_key    chain     jvpglu
     c                   if        %found(jvpglu)
     c                   else
     c                   exsr      skr_gpakn
     c                   endif
     c                   endif
     c                   eval      w_jvpke_num = w_jvpke_num + 1
     c                   update    jvpklur
      *
     c                   else
      *
6.36 c                   eval      jwofap = 0
6.30 c                   eval      jwvare = d_varp
     c                   eval      jwldor = d_del_numP
     c                   eval      jwpakn = d_pakn
     c                   time                    jwodat
      *
     c                   eval      w_jvpk_num = w_jvpk_num + 1
     c                   write     jvpklur
      *
     c                   endif
     c
     c                   eval      n1 = n1 + 1
     c                   eval      d_pakLest(n1) =  jwpakn
     c
     c                   endfor
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sette utgårdato for enkelt pakning
      * Settes hvis enkelt pakning mangler i XML, sammenlignet med DB
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utg_pakn      begsr
      *
      * Henter første pakning og sjekker om vare nr er satt
      *
     c                   eval      d_prec = p_prec(1)
6.30 c                   if        d_vare_numP = *zero
     c                   leavesr
     c                   endif
     c
     c                   eval      d_utgdat = d_idag
     c
6.30 c                   eval      jvpklr_vare = d_varp
     c                   eval      jvpklr_leve = d_del_numP
      *
6.30 c                   eval      jvpklu_vare = d_varp
     c                   eval      jvpklu_leve = d_del_numP
     c                   eval      jvpklu_pakn = *loval
     c     jvpklu_key    setll     jvpklu
     c     jvpklr_key    reade     jvpklur
     c                   dow       not %eof
6.38 c                   if        jwrsts <> 1
     c                   if        (%lookup(jwpakn:d_pakLest) = *zero)
     c                             and (jwtdat = *loval or jwtdat > d_utgdat)
     c                             and jwpakn <> '000'
     c                   eval      jwtdat = d_utgdat
     c                   time                    jwedat
     c                   time                    jwetim
     c                   update    jvpklur
     c                   eval      w_jvpku_num = w_jvpku_num + 1
      * logger endring
     c                   eval      w_ttyp = 'B'
     c                   eval      w_ttxt = %trim(a_meld(2))+' '+jwpakn
     c                               +' '+%Char(d_utgdat)
6.30 c                   eval      w_tnob = d_vare_numP
     c                   eval      w_tldo = jwldor
     c                   exsr      logg_utg
     c                   else
     c                   unlock    jvpklu
     c                   endif
6.38 c                   endif
     c     jvpklr_key    reade     jvpklur
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sette utgårdato for alle pakninger for en leveradør
      * Settes hvis leverandør mangler i XML sammenlignet med DB
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      /FREE
       begsr utg_levd;

6.3A   //  jvpklr_vare = d_sist_var;
         //
6.3A   //  jvpklu_vare = d_sist_var;
6.3A   //  jvpklu_leve = *zero;
6.3A   //  jvpklu_pakn = *blanks;

         d_utgdat = d_idag;

6.3A   jvlel1_lvnr = d_sist_var;
6.3A   jvlel1_lldo = *zero;
6.3A   jvlelu_lvnr = d_sist_var;
6.3A   setll jvlel1_key jvlelu;
6.3A   reade jvlelu_key jvlelur;
6.3A   dow not %eof;
6.3A   //chain jvlelu_key jvlelur;
6.3D   if (jvlldo < 900000 and jvlldo <> 9999) // ikke utgårdato for "egne" leverandører
6.3A          and (%lookup(jvlldo:d_levLest) = *zero)
6.3A          and (jvluda = *loval or (jvluda > d_utgdat));
6.3A         jvluda = d_utgdat;
6.3A         jvleda = %Date();
6.3A         jvleti = %Time();
6.3A         jvleus = l_user;
6.3A         update jvlelur;
6.3A         w_jvlevu_num = w_jvlevu_num + 1;
6.3A         w_ttyp = 'F';
6.3A         w_ttxt = %trim(a_meld(4))+' '+%Char(d_utgdat);
6.3A         w_tnob = %Int(jvlvnr);
6.3A         w_tldo = jvlldo;
6.3A         exsr logg_utg;
6.3A    else;
6.3A         unlock jvlelu;
6.3A    endif;
6.3A    reade jvlelu_key jvlelur;
6.3A    //
6.3A   enddo;




6.3A   //  setll jvpklu_key jvpklu;
6.3A   //  reade jvpklr_key2 jvpklur;
6.3A   //  dow not %eof;
6.3A   //    // hvis leverandør ikke finnes i XML,
6.3A   //    // og utgåttdato ikke satt, sett utgåttdato
6.3A   //    if (jwldor < 900000) // ikke utgårdato for "egne" leverandører
6.3A   //       and (%lookup(jwldor:d_levLest) = *zero)
6.3A   //       and (jwtdat = *loval or (jwtdat > d_utgdat));
6.3A   //      jwtdat = d_utgdat;
6.3A   //      update jvpklur;
6.3A   //      w_jvpku_num = w_jvpku_num + 1;
6.3A   //      // logger endring
6.3A   //      w_ttyp = 'B';
6.3A   //      w_ttxt = %trim(a_meld(2))+' '+jwpakn
6.3A   //          +' '+%Char(d_utgdat);
6.3A   //      w_tnob = %Int(jwvare);
6.3A   //      w_tldo = jwldor;
6.3A   //      exsr logg_utg;
6.3A   //      // prisen må også ha utgår dato
6.3A   //      exsr utg_pris;
6.3A   //    else;
6.3A   //      unlock jvpklu;
6.3A   //    endif;
6.3A   //    reade jvpklr_key2 jvpklur;
6.3A   //    //
6.3A   //  enddo;
         //
       endsr;
       //
      /END-FREE
      *
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34  * Subrutine for å sette utgårdato pga leverandør er utgått
6.34  * denne er ikke ferdig. Usikker hva som skal gjøres hvis datoer konkurerer
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34  *
6.34 c*    utg_levd2     begsr
6.34 c*
6.34 c*                  if        (d_udat_iso = *loval)
6.34 c*                  leavesr
6.34 c*                  endif
6.34 c*                  eval      d_utgdat = d_udat_iso
6.34 c*
6.30 c*                  eval      jvpklr_vare = d_varp
6.34  *
6.30 c*                  eval      jvpklu_vare = d_varp
6.34 c*                  eval      jvpklu_leve = d_del_numP
6.34 c*                  eval      jvpklu_pakn = *blanks
6.34 c*    jvpklu_key    setll     jvpklu
6.34 c*    jvpklr_key    reade     jvpklur
6.34 c*                  dow       not %eof
6.34  *
6.34 c*                  if        (jwldor = d_del_numP)
6.34 c*                            and (jwtdat = *loval or jwtdat > d_utgdat)
6.34 c*                  eval      jwtdat = d_utgdat
6.34 c*                  update    jvpklur
6.34 c*                  eval      w_jvpku_num = w_jvpku_num + 1
      * logger endring
6.34 c*                  eval      w_ttyp = 'C'
6.34 c*                  eval      w_ttxt = %trim(a_meld(2))+' '+jwpakn
6.34 c*                              +' '+%Char(d_utgdat)
6.34 c*                  eval      w_tnob = %Int(jwvare)
6.34 c*                  eval      w_tldo = jwldor
6.34 c*                  exsr      logg_utg
      * prisen må også ha utgår dato
6.34 c*                  exsr      utg_pris
6.34 c*                  else
6.34 c*                  unlock    jvpklu
6.34 c*                  endif
6.34 c*    jvpklr_key    reade     jvpklur
      *
6.34 c*                  enddo
      *
6.34 c*                  endsr
      *
      ** - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      ** Subrutine for å sette utgårdato for pris, hvis varen utgår fra lev
      ** - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      **
     c*     utg_pris      begsr
      **
     c*                   clear                   jvprlur
      **
     c*                   eval      jvprlu_vare = jwvare
     c*                   eval      jvprlu_ldor = jwldor
6.39 c**    jvprlu_key    chain     jvprlur
6.39 c*     jvprlu_key    setll     jvprlu
6.39 c*     jvprlu_key    reade     jvprlur
6.39 c*                   dow       not %eof
6.39 c*                   if        jxrsts <> 1
      **
      **
     c*                   if        %found
     c*                   if        ((jxtdat = *loval) or (jxtdat > d_utgdat))
     c*                   eval      jxtdat = d_utgdat
6.39 c*                   eval      jxeusr = l_user
6.39 c*                   time                    jxedat
6.39 c*                   time                    jxetim
     c*                   update    jvprlur
      **
     c*                   eval      w_jvpru_num = w_jvpru_num + 1
     c*                   eval      w_ttyp = 'D'
     c*                   eval      w_ttxt = %trim(a_meld(1))+' '+%Char(d_utgdat)
     c*                   eval      w_tnob = %Int(jwvare)
     c*                   eval      w_tldo = jwldor
     c*                   exsr      logg_utg
     c*                   endif
     c*                   endif
     c*
6.39 c*                   endif
6.39 c*     jvprlu_key    reade     jvprlur
6.39 c*                   enddo
     c*                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å logge utgått pakning/pris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     logg_utg      begsr
      *
     c                   eval       w_lnr = w_lnr  + 1
     c                   clear                   jjltlur
     c                   eval      jjtfir = l_firm
     c                   eval      jjtjid = p_jbid
     c                   eval      jjtlin = w_lnr
     c                   eval      jjtsta = 10
     c                   eval      jjttyp = w_ttyp
     c                   eval      jjtnob = w_tnob
     c                   eval      jjtldo = w_tldo
     c                   eval      jjttxt = w_ttxt
     c                   time                    jjtoda
     c                   time                    jjtoti
     c                   eval      jjtous = l_user
     c                   write     jjltlur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for opretting av pakninger på g register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_gpakn     begsr
      *
     c                   clear                   jvpglur
      *
6.30 c                   eval      jwgvar = d_varp
     c                   eval      jwgpak = jwpakn
     c                   eval      jwgpkl = jwpkla
     c                   eval      jwgenh = jwenhe
     c                   eval      jwgofa = jwofak
     c                   eval      jwgbre = jwbred
     c                   eval      jwglen = jwleng
     c                   eval      jwghoy = jwhoyd
     c                   eval      jwgvek = jwvekt
     c                   eval      jwgvol = jwvolu
     c                   eval      jwggti = jwgtin
     c                   eval      jwgbes = jwbest
     c                   eval      jwgpse = jwpsen
     c                   eval      jwgpas = jwpaks
     c                   eval      jwganp = jwantp
     c                   eval      jwgldo = jwLdor
     c                   eval      jwgeus = l_user
      *
     c                   eval      jwgnod = jwnoda
     c                   eval      jwgned = jwneda
     c                   eval      jwgnsd = jwnsda
      *
     c                   time                    jwgoda
     c                   time                    jwgeda
     c                   time                    jwgeti
      *
     c                   eval      w_jvpg_num = w_jvpg_num + 1
     c                   write     jvpglur
      *
     c                   endsr

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Error rutine for file errors
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     FileErr       begsr
      *
     c                   eval      p_stat = '1'
     c                   eval      p_jsts = 50
     c                   eval      p_jmld = 'Fil ' + %trim(last_file) +
     c                             ' feil. Info=' + %trim(excp_data) +
     c                             '. Job=' + %trim(job_name) + '/' +
     c                             %trim(job_user) + '/' +
     c                             %char(job_numb)

      *
     c                   call      'AB705R'
     c                   parm                    p_jbid
     c                   parm                    p_jsts
     c                   parm                    p_jmld
      *
     c                   goto      avslutt
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_prec
6.30 c                   parm                    p_sist_var
     c                   parm                    p_levLest
     c                   parm                    p_job_log
     c                   parm                    p_inlr
     c
      *
      * Key for lesing av paking pr vare og leverandor
      *
     c     jvpklr_key    klist
     c                   kfld                    jvpklr_vare
     c                   kfld                    jvpklr_leve
      *
      * Key for lesing av paking pr vare
      *
     c     jvpklr_key2   klist
     c                   kfld                    jvpklr_vare

      *
      * Key for oppdatering av vare-pakning 'gamle' verdier
      *
     c     jvpglu_key    klist
     c                   kfld                    jvpglu_vare
     c                   kfld                    jvpglu_pakn
      *
      * Key for oppdatering av vare-pakning
      *
     c     jvpklu_key    klist
     c                   kfld                    jvpklu_vare
     c                   kfld                    jvpklu_leve
     c                   kfld                    jvpklu_pakn
      *
      * Key for oppdatering av vare-pris
      *
     c     jvprlu_key    klist
     c                   kfld                    jvprlu_vare
     c                   kfld                    jvprlu_ldor
      *
      * Key for oppdatering av vare leverandør
      *
     c     jvlelu_key    klist
     c                   kfld                    jvlelu_lvnr
      *
      * Key for lese av vare leverandør
      *
     c     jvlel1_key    klist
     c                   kfld                    jvlel1_lvnr
     c                   kfld                    jvlel1_lldo
      *
      *
     c                   endsr
**
Pris utgått dato:
Pakning utgått dato:
Vare-leverandør utgått dato:
