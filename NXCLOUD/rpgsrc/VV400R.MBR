     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VV400R
      * Beskrivelse . . : Vare, sletting av vare m/relasjoner
      *
      * NB! Dersom det legges inn sletting av vare fra flere tabeller må
      *     også jobben for kopiering fra filgruppe til filgruppe endres
      *     m.h.p. overriding av tabeller mot databibliotek !!!
      *
      *     LR-flagg må være lik '1' for at *inLR ikke skal settes
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       120199 HKO  Nytt program
      *       250200 HKO  Lagt inn sletting av notat
      *       130102 HKO  Sletting av lager er erstattet med kall av
      *                   program for sletting i lager
      *                   Lagt inn sletting av påslag-linje
      *       201003 HKO  Merker vare i vare-slettet med slettet tidspunkt
      *                   og slettet av
      *       161103 HKO  Fjernet behandling av rabattgruppe-knytning
      *       180104 HKO  Lagt inn sletting av bruksrett-knytning
      *       100204 HKO  Lagt inn *inLR som parameter
      * 6.10  250609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  070110 bof  Fjerner vare fra påslag og vare-enh-prisgiver
6.20  * 6.20  051110 bof  Utvidet med lager på sortiment
6.21  * 6.20  020511 bof  Utvidet varegr/lev.nr på vtilpf
6.31  *       180817 HKO  Lagt inn sletting av vare-erstatningsvare
6.32  *       280618 bof  Hvis frigjort i JVDTPF, så hold tilbake.
6.33  *       231118 bof  Lagt inn sletting av vvlepf
8.01  *PRG2   200622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarlu    uf   e           k disk    rename(vvarpfr:vvarlur)
     fvvaslu    uf a e           k disk    rename(vvaspfr:vvaslur)
      *
     fvsdtl7    uf   e           k disk    rename(vsdtpfr:vsdtl7r)
     fvpdtla    uf   e           k disk    rename(vpdtpfr:vpdtlar)
     fvbrkl6    uf   e           k disk    rename(vbrkpfr:vbrkl6r)
     ffpril5    uf   e           k disk    rename(fpripfr:fpril5r)
     ffrabla    uf   e           k disk    rename(frabpfr:frablar)
     fvvenlu    uf   e           k disk    rename(vvenpfr:vvenlur)
6.11 fvveplu    uf   e           k disk    rename(vveppfr:vveplur)
     fvvprlu    uf   e           k disk    rename(vvprpfr:vvprlur)
     fvvsolu    uf   e           k disk    rename(vvsopfr:vvsolur)
     fvsoklu    uf   e           k disk    rename(vsokpfr:vsoklur)
6.21 fvtill9    uf   e           k disk    rename(vtilpfr:vtill9r)
     fvetilu    uf   e           k disk    rename(vetipfr:vetilur)
     fveanl2    uf   e           k disk    rename(veanpfr:veanl2r)
6.31 fvverlu    uf   e           k disk    rename(vverpfr:vverlur)
6.31 fvverl2    uf   e           k disk    rename(vverpfr:vverl2r)
6.33 fvvlelu    uf   e           k disk    rename(vvlepfr:vvlelur)
6.11 fjpfal6    uf   e           k disk    rename(jpfapfr:jpfal6r)
6.32 fjvdtlu    uf   e           k disk    rename(jvdtpfr:jvdtlur)
     fanotlu    uf   e           k disk    rename(anotpfr:anotlur)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vvfirm)
     d p_vare          s                   like(vvvare)
     d p_inlr          s              1
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av variable i nøkler
      *
     d anotlu_syst     s                   like(ausyst)
     d anotlu_noid     s                   like(aunoid)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vvfirm)
     d w_vare          s                   like(vvvare)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter firma og vare fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_vare = p_vare
      *
      * Fjerner vare fra vareregister
      *
     c     key01         chain     vvarlur                            91
     c                   if        *in91 = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   delete    vvarlur
      *
      * Oppretter vare i register over slettede varer
      * Hver oppmerksom på at recordbeskrivelsen er identisk med VVARLUR
      *
     c     key01         chain     vvaslur                            91
     c                   if        *in91 = *on
     c                   eval      vvsusr = l_user
     c                   time                    vvsdat
     c                   time                    vvstim
6.10 c                   time                    vvodat
6.10 c                   time                    vvotim
6.10 c                   eval      vvousr = l_user
6.10 c                   time                    vvedat
6.10 c                   time                    vvetim
6.10 c                   eval      vveusr = l_user
     c                   write     vvaslur
     c                   endif
      *
      * Fjerner vare fra sortiments-linje-register
      *
     c     key01         setll     vsdtl7
     c     key01         reade     vsdtl7r                                91
     c                   dow       *in91 = *off
     c                   delete    vsdtl7r
     c     key01         reade     vsdtl7r                                91
     c                   enddo
      *
      * Fjerner vare fra påslag-linje-register
      *
     c     key01         setll     vpdtla
     c     key01         reade     vpdtlar                                91
     c                   dow       *in91 = *off
     c                   delete    vpdtlar
     c     key01         reade     vpdtlar                                91
     c                   enddo
      *
      * Fjerner vare fra bruksrett-knytning
      *
     c     key01         setll     vbrkl6
     c     key01         reade     vbrkl6r
     c                   dow       not %eof
     c                   delete    vbrkl6r
     c     key01         reade     vbrkl6r
     c                   enddo
      *
      * Fjerner vare fra spesialpris-register
      *
     c     key01         setll     fpril5
     c     key01         reade     fpril5r                                91
     c                   dow       *in91 = *off
     c                   delete    fpril5r
     c     key01         reade     fpril5r                                91
     c                   enddo
      *
      * Fjerner vare fra rabattmatrise-register
      *
     c     key01         setll     frabla
     c     key01         reade     frablar                                91
     c                   dow       *in91 = *off
     c                   delete    frablar
     c     key01         reade     frablar                                91
     c                   enddo
      *
      * Fjerner vare fra vare-enhets-register
      *
     c     key01         setll     vvenlu
     c     key01         reade     vvenlur                                91
     c                   dow       *in91 = *off
     c                   delete    vvenlur
     c     key01         reade     vvenlur                                91
     c                   enddo
6.11  *
6.11  * Fjerner vare fra vare-enhets-prisgiver-register
6.11  *
6.11 c     key01         setll     vveplu
6.11 c     key01         reade     vveplur                                91
6.11 c                   dow       *in91 = *off
6.11 c                   delete    vveplur
6.11 c     key01         reade     vveplur                                91
6.11 c                   enddo
      *
      * Fjerner vare fra vare-pris-register
      *
     c     key01         setll     vvprlu
     c     key01         reade     vvprlur                                91
     c                   dow       *in91 = *off
     c                   delete    vvprlur
     c     key01         reade     vvprlur                                91
     c                   enddo
      *
      * Fjerner vare fra vare-søketekst-register
      *
     c     key01         setll     vvsolu
     c     key01         reade     vvsolur                                91
     c                   dow       *in91 = *off
     c                   delete    vvsolur
     c     key01         reade     vvsolur                                91
     c                   enddo
      *
      * Fjerner vare fra søketekst-register
      *
     c     key01         setll     vsoklu
     c     key01         reade     vsoklur                                91
     c                   dow       *in91 = *off
     c                   delete    vsoklur
     c     key01         reade     vsoklur                                91
     c                   enddo
      *
      * Fjerner vare fra tibudspris-register
      *
6.21 c     key01         setll     vtill9
6.21 c     key01         reade     vtill9r                                91
     c                   dow       *in91 = *off
6.21 c                   delete    vtill9r
6.21 c     key01         reade     vtill9r                                91
     c                   enddo
      *
      * Fjerner vare fra etikett-register
      *
     c     key01         setll     vetilu
     c     key01         reade     vetilur                                91
     c                   dow       *in91 = *off
     c                   delete    vetilur
     c     key01         reade     vetilur                                91
     c                   enddo
      *
      * Fjerner vare fra EAN-register
      *
     c     key01         setll     veanl2
     c     key01         reade     veanl2r                                91
     c                   dow       *in91 = *off
     c                   delete    veanl2r
     c     key01         reade     veanl2r                                91
     c                   enddo
6.33  *
6.33  * Fjerner vare fra vare-erstatningsvare
6.33  *
6.33 c     key01         setll     vvlelu
6.33 c     key01         reade     vvlelur
6.33 c                   dow       not %eof
6.33 c                   delete    vvlelur
6.33 c     key01         reade     vvlelur
6.33 c                   enddo
6.31  *
6.31  * Fjerner vare fra vare-erstatningsvare
6.31  *
6.31 c     key01         setll     vverlu                                       Ny vare er slettet
6.31 c     key01         reade     vverlur
6.31 c                   dow       not %eof
6.31 c                   delete    vverlur
6.31 c     key01         reade     vverlur
6.31 c                   enddo
6.31  *
6.31 c     key01         setll     vverl2                                       Gml vare er slettet
6.31 c     key01         reade     vverl2r
6.31 c                   dow       not %eof
6.31 c                   delete    vverl2r
6.31 c     key01         reade     vverl2r
6.31 c                   enddo
      *
6.11  *
6.11  * Fjerner vare fra påslag VA
6.11  *
6.11 c     key01         setll     jpfal6
6.11 c     key01         reade     jpfal6r                                91
6.11 c                   dow       *in91 = *off
6.11 c                   delete    jpfal6r
6.11 c     key01         reade     jpfal6r                                91
6.11 c                   enddo
6.32  *
6.32  * Setter holdekode til hold
6.32  *
6.32 c     key01         chain     jvdtlu
6.32 c                   if        %found(jvdtlu)
6.32 c                   eval      jvdhko = 1
6.32 c                   update    jvdtlur
6.32 c                   endif
      *
      * Fjerner notat på vare
      *
     c                   eval      anotlu_syst = 'VVAR'
     c                   eval      anotlu_noid = w_vare
     c     anotlu_key    setll     anotlu
     c     anotlu_key    reade     anotlur                                91
     c                   dow       *in91 = *off
     c                   delete    anotlur
     c     anotlu_key    reade     anotlur                                91
     c                   enddo
      *
      * Fjerner vare fra lager-register
      *
     c                   call      'LL762R'
     c                   parm                    w_firm
     c                   parm                    w_vare
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   if        p_inlr <> *on
     c                   eval      *inlr = *on
     c                   endif
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_inlr
      *
      * Key for oppslag på vare
      *
     c     key01         klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      * Key for oppdatering av notat
      *
     c     anotlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    anotlu_syst
     c                   kfld                    anotlu_noid
      *
     c                   endsr
