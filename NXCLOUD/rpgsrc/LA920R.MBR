     h/title  ASLAGR: Oppdatering av lager-register utfra bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LA920R
      * Beskrivelse . . : Oppdatering av lager-register utfra bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       070313 AMD  Dette programmet er en helt ny versjon av LA920R
      *                   og er skrevet pga. problemer med replikering i
      *                   2DPoint.  Her skal kun avvik oppdateres, og ikke
      *                   alle poster hver gang som i gammel versjon. Det
      *                   er derfor også sterkt forenklet.
6.25  *  6.25 070313 AMD  Her ble LA920RNAT laget først. Derette ble
6.25  *                   LA920R laget utfra dette igjen. Forskjeller er
6.25  *                   merket med 6.25, og går på at det i LA920R er
6.25  *                   skjermbilde for bekreftelse på at jobben skal
6.25  *                   startes.
6.26  *  6.26 020713 bof  Må omregne hvis best.enh. <> lagerenhet
6.27  *       208013 bof  Teste på leveringstype <> 1
6.nu  * 6.30  050614 bof  Utvidelser mtp. nummerutvidelse
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvlaglu    uf   e           k disk    rename(vlagpfr:vlaglur)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flodtlx    if   e           k disk    rename(lodtpfr:lodtlxr)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
6.26 fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
6.26 fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
6.25  *
6.25 fla920d    cf   e             workstn
      *
     d                uds
     d l_user                911    920
     d  dssfir               944    946  0
      *
      * Definering av variable
      *
     d w_firm          s                   like(lofirm)
     d w_anta          s                   like(ldanta)
     d w_bbes          s                   like(vlbbes)
     d w_updt          s              1  0
6.26 d w_omrl          s                   like(veomre)
6.26 d w_omrs          s                   like(veomre)
      *
      * Definering av variable i nøkler
      *
     d vlaglu_vare     s                   like(vlvare)
     d vlaglu_lage     s                   like(vllage)
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d lodtlx_lage     s                   like(ldlage)
     d lodtlx_vare     s                   like(ldvare)
     d votyl1_otyp     s                   like(vaotyp)
     d vltyl1_ltyp     s                   like(valtyp)
6.26 d vvarl1_vare     s                   like(vvvare)
6.26 d vvenl1_vare     s                   like(vevare)
6.25  *
6.25  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.25  * A1BLD Sjekk om jobben virkelig skal kjøres
6.25  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.25  *
6.25 c                   exfmt     a1bld
6.25  *
6.25  * Behandling av funksjonstaster
6.25  *
6.25 c                   select
6.25 c                   when      *inkc or *inkl
6.25 c                   goto      end_pgm
6.25 c                   endsl
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser igjennom LAGER-REGISTER for å sjekke avvik
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      vlaglu_vare = *blank
     c                   eval      vlaglu_lage = *zero
     c     vlaglu_key    setll     vlaglur
     c                   read      vlaglur
      *
     c                   dow       not %eof
      *
     c                   if        vlfirm <> w_firm
     c                   unlock    vlaglu
     c                   leave
     c                   endif
6.26  *
6.26 c                   eval      vvarl1_vare = vlvare
6.26 c     vvarl1_key    chain     vvarl1r
6.26 c                   if        %found(vvarl1)
      *
     c                   exsr      sjek_ordr
      *
      *> Ikke i bruk               vlbres = 0
      *
     c                   if        vlbbes = w_bbes
     c                   eval      w_updt = 0
     c                   endif
      *
     c                   if        w_updt = 1
     c                   eval      vlbbes = w_bbes
     c                   time                    vledat
     c                   time                    vletim
     c                   eval      vleusr = l_user
     c                   update    vlaglur
     c                   else
     c                   unlock    vlaglu
     c                   endif
      *
6.26 c                   endif
     c                   read      vlaglur
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     end_pgm       tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekke ordrelinjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     csr   sjek_ordr     begsr
      *
     c                   eval      w_updt = 1
     c                   eval      w_bbes = 0
      *
     c                   eval      lodtlx_lage = vllage
     c                   eval      lodtlx_vare = vlvare
     c     lodtlx_key    setll     lodtlx
     c     lodtlx_key    reade     lodtlx
      *
     c                   dow       not %eof
      *
6.27 c                   if        ldvare <> *blank and
6.27 c                             ldlety <> 1
      *
      *  Hent opplysninger fra BESTILLINGS-HODE-REGISTER
      *
     c                   eval      lohel1_numm = ldnumm
     c                   eval      lohel1_suff = ldsuff
     c     lohel1_key    chain     lohel1r
     c                   if        %found(lohel1)
      *
      *  Hent opplysninger fra Ordre-type
      *
     c                   eval      votyl1_otyp = lootyp
     c     votyl1_key    chain     votyl1r
      *
      *  Hent opplysninger fra Linje-type
      *
     c                   eval      vltyl1_ltyp = ldltyp
     c     vltyl1_key    chain     vltyl1r
      *
      * Oppdatering skal bare foregå for
      * behandlingskode som = 1
      *
     c                   if        vaoakk = 1
     c                   eval      w_anta = ldanta
6.26  *
6.26 c                   exsr      sjek_omr
      *
      * Sjekk om negativ ordretype
      *
     c                   if        vaokre = '-'
     c                   eval      w_anta = w_anta * -1
     c                   endif
      *
      * Sjekk om negativ linjetype
      *
     c                   if        valkre = '-'
     c                   eval      w_anta = w_anta * -1
     c                   endif
      *
     c                   eval      w_bbes = w_bbes + w_anta
      *
     c                   endif
     c                   endif
     c                   endif
      *
     c     lodtlx_key    reade     lodtlx
     c                   enddo
      *
     csr                 endsr
      *
6.26  *
6.26  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.26  *  Hvis enhet på bestilling ikke er lagerenhet - så omregn
6.26  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.26  *
6.26 c     sjek_omr      begsr
6.26  *
6.26  *
6.26 c                   if        ldenh1 = vvenhl
6.26 c                   goto      end_omr
6.26 c                   endif
6.26  *
6.26  * Finner omregningsfaktor volum for lagerenhet og for solgt enhet
6.26  * Antall regnes om etter forholdet mellom disse
6.26  *
6.26 c                   eval      vvenl1_vare = vvvare
6.26 c     vvenl1_key    setll     vvenl1
6.26 c     vvenl1_key    reade     vvenl1                                 90
6.26 c                   dow       *in90 = *off
6.26 c                   if        ldenh1 = veenhe
6.26 c                   eval      w_omrs = veomre
6.26 c                   endif
6.26 c                   if        vvenhl = veenhe
6.26 c                   eval      w_omrl = veomre
6.26 c                   endif
6.26 c     vvenl1_key    reade     vvenl1                                 90
6.26 c                   enddo
6.26  *
6.26 c                   if        w_omrs = 0
6.26 c                   eval      w_omrs = 1
6.26 c                   endif
6.26  *
6.26 c                   if        w_omrl = 0
6.26 c                   eval      w_omrl = 1
6.26 c                   endif
6.26  *
6.26 c                   eval      w_anta = ldanta * w_omrs / w_omrl
6.26  *
6.26 c     end_omr       endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     csr   *inzsr        begsr
      *
      *  Key for file : LAGER-REGISTER
      *
     c     vlaglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglu_vare
     c                   kfld                    vlaglu_lage
      *
      *  Key for file : ORDRE-HODE-REGISTER
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      *  Key for file : ORDRE-LINJE-REGISTER
      *
     c     lodtlx_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlx_lage
     c                   kfld                    lodtlx_vare
      *
      * Key for file : ORDRE-TYPE-REGISTER
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for file : LINJE-TYPE-REGISTER
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
6.26  *
6.26  * Key for oppslag på vare-register
6.26  *
6.26 c     vvarl1_key    klist
6.26 c                   kfld                    w_firm
6.26 c                   kfld                    vvarl1_vare
6.26  *
6.26  * Key for les på vare-enhet-register
6.26  *
6.26 c     vvenl1_key    klist
6.26 c                   kfld                    w_firm
6.26 c                   kfld                    vvenl1_vare
      *
      *  Legger inn firmanummer i nøklene
      *
     c                   eval      w_firm = dssfir
      *
     csr                 endsr
