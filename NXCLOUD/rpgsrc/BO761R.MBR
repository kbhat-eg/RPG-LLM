     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSHOP
      * Program . . . . : BO761R
      * Beskrivelse . . : Radioterminal, Styreprogram ved dir. pakkseddel
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       190901 AMD  Nytt program
3.41  *       220102 AMD  Korrigering av skjermid for printerstyring
3.42  *       281102 AMD  Selger skal følge med i gjennom systemet
5.21  * R5.21 200803 AMD  Lagt om til AP600R i stedet for AP100R
5.30  * R5.30 141004 HFL  I bonghode og linje er bongnr. 12A.
5.30  *                   Ordrenr. legges ut i BODTPF.
5.60  * R5.60 061107 AMD  Lagt inn løsning for kontroll av grense for
      *                   almenningsrabatter
      *                   NB! Ingen løsning for dette i gammelt butikk-
      *                   system. Endringer her gjelder bare for å få
      *                   riktig parameter-liste.
      *                   NB2 Det må sjekkes litt mer rundt dette pro-
      *                       grammet, og hvordan memosaldo for
      *                       almenningsrabatter skal påvirkes
      * 6.10  110909 BSA  Oppdatert med sporingsinformasjon
      * 6.10  220909 ASK  Nye parametre til VL001R
6.11  * 6.11  211010 AMD  Pakkseddel fra radioterminal oppdaterte
6.11  *                   ikke lagerbeholdning
6.20  * 6.20  021210 NHO  Prosjekt er nå alfa
6.21  * R6.20 140112 bof  Mulighet til å legge ut ByggDok innsamling
6.22  * R6.20 040113 bof  Henter byggdok - kode fra FKPRPF
6.23  * R6.20 250216 bkv  Lagt inn vareadresse lik fakturaadresse
      *                   dersom det er satt opp krav om dette i AVALPF
6.nu  * 6.30  230614 nho  endring i forb. nummerutvidelse
6.30  * 6.30  221014 BKV  Søtte for cobuilder
6.31  * 6.30  050716 bof  Byggdok/Cob. skal evt. ta mailadr. fra kundeprosjekt
6.32  * 6.30  180816 BOF  Legger ordrenr i bonghode
7.01  *       240220 bof  Cobuilder, men ikke krav til nobbnummer
7.02  *  6.20 021214 bsa  Lagt til rette for retur mht. ordretype
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     faforl1    if   e           k disk    rename(aforpfr:aforl1r)
     fbstsl1    if   e           k disk    rename(bstspfr:bstsl1r)
     fbwsdl1    if   e           k disk    rename(bwsdpfr:bwsdl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
     fbmhel1    if   e           k disk    rename(bmhepfr:bmhel1r)
     fbmdtl1    if   e           k disk    rename(bmdtpfr:bmdtl1r)
     fbmhelu    uf   e           k disk    rename(bmhepfr:bmhelur)
     fbmdtlu    uf   e           k disk    rename(bmdtpfr:bmdtlur)
     fbohelu    uf a e           k disk    rename(bohepfr:bohelur)
     fbodtlu    uf a e           k disk    rename(bodtpfr:bodtlur)
6.21 fafpslr    if   e           k disk    rename(afpspfr:afpslrr)
6.22 ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
6.21 frukpl6    if   e           k disk    rename(rukppfr:rukpl6r)
6.21 famodl1    if   e           k disk    rename(amodpfr:amodl1r)
6.21 frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
6.21 ffbdopf    o  a e             disk
6.23 favall1    if   e           k disk    rename(avalpfr:avall1r)
      *
      * Definering av parametere
      *
     d p_firm          s                   like(fofirm)
6.nu d p_refn          s              8  0
7.02 d p_retu          s              1
      *
      * Definering av Local data area
      *
     d lda            uds
     d l_ruti                800    809
     d l_chgv                844    844
     d l_wsid                901    910
     d l_user                911    920
6.21 d l_filg                931    933
     d l_firm                944    946  0
     d l_fnak                951    970
      *
      * Definering av datastruktur for lageroppdatering
      *
     D                 ds
6.nu d hlrec                        128
     d  hlvare                       15    overlay(hlrec)
     d  hllage                        2  0 overlay(hlrec:16)
     d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
     d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
     d  hlotyp                        2    overlay(hlrec:36)
     d  hlltyp                        2    overlay(hlrec:38)
     d  hlanta                       11  3 overlay(hlrec:40)
     d  hlkopr                        9  2 overlay(hlrec:51)
     d  hlrefr                       20    overlay(hlrec:60)
     d  hlsyst                        1    overlay(hlrec:80)
     d  hlaltk                        2    overlay(hlrec:81)
     d  hlrest                        1    overlay(hlrec:83)
     d  hltype                        1    overlay(hlrec:84)
     d  hlenhe                        3    overlay(hlrec:85)
     d  hlinlr                        1    overlay(hlrec:88)
     d  hllety                        1    overlay(hlrec:89)
6.nu d  hlonum                        8  0 overlay(hlrec:90)
6.nu d  hlolin                        4  0 overlay(hlrec:98)
      *
      * Definering av variable i nøkler
      *
     d w_firm          s                   like(fofirm)
     d votyl1_otyp     s                   like(vaotyp)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fusrl1_user     s                   like(fbuser)
     d aforl1_ruti     s                   like(afruti)
     d bwsdl1_wsid     s                   like(bwwsid)
     d bmhel1_aarr     s                   like(bnaarr)
     d bmhel1_wsid     s                   like(bnwsid)
     d bmhel1_numm     s                   like(bnnumm)
     d bmdtl1_aarr     s                   like(bmaarr)
     d bmdtl1_wsid     s                   like(bmwsid)
     d bmdtl1_numm     s                   like(bmnumm)
     d bmdtl1_line     s                   like(bmline)
     d bmhelu_aarr     s                   like(bnaarr)
     d bmhelu_wsid     s                   like(bnwsid)
     d bmhelu_numm     s                   like(bnnumm)
     d bmdtlu_aarr     s                   like(bmaarr)
     d bmdtlu_wsid     s                   like(bmwsid)
     d bmdtlu_numm     s                   like(bmnumm)
     d bmdtlu_line     s                   like(bmline)
     d bohelu_aarr     s                   like(boaarr)
     d bohelu_wsid     s                   like(bowsid)
5.30 d bohelu_numm     s                   like(bobong)
     d bodtlu_aarr     s                   like(bdaarr)
     d bodtlu_wsid     s                   like(bdwsid)
5.30 d bodtlu_numm     s                   like(bdbong)
     d bodtlu_line     s                   like(bdline)
6.22 d fkprl1_kund     s                   like(flkund)
6.22 d fkprl1_kpro     s                   like(flkpro)
6.21 d amodl1_modu     s                   like(axmodu)
6.21 d afpslr_ufil     s                   like(l_filg)
6.21 d afpslr_uide     s                   like(afuide)
6.21 d rukpl6_kund     s                   like(rukund)
6.21 d rkunl1_kund     s                   like(rkkund)
6.23 d avall1_apgm     s                   like(avapgm)
      *
      * Definering av variable
      *
     d w_udat          s               d   datfmt(*iso)
     d w_aarr          s              4  0
     d w_line          s              4  0
6.nu d w_refn          s              8  0
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
6.nu d w_lrec          s            128
     d w_wsid          s             10
     d w_prog          s             10
     d w_ruti          s             10
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
     d w_retu          s              1
     d w_kode          s              1
     d w_stat          s              1
     d w_in03          s              1
     d w_kopi          s              1  0 inz(0)
     d w_valg          s              1  0 inz(2)
5.21 d w_tvbt          s              1
5.21 d w_btch          s              1
5.60 d w_sal2          s             11  2
     d w_otyp          s                   like(footyp)
     d w_numm          s                   like(fonumm)
     d w_totb          s                   like(bototb)
     d w_tots          s                   like(botots)
     d w_totk          s                   like(bototk)
      *w_totr          s                   like(bototr)
6.21 d w_btms          s               Z
6.21 d w_ti26          s             26
6.21 d w_bnum          s              6
      *
6.21 d b_bdo1          s              1    inz(*off)
6.21 d b_bdo2          s              1    inz(*off)
6.21 d b_bdo3          s              1    inz(*off)
6.21 d b_bdo4          s              1    inz(*off)
6.21 d b_kper          s              1    inz(*off)
6.23  *
6.23  * Firmastyrt validering av felt
6.23  *
6.23 d u_vref          s              1    inz(*off)
6.23 d u_navn          s              1    inz(*off)
      *
      * Definering av save-variable
      *
     d s_numm          s                   like(fonumm)
     d s_bong          s                   like(fonumm)
6.21  *
6.21  * Definering av konstanter
6.21  *
6.21 d lo              c                   'abcdefghijklmnopqrstuvwxyzæøå'
6.21 d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅ'
      *
7.01  *
7.01 d u_s701          s              1    inz(*off)                            Klient
7.01  *
7.01  * Definering av eksterne prg
7.01  *
7.01   dcl-s co402_verdi1  char(1);
7.01   dcl-s co402_verdi2  char(2048);
7.01   DCL-PR CO402R EXTPGM('CO402R');
7.01     p_filgrp            char(3)     const;
7.01     p_firm              packed(3:0) const;
7.01     p_lager             packed(2:0) const;
7.01     p_nokkel            char(50)    const;
7.01     p_verdi1            char(1);
7.01     p_verdi2            char(2048);
7.01   END-PR;
7.01  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Kalle program som henter inn til butikkregister
      *
     c                   call      'BOHNHR'
     c                   parm                    w_firm
     c                   parm                    w_aarr
     c                   parm                    w_wsid
     c                   parm                    w_refn
     c                   parm                    w_line
     c                   parm                    w_tots
      *
      * Finn riktig ordre-type
      *
7.02 c                   if        p_retu = '1'
7.02 c                   eval      w_otyp = baaock
7.02 c                   else
     c                   eval      w_otyp = baaocd
7.02 c                   endif
      *
      * Henter inn ordrenummer fra NUMMER-REGISTER
      *
     c     h1tagh        tag
      *
     c                   eval      w_fell = baaord
     c                   eval      w_type = w_otyp
     c                   eval      w_fast = w_wsid
     c                   exsr      hent_numm
     c                   eval      s_numm = w_numm
      *
      * Finnes ordrenummer fra før ?
      *
     c                   eval      fohel1_numm = w_numm
     c     fohel1_key    chain     fohel1r                            90
     c                   if        *in90 = *off
     c                   goto      h1tagh
     c                   endif
      *
      * Oppdater BUTIKK-HODE-REGISTER med :
      *        - Ordrenummer Ofak
      *        - Ordretype
      *        - Avdeling
      *        - Lager
      *
     c                   eval      bmhelu_aarr = w_aarr
     c                   eval      bmhelu_wsid = w_wsid
     c                   eval      bmhelu_numm = *zero
     c     bmhelu_key    chain     bmhelur                            90
      *
     c                   if        *in90 = *off
     c                   eval      bnornr = s_numm
     c                   eval      bnorsu = *zero
     c                   eval      bnotyp = w_otyp
3.42 c***                eval      bnselg = fbselg
     c                   eval      bnavde = bwavde
     c                   eval      bnlage = bwlage
6.23  *
6.23  * Dersom leveringsadresse fortsatt er blank,
6.23  * og det er satt krav til leveringsadresse,
6.23  * legges fakturaadresse til leveringsadresse,
6.23  *
6.23 c                   if        u_navn = *on
6.23 c                   if        bnnavn = *blank
6.23 c                   eval      rkunl1_kund = bnkund
6.23 c     rkunl1_key    chain     rkunl1
6.23 c                   if        %found
6.23 c                   eval      bnnavn = rknavn
6.23 c                   eval      bnadr1 = rkgate
6.23 c                   eval      bnadr2 = rkadr2
6.23 c                   eval      bnsted = rksted
6.23 c                   endif
6.23 c                   endif
6.23 c                   endif
6.23 c                   if        u_vref = *on
6.23 c                   if        bnvref = *blank
6.23 c                   eval      bnvref = fbvref
6.23 c                   endif
6.23 c                   endif
6.23  *
     c                   update    bmhelur
     c                   endif
      *
      * Hent inn opplysninger om ordretypen
      *
     c                   eval      votyl1_otyp = w_otyp
     c     votyl1_key    chain     votyl1r                            90
     c                   eval      w_ruti = vaorub
      *
6.21  * Sjekker om kundeprosjekt skal legge ut ByggDok
6.21  *
6.21 c                   eval      b_bdo2 = *off
6.21 c                   eval      b_bdo4 = *off
6.21 c                   if        b_bdo1 = *on
6.22 c                   eval      fkprl1_kund = bnkund
6.22 c                   eval      fkprl1_kpro = bnkpro
6.22 c     fkprl1_key    chain     fkprl1r
6.31 c                   if        %found(fkprl1)
6.31 c                   if        flbdok = 1  or
6.31 c                             flcobu = 1
6.21 c                   time                    w_btms
6.21 c                   eval      b_bdo2 = *on
6.31 c                   endif
6.21 c                   endif
6.21 c                   eval      b_bdo3 = *on
6.21 c                   endif
      *
      * Legg inn verdi for bare henting av opplysninger
      *
     c                   eval      l_chgv = *blank
     c                   eval      l_ruti = w_ruti
3.41 c                   eval      l_wsid = w_wsid
      *
      * Oppdaterer LDA
      *
     c                   out       *dtaara
      *
      * Kaller opp program før utskrift
      *
5.21 c                   call      'AP600R'
5.21 c                   parm                    l_ruti
5.21 c                   parm                    l_chgv
5.21 c                   parm                    w_tvbt
5.21 c                   parm                    w_in03
5.21 c                   parm                    w_btch
      *
      * Henter inn bongnummer fra NUMMER-REGISTER
      *
     c                   eval      w_fell = bwbong
     c                   eval      w_type = *blank
     c                   eval      w_fast = w_wsid
     c                   exsr      hent_numm
     c                   eval      s_bong = w_numm
      *
      * Hent inn opplysninger om formularet
      *
     c                   eval      aforl1_ruti = w_ruti
     c     aforl1_key    chain     aforl1r                            90
     c                   eval      w_prog = afprog
      *
      * Kaller opp utskriftsprogram
      * (utskrift av pakkseddel)
      *
     c                   call      w_prog
     c                   parm                    bwkbon
     c                   parm                    w_aarr
     c                   parm                    w_wsid
     c                   parm                    s_bong
     c                   parm                    w_kopi
      *
      * Arbeide med utlegging til BONG-REGISTER(E)
      *
     c                   exsr      add_hist
      *
      * Arbeide med utlegging til ORDRE-REGISTER
      *
     c                   call      'BOOFOR'
     c                   parm                    w_retu
     c                   parm                    w_firm
     c                   parm                    w_aarr
     c                   parm                    w_wsid
     c                   parm                    w_valg
      *
      * Kaller opp subrutine for oppdatering av memosaldo
      *
     c                   call      'FA922R'
     c                   parm                    w_firm
     c                   parm                    w_otyp
     c                   parm                    bnkund
     c                   parm                    bntots
      * erstattet        parm                    wpsald
5.60 c                   parm                    w_sal2
      *
      * Sletter transaksjoner fra håndterminalfil
      *
     c                   exsr      del_handterm
      *
      * Sletter transaksjoner etter utlegging til BONG-REGISTER(E)
      *
     c                   exsr      del_butikk
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter/Oppdatering av NUMMER-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_numm     begsr
      *
     c                   eval      w_kode = '0'
      *
      * Kaller opp subprogram for henting av nummer fra NUMMER-REGISTER
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer historisk hode og linje register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     add_hist      begsr
      *
      * Leser og henter linjer i BUTIKK-LINJE-REGISTER
      *
     c                   eval      w_totb = 0
     c                   eval      w_tots = 0
     c                   eval      w_totk = 0
      *
     c                   eval      bmdtl1_aarr = w_aarr
     c                   eval      bmdtl1_wsid = w_wsid
     c                   eval      bmdtl1_numm = *zero
     c     bmdtl1_key2   setll     bmdtl1
     c     bmdtl1_key2   reade     bmdtl1                                 90
      *
     c                   dow       *in90 = *off
     c                   eval      bodtlu_aarr = w_aarr
     c                   eval      bodtlu_wsid = w_wsid
5.30 c*                  eval      bodtlu_numm = s_bong
5.30 c                   movel     s_bong        bodtlu_numm
     c                   eval      bodtlu_line = bmline
     c     bodtlu_key    chain     bodtlur                            92
      *
      * Beregner kostpris til butikk-linje og hode
      *
     c                   eval      bdsumk = bmanta * bmkopr
     c                   eval      w_totk = w_totk + bdsumk
     c                   eval      w_totb = w_totb + bmsums
     c                   eval      w_tots = w_tots + bmsums
      *
      * Legger ut records i HISTORISK-BONG-LINJE-REGISTER
      *
     c                   eval      bdltyp = bmltyp
     c                   eval      bdlety = bmlety
     c                   eval      bdvare = bmvare
     c                   eval      bdenh1 = bmenh1
     c                   eval      bdtek1 = bmtek1
     c                   eval      bdtek2 = bmtek2
     c                   eval      bdanta = bmanta
     c                   eval      bdoppr = bmoppr
     c                   eval      bdsapr = bmsapr
     c                   eval      bdkopr = bmkopr
     c                   eval      bdselk = bmselk
     c                   eval      bdrab1 = bmrab1
     c                   eval      bdrab2 = bmrab2
     c                   eval      bdkpri = bmkpri
     c                   eval      bdsums = bmsums
     c                   eval      bdkamp = bmkamp
     c                   eval      bdogrp = bmogrp
     c                   eval      bdhgrp = bmhgrp
     c                   eval      bdugrp = bmugrp
     c                   eval      bdlvre = bmlvre
      *
     c                   if        *in92 = *on
     c                   eval      bdfirm = w_firm
     c                   eval      bdaarr = w_aarr
     c                   eval      bdwsid = w_wsid
5.30 c*                  eval      bdbong = s_bong
5.30 c                   movel     s_bong        bdbong
     c                   eval      bdline = bmline
      *
5.30  * Hente inn record  i BUTIKK-HODE-REGISTER for å finne ordrenr.
      *
5.30 c                   eval      bmhel1_aarr = w_aarr
5.30 c                   eval      bmhel1_wsid = w_wsid
5.30 c                   eval      bmhel1_numm = *zero
     c     bmhel1_key    chain     bmhel1r                            90
      *
5.30 c                   if        *in90 = *off
5.30 c                   eval      bdornr = bnornr
5.30 c                   eval      bdorsu = bnorsu
5.30 c                   endif
      *
6.10 c                   time                    bdodat
6.10 c                   time                    bdotim
6.10 c                   eval      bdousr = l_user
6.10 c                   time                    bdedat
6.10 c                   time                    bdetim
6.10 c                   eval      bdeusr = l_user
     c                   write     bodtlur
     c                   endif
      *
      * Kaller opp subrutine for oppdatering av lager
      *
     c                   if        w_valg <> 4
     c                   if        baalag <> *zero
     c                   exsr      opplag
     c                   endif
     c                   endif
      *
6.21 c                   if        vaoakk = 2 and
6.2c c                             vaokre = *blank
6.21 c                   exsr      byggdok
6.21 c                   endif
      *
     c     bmdtl1_key2   reade     bmdtl1                                 90
     c                   enddo
      *
      * Hente inn record  i BUTIKK-HODE-REGISTER
      *
     c                   eval      bmhel1_aarr = w_aarr
     c                   eval      bmhel1_wsid = w_wsid
     c                   eval      bmhel1_numm = *zero
     c     bmhel1_key    chain     bmhel1r                            90
      *
      * Legger ut records i HISTORISK-BONG-HODE-REGISTER
      *
     c                   if        *in90 = *off
     c                   eval      bohelu_aarr = w_aarr
     c                   eval      bohelu_wsid = w_wsid
5.30 c*                  eval      bohelu_numm = s_bong
5.30 c                   movel     s_bong        bohelu_numm
     c     bohelu_key    chain     bohelur                            91
      *
     c                   eval      bototk = w_totk
     c                   eval      bototb = w_tots
     c                   eval      botots = w_tots
      *
      * Legger til mva på bototb
      *
     c                   exsr      mva_add
      *
      * Flytter over felter
      *
     c                   eval      bootyp = w_otyp
     c                   eval      bolety = bnlety
     c                   eval      bolage = bnlage
     c                   eval      bobinr = bnbinr
     c                   eval      bokref = bnkref
     c                   eval      boperi = *zero
     c                   eval      bobdat = bnbdat
     c                   eval      boldat = bnldat
     c                   eval      bofkda = bnfkda
     c                   eval      boffda = bnffda
     c                   eval      bobetb = bnbetb
     c                   eval      boselg = bnselg
     c                   eval      bodist = bndist
     c                   eval      bolmat = bnlmat
     c                   eval      bolbet = bnlbet
     c                   eval      bovalk = bnvalk
     c                   eval      borkat = bnrkat
     c                   eval      bosped = bnsped
     c                   eval      bokjor = bnkjor
     c                   eval      bomkod = bnmkod
     c                   eval      bolkun = bnlkun
     c                   eval      bokund = bnkund
     c                   eval      bokuna = bnkuna
     c                   eval      bonavn = bnnavn
     c                   eval      boadr1 = bnadr1
     c                   eval      boadr2 = bnadr2
     c                   eval      bosted = bnsted
     c                   eval      boavde = bnavde
     c                   eval      bokont = bnkont
     c                   eval      bospes = bnspes
     c                   eval      boproj = bnproj
     c                   eval      boakti = bnakti
     c                   eval      bovref = bnvref
     c                   eval      bodref = bndref
     c                   eval      borekv = bnrekv
     c                   eval      bolmtx = bnlmtx
     c                   eval      bolbtx = bnlbtx
     c                   eval      borabp = bnrabp
6.32 c                   eval      boornr = bnornr
5.32 c                   eval      boorsu = bnorsu
     c                   eval      bopord = bnpord
     c                   eval      bopsuf = bnpsuf
     c                   eval      bototx = bntotx
     c                   eval      bototr = *zero
     c                   eval      bodate = bndate
     c                   eval      botime = bntime
     c                   eval      bokavs = *zero
     c                   eval      bokdet = *zero
     c                   eval      bokpro = bnkpro
     c                   eval      boneto = bnneto
      *
     c                   if        *in91 = *on
     c                   eval      bofirm = w_firm
     c                   eval      boaarr = w_aarr
     c                   eval      bowsid = w_wsid
5.30 c*                  eval      bobong = s_bong
5.30 c                   movel     s_bong        bobong
6.10 c                   time                    bodate
6.10 c                   time                    botime
6.10 c                   eval      bouser = l_user
6.10 c                   time                    boedat
6.10 c                   time                    boetim
6.10 c                   eval      boeusr = l_user
     c                   write     bohelur
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatering av Lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opplag        begsr
      *
      * Legger over verdier i overførings-felter
      *
     c                   eval      hlvare = bdvare
     c                   eval      hllage = bnlage
     c                   eval      hlenhe = bdenh1
     c                   eval      hldato = *loval
     c                   eval      hltime = *loval
     c                   eval      hlotyp = w_otyp
     c                   eval      hlltyp = bdltyp
      *
     c                   eval      hlanta = bdanta
     c                   eval      hlkopr = bdkopr
      *
     c                   eval      hlrefr = *blank
     c                   eval      hlsyst = 'F'
     c                   eval      hlaltk = *blank
     c                   eval      hlrest = *blank
     c                   eval      hlrefr = 'Bong: ' + bdbong
     c                   eval      hlonum = w_numm
     c                   eval      hlolin = 0
      *
     c                   move      bdline        hlrefr
     c                   move      bdlety        hllety
      *
      * Kaller opp lageroppdaterings-program
      *
     c                   eval      w_stat = *blank
     c                   eval      w_lrec = hlrec
      *
     c                   call      'VL001R'
     c                   parm                    w_stat
     c                   parm                    w_lrec
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sletting fra håndterminal
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Sletter alle håndterminal-transer med referansenummer som inngår i
      * salget
      *
     c     del_handterm  begsr
      *
     c                   eval      w_refn = 0
      *
     c     bmdtl1_key2   setll     bmdtl1
     c     bmdtl1_key2   reade     bmdtl1                                 90
     c                   dow       *in90 = *off
      *
     c                   if        bmrefn <> 0 and
     c                             bmrefn <> w_refn
     c                   call      'BO751C'
     c                   parm                    w_firm
     c                   parm                    bmrefn
     c                   endif
      *
     c                   eval      w_refn = bmrefn
      *
     c     bmdtl1_key2   reade     bmdtl1                                 90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sletting av poster i butikk - hode og linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     del_butikk    begsr
      *
      * Sletter records i BUTIKK-LINJE-REGISTER
      *
     c     bmdtl1_key2   setll     bmdtl1
     c     bmdtl1_key2   reade     bmdtl1                                 90
      *
     c                   dow       *in90 = *off
     c                   eval      bmdtlu_aarr = bmaarr
     c                   eval      bmdtlu_wsid = bmwsid
     c                   eval      bmdtlu_numm = bmnumm
     c                   eval      bmdtlu_line = bmline
     c     bmdtlu_key    chain     bmdtlur                            92
      *
     c                   if        *in92 = *off
     c                   delete    bmdtlur
     c                   endif
      *
     c     bmdtl1_key2   reade     bmdtl1                                 90
     c                   enddo
      *
      * Sletter records i BUTIKK-HODE-REGISTER
      *
     c                   eval      bmhelu_aarr = bmdtlu_aarr
     c                   eval      bmhelu_wsid = bmdtlu_wsid
     c                   eval      bmhelu_numm = bmdtlu_numm
     c     bmhelu_key    chain     bmhelur                            91
     c                   if        *in91 = *off
     c                   delete    bmhelur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Beregning av  beløp inkl. moms                                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     csr   mva_add       begsr
      *
     c                   if        bnmkod = 0
     c                   eval      bototb = bototb * w_mvat
     c                   end
      *
     csr                 endsr
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  * Subrutine for å legge ut i Byggdok
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  *
6.21 c     byggdok       begsr
6.21  *
6.21  * legger evt. linjer til BygDok
6.21  *
6.21 c                   if        b_bdo1 = *on and
6.21 c                             b_bdo2 = *on and
6.21 c                             b_bdo3 = *on and
7.01 c                             ((bdvare > '10000000' and
7.01 c                             bdvare < '90000000') or
7.01 c                             (u_s701 = *on and
7.01 c                              bdvare <> *blank))
6.21 c                   eval      rkunl1_kund = bnkund
6.21 c     rkunl1_key    chain     rkunl1
6.21  * finner kontaktperson for kunde som har rolle BYGGDOK
6.21 c                   eval      b_kper  = *off
6.21 c                   eval      rukpl6_kund = bnkund
6.21 c     rukpl6_key    setll     rukpl6
6.21 c     rukpl6_key    reade     rukpl6
6.21 c                   dow       not %eof
6.21 c     lo:up         xlate     rukrol        rukrol
6.30 c                   if        rukrol  = 'BYGGDOK' or rukrol = 'COBUILDER'
6.21 c                   eval      b_kper  = *on
6.21 c                   goto      fbdo_tag
6.21 c                   endif
6.21 c     rukpl6_key    reade     rukpl6
6.21 c                   enddo
6.21 c     fbdo_tag      tag
6.21 c                   eval      fwbfir = w_firm
6.21 c                   eval      fwbkun = bnkund
6.21 c                   eval      fwbkpr = bnkpro
6.21 c                   movel     s_bong        fwbnum
6.21 c                   eval      fwbnav = rknavn
6.21 c                   if        b_kper  = *on and
6.21 c                             ruenav <> *blank and
6.21 c                             rumail <> *blank
6.21 c                   eval      fwbper = %trim(ruenav) + ',' +
6.21 c                                      %trim(rufnav)
6.21 c                   eval      fwbmai = rumail
6.21 c                   else
6.21 c                   eval      fwbper = rknavn
6.21 c                   eval      fwbmai = rkemal
6.21 c                   endif
6.31 c                   if        flmail <> *blank
6.31 c                   eval      fwbmai = flmail
6.31 c                   if        flkprs <> *blank
6.31 c                   eval      fwbper = flkprs
6.31 c                   endif
6.31 c                   endif
6.21 c                   eval      fwbftn = rkftnr
6.21 c                   move      w_btms        w_ti26
6.21 c                   eval      fwbtms = w_ti26
6.21 c                   movel     bdvare        fwbnob
6.21 c                   time                    fwboda
6.21 c                   time                    fwboti
6.21 c                   time                    fwbeda
6.21 c                   time                    fwbeti
6.21 c                   eval      fwbous = l_user
6.21 c                   eval      fwbeus = l_user
6.21 c                   write     fbdopfr
6.21 c                   move      fwbnum        w_bnum
6.21 c                   eval      b_bdo4 = *on
6.21 c                   endif
6.21  *
6.21 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Definisjon av local data area for output
      *
     c     *dtaara       define    *lda          lda
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_refn
7.02 c                   parm                    p_retu
      *
      * Key for file : BUTIKK-STATUS-KODER
      *
     c     bstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for file : SKJERM/KASSE-REGISTER
      *
     c     bwsdl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bwsdl1_wsid
      *
      * Key for file : ORDRE-TYPE-REGISTER
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for file : ORDRE-HODE-REGISTER
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for file : FORMULAR/RUTINE-REGISTER
      *
     c     aforl1_key    klist
     c                   kfld                    aforl1_ruti
      *
      * Key for file : BUTIKK-HODE-REGISTER
      *
     c     bmhel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bmhel1_aarr
     c                   kfld                    bmhel1_wsid
     c                   kfld                    bmhel1_numm
      *
     c     bmhelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bmhelu_aarr
     c                   kfld                    bmhelu_wsid
     c                   kfld                    bmhelu_numm
      *
      * Key for file : BUTIKK-LINJE-REGISTER
      *
     c     bmdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bmdtl1_aarr
     c                   kfld                    bmdtl1_wsid
     c                   kfld                    bmdtl1_numm
     c                   kfld                    bmdtl1_line
      *
     c     bmdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bmdtlu_aarr
     c                   kfld                    bmdtlu_wsid
     c                   kfld                    bmdtlu_numm
     c                   kfld                    bmdtlu_line
      *
     c     bmdtl1_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    bmdtl1_aarr
     c                   kfld                    bmdtl1_wsid
     c                   kfld                    bmdtl1_numm
      *
      * Key for file : BONG-HODE-REGISTER
      *
     c     bohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bohelu_aarr
     c                   kfld                    bohelu_wsid
     c                   kfld                    bohelu_numm
      *
      * Key for file : BONG-LINJE-REGISTER
      *
     c     bodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bodtlu_aarr
     c                   kfld                    bodtlu_wsid
     c                   kfld                    bodtlu_numm
     c                   kfld                    bodtlu_line
      *
      * Key for oppslag på bruker
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrl1_user
6.21  *
6.21  * Key for les av moduler Nexstep
6.21  *
6.21 c     amodl1_key    klist
6.21 c                   kfld                    amodl1_modu
6.21  *
6.21  * Key for les av kundeprosjekt
6.21  *
6.22 c     fkprl1_key    klist
6.22 c                   kfld                    w_firm
6.22 c                   kfld                    fkprl1_kund
6.22 c                   kfld                    fkprl1_kpro
6.21  *
6.21  * Key for propertiesfil
6.21  *
6.21 c     afpslr_key    klist
6.21 c                   kfld                    afpslr_ufil
6.21 c                   kfld                    afpslr_uide
6.21  *
6.21  * Key for oppslag på kunde
6.21  *
6.21 c     rkunl1_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    rkunl1_kund
6.21  *
6.21  * Key for oppslag på kunde - kontaktperson
6.21  *
6.21 c     rukpl6_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    rukpl6_kund
      *
      * Finne året
      *
     c                   time                    w_udat
     c                   extrct    w_udat:*y     w_aarr
      *
      * Legg inn firmanummer
      *
     c                   eval      w_firm = p_firm
      *
      * Legg inn referansenummer
      *
     c                   eval      w_refn = p_refn
      *
      * Legg inn øvrige verdier
      *
     c                   eval      w_line = 0
     c                   eval      w_tots = 0
      *
      * Finner moms-sats
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    w_udat
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
      *
      * Nb! Skjermid er satt sammen av
      *     RT + brukerid da radioterminalen
      *     ikke gir noen unik skjermid.
      *
     c                   eval      w_wsid = %trim('RT' + l_user)
      *
      * Hent opplysninger fra SHOP-KODE-REGISTER
      *
     c     bstsl1_key    chain     bstsl1r                            90
      *
      * Hent opplysninger fra KASSE-REGISTER
      *
     C                   eval      bwsdl1_wsid = w_wsid
     C     bwsdl1_key    chain     bwsdl1r                            90
      *
      * Henter inn bruker-info                                         er
      *
     c                   eval      fusrl1_user = l_user
     c     fusrl1_key    chain     fusrl1                             90
6.21  *
6.21  * Sjekker om Byggdok skal kjøres forutsetter at
6.21  * modul er på plass og at
6.21  *
6.21 c                   eval      b_bdo1 = *off
6.21 c                   eval      amodl1_modu = 'BYGGDOK'
6.21 c     amodl1_key    chain     amodl1
6.21 c                   if        %found(amodl1)
6.21 c                   eval      afpslr_ufil = l_filg
6.21 c                   eval      afpslr_uide = 'BYGGDOK   '
6.21  *
6.30  * Sjekk om byggdok eller cobuilder aktivering er på/av
6.21  *
6.21 c     afpslr_key    chain     afpslrr
6.21 c                   if        %found(afpslr)
6.21 c                   eval      b_bdo1 = *on
6.30 c                   else
6.30 c                   eval      afpslr_uide = 'COBUILDER '
6.30 c     afpslr_key    chain     afpslrr
6.30 c                   if        %found(afpslr)
6.30 c                   eval      b_bdo1 = *on
6.30 c                   endif
6.21 c                   endif
6.21 c                   endif
6.23  *
6.23  * Key for file : Firmastyrt validering
6.23  *
6.23 c     avall1_key    klist
6.23 c                   kfld                    w_firm
6.23 c                   kfld                    avall1_apgm
6.23  *
6.23  * Sjekk om det er lagt opp krav til leveringsadresse
6.23  *
6.23  *
6.23 c                   eval      avall1_apgm = 'FO100R'
6.23 c     avall1_key    setll     avall1
6.23 c     avall1_key    reade     avall1
6.23 c                   dow       not %eof(avall1)
6.23 c                   if        avaflt = 'FOVREF'  and
6.23 c                             avaval = 1
6.23 c                   eval      u_vref = *on
6.23 c                   endif
6.23 c                   if        avaflt = 'FONAVN'  and
6.23 c                             avaval = 1
6.23 c                   eval      u_navn = *on
6.23 c                   endif
6.23 c     avall1_key    reade     avall1
6.23 c                   enddo
6.23  *
7.01  *
7.01  * Sjekker om det ikke er krav til nobbnummer
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'COBU_IKKENOBB':
7.01   co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_s701 = *on;
7.01   endif;
7.01  *

      *
     c                   endsr
