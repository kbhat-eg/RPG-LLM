     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LI811R
      * Beskrivelse . . : Bygger info rundt bestilling fra EDI pakkseddel
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       081122 bsa  Nytt program
7.01  *       041019 bsa  Må finne ut om bestilling er gyldig
7.02  *       220120 bsa  Status må endres.
7.03  *       040220 bsa  Justert felter.
7.04  * K000  301120 bsa  Sjekker lengden på bestillingsnummer. Høyrestiller
7.04  * K000              og fyller med foranstilte 0'er.
7.05  * K000  111220 bsa  Justering av fiks 7.04
      *
8.01  * K000  270422 bsa  Sjekker om pakkseddel skal oppdateres
8.01  * K000              automatisk.
8.02  * K000  090622 bsa  Uheldig låsing av poster.
8.03  * K000  200622 bsa  Feil logikk ved behandling av poster.
8.04  * K000  090822 bsa  Hvis det finnes carrier part på pakkseddelen, skal
8.04  * K000              den starte oppdatering automatisk. Endrer logikken
8.04  * K000              rundt automatisk oppdatering.
8.05  * K000  240822 bsa  Skal automatisk oppdateres, hvis tilknyttet SO
8.05  * K000              er koblet til kjørekontor.
8.06  * K000  061222 bsa  Flytte sjekk om oppdatering av tilhørende salgsordre.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flphoir    if   e           k disk    rename(lphost:lphoirr)
     flpkliu    uf   e           k disk    rename(lpklst:lpkliur)
     flpkoi1    if   e           k disk    rename(lpkost:lpkoi1r)
     flbhoiu    uf a e           k disk    rename(lbhost:lbhoiur)
     flbliiu    uf a e           k disk    rename(lblist:lbliiur)
7.01 flohel1    if   e           k disk    rename(lohepfr:lohel1r)
8.01 flldnl1    if   e           k disk    rename(lldnpfr:lldnl1r)
8.04 flppai1    if   e           k disk    rename(lppast:lppai1r)
8.05 fra17l1    if   e           k disk    rename(ra17pfr:ra17l1r)
8.05 ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
7.04 d                 ds
7.04 d d_inpu                         8
7.04 d  d_bestn                       8  0 overlay(d_inpu:1)
7.04 d   d_besta                      8    overlay(d_bestn:1)
7.04 d   d_best7                      7    overlay(d_bestn:1)
7.04 d   d_best6                      6    overlay(d_bestn:1)
7.04 d   d_best5                      5    overlay(d_bestn:1)
7.04 d   d_best4                      4    overlay(d_bestn:1)
7.04 d   d_best3                      3    overlay(d_bestn:1)
7.04 d   d_best2                      2    overlay(d_bestn:1)
7.04 d   d_best1                      1    overlay(d_bestn:1)
      *
      * Definering av variable i nøkler
      *
     d lphoir_type     s                   like(phtype)
      *
     d lpkliu_type     s                   like(pltype)
     d lpkliu_mnum     s                   like(plmnum)
     d lpkliu_aarr     s                   like(plaarr)
     d lpkliu_paid     s                   like(plpaid)
     d lpkliu_pver     s                   like(plpver)
      *
     d lpkoi1_type     s                   like(potype)
     d lpkoi1_mnum     s                   like(pomnum)
     d lpkoi1_aarr     s                   like(poaarr)
     d lpkoi1_paid     s                   like(popaid)
     d lpkoi1_pver     s                   like(popver)
     d lpkoi1_fsni     s                   like(pofsni)
     d lpkoi1_fniv     s                   like(pofniv)
     d lpkoi1_sscc     s                   like(posscc)
      *
     d lbhoiu_hssc     s                   like(pbhssc)
     d lbhoiu_hnum     s                   like(pbhnum)
     d lbhoiu_hsuf     s                   like(pbhsuf)
     d lbhoiu_hpid     s                   like(pbhpid)
     d lbhoiu_hpve     s                   like(pbhpve)
     d lbhoiu_haar     s                   like(pbhaar)
      *
     d lbliiu_lssc     s                   like(pblssc)
     d lbliiu_lnum     s                   like(pblnum)
     d lbliiu_lsuf     s                   like(pblsuf)
     d lbliiu_llin     s                   like(pbllin)
     d lbliiu_lpid     s                   like(pblpid)
     d lbliiu_lpve     s                   like(pblpve)
     d lbliiu_laar     s                   like(pblaar)
      *
7.01 d lohel1_numm     s                   like(lonumm)
7.01 d lohel1_suff     s                   like(losuff)
      *
8.01 d lldnl1_ldor     s                   like(lnldor)
8.01 d lldnl1_lage     s                   like(lnlage)
      *
8.04 d lppai1_type     s                   like(pptype)
8.04 d lppai1_mnum     s                   like(ppmnum)
8.04 d lppai1_aarr     s                   like(ppaarr)
8.04 d lppai1_paid     s                   like(pppaid)
8.04 d lppai1_pver     s                   like(pppver)
8.04 d lppai1_paty     s                   like(pppaty)
8.05 d ra17l1_qkod     s                   like(raqkod)
8.05 d fohel1_numm     s                   like(fonumm)
8.05 d fohel1_suff     s                   like(fosuff)
      *
      * Definering av variable
      *
     d w_firm          s                   like(phfirm)
7.01 d w_numm          s                   like(lonumm)
7.01 d w_leng          s              2  0
7.01 d w_anum          s             17
7.01 d w_anum8         s              8
      *
7.01 d b_best          s              1    inz(*off)
8.01 d b_pakk          s              1    inz(*off)
      *
8.01 d p_type          s              4
8.01 d p_mnum          s              8  0
8.01 d p_aarr          s              4  0
8.01 d p_paid          s             17
8.01 d p_pver          s              3  0
8.01 d p_orgi          s              5
8.01 d p_levr          s                   like(loldor)
      *
7.01 d c_digi          c                   '0123456789'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   exsr      bygg_Best
      *
      * Avslutt program
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å flytte poster på lager til annet lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bygg_Best     begsr
      *
     c                   eval      lphoir_type = *blanks
     c     lphoir_key    setll     lphoir
     c     lphoir_key    reade     lphoir
     c                   dow       not %eof
      *
      * Finnes bestilling som ikke er ferdigbehandlet = Status <> 99
      *
7.02 c**                 if        phstat <> '99'
7.02 c                   if        phstat < '90'
      *
      * Les direkte på kollilinjer, og legg ut de som har ref til bestillingslinjer
      *
     c                   eval      lpkliu_type = phtype
     c                   eval      lpkliu_mnum = phmnum
     c                   eval      lpkliu_aarr = phaarr
     c                   eval      lpkliu_paid = phpaid
     c                   eval      lpkliu_pver = phpver
     c     lpkliu_key    setll     lpkliu
     c     lpkliu_key    reade     lpkliu
     c                   dow       not %eof
      *
7.01 c                   eval      b_best = *off
7.01 c                   eval      w_numm = 0
7.04 c                   eval      w_anum = *blanks
7.04 c                   eval      d_besta = *blanks
7.01 c                   if        %trim(plbnum) <> *blanks
7.04 c**                 eval      w_anum = %trim(plbnum)
7.04 c**                 eval      w_leng = %len(%trim(w_anum))
7.04 c                   eval      d_besta = %trim(plbnum)
7.04 c                   eval      w_leng = %len(%trim(d_besta))
7.04 c                   if        w_leng = 1
7.04 c                   move      d_best1       w_anum
7.04 c                   elseif    w_leng = 2
7.04 c                   move      d_best2       w_anum
7.04 c                   elseif    w_leng = 3
7.04 c                   move      d_best3       w_anum
7.04 c                   elseif    w_leng = 4
7.04 c                   move      d_best4       w_anum
7.04 c                   elseif    w_leng = 5
7.04 c                   move      d_best5       w_anum
7.04 c                   elseif    w_leng = 6
7.04 c                   move      d_best6       w_anum
7.04 c                   elseif    w_leng = 7
7.04 c                   move      d_best7       w_anum
7.04 c                   elseif    w_leng = 8
7.04 c                   move      d_besta       w_anum
7.04 c                   endif
7.01 c                   if        w_leng < 9
7.05 c     ' ':'0'       xlate     w_anum        w_anum
7.05 c                   move      w_anum        w_anum8
7.05 c**                 eval      w_anum8 = %trim(w_anum)
7.01 c     ' ':'0'       xlate     w_anum8       w_anum8
7.01 c                   if        %check(c_digi:w_anum8) = 0
7.01 c                   eval      w_numm = %int(w_anum8)
7.01 c                   eval      lohel1_numm = w_numm
7.01 c                   eval      lohel1_suff = 0
7.01 c     lohel1_key    chain     lohel1
7.01 c                   if        %found
7.01 c                   eval      b_best = *on
7.01 c                   endif
7.01 c                   endif
7.01 c                   endif
7.01 c                   endif
7.01  *
7.01 c                   eval      plnumm = w_numm
7.01 c                   update    lpkliur
7.01  *
7.01 c                   if        b_best = *on
      * Hent også info fra kolliheader
     c                   eval      lpkoi1_type = pltype
     c                   eval      lpkoi1_mnum = plmnum
     c                   eval      lpkoi1_aarr = plaarr
     c                   eval      lpkoi1_paid = plpaid
     c                   eval      lpkoi1_pver = plpver
     c                   eval      lpkoi1_fsni = plfsni
     c                   eval      lpkoi1_fniv = plfniv
     c                   eval      lpkoi1_sscc = plsscc
     c     lpkoi1_key    chain     lpkoi1
     c                   if        not %found
     c                   eval      popant = 0
     c                   endif
      *
     c                   eval      lbhoiu_hssc = plsscc
     c                   eval      lbhoiu_hnum = plnumm
     c                   eval      lbhoiu_hsuf = plsuff
     c                   eval      lbhoiu_hpid = plpaid
     c                   eval      lbhoiu_hpve = plpver
     c                   eval      lbhoiu_haar = plaarr
     c     lbhoiu_key    chain     lbhoiu
      * Legg ut ny bestref hvis ikke finnes.
     c                   if        not %found
     c                   clear                   lbhoiur
     c                   eval      pbhfir = w_firm
     c                   eval      pbhssc = plsscc
     c                   eval      pbhnum = plnumm
     c                   eval      pbhsuf = plsuff
     c                   eval      pbhpid = plpaid
     c                   eval      pbhpve = plpver
     c                   eval      pbhaar = plaarr
      *
     c                   eval      pbhmnu = plmnum
     c                   eval      pbhpdt = phpadt
7.03 c                   eval      pbhbnu = plbnum
     c                   eval      pbhlon = phlonr
     c                   eval      pbhpnr = phprnr
     c                   eval      pbhpan = popant
     c                   eval      pbhfsn = plfsni
     c                   eval      pbhfni = plfniv
     c                   eval      pbhmva = phmvar
      *
     c                   time                    pbhoda
     c                   time                    pbhoti
     c                   eval      pbhous = l_user
     c                   time                    pbheda
     c                   time                    pbheti
     c                   eval      pbheus = l_user
     c                   write     lbhoiur
8.02 c                   else
8.02 c                   unlock    lbhoiu
     c                   endif
      * Skriv linjer også
     c                   eval      lbliiu_lssc = plsscc
     c                   eval      lbliiu_lnum = plnumm
     c                   eval      lbliiu_lsuf = plsuff
     c                   eval      lbliiu_llin = plblin
     c                   eval      lbliiu_lpid = plpaid
     c                   eval      lbliiu_lpve = plpver
     c                   eval      lbliiu_laar = plaarr
     c     lbliiu_key    chain     lbliiu
      * Legg ut ny bestref hvis ikke finnes.
     c                   if        not %found
     c                   clear                   lbliiur
     c                   eval      pblfir = w_firm
     c                   eval      pblssc = plsscc
     c                   eval      pblnum = plnumm
     c                   eval      pblsuf = plsuff
     c                   eval      pbllin = plblin
     c                   eval      pblpid = plpaid
     c                   eval      pblpve = plpver
     c                   eval      pblaar = plaarr
      *
     c                   eval      pblgti = plgtin
     c                   eval      pbllva = pllvar
     c                   eval      pblnob = plnobb
     c                   eval      pbltun = pltunn
     c                   eval      pblfin = plfinf
     c                   eval      pblbes = plnavn
     c                   eval      pblban = plbant
     c                   eval      pblben = plbenh
     c                   eval      pblpan = plpant
     c                   eval      pblpen = plpenh
     c                   eval      pblran = plrant
     c                   eval      pblren = plrenh
     c                   eval      pblrkd = plrkod
     c                   eval      pblrbe = plrbes
     c                   eval      pblrdt = plrdat
     c                   eval      pblvan = plvant
     c                   eval      pblven = plvenh
     c                   eval      pblfsn = plfsni
     c                   eval      pblfni = plfniv
      *
     c                   time                    pbloda
     c                   time                    pbloti
     c                   eval      pblous = l_user
     c                   time                    pbleda
     c                   time                    pbleti
     c                   eval      pbleus = l_user
     c                   write     lbliiur
8.02 c                   else
8.02 c                   unlock    lbliiu
     c                   endif
      *
7.01 c                   endif
      *
     c     lpkliu_key    reade     lpkliu
     c                   enddo
8.06  *
8.01  * Sjekk om pakkseddelen skal oppdateres automatisk.
8.01  *
8.01 c                   if        b_best = *on
8.01 c                   exsr      upd_pakk
8.01 c                   endif
      *
     c                   endif
      *
     c     lphoir_key    reade     lphoir
     c                   enddo
      *
     c                   endsr
      *
8.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.01  * Subrutine for å starte oppdatering av pakkseddelen
8.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.01  *
8.01 c     upd_pakk      begsr
8.01  *
8.01 c                   eval      b_pakk = *off
8.01 c                   eval      lldnl1_ldor = loldor
8.01 c                   eval      lldnl1_lage = %char(lolage)
8.01 c     lldnl1_key    chain     lldnl1
8.01 c                   if        %found
8.01 c                   if        lnapak = 1
8.01 c                   eval      b_pakk = *on
8.01 c                   endif
8.01 c                   else
8.01 c                   eval      lldnl1_lage = *blanks
8.01 c     lldnl1_key    chain     lldnl1
8.01 c                   if        %found
8.01 c                   if        lnapak = 1
8.01 c                   eval      b_pakk = *on
8.01 c                   endif
8.01 c                   endif
8.01 c                   endif
8.04  *
8.04  * Hvis ikke automatisk oppdatering på leverandøren, skal vi allikevel
8.04  * gjøre det hvis pakkseddelen er gått via distribusjonslager. Det betyr
8.04  * at varemottak er gjort der, og vi må bare stole på at alle kvantum
8.04  * osv er riktig.
8.04  *
8.04 c                   if        b_pakk = *off
8.04 c                   eval      lppai1_type = phtype
8.04 c                   eval      lppai1_mnum = phmnum
8.04 c                   eval      lppai1_aarr = phaarr
8.04 c                   eval      lppai1_paid = phpaid
8.04 c                   eval      lppai1_pver = phpver
8.04 c                   eval      lppai1_paty = 'CA'
8.04 c     lppai1_key    chain     lppai1
8.04 c                   if        %found
8.04 c                   eval      b_pakk = *on
8.04 c                   endif
8.04 c                   endif
8.05  *
8.05  * Overstyres også hvis tilknyttet SO er koblet til kjørekontor.
8.05  *
8.05 c                   if        b_pakk = *off
8.05 c                   if        loornr <> 0
8.05  *
8.05 c                   eval      fohel1_numm = loornr
8.05 c                   eval      fohel1_suff = loorsu
8.05 c     fohel1_key    chain     fohel1
8.05 c                   if        %found
8.05 c                   eval      ra17l1_qkod = folmat
8.05 c     ra17l1_key    chain     ra17l1
8.05 c                   if        %found and
8.05 c                             raqkjo = 1
8.05 c                   eval      b_pakk = *on
8.05 c                   endif
8.05 c                   endif
8.05  *
8.05 c                   endif
8.05 c                   endif
8.01  *
8.01  * Hvis automatisk oppdatering, start oppdatering nå
8.01  *
8.01 c                   if        b_pakk = *on
8.01  *
8.01 c                   eval      p_type = *blanks
8.01 c                   eval      p_mnum = phmnum
8.01 c                   eval      p_aarr = phaarr
8.01 c                   eval      p_paid = phpaid
8.01 c                   eval      p_pver = phpver
8.01 c                   eval      p_orgi = 'LI200'
8.01 c                   eval      p_levr = loldor
8.01  *
8.01 c                   call      'LI320R'
8.01 c                   parm                    p_type
8.01 c                   parm                    p_mnum
8.01 c                   parm                    p_aarr
8.01 c                   parm                    p_paid
8.01 c                   parm                    p_pver
8.01 c                   parm                    p_orgi
8.01 c                   parm                    p_levr
8.01  *
8.01 c                   endif
8.01  *
8.01 c                   endsr
8.01  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les på EDI pakksedler - hode
      *
     c     lphoir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lphoir_type
7.01  *
7.01  * Key for les på bestilling - hode
7.01  *
7.01 c     lohel1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    lohel1_numm
7.01 c                   kfld                    lohel1_suff
8.01  *
8.01  * Key for les på leverandør tilleggsinformasjon
8.01  *
8.01 c     lldnl1_key    klist
8.01 c                   kfld                    w_firm
8.01 c                   kfld                    lldnl1_ldor
8.01 c                   kfld                    lldnl1_lage
      *
      *  Key for oppdatering EDI Pakkseddel kolli - linjer
      *
     c     lpkliu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lpkliu_type
     c                   kfld                    lpkliu_mnum
     c                   kfld                    lpkliu_aarr
     c                   kfld                    lpkliu_paid
     c                   kfld                    lpkliu_pver
      *
      *  Key for oppdatering EDI Pakkseddel kolli - hode
      *
     c     lpkoi1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lpkoi1_type
     c                   kfld                    lpkoi1_mnum
     c                   kfld                    lpkoi1_aarr
     c                   kfld                    lpkoi1_paid
     c                   kfld                    lpkoi1_pver
     c                   kfld                    lpkoi1_fsni
     c                   kfld                    lpkoi1_fniv
     c                   kfld                    lpkoi1_sscc
      *
      *  Key for oppdatering EDI Pakkseddel Bestillingsnummer - hode
      *
     c     lbhoiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbhoiu_hssc
     c                   kfld                    lbhoiu_hnum
     c                   kfld                    lbhoiu_hsuf
     c                   kfld                    lbhoiu_hpid
     c                   kfld                    lbhoiu_hpve
     c                   kfld                    lbhoiu_haar
      *
      *  Key for oppdatering EDI Bestillingsnummer - linjer
      *
     c     lbliiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbliiu_lssc
     c                   kfld                    lbliiu_lnum
     c                   kfld                    lbliiu_lsuf
     c                   kfld                    lbliiu_llin
     c                   kfld                    lbliiu_lpid
     c                   kfld                    lbliiu_lpve
     c                   kfld                    lbliiu_laar
8.04  *
8.04  *  Key for oppdatering EDI Pakkseddel parter
8.04  *
8.04 c     lppai1_key    klist
8.04 c                   kfld                    w_firm
8.04 c                   kfld                    lppai1_type
8.04 c                   kfld                    lppai1_mnum
8.04 c                   kfld                    lppai1_aarr
8.04 c                   kfld                    lppai1_paid
8.04 c                   kfld                    lppai1_pver
8.04 c                   kfld                    lppai1_paty
8.05  *
8.05  * Key for les av forsendelsesmåte
8.05  *
8.05 c     ra17l1_key    klist
8.05 c                   kfld                    w_firm
8.05 c                   kfld                    ra17l1_qkod
8.05  *
8.05  * Key for les av ordrehodet
8.05  *
8.05 c     fohel1_key    klist
8.05 c                   kfld                    w_firm
8.05 c                   kfld                    fohel1_numm
8.05 c                   kfld                    fohel1_suff
      *
      * Henter firma
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
