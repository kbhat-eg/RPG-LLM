     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLOGI
      * Program . . . . : MC110R
      * beskrivelse . . : Erstatningsvare, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       100205 BØ   Nytt program
5.60  *       170609 BØ   Erstatningsvare skal arve abckoder
5.61  *       280909 BØ   JUstert logikk vedr. endring
6.10  * 6.10  290609 BSA  Oppdatert med sporingsinformasjon
6.30  * 6.30  050214 BSA  Utvidet med felter for BM pakke
6.31  * 6.30  050214 BSA  Oppdatering av VVAR og VLAG aktiverer trigger
6.31  *                   program, slik at endringsflagg må legges inn.
6.32  * 6.30  270314 BSA  Kan ikke være to like "nye" eller "gamle"
6.32  *                   varer. En gammel vare kan ikke være ny og
6.32  *                   motsatt.
6.33  * 6.30  170614 BKV  Siste innkjøpsdato må være større enn Første innkjøpsdato
6.34  * 6.30  280515 Bsa  Sjekker mot modul. Må ha tilgang via BMP modulen.
7.xx  * 7.00  220316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  *K00    021020 bof  Lagt på en UNLOCK på vvarlu
7.02  * 6.30  300616 Bsa  Setter 0 som sluttdato. Skal fungere som sluttdato ikke satt
7.03  * 6.30  110917 Bsa  Mulighet for å legge inn at dette kun gjelder for et
7.03  *                   lager.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvverl1    if   e           k disk    rename(vverpfr:vverl1r)
     fvverl2    if   e           k disk    rename(vverpfr:vverl2r)
     fvverl3    if   e           k disk    rename(vverpfr:vverl3r)
6.32 fvverl4    if   e           k disk    rename(vverpfr:vverl4r)
     fvverlr    if   e           k disk    rename(vverpfr:vverlrr)
     fvvarlr    if   e           k disk    rename(vvarpfr:vvarlrr)
5.60 fvvarlu    uf   e           k disk    rename(vvarpfr:vvarlur)
5.60 fvlaglr    if   e           k disk    rename(vlagpfr:vlaglrr)
5.60 fvlaglu    uf   e           k disk    rename(vlagpfr:vlaglur)
     fvverlu    uf a e           k disk    rename(vverpfr:vverlur)
7.03 fvve2lu    uf a e           k disk    rename(vve2pfr:vve2lur)
7.03 fvve2l1    if   e           k disk    rename(vve2pfr:vve2l1r)
7.03 fra10l1    if   e           k disk    rename(ra10pfr:ra10l1r)
      *
     fmc110d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d vverl1_envr     s                   like(vvenvr)
6.32 d vverl4_envr     s                   like(vvenvr)
     d vverl2_egvr     s                   like(vvegvr)
     d vverl3_egtx     s                   like(vvegtx)
     d vverlr_envr     s                   like(vvenvr)
     d vverlr_egvr     s                   like(vvegvr)
     d vvarlr_vare     s                   like(vvvare)
     d vverlx_envr     s                   like(vvenvr)
     d vverlu_envr     s                   like(vvenvr)
     d vverlu_egvr     s                   like(vvegvr)
5.60 d vvarlu_vare     s                   like(vvvare)
5.60 d vlaglu_vare     s                   like(vlvare)
5.60 d vlaglu_lage     s                   like(vllage)
5.60 d vlaglr_vare     s                   like(vlvare)
7.03 d ra10l1_jkod     s                   like(rajkod)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_kabc          s              1    inz(*off)
      *
     d w_firm          s                   like(vvefir)
     d w_vare          s                   like(vvvare)
     d w_vtxt          s                   like(vvtek1)
5.60 d w_kabc          s                   like(vvkabc)
6.30 d w_udate         s               d   datfmt(*iso)
6.34 d w_stat          s              1
6.34 d w_disp          s              1
6.34 d w_modu          s             10
6.34 d w_frad          s               d   datfmt(*iso)
6.34 d w_tild          s               d   datfmt(*iso)
7.03 d w_jkod          s              2  0
7.03 d w_jtxt          s             20
      *
     d s_envr          s                   like(vvenvr)
     d s_egvr          s                   like(vvegvr)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
6.34 d c_modu          c                   'BM_MODUL  '
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2envr <> 0 or
     c                             b2egvr <> 0 or
     c                             b2egtx <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'C'
     c                   eval      vverl3_egtx = b1egtx
     c     vverl3_key    setll     vverl3
     c                   when      w_seqe = 'B'
     c                   eval      vverl2_egvr = b1egvr
     c     vverl2_key    setll     vverl2
     c                   other
     c                   eval      vverl1_envr = b1envr
     c     vverl1_key    setll     vverl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Nytt varenr
      *
     c                   if        b2envr <> 0
     c                   eval      w_seqe = *blank
     c                   movel     b2envr        vverl1_envr
     c     vverl1_key    setll     vverl1
     c                   goto      end_pos
     c                   endif
      *
      * Gammelt varenr
      *
     c                   if        b2egvr <> 0
     c                   eval      w_seqe = 'B'
     c                   movel     b2egvr        vverl2_egvr
     c     vverl2_key    setll     vverl2
     c                   goto      end_pos
     c                   endif
      *
      * Varetekst
      *
     c                   if        b2egtx <> *blank
     c                   eval      w_seqe = 'C'
     c                   eval      vverl3_egtx = b2egtx
     c     vverl3_key    setll     vverl3
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2envr = 0
     c                   eval      b2egvr = 0
     c                   eval      b2egtx = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   movel     b1envr        c1envr
     c                   movel     b1egvr        c1egvr
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   movel     b1envr        k1envr
     c                   movel     b1egvr        k1egvr
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1envr = b1envr
     c                   movel     b1txt1        d1navn
     c                   movel     b1egtx        d1egtx
     c                   eval      d1egvr = b1egvr
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   movel     b1envr        c1envr
     c                   movel     b1egvr        c1egvr
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1envr = b2envr
     c                   eval      c1egvr = b2egvr
     c                   eval      c1nvna = *blank
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      c1taga
     c                   endsl
      *
     c                   if        c1envr = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
     c                   movel     c1envr        vvarlr_vare
     c     vvarlr_key    chain     vvarlr
     c                   if        not %found(vvarlr)
     c                   eval      *in31 = *on
     c                   goto      c1taga
     c                   endif
6.32  *
6.32  * En ny vare kan ikke allerede være en erstatningsvare for andre
6.32  *
6.32 c                   movel     c1envr        vverl4_envr
6.32 c     vverl4_key    chain     vverl4
6.32 c                   if        %found
6.32 c                   eval      *in32 = *on
6.32 c                   goto      c1taga
6.32 c                   endif
6.32  *
6.32  * En ny vare kan ikke allerede være en erstatningsvare for andre
6.32  *
6.32 c                   movel     c1envr        vverl2_egvr
6.32 c     vverl2_key    chain     vverl2
6.32 c                   if        %found
6.32 c                   eval      *in33 = *on
6.32 c                   goto      c1taga
6.32 c                   endif
6.32  *
6.32  * En vare som erstattes kan ikke erstatte andre
6.32  *
6.32 c                   movel     c1egvr        vverl4_envr
6.32 c     vverl4_key    chain     vverl4
6.32 c                   if        %found
6.32 c                   eval      *in34 = *on
6.32 c                   goto      c1taga
6.32 c                   endif
6.32  *
6.32  * En vare kan ikke allerede være en erstatningsvare for andre
6.32  *
6.32 c                   movel     c1egvr        vverl2_egvr
6.32 c     vverl2_key    chain     vverl2
6.32 c                   if        %found
6.32 c                   eval      *in35 = *on
6.32 c                   goto      c1taga
6.32 c                   endif
      *
     c                   movel     vvtek1        c1nvna
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   movel     c1envr        vverlr_envr
     c                   movel     c1egvr        vverlr_egvr
     c     vverlr_key    chain     vverlr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1envr = 0
     c                   eval      c1egvr = 0
     c                   eval      c1nvna = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2envr = 0
     c                   eval      b2egvr = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg1       begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
     c                   endsr
5.60  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.60  * Subrutine for utlegging av melding om abckode arv
5.60  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.60  *
5.60 c     xc2msg        begsr
5.60  *
5.60  *
5.60 c                   exfmt     c2msg
5.60  *
5.60  * Behandling av funksjonstaster
5.60  *
5.60 c                   if        *inkc or *inkl
5.60 c                   eval      b_anul = *on
5.60 c                   leavesr
5.60 c                   endif
5.60  *
5.60 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   movel     c1envr        vverlr_envr
     c                   movel     c1egvr        vverlr_egvr
     c     vverlr_key    chain     vverlr
      *
     c                   if        %found
     c                   eval      vvarlr_vare = vvenvr
     c     vvarlr_key    chain     vvarlr
     c                   if        %found(vvarlr)
     c                   eval      c1nvna = vvtek1
     c                   else
     c                   eval      c1nvna = *blank
     c                   endif
     c                   eval      c2egtx = vvegtx
6.30 c                   if        vvfidt <> *loval
6.30 c     *dmy          move      vvfidt        c2fidt
6.30 c                   else
6.30 c                   eval      c2fidt = 0
6.30 c                   endif
6.30 c                   if        vvsidt <> *loval
6.30 c     *dmy          move      vvsidt        c2sidt
6.30 c                   else
6.30 c                   eval      c2sidt = 0
6.30 c                   endif
6.30 c                   eval      c2omre = vvomre
6.30 c                   eval      c2antb = vvantb
      *
     c                   eval      c2eeus = vveeus
     c     *dmy          move      vveoda        c2eoda
     c     *dmy          move      vveeda        c2eeda
5.61 c                   movel     c1egvr        vvarlr_vare
5.61 c     vvarlr_key    chain     vvarlr
5.61 c                   if        %found(vvarlr)
5.61 c                   movel     vvtek1        c2egtx
5.61 c                   eval      w_kabc = vvkabc
5.61 c                   endif
      *
     c                   else
     c                   movel     c1egvr        vvarlr_vare
     c     vvarlr_key    chain     vvarlr
     c                   if        %found(vvarlr)
     c                   movel     vvtek1        c2egtx
5.60 c                   eval      w_kabc = vvkabc
     c                   else
     c                   eval      c2egtx = *blank
     c                   endif
6.30 c     *dmy          move      w_udate       c2fidt
6.30 c     *dmy          move      w_udate       c2sidt
6.30 c                   z-add     1             c2omre
6.30 c                   eval      c2antb = 0
6.30  *
     c                   eval      c2eeus = *blank
     c                   eval      c2eoda = 0
     c                   eval      c2eeda = 0
     c                   endif
7.03  * Hent evnt info med lagerbetingelse
7.03 c     vverlr_key    chain     vve2l1
7.03  *
7.03 c                   if        %found
7.03 c                   eval      c2elag = v2elag
7.03 c                   else
7.03 c                   eval      c2elag = 0
7.03 c                   endif
7.03  *
6.34  * Hvis ikke BMP modul, nullstilles enkelte felter
6.34 c                   if        *in40 = *on
6.34 c                   eval      c2fidt = 0
6.34 c                   eval      c2sidt = 0
6.34 c                   eval      c2omre = 0
6.34 c                   eval      c2antb = 0
6.34 c                   endif
6.34  *
     c                   if        b1valg = 3
     c                   eval      c1envr = k1envr
     c                   eval      c1egvr = k1egvr
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
7.03 c                   exsr      spØrring
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2egtx = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
6.30  * Kun hvis BMP modul
6.34 c                   if        *in40 = *off
6.34  *
6.30 c                   if        c2fidt <> 0
6.30 c     *dmy          test(d)                 c2fidt                 32
6.30 c                   if        *in32 = *on
6.30 c                   goto      c2taga
6.30 c                   endif
6.30 c                   endif
6.30 c                   if        c2sidt <> 0
6.30 c     *dmy          test(d)                 c2sidt                 33
6.30 c                   if        *in33 = *on
6.30 c                   goto      c2taga
6.30 c                   endif
6.30 c                   endif
6.30 c                   if        c2omre = 0
6.30 c                   eval      *in34 = *on
6.30 c                   goto      c2taga
6.30 c                   endif
6.33  *
6.33  *  Siste innkjøpsdato må være større enn Første innkjøpsdato
6.33  *
6.34 c     *dmy          move      c2fidt        w_frad
6.34 c     *dmy          move      c2sidt        w_tild
6.33  *
6.34 c**                 if        c2fidt >= c2sidt
7.02 c                   if        c2sidt <> 0
6.34 c                   if        w_frad >= w_tild
6.33 c                   eval      *in36 = *on
6.33 c                   goto      c2taga
6.33 c                   endif
7.02 c                   endif
7.03  *
7.03  *  Oppgitt verdi må vær gyldig lager
7.03  *
7.03 c                   if        c2elag <> 0
7.03 c                   eval      ra10l1_jkod = c2elag
7.03 c     ra10l1_key    chain     ra10l1
7.03 c                   if        not %found
7.03 c                   eval      *in41 = *on
7.33 c                   goto      c2taga
7.03 c                   endif
7.03 c                   endif
6.34  *
6.34 c                   endif
      *
      * sjekker om det finnes tidligere knytninger
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   movel     c1egvr        vverl1_envr
     c     vverl1_key    setll     vverl1
     c                   if        %found
     c                   dou       %eof
     c     vverl1_key    reade     vverl1
     c                   if        not %eof(vverl1)
     c                   eval      m1egvr = c1egvr
     c                   eval      m1envr = c1envr
     c                   exsr      xm1win
     c                   leave
     c                   endif
     c                   enddo
     c                   endif
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   vverlur
      *
     c                   movel     c1envr        vverlu_envr
     c                   movel     c1egvr        vverlu_egvr
     c     vverlu_key    chain     vverlur
      *
     c                   eval      vvegtx = c2egtx
6.30 c                   eval      vvomre = c2omre
6.30 c                   if        c2fidt <> 0
6.30 c     *dmy          move      c2fidt        vvfidt
6.30 c                   endif
6.30 c                   if        c2sidt <> 0
6.30 c     *dmy          move      c2sidt        vvsidt
7.02 c                   else
7.02 c                   eval      vvsidt = *loval
6.30 c                   endif
      *
     c                   time                    vveeda
     c                   time                    vveeti
     c                   eval      vveeus = l_user
      *
     c                   if        %found
     c                   update    vverlur
     c                   else
     c                   eval      vvefir = w_firm
     c                   movel     c1envr        vvenvr
     c                   movel     c1egvr        vvegvr
     c                   time                    vveoda
6.10 c                   time                    vveoti
6.10 c                   eval      vveous = l_user
     c                   write     vverlur
     c                   endif
7.03  * Legg til i sideregister
7.03 c                   clear                   vve2lur
7.03 c     vverlu_key    chain     vve2lur
7.03  *
7.03 c                   eval      v2elag = c2elag
7.03  *
7.03 c                   time                    v2eeda
7.03 c                   time                    v2eeti
7.03 c                   eval      v2eeus = l_user
7.03  *
7.03 c                   if        %found
7.03 c                   update    vve2lur
7.03 c                   else
7.03 c                   eval      v2efir = w_firm
7.03 c                   movel     c1envr        v2envr
7.03 c                   movel     c1egvr        v2egvr
7.03 c                   time                    v2eoda
7.03 c                   time                    v2eoti
7.03 c                   eval      v2eous = l_user
7.03 c                   write     vve2lur
7.03 c                   endif
5,60  *
5.60 c                   eval      b_kabc = *off
5.60 c                   exsr      upd_kabc
5.60 c                   if        b_kabc = *on
5.60 c                   exsr      xc2msg
5.60 c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1egvr = vvegvr
     c                   eval      b1egtx = vvegtx
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      vverlr_envr = d1envr
     c                   eval      vverlr_egvr = d1egvr
     c     vverlr_key    chain     vverlr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      vverlu_envr = d1envr
     c                   eval      vverlu_egvr = d1egvr
     c     vverlu_key    chain     vverlur
      *
     c                   delete    vverlur
7.03  * Må også slettes fra sideregister
7.03 c     vverlu_key    chain     vve2lur
7.03 c                   if        %found
7.03 c                   delete    vve2lur
7.03 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   movel     k1envr        s_envr
     c                   movel     k1egvr        s_egvr
      *
      * Fyller kopierings-bildet
      *
     c                   movel     k1envr        vverlr_envr
     c                   movel     k1egvr        vverlr_egvr
     c     vverlr_key    chain     vverlr
     c                   movel     vvenvr        k2envr
     c                   movel     vvegvr        k2egvr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
     c                   if        *inkd
     c                   exsr      spØrring
     c                   goto      k1taga
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1envr = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
     c                   movel     k1envr        vvarlr_Vare
     c     vvarlr_key    chain     vvarlr
     c                   if        not %found(vvarlr)
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      *
     c                   if        k1egvr = 0
     c                   eval      *in33 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   movel     k1envr        vverlr_envr
     c                   movel     k1egvr        vverlr_egvr
     c     vverlr_key    chain     vverlr
     c                   if        %found
     c                   eval      *in34 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   movel     s_envr        c1envr
     c                   movel     s_egvr        c1egvr
     c                   exsr      xc2bld
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (M1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xm1win        begsr
      *
     c                   exfmt     m1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * her slettes gamle knytninger og gir de nye
      *
     c                   movel     c1egvr        vverlx_envr
     c     vverlx_key    setll     vverlr
     c                   if        %found(vverlr)
     c                   dou       %eof(vverlr)
     c     vverlx_key    reade     vverlr
     c                   if        not %eof(vverlr)
     c                   eval      vverlu_envr = vvenvr
     c                   eval      vverlu_egvr = vvegvr
     c     vverlu_key    chain     vverlur
     c                   if        %found(vverlu)
     c                   movel     c1envr        vvenvr
6.10 c                   time                    vveeda
6.10 c                   time                    vveeti
6.10 c                   eval      vveeus = l_user
     c                   update    vverlur
     c                   endif
     c                   endif
     c                   enddo
     c                   endif
      *
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'C'
     c                   read      vverl3
     c                   when      w_seqe = 'B'
     c                   read      vverl2
     c                   other
     c                   read      vverl1
     c                   endsl
      *
     c                   if        %eof or
     c                             vvefir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'C'
     c                   eval      vverl3_egtx = vvegtx
     c                   when      w_seqe = 'B'
     c                   eval      vverl2_egvr = vvegvr
     c                   other
     c                   eval      vverl1_envr = vvenvr
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1envr = vvenvr
     c                   eval      vvarlr_vare = vvenvr
     c     vvarlr_key    chain     vvarlr
     c                   if        %found(vvarlr)
     c                   movel     vvtek1        b1txt1
     c                   else
     c                   eval      b1txt1 = *blank
     c                   endif
      *
     c                   eval      b1egvr = vvegvr
     c                   eval      b1egtx = vvegtx
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'C'
     c     vverl3_key    setgt     vverl3
     c                   readp     vverl3
     c                   when      w_seqe = 'B'
     c     vverl2_key    setgt     vverl2
     c                   readp     vverl2
     c                   other
     c     vverl1_key    setgt     vverl1
     c                   readp     vverl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'C'
     c                   readp     vverl3
     c                   when      w_seqe = 'B'
     c                   readp     vverl2
     c                   other
     c                   readp     vverl1
     c                   endsl
      *
     c                   if        %eof or
     c                             vvefir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'C'
     c                   eval      vverl3_egtx = vvegtx
     c                   when      w_seqe = 'B'
     c                   eval      vverl2_egvr = vvegvr
     c                   other
     c                   eval      vverl1_envr = vvenvr
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'C'
     c     vverl3_key    setll     vverl3
     c                   when      w_seqe = 'B'
     c     vverl2_key    setll     vverl2
     c                   other
     c     vverl1_key    setll     vverl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Vare
     c                   if        w_afld = 'C1ENVR'
     c                   movel     c1envr        w_vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   movel     w_vare        c1envr
     c                   goto      end_spØrr
     c                   endif
      * Vare
     c                   if        w_afld = 'C1EGVR'
     c                   movel     c1egvr        w_vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   movel     w_vare        c1egvr
     c                   goto      end_spØrr
     c                   endif
      * Vare
     c                   if        w_afld = 'K1ENVR'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   movel     w_vare        k1envr
     c                   goto      end_spØrr
     c                   endif
      * Vare
     c                   if        w_afld = 'K1EGVR'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   movel     w_vare        k1egvr
     c                   goto      end_spØrr
     c                   endif
7.03  * Lager
7.03 c                   if        w_afld = 'C2ELAG'
7.03 c                   call      'RA510R'
7.03 c                   parm                    w_firm
7.03 c                   parm                    w_jkod
7.03 c                   parm                    w_jtxt
7.03 c                   if        w_jkod <> 0
7.03 c                   eval      c2elag = w_jkod
7.03 c                   goto      end_spØrr
7.03 c                   endif
7.03 c                   endif
      *
     c     end_spØrr     endsr
5.60  *
5.60  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.60  * Subrutine for å arve abc-kode fra gml.vare til ny hvis ny ikke
5.60  *               har abckode
5.60  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.60  *
5.60 c     upd_kabc      begsr
5.60  *
     c                   movel     c1envr        vvarlu_vare
5.60 c     vvarlu_key    chain     vvarlu
5.60 c                   if        %found(vvarlu) and
6.31 c                             vvkabc <> w_kabc and
6.31 c                             w_kabc <> *blanks
5.60 c                   eval      vvkabc = w_kabc
6.31 c                   time                    vvetim
6.31 c                   time                    vvedat
6.31 c                   eval      vveusr = l_user
5.60 c                   update    vvarlur
     c                   eval      b_kabc = *on
7.01 c                   else
7.01 c                   unlock    vvarlu
5.60 c                   endif
5.60  *
     c                   movel     c1egvr        vlaglr_vare
5.60 c     vlaglr_key    setll     vlaglr
5.60 c     vlaglr_key    reade     vlaglr
5.60 c                   dow       not %eof(vlaglr)
5.60 c                   if        vlkabc <> *blank
5.60 c                   eval      w_kabc = vlkabc
     c                   movel     c1envr        vlaglu_vare
5.60 c                   eval      vlaglu_lage = vllage
5.60 c     vlaglu_key    chain     vlaglu
5.60 c                   if        %found(vlaglu)   and
6.31 c                             vlkabc <> w_kabc and
6.31 c                             w_kabc <> *blank
5.60 c                   eval      vlkabc = w_kabc
6.31 c                   time                    vletim
6.31 c                   time                    vledat
6.31 c                   eval      vleusr = l_user
5.60 c                   update    vlaglur
     c                   eval      b_kabc = *on
5.60 c                   endif
5.60 c                   endif
5.60  *
5.60 c     vlaglr_key    reade     vlaglr
5.60 c                   enddo
5.60  *
5.60 c                   endsr
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på nytt varenr
      *
     c     vverl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vverl1_envr
6.32  *
6.32  * Key for posisjonering på nytt varenr
6.32  *
6.32 c     vverl4_key    klist
6.32 c                   kfld                    w_firm
6.32 c                   kfld                    vverl4_envr
      *
      * Key for posisjonering på gammel varenr
      *
     c     vverl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vverl2_egvr
      *
      * Key for posisjonering på gammel varenr
      *
     c     vverl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vverl3_egtx
      *
      * Key for les på nytt varenr
      *
     c     vverlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vverlr_envr
     c                   kfld                    vverlr_egvr
      *
      * Key for oppdatering av nytt varenr
      *
     c     vverlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vverlu_envr
     c                   kfld                    vverlu_egvr
      *
      * Key for les av nytt varenr
      *
     c     vverlx_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vverlx_envr
      *
      * Key for les av vare
      *
     c     vvarlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlr_vare
5.60  *
5.60  * Key for oppdater vare
5.60  *
5.60 c     vvarlu_key    klist
5.60 c                   kfld                    w_firm
5.60 c                   kfld                    vvarlu_vare
5.60  *
5.60  * Key for oppdater lager
5.60  *
5.60 c     vlaglu_key    klist
5.60 c                   kfld                    w_firm
5.60 c                   kfld                    vlaglu_vare
5.60 c                   kfld                    vlaglu_lage
5.60  *
5.60  * Key for les lager
5.60  *
5.60 c     vlaglr_key    klist
5.60 c                   kfld                    w_firm
5.60 c                   kfld                    vlaglr_vare
7.03  *
7.03  * Key for les lager
7.03  *
7.03 c     ra10l1_key    klist
7.03 c                   kfld                    w_firm
7.03 c                   kfld                    ra10l1_jkod
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
6.30 c                   time                    w_udate
      *
     c                   eval      *in22 = *on
6.34  * Sjekk om du har tilgang til alle felter
6.34 c                   eval      *in40 = *off
6.34 c                   eval      w_disp = '0'
6.34 c                   eval      w_stat = '0'
6.34 c                   eval      w_modu = c_modu
6.34 c                   call      'AX700R'
6.34 c                   parm                    w_modu
6.34 c                   parm                    w_disp
6.34 c                   parm                    w_stat
6.34  *
6.34 c                   if        w_stat = '1'
6.34 c                   eval      *in40 = *on
6.34 c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      vverl1_envr = *blank
     c     vverl1_key    setll     vverl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
