      /TITLE  ASLAGR: Lagertelling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : VL003R
      *  Beskrivelse . . : Omberegner beholdning på historisk lagerbok
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  -------------------------------------------------
      *            150109  Nytt program                            bof
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvhisl1    if   e           k disk    rename(vhispfr:vhisl1r)
     fvhislu    uf   e           k disk    rename(vhispfr:vhislur)
      *
      * Local Data Area
      *
     d                uds
     d l_firm                944    946  0
      *
      *   Definering av variable i nøkler
      *
     d vhislu_dato     s                   like(vhdato)
     d vhislu_lage     s                   like(vhlage)
     d vhislu_time     s                   like(vhtime)
     d vhislu_vare     s                   like(vhvare)
     d vlaglu_lage     s                   like(vhlage)
     d vlaglu_vare     s                   like(vhvare)
     d vhisl1_lage     s                   like(vhlage)
     d vhisl1_vare     s                   like(vhvare)
     d vhisl1_dato     s                   like(vhdato)
     d vhisl1_time     s                   like(vhtime)
      *
      *   Definering av variable
      *
     d p_dato          s                   like(vhdato)
     d p_time          s                   like(vhtime)
     d p_vare          s                   like(vhvare)
     d p_lage          s                   like(vhlage)
     d w_firm          s                   like(vhfirm)
     d w_behl          s                   like(vhbehl)
      *
     d b_forst         s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandle utplukk, Setter inn riktige parameterverdier
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c                   exsr      finn_behold
      *
     c     vhislu_key    setll     vhislur
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser poster fra register for utplukks-test
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     start         tag
      *
     c                   eval      *in90 = *off
     c                   read      vhislur                                90
      *
     c                   if        *in90 = *on
     c                   goto      end_pgm
     c                   endif
      *
      *  Sjekk   Er firmanummer innenfor grensene, Ellers hopp til Slutt
      *
     c                   if        vhfirm <> w_firm
     c                   goto      end_pgm
     c                   endif
      *
      *  Sjekk   Er varenummer innenfor grensene, Ellers hopp til slutt
      *
     c                   if        vhvare <> p_vare
     c                   goto      end_pgm
     c                   endif
      *
      *  Sjekk   Er lager riktig                , Ellers hopp til Start
      *
     c                   if        vhlage <> p_lage
     c                   goto      end_pgm
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppdaterer  historisk lagerbok
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * første gangen så legges beholdnig fra sist trans dagen før inn
     c                   if        b_forst = *off
     c                   eval      vhbehl = w_behl
     c                   eval      b_forst = *on
     c                   endif
      *
      *  Alle transaksjoner fra historisk lagerbok
      *
     c                   if        vhtype = 'U'
     c                   eval      w_behl = w_behl - vhanta
     c                   eval      vhbehl = w_behl
     c                   else
     c                   eval      w_behl = w_behl + vhanta
     c                   eval      vhbehl = w_behl
     c                   endif
      *
     c                   update    vhislur
      *
      *  Velg neste record fra HISTORISK LAGERBOK
      *
     c                   goto      start
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne beholdning på angitt dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_behold   begsr
      *
     c                   eval      w_behl = 0
     c                   eval      vhisl1_dato = p_dato
     c                   eval      vhisl1_time = *loval
     c     vhisl1_key    setll     vhisl1r
     c                   readp     vhisl1r
      *
     c                   if        %eof = *off and
     c                             vhfirm = w_firm and
     c                             vhvare = p_vare and
     c                             vhlage = p_lage
     c                   eval      w_behl = vhbehl
     c                   endif
      *
     c     end_dato      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for file : HISTORISK-LAGERBOK-REGISTER
      *
     c     vhislu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhislu_lage
     c                   kfld                    vhislu_vare
     c                   kfld                    vhislu_dato
     c                   kfld                    vhislu_time
      *
      *  Key for les av HISTORISK-LAGERBOK
      *
     c     vhisl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhisl1_lage
     c                   kfld                    vhisl1_vare
     c                   kfld                    vhisl1_dato
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_dato
     c                   parm                    p_vare
     c                   parm                    p_lage
      *
      *  Legge inn startverdier i nøklene
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      vhislu_lage = p_lage
     c                   eval      vhislu_vare = p_vare
     c                   eval      vhislu_dato = p_dato
     c                   eval      vhislu_time = *loval
     c                   eval      vhisl1_lage = p_lage
     c                   eval      vhisl1_vare = p_vare
      *
     c                   endsr
