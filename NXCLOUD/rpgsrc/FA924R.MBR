     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FA924R
      * Beskrivelse . . : Oppdatering av akk saldo i kunde-register (Almrab)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       180408 AMD  Nytt program
6.10  * V6.10 230909 BSA  Lagt inn sporingsfelter
6.11  * V6.10 070510 BSA  Endret sporingsmåte
6.30  * V6.30 100914 BSA  Felt flyttet fra RKUN til RKME (RKKJAL)
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.30 f*rkunlu    uf   e           k disk    rename(rkunpfr:rkunlur)
6.30 frkmelu    uf   e           k disk    rename(rkmepfr:rkmelur)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
      *
      * Definering av parametere
      *
     d p_firm          s                   like(rkfirm)
     d p_kund          s                   like(rkkund)
     d p_otyp          s                   like(vaotyp)
     d p_sald          s                   like(rkkjal)
      *
      * Definering av variable i nøkler
      *
     d rkunlu_kund     s                   like(rkkund)
     d votyl1_otyp     s                   like(vaotyp)
      *
      * Definering av variable
      *
     d w_firm          s                   like(rkfirm)
      *
      * Definering av Local data area
      *
6.10 d                uds
6.10 d l_user                911    920
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdater akkumulert almenningsrabatt i KUNDE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Hent opplysninger fra ORDRETYPE-REGISTER
      *
     c                   eval      votyl1_otyp = p_otyp
     c     votyl1_key    chain     votyl1r                            90
      *
      * Snur fortegn
      *
     c                   if        vaokre <> *blank
     c                   eval      p_sald = p_sald * -1
     c                   endif
      *
      * Oppdaterer KUNDE-REGISTER
      *
     c                   eval      rkunlu_kund = p_kund
6.30 c**   rkunlu_key    chain     rkunlu                             90
6.30 c     rkunlu_key    chain     rkmelu                             90
     c                   if        *in90 = *off
     c                   eval      rkkjal = rkkjal + p_sald
6.30 c                   time                    rkmeda
6.30 c                   time                    rkmeti
6.30 c                   eval      rkmeus = l_user
6.30 c**                 time                    rk2dpt
6.30 c**                 eval      rkepgm = 'FA924R    '
6.30 c                   update    rkmelur
6.30 c**                 update    rkunlur
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for file : KUNDE-REGISTER
      *
     c     rkunlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunlu_kund
      *
      * Key for file : ORDRETYPE-REGISTER
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_otyp
     c                   parm                    p_kund
     c                   parm                    p_sald
      *
      * Legger inn firmanummer i nøklene
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
