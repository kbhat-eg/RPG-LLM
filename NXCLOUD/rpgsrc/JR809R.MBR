     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JR809R
      * Beskrivelse . . : Leser std. csv format - nobb priser
      *
      *                   Prisoppdatering skjer på bakgrunn av match på
      *                   leverandørens varenummer,nobbnr  eller ean-nr.
      *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       090810 bof  Nytt program
6.11  *       051011 bof  Feilretting i hent av varenr fra gtin-oppslag
6.30  *       240314 bof  Erstattet søk fra JVPPl2 til JVPKl2
6.31  *       230215 bkv  Legger inn enheter for pris leverandør
6.32  * 6.30  290514 BKV  LVAR økt fra 15 til 20      *
6.33  * 6.30  110714 bof  Nytt nivå, JVLEPF
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjrvapf    if   e           k disk
      *
     fjrapl1    if   e           k disk    rename(jrappfr:jrapl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjvarl4    if   e           k disk    rename(jvarpfr:jvarl4r)
     fjvarld    if   e           k disk    rename(jvarpfr:jvarldr)
     fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
6.30 fjvpkl3    if   e           k disk    rename(jvpkpfr:jvpkl3r)
     fjvprlu    uf a e           k disk    rename(jvprpfr:jvprlur)
     fjwrkpf    uf a e           k disk    rename(jwrkpfr:jwrkpfr)
6.31 fjvpklu    uf a e           k disk    rename(jvpkpfr:jvpklur)
6.33 fjvlelu    uf a e           k disk    rename(jvlepfr:jvlelur)
      *
      * Definering av parametre
      *
     d p_list          s             32
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av parameterliste -inn
      *
     d                 ds
     d d_list                        32
     d  d_firm                        3  0 overlay(d_list)
     d  d_rapp                        2  0 overlay(d_list:4)
     d  d_dato                         d   overlay(d_list:6) datfmt(*iso)
      *
      * Definering av recordbeskrivelse - Ahlsell
      *
     d                 ds
     d d_inpu                       140
6.32 d  d_lvnr                       20    overlay(d_inpu:1)
     d  d_netp                       10    overlay(d_inpu:93)
     d   d_netp_kr                    7  0 overlay(d_netp:1)
     d   d_netp_Øre                   2  2 overlay(d_netp:9)
     d  d_eann                       13    overlay(d_inpu:117)
     d  d_eannn                      13  0 overlay(d_eann:1)
     d  d_nobb                        8    overlay(d_inpu:130)
     d  d_nobbn                       8  0 overlay(d_nobb:1)
      *
     d  d_ex           s            512
     d ew92            c                   '       ,  -'
      *
      * Definering av variable i nøkler
      *
     d jrapl1_rapp     s                   like(jrrapp)
     d jvarld_ean1     s                   like(jvean1)
     d jvarl1_vare     s                   like(jvvare)
     d jvarl3_nobb     s                   like(jvnobb)
     d jvarl4_lvar     s                   like(jvlvar)
6.30 d jvpkl3_gtin     s                   like(jwgtin)
     d jvprlu_vare     s                   like(jxvare)
     d jvprlu_ldor     s                   like(jxldor)
6.31 d jvpklu_vare     s                   like(jwvare)
6.31 d jvpklu_ldor     s                   like(jwldor)
6.33 d jvlelu_lvnr     s                   like(jvlvnr)
6.33 d jvlelu_lldo     s                   like(jvlldo)
      *
      * Definering av variable
      *
     d b_inlr          s              1
     d b_ok            s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_netp          s              9  2
     d w_leng          s              2  0
     d w_posi          s              2  0
     d w_navn          s             12
      *
      *
      * Definering av konstanter
      *
     d c_digi          s             15    inz('0123456789, ')
     d c_null          s              7    inz('0000000')
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter rapportør-informasjon
      *
     c                   eval      jrapl1_rapp = d_rapp
     c     jrapl1_key    chain     jrapl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
     c                   exsr      feil_init
      *
      * Leser fil.
      *
     c                   read      jrvapf
     c                   dow       not %eof
     c                   exsr      behandling
     c                   read      jrvapf
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
     c                   if        b_feil = *on
     c                   eval      w_navn = 'fl' +
     c                                      %char(jrldor) +
     c                                      '.xls'
     c                   call      'JR809C '
     c                   parm                    w_navn
     c                   parm                    jrfldr
     c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av varer/priser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   eval      d_inpu = *blank
     c                   exsr      utled_ds
      *
     c                   if        d_lvnr = *blank or b_ok = *off
     c                   goto      end_oppdat
     c                   endif
      *
      * Starter oppdatering
      *
      * prøver med eann
     c                   if        d_eannn <> 0
     c                   eval      jvarld_ean1 = d_eannn
     c     jvarld_key    chain     jvarld
     c                   if        %found(jvarld)
     c                   exsr      oppd_pris
6.31 c                   exsr      dann_pakn
     c                   goto      end_oppdat
     c                   endif
6.30 c                   eval      jvpkl3_gtin = d_eannn
6.30 c     jvpkl3_key    chain     jvpkl3
6.30 c                   if        %found(jvpkl3)
6.30 c                   eval      jvarl1_vare = jwvare
     c     jvarl1_key    chain     jvarl1
     c                   if        %found(jvarl1)
     c                   exsr      oppd_pris
6.31 c                   exsr      dann_pakn
     c                   goto      end_oppdat
     c                   endif
     c                   endif
     c                   endif
      * prøver med nobb.nr
     c                   if        d_nobbn <> 0
     c                   eval      jvarl3_nobb = d_nobbn
     c     jvarl3_key    chain     jvarl3
     c                   if        %found(jvarl3)
     c                   exsr      oppd_pris
6.31 c                   exsr      dann_pakn
     c                   goto      end_oppdat
     c                   endif
     c                   endif
      * prøver med lev.varenr
     c                   eval      jvarl4_lvar = d_lvnr
     c     jvarl4_key    chain     jvarl4
     c                   if        %found(jvarl4)
     c                   exsr      oppd_pris
6.31 c                   exsr      dann_pakn
     c                   goto      end_oppdat
     c                   endif
     c                   exsr      feilliste
      *
     c     end_oppdat    endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av vare-pris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_pris     begsr
      *
      * Sletter priser på leverandør
      *
     c                   eval      jvprlu_vare = jvvare
     c                   eval      jvprlu_ldor = jrldor
     c     jvprlu_key    setll     jvprlu
     c     jvprlu_key    reade     jvprlur
     c                   dow       not %eof
     c                   delete    jvprlur
     c     jvprlu_key    reade     jvprlur
     c                   enddo
      *
     c                   clear                   jvprlur
      *
     c                   eval      jxpris = d_netp_kr + d_netp_Øre
      *
     c                   eval      jxvare = jvvare
     c                   eval      jxldor = jrldor
     c                   eval      jxlvar = d_lvnr
     c                   eval      jxeann = d_eannn
     c                   eval      jxvalu = *blank
     c                   eval      jxgdat = d_dato
     c                   eval      jxtdat = *loval
     c                   eval      jxrsts = 0
     c                   eval      jxeusr = l_user
      *
     c                   time                    jxodat
     c                   time                    jxedat
     c                   time                    jxetim
      *
     c                   write     jvprlur
      *
      *
      * Oppretter/oppdaterer søketekster
      *
     c                   eval      b_inlr = *on
      *
     c                   call      'JV790R'
     c                   parm                    w_firm
     c                   parm                    jvvare
     c                   parm                    b_inlr
      *
     c                   endsr
      *
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  * Subrutine for oppretting av vare-pakning-leverandør
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31
6.31
6.31  *
6.31 c     dann_pakn     begsr
6.31  *
6.31  * Sletter pakning på leverandør
6.31  *
6.31  *
6.31  * Avbryt hvis vareeier
6.31 c                   if        jvldor = jrldor
6.31 c                   leavesr
6.31 c                   endif
6.31  *
6.31  * sletter alle pakninger for pris leverandør
6.31 c                   clear                   jvpklur
6.31 c                   move      jvvare        jvpklu_vare
6.31 c                   eval      jvpklu_ldor = jrldor
6.31 c     jvpklu_key    setll     jvpklu
6.31 c     jvpklu_key    reade     jvpklur
6.31 c                   dow       not %eof
6.31 c                   delete    jvpklur
6.31 c     jvpklu_key    reade     jvpklur
6.31 c                   enddo

6.31  * kopier pakninger for vareeier til prisleverandør
6.31 c                   clear                   jvpklur
6.31 c                   move      jvvare        jvpklu_vare
6.31 c                   eval      jvpklu_ldor = jvldor
6.31 c     jvpklu_key    setll     jvpklu
6.31 c     jvpklu_key    reade     jvpklur
6.31 c                   dow       not %eof
6.31 c                   time                    jwodat
6.31 c                   time                    jwedat
6.31 c                   time                    jwetim
6.31 c                   eval      jweusr = l_user
6.31 c                   eval      jwldor = jrldor
6.31 c                   write     jvpklur
6.31 c     jvpklu_key    reade     jvpklur
6.31 c                   enddo
6.33  *
6.33  * Sletter eventuelt vare-leverandør
6.33  *
6.33 c                   eval      jvlelu_lvnr = jvvare
6.33 c                   eval      jvlelu_lldo = jrldor
6.33 c     jvlelu_key    setll     jvlelu
6.33 c     jvlelu_key    reade     jvlelur
6.33 c                   dow       not %eof
6.33 c                   delete    jvlelur
6.33 c     jvlelu_key    reade     jvlelur
6.33 c                   enddo
6.33  *
6.33  * Oppretter vare leverandør
6.33  *
6.33 c                   clear                   jvlelur
6.33  *
6.33 c                   eval      jvlvnr = jvvare
6.33 c                   movel     jvlvar        jvllva
6.33 c                   eval      jvlldo = jrldor
6.33 c                   eval      jvleus = l_user
6.33 c                   eval      jvlous = l_user
6.33  *
6.33 c                   time                    jvloda
6.33 c                   time                    jvleda
6.33 c                   time                    jvloti
6.33 c                   time                    jvleti
6.33 c                   write     jvlelur
      *
6.31 c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utleding av datastruktur
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utled_ds      begsr
      *
     c     '"':' '       xlate     jrvrad        jrvrad
     c                   eval      b_ok = *off
      *
      * Lev. varenr
      *
     c                   eval      w_posi = %scan(';':jrvrad)
     c                   eval      d_lvnr = %trim(%subst(jrvrad:1:w_posi-1))
      * kun blanke hoppes over
     c                   if        d_lvnr = *blank
     c                   leavesr
     c                   endif
      *
      * EAN
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
     c                   eval      d_eann = %trim(%subst(jrvrad:1:w_posi-1))
     c     ' ':'0'       xlate     d_eann        d_eann
      *
      *  nettopris
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
     c                   eval      d_netp = %trim(%subst(jrvrad:1:w_posi-1))
     c                   if        %check(c_digi : d_netp) > *zero
     c                   leavesr
     c                   endif
      *
      * nobb.nr
      *
     c                   eval      jrvrad = %subst(jrvrad:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':jrvrad)
     c                   if        w_posi = 0
     c                   eval      d_nobb = %trim(%subst(jrvrad:1:8))
     c                   else
     c                   eval      d_nobb = %trim(%subst(jrvrad:1:w_posi-1))
     c                   endif
     c     ' ':'0'       xlate     d_nobb        d_nobb
      *
      * Utleder  nettopris
      *
     c                   if        d_netp <> *blank
      *
     c                   eval      d_netp = %trim(d_netp)
     c                   eval      w_posi = %scan(',':d_netp)
     c                   eval      w_leng = %len(%trim(d_netp))
      *
     c                   if        w_posi = 0
     c                   eval      w_posi = w_leng + 1
     c                   eval      d_netp = %trim(d_netp) + '.00'
     c                   endif
      *
     c     ' ':'0'       xlate     d_netp        d_netp
      *
     c                   if        (w_posi-1) < 7
     c                   eval      d_netp = %subst(c_null:1:7-w_posi+1) +
     c                                      %subst(d_netp:1:w_posi-1) +
     c                                      '.' +
     c                                      %subst(d_netp:w_posi+1:2)
     c                   endif
      *
     c                   else
     c                   eval      d_netp = '0000000.00'
     c                   endif
      *
     c                   eval      b_ok = *on
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å skrive ut varer som ikke ble lest inn
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     feilliste     begsr
      *
     c                   eval      b_feil = *on
     c                   eval      w_netp = d_netp_kr + d_netp_Øre
      *
     c                   eval      d_ex = *blank
     c                   eval      d_ex = %trim(d_lvnr) + X'05' +
     c                                    %char(d_Eannn) + X'05' +
     c                                    %char(d_nobbn) + X'05' +
     c                                    %editw(w_netp : ew92) + X'05'
     c                   eval      jwdata   = d_ex
     c                   write     jwrkpfr
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å nullstiller JWRKPF og skrive heading
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     feil_init     begsr
      *
     c                   read      jwrkpf
     c                   dow       not %eof(jwrkpf)
     c                   delete    jwrkpfr
     c                   read      jwrkpf
     c                   enddo
      *
     c                   eval      d_ex = *blank
     c                   eval      d_ex = 'Lev.varenr.'  + X'05' +
     c                                    'GTIN-nr'      + X'05' +
     c                                    'Nobb.nr '     + X'05' +
     c                                    'Nettopris'    + X'05'
     c                   eval      jwdata   = d_ex
     c                   write     jwrkpfr
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for oppslag på rapportør
      *
     c     jrapl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jrapl1_rapp
      *
      * Key for oppslag på vare m/ean-nr
      *
     c     jvarld_key    klist
     c                   kfld                    jvarld_ean1
      *
      * Key for oppslag på vare m/nobb
      *
     c     jvarl3_key    klist
     c                   kfld                    jvarl3_nobb
      *
      * Key for oppslag på vare m/lev.varenr
      *
     c     jvarl4_key    klist
     c                   kfld                    jvarl4_lvar
      * Key for oppslag på vare     r
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på varepaking med eann
      *
6.30 c     jvpkl3_key    klist
6.30 c                   kfld                    jvpkl3_gtin
      *
      * Key for oppdatering av vare-pris
      *
     c     jvprlu_key    klist
     c                   kfld                    jvprlu_vare
     c                   kfld                    jvprlu_ldor
      *
      * Key for oppdatering av kode/status
      *
     c     jstslu_key    klist
     c                   kfld                    w_firm
6.31  *
6.31  * Key for oppdatering av vare-pakning
6.31  *
6.31 c     jvpklu_key    klist
6.31 c                   kfld                    jvpklu_vare
6.31 c                   kfld                    jvpklu_ldor
6.33  *
6.33  * Key for oppdatering av vare-pris
6.33  *
6.33 c     jvlelu_key    klist
6.33 c                   kfld                    jvlelu_lvnr
6.33 c                   kfld                    jvlelu_lldo
      *
      *
      * Fyller datastruktur fra parameter, og henter firma
      *
     c                   eval      d_list = p_list
     c                   eval      w_firm = d_firm
      *
     c                   endsr
