      /TITLE  ASLAGR Vise fakturalinje
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : SH411R
      * Beskrivelse . . : Vise fakturalinje
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       100698 HKO  Nytt program
      *       260898 HKO  Viser rabattbeløp i tillegg til prosent.
      *                   Viser ordrerabatt og tar denne med i beregningen
      *                   av netto kjøpspris.
6.nu  * R6.30 270514 bsa  Programmet gjennomgått mtp nummerutvidelser.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsihel1    if   e           k disk    rename(sihepfr:sihel1r)
     fsidtl1    if   e           k disk    rename(sidtpfr:sidtl1r)
      *
     fsh411d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_firm          s                   like(slfirm)
     d p_aarr          s                   like(slaarr)
6.nu d p_binr          s                   like(slbinr)
6.nu d p_numm          s                   like(slnumm)
     d p_suff          s                   like(slsuff)
     d p_ktxt          s                   like(slktxt)
     d p_line          s                   like(slline)
      *
      * Definering av Local data area
      *
     d                uds
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d sihel1_aarr     s                   like(shaarr)
     d sihel1_binr     s                   like(shbinr)
     d sihel1_numm     s                   like(shnumm)
     d sihel1_suff     s                   like(shsuff)
     d sidtl1_aarr     s                   like(slaarr)
     d sidtl1_binr     s                   like(slbinr)
     d sidtl1_numm     s                   like(slnumm)
     d sidtl1_suff     s                   like(slsuff)
     d sidtl1_ktxt     s                   like(slktxt)
     d sidtl1_line     s                   like(slline)
      *
      * Definering av variable
      *
     d w_firm          s                   like(slfirm)
     d w_raba          s                   like(slrab1)
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle bildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer skjermbildet
      *
     c                   eval      sihel1_aarr = p_aarr
     c                   eval      sihel1_binr = p_binr
     c                   eval      sihel1_numm = p_numm
     c                   eval      sihel1_suff = p_suff
     c     sihel1_key    chain     sihel1                             60
     c                   if        *in60 = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      sidtl1_aarr = p_aarr
     c                   eval      sidtl1_binr = p_binr
     c                   eval      sidtl1_numm = p_numm
     c                   eval      sidtl1_suff = p_suff
     c                   eval      sidtl1_ktxt = p_ktxt
     c                   eval      sidtl1_line = p_line
     c     sidtl1_key    chain     sidtl1                             60
     c                   if        *in60 = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      b1fkda = 0
     c                   eval      b1binr = slbinr
     c                   eval      b1numm = slnumm
     c                   eval      b1otyp = slotyp
     c                   eval      b1ltyp = slltyp
     c                   eval      b1vare = slvare
     c                   eval      b1tek1 = sltek1
     c                   eval      b1tek2 = sltek2
     c                   eval      b1anta = slanta
     c                   eval      b1enh1 = slenh1
     c***                eval      b1kpri = slkpri
     c                   eval      b1kjop = slkjop
     c                   eval      b1kost = slkost
     c                   eval      b1rkr1 = slrkr1
     c                   eval      b1rkr2 = slrkr2
     c                   eval      b1rkro = slrkro
     c                   eval      b1rab1 = slrab1
     c                   eval      b1rab2 = slrab2
     c                   eval      b1ornr = slornr
      *
     c                   eval      b1kjop = slkjop - slrkr1 - slrkr2 -
     c                                      slrkro
      *
     c                   if        shfkda <> *loval
     c     *dmy          move      shfkda        b1fkda
     c                   endif
      *
      * Behandler bildet
      *
     c     b1taga        tag
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   endsl
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_aarr
6.nu c                   parm                    p_binr
6.nu c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_ktxt
     c                   parm                    p_line
      *
      * Key for oppslag på faktura-hode-register
      *
     c     sihel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sihel1_aarr
     c                   kfld                    sihel1_binr
     c                   kfld                    sihel1_numm
     c                   kfld                    sihel1_suff
      *
      * Key for oppslag på faktura-detalj-register
      *
     c     sidtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sidtl1_aarr
     c                   kfld                    sidtl1_binr
     c                   kfld                    sidtl1_numm
     c                   kfld                    sidtl1_suff
     c                   kfld                    sidtl1_ktxt
     c                   kfld                    sidtl1_line
      *
      *
      * Henter firmakode fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
