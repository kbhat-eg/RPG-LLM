      /TITLE    Kvittering av innkomne returer
     h decedit(',') datedit(*dmy.)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASTELE                                              *
      * Program . . . . : GL151R                                              *
      * Beskrivelse . . : Kvittering av innkomne returer                      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * 07023     070203  Erstattet O-skjema med Printfile.        RH   *
6.30  *  6.30     150814  Innført mulighet for arkivering og PDF   Bsa  *
6.31  * R6.30 180816 bsa  Rokkert på rekkefølge av close printerfile
      *                                                                 *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     fgretpf    ip   e             disk
     fgavtl1    if   e           k disk    rename(gavtpfr:gavtl1r)
      *
     f**liste     o    f  132        printer oflind(*inof)
     fgl151p    o    e             printer
6.30 f                                     usropn
      *
     d kto             s             11    dim(999)
     d txt             s             20    dim(4) ctdata perrcd(1)
      *
     d                 ds
     d gdqret                       160
     d  ocrret                        8    overlay(gdqret)
     d  ocrkto                       11    overlay(gdqret:25)
     d  betf21                        8    overlay(gdqret:41)
     d  betfkt                       11    overlay(gdqret:60)
      *
      *  LDA
      *
     d                uds
6.30 d l_poff                584    584
6.30 d l_utqm                585    594
6.30 d l_bach                595    635
6.30 d l_arch                636    636  0
6.30 d l_skje                637    637  0
6.30 d l_land                638    638  0
6.30 d l_exlp                639    639  0
6.30 d l_mail                640    640  0
6.30 d l_ruti                800    809
6.30 d l_outq                810    819
     d l_user                911    920
     d l_firm                944    946  0
     d l_navn                951    980
      *
     d it              s                   like(ix)
     d iu              s                   like(ix)
     d ix              s              4  0
      *
6.30 d p_btyp          s            100
6.30 d w_year          s              4  0
6.30 d w_dato          s               d   datfmt(*iso)
      *
6.30 d splfname2       s             10a
6.30 d jobname2        s             10a
6.30 d username2       s             10a
6.30 d jobnbr2         s              6a
6.30 d splfnbr2        s             10i 0
6.30 d splfname3       s             10a
6.30 d jobname3        s             10a
6.30 d username3       s             10a
6.30 d jobnbr3         s              6a
6.30 d splfnbr3        s             10i 0
6.30 d p_tagg0         s             20
6.30 d p_tagg0val      s             50
6.30 d p_tagg1         s             20
6.30 d p_tagg1val      s             50
6.30 d p_tagg2         s             20
6.30 d p_tagg2val      s             50
6.30 d p_tagg3         s             20
6.30 d p_tagg3val      s             50
6.30 d p_tagg4         s             20
6.30 d p_tagg4val      s             50
6.30 d p_tagg5         s             20
6.30 d p_tagg5val      s             50
6.30 d p_tagg6         s             20
6.30 d p_tagg6val      s             50
6.30 d p_tagg7         s             20
6.30 d p_tagg7val      s             50
6.30 d p_tagg8         s             20
6.30 d p_tagg8val      s             50
6.30 d p_tagg9         s             20
6.30 d p_tagg9val      s             50
6.30 d p_refn          s             50
6.30 d p_dspl          s            100
      *
6.30 d qsprilsp        pr                  extpgm('QSPRILSP')
6.30 d rcvvar                     32767a   options(*varsize)
6.30 d rcvvarlen                     10i 0 const
6.30 d format                         8a   const
6.30 d errorcode                  32767a   options(*varsize)
      *
6.30 d sprl0100        ds
6.30 d  bytesrtn                     10i 0
6.30 d  bytesavl                     10i 0
6.30 d  splfname                     10a
6.30 d  jobname                      10a
6.30 d  username                     10a
6.30 d  jobnbr                        6a
6.30 d  splfnbr                      10i 0
6.30 d  systemname                    8a
6.30 d  createdate                    7a
6.30 d                                1a
6.30 d  createtime                    6a
      *
6.30 d errorcode       ds
6.30 d  bytesprov                    10i 0 inz(0)
6.30 d  byteavail                    10i 0 inz(0)
      *  Les returer
      *
     c     lesrk         tag
      *
      *  KONTO FINNES/NY
      *
INNL.c                   if        betf21 = 'BETFOR21'
UTL. c                             or betf21 = 'BETFOR01'
     c     betfkt        lookup    kto                                    50
     c                   if        *in50 = *off
     c                   eval      ix = ix + 1
     c                   move      betfkt        kto(ix)
     c                   endif
     c                   endif
      *
     c                   if        ocrret = 'NY090020'
     c     ocrkto        lookup    kto                                    50
     c                   if        *in50 = *off
     c                   eval      ix = ix + 1
     c                   move      ocrkto        kto(ix)
     c                   endif
     c                   endif
      *
     c     slutt         tag
      *
     clr                 exsr      subskr
      * Generer xml for videre utskrift
6.30 clr                 if        l_poff = '1'
6.30 clr                 exsr      spoolnbr
6.31 clr                 close     gl151p
6.31 clr                 exsr      xml_create
6.30 clr                 exsr      pdf_preview
6.30  * Avslutt jobbiden
6.30 clr                 call      'AB710R'
6.30 clr                 parm                    l_bach
6.30 clr                 endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for utskrift av kvittering av returmottak             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     subskr        begsr
      *
07023c*********          except    h101
      *
     c                   eval      it = 1
      *
     c                   dou       it > ix
     c                   eval      iu = 4
     c                   move      kto(it)       gcbgir
     c     avtkey        chain     gavtl1                             41
      *
     c                   if        *in41 = *on
     c                   eval      iu = 3
     c                   eval      gcbgir = *zeros
     c                   eval      d1txt  = txt(3)
     c                   move      kto(it)       d1kto
     c                   else
      *
     c                   eval      d1filg = gcfilg
     c                   eval      d1firm = gcfirm
     c                   move      kto(it)       d1kto
     c                   endif
      *                                                             .
07023c                   write     d1det                                30
      *                                                             .
07023c                   if        *in30 = *on
07023c                   write     a1hdr
07023c                   endif
      *
     c                   eval      gckomm = *blank
     c                   eval      gcfilg = *blank
     c                   eval      gcfirm = *zeros
     c                   eval      gcbgir = *zeros
     c                   eval      gcpgir = *zeros
     c                   eval      d1txt  = *blank
     c                   eval      d1filg = *blank
     c                   eval      d1firm = *zeros
     c                   eval      d1bgir = *zeros
     c                   eval      it = it + 1
     c                   enddo
      *
     c                   endsr
      *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30  * Finner spool som er generert av jobben
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30 c     spoolnbr      begsr
6.30  /Free
6.30
6.30           QSPRILSP( SPRL0100
6.30                   : %size(SPRL0100)
6.30                   : 'SPRL0100'
6.30                   : errorcode );
6.30  /End-free
6.30  *
6.30 c                   endsr
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30  * Kaller eget program for generering av xml fil. Eget pgm         *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30 c     xml_create    begsr
6.30  *
6.30 c                   eval      splfname2 = *blanks
6.30 c                   eval      splfname3 = *blanks
6.30 c                   eval      jobname2 = *blanks
6.30 c                   eval      jobname3 = *blanks
6.30 c                   eval      username2 = *blanks
6.30 c                   eval      username3 = *blanks
6.30 c                   eval      jobnbr2 = *blanks
6.30 c                   eval      jobnbr3 = *blanks
6.30 c                   eval      splfnbr2 = *zeros
6.30 c                   eval      splfnbr3 = *zeros
6.30 c                   eval      p_btyp = 'BANKRETUR'
6.30 c                   eval      p_dspl = 'Bankretur - kvittering '+
6.30 c                                      %char(w_dato)
6.30 c                   eval      p_refn = %char(w_year)+%char(w_dato)
6.30 c                   eval      p_tagg0 = 'KJOREDATO'
6.30 c                   eval      p_tagg0val = %char(w_dato)
6.30 c                   eval      p_tagg1 = *blanks
6.30 c                   eval      p_tagg1val = *blanks
6.30 c                   eval      p_tagg2 = *blanks
6.30 c                   eval      p_tagg2val = *blanks
6.30 c                   eval      p_tagg3 = *blanks
6.30 c                   eval      p_tagg3val = *blanks
6.30 c                   eval      p_tagg4 = *blanks
6.30 c                   eval      p_tagg4val = *blanks
6.30 c                   eval      p_tagg5 = *blanks
6.30 c                   eval      p_tagg5val = *blanks
6.30 c                   eval      p_tagg6 = *blanks
6.30 c                   eval      p_tagg6val = *blanks
6.30 c                   eval      p_tagg7 = *blanks
6.30 c                   eval      p_tagg7val = *blanks
6.30 c                   eval      p_tagg8 = *blanks
6.30 c                   eval      p_tagg8val = *blanks
6.30 c                   eval      p_tagg9 = *blanks
6.30 c                   eval      p_tagg9val = *blanks
      *
6.30 c                   call      'AP627R'
6.30 c                   PARM                    splfname
6.30 c                   PARM                    jobname
6.30 c                   PARM                    username
6.30 c                   PARM                    jobnbr
6.30 c                   PARM                    splfnbr
6.30 c                   PARM                    splfname2
6.30 c                   PARM                    jobname2
6.30 c                   PARM                    username2
6.30 c                   PARM                    jobnbr2
6.30 c                   PARM                    splfnbr2
6.30 c                   PARM                    splfname3
6.30 c                   PARM                    jobname3
6.30 c                   PARM                    username3
6.30 c                   PARM                    jobnbr3
6.30 c                   PARM                    splfnbr3
6.30 c                   parm                    p_tagg0
6.30 c                   parm                    p_tagg0val
6.30 c                   parm                    p_tagg1
6.30 c                   parm                    p_tagg1val
6.30 c                   parm                    p_tagg2
6.30 c                   parm                    p_tagg2val
6.30 c                   parm                    p_tagg3
6.30 c                   parm                    p_tagg3val
6.30 c                   parm                    p_tagg4
6.30 c                   parm                    p_tagg4val
6.30 c                   parm                    p_tagg5
6.30 c                   parm                    p_tagg5val
6.30 c                   parm                    p_tagg6
6.30 c                   parm                    p_tagg6val
6.30 c                   parm                    p_tagg7
6.30 c                   parm                    p_tagg7val
6.30 c                   parm                    p_tagg8
6.30 c                   parm                    p_tagg8val
6.30 c                   parm                    p_tagg9
6.30 c                   parm                    p_tagg9val
6.30 c                   parm                    l_bach
6.30 c                   parm                    p_btyp
6.30 c                   parm                    p_refn
6.30 c                   parm                    p_dspl
6.30  *
6.30 c                   endsr
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30  * Subrutiner for XML/jasper integrasjon - Skjermvisning           *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30 c     pdf_preview   begsr
6.30 c                   if        l_skje = 1
6.30  * Vi kaller program for launch av PDF i browser
6.30 c                   call      'AP621R'
6.30 c                   parm                    l_bach
6.30  *
6.30 c                   endif
6.30  *
6.30 c                   endsr
6.30  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   eval      ix = *zeros
      *
      * Key for AVTALEREGISTER
      *
     c     avtkey        klist
     c                   kfld                    gcbgir
      *
6.30 c                   open      gl151p
6.30 c                   eval      w_year = *year
6.30 c                   time                    w_dato
      *
      * Skriver ut heading på rapport.
      *
     c                   write     a1hdr
      *
     c                   endsr
      * ===========================================
**   TEKSTER
Motaksretur (1. gang)
Motaksretur (2. gang)
Konto uten avtale
.
