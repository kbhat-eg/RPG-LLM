      /TITLE  ASLAGR Opprette ny lagervare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LL761R
      * Beskrivelse . . : Administrerer lager og lagerbok ved oppretting
      *                   Her for et bestemt lager
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       051201 ASK  Nytt program
      *       260902 HKO  Kaller program for oppdatering av ordre- og
      *                   bestillings-akkumulatorerer i lager
5.70  * PRGR  161208 bof  Utvidet med varetype på lager
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvhislu    uf a e           k disk    rename(vhispfr:vhislur)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvlaglu    uf a e           k disk    rename(vlagpfr:vlaglur)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
5.70 fra10l1    if   e           k disk    rename(ra10pfr:ra10l1r)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vhfirm)
     d p_vare          s                   like(vhvare)
5.70 d p_lage          s                   like(vhlage)
      *
      * Definering av variable i nøkler
      *
     d vhislu_lage     s                   like(vhlage)
     d vhislu_vare     s                   like(vhvare)
     d vhislu_dato     s                   like(vhdato)
     d vhislu_time     s                   like(vhtime)
     d vlagl1_vare     s                   like(vlvare)
5.70 d vlagl1_lage     s                   like(vllage)
     d vlaglu_vare     s                   like(vlvare)
     d vlaglu_lage     s                   like(vllage)
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vlfirm)
      *
5.70 d b_skaf          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
5.70 c                   if        b_skaf = *off
     c                   exsr      lagerbok
5.70 c                   endif
      *
     c                   exsr      slett_lager
      *
     c                   exsr      nytt_lager
      *
      * Oppdaterer ordre-akkumulatorer
      *
     c                   call      'FO761R'
     c                   parm                    p_firm
     c                   parm                    p_vare
      *
      * Oppdaterer bestillings-akkumulatorer
      *
     c                   call      'LA921R'
     c                   parm                    p_firm
     c                   parm                    p_vare
      *
      * Avslutt program
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å opprette post i historisk lagerbok
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Logger oppretting av ny vare i historisk-lagerbok, dette gjøres
      * bare på hovedlager.
      *
     c     lagerbok      begsr
      *
     c                   eval      vhfirm = w_firm
     c                   eval      vhvare = vhislu_vare
     c                   eval      vhlage = vhislu_lage
     c                   eval      vhdato = vhislu_dato
     c                   eval      vhtime = vhislu_time
     c                   eval      vhuser = l_user
     c                   eval      vhsyst = *blank
     c                   eval      vhkode = *blank
     c                   eval      vhtype = 'I'
     c                   eval      vhrefr = 'Opprettet'
     c                   eval      vhbehl = 0
     c                   eval      vhanta = 0
      *
     c                   write     vhislur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å slette alle poster på lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Finner evt. gamle lagerposter på dette varenummer og sletter disse.
      *
     c     slett_lager   begsr
      *
     c     vlagl1_key    setll     vlagl1
     c     vlagl1_key    reade     vlagl1                                 91
      *
     c                   dow       *in91 = *off
     c                   eval      vlaglu_lage = vllage
     c     vlaglu_key    chain     vlaglur                            92
     c                   if        *in92 = *off
     c                   delete    vlaglur
     c                   endif
     c     vlagl1_key    reade     vlagl1                                 91
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å opprette post på hovedlager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Oppretter en tom lagerpost på hovedlager for varen.
      * Henter lagerenhet fra vareregister.
      *
     c     nytt_lager    begsr
      *
     c     vvarl1_key    chain     vvarl1                             93
     c                   if        *in93 = *on
     c                   eval      vvenhl = *blank
     c                   endif
5.70  *
5.70 c     ra10l1_key    setll     ra10l1
5.70 c     ra10l1_key    reade     ra10l1
5.70  *
5.70 c                   dow       not %eof(ra10l1)
      *
5.70 c                   eval      vlaglu_lage = rajkod
     c     vlaglu_key    chain     vlaglur                            94
     c                   if        *in94 = *on
     c                   clear                   vlaglur
     c                   eval      vlfirm = w_firm
     c                   eval      vllage = vlaglu_lage
     c                   eval      vlvare = vlaglu_vare
     c                   eval      vlenhl = vvenhl
5.70 c                   eval      vlvtyp = vvvtyp
     c                   write     vlaglur
     c                   endif
5.70  *
5.70 c     ra10l1_key    reade     ra10l1
5.70 c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
5.70 c                   parm                    p_lage
      *
      * Key for oppdatering av historisk lagerbok
      *
     c     vhislu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhislu_lage
     c                   kfld                    vhislu_vare
     c                   kfld                    vhislu_dato
     c                   kfld                    vhislu_time
      *
      * Key for les på lagerregister
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
5.70 c                   kfld                    vlagl1_lage
      *
      * Key for oppdatering på lagerregister
      *
     c     vlaglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglu_vare
     c                   kfld                    vlaglu_lage
      *
      * Key for oppslag på vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på status-register
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
5.70  *
5.70  * Key for les av alle lager
5.70  *
5.70 c     ra10l1_key    klist
5.70 c                   kfld                    w_firm
      *
      *
      * Legger inn verdier i key-felter
      *
     c                   eval      w_firm = p_firm
      *
     c                   eval      vhislu_vare = p_vare
     c                   eval      vlagl1_vare = p_vare
5.70 c                   eval      vlagl1_lage = p_lage
     c                   eval      vlaglu_vare = p_vare
     c                   eval      vvarl1_vare = p_vare
      *
     c                   time                    vhislu_dato
     c                   time                    vhislu_time
      *
      * Henter hovedlager fra statusregister
      *
     c     fstsl1_key    chain     fstsl1                             90
     c                   eval      vhislu_lage = faahla
5.70  *
5.70  * Fastslår om vare er skaffe eller lagervare
5.70  *
5.70 c     vvarl1_key    chain     vvarl1                             90
5.70 c                   if        vvvtyp = 'S'
5.70 c                   eval      b_skaf = *on
5.70 c                   endif
      *
     c                   endsr
