     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASNAVIOKO
      * Program . . . . : IT115R
      * Beskrivelse . . : Oppdaterer lager fra NAV
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       250214 ask  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra10lu    uf a e           k disk    rename(ra10pfr:ra10lur)
      *
      * Parameter-inn
      *
     d p_firm          s                   like(rajfir)
     d p_comd          s             20
     d p_parm          s           2048
     d p_user          s             10
     d p_stat          s              1
      *
      * Definering av variabler i nøkler
      *
     d ra10lu_jkod     s                   like(rajkod)
      *
      *  Definering av datastrukturer for parameter
      *
     d d_parm          ds          2048
     d  d_jkod                        2
     d  d_besk                       40
     d  d_adr1                       30
     d  d_adr2                       30
     d  d_ponr                        4
     d  d_pste                       30
     d  d_tlfn                       11
     d  d_tfax                       11
     d  d_avde                        4
      *
      * Definering av variable
      *
     d w_firm          s                   like(rajfir)
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_jkod          s                   like(rajkod)
     d w_ponr          s                   like(rajpnr)
     d w_avde          s                   like(rajavd)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram - oppdaterer informasjon på jkod., setter statusflagg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Redigerer parameterdata før behandling
      *
     c                   eval      d_parm = p_parm
     c                   exsr      red_num
     c                   exsr      red_flt
      *
      * Velger oppdatering avhengig av kommando
      *
     c                   select
     c                   when      p_comd = 'INSERT'
     c                   exsr      nyrec
     c                   when      p_comd = 'UPDATE'
     c                   exsr      oppdrec
     c                   when      p_comd = 'DELETE'
     c                   exsr      sletrec
     c                   endsl
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slutt         tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Opprette kodepost
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     nyrec         begsr
      *
     c                   eval      w_firm = p_firm
     c                   eval      ra10lu_jkod = w_jkod
     c     ra10lu_key    chain     ra10lu
     c                   if        not %found
     c                   eval      rajfir = w_firm
     c                   eval      rajkod = w_jkod
     c                   eval      rajtxt = %subst(d_besk:1:20)
     c                   eval      rajadr = d_adr1
     c                   eval      rajad2 = d_adr2
     c                   eval      rajpnr = w_ponr
     c                   eval      rajste = d_pste
     c                   eval      rajtlf = d_tlfn
     c                   eval      rajfax = d_tfax
     c                   eval      rajavd = w_avde
     c                   eval      rajdat = w_date
     c                   eval      rajtim = w_time
     c                   eval      rajusr = p_user
     c                   write     ra10lur
     c                   eval      p_stat = '1'
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatere kodepost
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdrec       begsr
      *
     c                   eval      w_firm = p_firm
     c                   eval      ra10lu_jkod = w_jkod
     c     ra10lu_key    chain     ra10lu
     c                   if        %found
     c                   eval      rajtxt = %subst(d_besk:1:20)
     c                   eval      rajadr = d_adr1
     c                   eval      rajad2 = d_adr2
     c                   eval      rajpnr = w_ponr
     c                   eval      rajste = d_pste
     c                   eval      rajtlf = d_tlfn
     c                   eval      rajfax = d_tfax
     c                   eval      rajavd = w_avde
     c                   eval      rajdat = w_date
     c                   eval      rajtim = w_time
     c                   eval      rajusr = p_user
     c                   update    ra10lur
     c                   eval      p_stat = '1'
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Slett kodepost
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sletrec       begsr
      *
     c                   eval      w_firm = p_firm
     c                   eval      ra10lu_jkod = w_jkod
     c     ra10lu_key    chain     ra10lu
     c                   if        %found
     c                   delete    ra10lur
     c                   eval      p_stat = '1'
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Bytter ut blanke med nuller i numeriske felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     red_num       begsr
      *
     c                   if        d_ponr = *blanks
     c     ' ':'0'       xlate     d_ponr        d_ponr
     c                   endif
     c                   if        d_avde = *blanks
     c     ' ':'0'       xlate     d_avde        d_avde
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Redigerer numeriske felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     red_flt       begsr
      *
      * Kodefelt
     c                   eval      w_jkod = %dec(d_jkod:2:0)
      * Postnummer
     c                   eval      w_ponr = %dec(d_ponr:4:0)
      * Avdeling
     c                   eval      w_avde = %dec(d_avde:4:0)
      *
     c     end_flt       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      p_stat = ' '
     c                   eval      *inlr = *on
     c                   return
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_comd
     c                   parm                    p_parm
     c                   parm                    p_user
     c                   parm                    p_stat
      *
      * Key for posisjonering på valgt kode
      *
     c     ra10lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra10lu_jkod
      *
      * Setter "timestamp" for postoppdateringer
      *
     c                   time                    w_date
     c                   time                    w_time
      *
     c                   endsr
