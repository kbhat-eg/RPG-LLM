     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASPGPL
      * Program . . . . : AA016R
      * Beskrivelse . . : Printerstyring, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       270605 AMD  Skrevet om til ny standard
7.xx  * 7.xx      140618  Endringer reltart til ny GUI. NB Kan kun gjelde BKV
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fawsdl1    if   e           k disk    rename(awsdpfr:awsdl1r)
     fawsdl2    if   e           k disk    rename(awsdpfr:awsdl2r)
     fawsdlr    if   e           k disk    rename(awsdpfr:awsdlrr)
     fawsdlu    uf a e           k disk    rename(awsdpfr:awsdlur)
      *
     faa016d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_sbib                934    943
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d awsdl1_wsid     s                   like(awwsid)
     d awsdl1_ruti     s                   like(awruti)
     d awsdl2_wsid     s                   like(awwsid)
     d awsdl2_ruti     s                   like(awruti)
     d awsdlr_wsid     s                   like(awwsid)
     d awsdlr_ruti     s                   like(awruti)
     d awsdlu_wsid     s                   like(awwsid)
     d awsdlu_ruti     s                   like(awruti)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_sjek          s              1
     d w_outq          s                   like(awoutq)
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d s_wsid          s                   like(awwsid)
     d s_ruti          s                   like(awruti)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2wsid <> *blank or
     c                             b2ruti <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'R'
     c                   eval      awsdl2_ruti = b1ruti
     c                   eval      awsdl2_wsid = *blank
     c     awsdl2_key    setll     awsdl2
     c                   other
     c                   eval      awsdl1_wsid = b1wsid
     c                   eval      awsdl1_ruti = *blank
     c     awsdl1_key    setll     awsdl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Skjerm / bruker
      *
     c                   if        b2wsid <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      awsdl1_wsid = b2wsid
     c     awsdl1_key    setll     awsdl1
     c                   goto      end_pos
     c                   endif
      *
      * Rutine
      *
     c                   if        b2ruti <> *blank
     c                   eval      w_seqe = 'R'
     c                   eval      awsdl2_ruti = b2ruti
     c     awsdl2_key    setll     awsdl2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2wsid = *blank
     c                   eval      b2ruti = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1wsid = b1wsid
     c                   eval      c1ruti = b1ruti
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1wsid = b1wsid
     c                   eval      k1ruti = b1ruti
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1wsid = b1wsid
     c                   eval      d1ruti = b1ruti
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1wsid = b1wsid
     c                   eval      c1ruti = b1ruti
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1wsid = b2wsid
     c                   eval      c1ruti = b2ruti
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1wsid = *blank
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
     c***                if        c1wsid = 0
     c***                eval      *in31  = *on
     c***                goto      c1taga
     c***                endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      awsdlr_wsid = c1wsid
     c                   eval      awsdlr_ruti = c1ruti
     c     awsdlr_key    chain     awsdlr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1wsid = *blank
     c                   eval      c1ruti = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2wsid = *blank
     c                   eval      b2ruti = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      awsdlr_wsid = c1wsid
     c                   eval      awsdlr_ruti = c1ruti
     c     awsdlr_key    chain     awsdlr
      *
     c                   if        %found
     c                   eval      c2text = awtext
     c                   eval      c2outq = awoutq
     c                   eval      c2form = awform
     c                   eval      c2akop = awakop
     c                   eval      c2skin = awskin
     c                   eval      c2skut = awskut
     c                   eval      c2dplx = awdplx
     c                   eval      c2pkva = awpkva
     c                   eval      c2prfi = awprfi
     c                   eval      c2eusr = aweusr
     c***  *dmy          move      awodat        c2odat
     c     *dmy          move      awedat        c2edat
     c                   else
     c                   eval      c2text = *blank
     c                   eval      c2outq = *blank
     c                   eval      c2form = *blank
     c                   eval      c2akop = 0
     c                   eval      c2skin = 0
     c                   eval      c2skut = 0
     c                   eval      c2dplx = *blank
     c                   eval      c2pkva = *blank
     c                   eval      c2prfi = 0
     c                   eval      c2eusr = *blank
     c***                eval      c2odat = 0
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1wsid = k1wsid
     c                   eval      c1ruti = k1ruti
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2text = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
      * Er det en gylding utkø ?
      *
     c                   if        c2outq <> '*JOB'
     c                   eval      w_sjek = *blank
     c                   eval      w_outq = c2outq
     c                   call      'AX020C'
     c                   parm                    w_sjek
     c                   parm                    w_outq
      *
     c                   if        w_sjek = '1'
     c                   eval      *in33  = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   awsdlur
      *
     c                   eval      awsdlu_wsid = c1wsid
     c                   eval      awsdlu_ruti = c1ruti
     c     awsdlu_key    chain     awsdlur
      *
     c                   eval      awtext = c2text
     c                   eval      awoutq = c2outq
     c                   eval      awform = c2form
     c                   eval      awakop = c2akop
     c                   eval      awskin = c2skin
     c                   eval      awskut = c2skut
     c                   eval      awdplx = c2dplx
     c                   eval      awpkva = c2pkva
     c                   eval      awprfi = c2prfi
      *
     c                   time                    awedat
     c                   time                    awetim
     c                   eval      aweusr = l_user
      *
     c                   if        %found
     c                   update    awsdlur
     c                   else
     c                   eval      awwsid = c1wsid
     c                   eval      awruti = c1ruti
     c***                time                    awodat
     c                   write     awsdlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1text = awtext
     c                   eval      b1outq = awoutq
     c                   eval      b1form = awform
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      awsdlr_wsid = d1wsid
     c                   eval      awsdlr_ruti = d1ruti
     c     awsdlr_key    chain     awsdlr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      awsdlu_wsid = d1wsid
     c                   eval      awsdlu_ruti = d1ruti
     c     awsdlu_key    chain     awsdlur
      *
     c                   delete    awsdlur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_wsid = k1wsid
     c                   eval      s_ruti = k1ruti
      *
      * Fyller kopierings-bildet
      *
     c                   eval      awsdlr_wsid = k1wsid
     c                   eval      awsdlr_ruti = k1ruti
     c     awsdlr_key    chain     awsdlr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1wsid = *blank
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      awsdlr_wsid = k1wsid
     c                   eval      awsdlr_ruti = k1ruti
     c     awsdlr_key    chain     awsdlr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1wsid = s_wsid
     c                   eval      c1ruti = s_ruti
     c                   exsr      xc2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'R'
     c                   read      awsdl2
     c                   other
     c                   read      awsdl1
     c                   endsl
      *
     c                   if        %eof
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'R'
     c                   eval      awsdl2_ruti = awruti
     c                   other
     c                   eval      awsdl1_wsid = awwsid
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1wsid = awwsid
     c                   eval      b1ruti = awruti
     c                   eval      b1text = awtext
     c                   eval      b1outq = awoutq
     c                   eval      b1form = awform
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'R'
     c     awsdl2_key    setgt     awsdl2
     c                   readp     awsdl2
     c                   other
     c     awsdl1_key    setgt     awsdl1
     c                   readp     awsdl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'R'
     c                   readp     awsdl2
     c                   other
     c                   readp     awsdl1
     c                   endsl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'R'
     c                   eval      awsdl2_ruti = awruti
     c                   other
     c                   eval      awsdl1_wsid = awwsid
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'R'
     c     awsdl2_key    setll     awsdl2
     c                   other
     c     awsdl1_key    setll     awsdl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på skjerm/bruker
      *
     c     awsdl1_key    klist
     c                   kfld                    awsdl1_wsid
     c                   kfld                    awsdl1_ruti
      *
      * Key for posisjonering på rutine
      *
     c     awsdl2_key    klist
     c                   kfld                    awsdl2_ruti
     c                   kfld                    awsdl2_wsid
      *
      * Key for les på skjerm / bruker
      *
     c     awsdlr_key    klist
     c                   kfld                    awsdlr_wsid
     c                   kfld                    awsdlr_ruti
      *
      * Key for oppdatering av skjerm / bruker
      *
     c     awsdlu_key    klist
     c                   kfld                    awsdlu_wsid
     c                   kfld                    awsdlu_ruti
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      awsdl1_wsid = *blank
     c                   eval      awsdl1_ruti = *blank
     c     awsdl1_key    setll     awsdl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
