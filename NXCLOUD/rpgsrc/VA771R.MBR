     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VA771R
      * Beskrivelse . . : Enhet, Omregning av antall til ny enhet
      *                   Denne har leverandør i parameterlista. Det har ikke VA770R
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       170123 bsa  Nytt program
8.01  *K0000  280923 bsa  Decimal data feil hvis enhet ikke finnes.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjvpkl2    if   e           k disk    rename(jvpkpfr:jvpkl2r)
     fjlevl3    if   e           k disk    rename(jlevpfr:jlevl3r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vefirm)
     d p_levr          s                   like(jvldor)
     d p_vare          s                   like(vevare)
     d p_enh1          s                   like(veenhe)
     d p_enh2          s                   like(veenhe)
     d p_ant1          s             11  3
     d p_ant2          s             11  3
      *
      * Definering av variable i nøkler
      *
     d vvenl1_vare     s                   like(vevare)
     d vvarl1_vare     s                   like(vvvare)
     d jvpkl2_vare     s                   like(jwvare)
     d jvpkl2_ldor     s                   like(jwldor)
     d jvarl1_vare     s                   like(jvvare)
     d jlevl3_enum     s                   like(jvldor)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vefirm)
     d w_omr1          s                   like(veomre)
     d w_omr2          s                   like(veomre)
     d w_dato          s               d   datfmt(*iso)
     d w_ldor          s                   like(jvldor)
      *
     d b_pakn          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Omregning fra enhet 1 til enhet 2
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer ny verdi for antall
      *
     c                   eval      p_ant2 = 0
     c                   eval      w_ldor = 0
      *
      * Leser vare
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   goto      va_vare
     c                   endif
      *
      * Omregner antall til valgt enhet
      *
8.01 c                   eval      w_omr1 = 0
8.01 c                   eval      w_omr2 = 0
      *
     c                   eval      vvenl1_vare = p_vare
     c     vvenl1_key    setll     vvenl1
     c     vvenl1_key    reade     vvenl1
     c                   dow       not %eof
      *
     c                   if        veenhe = p_enh1
     c                   eval      w_omr1 = veomre
     c                   endif
      *
     c                   if        veenhe = p_enh2
     c                   eval      w_omr2 = veomre
     c                   endif
      *
     c     vvenl1_key    reade     vvenl1
     c                   enddo
8.01  *
8.01  * Hvis enhet ikke finnes for en av enheneten, er det feil i data, men
8.01  * vi kan ikke la programmet "tryne". I praksis betyr det av vi sender
8.01  * samme antall videre.
8.01  *
8.01 c                   if        w_omr2 <> 0 and
8.01 c                             w_omr1 = 0
8.01 c                   eval      w_omr1 = 1
8.01 c                   endif
      *
     c                   if        w_omr2 <> 0
     c                   eval(h)   p_ant2 = p_ant1 * w_omr1 / w_omr2
     c                   endif
      *
     c     va_vare       tag
      *
      * Hvis faktor ikke er funnet, sjekk på nytt mot vareadministrasjon
      *
     c                   if        p_ant2 = 0
      *
     c                   if        p_levr <> 0
     c                   eval      jlevl3_enum = p_levr
     c     jlevl3_key    chain     jlevl3
     c                   if        %found
     c                   eval      w_ldor = jlldor
     c                   endif
     c                   endif
      *
     c                   if        w_ldor = 0
     c                   eval      jvarl1_vare = p_vare
     c     jvarl1_key    chain     jvarl1
     c                   if        %found
     c                   eval      w_ldor = jvldor
     c                   endif
     c                   endif
      *
     c                   eval      jvpkl2_vare = p_vare
     c                   eval      jvpkl2_ldor = w_ldor
     c     jvpkl2_key    setll     jvpkl2
     c     jvpkl2_key    reade     jvpkl2
     c                   dow       not %eof
      *
      * sjekker fra / tildato
     c                   exsr      sjekk_pkdato
     c                   if        b_pakn = *on
      *
     c                   if        jwenhe = p_enh1
     c                   eval      w_omr1 = jwofak
     c                   endif
      *
     c                   if        jwenhe = p_enh2
     c                   eval      w_omr2 = jwofak
     c                   endif
      *
     c                   endif
      *
     c     jvpkl2_key    reade     jvpkl2
     c                   enddo
      *
     c                   if        w_omr2 <> 0
     c                   eval(h)   p_ant2 = p_ant1 * w_omr1 / w_omr2
     c                   endif
      *
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke om dato er innenfor fra /til dato
      * hvis *loval i begge datoer = ok
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_pkdato  begsr
      *
     c                   eval      b_pakn = *on
     c                   time                    w_dato
      *
     c                   if        jwfdat = *loval and
     c                             jwtdat = *loval
     c                   leavesr
     c                   endif
      *
     c                   if        jwfdat <= w_dato and
     c                             jwtdat >  w_dato
     c                   leavesr
     c                   endif
      *
     c                   if        jwfdat = *loval  and
     c                             jwtdat >  w_dato
     c                   leavesr
     c                   endif
      *
     c                   if        jwfdat <= w_dato and
     c                             jwtdat =  *loval
     c                   leavesr
     c                   endif
      *
     c                   eval      b_pakn = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_levr
     c                   parm                    p_vare
     c                   parm                    p_enh1
     c                   parm                    p_ant1
     c                   parm                    p_enh2
     c                   parm                    p_ant2
      *
      * Key for oppslag på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på pakningsregister
      *
     c     jvpkl2_key    klist
     c                   kfld                    jvpkl2_vare
     c                   kfld                    jvpkl2_ldor
      *
      * Key for oppslag på pakningsregister
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på leverandørreg VA
      *
     c     jlevl3_key    klist
     c                   kfld                    jlevl3_enum
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
      *
