     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LI800R
      * Beskrivelse . . : EDI, Kvalitetskontroll - parameterprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fli650d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Parameter-ut
      *
     d                 ds
     d d_parm                        15
     d  d_firm                        3  0 overlay(d_parm)
     d  d_ldor                        6  0 overlay(d_parm:4)
     d  d_val1                        1  0 overlay(d_parm:10)
     d  d_val2                        1  0 overlay(d_parm:11)
     d  d_val3                        1  0 overlay(d_parm:12)
      *
      * Definering av variabler i nøkler
      *
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d w_firm          s                   like(rlfirm)
     d w_parm          s             15
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1BLD  - B1 Skjermbilde for parametervalg
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram: Velge leverandør og angi sjekkparametre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Nullstiller/Blanker felter som skal benyttes
      *
     c     start_pgm     tag
      *
     c                   eval      b1ldor = 0
     c                   eval      b1val1 = 1
     c                   eval      b1val2 = 1
     c                   eval      b1val3 = 1
      *
     c     bilde_ut      tag
     c                   exfmt     b1bld
      *
      *  F3=Avslutt program
      *
     c                   if        *inkc = *on
     c                   goto      end_pgm
     c                   endif
      *
      *  F4=Spørring
      *
     c                   if        *inkd = *on
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    b1ldor
     c                   parm                    b1ltek
     c                   goto      bilde_ut
     c                   endif
      *
      *  Er gyldig leverandør angitt
      *
     c                   eval      rlevl1_levr = b1ldor
     c     rlevl1_key    chain     rlevl1
     c                   if        not %found
     c                   eval      *in33 = *on
     c                   goto      bilde_ut
     c                   endif
      *
      *  Oppkall av kontrollrutine
      *
     c                   if        b1val1 = 0 and
     c                             b1val2 = 0 and
     c                             b1val3 = 0
     c                   goto      end_pgm
     c                   endif
      *
     c                   eval      d_firm = w_firm
     c                   eval      d_ldor = b1ldor
     c                   eval      d_val1 = b1val1
     c                   eval      d_val2 = b1val2
     c                   eval      d_val3 = b1val3
     c                   eval      w_parm = d_parm
      *
     c                   call      'LI651C'
     c                   parm                    w_parm
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      *  Legger inn startverdier
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
