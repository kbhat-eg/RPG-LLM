     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON
      * Program . . . . : RK403R
      * Beskrivelse . . : Kunder, sletting av posteringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * V3.30 160202 KOB  Endret logikk for lesing av poster
      * V5.20 160205 HFL  Rettet feil, W_DATO endret.
6.10  * V6.10 220909 BSA  Lagt inn sporingsfelter
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frktrl1    if   e           k disk    rename(rktrpfr:rktrl1r)
     frktrlu    uf a e           k disk    rename(rktrpfr:rktrlur)
      *
     d rkunl1_firm     s                   like(rkfirm)
     d rktrl1_firm     s                   like(rnfirm)
     d rktrl1_kund     s                   like(rnkund)
      *
     d rktrlu_firm     s                   like(rnfirm)
     d rktrlu_kund     s                   like(rnkund)
     d rktrlu_bdat     s                   like(rnbdat)
     d rktrlu_tist     s                   like(rntist)
      *
     d w_belØ          s                   like(rnbelØ)
     d w_rest          s                   like(rnrest)
     d w_firm          s                   like(rnfirm)
      *
      * Definering av LDA
      *
     d                uds
6.10 d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Datastruktur for parameterliste - inn
      *
     d p_list          s             64
      *
     d                 ds
     d d_list                        64
     d  d_raar                        4  0 overlay(d_list)
      *
     d                 ds
E5.20d w_dato                         8  0
E5.20d  w_aar                         4  0 overlay(w_dato)
E5.20d  w_mnd                         2  0 overlay(w_dato:5)
E5.20d  w_dag                         2  0 overlay(w_dato:7)
      *
     d w_tmst          s               z
     d w_tmst_20       s             20
     d w_tell          s              6  0
     d w_tell_alf      s              6
      *
      * Leser alle kunder
      *
     c                   eval      rkunl1_firm = w_firm
      *
     c     rkunl1_key    setll     rkunl1r                                90
     c     rkunl1_key    reade     rkunl1r                                90
      *
     c                   dow       *in90 = *off
      *
     c                   exsr      poster
      *
      * Lag en ny saldopost
      *
     c                   if        w_belØ <> 0 or
     c                             w_rest <> 0
      *
     c                   eval      w_aar = d_raar - 1
     c                   eval      w_mnd = 12
     c                   eval      w_dag = 31
E5.20c                   move      w_dato        rnbdat
      *
     c                   eval      w_tell = 1
      *
     c     rktrlu_tst    tag
      *
      * Teller/Timestamp
      *
     c                   move      w_tell        w_tell_alf
     c                   time                    rntist
     c     *iso0         move      rntist        w_tmst_20
     c                   move      w_tell_alf    w_tmst_20
     c     *iso0         move      w_tmst_20     rntist
      *
     c                   eval      rktrlu_firm = rnfirm
     c                   eval      rktrlu_kund = rnkund
     c                   eval      rktrlu_bdat = rnbdat
     c                   eval      rktrlu_tist = rntist
      *
     c     rktrlu_key    chain     rktrlur                            91
      *
     c                   if        *in91 = *off
     c                   eval      w_tell = w_tell + 1
     c                   goto      rktrlu_tst
     c                   endif
      *
     c                   eval      rnbelØ = w_belØ
     c                   eval      rnrest = w_rest
6.NU c                   eval      rnbiln = 99999999
E5.20c                   eval      rnraar = w_aar
     c                   eval      rnrper = 12
     c                   eval      rnfdat = rnbdat
     c                   eval      rnbilk = 98
     c                   eval      rntext = 'Saldo pr. 31.12.'
     c                   move      w_aar         rntext
     c                   eval      rnjour = *zeros
     c                   eval      rnvalk = *blank
     c                   eval      rnvalb = *zeros
     c                   eval      rnvalr = *zeros
     c                   eval      rnrefn = *zeros
     c                   eval      rnvirk = *blank
     c                   eval      rnavde = *zeros
     c                   eval      rnrekl = *blank
     c                   eval      rnantp = *zeros
     c                   eval      rnrenk = *zeros
     c                   eval      rnsamf = *zeros
     c                   eval      rninko = *blank
     c                   eval      rnslgr = *zeros
     c                   eval      rnidat = *loval
      *
6.10 c                   time                    rnodat
6.10 c                   time                    rnotim
6.10 c                   eval      rnousr = l_user
6.10 c                   time                    rnedat
6.10 c                   time                    rnetim
6.10 c                   eval      rneusr = l_user
     c                   write     rktrlur
     c                   endif
      *
     c                   eval      w_belØ = 0
     c                   eval      w_rest = 0
      *
     c     rkunl1_key    reade     rkunl1r                                90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av poster
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     poster        begsr
      *
      * Leser kundeposter
      *
     c                   eval      rktrl1_firm = w_firm
     c                   eval      rktrl1_kund = rkkund
     c     rktrl1_key    setll     rktrl1r                                90
     c     rktrl1_key    reade     rktrl1r                                90
      *
     c                   dow       *in90 = *off
      *
     c                   if        rnraar < d_raar and
     c                             rnrest = 0
      *
     c                   eval      w_belØ = w_belØ + rnbelØ
     c                   eval      w_rest = w_rest + rnrest
      *
     c                   eval      rktrlu_firm = rnfirm
     c                   eval      rktrlu_kund = rnkund
     c                   eval      rktrlu_bdat = rnbdat
     c                   eval      rktrlu_tist = rntist
     c     rktrlu_key    chain     rktrlur                            91
      *
     c                   delete    rktrlur
     c                   endif
      *
     c     rktrl1_key    reade     rktrl1r                                90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameter
      *
     c     *entry        plist
     c                   parm                    p_list
      *
     c                   eval      d_list = p_list
      *
     c                   eval      w_firm = l_firm
      *
      * Key for kunderegister
      *
     c     rkunl1_key    klist
     c                   kfld                    rkunl1_firm
      *
      * Key for kundeposter
      *
     c     rktrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrl1_kund
      *
      * Key for kundeposter
      *
     c     rktrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrlu_kund
     c                   kfld                    rktrlu_bdat
     c                   kfld                    rktrlu_tist
      *
     c                   endsr
