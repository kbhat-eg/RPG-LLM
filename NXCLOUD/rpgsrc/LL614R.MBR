      /TITLE  Utskrift av ABC-analyse
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Program . . . . : LL614R                                        *
      * Beskrivelse . . : Utskrift av ABC-analyse                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      *                                                                 *
      *                                                                 *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     flwa2l2    if   e           k disk    rename(lwa2pfr:lwa2l2r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
      *
     fll614p    o    e             printer
      *
      /eject
      *
      * Local Data Area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
      *
      *  Parameter-rec
      *
     d                 ds
     d p_prec                       128
     d  p_faar                        4  0 overlay(p_prec)
     d  p_fper                        2  0 overlay(p_prec:5)
     d  p_taar                        4  0 overlay(p_prec:7)
     d  p_tper                        2  0 overlay(p_prec:11)
     d  p_fvar                        8  0 overlay(p_prec:13)
     d  p_tvar                        8  0 overlay(p_prec:21)
     d  p_flag                        2  0 overlay(p_prec:29)
     d  p_tlag                        2  0 overlay(p_prec:31)
     d  p_fogr                        2  0 overlay(p_prec:33)
     d  p_togr                        2  0 overlay(p_prec:35)
     d  p_fhgr                        2  0 overlay(p_prec:37)
     d  p_thgr                        2  0 overlay(p_prec:39)
     d  p_fugr                        3  0 overlay(p_prec:41)
     d  p_tugr                        3  0 overlay(p_prec:44)
     d  p_levn                        6  0 overlay(p_prec:47)
     d  p_sakb                        4    overlay(p_prec:53)
     d  p_sort                        1  0 overlay(p_prec:57)
     d  p_glag                        1  0 overlay(p_prec:58)
      *
     d w_parm          s            128
     d totlve          s             13  2
     d totoms          s             13  2
     d w_lave          s             15  2
     d w_omse          s             15  2
      *
     d w_enhl          s                   like(mwenhl)
     d w_firm          s                   like(mwfirm)
     d w_dato          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_ogrp          s                   like(mwogrp)
     d w_hgrp          s                   like(mwhgrp)
     d w_ugrp          s                   like(mwugrp)
     d w_oogr          s                   like(vgoogr)
     d w_otxt          s                   like(vgotxt)
     d w_tek1          s                   like(vvtek1)
     d w_hogr          s                   like(vghogr)
     d w_hhgr          s                   like(vghhgr)
     d w_htxt          s                   like(vghtxt)
     d w_uogr          s                   like(vguogr)
     d w_uhgr          s                   like(vguhgr)
     d w_uugr          s                   like(vguugr)
     d w_utxt          s                   like(vgutxt)
      *
      /eject
      *
      * Behandling av detalj-records
      *
     c     les           tag
      *
     c                   read      lwa2l2r                                80
     c                   if        *in80 = *on
     c                   goto      slutt
     c                   endif
      *
      * Finner varetekst
      *
     c                   if        p_sort = 1
     c                   movel     mwvare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1                             80
     c                   eval      w_tek1 = *blank
     c                   if        *in80 = *off
     c                   movel     vvtek1        w_tek1
     c                   endif
      *                                                                 .
      * Legger ut enhet i enhetsfeltet                                  .
      *                                                                 .
     c                   move      mwenhl        d1enhl
      *                                                                 .
     c                   endif
      *
      * Finner overgruppetekst
      *
     c                   if        p_sort = 2
     c                             or p_sort = 3
     c                   eval      vogrl1_oogr = mwogrp
     c     vogrl1_key    chain     vogrl1                             80
     c                   eval      w_otxt = *blank
     c                   if        *in80 = *off
     c                   movel     vgotxt        w_otxt
     c                   endif
     c                   endif
      *
      * Finner hovedgruppetekst
      *
     c                   if        p_sort = 3
     c                   eval      vhgrl1_hogr = mwogrp
     c                   eval      vhgrl1_hhgr = mwhgrp
     c     vhgrl1_key    chain     vhgrl1                             80
     c                   eval      w_htxt = *blank
     c                   if        *in80 = *off
     c                   movel     vghtxt        w_htxt
     c                   endif
     c                   endif
      *
      * Finner undergruppetekst
      *
     c                   if        p_sort = 4
     c                   eval      vugrl1_uogr = mwogrp
     c                   eval      vugrl1_uhgr = mwhgrp
     c                   eval      vugrl1_uugr = mwugrp
     c     vugrl1_key    chain     vugrl1                             80
     c                   eval      w_utxt = *blank
     c                   if        *in80 = *off
     c                   movel     vgutxt        w_utxt
     c                   endif
     c                   endif
      *
      * Beregner akkumulatorer og prosenter
      *
     c                   eval      w_omse = mwomst * 100
     c                   eval      d1omst = mwomst
     c                   eval      d1pro1 = *zeros
      *
     c                   if        totoms <> *zero and
     c                             w_omse <> *zero
     c                   select
     c                   when      w_omse / totoms > 99,99
     c                   eval      d1pro1 = 99,99
     c                   when      w_omse / totoms < -99,99
     c                   eval      d1pro1 = -99,99
     c                   other
     c                   eval(h)   d1pro1 = w_omse / totoms
     c                   endsl
     c                   endif
      *
     c                   eval      w_lave = mwlagv * 100
     c                   eval      d1lagv = mwlagv
     c                   eval      d1pro2 = *zeros
      *
     c                   if        totlve <> *zero and
     c                             w_lave <> *zero
     c                   select
     c                   when      w_lave / totlve > 99,99
     c                   eval      d1pro2 = 99,99
     c                   when      w_lave / totlve < -99,99
     c                   eval      d1pro2 = -99,99
     c                   other
     c                   eval(h)   d1pro2 = w_lave / totlve
     c                   endsl
     c                   endif
      *
     c                   eval      d1akk1 = d1akk1 + mwomst
     c                   eval      d1akk2 = d1akk2 + mwlagv
      *
     c                   eval      w_omse = d1akk1 * 100
     c                   eval      d1apr1 = *zeros
      *
     c                   if        totoms <> *zero and
     c                             w_omse <> *zero
     c                   select
     c                   when      w_omse / totoms > 999,99
     c                   eval(h)   d1apr1 = 999,99
     c                   when      w_omse / totoms < -999,99
     c                   eval(h)   d1apr1 = -999,99
     c                   other
     c                   eval(h)   d1apr1 = w_omse / totoms
     c                   endsl
     c                   endif
      *
     c                   eval      w_lave = d1akk2 * 100
     c                   eval      d1apr2 = *zeros
      *
     c                   if        totlve <> *zero and
     c                             w_lave <> *zero
     c                   select
     c                   when      w_lave / totlve > 999,99
     c                   eval      d1apr2 = 999,99
     c                   when      w_lave / totlve < -999,99
     c                   eval      d1apr2 = -999,99
     c                   other
     c                   eval(h)   d1apr2 = w_lave / totlve
     c                   endsl
     c                   endif
      *
     c                   eval      d1tell = d1tell + 1
      *
      * Utvalg på vare
      *
     c                   if        *in71 = *on
     c                   eval      d1vare = mwvare
     c                   eval      d1tek1 = w_tek1
     c                   eval      d1enhl = mwenhl
     c                   endif
      *
      * Utvalg på overgruppe
      *
     c                   if        *in72 = *on
     c                   eval      d1oogr = mwogrp
     c                   eval      d1otxt = w_otxt
     c                   endif
      *
      * Utvalg på hovedgruppe
      *
     c                   if        *in73 = *on
     c                   eval      d1hogr = mwogrp
     c                   eval      d1hhgr = mwhgrp
     c                   eval      d1htxt = w_htxt
     c                   endif
      *
      * Utvalg på undergruppe
      *
     c                   if        *in74 = *on
     c                   eval      d1uogr = mwogrp
     c                   eval      d1uhgr = mwhgrp
     c                   eval      d1uugr = mwugrp
     c                   eval      d1utxt = w_utxt
     c                   endif
      *
      * Skriv heading ved overflow
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
      *
      * Skriv detaljlinje
      *
     c                   write     d1det                                30
      *
     c                   goto      les
      /SPACE 3
      *
      *   Slutt
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    w_parm
     c                   eval      p_prec = w_parm
      *
      * Legge inn firmanummer
      *
     c                   move      l_firm        w_firm
     c                   move      l_firm        mwfirm
      *
      * Riktig selektering
      *
     c                   eval      *in71 = p_sort = 1
     c                   eval      *in72 = p_sort = 2
     c                   eval      *in73 = p_sort = 3
     c                   eval      *in74 = p_sort = 4
      *
     c                   eval      *in91 = p_glag = 1
     c                   eval      *in92 = p_glag = 2
     c                   eval      *in93 = p_glag = 3
      *
     c                   eval      totoms = 0
     c                   eval      totlve = 0
      *
      * Les første record med totaler
      *
     c                   read      lwa2l2r                                80
      *
     c                   eval      totoms = mwomst
     c                   eval      totlve = mwlagv
      *
      * Key for vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Redigere heading
      *
     c                   time                    w_dato
     c                   time                    w_time
      *
     c     *dmy          move      w_dato        a1date
     c     *hms          move      w_time        a1time
     c                   eval      a1user = l_user
     c                   eval      a1wsid = l_wsid
     c                   eval      a1firm = l_firm
     c                   eval      a1fnav = l_fnav
     c                   eval      a1faar = p_faar
     c                   eval      a1fper = p_fper
     c                   eval      a1taar = p_taar
     c                   eval      a1tper = p_tper
      *
      * Skriv heading
      *
     c                   write     a1hdr
      *
     c                   endsr
