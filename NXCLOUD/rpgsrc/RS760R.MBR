      /TITLE  Kontroll av gyldig leverandørnummer
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RS760R                                              *
      * Beskrivelse . . : Kontroll av gyldig leverandørnummer                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * V5.00     020802  Fjernet firma fra parameterliste                    *
6.30  *           280814  Lagt inn ny returkode - hvis lev er sperret    BOF
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *    60-61 = Fileindikatorer chain etc.                           *
      *                                                                 *
      *  BRUK AV RETURKODE                                              *
      *                                                                 *
      *  N = Leverandøren finnes ikke i registeret                      *
      *  S = Leverandør er sperret for bruk                             *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
      *
6.30 d txt             s             30    dim(3) ctdata perrcd(1)
      *
     d p_levr          s                   like(rllevr)
     d p_navn          s                   like(rlnavn)
     d p_retk          s              1
     d w_firm          s                   like(rlfirm)
      *
     d w_levr          s                   like(rllevr)
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d rlevl1_levr     s                   like(rllevr)
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_retk
S500 c***                parm                    p_firm
     c                   parm                    p_levr
     c                   parm                    p_navn
      *
      * Legge inn firmanummer
      *
     c                   eval      w_firm = l_firm
     c                   eval      p_retk = *blank
     c                   eval      p_navn = *blank
      *
      * Sjekke leverandør
      *
     c                   eval      rlevl1_levr = p_levr
     c     rlevl1_key    chain     rlevl1r                            60
      *
     c                   if        *in60 = *on                                           .....
     c                   move      'N'           p_retk                                      .
     c                   move      txt(1)        p_navn                                      .
     c                   goto      slutt                                                     .
     c                   endif                                                           .....
      *
      * Er leverandør sperret?
      *
     c                   if        rlsprk = 'J'                                          .....
     c                   move      'S'           p_retk                                      .
     c                   move      txt(2)        p_navn                                      .
     c                   goto      slutt                                                     .
     c                   endif                                                           .....
6.30  *
6.30  * Er leverandør passiv ?
6.30  *
6.30 c                   if        rlpass = 'J'                                          .....
6.30 c                   move      'P'           p_retk                                      .
6.30 c                   move      txt(3)        p_navn                                      .
6.30 c                   goto      slutt                                                     .
6.30 c                   endif                                                           .....
      *
     c                   move      rlnavn        p_navn
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      *
      * Key for leverandører
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
     c                   endsr
**
LEVERANDØRNUMMER ER UGYLDIG
LEVERANDØREN ER SPERRET
LEVERANDØREN ER PASSIV
