     h option(*nodebugio) decedit('0,') datedit(*dmy.)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FO657R
      * Beskrivelse . . : Utskrift av kjøre-liste - sammendrag
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
6.20  *       211110 bof  Nytt program
6.nu  * R6.30 240614 bof  Gjennomgått mtp. nummerutvidelse
6.30  *       070714 bof  Skriver ikke heading første gang
pdf   * R7.00 160819 BSA  Innført PDF utskrift. Innebærer primært å finne
pdf   *                   generert spool, og kall av AP620R med dette som
pdf   *                   parametre.
9.01  *       140613 bof  Utvidet med noen felter
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     ffohelb    if   e           k disk    rename(fohepfr:fohelbr)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fra17l1    if   e           k disk    rename(ra17pfr:ra17l1r)
9.01 ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
9.01 flwexpf    uf a e           k disk    rename(lexcpfr:lwexpfr)
      *
     ffo658p    o    e             printer
pdf  f                                     usropn
      *
     d                 ds
      *                                                      Parameter-inn
     d ldprec                        64
     d  ldotyp                        2    overlay(ldprec)
     d  ldfdat                       10d   datfmt(*iso)
     d                                     overlay(ldprec:03)
     d  ldtdat                       10d   datfmt(*iso)
     d                                     overlay(ldprec:13)
     d  ldfavd                        4  0 overlay(ldprec:23)
     d  ldtavd                        4  0 overlay(ldprec:27)
     d  ldfkjo                        2  0 overlay(ldprec:31)
     d  ldtkjo                        2  0 overlay(ldprec:33)
     d  ldvari                        1  0 overlay(ldprec:35)
      *
      * - - - - - - - - - - - - - -
      * Henter inn data fra LDA
      * - - - - - - - - - - - - - -
      *
     d                uds
pdf  d l_poff                584    584
pdf  d l_bach                595    635
pdf  d l_skje                637    637  0
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
      * Definering av variable
      *
     d w_time          s              6  0
     d w_date          s                   like(foedat)
     d w_retk          s              1
     d w_ytxt          s             30
     d w_firm          s                   like(fofirm)
     d w_aarr          s              4  0
     d w_kode          s              1
9.01 d w_ex            s           1024
      *
     d vvarl1_vare     s                   like(vvvare)
     d vvenl1_vare     s                   like(vevare)
     d vvenl1_enhe     s                   like(veenhe)
     d fohelb_kjor     s                   like(fokjor)
     d rkunl1_kund     s                   like(fokund)
     d vltyl1_ltyp     s                   like(fdltyp)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d votyl1_otyp     s                   like(vaotyp)
     d ra17l1_qkod     s                   like(raqkod)
9.01 d fkprl1_kund     s                   like(flkund)
9.01 d fkprl1_kpro     s                   like(flkpro)
      *
     d s_kjor          s                   like(fokjor)
      *
     d b_first         s              1    inz(*off)
     d b_list          s              1    inz(*off)
      *
     d p_firm          s                   like(fofirm)
     d p_kjor          s                   like(fokjor)
     d p_lage          s                   like(folage)
     d p_ldat          s                   like(foldat)
9.01 d p_excl          s              1  0
      *
pdf  d p_btyp          s            100
      *
pdf  d splfname2       s             10a
pdf  d jobname2        s             10a
pdf  d username2       s             10a
pdf  d jobnbr2         s              6a
pdf  d splfnbr2        s             10i 0
pdf  d splfname3       s             10a
pdf  d jobname3        s             10a
pdf  d username3       s             10a
pdf  d jobnbr3         s              6a
pdf  d splfnbr3        s             10i 0
pdf  d p_tagg0         s             20
pdf  d p_tagg0val      s             50
pdf  d p_tagg1         s             20
pdf  d p_tagg1val      s             50
pdf  d p_tagg2         s             20
pdf  d p_tagg2val      s             50
pdf  d p_tagg3         s             20
pdf  d p_tagg3val      s             50
pdf  d p_tagg4         s             20
pdf  d p_tagg4val      s             50
pdf  d p_tagg5         s             20
pdf  d p_tagg5val      s             50
pdf  d p_tagg6         s             20
pdf  d p_tagg6val      s             50
pdf  d p_tagg7         s             20
pdf  d p_tagg7val      s             50
pdf  d p_tagg8         s             20
pdf  d p_tagg8val      s             50
pdf  d p_tagg9         s             20
pdf  d p_tagg9val      s             50
pdf  d p_refn          s             50
pdf  d p_dspl          s            100
pdf   *
pdf  d qsprilsp        pr                  extpgm('QSPRILSP')
pdf  d rcvvar                     32767a   options(*varsize)
pdf  d rcvvarlen                     10i 0 const
pdf  d format                         8a   const
pdf  d errorcode                  32767a   options(*varsize)
pdf   *
pdf  d sprl0100        ds
pdf  d  bytesrtn                     10i 0
pdf  d  bytesavl                     10i 0
pdf  d  splfname                     10a
pdf  d  jobname                      10a
pdf  d  username                     10a
pdf  d  jobnbr                        6a
pdf  d  splfnbr                      10i 0
pdf  d  systemname                    8a
pdf  d  createdate                    7a
pdf  d                                1a
pdf  d  createtime                    6a
pdf   *
pdf  d errorcode       ds
pdf  d  bytesprov                    10i 0 inz(0)
pdf  d  byteavail                    10i 0 inz(0)
pdf   *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Styre-rutine                                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c                   eval      s_kjor = 0
     c                   eval      t2brut = 0
     c                   eval      t2volu = 0
      *
     c                   if        p_kjor = 0
     c                   eval      fohelb_kjor = 0
     c                   else
     c                   eval      fohelb_kjor = p_kjor
     c                   endif
     c     fohelb_key    setll     fohelb
     c                   read      fohelb
     c                   dow       not %eof(fohelb)
     c                   if        p_kjor <> 0  and
     c                             fokjor >  p_kjor
     c                   goto      totalt
     c                   endif
     c                   exsr      chk_ordre
     c                   if        b_list = *on
     c                   exsr      behandle
     c                   endif
     c                   read      fohelb
     c                   enddo
     c     totalt        tag
      *
     c                   if        t2brut <> 0 or
     c                             t2volu <> 0
     c                   exsr      total2
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
pdf   * Generer xml for videre utskrift
pdf  c                   if        l_poff = '1'
pdf  c                   exsr      spoolnbr
pdf  c                   exsr      xml_create
pdf  c                   close     fo658p
pdf  c                   exsr      pdf_preview
pdf   * Avslutt jobbiden
pdf  c                   call      'AB710R'
pdf  c                   parm                    l_bach
pdf  c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A1HDR Behandling                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     behandle      begsr
      *
     c                   eval      t1brut = 0
     c                   eval      t1volu = 0
      *
6.30 c                   if        s_kjor <> fokjor or
6.30 c                             b_first = *off
     c                   exsr      hode1
     c                   eval      s_kjor = fokjor
6.30 c                   eval      b_first = *on
9.01 c                   if        p_excl  = 1
9.01 c                   exsr      xls_hode
9.01 c                   endif
     c                   endif
      *
     c                   exsr      hode2
      *
     c                   eval      fodtl1_numm = fonumm
     c                   eval      fodtl1_suff = fosuff
     c     fodtl1_key    setll     fodtl1
     c     fodtl1_key    reade     fodtl1
      *
     c                   dow       not %eof(fodtl1)
      *
     c                   exsr      vare
     c     fodtl1_key    reade     fodtl1
     c                   enddo
      *
     c                   exsr      total
      *
     c                   if        *in30 = *on
     c                   exsr      overflow
     c                   endif
5.55  *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A1HDR Behandle heading A1                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     hode1         begsr
      *
      *
      *  Hent tekst for kjørerute
      *
     c                   eval      a1ktxt = *blank
     c                   move      footyp        a1otyp
      *
     c                   if        fokjor <> *zeros
     c                   eval      w_retk = *blank
     c                   call      'RS725R'
     c                   parm                    w_retk
     c                   parm                    fokjor
     c                   parm                    w_ytxt
     c                   move      fokjor        a1kjor
     c                   move      w_ytxt        a1ktxt
     c                   endif
      *
      * Skrive heading
      *
     c                   write     a1hdr                                30
      *
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A1HDR Behandle heading A1                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     hode2         begsr
9.01  * beregner ordre total
9.01 c                   eval      t1tota = fotots - fototr
      *
      * Snu dato
      *
     c                   if        foldat <> *loval
     c     *dmy          move      foldat        a1ldat
     c                   endif
      *
      * Legg inn ordrenummer
      *
     c                   move      fonumm        a1numm
     c                   movel     '/'           a1suff
     c                   move      fosuff        a1suff
      *
      * Hente inn kunderegister
      *
     c                   move      fokund        rkunl1_kund
     c     rkunl1_key    chain     rkunl1r                            64
     c                   move      fokund        a1kund
9.01 c                   move      rknavn        a1kuna
9.01 c                   move      fonavn        a1navn
9.01  *
9.01 c                   eval      a1kpro = *blank
9.01  *
9.01  * finner prosjektnavn
9.01 c                   eval      fkprl1_kund = fokund
9.01 c                   eval      fkprl1_kpro = fokpro
9.01 c     fkprl1_key    chain     fkprl1r
9.01 c                   if        %found
9.01 c                   eval      a1kpro = %char(fokpro) + ' ' +
9.01 c                                      %trim(flnavn)
9.01 c                   endif
      *
      *  Hent tekst for kjørerute
      *
     c                   eval      a1ktxt = *blank
     c                   move      footyp        a1otyp
      *
     c                   if        fokjor <> *zeros
     c                   eval      w_retk = *blank
     c                   call      'RS725R'
     c                   parm                    w_retk
     c                   parm                    fokjor
     c                   parm                    w_ytxt
     c                   move      fokjor        a1kjor
     c                   move      w_ytxt        a1ktxt
     c                   endif
      *
      * Skrive heading
      *
     c                   write     a1hdr1                               30
9.01 c                   write     a1hdr2                               30
      *
9.01 c                   if        *in30 = *on
9.01 c                   exsr      overflow
9.01 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * D1DET Behandle detail D1                                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     vare          begsr
      *
      * Nullstilling av variabler
      *
     c                   eval      d1vare = *blank
     c                   eval      d1tek1 = *blank
     c                   eval      d1enh1 = *blank
     c                   eval      d1ant0 = *zero
     c                   eval      d1ant1 = *zero
     c                   eval      d1ant2 = *zero
     c                   eval      d1ant3 = *zero
      *
      * Hent linje-type
      *
     c                   move      fdltyp        vltyl1_ltyp
     c     vltyl1_key    chain     vltyl1r                            68
      *
      * Sjekker om tekstlinjer
      *
     c                   if        valktx = 1
     c                   leavesr
     c                   endif
      *
      * Oppslag mot vareregister
      *
     c                   eval      vvarl1_vare = fdvare
     c     vvarl1_key    chain     vvarl1r
      *
      * Test på antall desimaler
      *
     c                   eval      *in40 = vvdesi = 0
     c                   eval      *in41 = vvdesi = 1
     c                   eval      *in42 = vvdesi = 2
     c                   eval      *in43 = vvdesi = 3
      *
     c                   if        *in40 = *on
     c                   eval      d1ant0 = fdanta
     c                   endif
     c                   if        *in41 = *on
     c                   eval      d1ant1 = fdanta
     c                   endif
     c                   if        *in42 = *on
     c                   eval      d1ant2 = fdanta
     c                   endif
     c                   if        *in43 = *on
     c                   eval      d1ant3 = fdanta
     c                   endif
      *
      * Varelinje
      *
     c                   move      fdvare        d1vare
     c                   movel     fdtek1        d1tek1
     c                   move      fdenh1        d1enh1
      *
      * Henter volum og vektinformasjon fra enhet
      *
     c                   eval      vvenl1_vare = fdvare
     c                   eval      vvenl1_enhe = fdenh1
     c     vvenl1_key    chain     vvenl1
     c                   if        %found
     c                   eval      d1leng = veleng
     c                   eval      d1bred = vebred
     c                   eval      d1hoyd = vehoyd
     c                   eval(h)   d1brut = vevekt * fdanta
     c                   eval(h)   d1volu = vevolu * fdanta
     c                   else
     c                   eval      d1hoyd = 0
     c                   eval      d1bred = 0
     c                   eval      d1leng = 0
     c                   eval(h)   d1brut = 0
     c                   eval(h)   d1volu = 0
     c                   endif
      *
      * Summer til total
      *
     c                   eval      t1brut = t1brut + d1brut
     c                   eval      t1volu = t1volu + d1volu
      *
     c                   eval      t2brut = t2brut + d1brut
     c                   eval      t2volu = t2volu + d1volu
      *
      * Skriv varelinjer
      *
     c                   write     d1det                                30
      *
     c                   if        *in30 = *on
     c                   exsr      overflow
     c                   endif
      *
9.01 c                   if        p_excl = 1
9.01 c                   exsr      skriv_list
9.01 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Behandle brudd på ordre                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     total         begsr
      *
      * Skrive sum kjørerute
      *
     c                   write     t1tot                                30
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Behandle brudd på totalen                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     total2        begsr
      *
      * Skrive sum kjørerute
      *
     c                   write     t2tot                                30
      *
     c                   if        *in30 = *on
     c                   exsr      overflow
     c                   endif
      *
     c                   endsr
     c                                                                                   .....
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  OVFLOW    Behandling av overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overflow      begsr
      *
      *  Skriver heading
      *
     c                   write     a1hdr                                30
     c                   write     a1hdr1                               31
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke om rad skal være med i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     chk_ordre     begsr
      *
     c                   eval      b_list = *off
      *
      * Viser ikke ordre dersom det ikke kjøres med kjørekontorløsning
      *
     c                   if        faakjk = 0
     c                   leavesr
     c                   endif
      *
      * Sjekker om lager r riktig
      *
     c                   if        p_lage <> 0
     c                   if        folage <> p_lage
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Sjekker om lev.dato er riktig
      *
     c                   if        p_ldat <> *loval
     c                   if        foldat <> p_ldat
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Viser kun ordre med leveringstype med kode for beh. i kjørekontor
      *
     c                   eval      ra17l1_qkod = folmat
     c     ra17l1_key    chain     ra17l1
     c                   if        not %found or
     c                             raqkjo <> 1
     c                   leavesr
     c                   endif
      *
      * Viser kun ordre med behandlingskode 1
      *
     c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1
     c                   if        not %found or
     c                             (vaoakk <> 1 and
     c                             vaoakk <> 2)
     c                   leavesr
     c                   endif
      *
5.54  *
5.54  * Skal bare vise ordre som er
      * - klar for plukking 2
      * - plukkliste kjørt  3
      * - under plukking    4
      * - ankommet lager    5
      * - ferdig plukket    6
      * - ferdig plukket    7
      *
5.54  * Henter inn siste kode  og sjekker om
5.54  * koden er som over
5.54  *
5.54 c                   eval      w_kode = *blank
5.60 c                   eval      w_aarr = %subdt(fobdat:*years)
5.54 c                   call      'FU706R'
5.60 c                   parm                    w_aarr
5.54 c                   parm                    fonumm
5.54 c                   parm                    fosuff
5.54 c                   parm                    w_kode
5.54  *
5.54 c                   if        w_kode = '2' or
5.55 c                             w_kode = '3' or
5.54 c                             w_kode = '4' or
5.54 c                             w_kode = '5' or
5.54 c                             w_kode = '6' or
5.54 c                             w_kode = '7'
5.54 c                   else
5.54 c                   leavesr
5.54 c                   endif
5.55  *
5.54  *
     c                   eval      b_list = *on
      *
     c                   endsr
      *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Finner spool som er generert av jobben
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     spoolnbr      begsr
pdf   /Free
pdf
pdf            QSPRILSP( SPRL0100
pdf                    : %size(SPRL0100)
pdf                    : 'SPRL0100'
pdf                    : errorcode );
pdf   /End-free
pdf  c*
pdf   *
pdf  c                   endsr
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Kaller eget program for generering av xml fil. Eget pgm         *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     xml_create    begsr
pdf   *
pdf  c                   eval      splfname2 = *blanks
pdf  c                   eval      splfname3 = *blanks
pdf  c                   eval      jobname2 = *blanks
pdf  c                   eval      jobname3 = *blanks
pdf  c                   eval      username2 = *blanks
pdf  c                   eval      username3 = *blanks
pdf  c                   eval      jobnbr2 = *blanks
pdf  c                   eval      jobnbr3 = *blanks
pdf  c                   eval      splfnbr2 = *zeros
pdf  c                   eval      splfnbr3 = *zeros
pdf  c                   eval      p_btyp = 'KJORELISTE_SAMMENDRAG_TOTAL'
pdf  c                   eval      p_dspl = *blanks
pdf  c                   eval      p_refn = 'KJORELISTE ' + %char(a1date)
pdf  c                   eval      p_tagg0 = *blanks
pdf  c                   eval      p_tagg0val = *blanks
pdf  c                   eval      p_tagg1 = *blanks
pdf  c                   eval      p_tagg1val = *blanks
pdf  c                   eval      p_tagg2 = *blanks
pdf  c                   eval      p_tagg2val = *blanks
pdf  c                   eval      p_tagg3 = *blanks
pdf  c                   eval      p_tagg3val = *blanks
pdf  c                   eval      p_tagg4 = *blanks
pdf  c                   eval      p_tagg4val = *blanks
pdf  c                   eval      p_tagg5 = *blanks
pdf  c                   eval      p_tagg5val = *blanks
pdf  c                   eval      p_tagg6 = *blanks
pdf  c                   eval      p_tagg6val = *blanks
pdf  c                   eval      p_tagg7 = *blanks
pdf  c                   eval      p_tagg7val = *blanks
pdf  c                   eval      p_tagg8 = *blanks
pdf  c                   eval      p_tagg8val = *blanks
pdf  c                   eval      p_tagg9 = *blanks
pdf  c                   eval      p_tagg9val = *blanks
pdf   *
pdf  c                   call      'AP627R'
pdf  c                   parm                    splfname
pdf  c                   parm                    jobname
pdf  c                   parm                    username
pdf  c                   parm                    jobnbr
pdf  c                   parm                    splfnbr
pdf  c                   parm                    splfname2
pdf  c                   parm                    jobname2
pdf  c                   parm                    username2
pdf  c                   parm                    jobnbr2
pdf  c                   parm                    splfnbr2
pdf  c                   parm                    splfname3
pdf  c                   parm                    jobname3
pdf  c                   parm                    username3
pdf  c                   parm                    jobnbr3
pdf  c                   parm                    splfnbr3
pdf  c                   parm                    p_tagg0
pdf  c                   parm                    p_tagg0val
pdf  c                   parm                    p_tagg1
pdf  c                   parm                    p_tagg1val
pdf  c                   parm                    p_tagg2
pdf  c                   parm                    p_tagg2val
pdf  c                   parm                    p_tagg3
pdf  c                   parm                    p_tagg3val
pdf  c                   parm                    p_tagg4
pdf  c                   parm                    p_tagg4val
pdf  c                   parm                    p_tagg5
pdf  c                   parm                    p_tagg5val
pdf  c                   parm                    p_tagg6
pdf  c                   parm                    p_tagg6val
pdf  c                   parm                    p_tagg7
pdf  c                   parm                    p_tagg7val
pdf  c                   parm                    p_tagg8
pdf  c                   parm                    p_tagg8val
pdf  c                   parm                    p_tagg9
pdf  c                   parm                    p_tagg9val
pdf  c                   parm                    l_bach
pdf  c                   PARM                    p_btyp
pdf  c                   PARM                    p_refn
pdf  c                   PARM                    p_dspl
pdf   *
pdf  c                   endsr
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Subrutiner for XML/jasper integrasjon - Skjermvisning           *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     pdf_preview   begsr
pdf  c                   if        l_skje = 1
pdf   * Vi kaller program for launch av PDF i browser
pdf  c                   call      'AP621R'
pdf  c                   parm                    l_bach
pdf   *
pdf  c                   endif
pdf   *
pdf  c                   endsr
pdf   *
9.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
9.01  * Subrutine for å skrive xls- linje
9.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
9.01  *
9.01 c     skriv_list    begsr
9.01  *
9.01 c                   eval      w_ex = *blank
9.01 c                   eval      w_ex = %char(a1numm) + X'05' +
9.01 c                                    %trim(%subst(a1suff:2:2)) +
9.01 c                                                    X'05' +
9.01 c                                    %char(a1ldat) + X'05' +
9.01 c                                    %trim(a1kuna) + X'05' +
9.01 c                                    %trim(a1navn) + X'05' +
9.01 c                                    %trim(fovref) + X'05' +
9.01 c                                    %char(fokjor) + X'05' +
9.01 c                                    %char(fokjnr) + X'05'
9.01 c     'Æ':X'46'     xlate     w_ex          w_ex
9.01 c     'æ':X'A0'     xlate     w_ex          w_ex
9.01 c     'Ø':X'77'     xlate     w_ex          w_ex
9.01 c     'ø':X'90'     xlate     w_ex          w_ex
9.01 c     'Å':X'2A'     xlate     w_ex          w_ex
9.01 c     'å':X'EF'     xlate     w_ex          w_ex
9.01 c                   eval      exclin   = w_ex
9.01 c                   write     lwexpfr
9.01  *
9.01 c                   endsr
9.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
9.01  * Subrutine for å skrive xls - heading
9.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
9.01  *
9.01 c     xls_hode      begsr
9.01  *
9.01 c                   eval      w_ex = *blank
9.01 c                   eval      w_ex = 'Ordrenr'     + X'05' +
9.01 c                                    'Suffix'      + X'05' +
9.01 c                                    'Lev.sdato' + X'05' +
9.01 c                                    'Kundenavn'   + X'05' +
9.01 c                                    'Lev.adresse' + X'05' +
9.01 c                                    'Vår ref.'    + X'05' +
9.01 c                                    'Kjørerute'   + X'05' +
9.01 c                                    'Kjørenr'     + X'05'
9.01 c     'Æ':X'46'     xlate     w_ex          w_ex
9.01 c     'æ':X'A0'     xlate     w_ex          w_ex
9.01 c     'Ø':X'77'     xlate     w_ex          w_ex
9.01 c     'ø':X'90'     xlate     w_ex          w_ex
9.01 c     'Å':X'2A'     xlate     w_ex          w_ex
9.01 c     'å':X'EF'     xlate     w_ex          w_ex
9.01 c                   eval      exclin   = w_ex
9.01 c                   write     lwexpfr
9.01  *
9.01 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_lage
     c                   parm                    p_kjor
     c                   parm                    p_ldat
9.01 c                   parm                    p_excl
      *
      * Key les av ordre detaljer
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
      *
      * Key for kunderegister
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for firmaregister
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for ordretyper
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for linjetyper
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
      * Key for statusregister
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for Heading-file
      *
     c     fohelb_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelb_kjor
      *
      * Key for oppslag på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
      *
      * Key for leveringsmåte
      *
     c     ra17l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra17l1_qkod
9.01  *
9.01  * Key for oppslag på kundeprosjekt
9.01  *
9.01 c     fkprl1_key    klist
9.01 c                   kfld                    w_firm
9.01 c                   kfld                    fkprl1_kund
9.01 c                   kfld                    fkprl1_kpro
      *
      *
      * Legger inn arb.stasjon og bruker i faste felter
      *
     c                   move      l_user        a1user
     c                   move      l_wsid        a1wsid
     c                   time                    w_date
     c     *dmy          move      w_date        a1date
     c                   time                    w_time
     c                   move      w_time        a1time
pdf  c                   open      fo658p
      *
      * Legger inn firma i nøkkel
      *
     c                   eval      w_firm  = p_firm
     c                   eval      a1firm  = p_firm
      *
      * Hente status
      *
     c     fstsl1_key    chain     fstsl1r
      *
      *
      * Klargjøring dersom firma -
      * opplysninger skal printes ut
      *
     c     afirl1_key    chain     afirl1r                            66
     c                   move      aafnav        a1fnav
      *
     c                   endsr
