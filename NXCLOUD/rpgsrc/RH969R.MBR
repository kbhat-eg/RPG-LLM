     h datedit(*dmy.) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RH969R                                              *
      * Beskrivelse . . : Hovedbok - Journaler med differanse                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * V5.40     230805  Lagt inn parameter for utskrift av all journ.  KOB  *
6.30  *  6.30     180814  Innført mulighet for arkivering og PDF         Bsa  *
6.31  * R6.30 180816 bsa  Rokkert på rekkefølge av close printerfile
6.32  * R6.30 310119 BKV  Økt størrelse på sum felt
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frhwapf    if   e           k disk
     fraa1l1    if   e           k disk    rename(raa1pfr:raa1l1r)
     frh969p    o    e             printer oflind(*in30)
6.30 f                                     usropn
      *
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      * LOCAL DATA
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      *
      *  Parameter i LDA
      *
     d                uds
     d rhpkda                300    305  0
     d rhprÅr                         4  0
     d rhpper                         2  0
     d rhppef                         2  0
     d rhppet                         2  0
     d rhpavf                         4  0
     d rhpavt                         4  0
     d rhpjof                         5  0
     d rhpjot                         5  0
     d rhpsrt                         1
     d rhpsml                         1
     d rhpavv                         1
     d rhpkgs                         1
     d rhpkks                         1
     d rhpsps                         1
     d rhpktu                         1
      *
6.30 d l_poff                584    584
6.30 d l_utqm                585    594
6.30 d l_bach                595    635
6.30 d l_arch                636    636  0
6.30 d l_skje                637    637  0
6.30 d l_land                638    638  0
6.30 d l_exlp                639    639  0
6.30 d l_mail                640    640  0
6.30 d l_ruti                800    809
6.30 d l_outq                810    819
      *
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d w_firm          s                   like(rbfirm)
     d w_jour          s                   like(rbjour)
     d w_first         s              1
      *
      * Definering av variable i nøkler
      *
     d rhwapf_kkey     s                   like(rwkkey)
      *
6.30 d p_btyp          s            100
6.30 d w_year          s              4  0
6.30 d w_dato          s               d   datfmt(*iso)
      *
6.30 d splfname2       s             10a
6.30 d jobname2        s             10a
6.30 d username2       s             10a
6.30 d jobnbr2         s              6a
6.30 d splfnbr2        s             10i 0
6.30 d splfname3       s             10a
6.30 d jobname3        s             10a
6.30 d username3       s             10a
6.30 d jobnbr3         s              6a
6.30 d splfnbr3        s             10i 0
6.30 d p_tagg0         s             20
6.30 d p_tagg0val      s             50
6.30 d p_tagg1         s             20
6.30 d p_tagg1val      s             50
6.30 d p_tagg2         s             20
6.30 d p_tagg2val      s             50
6.30 d p_tagg3         s             20
6.30 d p_tagg3val      s             50
6.30 d p_tagg4         s             20
6.30 d p_tagg4val      s             50
6.30 d p_tagg5         s             20
6.30 d p_tagg5val      s             50
6.30 d p_tagg6         s             20
6.30 d p_tagg6val      s             50
6.30 d p_tagg7         s             20
6.30 d p_tagg7val      s             50
6.30 d p_tagg8         s             20
6.30 d p_tagg8val      s             50
6.30 d p_tagg9         s             20
6.30 d p_tagg9val      s             50
6.30 d p_refn          s             50
6.30 d p_dspl          s            100
      *
6.30 d qsprilsp        pr                  extpgm('QSPRILSP')
6.30 d rcvvar                     32767a   options(*varsize)
6.30 d rcvvarlen                     10i 0 const
6.30 d format                         8a   const
6.30 d errorcode                  32767a   options(*varsize)
      *
6.30 d sprl0100        ds
6.30 d  bytesrtn                     10i 0
6.30 d  bytesavl                     10i 0
6.30 d  splfname                     10a
6.30 d  jobname                      10a
6.30 d  username                     10a
6.30 d  jobnbr                        6a
6.30 d  splfnbr                      10i 0
6.30 d  systemname                    8a
6.30 d  createdate                    7a
6.30 d                                1a
6.30 d  createtime                    6a
      *
6.30 d errorcode       ds
6.30 d  bytesprov                    10i 0 inz(0)
6.30 d  byteavail                    10i 0 inz(0)
      *
      * Les transaksjoner
      *
     c     les           tag
      *
     c     rhwapf_key    setll     rhwapf
     c                   read      rhwapf
      *
     c                   dow       *in15 = *off
      *
     c                   if        %eof
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        *in30 = *on
     c                   write     head1
     c                   eval      *in30 = *off
     c                   endif
      *
     c                   if        rbjour <> w_jour
      *
      * Skriv ut alle eller bare journaler med differanse
      *
     c                   if        w_first <> ' '
      *
     c                   if        rhpsrt = ' '
      *
     c                   if        l1diff <> 0
     c                   write     linje
     c                   endif
      *
     c                   else
      *
     c                   write     linje
     c                   endif
      *
     c                   endif
      *
     c                   eval      w_first= 'X'
     c                   eval      w_jour = rbjour
      *
     c                   eval      l1jour = rbjour
     c                   eval      l1rper = rbrper
     c                   eval      l1raar = rbraar
      *
     c                   eval      l1debe = 0
     c                   eval      l1kred = 0
     c                   eval      l1diff = 0
      *
     c                   endif
      *
      * Summer
      *
     c                   if        rbneto < 0
     c                   eval      l1kred = l1kred - rbneto
     c                   else
     c                   eval      l1debe = l1debe + rbneto
     c                   endif
      *
     c                   eval      l1diff = l1diff + rbneto
      *
     c                   read      rhwapf                               15
      *
     c                   enddo
      *
     c     slutt         tag
      *
     c***                if        rhpsrt = ' ' and
     c                   if        l1diff <> 0
     c                   write     linje
     c                   endif
      *
6.30 c                   if        l_poff = '1'
6.30 c                   exsr      spoolnbr
6.31 c                   close     rh969p
6.31 c                   exsr      xml_create
6.30 c                   exsr      pdf_preview
6.30  * Avslutt jobbiden
6.30 c                   call      'AB710R'
6.30 c                   parm                    l_bach
6.30 c                   endif
      *
     c                   eval      *inlr = *on
      *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30  * Finner spool som er generert av jobben
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30 c     spoolnbr      begsr
6.30  /Free
6.30
6.30           QSPRILSP( SPRL0100
6.30                   : %size(SPRL0100)
6.30                   : 'SPRL0100'
6.30                   : errorcode );
6.30  /End-free
6.30  *
6.30 c                   endsr
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30  * Kaller eget program for generering av xml fil. Eget pgm         *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30 c     xml_create    begsr
6.30  *
6.30 c                   eval      splfname2 = *blanks
6.30 c                   eval      splfname3 = *blanks
6.30 c                   eval      jobname2 = *blanks
6.30 c                   eval      jobname3 = *blanks
6.30 c                   eval      username2 = *blanks
6.30 c                   eval      username3 = *blanks
6.30 c                   eval      jobnbr2 = *blanks
6.30 c                   eval      jobnbr3 = *blanks
6.30 c                   eval      splfnbr2 = *zeros
6.30 c                   eval      splfnbr3 = *zeros
6.30 c                   eval      p_btyp = 'JOURNALLISTEDIFF'
6.30 c                   eval      p_dspl = 'Journalliste med differanse ' +
6.30 c                                      %char(w_dato)
6.30 c                   eval      p_refn = %char(w_year)+%char(w_dato)
6.30 c                   eval      p_tagg0 = 'KJOREDATO'
6.30 c                   eval      p_tagg0val = %char(w_dato)
6.30 c                   eval      p_tagg1 = *blanks
6.30 c                   eval      p_tagg1val = *blanks
6.30 c                   eval      p_tagg2 = *blanks
6.30 c                   eval      p_tagg2val = *blanks
6.30 c                   eval      p_tagg3 = *blanks
6.30 c                   eval      p_tagg3val = *blanks
6.30 c                   eval      p_tagg4 = *blanks
6.30 c                   eval      p_tagg4val = *blanks
6.30 c                   eval      p_tagg5 = *blanks
6.30 c                   eval      p_tagg5val = *blanks
6.30 c                   eval      p_tagg6 = *blanks
6.30 c                   eval      p_tagg6val = *blanks
6.30 c                   eval      p_tagg7 = *blanks
6.30 c                   eval      p_tagg7val = *blanks
6.30 c                   eval      p_tagg8 = *blanks
6.30 c                   eval      p_tagg8val = *blanks
6.30 c                   eval      p_tagg9 = *blanks
6.30 c                   eval      p_tagg9val = *blanks
      *
6.30 c                   call      'AP627R'
6.30 c                   PARM                    splfname
6.30 c                   PARM                    jobname
6.30 c                   PARM                    username
6.30 c                   PARM                    jobnbr
6.30 c                   PARM                    splfnbr
6.30 c                   PARM                    splfname2
6.30 c                   PARM                    jobname2
6.30 c                   PARM                    username2
6.30 c                   PARM                    jobnbr2
6.30 c                   PARM                    splfnbr2
6.30 c                   PARM                    splfname3
6.30 c                   PARM                    jobname3
6.30 c                   PARM                    username3
6.30 c                   PARM                    jobnbr3
6.30 c                   PARM                    splfnbr3
6.30 c                   parm                    p_tagg0
6.30 c                   parm                    p_tagg0val
6.30 c                   parm                    p_tagg1
6.30 c                   parm                    p_tagg1val
6.30 c                   parm                    p_tagg2
6.30 c                   parm                    p_tagg2val
6.30 c                   parm                    p_tagg3
6.30 c                   parm                    p_tagg3val
6.30 c                   parm                    p_tagg4
6.30 c                   parm                    p_tagg4val
6.30 c                   parm                    p_tagg5
6.30 c                   parm                    p_tagg5val
6.30 c                   parm                    p_tagg6
6.30 c                   parm                    p_tagg6val
6.30 c                   parm                    p_tagg7
6.30 c                   parm                    p_tagg7val
6.30 c                   parm                    p_tagg8
6.30 c                   parm                    p_tagg8val
6.30 c                   parm                    p_tagg9
6.30 c                   parm                    p_tagg9val
6.30 c                   parm                    l_bach
6.30 c                   parm                    p_btyp
6.30 c                   parm                    p_refn
6.30 c                   parm                    p_dspl
6.30  *
6.30 c                   endsr
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30  * Subrutiner for XML/jasper integrasjon - Skjermvisning           *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30 c     pdf_preview   begsr
6.30 c                   if        l_skje = 1
6.30  * Vi kaller program for launch av PDF i browser
6.30 c                   call      'AP621R'
6.30 c                   parm                    l_bach
6.30  *
6.30 c                   endif
6.30  *
6.30 c                   endsr
6.30  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Key for statusopplysninger - koderegister
      *
     c     raa1l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for arbeidsregister
      *
     c     rhwapf_key    klist
     c                   kfld                    rhwapf_kkey
      *
     c                   move      *zeros        rhwapf_kkey
     c                   move      l_firm        w_firm
     c                   eval      *in30 = *on
      *
6.30 c                   open      rh969p
6.30 c                   eval      w_year = *year
6.30 c                   time                    w_dato
      *
     c     raa1l1_key    chain     raa1l1                             91
      *
     c                   endsr
