     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VE110R
      * Beskrivelse . . : Vare-enhet, vedlikehold for vare
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       190599 HKO  Nytt program
      *       150903 HKO  Lagt inn valg for bytting av enhet
      *       010805 HKO  Fjernet linjenr og erstattet dette med
      *                   individuell rekkefølge for salgsenhet,
      *                   innkjøpsenhet og prisbokenhet
      *       050805 HKO  Lagt inn vedlikehold av lengde, bredde, høyde,
      *                   volum og vekt
      *       160905 HKO  Lagt inn test på at enhet uten pris ikke kan
      *                   velges som salgsenhet for lager og skaffevarer
5.70  * PRGR  041108 BOF  Utivdet med prisgruppe
5.70  * prgr  121208 bof  Utvidet med varetype lager
      * 6.10  260609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  100909 bof  Utvidet med pakningsklasse
6.12  * 6.10  011009 bof  Lagt inn valg på info pr. prisgiver
6.21  * 6.20  200511 bof  Utvidet vl710r med utg.dato og lev.nr
6.30  * 6.30  170918 bkv  Utvidet med trelast sortiment
7.xx  * 7.00  020216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fvvenlr    if   e           k disk    rename(vvenpfr:vvenlrr)
     fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
     fvvenlu    uf   e           k disk    rename(vvenpfr:vvenlur)
6.30 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
     fve110d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vvfirm)
     d p_vare          s                   like(vvvare)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
5.70 d l_lage               1001   1002  0
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variable i nøkler
      *
     d vvenlr_enhe     s                   like(veenhe)
6.20 d vvprl1_ldor     s                   like(vvldor)
5.70 d vvprl1_prgr     s                   like(vpprgr)
     d vvprl1_enhe     s                   like(vpenhe)
     d vvenlu_enhe     s                   like(veenhe)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_srrn_w        s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d b_feil          s              1    inz(*off)
     d b_subf          s              1    inz(*off)
     d b_enhe_salg     s              1    inz(*off)
6.20 d b_inlr          s              1    inz(*off)
      *
     d w_firm          s                   like(vvfirm)
     d w_vare          s                   like(vvvare)
      *
8.01 d w_prgr          s              2
5.70 d w_lage          s              2  0
6.21 d w_vtyp          s                   like(vvvtyp)
6.21 d w_ldor          s                   like(vpldor)
6.21 d w_udat          s                   like(vvudat)
6.30 d w_lv_nobb       s                   like(vvlnob)
6.30 d w_lv_modn       s                   like(vvlmod)
6.30 d w_lv_lldo       s                   like(vvlnle)
6.30 d w_lv_llva       s                   like(vvllva)
      *
      * Konstanter
      *
     d c_sfil          s              2  0 inz(10)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierering av skjermbildet
      *
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      b2vare = vvvare
     c                   eval      b2tek1 = vvtek1
     c                   eval      b2enhe = vvenhe
      *
      * Henter vare-enhet informasjon
      *
     c     vvenl1_key    setll     vvenl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * F13=Endre statestikkenhet
      *
     c                   if        *inkm = *on
     c                   call      'VV107R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   goto      b2tagb
     c                   endif
      *
      * F15=Endre lagerenhet
      *
     c                   if        *inkp = *on
     c                   call      'VV108R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   goto      b2tagb
     c                   endif
      *
      * F17=Bytt enhet
      *
     c                   if        *inkr = *on
     c                   call      'VV109R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c     vvenl1_key    setll     vvenl1
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
     c                   goto      b2taga
     c                   endif
      *
      * Behandler valg i subfilen
      *
     c                   exsr      subfile
      *
     c                   if        b_subf = *on
     c                   goto      b2taga
     c                   endif
      *
      * Validering av input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b2tagb
     c                   endif
      *
      * Oppdater vare-enhet
      *
     c                   exsr      oppdater
      *
     c                   goto      b2taga
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av valg i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_subf = *off
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   exsr      xc2bld
     c                   eval      b_subf = *on
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   exsr      xc2bld
     c                   eval      b_subf = *on
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
6.12  *
6.12  * Pr. Prisgiver
6.12  *
6.12 c                   if        b1valg = 7
6.12 c                   call      'VE115R  '
6.12 c                   parm                    w_firm
6.12 c                   parm                    p_vare
6.12 c                   parm                    b1enhe
6.12 c                   eval      b_subf = *on
6.12 c                   eval      b1valg = 0
6.12 c                   update    b1sfl
6.12 c                   iter
6.12 c                   endif
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for endring og vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      c2enhe = b1enhe
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      vvenlr_enhe = c2enhe
     c     vvenlr_key    chain     vvenlr
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
     c                   eval      c2omre = veomre
     c                   eval      c2leng = veleng
     c                   eval      c2bred = vebred
     c                   eval      c2hoyd = vehoyd
     c                   eval      c2volu = vevolu
     c                   eval      c2vekt = vevekt
     c                   eval      c2saen = vesaen
     c                   eval      c2inen = veinen
     c                   eval      c2pben = vepben
     c                   eval      c2eusr = veeusr
     c     *dmy          move      veodat        c2odat
     c     *dmy          move      veedat        c2edat
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2omre = 0
     c                   eval      *in31  = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   if        c2enhe = b2enhe and
     c                             c2omre <> 1
     c                   eval      *in32  = *on
     c                   goto      c2taga
     c                   leavesr
     c                   endif
      *
      * Kalkulerer volum
      *
     c                   if        c2volu = 0 and
     c                             c2leng <> 0 and
     c                             c2bred <> 0 and
     c                             c2hoyd <> 0
     c                   eval(h)   c2volu = (c2leng / 1000) *
     c                                      (c2bred / 1000) *
     c                                      (c2hoyd / 1000)
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   vvenlur
      *
     c                   eval      vvenlu_enhe = c2enhe
     c     vvenlu_key    chain     vvenlur
      *
     c                   eval      veomre = c2omre
     c                   eval      veleng = c2leng
     c                   eval      vebred = c2bred
     c                   eval      vehoyd = c2hoyd
     c                   eval      vevolu = c2volu
     c                   eval      vevekt = c2vekt
     c                   eval      veeusr = l_user
      *
     c                   time                    veedat
     c                   time                    veetim
      *
     c                   update    vvenlur
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1omre = veomre
     c                   eval      b1volu = vevolu
     c                   eval      b1vekt = vevekt
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile med enhet-informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
      * Fyller subfile med enheter
      *
     c                   dou       w_srrn = w_spge
      *
     c     vvenl1_key    reade     vvenl1
      *
     c                   if        %eof
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1enhe = veenhe
     c                   eval      b1omre = veomre
     c                   eval      b1saen = vesaen
     c                   eval      b1inen = veinen
     c                   eval      b1pben = vepben
     c                   eval      b1volu = vevolu
     c                   eval      b1vekt = vevekt
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørringer (F4)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
     c                   eval      b_enhe_salg = *off
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser alle rader i subfile
      *
     c                   eval      w_srrn_w = 0
      *
     c                   do        w_ssrn
      *
     c                   eval      w_srrn_w = w_srrn_w + 1
     c     w_srrn_w      chain     b1sfl
      *
     c                   if        not %found
     c                   leave
     c                   endif
      *
     c                   eval      w_virn = w_srrn
      *
      * Omregningsfaktor
      *
     c                   if        b1omre = 0
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b1enhe = b2enhe and
     c                             b1omre <> 1
     c                   eval      *in32  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Salgsenhet
      *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    w_lage
6.21 c                   parm                    w_vtyp
6.21 c                   parm                    w_udat
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
      *
5.70 c                   if        w_vtyp = 'L' or
5.70 c                             w_vtyp = 'S'
     c                   if        b1saen > 0
      *
6.20  * 1. leverandør for lageret
6.20 c                   eval      vvprl1_ldor = w_ldor
6.20 c                   exsr      sjekk_pris
6.20 c                   if        b_feil = *off  and
6.20 c                             w_ldor <> vvldor
6.20  * 2. hovedleverandør
6.20 c                   eval      vvprl1_ldor = vvldor
6.20 c                   exsr      sjekk_pris
6.20 c                   if        b_feil = *off
6.20  * siste mulighet...
6.20 c                   eval      vvprl1_ldor = 0
6.20 c                   exsr      sjekk_pris
6.20 c                   endif
6.20 c                   endif
      *
     c                   endif
     c                   endif
      *
     c                   if        b1saen > 0
     c                   eval      b_enhe_salg = *on
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_enhe_salg = *off
     c                   eval      *in40  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
6.20  *--------------------------------------------------------------
6.20  * sjekker om vare-pris pr. leverandør
6.20  *--------------------------------------------------------------
6.20 c     sjekk_pris    begsr
6.20  *
6.20 c                   eval      vvprl1_prgr = w_prgr
6.20 c                   eval      vvprl1_enhe = b1enhe
6.20 c     vvprl1_key    setll     vvprl1
6.20 c     vvprl1_key    reade     vvprl1
6.20 c                   dow       not %eof
6.20 c                   if        vpsapr = 0 or
6.20 c                             vpsapr = 0,01
6.20 c                   eval      *in33 = *on
6.20 c                   eval      b_feil = *on
6.20 c                   leavesr
6.20 c                   endif
6.20 c     vvprl1_key    reade     vvprl1
6.20 c                   enddo
6.20  *
6.20 c                   eval      vvprl1_prgr = *blank
6.20 c                   eval      vvprl1_enhe = b1enhe
6.20 c     vvprl1_key    setll     vvprl1
6.20 c     vvprl1_key    reade     vvprl1
6.20 c                   dow       not %eof
6.20 c                   if        vpsapr = 0 or
6.20 c                             vpsapr = 0,01
6.20 c                   eval      *in33 = *on
6.20 c                   eval      b_feil = *on
6.20 c                   leavesr
6.20 c                   endif
6.20 c     vvprl1_key    reade     vvprl1
6.20 c                   enddo
6.20  *
6.20 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering vare-enhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser alle rader i subfile
      *
     c                   eval      w_srrn_w = 0
      *
     c                   do        w_ssrn
      *
     c                   eval      w_srrn_w = w_srrn_w + 1
     c     w_srrn_w      chain     b1sfl
      *
     c                   if        not %found
     c                   leave
     c                   endif
      *
     c                   eval      vvenlu_enhe = b1enhe
     c     vvenlu_key    chain     vvenlur
     c                   if        %found
     c                   eval      veomre = b1omre
     c                   eval      vesaen = b1saen
     c                   eval      veinen = b1inen
     c                   eval      vepben = b1pben
6.10 c                   time                    veedat
6.10 c                   time                    veetim
6.10 c                   eval      veeusr = l_user
     c                   update    vvenlur
     c                   endif
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      * Key for les på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      * Key for oppslag på vare-enhet
      *
     c     vvenlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
     c                   kfld                    vvenlr_enhe
      *
      * Key for oppslag på vare-pris
      *
     c     vvprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
6.20 c                   kfld                    vvprl1_ldor
5.70 c                   kfld                    vvprl1_prgr
     c                   kfld                    vvprl1_enhe
      *
      * Key for oppdatering av vare-enhet
      *
     c     vvenlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
     c                   kfld                    vvenlu_enhe
      *
      *
      * Henter firma og vare fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_vare = p_vare
      *
5.70  * Henter prisgruppe
5.70  *
5.70 c                   call      'VL711R   '
5.70 c                   parm                    w_prgr
      *
5.70 c                   eval      w_lage = l_lage
      *
     c                   endsr
