     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : OFAK
      * Program . . . . : FP269R
      * beskrivelse . . : Vedlikehold av elektroniske hylleforkanter (Administrasjon)
      *
      * Spesialversjon. :
      * Tilpasset for . : Mestergruppen
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       060218 bsa  Nytt program
7.01  *       130818 bsa  Lagt ut aktivkode
      *
8.01  * K000  030921 bsa  Utvidet med leverandør Delfi/Breece
8.02  * K000  080622 bsa  Mulighet for REST integrasjon
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     faelhl1    if   e           k disk    rename(aelhpfr:aelhl1r)
     faelhlr    if   e           k disk    rename(aelhpfr:aelhlrr)
     faelhlu    uf a e           k disk    rename(aelhpfr:aelhlur)
      *
     ffp269d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d aelhl1_filg     s                   like(aefilg)
     d aelhl1_firm     s                   like(aefirm)
     d aelhl1_lage     s                   like(aelage)
     d aelhlu_filg     s                   like(aefilg)
     d aelhlu_firm     s                   like(aefirm)
     d aelhlu_lage     s                   like(aelage)
     d aelhlr_filg     s                   like(aefilg)
     d aelhlr_firm     s                   like(aefirm)
     d aelhlr_lage     s                   like(aelage)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(aefirm)
     d w_udate         s               d   datfmt(*iso)
     d w_stat          s              1
     d w_frad          s               d   datfmt(*iso)
     d w_tild          s               d   datfmt(*iso)
      *
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2filg <> *blanks
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      aelhlr_filg = b1filg
     c                   eval      aelhlr_firm = b1firm
     c                   eval      aelhlr_lage = b1lage
     c     aelhlr_key    setll     aelhlr
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Kjedekategori
      *
     c                   if        b2filg <> *blanks
     c                   eval      aelhlr_filg = b2filg
     c                   eval      aelhlr_firm = 0
     c                   eval      aelhlr_lage = 0
     c     aelhlr_key    setll     aelhlr
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2filg = *blanks
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1filg = b1filg
     c                   eval      c1firm = b1firm
     c                   eval      c1lage = b1lage
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1filg = b1filg
     c                   eval      d1firm = b1firm
     c                   eval      d1lage = b1lage
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1filg = b1filg
     c                   eval      c1firm = b1firm
     c                   eval      c1lage = b1lage
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1filg = *blanks
     c                   eval      c1firm = 0
     c                   eval      c1lage = 0
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
     c                   if        c1filg = *blanks
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      aelhl1_filg = c1filg
     c                   eval      aelhl1_firm = c1firm
     c                   eval      aelhl1_lage = c1lage
     c     aelhl1_key    chain     aelhl1
     c                   if        %found
     c                   exsr      xc2msg
     c                   goto      c1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1filg = *blanks
     c                   eval      c1firm = 0
     c                   eval      c1lage = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2filg = *blanks
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2msg        begsr
      *
     c                   exfmt     c2msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      aelhl1_filg = c1filg
     c                   eval      aelhl1_firm = c1firm
     c                   eval      aelhl1_lage = c1lage
     c     aelhl1_key    chain     aelhl1
      *
     c                   if        %found
     c                   eval      c2akti = aeakti
     c                   eval      c2etik = aeetik
     c                   eval      c2fide = aefide
     c                   eval      c2ovfa = aeovfa
     c                   eval      c2mpty = aempty
      *
     c                   if        aeskda <> *loval
     c     *dmy          move      aeskda        c2skda
     c                   else
     c                   eval      c2skda = 0
     c                   endif
      *
     c                   if        aesoda <> *loval
     c     *dmy          move      aesoda        c2soda
     c                   else
     c                   eval      c2soda = 0
     c                   endif
      *
     c     *dmy          move      aeedat        c2edat
     c                   eval      c2eusr = aeeusr
     c                   else
     c                   eval      c2akti = '0'
     c                   eval      c2etik = *blanks
     c                   eval      c2fide = *blanks
     c                   eval      c2ovfa = *blanks
     c                   eval      c2mpty = *blanks
     c**                 eval      c2skda = *loval
     c**                 eval      c2soda = *loval
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   aelhlur
      *
     c                   eval      aelhlu_filg = c1filg
     c                   eval      aelhlu_firm = c1firm
     c                   eval      aelhlu_lage = c1lage
     c     aelhlu_key    chain     aelhlur
      *
     c                   eval      aeakti = c2akti
     c                   eval      aeetik = c2etik
     c                   eval      aefide = c2fide
     c                   eval      aeovfa = c2ovfa
     c                   eval      aempty = c2mpty
      *
     c                   time                    aeedat
     c                   time                    aeetim
     c                   eval      aeeusr = l_user
      *
     c                   if        %found
     c                   update    aelhlur
     c                   else
     c                   eval      aefirm = c1firm
     c                   eval      aefilg = c1filg
     c                   eval      aelage = c1lage
     c                   time                    aeodat
     c                   time                    aeotim
     c                   eval      aeousr = l_user
     c                   write     aelhlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1filg = aefilg
     c                   eval      b1firm = aefirm
     c                   eval      b1lage = aelage
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      aelhl1_filg = d1filg
     c                   eval      aelhl1_firm = d1firm
     c                   eval      aelhl1_lage = d1lage
     c     aelhl1_key    chain     aelhl1
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      aelhlu_filg = d1filg
     c                   eval      aelhlu_firm = d1firm
     c                   eval      aelhlu_lage = d1lage
     c     aelhlu_key    chain     aelhlur
      *
     c                   delete    aelhlur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      aelhlr
      *
     c                   if        %eof
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      aelhl1_filg = aefilg
     c                   eval      aelhl1_firm = aefirm
     c                   eval      aelhl1_lage = aelage
      *
     c                   eval      b1valg = 0
     c                   eval      b1filg = aefilg
     c                   eval      b1firm = aefirm
     c                   eval      b1lage = aelage
     c                   eval      b1fide = aefide
7.01 c                   eval      b1akti = aeakti
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     aelhlr_key    setgt     aelhlr
     c                   readp     aelhlr
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     aelhlr
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      aelhlr_filg = aefilg
     c                   eval      aelhlr_firm = aefirm
     c                   eval      aelhlr_lage = aelage
      *
     c                   enddo
      *
     c                   if        %eof
     c     aelhlr_key    setll     aelhlr
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for les på elektroniske hylleforkanter
      *
     c     aelhl1_key    klist
     c                   kfld                    aelhl1_filg
     c                   kfld                    aelhl1_firm
     c                   kfld                    aelhl1_lage
      *
      * Key for les på elektroniske hylleforkanter
      *
     c     aelhlu_key    klist
     c                   kfld                    aelhlu_filg
     c                   kfld                    aelhlu_firm
     c                   kfld                    aelhlu_lage
      *
      * Key for les på elektroniske hylleforkanter
      *
     c     aelhlr_key    klist
     c                   kfld                    aelhlr_filg
     c                   kfld                    aelhlr_firm
     c                   kfld                    aelhlr_lage
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
     c                   time                    w_udate
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      aelhlr_filg = *blanks
     c                   eval      aelhlr_firm = 0
     c                   eval      aelhlr_lage = 0
     c     aelhlr_key    setll     aelhlr
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
