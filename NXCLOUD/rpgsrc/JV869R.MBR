     h option(*nodebugio) datedit(*dmy)

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JV868R
      * Beskrivelse . . : Oppdater fra NOBB, produsent
      *
      *                   Oppdaterer deltagerr NOBB (XML-format)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       040414 BKV
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjprolu    uf a e           k disk    rename(jpropfr:jprolur)
     f                                     infsr(FileErr)
     fjstsl1    if   e           k disk    rename(jstspfr:jstsl1r)
     f                                     infsr(FileErr)
      *
      * Definering av variable i nøkler
      *
     d jprolu_prod     s                   like(jqprod)
      * Brukes som parametre til jobblogging
     d p_jbid          s             41
     d p_jsts          s              2  0
     d p_jmld          s            200
      *
      * Definering av Local Data Area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
     d w_firm          s              3  0
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_stat          s              1
     d p_orec          s            100
     d p_hrec          s            100
     d p_urec          s            100
     d p_prec          s            150
     d p_job_log       s             50
     d p_inlr          s              1
      *
      *  Dataelementer produsent
      *
     d                 ds
     d d_prec                       150
     d  d_prnr                        4    overlay(d_prec:1)
     d    d_prnr_num                  4  0 overlay(d_prnr:1)
     d  d_navn                      100    overlay(d_prec:5)
      *
     d d_job_log                     50
     d  w_aprot                       5    overlay(d_job_log:2)
     d  w_apro                        5  0 overlay(w_aprot:1)
      * Definering av variable
      *
     d b_inlr          s              1
     d b_vfel          s              1    inz(*off)
     d b_slett         s              1    inz(*off)
      *
      * Datastruktur for informasjon ved exceptions
      *
     d PgmError       sds
     d  proc_name        *proc
     d  pgm_status       *STATUS
     d  prv_status            16     20s 0
     d  line_numm             21     28
     d  routine          *ROUTINE
     d  parms            *PARMS
     d  excp_type             40     42
     d  excp_numm             43     46
     d  pgm_lib               81     90
     d  excp_data             91    170
     d  excp_id              171    174
     d  date                 191    198
     d  year                 199    200s 0
     d  last_file            201    208
     d  file_info            209    243
     d  job_name             244    253
     d  job_user             254    263
     d  job_numb             264    269s 0

      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

     c                   eval      d_job_log = p_job_log
     c                   if        p_inlr = *on
     c                   eval      d_prec = p_prec
      *
     c                   exsr      skr_prod
      *
     c                   endif

      * retunerer jobb status
     c                   eval      p_job_log = d_job_log

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     avslutt       tag

     c                   if        p_inlr <> *on
     c                   eval      *inlr = *on
     c                   endif
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppdaterer produsent
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_prod      begsr
      *

     c                   eval      jprolu_prod = d_prnr_num
     c     jprolu_key    chain     jprolur
      *
     c                   time                    jqedat
     c                   time                    jqetim
     c                   eval      jqeusr     =  l_user
      *
     c                   if        %found
     c                   eval      jqnavn     = d_navn
     c                   update    jprolur
     c                   else
     c                   clear                   jprolur
     c                   eval      jqprod     =  d_prnr_num
     c                   eval      jqnavn     =  d_navn
     c                   time                    jqedat
     c                   time                    jqetim
     c                   eval      jqeusr     =  l_user
     c                   time                    jqodat
     c                   eval      w_apro = w_apro + 1
     c                   write     jprolur
     c                   endif
      *
      *

      *
     c                   endsr

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Error rutine for file errors
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     FileErr       begsr
      *
     c                   eval      p_stat = '1'
     c                   eval      p_jsts = 50
     c                   eval      p_jmld = 'Fil ' + %trim(last_file) +
     c                             ' feil. Info=' + %trim(excp_data) +
     c                             '. Job=' + %trim(job_name) + '/' +
     c                             %trim(job_user) + '/' +
     c                             %char(job_numb)
      *
     c                   call      'AB705R'
     c                   parm                    p_jbid
     c                   parm                    p_jsts
     c                   parm                    p_jmld
      *
     c                   goto      avslutt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_prec
     c                   parm                    p_job_log
     c                   parm                    p_inlr
      *
      * Key for oppdatering av produsent
      *
     c     jprolu_key    klist
     c                   kfld                    jprolu_prod
      *
      * Key for oppslag på status
      *
     c     jstsl1_key    klist
     c                   kfld                    w_firm
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = l_firm
      *
     c     jstsl1_key    chain     jstsl1
      *
     c                   endsr

