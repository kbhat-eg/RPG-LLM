     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FL505R
      * Beskrivelse . . : Kundeprosjekt, vise detaljer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       121005 HKO  Nytt program
      *       080108 bof  utvidet med kjørerute og nr.
5.61  * R5.61 080108 AMD  Fakturatype ble ikke lagt ut i skjermbildet
6.20  * 6.20  140211 nho  Oppslag mot nytt prosjekt
6.21  * 6.20  070113 bof  Utvidet med felt for byggdok.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     ffkprlu    uf a e           k disk    rename(fkprpfr:fkprlur)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
     fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
     fra25lr    if   e           k disk    rename(ra25pfr:ra25lrr)
     fra06pf    if   e           k disk    rename(ra06pfr:ra06pfr)
     faposl1    if   e           k disk    rename(apospfr:aposl1r)
      *
     ffl505d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_firm          s                   like(flfirm)
     d p_kund          s                   like(flkund)
     d p_kpro          s                   like(flkpro)
6.30 d p_stat          s              1
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d fkprlu_kund     s                   like(flkund)
     d fkprlu_kpro     s                   like(flkpro)
     d rkunl1_kund     s                   like(rkkund)
     d fusrl1_user     s                   like(fbuser)
     d ra09l1_ikod     s                   like(raikod)
     d ra06pf_fkod     s                   like(rafkod)
     d ra25lr_yrut     s                   like(rayrut)
     d aposl1_ponr     s                   like(apponr)
      *
      * Definering av variable
      *
     d w_akti          s              3
     d w_kont          s              5  0
     d w_ptx1          s             30
     d w_ptx2          s             30
     d w_retk          s              1
     d w_spes          s              4  0
6.20 d w_uprs          s              1
6.20 d w_upro          s                   like(c2proj)
      *
     d w_firm          s                   like(flfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      fkprl1_kund = p_kund
     c                   eval      fkprl1_kpro = p_kpro
     c     fkprl1_key    chain     fkprl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      c2fdat = 0
     c                   eval      c2tdat = 0
     c                   eval      c2selg = flselg
     c                   eval      c2navn = flnavn
     c                   eval      c2adr1 = fladr1
     c                   eval      c2adr2 = fladr2
     c                   eval      c2ponr = flponr
     c                   eval      c2epro = flepro
     c                   eval      c2kprs = flkprs
     c                   eval      c2tlfa = fltlfa
     c                   eval      c2tlfp = fltlfp
     c                   eval      c2tlfm = fltlfm
     c                   eval      c2mail = flmail
     c                   eval      c2proj = flproj
     c                   eval      c2boku = flboku
     c                   eval      c2priv = flpriv
     c                   eval      c2orka = florka
     c                   eval      c2okun = flokun
     c                   eval      c2okpr = flokpr
     c                   eval      c2opda = 0
5.61 c                   eval      c2faty = flfaty
     c                   eval      c2kjor = flkjor
     c                   eval      c2kjnr = flkjnr
6.21 c                   eval      c2bdok = flbdok
     c                   eval      c2eusr = fleusr
      *
     c                   if        flfdat <> *loval
     c     *dmy          move      flfdat        c2fdat
     c                   endif
      *
     c                   if        fltdat <> *loval
     c     *dmy          move      fltdat        c2tdat
     c                   endif
      *
     c                   if        flopda <> *loval
     c     *dmy          move      flopda        c2opda
     c                   endif
      *
     c     *dmy          move      flodat        c2odat
     c     *dmy          move      fledat        c2edat
      *

     c                   eval      c2knav = *blank
     c                   eval      c2snav = *blank
     c                   eval      c2sted = *blank
     c                   eval      c2bokn = *blank
     c                   eval      c2ortx = *blank
     c                   eval      c2okna = *blank
     c                   eval      c2opna = *blank
     c                   eval      c2ptxt = *blank
      *
      * Henter selgernavn
      *
     c                   if        c2selg <> 0
     c                   eval      ra09l1_ikod = c2selg
     c     ra09l1_key    chain     ra09l1
     c                   if        %found
     c                   eval      c2snav = raitxt
     c                   endif
     c                   endif
      *
      * Henter kundenavn
      *
     c                   eval      rkunl1_kund = p_kund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      c2knav = rknavn
     c                   endif
      *
      * Henter poststed
      *
     c                   if        c2ponr <> 0
     c                   eval      aposl1_ponr = c2ponr
     c     aposl1_key    chain     aposl1
     c                   if        %found
     c                   eval      c2sted = apsted
     c                   endif
     c                   endif
      *
      * Henter navn på bonuskunde
      *
     c                   if        c2boku <> 0
     c                   eval      rkunl1_kund = c2boku
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      c2bokn = rknavn
     c                   endif
     c                   endif
      *
      * Henter beskrivelse av overstyringer
      *
     c                   if        c2orka <> 0
     c                   eval      ra06pf_fkod = c2orka
     c     ra06pf_key    chain     ra06pf
     c                   if        %found
     c                   eval      c2ortx = raftxt
     c                   endif
     c                   endif
      *
     c                   if        c2okun <> 0
     c                   eval      rkunl1_kund = c2okun
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      c2okna = rknavn
     c                   endif
     c                   endif
      *
     c                   if        c2okpr <> 0
     c                   eval      fkprl1_kund = c2okun
     c                   eval      fkprl1_kpro = c2okpr
     c     fkprl1_key    chain     fkprl1
     c                   if        %found
     c                   eval      c2opna = flnavn
     c                   endif
     c                   endif
      *
      * Henter kjørerute beskrivelse
      *
     c                   eval      ra25lr_yrut = c2kjor
     c     ra25lr_key    chain     ra25lr
     c                   if        %found
     c                   eval      c2kjna = raytxt
     c                   endif
      *
      *
      * Henter prosjektinfo fra regnskap
      *
     c                   exsr      sjekk_prosj
      *
      * Viser bildet
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
6.30  *
6.30  * Call program for Flere koder
6.30  *
6.30 c                   if        *inkf = *on
6.30 c                   exsr      xf2win
6.30 c                   goto      c2tagb
6.30 c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      *
     c     xc2bld        begsr
     c                   endsr
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30  * F2WIN Behandle bilde                                            *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30  *
6.30 c     xf2win        begsr
6.30  *
6.30  * Henter betalingsbetingelse tekst
6.30  *
6.30 c                   eval      f2mkod = flmkod
6.30 c                   eval      f2ekgb = flekgb
6.30 c                   eval      f2prpa = flprpa
6.30 c                   eval      f2neto = flneto
6.30 c                   eval      f2samf = flsamf
6.30 c                   eval      f2sekv = flsekv
6.30 c                   eval      f2ousr = flousr
6.30 c     *dmy          move      flodat        f2odat

6.30 c                   if        f2betb = 0
6.30 c                   eval      f2betb = flbetb

6.30 c                   if        f2betb <> *zero

6.30 c                   call      'RS703R'
6.30 c                   parm                    p_stat
6.30 c                   parm                    f2betb
6.30 c                   parm                    f2ctxt
      *
6.30 c                   if        p_stat = 'N'
6.30 c                   eval      f2ctxt = *blank
6.30 c                   endif
6.30 c                   endif
6.30 c                   endif
      *
6.30 c                   exfmt     f2win
      *
6.30  *
6.30 c                   endsr
6.30  *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av prosjekt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_prosj   begsr
      *
      * Prosjekt
      *
     c                   eval      w_kont = 0
     c                   eval      w_spes = 0
      *
6.20 c*                  call      'RS770R'
6.20 c                   call      'RS880R'
     c                   parm                    w_retk
     c                   parm                    c2proj
6.20 c                   parm                    w_upro
6.20 c*                  parm                    w_akti
6.20 c*                  parm                    w_kont
6.20 c*                  parm                    w_spes
     c                   parm                    w_ptx1
     c                   parm                    w_ptx2
      *
     c                   eval      c2ptxt = *blank
     c                   if        w_retk = *blank
     c                   eval      c2ptxt = w_ptx1
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kund
     c                   parm                    p_kpro
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppdatering av kundeprosjekt
      *
     c     fkprlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprlu_kund
     c                   kfld                    fkprlu_kpro
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på bruker
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrl1_user
      *
      * Key for oppslag på selger
      *
     c     ra09l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra09l1_ikod
      *
      * Key for oppslag på rabattkategori
      *
     c     ra06pf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra06pf_fkod
      *
      * Key for oppslag på kjørerute
      *
     c     ra25lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra25lr_yrut
      *
      * Key for oppslag på poststed-register
      *
     c     aposl1_key    klist
     c                   kfld                    aposl1_ponr
      *
      *
      * Henter verdier fra parameter, og initierer skjermbilde
      *
     c                   eval      w_firm = p_firm
      *
     c                   eval      c2kund = p_kund
     c                   eval      c2kpro = p_kpro
      *
     c                   endsr
