     h option(*nodebugio) datedit(*dmy) decedit(',')
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSHOP
      * Program . . . . : BU921R
      * Beskrivelse . . : Butikk, henter vare med enheter og kalkulert pris
      *                   og rabatt for første salgsenhet
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       020504 HKO  Nytt program
      *       180805 HKO  Endret logikk for lesing av salgsenheter
      *       150507 MHA  La til uthenting av salgsenhet for EAN
      * EANx  191108 MHA  Sjekke om salgsenheten i ean register faktisk finnes
      *                   på varen. Hvis ikke settes til blank.
5.61  * EANx  210109 bof  ..og om det er salgsenhet 1
5.70  * PRGR  121208 bof  Utvidet med prisgruppe
5.62  * EANx  300109 bof  Legger ut vare til logg-fil hvis feilsituasjon
5.63  * EANx  230209 bof  Fjernet loggfil-variant inntil videre-- v 6.10
5.64  *       151009 bof  Sjekker om enhet er salgsenh. før den legges ut
6.10  *       031109 bof  sjekker om det er gyldig salgsenhet vesaen > 0
6.11  *       031109 bof  Tatt bort direkte oppslag på veanl1 - subprogram
6.12  *       310712 bof  Sjekker om varenr er noen av spes.varenr.
6.20  * 6.20  190511 bof  Utvidet vl710r med utg.dato og lev.nr
6.30  * 6.30  290514 BKV  LVAR økt fra 15 til 20
6.31  * 6.30  210815 bof  feilretting i parameter til VP900R
6.32  * 6.30  310316 bof  Hvis lager er 0 fra parm, så hent fra kunde
6.33  * 6.30  100516 bof  Sender prior.lev. inn i prisberegningsprogrammet
6.34  * 6.30  170918 bkv  Utvidet med trelast sortiment
8.01  *PRG2   080622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarl4    if   e           k disk    rename(vvarpfr:vvarl4r)
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
EANx fvvenlr    if   e           k disk    rename(vvenpfr:vvenlrr)
6.12 fbstsl1    if   e           k disk    rename(bstspfr:bstsl1r)
6.32 frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
6.34 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
      * Definering av parametre
      *
     d p_firm          s              3
     d p_kund          s              6  0
     d p_kpro          s              6  0
     d p_scan          s             50
     d p_lety          s              1  0
     d p_lagr          s              2  0
     d p_avde          s              4  0
      *
     d p_vare          s             15
     d p_vinf          s            240
     d p_einf          s             96
6.11 d p_eann          s             14  0
     d p_anta          s             11  3
     d p_olet          s              1  0
     d p_okpr          s              1
     d p_oppr          s              9  2
     d p_sapr          s              9  2
     d p_kopr          s              9  2
     d p_rab1          s              5  2
     d p_rab2          s              5  2
     d p_kamp          s              5
     d p_alme          s              4  0
     d p_brty          s              1  0
     d p_brr1          s              5  2
     d p_brr2          s              5  2
     d p_kmva          s              1
      *
      * Definering av datastrukturer
      *
      * Datastruktur - vareinformasjon
      *
     d                 ds
6.30 d d_vinf                       245
6.30 d  d_tek1                       35    overlay(d_vinf:1)
6.30 d  d_tek2                       35    overlay(d_vinf:36)
6.30 d  d_ogrp                        2  0 overlay(d_vinf:71)
6.30 d  d_hgrp                        2  0 overlay(d_vinf:73)
6.30 d  d_ugrp                        3  0 overlay(d_vinf:75)
6.30 d  d_nobb                        8  0 overlay(d_vinf:78)
6.30 d  d_modn                        8  0 overlay(d_vinf:86)
6.30 d  d_ldor                        6  0 overlay(d_vinf:94)
6.30 d  d_lvar                       20    overlay(d_vinf:100)
6.30 d  d_kpri                        1    overlay(d_vinf:120)
6.30 d  d_vtyp                        1    overlay(d_vinf:121)
6.30 d  d_enhe                        3    overlay(d_vinf:122)
6.30 d  d_enhs                        3    overlay(d_vinf:125)
6.30 d  d_enhl                        3    overlay(d_vinf:128)
6.30 d  d_crdo                        1  0 overlay(d_vinf:131)
6.30 d  d_kord                        1  0 overlay(d_vinf:132)
6.30 d  d_omrn                       13    overlay(d_vinf:133)
6.30 d  d_udat                         d   overlay(d_vinf:146) datfmt(*iso)
6.30 d  d_odat                         d   overlay(d_vinf:156) datfmt(*iso)
6.30 d  d_edat                         d   overlay(d_vinf:166) datfmt(*iso)
6.30 d  d_etim                         t   overlay(d_vinf:176) timfmt(*iso)
6.30 d  d_eusr                       10    overlay(d_vinf:184)
6.30 d  d_eane                        3    overlay(d_vinf:194)
6.30 d  d_meld                       25    overlay(d_vinf:197)
6.30 d  d_vdum                        1    overlay(d_vinf:245) inz('*')
      *
      * Datastruktur - enhetsinformasjon
      *
     d                 ds
     d d_einf                        96
     d  d_enh1                        3    overlay(d_einf:1)
     d  d_lin1                        2  0 overlay(d_einf:4)
     d  d_omr1                       13    overlay(d_einf:6)
     d  d_enh2                        3    overlay(d_einf:19)
     d  d_lin2                        2  0 overlay(d_einf:22)
     d  d_omr2                       13    overlay(d_einf:24)
     d  d_enh3                        3    overlay(d_einf:37)
     d  d_lin3                        2  0 overlay(d_einf:40)
     d  d_omr3                       13    overlay(d_einf:42)
     d  d_enh4                        3    overlay(d_einf:55)
     d  d_lin4                        2  0 overlay(d_einf:58)
     d  d_omr4                       13    overlay(d_einf:60)
     d  d_enh5                        3    overlay(d_einf:73)
     d  d_lin5                        2  0 overlay(d_einf:76)
     d  d_omr5                       13    overlay(d_einf:78)
     d  d_edum                        1    overlay(d_einf:96) inz('*')
      *
      * Datastruktur - EAN128
      *
     d                 ds
     d d_e128                        26
6.11 d  d_fast                        2  0 overlay(d_e128)
6.11 d  d_eann                       14  0 overlay(d_e128:3)
     d  d_kode                        4  0 overlay(d_e128:17)
     d  d_anta                        6  2 overlay(d_e128:21)
      *
      * Parametre for henting av pris -inn
      *
     d                 ds
     d hirec                        120
     d  hifirm                        3  0 overlay(hirec)
     d  hirkat                        3  0 overlay(hirec:4)
     d  hikund                        6  0 overlay(hirec:7)
     d  hikpro                        6  0 overlay(hirec:13)
     d  hivare                       15    overlay(hirec:19)
     d  hilety                        1  0 overlay(hirec:34)
     d  hienhe                        3    overlay(hirec:35)
     d  hidato                         d   overlay(hirec:38) datfmt(*iso)
     d  hitime                         t   overlay(hirec:48) timfmt(*iso)
6.31 d  hinumm                        8  0 overlay(hirec:56)
6.31 d  hikode                        1    overlay(hirec:64)
6.31 d  hidekn                        5  2 overlay(hirec:65)
6.31 d  hiavgf                        1  0 overlay(hirec:70)
6.31 d  hipriv                        1  0 overlay(hirec:71)
6.31 d  hitilb                        1    overlay(hirec:72)
6.31 d  hiskaf                        1    overlay(hirec:73)
6.31 d  hildor                        6  0 overlay(hirec:74)
6.31 d  hisapr                        9  2 overlay(hirec:80)
6.31 d  hikopr                        9  2 overlay(hirec:89)
8.01 d  hiprgr                        2    overlay(hirec:98)
     d  hiinlr                        1    overlay(hirec:120)
      *
      * Parametre for henting av pris -ut
      *
     d                 ds
     d horec                         80
     d  holety                        1  0 overlay(horec)
     d  hokpri                        1    overlay(horec:2)
     d  hooppr                        9  2 overlay(horec:3)
     d  hosapr                        9  2 overlay(horec:12)
     d  hokopr                        9  2 overlay(horec:21)
     d  horab1                        5  2 overlay(horec:30)
     d  horab2                        5  2 overlay(horec:35)
     d  hodekn                        5  2 overlay(horec:40)
     d  hopros                        5  2 overlay(horec:45)
     d  hokamp                        5    overlay(horec:50)
     d  hoalme                        4  0 overlay(horec:55)
     d  hobrty                        1  0 overlay(horec:59)
     d  hobrr1                        5  2 overlay(horec:60)
     d  hobrr2                        5  2 overlay(horec:65)
     d  hoomva                        1    overlay(horec:70)
8.01 d  hoprgr                        2    overlay(horec:71)
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vvarl4_lvar     s                   like(vvlvar)
     d vvenl2_vare     s                   like(vevare)
     d vvenl2_saen     s                   like(vesaen)
     d vvenlr_vare     s                   like(vevare)
     d vvenlr_enhe     s                   like(veenhe)
     d rkunl1_kund     s                   like(rkkund)
      *
      * Definering av variable
      *
     d w_leng          s              2  0
     d w_vare          s             15
6.11 d w_eanx          s             14
6.11 d w_eann          s             14  0
6.11 d w_eava          s                   like(vvvare)
6.11 d w_eaen          s                   like(vvenhe)
6.11 d w_east          s              1
      *
8.01 d w_prgr          s              2
5.70 d w_lage          s              2  0
5.70 d w_avde          s              4  0
5.70 d w_vtyp          s                   like(vvvtyp)
6.20 d w_utda          s               d   datfmt(*iso)
6.20 d w_ldor          s                   like(vvldor)
6.20 d b_inlr          s              1    inz(*off)
6.34 d w_lv_nobb       s                   like(vvlnob)
6.34 d w_lv_modn       s                   like(vvlmod)
6.34 d w_lv_lldo       s                   like(vvlnle)
6.34 d w_lv_llva       s                   like(vvllva)
6.31 d w_lv_vare       s                   like(vvlvar)
      *
     d w_firm          s                   like(vvfirm)
      *
      * Definering av konstanter
      *
     d c_numm          c                   '0123456789 '
     d c_null          c                   '0000000000000'
      *
5.70 d                uds
5.70 d l_lagr               1001   1002  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Avbryter dersom scannefelt ikke er oppgitt
      *
     c                   if        p_scan = *blank
     c                   goto      avslutt
     c                   endif
      *
      * Utleder parametre for kall av program og initierer enkeltverdier
      * i datastruktuen
      *
6.12 c                   move      p_firm        w_firm
6.12  *
6.12 c     bstsl1_key    chain     bstsl1
      *
     c                   eval      d_lin1 = 0
     c                   eval      d_lin2 = 0
     c                   eval      d_lin3 = 0
     c                   eval      d_lin4 = 0
     c                   eval      d_lin5 = 0
      *
     c                   eval      d_omrn = '000000,000000'
     c                   eval      d_omr1 = '000000,000000'
     c                   eval      d_omr2 = '000000,000000'
     c                   eval      d_omr3 = '000000,000000'
     c                   eval      d_omr4 = '000000,000000'
     c                   eval      d_omr5 = '000000,000000'
      *
      * Utleder vare fra scannet verdi
      * Avbryter dersom vare ikke ble funnet
      *
     c                   exsr      scanning
      *
     c                   if        p_vare = *blank
     c                   goto      avslutt
     c                   endif
6.12  *
6.12  *  hvis varenr er spes.varenr, settes varenr til blank og det
6.12  *  returneres
6.12  *
6.12 c                   if        p_vare = baavar or
6.12 c                             p_vare = baagva or
6.12 c                             p_vare = baadva
6.12 c                   eval      p_vare = *blank
6.12 c                   goto      avslutt
6.12 c                   endif
      *
      * Henter første salgsenhet fra varen
      *
     c                   eval      vvenl2_vare = p_vare
     c                   eval      vvenl2_saen = 1
     c     vvenl2_key    setll     vvenl2
     c     vvenl2_key2   reade     vvenl2
     c                   dow       not %eof
     c                   if        vesaen > 0
     c                   select
     c                   when      d_lin1 = 0 and d_enh1 = *blank
     c                   eval      d_enh1 = veenhe
     c                   eval      d_lin1 = vesaen
     c***                eval      d_omr1 = veomre
     c                   eval      d_omr1 = %editc(veomre:'4')
     c                   eval      d_omr1 = %xlate(' ':'0':d_omr1)
     c                   when      d_lin2 = 0 and d_enh2 = *blank
     c                   eval      d_enh2 = veenhe
     c                   eval      d_lin2 = vesaen
     c***                eval      d_omr2 = veomre
     c                   eval      d_omr2 = %editc(veomre:'4')
     c                   eval      d_omr2 = %xlate(' ':'0':d_omr2)
     c                   when      d_lin3 = 0 and d_enh3 = *blank
     c                   eval      d_enh3 = veenhe
     c                   eval      d_lin3 = vesaen
     c***                eval      d_omr3 = veomre
     c                   eval      d_omr3 = %editc(veomre:'4')
     c                   eval      d_omr3 = %xlate(' ':'0':d_omr3)
     c                   when      d_lin4 = 0 and d_enh4 = *blank
     c                   eval      d_enh4 = veenhe
     c                   eval      d_lin4 = vesaen
     c***                eval      d_omr4 = veomre
     c                   eval      d_omr4 = %editc(veomre:'4')
     c                   eval      d_omr4 = %xlate(' ':'0':d_omr4)
     c                   when      d_lin5 = 0 and d_enh5 = *blank
     c                   eval      d_enh5 = veenhe
     c                   eval      d_lin5 = vesaen
     c***                eval      d_omr5 = veomre
     c                   eval      d_omr5 = %editc(veomre:'4')
     c                   eval      d_omr5 = %xlate(' ':'0':d_omr5)
     c                   endsl
     c                   endif
     c     vvenl2_key2   reade     vvenl2
     c                   enddo
5.70  *
5.70  * henter varetype
5.70  *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    w_lage
5.70 c                   parm                    w_vtyp
6.20 c                   parm                    w_utda
6.20 c                   parm                    w_ldor
6.20 c                   parm                    b_inlr
6.34 c                   parm                    w_lv_nobb
6.34 c                   parm                    w_lv_modn
6.34 c                   parm                    w_lv_lldo
6.34 c                   parm                    w_lv_llva
      *
      * Fyller opp datastruktur for kall av prisberegningsprogram
      *
     c                   eval      hifirm = w_firm
     c                   eval      hirkat = 0
     c                   eval      hikund = p_kund
     c                   eval      hikpro = p_kpro
     c                   eval      hivare = p_vare
     c                   eval      hilety = p_lety
     c                   eval      hienhe = d_enh1
     c                   eval      hidato = *loval
     c                   eval      hitime = *loval
     c                   eval      hinumm = 0
     c                   eval      hikode = *blank
     c                   eval      hidekn = 0
     c                   eval      hiavgf = 0
     c                   eval      hipriv = 0
     c                   eval      hitilb = *off
     c                   eval      hiskaf = *off
6.33 c                   eval      hildor = w_ldor
     c                   eval      hisapr = 0
     c                   eval      hikopr = 0
     c                   eval      hiinlr = *off
5.70 c                   eval      hiprgr = w_prgr
      *
     c                   time                    hidato
     c                   time                    hitime
      *
      * Kaller prisberegningsprogram
      *
     c                   call      'VP900R'
     c                   parm                    hirec
     c                   parm                    horec
      *
      * Fyller output-parametre
      *
     c                   eval      d_tek1 = vvtek1
     c                   eval      d_tek2 = vvtek2
     c                   eval      d_ogrp = vvogrp
     c                   eval      d_hgrp = vvhgrp
     c                   eval      d_ugrp = vvugrp
6.34 c                   if        w_lv_nobb > 0
6.34 c                   eval      d_nobb = w_lv_nobb
6.34 c                   eval      d_modn = w_lv_modn
6.34 c                   eval      d_lvar = w_lv_llva
6.34 c                   else
     c                   eval      d_nobb = vvnobb
     c                   eval      d_modn = vvnmod
     c                   eval      d_lvar = vvlvar
6.34 c                   endif
     c                   eval      d_ldor = vvldor
     c                   eval      d_kpri = vvkpri
5.70 c                   eval      d_vtyp = w_vtyp
5.70 c**                 eval      d_vtyp = vvvtyp
     c                   eval      d_enhe = vvenhe
     c                   eval      d_enhs = vvenhs
     c                   eval      d_enhl = vvenhl
     c                   eval      d_crdo = vvcrdo
     c                   eval      d_kord = vvkord
6.20 c                   eval      d_udat = w_utda
     c                   eval      d_odat = vvodat
     c                   eval      d_edat = vvedat
     c                   eval      d_etim = vvetim
     c                   eval      d_eusr = vveusr
      *
     c                   if        vvomrn <> 0
     c***                eval      d_omrn = vvomrn
     c                   eval      d_omrn = %editc(vvomrn:'4')
     c                   eval      d_omrn = %xlate(' ':'0':d_omrn)
     c                   endif
5.61  *
5.61  * hvis salgspris = 0, så blankes ean-enhet
5.61  *
5.61 c                   if        hosapr = 0 and
5.61 c                             d_eane <> *blank
5.61 c                   eval      d_eane = *blank
5.61 c                   endif
      *
     c                   eval      p_vinf = d_vinf
      *
     c                   eval      p_einf = d_einf
6.11  *
6.11 c                   call      'VE711R'
6.11 c                   parm                    w_firm
6.11 c                   parm                    p_vare
6.11 c                   parm                    d_enh1
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_east
6.11 c                   if        w_east = '1'
6.11 c                   eval      p_eann = w_eann
     c                   else
     c                   eval      p_eann = 0
     c                   endif
      *
     c                   eval      p_olet = holety
     c                   eval      p_okpr = hokpri
     c                   eval      p_oppr = hooppr
     c                   eval      p_sapr = hosapr
     c                   eval      p_kopr = hokopr
     c                   eval      p_rab1 = horab1
     c                   eval      p_rab2 = horab2
     c                   eval      p_kamp = hokamp
     c                   eval      p_alme = hoalme
     c                   eval      p_brty = hobrty
     c                   eval      p_brr1 = hobrr1
     c                   eval      p_brr2 = hobrr2
     c                   eval      p_kmva = hoomva
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for identifisering av scannet vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     scanning      begsr
      *
      * Identifisering av vare foregår etter følgende rekkefølge, hvor
      * lengden på scannefeltet, og om scannefeltet er alfa eller numerisk
      * er avgjørende:
      *
      * 1) alfa:
      *      - Lev. varenummer (første 15 pos.)
      * 2) num + lengde = 26:
      *      - EAN-128
      * 3) num + lengde <= 8:
      *      - Varenr
      * 4) num + lengde <= 13:
      *      - EAN-nummer
      * 5) num + lengde < 26:
      *      - Lev. varenummer (første 15 pos.)
      *
     c                   eval      p_scan = %trim(p_scan)
      *
     c                   eval      w_leng = %len(%trim(p_scan))
      *
      * Behandling dersom alfa
      *
     c                   if        %check(c_numm:p_scan) > 0
      *
6.31  * sjekker om logistikk-vare
6.31 c                   eval      w_lv_vare = *blank
6.31 c/exec sql
6.31 c+ select vvlvnr
6.31 c+ into   :w_lv_vare
6.31 c+ from   vvlepf
6.31 c+ where  vvllva = :p_scan
6.31 c+ fetch first 1 rows only
6.31 c/end-exec
6.31 c                   if        w_lv_vare <> *blank
6.31 c                   movel     w_lv_vare     p_vare
6.31 c                   leavesr
6.31 c                   endif
      *
     c                   eval      vvarl4_lvar = p_scan
     c     vvarl4_key    chain     vvarl4
     c                   if        %found
     c                   eval      p_vare = vvvare
     c                   endif
      *
     c                   leavesr
      *
     c                   endif
      *
      * Behandling dersom lengde = 26
      *
     c                   if        w_leng = 26
      *
     c                   eval      d_e128 = p_scan
     c                   eval      w_eann = d_eann
6.11 c                   call      'VE710R  '
6.11 c                   parm                    w_firm
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_eava
6.11 c                   parm                    w_eaen
6.11 c                   parm                    w_east
6.11 c                   if        w_east = '1'
     c                   eval      vvarl1_vare = w_eava
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      p_vare = vvvare
     c                   eval      p_anta = d_anta
      *
     c                   if        w_eaen <> *blank
EANx  * Sjekke om enheten finnes på varen
     c                   eval      vvenlr_vare = vvvare
     c                   eval      vvenlr_enhe = w_eaen
     c     vvenlr_key    chain     vvenlr
5.61 c                   if        %found(vvenlr) and
6.10 c                             vesaen > 0
     c                   eval      d_eane = w_eaen
     c                   else
5.63 c*                  move      d_eane        w_eanx
     c                   eval      d_eane = *blank
5.63 c                   eval      d_meld = 'Ikke gyldig salgsenhet'
5.63 c*                  call      'BX921R '
5.53 c*                  parm                    vvvare
5.53 c*                  parm                    w_eanx
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   leavesr
      *
     c                   endif
      *
      * Behandling dersom lengde <= 8
      *
     c                   if        w_leng <= 8
      *
     c                   eval      w_vare = %subst(c_null:1:8-w_leng) +
     c                                      p_scan
      *
     c                   eval      vvarl1_vare = w_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      p_vare = vvvare
     c                   leavesr
     c                   endif
      *
     c                   endif
      *
      * Behandling dersom lengde <= 14
      *
     c                   if        w_leng <= 14
      *
6.11 c                   eval      w_eanx = %subst(c_null:1:14-w_leng) +
     c                                      p_scan
      *
6.11 c                   movel     w_eanx        w_eann
6.11 c                   call      'VE710R  '
6.11 c                   parm                    w_firm
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_eava
6.11 c                   parm                    w_eaen
6.11 c                   parm                    w_east
6.11 c                   if        w_east = '1'
     c                   eval      vvarl1_vare = w_eava
5.6x c                   if        w_eaen <> *blank
EANx  * Sjekke om enheten finnes på varen
     c                   eval      vvenlr_vare = w_eava
     c                   eval      vvenlr_enhe = w_eaen
     c     vvenlr_key    chain     vvenlr
5.61 c                   if        %found(vvenlr) and
6.10 c                             vesaen > 0
     c                   eval      d_eane = w_eaen
     c                   else
5.63 c*                  move      d_eane        w_eanx
     c                   eval      d_eane = *blank
5.63 c                   eval      d_meld = 'Ikke gyldig salgsenhet'
5.63 c*                  call      'BX921R '
5.53 c*                  parm                    vvvare
5.53 c*                  parm                    w_eanx
     c                   endif
5.6x c                   endif
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      p_vare = vvvare
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Samlepost
      *
6.31  * sjekker om logistikk-vare
6.31 c                   eval      w_lv_vare = *blank
6.31 c/exec sql
6.31 c+ select vvlvnr
6.31 c+ into   :w_lv_vare
6.31 c+ from   vvlepf
6.31 c+ where  vvllva = :p_scan
6.31 c+ fetch first 1 rows only
6.31 c/end-exec
6.31 c                   if        w_lv_vare <> *blank
6.31 c                   movel     w_lv_vare     p_vare
6.31 c                   leavesr
6.31 c                   endif
     c                   eval      vvarl4_lvar = p_scan
     c     vvarl4_key    chain     vvarl4
     c                   if        %found
     c                   eval      p_vare = vvvare
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameter
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kund
     c                   parm                    p_kpro
     c                   parm                    p_scan
     c                   parm                    p_lety
     c                   parm                    p_lagr
     c                   parm                    p_avde
     c                   parm                    p_vare
     c                   parm                    p_vinf
     c                   parm                    p_einf
     c                   parm                    p_eann
     c                   parm                    p_anta
     c                   parm                    p_olet
     c                   parm                    p_okpr
     c                   parm                    p_oppr
     c                   parm                    p_sapr
     c                   parm                    p_kopr
     c                   parm                    p_rab1
     c                   parm                    p_rab2
     c                   parm                    p_kamp
     c                   parm                    p_alme
     c                   parm                    p_brty
     c                   parm                    p_brr1
     c                   parm                    p_brr2
     c                   parm                    p_kmva
      *
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på vare m/lev.varenummer
      *
     c     vvarl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl4_lvar
      *
      * Key for oppslag på vare-enhet
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
     c                   kfld                    vvenl2_saen
      *
     c     vvenl2_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
      *
EANx c     vvenlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenlr_vare
     c                   kfld                    vvenlr_enhe
6.12  *
6.12  * Butikk status register
6.12  *
6.12 c     bstsl1_key    klist
6.12 c                   kfld                    w_firm
      *
6.32 c     rkunl1_key    klist
6.32 c                   kfld                    w_firm
6.32 c                   kfld                    rkunl1_kund
      *
     c                   eval      d_eane = *blank
5.61 c                   eval      d_meld = *blank
      *
     c                   move      p_firm        w_firm
5.70 c                   eval      w_lage = p_lagr
     c                   eval      w_avde = p_avde
      *
      * hvis lager er 0 fra parameter, tas det evt. fra kunde
6.32 c                   if        w_lage = 0
6.32 c                   eval      rkunl1_kund = p_kund
6.32 c     rkunl1_key    chain     rkunl1
6.32 c                   if        %found(rkunl1)
6.32 c                   eval      w_lage = rklagr
6.32 c                   endif
6.32 c                   endif
5.70  *
5.70  * henter prisgruppe
5.70  *
5.70 c                   call      'VL712R   '
5.70 c                   parm                    w_firm
5.70 c                   parm                    w_lage
5.70 c                   parm                    w_avde
5.70 c                   parm                    w_prgr
      *
     c                   endsr
