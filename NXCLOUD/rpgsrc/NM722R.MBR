     H decedit('0.') datedit(*dmy-) option(*nodebugio)
     h DftActgrp(*No) Actgrp(*NEW)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : NM722R
      * Beskrivelse . . : Eksport av lagerinfo til Blueridge datahub
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       091221 bsa  Nytt program
8.01  *       201221 bsa  Justert til å skrive til db2, ikke json
8.02  *       130122 bsa  Feil håndtering av member.
8.03  *       280122 bsa  Utelater ekstra ordretyper. Ref Gisle 28.01.22
8.04  * K000  010222 bsa  Lokal leverandør på lager, skal overstyre
8.04  * K000              evnt hovedleverandør fra vare.
8.05  * K000  070222 bsa  Feil håndtering av leverandør/prisgiver.
8.06  * K000  080222 bsa  Ikke alle prisgivere har reskontronummer. Endrer logikk.
8.07  * K000  100222 bsa  Feil logikk i beregning av "i ordre passert" og "i ordre".
8.08  * K000  160222 bsa  Legger ut *loval i første dato, hvis ikke finner ordre.
8.09  * K000  210222 bsa  Endring av logikk rundt det å finne leverandør.
8.10  * K000  230222 bsa  Fortsetter med logikken, da det er endel "hull" i
8.10  * K000              grunndata rundt leverandører.
8.11  * K000  190922 bsa  Skal ikke ta med direkteleverte ordre. Rapporten skal kun
8.11  * K000              gjelde kjøp til lager. (Ref Jirasal NXS-14433)
8.12  * K000  200922 bsa  Ordrelinjer med leveringsdato mer enn 30 dager fram
8.12  * K000              i tid skal ikke være med.
8.13  * K000  181022 bsa  Innfører ny tabell for overstyring av leverandør
8.13  * K000              via RDS lager.
8.14  * K000  211222 bsa  Problemer med å sette tilbake verdier for leverandør
8.15  * K000  240223 bsa  Sjekk om ordretypen oppdaterer lager, hvis ikke skal
8.15  * K000              ikke antall være med.
8.16  * K000  210923 bsa  Må også sjekke ordretype når det er brudd på suffiks.
8.17  * K000  310524 bsa  "I Bestilling" skal beregnes på samme måte som  "i ordre"
8.18  * K000  270624 bsa  Ref 8.17. Må ta hensyn til ordretyper på samme måte som ved
8.18  * K000              beregning av i ordre.
      *
9.xx  * 9.xx  101221 bsa  Div test
9.01  * 9.xx  270522 bsa  Div ifbm manipulering av kjøreperioder. Kan
9.01  * 9.xx              være nødvendig hvis stopp i normale rutiner.
9.02  * K000  050424 bsa  Ordre med leveringstype 7, skal ikke med
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarlr    if   e           k disk    rename(vvarpfr:vvarlrr)
     fvlaglr    if   e           k disk    rename(vlagpfr:vlaglrr)
     fra10l1    if   e           k disk    rename(ra10pfr:ra10l1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
8.04 fjlevl3    if   e           k disk    rename(jlevpfr:jlevl3r)
8.04 frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     ffodtlx    if   e           k disk    rename(fodtpfr:fodtlxr)
8.17 flodtlx    if   e           k disk    rename(lodtpfr:lodtlxr)
8.17 flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
8.01 fbrstiu    uf a e           k disk    rename(brstst:brstiur)
8.09 fjvkhl1    if   e           k disk    rename(jvkhpfr:jvkhl1r)
     fafpspf    if   e           k disk    rename(afpspfr:afpspfr)
8.13 fvla2i1    if   e           k disk    rename(vla2st:vla2i1r)
      *
      * Interne tabeller
      *
     d t_lage          s              2  0 dim(99)
     d t_ltxt          s             35    dim(99)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_figr                934    939
     d l_filg                937    939
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d vvarlr_vare     s                   like(vvvare)
     d vlaglr_vare     s                   like(vlvare)
     d vlaglr_lage     s                   like(vllage)
     d ra10l1_jkod     s                   like(rajkod)
     d jlevl1_ldor     s                   like(jlldor)
8.04 d jlevl3_enum     s                   like(jlenum)
8.04 d rlevl1_levr     s                   like(rllevr)
     d vvenl1_vare     s                   like(vevare)
     d vvenl1_enhe     s                   like(veenhe)
     d fodtlx_lage     s                   like(fdlage)
     d fodtlx_vare     s                   like(fdvare)
     d fodtlx_edat     s                   like(fdedat)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d votyl1_otyp     s                   like(vaotyp)
8.01 d brstiu_tmem     s                   like(bstmem)
8.01 d brstiu_tvar     s                   like(bstvar)
8.01 d brstiu_tlag     s                   like(bstlag)
8.09 d jvkhl1_kvnr     s                   like(jvkvnr)
8.13 d vla2i1_2var     s                   like(vl2var)
8.13 d vla2i1_2lag     s                   like(vl2lag)
8.17 d lodtlx_lage     s                   like(ldlage)
8.17 d lodtlx_vare     s                   like(ldvare)
8.17 d lodtlx_edat     s                   like(ldedat)
8.17 d lohel1_numm     s                   like(lonumm)
8.17 d lohel1_suff     s                   like(losuff)
      *
      * Definering av datastrukturer
      *
     d                 ds
     d d_timstp                      27
     d  d_dato                       10    overlay(d_timstp:1)
     d  d_time                       15    overlay(d_timstp:12)
      *
     d                 ds
     d d_timstp1                     27
     d  d_dato1                      10    overlay(d_timstp1:1)
     d  d_time1                      15    overlay(d_timstp1:12)
      *
      * Definering av parametere
      *
     d p_memb          s             10
      *
      * Definering av variable
      *
     d w_kode          s              1
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
     d w_numm          s              8  0
     d w_size          s              8  0
     d w_mal           s            200
     d w_path          s            200
     d w_ldor          s              6  0
     d w_ltxt          s             30
     d w_lageid        s              5
     d w_lgtx          s             30
     d w_item          s             15
     d w_itemname      s            100
     d w_behl          s             11  3
     d w_bbes          s             11  3
     d w_oord          s             11  3
     d w_odue          s             11  3
     d w_firm          s                   like(vvfirm)
     d w_created       s             27
     d w_ingested      s             27
     d w_memb          s             10
     d w_onum          s              8  0
     d w_osuf          s              2  0
8.17 d w_bnum          s              8  0
8.17 d w_bsuf          s              2  0
     d w_omrl          s                   like(veomre)
     d w_omrs          s                   like(veomre)
     d w_anta          s                   like(fdanta)
     d w_event         s             15
8.09 d w_valg          s              1  0
8.09 d w_jdor          s              6  0
      *
8.04 d s_ldor          s              6  0
8.04 d s_ltxt          s             30
      *
     d x               s              2  0
      *
     d b_ordr          s              1    inz(*off)
8.17 d b_best          s              1    inz(*off)
8.14 d b_vla2          s              1    inz(*off)
      *
     d IFS_path        s            256a   Varying
     d IFS_Output      s            256a   Varying
     d IFS_Output1     s            256a   Varying
      *
      * Felter som legges ut i json rapporten
      *
     d*w_tmstp         s               z
     d w_tmstp         s             20
     d w_tmptime       s               t
8.12 d w_ldat          s               d   datfmt(*iso)
     d w_odat          s               d   datfmt(*iso)
8.07 d w_odate         s               d
8.17 d w_bdate         s               d
     d w_date          s               d   datfmt(*iso)
     d w_date_1        s               d   datfmt(*iso)
     d w_time          s               t
     d w_timestp       s               z
     d w_timestp1      s               z
      *
     c/Exec SQL
     c+  Set Option DatFmt=*ISO
     c/End-Exec
      *
      *-----------------------------------------------------------------*
      * Hovedprogram:                                                   *
      *   Skriver lagerbeholdning til json file.                        *
      *                                                                 *
      *                                                                 *
      *-----------------------------------------------------------------*
      *
      * Henter inn løpenummer teller
      *
8.01 c                   if        p_memb = *blanks
     c                   eval      w_kode = '0'
     c                   eval      w_fell = 'NXTKLI'
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
     c                   eval      w_numm = *zero
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
8.01 c                   eval      w_memb = 'AA' + %char(w_numm)
8.01 c                   eval      brstiu_tmem = w_memb
8.02 c                   eval      p_memb = w_memb
      *
8.01 c                   else
8.01 c                   eval      brstiu_tmem = p_memb
8.01 c                   endif
      *
      * Nullstiller teller for antall varer funnet
      *
     c                   eval      w_size = 0
      *
      * Ta vare på alle lagernummer og tekster, så slipper vi så mange oppslag
      *
     c                   eval      x = 0
     c                   eval      ra10l1_jkod = 0
     c     ra10l1_key    setll     ra10l1
     c     ra10l1_key2   reade     ra10l1
     c                   dow       not  %eof(ra10l1)
     c                   eval      x = x + 1
     c                   eval      t_lage(x) = rajkod
     c                   eval      t_ltxt(x) = rajtxt
     c     ra10l1_key2   reade     ra10l1
     c                   enddo
      *
      * Så begynner vi lese alle varer, NB Forutsetter at varen er L/S på vare
      *
     c                   eval      vvarlr_vare = *blanks
     c     vvarlr_key    setll     vvarlr
     c     vvarlr_key2   reade     vvarlr
     c                   dow       not %eof
      *
      * Kun varetype S/L fra varenummer (S varer kan være L på enkelte lagre)
      *
     c                   if        vvvtyp = 'S' or
     c                             vvvtyp = 'L'
      *
8.01 c*                  time                    w_timestp1
8.01 c*                  eval      d_timstp1 = %char(w_timestp1)
8.01 c*                  eval      w_ingested = d_dato1 + 'T' +
8.01 c*                            d_time1 + 'Z'
8.01 c*                  eval      w_event = %char(d_time1)
      *
9.xx c**                 if        w_size = 100
9.xx c**                 goto      test_slutt
9.xx c**                 endif
      *
      * Hent inn info som er felles for varen
      *
     c                   eval      w_item = *blanks
     c                   eval      w_itemname = *blanks
     c                   eval      w_item = vvvare
     c                   eval      w_itemname = %trim(vvtek1) + ' ' +
     c                             %trim(vvtek2)
      *
      * Hent info fra leverandør
      *
8.09 c                   eval      w_jdor = 0
     c                   eval      w_ldor = 0
     c                   eval      w_ltxt = *blanks
8.04 c                   eval      s_ldor = 0
8.04 c                   eval      s_ltxt = *blanks
      *
8.06  * Hente leverandør
      *
8.09 c                   exsr      hent_levr
      *
      * Da henter vi info fra lager
      *
     c                   eval      vlaglr_vare = vvvare
     c                   eval      vlaglr_lage = 0
     c     vlaglr_key    setll     vlaglr
     c     vlaglr_key2   reade     vlaglr
     c                   dow       not %eof
      *
9.xx c**                 if        vllage = 31 and
9.xx c**                           vlvare = '11302619'
      *
      * Kun varetype L
      *
     c                   if        vlvtyp = 'L'
      *
      * Hvis beholdning er 0, og varen er utgått mer enn 1 dag før dd,
      * sendes den ikke.
      *
     c                   if        vlbehl <= 0 and
     c                             vvnuda <> *loval and
     c                             vvnuda < w_date_1
     c                   goto      ny_les
     c                   endif
      *
     c                   eval      w_lageid = *blanks
     c                   eval      w_lgtx = *blanks
      *
      * Finn info om lager. Oppslag i intern tabell er mye raskere
      *
     c                   eval      x=1
     c     vllage        lookup    t_lage(x)                              90
     c                   if        *in90 = *on
     c                   eval      w_lageid = l_filg + %char(t_lage(x))
     c                   eval      w_lgtx = %trim(t_ltxt(x))
     c                   endif
      *
      * Hent info om beholdninger
      *
     c                   exsr      lag_info
      *
      * Legg ut info
      *
8.01 c                   exsr      wrt_stock
      *
     c     ny_les        tag
      *
     c                   endif
      *
9.xx c**                 endif
      *
     c     vlaglr_key2   reade     vlaglr
     c                   enddo
      *
     c                   endif
      *
     c     vvarlr_key2   reade     vvarlr
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent info om beholdninger osv
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lag_info      begsr
      *
     c                   eval      w_behl = 0
     c                   eval      w_bbes = 0
     c                   eval      w_oord = 0
     c                   eval      w_odue = 0
     c                   eval      w_odat = *hival
     c                   eval      b_ordr = *off
      *
     c                   eval      w_behl = vlbehl                              Beholdning
8.17 c**                 eval      w_bbes = vlbbes                              I bestilling
8.07 c**                 eval      w_oord = vloord                              I ordre
      *
      * Hopp over, hvis det ikke er noe i ordre
      *
     c                   if        vloord <> 0
      *
      * Da henter vi info fra ordre
      *
     c                   eval      w_onum = 0
     c                   eval      w_osuf = 0
      *
     c                   eval      fodtlx_lage = vllage
     c                   eval      fodtlx_vare = vvvare
     c                   eval      fodtlx_edat = *loval
     c     fodtlx_key    setll     fodtlx
     c     fodtlx_key2   reade     fodtlx
     c                   dow       not %eof
      *
8.07 c                   eval      w_odate = *loval
      *
8.16 c                   if        fdnumm <> w_onum or
8.16 c                             fdnumm = w_onum and
8.16 c                             fdsuff <> w_osuf
     c                   eval      b_ordr = *off
     c                   eval      fohel1_numm = fdnumm
     c                   eval      fohel1_suff = fdsuff
     c     fohel1_key    chain     fohel1
     c                   if        %found
      * Sjekk at ordretypen er av type "ordre"
     c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1
8.15 c                   if        %found
8.15 c                   if        vaoakk = 1
     c                   eval      b_ordr = *on
8.15 c                   endif
8.13 c                   if        vaolag = 0
8.13 c                   eval      b_ordr = *off
8.13 c                   endif
     c                   endif
     c                   endif
     c                   eval      w_onum = fdnumm
     c                   eval      w_osuf = fdsuff
8.03  *
8.03  * Ordretype BN og RO skal ikke telle med
8.03  *
8.03 c                   if        footyp = 'BN' or
8.03 c                             footyp = 'RO'
8.03 c                   eval      b_ordr = *off
8.03 c                   endif
8.11  *
8.11  * Direkteleverte ordre skal ikke tas med.
9.02  * Ordre med leveringstype 7, skal ikke med (mellomlagring)
8.11  *
8.11 c                   if        b_ordr = *on
9.02 c*                  if        folety = 1
9.02 c                   if        folety = 1 or
9.02 c                             folety = 7
8.11 c                   eval      b_ordr = *off
8.11 c                   endif
8.11 c                   endif
8.12  *
8.12  * Tar ikke med ordre med leveringsdato mer enn 30 dager fram i tid.
8.12  *
8.12 c                   if        b_ordr = *on
8.12 c                   if        fdldat <> *loval and
8.12 c                             fdldat > w_ldat
8.12 c                   eval      b_ordr = *off
8.12 c                   else
8.12 c                   if        foldat > w_ldat
8.12 c                   eval      b_ordr = *off
8.12 c                   endif
8.12 c                   endif
8.12 c                   endif
8.12  *
     c                   endif
      *
      * Ordren skal telle med for å beregne ordre som ikke er levert
      *
     c                   if        b_ordr = *on
      * Finn "laveste" leveringsdato, hent først på linja. Ta vare på verdi til neste ordre
     c                   if        fdldat <> *loval and
     c                             fdldat < w_odat
     c                   eval      w_odat = fdldat
     c                   endif
      * Sjekk også på hodet, da det er sjeldent levdato settes på linje
     c                   if        foldat <> *loval and
     c                             foldat < w_odat
     c                   eval      w_odat = foldat
     c                   endif
8.07  * Finn leveringsdato for ordrelinja
8.07 c                   if        fdldat <> *loval
8.07 c                   eval      w_odate = fdldat
8.07 c                   endif
8.07  * Hvis ikke leveringsdato er satt poå linja, bruk den som ligger på hodet
8.07 c                   if        w_odate = *loval
8.07 c                   eval      w_odate = foldat
8.07 c                   endif
8.07  *
8.07  * Sjekk mot dagensdato. Hvis leveringsdato < dd - "i ordre passert"
8.07  * om levering fram i tid - "i ordre"
8.07  *
8.07 c                   if        w_odate < w_date
8.07  *
8.07  * Sjekk at antallet kommer i lagerenhet, hvis diff må det omregnes
8.07  *
     c                   if        vvenhl = fdenh1
     c                   eval      w_odue = w_odue + fdanta
     c                   else
     c                   exsr      omr_enh
8.07 c                   eval      w_odue = w_odue + w_anta
     c                   endif
      *
8.07 c                   else
8.07  *
8.07  * Sjekk at antallet kommer i lagerenhet, hvis diff må det omregnes
8.07  *
8.07 c                   if        vvenhl = fdenh1
8.07 c                   eval      w_oord = w_oord + fdanta
8.07 c                   else
8.07 c                   exsr      omr_enh
8.07 c                   eval      w_oord = w_oord + w_anta
8.07 c                   endif
8.07  *
8.07 c                   endif
      *
     c                   endif
      *
     c     fodtlx_key2   reade     fodtlx
     c                   enddo
      *
     c                   endif
8.17  *
8.17  * Hopp over, hvis det ikke er noe i bestilling
8.17  *
8.17 c                   if        vlbbes <> 0
8.17  *
8.17  * Da henter vi info fra bestillingen
8.17  *
8.17 c                   eval      w_bnum = 0
8.17 c                   eval      w_bsuf = 0
8.17  *
8.17 c                   eval      lodtlx_lage = vllage
8.17 c                   eval      lodtlx_vare = vvvare
8.17 c                   eval      lodtlx_edat = *loval
8.17 c     lodtlx_key    setll     lodtlx
8.17 c     lodtlx_key2   reade     lodtlx
8.17 c                   dow       not %eof
8.17  *
8.17 c                   eval      w_bdate = *loval
8.17  *
8.17 c                   if        ldnumm <> w_bnum or
8.17 c                             ldnumm = w_bnum and
8.17 c                             ldsuff <> w_bsuf
8.17 c                   eval      b_best = *off
8.17 c                   eval      lohel1_numm = ldnumm
8.17 c                   eval      lohel1_suff = ldsuff
8.17 c     lohel1_key    chain     lohel1
8.17 c                   if        %found
8.17  * Sjekk at ordretypen er av type "bestilling".
8.17 c                   eval      votyl1_otyp = lootyp
8.17 c     votyl1_key    chain     votyl1
8.17 c                   if        %found
8.17 c                   if        vaoakk = 1
8.17 c                   eval      b_best = *on
8.17 c                   endif
8.17 c                   if        vaolag = 0
8.17 c                   eval      b_best = *off
8.17 c                   endif
8.18  *
8.18  * Ordretype BN og RO skal ikke telle med
8.18  *
8.18 c                   if        lootyp = 'BN' or
8.18 c                             lootyp = 'RO'
8.18 c                   eval      b_best = *off
8.18 c                   endif
8.17  *
8.17 c                   endif
8.17 c                   endif
8.17  *
8.17 c                   eval      w_bnum = ldnumm
8.17 c                   eval      w_bsuf = ldsuff
8.17  *
8.17  * Er bestilling koblet til direkteleverte ordre skal ikke tas med.
8.17  * Ordre med leveringstype 7, skal ikke med (mellomlagring)
8.17  *
8.17 c                   if        loornr <> 0
8.17 c                   eval      fohel1_numm = loornr
8.17 c                   eval      fohel1_suff = loorsu
8.17 c     fohel1_key    chain     fohel1
8.17 c                   if        %found
8.17 c                   if        folety = 1 or
8.17 c                             folety = 7
8.17 c                   eval      b_best = *off
8.17 c                   endif
8.17 c                   endif
8.17 c                   endif
8.17  *
8.17  * Tar ikke med ordre med leveringsdato mer enn 30 dager fram i tid.
8.17  * Usikker på om dette også skal gjelde her ??
8.17  *
8.17 c*                  if        b_ordr = *on
8.17 c*                  if        fdldat <> *loval and
8.17 c*                            fdldat > w_ldat
8.17 c*                  eval      b_ordr = *off
8.17 c*                  else
8.17 c*                  if        foldat > w_ldat
8.17 c*                  eval      b_ordr = *off
8.17 c*                  endif
8.17 c*                  endif
8.17 c*                  endif
8.17  *
8.17 c                   endif
8.17  *
8.17  * Bestillingen skal telle med
8.17  *
8.17 c                   if        b_best = *on
8.17  *
8.17  * Sjekk at antallet kommer i lagerenhet, hvis diff må det omregnes
8.17  *
8.17 c                   if        vvenhl = ldenh1
8.17 c                   eval      w_bbes = w_bbes + ldanta
8.17 c                   else
8.17 c                   exsr      omr_enh_best
8.17 c                   eval      w_bbes = w_bbes + w_anta
8.17 c                   endif
8.17  *
8.17 c                   endif
8.17  *
8.17 c     lodtlx_key2   reade     lodtlx
8.17 c                   enddo
8.17  *
8.17 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Fyll innholdet i json fila
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
8.01 c     wrt_stock     begsr
8.04  *
8.04 c                   if        s_ldor <> 0 and
8.04 c                             vlbldo <> 0 and
8.04 c                             s_ldor <> vlbldo
8.06  * Slå opp mot leveradørtabell
8.05 c                   eval      rlevl1_levr = vlbldo
8.05 c     rlevl1_key    chain     rlevl1
8.05 c                   if        %found
8.05 c                   eval      w_ldor = vlbldo
8.05 c                   eval      w_ltxt = rlnavn
8.06 c                   else
8.04 c                   eval      jlevl3_enum = vlbldo
8.04 c     jlevl3_key    chain     jlevl3
8.04 c                   if        %found
8.05 c**                 eval      w_ldor = jlldor
8.05 c                   eval      w_ldor = vlbldo
8.05 c                   eval      w_ltxt = jlnavn
8.05 c                   endif
8.04 c                   endif
8.04  *
8.06 c                   endif
8.04  *
     c                   eval      w_size = w_size + 1
8.13  *
8.13  * For ikke ødelegge øvrig logikk, legges siste sjekk mot RDS
8.13  * leverandør her.
8.13  *
8.14 c                   eval      b_vla2 = *off
8.13 c                   eval      vla2i1_2var = w_item
8.13 c                   eval      vla2i1_2lag = vllage
8.13 c     vla2i1_key    chain     vla2i1
8.13 c                   if        %found and
8.13 c                             vl2gin = 1
8.13 c                   eval      rlevl1_levr = vl2rle
8.13 c     rlevl1_key    chain     rlevl1
8.13 c                   if        %found
8.14 c                   eval      b_vla2 = *on
8.13 c                   eval      w_ldor = vl2rle
8.13 c                   eval      w_ltxt = rlnavn
8.13 c                   endif
8.13 c                   endif
8.01  * Så legger vi ut info
8.01  *
8.01 c                   eval      brstiu_tvar = w_item
8.01 c                   eval      brstiu_tlag = vllage
8.01 c     brstiu_key    chain     brstiu
8.01 c                   if        not %found
8.01 c                   clear                   brstiur
8.01 c                   eval      bstmem = brstiu_tmem
8.01 c                   eval      bstlin = w_size
8.01 c                   eval      bstvar = brstiu_tvar
8.01 c                   eval      bstlag = brstiu_tlag
8.01  *
8.01 c                   eval      bstltx = %trim(w_lgtx)
8.01 c                   eval      bstlid = %trim(w_lageid)
8.01 c                   eval      bstldo = w_ldor
8.01 c                   eval      bstldt = %trim(w_ltxt)
8.01 c                   eval      bstvtx = %trim(w_itemname)
8.01  *
8.01 c                   eval      bstbeh = w_behl
8.01 c                   eval      bstibe = w_bbes
8.08 c                   if        w_odat <> *hival
8.01 c                   eval      bstfld = w_odat
8.08 c                   else
8.08 c                   eval      bstfld = *loval
8.08 c                   endif
8.01 c                   eval      bstiop = w_odue
8.01 c                   eval      bstior = w_oord
8.01  *
8.01 c                   time                    bstoda
8.01 c                   time                    bstoti
8.01 c                   eval      bstous = l_user
8.01 c                   write     brstiur
8.01 c                   endif
8.14  *
8.14  * Sett verdier tilbake
8.14  *
8.04 c                   if        s_ldor <> 0 and
8.04 c                             vlbldo <> 0 and
8.04 c                             s_ldor <> vlbldo
8.04 c                   eval      w_ldor = s_ldor
8.04 c                   eval      w_ltxt = s_ltxt
8.04 c                   endif
8.14  * Kan også være overstyrt via VLA2ST
8.14 c                   if        b_vla2 = *on and
8.14 c                             s_ldor <> w_ldor
8.14 c                   eval      w_ldor = s_ldor
8.14 c                   eval      w_ltxt = s_ltxt
8.14 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for omregning av antall i forhold til lagerenhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     omr_enh       begsr
      *
     c                   eval      w_anta = fdanta
      *
      * Finner omregningsfaktor volum for lagerenhet og for solgt enhet
      * Antall regnes om etter forholdet mellom disse
      *
     c                   eval      vvenl1_vare = vvvare
     c     vvenl1_key    setll     vvenl1
     c     vvenl1_key    reade     vvenl1
     c                   dow       not %eof
     c                   if        fdenh1 = veenhe
     c                   eval      w_omrs = veomre
     c                   endif
     c                   if        vvenhl = veenhe
     c                   eval      w_omrl = veomre
     c                   endif
     c     vvenl1_key    reade     vvenl1
     c                   enddo
      *
     c                   if        w_omrs = 0
     c                   eval      w_omrs = 1
     c                   endif
      *
     c                   if        w_omrl = 0
     c                   eval      w_omrl = 1
     c                   endif
      *
     c                   eval      w_anta = w_anta * w_omrs / w_omrl
8.07 c**                 eval      w_odue = w_odue + w_anta
      *
     c                   endsr
      *
8.17  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.17  * Subrutine for omregning av antall i forhold til lagerenhet
8.17  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.17  *
8.17 c     omr_enh_best  begsr
8.17  *
8.17 c                   eval      w_anta = ldanta
8.17  *
8.17  * Finner omregningsfaktor volum for lagerenhet og for bestilt antall
8.17  * Antall regnes om etter forholdet mellom disse
8.17  *
8.17 c                   eval      vvenl1_vare = vvvare
8.17 c     vvenl1_key    setll     vvenl1
8.17 c     vvenl1_key    reade     vvenl1
8.17 c                   dow       not %eof
8.17 c                   if        ldenh1 = veenhe
8.17 c                   eval      w_omrs = veomre
8.17 c                   endif
8.17 c                   if        vvenhl = veenhe
8.17 c                   eval      w_omrl = veomre
8.17 c                   endif
8.17 c     vvenl1_key    reade     vvenl1
8.17 c                   enddo
8.17  *
8.17 c                   if        w_omrs = 0
8.17 c                   eval      w_omrs = 1
8.17 c                   endif
8.17  *
8.17 c                   if        w_omrl = 0
8.17 c                   eval      w_omrl = 1
8.17 c                   endif
8.17  *
8.17 c                   eval      w_anta = w_anta * w_omrs / w_omrl
8.17  *
8.17 c                   endsr
8.17  *
8.09  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.09  * Hent leverandør
8.09  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.09  *
8.09 c     hent_levr     begsr
8.09  *
8.09  * 1. Hvis VVNLEV er 0, bruk VVLDOR. Vi forusetter at dette ikke gjelder
8.09  *    logistikkvarer.
8.09  * 2. Hvis logistikkvare, i JVKHPF, hent JVKLDO og slå opp i JLEVPF. Hvis
8.09  *    reskontronummer er 0, eller ikke treff i RLEV, bruk VVLDOR.
8.09  * 3. Hent vareeier fra JVARPF, hent reskontronummer fra JLEVPF. Hvis
8.09  *    reskontronummer er 0 eller ikke treff i RLEV, bruk VVLDOR.
8.09  * Ref dialog med Jørn
8.09  *
8.09 c                   eval      w_valg = 0
8.09  * Alt 1
8.09 c                   if        vvnlev =  0 and
8.09 c                             vvldor <> 0
8.09 c                   eval      w_valg = 1
8.09 c                   endif
8.09  * Alt 2
8.10 c                   if        w_valg = 0
8.09 c                   eval      jvkhl1_kvnr = vvvare
8.09 c     jvkhl1_key    chain     jvkhl1
8.09 c                   if        %found
8.09 c                   eval      w_valg = 2
8.09 c                   endif
8.10 c                   endif
8.09  * Alt 3
8.10 c                   if        w_valg = 0
8.09 c                   if        vvnlev <> 0
8.10 c                   eval      jlevl1_ldor = vvnlev
8.10 c     jlevl1_key    chain     jlevl1
8.10 c                   if        %found and
8.10 c                             jlenum <> 0
8.09 c                   eval      w_valg = 3
8.10 c                   else
8.10 c                   eval      w_valg = 1
8.10 c                   endif
8.09 c                   endif
8.10 c                   endif
8.09  *
8.09 c                   if        w_valg = 1
8.09 c                   eval      w_ldor = vvldor
8.09 c                   exsr      hent_rlev
8.09 c                   eval      s_ldor = vvldor
8.09 c                   eval      s_ltxt = rlnavn
8.09 c                   elseif    w_valg = 2
8.09 c                   eval      w_jdor = jvkldo
8.09 c                   exsr      hent_jlev
8.09 c                   elseif    w_valg = 3
8.09 c                   eval      w_jdor = vvnlev
8.09 c                   exsr      hent_jlev
8.09 c                   else
8.09 c                   endif
8.09  *
8.09 c                   endsr
8.09  *
8.09  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.09  * Henter leverandørinformasjon
8.09  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.09  *
8.09 c     hent_rlev     begsr
8.09  *
8.09  * Slå opp mot leveradør for å hente inn land/valutakode
8.09  *
8.09 c                   eval      rlevl1_levr = w_ldor
8.09 c     rlevl1_key    chain     rlevl1
8.09 c                   if        %found
8.09 c                   eval      s_ldor = vvldor
8.09 c                   eval      s_ltxt = rlnavn
8.09 c                   eval      w_ltxt = rlnavn
8.09 c                   endif
8.09  *
8.09 c                   endsr
8.09  *
8.09  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.09  * Henter leverandørinformasjon via JLEVPF - NOBB vareeier
8.09  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.09  *
8.09 c     hent_jlev     begsr
8.09  *
8.09 c                   eval      jlevl1_ldor = w_jdor
8.09 c     jlevl1_key    chain     jlevl1
8.09 c                   if        %found
8.09 c                   eval      w_ldor = jlenum
8.09 c                   eval      s_ldor = jlenum
8.09 c                   eval      w_ltxt = jlnavn
8.09 c                   eval      s_ltxt = jlnavn
8.09 c                   endif
8.09  *
8.09 c                   endsr
8.09  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine  - Brukes ved mislykket web-service kall
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c**                 eval      p_link = 'Blueridge eksport feilet'
     c                   goto      avslutt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Henter inn parameter
      *
     c     *entry        plist
     c                   parm                    p_memb
      *
      * Key for oppslag på vare
      *
     c     vvarlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlr_vare
      *
      * Key for oppslag på vare
      *
     c     vvarlr_key2   klist
     c                   kfld                    w_firm
      *
      * Key for les på lagerregister
      *
     c     vlaglr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglr_vare
     c                   kfld                    vlaglr_lage
      *
      * Key for les på lagerregister
      *
     c     vlaglr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglr_vare
      *
      * Key for info fra lagerregister
      *
     c     ra10l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra10l1_jkod
      *
      * Key for info fra lagerregister
      *
     c     ra10l1_key2   klist
     c                   kfld                    w_firm
      *
      * Key for info fra leverandørregister (prisgriver)
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for info fra ordrelinje
      *
     c     fodtlx_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlx_lage
     c                   kfld                    fodtlx_vare
     c                   kfld                    fodtlx_edat
      *
      * Key for info fra ordrelinje
      *
     c     fodtlx_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlx_lage
     c                   kfld                    fodtlx_vare
      *
      * Key for info fra ordrehode
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for les på ordretype
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for les på vare-enhet-register
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
8.01  *
8.01  * Key for info fra blueridge export - items
8.01  *
8.01 c     brstiu_key    klist
8.01 c                   kfld                    brstiu_tmem
8.01 c                   kfld                    brstiu_tvar
8.01 c                   kfld                    brstiu_tlag
8.04  *
8.04  * Key for info fra leverandørregister (prisgriver)
8.04  *
8.04 c     jlevl3_key    klist
8.04 c                   kfld                    jlevl3_enum
8.05  *
8.05  * Key for info fra leverandør
8.05  *
8.05 c     rlevl1_key    klist
8.05 c                   kfld                    w_firm
8.05 c                   kfld                    rlevl1_levr
8.09  *
8.09  * Key for info fra logistikkvarenummer
8.09  *
8.09 c     jvkhl1_key    klist
8.09 c                   kfld                    w_firm
8.09 c                   kfld                    jvkhl1_kvnr
8.13  *
8.13  * Key for les på lagerregister - tilleggstabell
8.13  *
8.13 c     vla2i1_key    klist
8.13 c                   kfld                    w_firm
8.13 c                   kfld                    vla2i1_2var
8.13 c                   kfld                    vla2i1_2lag
8.17  *
8.17  * Key for info fra bestillingslinjer
8.17  *
8.17 c     lodtlx_key    klist
8.17 c                   kfld                    w_firm
8.17 c                   kfld                    lodtlx_lage
8.17 c                   kfld                    lodtlx_vare
8.17 c                   kfld                    lodtlx_edat
8.17  *
8.17  * Key for info fra bestillingslinjer
8.17  *
8.17 c     lodtlx_key2   klist
8.17 c                   kfld                    w_firm
8.17 c                   kfld                    lodtlx_lage
8.17 c                   kfld                    lodtlx_vare
8.17  *
8.17  * Key for info fra bestilling
8.17  *
8.17 c     lohel1_key    klist
8.17 c                   kfld                    w_firm
8.17 c                   kfld                    lohel1_numm
8.17 c                   kfld                    lohel1_suff
      *
     c**                 eval      p_memb = *blanks
     c                   eval      w_firm = l_firm
      *
     c                   time                    w_date_1
     c                   subdur    1:*d          w_date_1
9.01 c**                 subdur    2:*d          w_date_1
     c                   time                    w_date
     c                   time                    w_time
     c                   time                    w_timestp
8.12 c                   time                    w_ldat
8.12 c                   adddur    30:*d         w_ldat
      *
     c*                  eval      w_tmstp = %char(w_date)+'T'+
     c*                            %char(w_time)+'Z'
      *
     c*                  eval      d_timstp = %char(w_timestp)
     c*                  eval      w_created = d_dato + 'T' +
     c*                            d_time + 'Z'
      *
     c                   endsr
      *
