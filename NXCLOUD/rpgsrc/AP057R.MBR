     H decedit('0.') datedit(*dmy-) option(*nodebugio)
     h DftActgrp(*No) Actgrp(*NEW) BndDir('CGIDEV2/CGIDEV2')
      /copy hspecsbnd
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : AP057R
      * Beskrivelse . . : Oppslag mot Lognet transporttjeneste
      *                   Hvis gammel forsendelsesmåte har transportintegrasjon,
      *                   og ny ikke har, sendes sletteflagg.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       050123 bsa  Nytt program
8.01  * K000  170123 bsa  Bruker annen logikk for å finne antall. Må ta
8.01  * K000              høyde for at det bestilles F8 varer.
      *
9.xx  * K000  271124 bsa  Ligger her pga utvidet VOT1PF, Fjernes etter frislipp
9.xx  * K000              av faktura som blir kreditnota og motsatt.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fra10l1    if   e           k disk    rename(ra10pfr:ra10l1r)
     ffloglr    if   e           k disk    rename(flogpfr:floglrr)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffohelu    uf   e           k disk    rename(fohepfr:fohelur)
     ffodtlr    if   e           k disk    rename(fodtpfr:fodtlrr)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fftrpi2    if   e           k disk    rename(ftrpst:ftrpi2r)
     fftrpiu    uf a e           k disk    rename(ftrpst:ftrpiur)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvasl1    if   e           k disk    rename(vvaspfr:vvasl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fjvpkl2    if   e           k disk    rename(jvpkpfr:jvpkl2r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fftrli1    if   e           k disk    rename(ftrlst:ftrli1r)
     fvot1l1    if   e           k disk    rename(vot1pfr:vot1l1r)
     fraf2l1    if   e           k disk    rename(raf2pfr:raf2l1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
8.01 fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
      *
      *
      * Definering av tabeller
      *
     d tr_item         s              8    dim(445)
      *
      * Definering av LDA
      *
     d                uds
     d l_user                911    920
     d l_figr                934    939
     d l_filg                937    939
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av parametere
      *
     d p_firm          s              3  0
     d p_numm          s              8  0
     d p_suff          s              2  0
     d p_gmat          s              2  0
     d p_toid          s             35
     d p_2fid          s             35
     d p_stat          s              5
      *
      * Definering av variabler i nøkler
      *
     d ftrli1_llag     s                   like(ftllag)
     d ftrpiu_roid     s                   like(ftroid)
     d ftrpi2_rnum     s                   like(ftrnum)
     d ftrpi2_rsuf     s                   like(ftrsuf)
     d rkunl1_kund     s                   like(rkkund)
     d ra10l1_jkod     s                   like(rajkod)
     d fodtlr_line     s                   like(fdline)
     d floglr_aarr     s                   like(fuaarr)
     d floglr_numm     s                   like(funumm)
     d floglr_suff     s                   like(fusuff)
     d vvarl1_vare     s                   like(vvvare)
     d vvasl1_vare     s                   like(vvvare)
     d vvenl1_vare     s                   like(vevare)
     d vvenl1_enhe     s                   like(veenhe)
     d jvpkl2_vare     s                   like(jwvare)
     d jvpkl2_ldor     s                   like(jwldor)
     d jvpkl2_enhe     s                   like(jwenhe)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d vot1l1_otyp     s                   like(va1typ)
     d raf2l1_2kod     s                   like(ra2kod)
     d votyl1_otyp     s                   like(vaotyp)
     d ra09l1_ikod     s                   like(raikod)
8.01 d jvarl1_vare     s                   like(jvvare)
      *
      * Definering av variable
      *
     d w_token         s           4000
     d w_tokeut        s           4014
     d w_indata        s           2000
     d w_url           s            200
     d w_url2          s            200
     d w_reqf          s            100
     d w_rspf          s            100
     d w_usrn          s             20
     d w_ident         s            221
     d w_reqt          s              6
     d w_enco          s             20
     d w_stat          s              1  0
     d w_kode          s              1
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
     d w_numm          s              8  0
     d w_size          s              8  0
     d w_start         s              4  0
     d w_slutt         s              4  0
     d w_lengd         s              4  0
     d w_coref         s             50
     d w_trid          s             35
     d w_toid          s             35
     d w_oref          s             15
     d w_odat          s               d   datfmt(*iso)
     d w_date          s               d   datfmt(*iso)
     d w_mal           s            200
     d w_ponr          s             10
     d w_city          s             35
     d w_lponr         s             10
     d w_lcity         s             35
     d w_kcity         s             35
     d w_len           s              3  0
     d w_totw          s              9  3
     d w_totv          s              9  3
     d w_akol          s              4  0
     d w_akola         s              3
     d w_avvi          s              1
     d w_disp          s              5
     d w_line          s              4  0
     d w_vekt          s             11  2
     d w_vektv         s             11  2
     d w_volu          s              9  3
     d w_voluv         s              9  3
     d w_lv_nobb       s                   like(vvnobb)
     d w_lv_modn       s                   like(vvnmod)
     d w_lv_lldo       s                   like(vvldor)
     d w_lv_llva       s                   like(vvlvar)
     d w_utda          s                   like(vvudat)
     d w_vtyp          s                   like(vvvtyp)
     d w_dref          s             50
     d w_mobi          s             15
     d w_aarr          s              4  0
     d w_date_cnv      s             18
     d w_tek1          s             50
     d w_noname        s             50
     d w_noadr1        s             50
     d w_noadr2        s             50
     d w_noponr        s             50
     d w_nocity        s             50
     d w_nename        s             50
     d w_neadr1        s             50
     d w_neadr2        s             50
     d w_neponr        s             50
     d w_necity        s             50
     d w_frname        s             50
     d w_fradr1        s             50
     d w_fradr2        s             50
     d w_frponr        s             50
     d w_frcity        s             50
     d w_toname        s             50
     d w_toadr1        s             50
     d w_toadr2        s             50
     d w_toponr        s             50
     d w_tocity        s             50
     d w_picktxt       s           1024
     d w_drivtxt       s           1024
     d w_string        s             70
     d w_tekst         s            256
     d w_status        s              1
     d w_message       s             50
     d w_tritem        s           4000
     d w_ngn_key       s            256
     d w_posi          s              5  0
     d w_item          s              8
     d w_trpo          s              5
     d w_frcont        s             50
     d w_fremai        s             50
     d w_frphon        s             15
     d w_toemai        s             50
     d w_ctrp          s              5
      *
     d x               s              4  0
      *
     d b_inlr          s              1    inz(*off)
     d b_snus          s              1    inz(*off)
     d b_tritem        s              1    inz(*off)
      *
     d u_konv          s              1    inz(*on)
     d u_flat          s              1    inz(*on)
      *
     d w_kurl          s            200
     d w_kusr          s            200
     d w_pass          s            200
     d w_prox          s            200
     d IFS_Output      s            256a   Varying
     d IFS_Output1     s            256a   Varying
     d IFS_Output2     s            256a   Varying
     d IFS_Input       s            256a   Varying
     d API_Output2     s            256a   Varying
     d API_Input       s            256a   Varying
     d IFS_path        s            256a   Varying
     D Len             S             10I 0
      *
      /copy prototypeb
      *--------------------------------------------------------------------
      * Change Directory
      *
      * int chdir(const char *path)
      *--------------------------------------------------------------------
     D chdir           PR            10I 0 ExtProc('chdir')
     D   path                          *   Value Options(*string)

     D fd              S             10I 0
     D RW              C                   6
     D R               C                   4
     D OWNER           C                   64
     D GROUP           C                   8
      /copy usec
      *
      * Datastruktur for IFS fra 1881
      *
     d Kundeionfo      ds                  qualified
     d                                     dim(99)
     d   Posting_Date                10a
     d   Document_Type...
     d                               10a
     d   Document_No                  8a
     d   Customer_No                  6a
     d   Description                 26a
     d   Amount                      11a
     d   Remaining_Amount...
     d                               11a
     d   Due_Date                    10a
      *
      * Definering av eksterne prg
      *
       dcl-s co402_verdi1  char(1);
       dcl-s co402_verdi2  char(2048);
       DCL-PR CO402R EXTPGM('CO402R');
         p_filgrp            char(3)     const;
         p_firm              packed(3:0) const;
         p_lager             packed(2:0) const;
         p_nokkel            char(50)    const;
         p_verdi1            char(1);
         p_verdi2            char(2048);
       END-PR;
      *
      *
      * Hovedprogram:
      *
      *  - slår opp med telefonnummer
      *  - mottar responsfil fra API
      *  - henter ut data fra responsfil
      *  - rydder på IFS
      *
      * Kjører vi med flåtestyring ?
      *
     c                   if        u_flat = *off
     c                   eval      p_stat = 'FALSE'
     c                   goto      avslutt
     c                   endif
      *
      * Sjekk om vi har mal for fletting av json fil
      *
     c                   if        %trim(w_mal) = *blanks or
     c                             %trim(w_url) = *blanks or
     c                             %trim(w_token) = *blanks or
     c                             %trim(w_trid) = *blanks
     c                   eval      p_stat = 'FALSE'
     c                   goto      avslutt
     c                   endif
      *
     c     p_firm        chain     afirl1
     c                   if        not %found
     c                   eval      p_stat = 'FALSE'
     c                   goto      avslutt
     c                   endif
      *
      * Hent inn info fra ordren.
      *
     c     fohel1_key    chain     fohel1
     c                   if        not %found
     c                   eval      p_stat = 'FALSE'
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      w_ctrp = 'FALSE'
      *
      * - Sjekk at ny forsendelsesmåten har flåtestyring.
      *
     c                   eval      raf2l1_2kod = folmat
     c     raf2l1_key    chain     raf2l1r
     c                   if        not %found or
     c                             ra2flt <> 1
     c                   eval      w_ctrp = 'TRUE'
     c                   endif
      *
      * Flåtestyring må også være satt opp på lager
      *
     c                   eval      ftrli1_llag = folage
     c     ftrli1_key    chain     ftrli1r
     c                   if        not %found or
     c                             ftlflt <> 1
     c                   eval      w_ctrp = 'TRUE'
     c                   endif
      *
     c                   if        %found and
     c                             ftlflt = 1 and
     c                             %trim(ftltid) <> *blanks
     c                   eval      w_trid = %trim(ftltid)
     c                   endif
      *
      * Hent inn info fra ordretypen
      *
     c                   eval      b_snus = *off
     c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1
     c                   if        %found and
     c                             vaoakk = 1 and
     c                             vaokre = '-'
     c                   eval      b_snus = *on
     c                   endif
      *
     c                   eval      w_dref = *blanks
     c                   eval      w_dref = %trim(fodref)
     c                   eval      w_mobi = *blanks
     c                   eval      w_mobi = %trim(fomobi)
      *
      * Hent inn info om kolli fra ordrelinjer
      *
     c                   eval      fodtlr_line = 9000
     c                   eval      w_akol = 0
     c     fodtlr_key    setll     fodtlr
     c     fodtlr_key2   reade     fodtlr
     c                   dow       not %eof
     c                   if        fdline = 9000 or
     c                             fdline = 9001 or
     c                             fdline = 9002
     c                   eval      w_akola = *blanks
     c                   eval      w_start = %scan(':' : fdtek1)
     c                   eval      w_start = w_start + 1
     c                   eval      w_akola = %subst(fdtek1:w_start:3)
     c                   eval      w_akol = w_akol + %int(%trim(w_akola))
     c                   endif
     c     fodtlr_key2   reade     fodtlr
     c                   enddo
      *
      * Må minimum sende 1 i antall kolli pga tjenesten til Lognet
      * Hvis 0, antagelig første request.
      *
     c                   if        w_akol = 0
     c                   eval      w_akol = 1
     c                   endif
      *
      * Hent inn vekt og volum for ordren
      *
     c                   eval      w_totv = 0
     c                   eval      w_totw = 0
     c                   eval      w_avvi = *blanks
      *
     c                   call      'FO780R'
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    w_totw
     c                   parm                    w_totv
     c                   parm                    w_avvi
      *
      * Hent inn info fra kunden.
      *
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1
     c                   if        not %found
     c                   eval      rknavn = *blanks
     c                   eval      rkgate = *blanks
     c                   eval      rkadr2 = *blanks
     c                   eval      rksted = *blanks
     c                   eval      rkponr = 0
     c                   endif
      *
      * Hent inn info fra avdling
      *
     c                   eval      ra10l1_jkod = folage
     c     ra10l1_key    chain     ra10l1
     c                   if        not %found
     c                   eval      rajtxt = *blanks
     c                   eval      rajadr = *blanks
     c                   eval      rajad2 = *blanks
     c                   eval      rajste = *blanks
     c                   eval      rajpnr = 0
     c                   endif
      *
      * Hent inn info fra selger
      *
     c                   eval      w_toemai = *blanks
     c                   eval      w_frcont = *blanks
     c                   eval      w_fremai = *blanks
     c                   eval      w_frphon = *blanks
     c                   eval      ra09l1_ikod = foselg
     c     ra09l1_key    chain     ra09l1
     c                   if        %found
     c                   eval      w_frcont = raitxt
     c                   eval      w_fremai = raiema
     c                   eval      w_frphon = raimob
     c                   endif
      *
      * Hvis vi ikke har unik ID fra før, lager vi en nå.
      *
     c                   eval      w_toid = *blanks
     c                   eval      ftrpi2_rnum = p_numm
     c                   eval      ftrpi2_rsuf = p_suff
     c     ftrpi2_key    chain     ftrpi2
     c                   if        %found
     c                   eval      w_toid = ftroid
     c                   endif
      *
     c                   if        %trim(w_toid) = *blanks
     c                   eval      w_toid = %trim(%trim(w_date_cnv)+':'+
     c                                     %char(w_numm))
     c                   endif
      *
      * Bygger opp felter i requesten.
      *
     c                   eval      w_oref = *blanks
     c                   eval      w_oref = %trim(%char(fonumm) + '-' +
     c                             %char(fosuff))
     c                   eval      w_odat = fobdat
     c                   if        foldat <> *loval
     c                   eval      w_odat = foldat
     c                   endif
      *
     c                   eval      w_city = *blanks
     c                   eval      w_ponr = *blanks
     c                   eval      w_len = %scan(' ' : aafste)
     c                   eval      w_ponr = %subst(aafste:1:w_len-1)
     c                   eval      w_city = %subst(aafste:w_len+1)
      *
     c                   eval      w_kcity = *blanks
     c                   eval      w_len = %scan(' ' : rksted)
     c                   eval      w_kcity = %subst(rksted:w_len+1)
      *
     c                   eval      w_lcity = *blanks
     c                   eval      w_lponr = *blanks
     c                   eval      w_len = %scan(' ' : fosted)
     c                   eval      w_lponr = %subst(fosted:1:w_len-1)
     c                   eval      w_lcity = %subst(fosted:w_len+1)
      *
      * Finn ut om den er klar for utsendelse, dvs loggkode 6 eller høyere
      *
     c                   eval      w_disp = 'false'
     c                   eval      w_aarr = %subdt(w_date:*years)
     c                   eval      floglr_aarr = w_aarr
     c                   eval      floglr_numm = fonumm
     c                   eval      floglr_suff = fosuff
     c     floglr_key    setll     floglr
     c     floglr_key    reade     floglr
     c                   dow       not %eof
     c                   if        fukode = '4' or
     c                             fukode = '6'
     c                   eval      w_disp = 'true '
     c                   endif
     c     floglr_key    reade     floglr
     c                   enddo
      *
      * Sjekk på forrige år, hvis ikke ordren er plukket. Året følger
      * første hendelse på ordren.
      *
     c                   if        w_disp = 'false'
     c                   eval      floglr_aarr = w_aarr - 1
     c     floglr_key    setll     floglr
     c     floglr_key    reade     floglr
     c                   dow       not %eof
     c                   if        fukode = '4' or
     c                             fukode = '6'
     c                   eval      w_disp = 'true '
     c                   endif
     c     floglr_key    reade     floglr
     c                   enddo
     c                   endif
      *
      * Hent kundens kontaktperson
      *
     c                   if        %trim(w_dref) = *blanks or
     c                             %trim(w_mobi) = *blanks
      *
     c                   if        fokpro <> 0
     c                   eval      fkprl1_kund = fokund
     c                   eval      fkprl1_kpro = fokpro
     c     fkprl1_key    chain     fkprl1
     c                   if        %found
     c                   if        w_dref = *blanks
     c                   eval      w_dref = %trim(flkprs)
     c                   endif
     c                   if        w_mobi = *blanks
     c                   eval      w_mobi = %trim(fltlfm)
     c                   endif
     c                   if        w_mobi = *blanks
     c                   eval      w_mobi = %trim(fltlfa)
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c                   if        w_mobi = *blanks
     c                   eval      w_mobi = %trim(rkmobn)
     c                   endif
      *
      * Hent inn json templaten
      *
     c                   callp     GetHTMLIFS(w_mal :'<AS400>')
     c                   callp     ClrHtmlBuffer
      *
      * Hvis det er en vareretur, må vi snu informasjonen i JSON.
      *
     c                   if        b_snus = *off
      * Avsender
     c                   eval      w_noname = aafnav
     c                   eval      w_noadr1 = aafad1
     c                   eval      w_noadr2 = aafad2
     c                   eval      w_noponr = w_ponr
     c                   eval      w_nocity = w_city
      * Mottaker
     c                   eval      w_nename = rknavn
     c                   eval      w_neadr1 = rkgate
     c                   eval      w_neadr2 = rkadr2
     c                   eval      w_neponr = %char(rkponr)
     c                   eval      w_necity = w_kcity
      * Avsender - lager
     c                   eval      w_frname = rajtxt
     c                   eval      w_fradr1 = rajadr
     c                   eval      w_fradr2 = rajad2
     c                   eval      w_frponr = %char(rajpnr)
     c                   eval      w_frcity = rajste
      * Motaker - ordren
     c                   eval      w_toname = fonavn
     c                   eval      w_toadr1 = foadr1
     c                   eval      w_toadr2 = foadr2
     c                   eval      w_toponr = w_lponr
     c                   eval      w_tocity = w_lcity
      *
     c                   else
      * Avsender
     c                   eval      w_noname = rknavn
     c                   eval      w_noadr1 = rkgate
     c                   eval      w_noadr2 = rkadr2
     c                   eval      w_noponr = %char(rkponr)
     c                   eval      w_nocity = w_kcity
      * Mottaker
     c                   eval      w_nename = aafnav
     c                   eval      w_neadr1 = aafad1
     c                   eval      w_neadr2 = aafad2
     c                   eval      w_neponr = w_ponr
     c                   eval      w_necity = w_city
      * Avsender - ordren
     c                   eval      w_frname = fonavn
     c                   eval      w_fradr1 = foadr1
     c                   eval      w_fradr2 = foadr2
     c                   eval      w_frponr = w_lponr
     c                   eval      w_frcity = w_lcity
     c                   eval      w_frcont = w_dref
     c                   eval      w_fremai = *blanks
     c                   eval      w_frphon = *blanks
      * Motaker - ordren
     c                   eval      w_toname = rajtxt
     c                   eval      w_toadr1 = rajadr
     c                   eval      w_toadr2 = rajad2
     c                   eval      w_toponr = %char(rajpnr)
     c                   eval      w_tocity = rajste
     c                   eval      w_dref = raitxt
     c                   eval      w_mobi = raimob
     c                   eval      w_toemai = raiema
     c                   endif
      *
      * Slå opp mot NGN for å hente plukk/fører info
      *
     c                   eval      w_picktxt = *blanks
     c                   eval      w_drivtxt = *blanks
     c                   eval      w_status = *blanks
     c                   eval      w_message = *blanks
      *
      * Kjøres ikke da programmet det hovedsaklig er laget for
      * å avlyse transportoppdrag
     c                   if        1=2
     c                   call      'NG001R'
     c                   parm                    p_firm
     c                   parm                    fonumm
     c                   parm                    fosuff
     c                   parm                    w_picktxt
     c                   parm                    w_drivtxt
     c                   parm                    w_status
     c                   parm                    w_message
      *
     c                   if        w_status = 'E'
     c                   eval      w_picktxt = *blanks
     c                   eval      w_drivtxt = *blanks
     c                   endif
     c                   endif
      *
      * Sjekk om kun transportordre
      *
     c                   eval      w_trpo = 'false'
     c                   exsr      sjk_trp_ord
     c                   if        b_tritem = *on
     c                   eval      w_trpo = 'true '
     c                   endif
      *
      /Free
           // Bygge json request - id info
               UpdHTMLVar('transportprovidercode':     %trim(w_trid));
               UpdHTMLVar('warehousenumber':           %char(folage));
               UpdHTMLVar('shipmentreference':         %trim(w_toid));
               UpdHTMLVar('orderreference':            %trim(w_oref));
               UpdHTMLVar('shipmentdate':              %char(w_odat));
               UpdHTMLVar('readyfordispatch':          %trim(w_disp));
               UpdHTMLVar('supplementtransportorder':  %trim(w_trpo));
               UpdHTMLVar('cancelled':                 %trim(w_ctrp));

           // Avsender
               UpdHTMLVar('consignorname':           %trim(w_noname));
               UpdHTMLVar('consignoraddress1':       %trim(w_noadr1));
               UpdHTMLVar('consignoraddress2':       %trim(w_noadr2));
               UpdHTMLVar('consignorpostalcode':     %trim(w_noponr));
               UpdHTMLVar('consignorcity':           %trim(w_nocity));
               UpdHTMLVar('consignorcountrycode':             'NO');
               UpdHTMLVar('consignorcontact':                  ' ');
               UpdHTMLVar('consignoremail':                    ' ');
               UpdHTMLVar('consignorphone':                    ' ');

           // Mottaker
               UpdHTMLVar('consigneename':           %trim(w_nename));
               UpdHTMLVar('consigneeaddress1':       %trim(w_neadr1));
               UpdHTMLVar('consigneeaddress2':       %trim(w_neadr2));
               UpdHTMLVar('consigneepostalcode':     %trim(w_neponr));
               UpdHTMLVar('consigneecity':           %trim(w_necity));
               UpdHTMLVar('consigneecountrycode':             'NO');
               UpdHTMLVar('consigneecontact':                  ' ');
               UpdHTMLVar('consigneeemail':                    ' ');
               UpdHTMLVar('consigneephone':                    ' ');
               UpdHTMLVar('consigneereference':                ' ');

           // Avsenderadresse - lager
               UpdHTMLVar('fromname':                %trim(w_frname));
               UpdHTMLVar('fromaddress1':            %trim(w_fradr1));
               UpdHTMLVar('fromaddress2':            %trim(w_fradr2));
               UpdHTMLVar('frompostalcode':          %trim(w_frponr));
               UpdHTMLVar('fromcity':                %trim(w_frcity));
               UpdHTMLVar('fromcountrycode':                  'NO');
               UpdHTMLVar('fromcontact':             %trim(w_frcont));
               UpdHTMLVar('fromemail':               %trim(w_fremai));
               UpdHTMLVar('fromphone':               %trim(w_frphon));

           // Leveringsadresse på ordren
               UpdHTMLVar('toname':                  %trim(w_toname));
               UpdHTMLVar('toaddress1':              %trim(w_toadr1));
               UpdHTMLVar('toaddress2':              %trim(w_toadr2));
               UpdHTMLVar('topostalcode':            %trim(w_toponr));
               UpdHTMLVar('tocity':                  %trim(w_tocity));
               UpdHTMLVar('tocountrycode':                    'NO');
               UpdHTMLVar('tocontact':               %trim(w_dref));
               UpdHTMLVar('toemail':                 %trim(w_toemai));
               UpdHTMLVar('tophone':                 %trim(w_mobi));

           // Godsinformasjon
               UpdHTMLVar('deliveryinformation1':    %trim(w_picktxt));
               UpdHTMLVar('deliveryinformation2':    %trim(w_drivtxt));
               UpdHTMLVar('totalpackages':           %char(w_akol));
               UpdHTMLVar('totalweight':             %char(w_totw));
               UpdHTMLVar('totalvolume':             %char(w_totv));

               WrtSection('Request');

      /End-Free
      *
      * Da skal vi lese linjer, og hent ned høyeste nummer
      *
     c                   eval      fodtlr_line = 8999
     c     fodtlr_key    setll     fodtlr
     c     fodtlr_key2   readpe    fodtlr
     c                   dow       not %eof
     c                   eval      w_line = fdline
     c                   leave
     c                   enddo
      *
      * Da skal vi lese linjer
      *
     c                   eval      fodtlr_line = 0
     c     fodtlr_key    setll     fodtlr
     c     fodtlr_key2   reade     fodtlr
     c                   dow       not %eof
      *
      * Finn vekt og volum for varelinje
      *
     c                   exsr      kalk_linje
      *
     c                   eval      w_tek1 = *blanks
      /Free

      * Scanner varestreng for tegn som gjør at json feiler.

               w_tek1 = %ScanRpl('"' : ' ' : fdtek1);

           // Godsinformasjon
               UpdHTMLVar('linenumber':               %char(fdline));
               UpdHTMLVar('articlenumber':            %trim(fdvare));
               UpdHTMLVar('articletext':              %trim(w_tek1));
               UpdHTMLVar('quantity':                 %char(fdanta));
               UpdHTMLVar('unitcode':                 %trim(fdenh1));
               UpdHTMLVar('weight':                  %char(w_vektv));
               UpdHTMLVar('volume':                  %char(w_voluv));

               WrtSection('RequestLineStart');

               if fdline <> w_line;
                   WrtSection('RequestLineEnd');
               else;
                   WrtSection('RequestLinesEnd');
               endif;

      /End-Free
     c                   if        fdline = w_line
     c                   leave
     c                   endif
      *
     c     fodtlr_key2   reade     fodtlr
     c                   enddo
      *
      * Skriv json fila til IFS området
      *
     c     fohel1_key    chain     fohelu
     c                   if        %found
     c                   time                    foedat
     c                   time                    foetim
     c                   eval      foeusr = l_user
     c                   update    fohelur
     c                   endif
      *
      * Skriv json fila til IFS området
      *
     c                   eval      IFS_path = '/home/' +
     c                                        l_figr +
     c                                        '/trp_tracking/'
      *
     c                   eval      IFS_Output1 = 'trp' +
     c                                           %trim(%char(w_numm)) +
     c                                           '.request'
      *
     c                   eval      IFS_Output = %trim(IFS_path) +
     c                                           %trim(IFS_Output1)
      *
     c                   callp     WrtHtmlToStmf(IFS_Output: 819)
      *
      * Oppretter headers file
      *
     c                   eval      IFS_Output2 = 'trp' +
     c                                           %trim(%char(w_numm)) +
     c                                           '.request.headers'
      *
     c                   if        chdir(%Trim(IFS_path)) >= 0
     c                   eval      fd = open(IFS_Output2 :
     c                               O_CREAT+O_WRONLY+O_CODEPAGE :
     c                               RW*OWNER + RW*GROUP + R : 1252  )
     c                   if        fd < 0
     c                   goto      avslutt
     c                   endif
     c                   endif
     c                   callp     close(fd)
      *
      * Åpnes igjen for å skrive inn APIKEY
      *
     c                   eval      w_tokeut = %triml('eg-apps-token=' +
     c                                        %trim(w_token))
      *
     c                   eval      fd = open(IFS_Output2 :
     c                               O_WRONLY+O_TEXTDATA)
     c                   if        fd < 0
     c                   goto      avslutt
     c                   endif
     c                   eval      w_size = %size(%trim(w_tokeut))
     c                   callp     write(fd: %addr(w_tokeut): w_size)
     c                   callp     close(fd)
      *
      * Bygger navn på retur file
      *
     c                   eval      IFS_Input = 'trp' +
     c                                           %trim(%char(w_numm)) +
     c                                           '.response'
      *
      * Initierer og kaller webservice
      *
     c                   eval      w_url2 = %trim(w_url) + '/api/shipment'
     c                   eval      API_Output2 = %triml(IFS_path + IFS_Output1)
     c                   eval      API_Input = %triml(IFS_path + IFS_Input)
     c                   eval      w_reqt = 'POST'
     c                   eval      w_reqf = API_Output2
     c                   eval      w_rspf = API_Input
     c                   eval      w_enco = 'ISO-8859-1'
     c                   eval      w_stat = 0
     c                   eval      w_usrn = *blanks
     c                   eval      w_pass = *blanks
      *
     c                   call      'AW702C'
     c                   parm                    w_url2
     c                   parm                    w_reqt
     c                   parm                    w_reqf
     c                   parm                    w_rspf
     c                   parm                    w_usrn
     c                   parm                    w_pass
     c                   parm                    w_enco
     c                   parm                    w_stat
      *
      * Henter ut data fra returfil
      *
     c                   eval      w_coref = *blanks
     c                   eval      IFS_Input = 'trp' +
     c                                           %trim(%char(w_numm)) +
     c                                           '.response'
     c                   eval      API_Input = %triml(IFS_path + IFS_Input)
     c                   eval      fd = open(API_Input :
     c                               O_RDONLY+O_TEXTDATA)
     c                   if        fd < 0
     c                   eval      p_stat = 'FALSE'
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      len = read(fd: %addr(w_indata):
     c                                            %size(w_indata))
      /FREE
      *
      * Partstype
      *
           w_start = %scan('consignmentReference' : w_indata);
           w_start = w_start + 23;
           w_slutt = %scan('"': w_indata : w_start);
           w_lengd = w_slutt - w_start;
           w_coref = %subst(w_indata:w_start : w_lengd);
      *
      /END-FREE
      *
      * Oppretter post med Consignment ref
      *
     c                   eval      ftrpiu_roid = w_toid
     c     ftrpiu_key    chain     ftrpiu
      *
     c                   eval      ftrnum = p_numm
     c                   eval      ftrsuf = p_suff
     c                   eval      ftrcre = w_coref
     c                   eval      ftrsta = 'SUCCESS'
     c                   eval      ftrako = w_akol
      *
     c                   time                    ftreda
     c                   time                    ftreti
     c                   eval      ftreus = l_user
      *
     c                   if        %found
     c                   update    ftrpiur
     c                   else
     c                   eval      ftrfir = p_firm
     c                   eval      ftroid = w_toid
      *
     c                   time                    ftroda
     c                   time                    ftroti
     c                   eval      ftrous = l_user
      *
     c                   write     ftrpiur
     c                   endif
      *
      * Rydder opp i filer på IFS
      *
     c                   eval      fd = open(IFS_Output2 :
     c                               O_WRONLY+O_TEXTDATA)
     c                   if        fd < 0
     c                   goto      avslutt
     c                   endif
      *
      * NBNBNB Temporært fjernet sletting av filer på IFS
      *
     c                   if        1=2
     c                   callp     unlink(IFS_Output1)
     c                   endif
     c                   callp     unlink(IFS_Output2)
     c                   callp     unlink(IFS_Input)
      *
     c                   eval      p_stat = 'TRUE'
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine  - Brukes ved mislykket web-service kall
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      p_stat = 'FALSE'
     c                   goto      avslutt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kalkulering av vekt og volum
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kalk_linje    begsr
      *
     c                   eval      w_vekt = 0
     c                   eval      w_vektv = 0
     c                   eval      w_volu = 0
     c                   eval      w_voluv = 0
      *
     c                   if        fdvare = *blank
     c                   goto      end_kalk
     c                   endif
      *
      * Behandler ikke tjenester og diversevarer
      *
     c                   eval      vvarl1_vare = fdvare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c     vvarl1_key    chain     vvasl1
8.01 c                   if        not %found
8.01 c                   eval      jvarl1_vare = fdvare
8.01 c     jvarl1_key    chain     jvarl1
8.01 c                   if        %found
8.01 c                   eval      vvldor = jvldor
8.01 c                   goto      va_vare
8.01 c                   endif
8.01 c                   endif
     c                   endif
      *
      * Kaller program for henting av varetype
      *
     c                   call      'VL710R'
     c                   parm                    p_firm
     c                   parm                    vvvare
     c                   parm                    fdlage
     c                   parm                    w_vtyp
     c                   parm                    w_utda
     c                   parm                    vvldor
     c                   parm                    b_inlr
     c                   parm                    w_lv_nobb
     c                   parm                    w_lv_modn
     c                   parm                    w_lv_lldo
     c                   parm                    w_lv_llva
      *
     c                   if        w_vtyp = 'T' or
     c                             w_vtyp = 'D'
     c                   goto      end_kalk
     c                   endif
      *
8.17 c     va_vare       tag
      *
      * Finner vekt og volum
      *
     c                   eval      vvenl1_vare = fdvare
     c                   eval      vvenl1_enhe = fdenh1
     c     vvenl1_key    chain     vvenl1
     c                   if        not %found or
     c                             vevekt = 0 or
     c                             vevolu = 0
     c                   eval      jvpkl2_vare = fdvare
     c                   eval      jvpkl2_ldor = vvldor
     c                   eval      jvpkl2_enhe = fdenh1
     c     jvpkl2_key    chain     jvpkl2
     c                   if        not %found
     c                   eval      jvpkl2_vare = fdvare
     c                   eval      jvpkl2_ldor = vvnlev
     c                   eval      jvpkl2_enhe = fdenh1
     c     jvpkl2_key    chain     jvpkl2
     c                   endif
     c                   endif
      *
     c                   if        vevekt <> 0
     c                   eval      w_vekt = vevekt
     c                   else
     c                   eval      w_vekt = jwvekt / 1000
     c                   endif
      *
     c                   if        vevolu <> 0
     c                   eval      w_volu = vevolu
     c                   else
     c                   eval      w_volu = jwvolu
     c                   endif
      *
      * Summerer vekt og volum
      *
     c                   eval      w_vektv = (w_vekt * fdanta)
     c                   eval      w_voluv = (w_volu * fdanta)
      *
      *
     c     end_kalk      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Splitt opp varenummer fra NGN transport i tabell
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_tr_item   begsr
      *
     c                   eval      x = 0
     c                   eval      tr_item(*) = *blanks
     c                   eval      w_posi = %scan(',':w_tritem)
     c                   dow       w_posi > 0
     c                   eval      w_posi = %scan(',':w_tritem)
     c                   if        w_posi > 0
     c                   eval      w_item = %trim(%subst(w_tritem:1:w_posi-1))
     c                   else
     c                   eval      w_item = %trim(w_tritem)
     c                   endif
     c                   eval      vvarl1_vare = w_item
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      x = x + 1
     c                   eval      tr_item(x) = w_item
     c                   endif
     c                   eval      w_tritem = %subst(w_tritem:w_posi+1:4000+
     c                                      -w_posi)
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekk om alle linjer består av tekstlinjer eller transportvarer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_trp_ord   begsr
      *
     c                   eval      b_tritem = *on
     c                   eval      fodtlr_line = 0
     c     fodtlr_key    setll     fodtlr
     c     fodtlr_key2   reade     fodtlr
     c                   dow       not %eof
     c                   if        fdvare <> *blanks
     c                   if        %lookup(fdvare:tr_item) = 0
     c                   eval      b_tritem = *off
     c                   endif
     c                   endif
     c     fodtlr_key2   reade     fodtlr
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Henter inn parameter
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_gmat
     c                   parm                    p_stat
      *
      * Key for posisjonering på ordrehodet
      *
     c     fohel1_key    klist
     c                   kfld                    p_firm
     c                   kfld                    p_numm
     c                   kfld                    p_suff
      *
      * Key for posisjonering på ordrelinjer
      *
     c     fodtlr_key    klist
     c                   kfld                    p_firm
     c                   kfld                    p_numm
     c                   kfld                    p_suff
     c                   kfld                    fodtlr_line
      *
      * Key for posisjonering på ordrelinjer
      *
     c     fodtlr_key2   klist
     c                   kfld                    p_firm
     c                   kfld                    p_numm
     c                   kfld                    p_suff
      *
      * Key for posisjonering på fraktregister
      *
     c     ftrpi2_key    klist
     c                   kfld                    p_firm
     c                   kfld                    ftrpi2_rnum
     c                   kfld                    ftrpi2_rsuf
      *
      * Key for posisjonering på fraktregister
      *
     c     ftrpiu_key    klist
     c                   kfld                    p_firm
     c                   kfld                    ftrpiu_roid
      *
      * Key for posisjonering på ordrehodet
      *
     c     rkunl1_key    klist
     c                   kfld                    p_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for posisjonering på avdeling
      *
     c     ra10l1_key    klist
     c                   kfld                    p_firm
     c                   kfld                    ra10l1_jkod
      *
      * Key for oppslag på loggfil
      *
     c     floglr_key    klist
     c                   kfld                    p_firm
     c                   kfld                    floglr_aarr
     c                   kfld                    floglr_numm
     c                   kfld                    floglr_suff
      *
      * Key for posisjonering på vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    p_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les av enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    p_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
      *
      * Key for oppslag på vare-pakning (VA)
      *
     c     jvpkl2_key    klist
     c                   kfld                    jvpkl2_vare
     c                   kfld                    jvpkl2_ldor
     c                   kfld                    jvpkl2_enhe
      *
      * Key for les av kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    p_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for posisjonering på transportinfo pr lager
      *
     c     ftrli1_key    klist
     c                   kfld                    p_firm
     c                   kfld                    ftrli1_llag
      *
      * Key for posisjonering på ordretyper
      *
     c     vot1l1_key    klist
     c                   kfld                    p_firm
     c                   kfld                    vot1l1_otyp
      *
      * Forsendelsesmåter - flåtestyring
      *
     c     raf2l1_key    klist
     c                   kfld                    p_firm
     c                   kfld                    raf2l1_2kod
      *
      * Ordretyper
      *
     c     votyl1_key    klist
     c                   kfld                    p_firm
     c                   kfld                    votyl1_otyp
      *
      * Selgerkoder
      *
     c     ra09l1_key    klist
     c                   kfld                    p_firm
     c                   kfld                    ra09l1_ikod
8.01  *
8.01  * Key for oppslag på varetabell VA
8.01  *
8.01 c     jvarl1_key    klist
8.01 c                   kfld                    jvarl1_vare
      *
      * Henter inn løpenummer teller
      *
     c                   eval      w_kode = '0'
     c                   eval      w_fell = 'NXTKLI'
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
     c                   eval      w_numm = *zero
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    p_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      /Free
      *
      * Henter inn verdier for å bygge opp streng til json
      *
      * Henter inn evnt bruker
      *
       exec sql
        select afudss
        into   :w_usrn
        from   afpspf
        where  afufil = :l_filg and
               afuide = 'WSSOKUSR';

      *
      * Henter inn passord til bruker
      *
       exec sql
        select afudss
        into   :w_pass
        from   afpspf
        where  afufil = :l_filg and
               afuide = 'WSSOKPASS';

      *
      * Henter inn URL hvor tjenesten ligger
      *
       exec sql
        select afudss
        into   :w_url
        from   afpspf
        where  afufil = :l_filg and
               afuide = 'cldTrpUrl';

      *
      * Henter inn mal for requst
      *
       exec sql
        select afudss
        into   :w_mal
        from   afpspf
        where  afufil = :l_filg and
               afuide = 'trp_reques';

      *
      * Henter inn appkey fra COMPANY_SETTINGS
      *
       exec sql
        select aposkval
        into   :w_token
        from   aposextnst
        where  aposfirm = :p_firm and
               apossyst = 'EGAPPS' and
               aposckey = 'CLOUD_API_KEY';

      *
      * Henter inn Flåteidentitet
      *
       exec sql
        select afudss
        into   :w_trid
        from   afpspf
        where  afufil = :l_filg and
               afuide = 'trp_tracid';

      /End-Free
      *
     c                   if        %trim(p_2fid) <> *blanks
     c                   eval      w_trid = p_2fid
     c                   endif
     c                   eval      w_toid = p_toid
     c                   time                    w_date
     c                   eval      w_date_cnv = %char(w_date)
      *
      * Hent inn info fra ordren.
      *
     c     fohel1_key    chain     fohel1
     c                   if        not %found
     c                   eval      folage = 0
     c                   endif
      *
     c                   eval      p_stat = 'TRUE'
      *
      * Henter inn alle varer som er definert som transportvarer basert på
      *
     c                   eval      w_tritem = *blanks
     c                   eval      w_ngn_key = 'so.deliverymethod.' +
     c                             %char(folmat) + '.freight'
      *
       exec sql
        select aposkval
        into   :w_tritem
        from   aposextnst
        where  aposfirm = :p_firm and
               apossyst = 'NGN-ORDER-MODULE' and
               aposckey = :w_ngn_key;
      *
       CallP CO402R(l_filg:p_firm:0:'AP055_KONVERTER_TEGN':
                        co402_verdi1:co402_verdi2);
       if co402_verdi1 = '1';
         u_konv = *on;
       endif;
      *
      * Endring 7.2u - Koblet til ekstern flåtestyring
      *
       CallP CO402R(l_filg:p_firm:0:'EKSTERN_FLATESTYRING':
                        co402_verdi1:co402_verdi2);
       if co402_verdi1 = '1';
         u_flat = *on;
       endif;
      *
     c                   if        u_konv = *on
      /FREE
          w_date_cnv = %ScanRpl('-':':':w_date_cnv:1);
      /END-FREE
     c                   endif
      *
     c                   if        %trim(w_tritem) <> *blanks
     c                   exsr      sjk_tr_item
     c                   endif
      *
     c                   endsr
