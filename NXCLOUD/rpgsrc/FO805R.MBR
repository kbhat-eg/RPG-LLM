     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO805R
      * Beskrivelse . . : Plukke ordrenummer automatisk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       020522 bsa  Nytt program
8.01  * 8.xx  100522 bsa  Vi har forhåpentligvis allerde lagt ut dette i
8.01  * 8.xx              utplukksregister.
8.02  * 8.xx  241124 bsa  Hvis den allerede finnes på utplukkstabell, skal vi
8.02  * 8.xx              bruke neste ordretype osv som allerede ligger her.
8.03  * 8.xx  091224 bsa  Sjekk neste ordretype fra utplukk før du evnt avslutter.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     ffohelu    uf   e           k disk    rename(fohepfr:fohelur)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffplolu    uf a e           k disk    rename(fplopfr:fplolur)
     ffplol1    if   e           k disk    rename(fplopfr:fplol1r)
     ffplolo    if   e           k disk    rename(fplopfr:fplolor)
     ffplllu    uf a e           k disk    rename(fpllpfr:fplllur)
     ffplll1    if   e           k disk    rename(fpllpfr:fplll1r)
     fvinfl1    if   e           k disk    rename(vinfpfr:vinfl1r)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     flwpll2    if   e           k disk    rename(lwplpfr:lwpll2r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frkmel1    if   e           k disk    rename(rkmepfr:rkmel1r)
     fafpslr    if   e           k disk    rename(afpspfr:afpslrr)
     fsdepl1    if   e           k disk    rename(sdeppfr:sdepl1r)
     ffst2l1    if   e           k disk    rename(fst2pfr:fst2l1r)
      *
      *
      * Definering av variable i nøkler
      *
     d vinfl1_ityp     s                   like(vaityp)
     d fusrl1_user     s                   like(fbuser)
     d lwpll2_numm     s                   like(mknumm)
     d lwpll2_suff     s                   like(mksuff)
     d rkunl1_kund     s                   like(rkkund)
     d afpslr_ufil     s                   like(l_filg)
     d afpslr_uide     s                   like(afuide)
     d sdepl1_kund     s                   like(skkund)
     d sdepl1_numm     s                   like(sknumm)
     d sdepl1_tell     s                   like(sktell)
     d fplll1_lonr     s                   like(fklonr)
     d fplll1_lsuf     s                   like(fklsuf)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d fodtl1_line     s                   like(fdline)
8.02 d fplol1_numm     s                   like(fdnumm)
8.02 d fplol1_suff     s                   like(fdsuff)
      *
      * Definering av variable
      *
     d w_alf1          s              1
     d w_firm          s                   like(fofirm)
     d w_on01          s                   like(vaon01)
     d w_on02          s                   like(vaon02)
     d w_on03          s                   like(vaon03)
     d w_on04          s                   like(vaon04)
     d w_on05          s                   like(vaon05)
     d w_on06          s                   like(vaon06)
     d w_onxt          s                   like(vaonxt)
     d w_aarr          s              4  0
     d p_kode          s              1
     d w_ldat          s                   like(foldat)
     d w_stat          s              1
     d p_urec          s             80
     d p_stat          s              1
     d b_depok         s              1
     d b_d100          s              1
     d w_sumi          s             11  2
     d w_sumu          s             11  2
     d w_avik          s             11  2
     d w_mld1          s             50
      *
     d                 ds
     d d_urec                        80
     d  d_ufir                        3  0 overlay(d_urec)
     d  d_aarr                        4  0 overlay(d_urec:4)
     d  d_unum                        8  0 overlay(d_urec:8)
     d  d_usuf                        2  0 overlay(d_urec:16)
     d  d_ukod                        1    overlay(d_urec:18)
     d  d_date                         d   overlay(d_urec:19) datfmt(*iso)
     d  d_time                         t   overlay(d_urec:29) timfmt(*iso)
     d  d_user                       10    overlay(d_urec:37)
     d  d_wsid                       10    overlay(d_urec:47)
      *
      *
     d                uds
     d dswsid                901    910
     d dsuser                911    920
     d l_filg                931    933
     d dssfir                944    946  0
     d l_fnav                951    980
     d dssyst                999    999
      *---------------------------------------------------------------------
      * Stand Alone Fields - TOP
      *---------------------------------------------------------------------
     d hedfir          s                   like(fofirm)
     d hednum          s                   like(fonumm)
     d hedsuf          s                   like(fosuff)
     d linfir          s                   like(fdfirm)
     d linlin          s                   like(fdline)
     d linnum          s                   like(fdnumm)
     d linsuf          s                   like(fdsuff)
     d otyfir          s                   like(vaofir)
     d otytyp          s                   like(vaotyp)
     d pllfir          s                   like(fklfir)
     d plllin          s                   like(fkllin)
     d pllnum          s                   like(fklonr)
     d pllsuf          s                   like(fklsuf)
     d plofir          s                   like(fkpfir)
     d plonum          s                   like(fkponr)
     d plosuf          s                   like(fkpsuf)
     d potfir          s                   like(fofirm)
     d potnum          s                   like(fonumm)
     d pototy          s                   like(footyp)
     d potsuf          s                   like(fosuff)
     d wpfirm          s                   like(fofirm)
     d wpftor          s              1
     d wpipor          s              1
     d w_jkod          s              2  0
     d wpnumm          s                   like(fonumm)
     d wpplor          s                   like(w_alf1)
     d w_retk          s              1
     d w_jtxt          s             20
     d wpsuff          s                   like(fosuff)
     d wseqen          s              1
     d wwoutq          s             10
     d wxnumm          s                   like(fkponr)
     d wxotyp          s                   like(fkpoty)
     d wxpdir          s              1  0
     d wxsuff          s                   like(fkpsuf)
     d w_otyp1         s                   like(footyp)
     d w_otyp2         s                   like(footyp)
8.02 d w_otyp          s                   like(footyp)
     d w_ityp          s                   like(foityp)
     d w_pos           s              2  0
     d w_len           s              3  0
     d w_teller        s              3  0
     d u_fsor          s              1    inz(*off)                            Fargekode fra sortim
     d u_fskv          s              1    inz(*off)                            Farge på skaffvare
     d w_fso1          s             10
     d w_fso2          s             10
     d w_sort          s             10
     d w_mvak          s              1
     d w_mvtx          s             30
     d w_date          s               d   datfmt(*iso)
     d w_mvas          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
     d w_bpro          s              5  2
     d w1grns          s             11  2
     d w1mems          s                   like(rkmems)
     d w2mems          s                   like(rkmems)
     d w3mems          s                   like(rkmems)
     d w1sums          s                   like(rkmems)
     d w1bere          s                   like(rkmems)
     d w2bere          s                   like(rkmems)
      *w_utvgrns = utvidelse av memosaldo(Mestergruppen)
     d w_utvgrns       s              2  0
      *w_memodagr = antall dager ordre skal med i memosaldo
     d w_memodagr      s              3  0
     d w_kgrn          s                   like(rksald)
     d w_ksal          s                   like(rksald)
     d w_3x            s              3
     d p_fir           s              3
     d p_kun           s              6
      *
      *---------------------------------------------------------------------
      * Stand Alone Fields - BOTTOM
      *---------------------------------------------------------------------
      *
     C/Exec SQL
     C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR,
     C+             commit=*none
     C/End-Exec
      *
      *
       dcl-s co402_verdi1  char(1);
       dcl-s co402_verdi2  char(2048);
       DCL-PR CO402R EXTPGM('CO402R');
         p_filgrp            char(3)     const;
         p_firm              packed(3:0) const;
         p_lager             packed(2:0) const;
         p_nokkel            char(50)    const;
         p_verdi1            char(1);
         p_verdi2            char(2048);
       END-PR;
     d u_701           s              1    inz(*off)
     d u_702           s              1    inz(*off)
     d u_703           s              1    inz(*off)
     d u_706           s              1    inz(*off)
     d u_707           s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandler plukking av ordrenummer                              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Hent opplysninger fra ORDRE-HODE-REGISTER
      *
     c                   move      wpnumm        hednum
     c                   move      wpsuff        hedsuf
     c     hedkey        chain     fohel1r                            90
      *
      *  Hent opplysninger fra ORDREYTYPE-REGISTER
      *
     c                   move      footyp        otytyp
     c     otykey        chain     votyl1r                            90
      *
      *  Hent opplysninger fra Kunderegister
      *
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1r
      *  Opphenting av meosaldo og beregning av en utvidet kr.grense gjøres
      *      her?  Sjekk om det er nok?
      * Beregne memosaldo pr dato
     c                   if        w_memodagr > 0
     c                   move      w_firm        p_fir
     c                   move      rkunl1_kund   p_kun
     c                   call      'FO763R'
     c                   parm                    p_fir
     c                   parm                    p_kun
     c                   endif
      *
      *  Ta vare på verdier fra ordretype-register
      *
     c                   move      vaon01        w_on01
     c                   move      vaon02        w_on02
     c                   move      vaon03        w_on03
     c                   move      vaon04        w_on04
     c                   move      vaon05        w_on05
     c                   move      vaon06        w_on06
     c                   move      vaonxt        w_onxt
      *
      * sjekke depositum
      *
     c                   exsr      sjekk_depo
      *
     c                   if        b_d100 = *on
     c                   goto      xslutt
     c                   endif
      *
      *  Sjekk,  Finnes ordretype i ORDRETYPE-REGISTER
      *
8.03 c*                  move      vaonxt        otytyp
8.03 c*    otykey        chain     votyl1r                            90
8.03 c*                  if        not %found
8.03 c**                 goto      xslutt
8.03 c*                  endif
      *
8.02  * Hvis utplukkstabell allerede finnes, går vi ut i fra at riktig ordetype
8.02  * allerede er funnet.
      *
8.02 c                   eval      fplol1_numm = wpnumm
8.02 c                   eval      fplol1_suff = wpsuff
8.02 c     fplol1_key    chain     fplol1
8.02 c                   if        %found
8.02 c                   eval      otytyp = fkpoty
8.02  *
8.02  *  Sjekk,  Finnes ordretype i ORDRETYPE-REGISTER
8.02  *
8.02 c     otykey        chain     votyl1r
8.02 c                   if        not %found
8.02 c                   goto      xslutt
8.03 c                   endif
8.03 c                   else
8.03 c                   move      vaonxt        otytyp
8.03 c     otykey        chain     votyl1r
8.03 c                   if        not %found
8.03 c                   goto      xslutt
8.03 c                   endif
8.02 c                   endif
      *
      * hvis depositum er 100 % og betalt, kan pakkseddel kjøres
     c                   if        b_depok = *off
      *
      *  Sjekk,  Kontantkunde kan ikke plukkes til pakkseddel
      *
     c                   if        u_701 = *on
     c                   if        vaoakk >  1 and
     c                             rkkres =  2 and
     c                             otytyp <> 'HK'
     c                   goto      xslutt
     c                   endif
     c                   else
     c                   if        rkkres =  2 and
     c                             vaoakk >  1
     c                   goto      xslutt
     c                   endif
     c                   endif
      *
     c                   endif
      *
      *  Sjekk ordresum/memosaldo mot kreditsperre
      *
     c                   if        u_706 = *on
     c                   if        footyp = 'HO'
      * Hent mva % for beregning av ordreverd inkl. mva
      *
     c                   eval      w_stat = *blank
     c                   eval      w_mvak = '3'
     c                   time                    w_date
      *
     c                   call      'RS205R'
     c                   parm                    w_stat
     c                   parm                    w_mvak
     c                   parm                    w_mvtx
     c                   parm                    w_date
     c                   parm                    w_mvas
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
     c                   parm                    w_bpro
      * Beregne memosaldo pr dato
     c                   if        w_memodagr > 0
     c                   move      w_firm        p_fir
     c                   move      rkunl1_kund   p_kun
     c                   call      'FO763R'
     c                   parm                    p_fir
     c                   parm                    p_kun
     c                   endif
      *
     c     rkunl1_key    chain     rkmel1
     c                   eval      w1mems = fotots * w_mvat
     c                   eval      w1mems = w1mems + rksald
     c                   eval      w1mems = w1mems + rkmems * w_mvat
     c                   eval      w3mems = rkmems
      *
      * Sjekk, er kredittgrense overskredet
      *
     c                   if        rkgrns <> 0
     c                   eval      w_ksal = rksald +
     c                                      (rkmems * w_mvat) -
     c                                      (fotots - fototr) * w_mvat
      *   Utvide kreditgrense   (Mestergruppen løsning 2017_01)  sst
     c                   if        w_ksal < rkgrns * 1000
     c                   eval      w_kgrn = rkgrns * 1000 *
     c                                      (1+w_utvgrns/100)
     c                   else
     c                   eval      w_kgrn = rkgrns * 1000
     c                   endif
     c                   eval      w_ksal = w_ksal +
     c                                      (fotots - fototr) * w_mvat
     c                   eval      w1grns = w_kgrn
     c                   eval      w1mems = w_ksal
     c                   else
     c                   eval      w1grns = rkgrns*1000
     c                   endif
      *
     c                   endif
     c                   endif
      *
      * Hent inn ordre-info-kode i inforegister
      *
     c                   eval      vinfl1_ityp = foityp
     c     vinfl1_key    chain     vinfl1r                            98
     c                   if        *in98  = *on
     c                   eval      vaifak = *blank
     c                   endif
      *
      *  Dersom ordre har en infokode, og dette er fakturering,
      *  sjekkes det om fakturering skal stoppes.
      *
     c                   if        foityp <> *blank
     c                   if        vaoakk =  3
     c                   if        vaifak = '1'
     c                   eval      wpplor = '9'
     c                   goto      xslutt
     c                   endif
     c                   endif
     c                   endif
      *
      *  Kontroll om bruker har lov til å sette igang fakturering.
      *
     c                   if        fbko08 <> 1
     c                   if        vaoakk =  3
     c                   eval      wpplor = '9'
     c                   goto      xslutt
     c                   endif
     c                   endif
      *
      *  Oppdater ORDRE-HODE-REGISTER, Ordren er til behandling
      *
     c                   call      'FO709R'
     c                   parm                    hednum
     c                   parm                    hedsuf
      *
     c     hedkey        chain     fohelur                            90
     c                   if        *in90 = *off
     c                   move      3             fokode
     c                   if        u_702 = *on
      * Legg inn hold kode hvis ordretype = PN
     c                   if        otytyp = 'PN'
     c                   eval      foityp = faak98
     c                   endif
      * hvis kunde ikke er internkunde og PI, så hold
     c                   if        rkmkod <> 1 and
     c                             rkspfa <> 1 and
     c                             otytyp = 'PI'
     c                   eval      foityp = faak98
     c                   endif
     c                   endif                                                  u_702
      *
     c                   if        u_703 = *on
     c                   if        footyp = w_otyp1 and otytyp = w_otyp2
     c                   if        foityp = *blank
     c                   eval      foityp = w_ityp
     c                   endif
     c                   endif
     c                   endif                                                  u_703
      *
     c                   time                    foedat
     c                   time                    foetim
     c                   eval      foeusr = dsuser
     c                   update    fohelur
     c                   endif
      *
      *  Oppdater UTPLUKK-PARAM-REGISTER med ny ordre
      *
     c                   move      wpnumm        plonum
     c                   move      wpsuff        plosuf
     c     plokey        chain     fplolur
     c                   if        not %found
     c                   move      plofir        fkpfir
     c                   move      plonum        fkponr
     c                   move      plosuf        fkpsuf
     c                   move      otytyp        fkpoty
     c                   move      folage        fkplag
     c                   move      dswsid        fkpwsd
     c                   move      dsuser        fkpusr
     c                   eval      fkpalt = *zero
     c                   eval      fkpdir = *zero
     c                   eval      fkpres = 0
     c                   eval      fkptxt = 0
     c                   eval      fkpkol = *zero
     c                   eval      fkpnet = *zero
     c                   eval      fkpbru = *zero
     c                   eval      fkplmt = *blank
     c                   eval      fkpfpr = *zero
     c                   eval      fkpfte = *blank
     c                   eval      fkpepr = *zero
     c                   eval      fkpete = *blank
     c                   write     fplolur
     c                   else
     c                   update    fplolur
     c                   endif
      *
      * Sjekk om det er foretatt plukking på Radioterminal
      *
     c                   exsr      sjk_rt
      *
      *  Så må vi legge ut varelinjer også
      *
     c                   exsr      ord_line
      *
      *  Kaller opp utskrift-program for å sette inn alternativ
      *  og om direktevalg skal benyttes
      *
     c                   call      'FO806R'
     c                   parm                    wpipor
     c                   parm                    plofir
     c                   parm                    plonum
     c                   parm                    plosuf
      *
     c                   move      wpipor        wpplor
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  D7 Behandle bilde for OPPDATERING AV PLUKK-REGISTER            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     ord_line      begsr
      *
      *  Benytter Z-ADD på antall inn i nytt felt fordi feltene
      *  ikke er like store, benytter kortere feltlengde i bildet
      *
     c                   eval      fodtl1_numm = fonumm
     c                   eval      fodtl1_suff = fosuff
     c                   eval      fodtl1_line = 0
     c     fodtl1_key    setll     fodtl1
     c     fodtl1_key2   reade     fodtl1
     c                   dow       not %eof
      *
     c                   eval      plllin = fdline
     c     pllkey        chain     fplllur
      *
     c                   if        %found(fplllu)
     c                   update    fplllur
     c                   else
     c                   eval      fklfir = pllfir
     c                   eval      fklonr = pllnum
     c                   eval      fklsuf = pllsuf
     c                   eval      fkllin = plllin
     c                   eval      fklant = fdanta
     c                   eval      fklkod = *blanks
     c                   write     fplllur
     c                   endif
      *
     c     fodtl1_key2   reade     fodtl1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke om det er plukket noe på radioterminal
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_rt        begsr
      *
      *  Sjekker siste status - bare status 7 skal behandles
      *
     c                   eval      p_kode = *blank
     c                   eval      w_aarr = %subdt(fobdat:*years)
     c                   call      'FU706R '
     c                   parm                    w_aarr
     c                   parm                    wpnumm
     c                   parm                    wpsuff
     c                   parm                    p_kode
      *
     c                   if        p_kode <> '7'
     c                   goto      end_rt
     c                   endif
      *
      * Leser plukkfil på radio og oppdaterer man. plukkefil
      *
     c                   eval      pllnum = wpnumm
     c                   eval      pllsuf = wpsuff
      *
     c                   eval      lwpll2_numm = wpnumm
     c                   eval      lwpll2_suff = wpsuff
     c     lwpll2_key    setll     lwpll2
     c     lwpll2_key    reade     lwpll2
      *
     c                   dow       not %eof
     c                   eval      plllin = mkline
     c     pllkey        chain     fplllur
      *
     c                   if        %found
     c                   eval      fklant = mkantp
     c                   update    fplllur
     c                   else
     c                   move      pllfir        fklfir
     c                   move      pllnum        fklonr
     c                   move      pllsuf        fklsuf
     c                   move      plllin        fkllin
     c                   eval      fklant = mkantp
     c                   write     fplllur
     c                   endif
      *
     c     lwpll2_key    reade     lwpll2
     c                   enddo
      *
     c     end_rt        endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for slette plukk-reg. på RadioTerm.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     upd_logg      begsr
      *
      *
     c                   time                    d_date
     c                   time                    d_time
     c                   eval      d_user = dsuser
     c                   eval      d_wsid = dswsid
     c                   eval      d_aarr = %subdt(fobdat:*years)
     c                   eval      p_urec = d_urec
     c                   call      'FU900R  '
     c                   parm                    p_stat
     c                   parm                    p_urec
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke om linje har ubetalt depositumskrav
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_depo    begsr
     c                   eval      b_depok = *off
     c                   eval      sdepl1_numm = fonumm
     c                   eval      sdepl1_kund = fokund
     c                   eval      sdepl1_tell = 0
     c     sdepl1_key    chain     sdepl1r
     c                   if        %found(sdepl1) and
     c                             skdepb >= skdepo
     c                   eval      b_depok = *on
     c                   endif
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke om linje har ubetalt depositumskrav
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_d100    begsr
      *
     c                   eval      b_d100 = *off
     c                   eval      *in45  = *off
      *
      * hvis 100% så sjekk at evt avvik er innenfor
     c                   if        sk100p = 1 and
     c                             faatjp <> 0
     c                   eval      w_sumi = 0
     c                   eval      w_sumu = 0
     c                   call      'FO705R '
     c                   parm                    w_firm
     c                   parm                    wpnumm
     c                   parm                    wpsuff
     c                   parm                    w_sumi
     c                   parm                    w_sumu
     c                   eval(h)   w_avik = skdepo * faatjp/100
      *
     c                   if         w_sumi >  (w_avik + skdepo)
     c                   eval      b_d100 = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *entry        plist
     c                   parm                    wpplor
     c                   parm                    wpfirm
     c                   parm                    wpnumm
     c                   parm                    wpsuff
      *
      *  Key for file : ORDRETYPE-REGISTER
      *
     c     otykey        klist
     c                   kfld                    otyfir
     c                   kfld                    otytyp
      *
      *  Key for file : ORDRE-HODE-REGISTER
      *
     c     hedkey        klist
     c                   kfld                    hedfir
     c                   kfld                    hednum
     c                   kfld                    hedsuf
      *
      *  Key for file : ORDRE-LINJE-REGISTER
      *
     c     linkey        klist
     c                   kfld                    linfir
     c                   kfld                    linnum
     c                   kfld                    linsuf
     c                   kfld                    linlin
      *
      *  Key for file : UTPLUKK-PARAM-REGISTER
      *
     c     plokey        klist
     c                   kfld                    plofir
     c                   kfld                    plonum
     c                   kfld                    plosuf
      *
      *  Key for file : UTPLUKK-PARAM-ORDRETYPE-REGISTER
      *
     c     potkey        klist
     c                   kfld                    potfir
     c                   kfld                    pototy
     c                   kfld                    potnum
     c                   kfld                    potsuf
      *
      *  Key for file : UTPLUKK-LINJE-REGISTER
      *
     c     pllkey        klist
     c                   kfld                    pllfir
     c                   kfld                    pllnum
     c                   kfld                    pllsuf
     c                   kfld                    plllin
      *
      * Key for file : KUNDE-REGISTER
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på ordre-info-type
      *
     c     vinfl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vinfl1_ityp
      *
      * Key for brukerregister
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrl1_user
      *
      * Key for ofak kode-register
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les på radioterminal-trans
      *
     c     lwpll2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwpll2_numm
     c                   kfld                    lwpll2_suff
      *
      * Key for propertiesfil
      *
     c     afpslr_key    klist
     c                   kfld                    afpslr_ufil
     c                   kfld                    afpslr_uide
      *
      * Key for oppslag på depositum-fil
      *
     c     sdepl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sdepl1_numm
     c                   kfld                    sdepl1_kund
     c                   kfld                    sdepl1_tell
      *
      * Key for oppslag på plukkfilen
      *
     c     fplll1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fplll1_lonr
     c                   kfld                    fplll1_lsuf
      *
      * Key for oppslag på ofak-koderegister sideregister
      *
     c     fst2l1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for file : UTPLUKK-LINJE-REGISTER
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
     c                   kfld                    fodtl1_line
      *
      *  Key for file : UTPLUKK-LINJE-REGISTER
      *
     c     fodtl1_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
8.02  *
8.02  * Key for oppslag på plukkfilen
8.02  *
8.02 c     fplol1_key    klist
8.02 c                   kfld                    w_firm
8.02 c                   kfld                    fplol1_numm
8.02 c                   kfld                    fplol1_suff
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     c                   eval      wwoutq = *blank
     c                   eval      wpipor = *blank
     c                   eval      wpftor = *blank
     c                   eval      wxpdir = *zero
      *
      *  Legger inn firma i nøkklene
      *
     c                   move      wpfirm        otyfir
     c                   move      wpfirm        hedfir
     c                   move      wpfirm        linfir
     c                   move      wpfirm        plofir
     c                   move      wpfirm        potfir
     c                   move      wpfirm        pllfir
     c                   move      wpfirm        w_firm
      *
      *  Nullstiller felter som alltid skal være blanke/null
      *
     c                   eval      plonum = *zero
     c                   eval      plosuf = *zero
     c                   eval      potnum = *zero
     c                   eval      potsuf = *zero
     c                   eval      pllnum = *zero
     c                   eval      pllsuf = *zero
     c                   eval      plllin = *zero
      *
     c                   eval      wxnumm = *zero
     c                   eval      wxsuff = *zero
     c                   eval      wxotyp = *blank
      *
      * Endring 7.01
      *
       CallP CO402R(l_filg:w_firm:0:'FO736R_701':
                        co402_verdi1:co402_verdi2);
       if co402_verdi1 = '1';
         u_701 = *on;
       endif;
      *
      * Endring 7.02
      *
       CallP CO402R(l_filg:w_firm:0:'FO736R_702':
                        co402_verdi1:co402_verdi2);
       if co402_verdi1 = '1';
         u_702 = *on;
       endif;
      *
      * Endring 7.03
      *
       CallP CO402R(l_filg:w_firm:0:'FO736R_703':
                        co402_verdi1:co402_verdi2);
       if co402_verdi1 = '1';
         w_pos = %scan(':' : co402_verdi2);
         if (w_pos > 0);
           w_otyp1 = %subst(co402_verdi2 : 1 : w_pos - 1);
           co402_verdi2 =  %subst(co402_verdi2:w_pos+1:512-w_pos);
           w_pos = %scan(':' : co402_verdi2);
           w_otyp2 = %subst(co402_verdi2 : 1 : w_pos - 1);
           co402_verdi2 =  %subst(co402_verdi2:w_pos+1:512-w_pos);
           w_pos = %scan(':' : co402_verdi2);
           w_ityp = %subst(co402_verdi2 : 1 : w_pos - 1);
           if (w_otyp1 <> *blank and w_otyp2 <> *blank  and w_ityp <> *blank);
      * Sjekk ordre-info-kode i inforegister
             vinfl1_ityp = w_ityp;
             chain vinfl1_key vinfl1r;
             if %found(vinfl1);
               u_703 = *on;
             endif;
             clear vinfl1r;
           endif;
         endif;
       endif;
      *
      * Endring 7.06
      *
       CallP CO402R(l_filg:w_firm:0:'UTVIDET_MEMOSALDO':
                        co402_verdi1:co402_verdi2);
       if co402_verdi1 = '1';
         u_706 = *on;
       endif;
      *
      * Endring 7.07
      *
       CallP CO402R(l_filg:w_firm:0:'FO736_707':
                        co402_verdi1:co402_verdi2);
       if co402_verdi1 = '1';
         u_707 = *on;
       endif;
      *
      * Hente inn brukerregister
      *
     c                   eval      fusrl1_user = dsuser
     c     fusrl1_key    chain     fusrl1r                            90
      *
      * Hente inn ofak kode-register
      *
     c     fstsl1_key    chain     fstsl1r                            90
     c     fst2l1_key    chain     fst2l1r
      *
      *   Hent inn % utvidelse av kreditgrense
     c                   eval      afpslr_ufil = l_filg
     c                   eval      afpslr_uide = 'KRGRNSUTV'
     c     afpslr_key    chain     afpslrr
     c                   if        %found(afpslr)
     c                   movel     afudss        w_utvgrns
     c                   endif
      *
      *   Hent inn ant dager fram ordre skal med i memosaldo
     c                   eval      afpslr_uide = 'MEMODAGER'
     c     afpslr_key    chain     afpslrr
     c                   if        %found(afpslr)
     c                   movel     afudss        w_3x
     c                   move      w_3x          w_memodagr
     c                   endif
      *
     c                   endsr
