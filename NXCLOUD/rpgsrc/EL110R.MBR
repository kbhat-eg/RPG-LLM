     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASPNIB
      * Program . . . . : EL110R
      * Beskrivelse . . : Leverandør bonuselement, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       271005 HKO  Nytt program
      *       071105 HKO  Lagt inn bonusbeløp og enhet knyttet mot element
      * 6.10  220609 BSA  Oppdatert med sporingsinformasjon
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     felbei1    if   e           k disk    rename(elbest:elbei1r)
     felbeir    if   e           k disk    rename(elbest:elbeirr)
     felbeiu    uf a e           k disk    rename(elbest:elbeiur)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvenhl1    if   e           k disk    rename(venhpfr:venhl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
     fvbotl1    if   e           k disk    rename(vbotpfr:vbotl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
      *
     fel110d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(elefir)
     d p_blev          s                   like(eleble)
     d p_ldor          s                   like(eleldo)
     d p_ogrp          s                   like(eleogr)
     d p_hgrp          s                   like(elehgr)
     d p_ugrp          s                   like(eleugr)
     d p_modn          s                   like(elemod)
     d p_vare          s                   like(elevar)
     d p_enhe          s                   like(eleenh)
     d p_lety          s                   like(elelet)
     d p_boty          s                   like(elebot)
     d p_fdat          s                   like(elefda)
     d p_tdat          s                   like(eletda)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d elbei1_ebgf     s                   like(elebgf)
     d elbei1_ebgt     s                   like(elebgt)
     d elbeir_ebgf     s                   like(elebgf)
     d elbeir_ebgt     s                   like(elebgt)
     d elbeiu_ebgf     s                   like(elebgf)
     d elbeiu_ebgt     s                   like(elebgt)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d rlevl1_levr     s                   like(rllevr)
     d vvarl1_vare     s                   like(vvvare)
     d venhl1_enhe     s                   like(vaeenh)
     d vltpl1_lety     s                   like(vylety)
     d vbotl1_tbot     s                   like(vatbot)
     d jvarl1_vare     s                   like(jvvare)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
      *
     d b_feil          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_oppr          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_subf          s              1    inz(*off)
      *
     d w_firm          s                   like(elefir)
     d w_blev          s                   like(eleble)
     d w_ldor          s                   like(eleldo)
     d w_ogrp          s                   like(eleogr)
     d w_hgrp          s                   like(elehgr)
     d w_ugrp          s                   like(eleugr)
     d w_modn          s                   like(elemod)
     d w_vare          s                   like(elevar)
     d w_enhe          s                   like(eleenh)
     d w_lety          s                   like(elelet)
     d w_boty          s                   like(elebot)
     d w_fdat          s                   like(elefda)
     d w_tdat          s                   like(eletda)
      *
     d s_ebgf          s                   like(elebgf)
     d s_ebgt          s                   like(elebgt)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(6)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile                                  CRT SFL
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   if        b_subf = *on
     c                   goto      b2taga
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c     elbei1_key    setll     elbei1
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
     c                   eval      b_subf = *off
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      b_subf = *on
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1ebgf = b1ebgf
     c                   eval      c1ebgt = b1ebgt
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1ebgf = b1ebgf
     c                   eval      k1ebgt = b1ebgt
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1ebgf = b1ebgf
     c                   eval      d1ebgt = b1ebgt
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1ebgf = b1ebgf
     c                   eval      c1ebgt = b1ebgt
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1ebgf = 0
     c                   eval      c1ebgt = 0
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      c1taga
     c                   endsl
      *
      * Validering
      *
     c                   if        c1ebgf > c1ebgt
     c                   eval      *in31 = *on
     c                   goto      c1taga
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      elbeir_ebgf = c1ebgf
     c                   eval      elbeir_ebgt = c1ebgt
     c     elbeir_key    chain     elbeir
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c     end_c1bld     tag
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   eval      b_oppr = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbilde legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle skjermbilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      elbeir_ebgf = c1ebgf
     c                   eval      elbeir_ebgt = c1ebgt
     c     elbeir_key    chain     elbeir
      *
     c                   if        %found
     c                   eval      c2ebpr = elebpr
     c                   eval      c2ebbe = elebbe
     c                   eval      c2eben = eleben
     c                   eval      c2emrk = elemrk
     c                   eval      c2ektb = elektb
     c                   eval      c2eeus = eleeus
     c     *dmy          move      eleoda        c2eoda
     c     *dmy          move      eleeda        c2eeda
     c                   eval      c2esbo = elesbo
     c                   eval      c2ebda = 0
     c                   eval      c2ebti = 0
     c                   if        elebda <> *loval
     c     *dmy          move      elebda        c2ebda
     c                   endif
     c                   if        elebti <> *loval
     c     *hms          move      elebti        c2ebti
     c                   endif
     c                   else
     c                   eval      c2ebpr = 0
     c                   eval      c2ebbe = 0
     c                   eval      c2eben = *blank
     c                   eval      c2emrk = *blank
     c                   eval      c2ektb = 0
     c                   eval      c2eoda = 0
     c                   eval      c2eeda = 0
     c                   eval      c2eeus = *blank
     c                   eval      c2esbo = 0
     c                   eval      c2ebda = 0
     c                   eval      c2ebti = 0
     c                   endif
      *
      * Kopiering
      *
     c                   if        b1valg = 3
     c                   eval      c1ebgf = k1ebgf
     c                   eval      c1ebgt = k1ebgt
     c                   eval      c2esbo = 0
     c                   eval      c2ebda = 0
     c                   eval      c2ebti = 0
     c                   endif
      *
      * Sperrer felter dersom 'vise'
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Validering
      *
     c                   if        c2ebpr = 0 and
     c                             c2ebbe = 0 and
     c                             c2emrk = *blank
     c                   eval      *in31 = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        c2ebpr <> 0 and
     c                             c2ebbe <> 0
     c                   eval      *in32 = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        c2ebbe <> 0 and
     c                             c2eben = *blank or
     c                             c2ebbe = 0 and
     c                             c2eben <> *blank
     c                   eval      *in33 = *on
     c                   goto      c2tagb
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   elbeiur
      *
     c                   eval      elbeiu_ebgf = c1ebgf
     c                   eval      elbeiu_ebgt = c1ebgt
     c     elbeiu_key    chain     elbeiur
      *
     c                   eval      elebpr = c2ebpr
     c                   eval      elebbe = c2ebbe
     c                   eval      eleben = c2eben
     c                   eval      elemrk = c2emrk
     c                   eval      elektb = c2ektb
     c                   eval      eleeus = l_user
      *
     c                   time                    eleeda
     c                   time                    eleeti
      *
      * Endrer/oppretter rad.
      *
     c                   if        %found
     c                   update    elbeiur
     c                   else
      *
     c                   eval      elefir = w_firm
     c                   eval      eleble = w_blev
     c                   eval      eleldo = w_ldor
     c                   eval      eleogr = w_ogrp
     c                   eval      elehgr = w_hgrp
     c                   eval      eleugr = w_ugrp
     c                   eval      elemod = w_modn
     c                   eval      elevar = w_vare
     c                   eval      eleenh = w_enhe
     c                   eval      elelet = w_lety
     c                   eval      elebot = w_boty
     c                   eval      elefda = w_fdat
     c                   eval      eletda = w_tdat
     c                   eval      elebgf = c1ebgf
     c                   eval      elebgt = c1ebgt
      *
     c                   time                    eleoda
     c                   time                    eleoti
6.10 c                   eval      eleous = l_user
     c
     c                   write     elbeiur
      *
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1ebpr = elebpr
     c                   eval      b1ebbe = elebbe
     c                   eval      b1eben = eleben
     c                   eval      b1emrk = elemrk
     c                   eval      b1ektb = elektb
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      elbeir_ebgf = d1ebgf
     c                   eval      elbeir_ebgt = d1ebgt
     c     elbeir_key    chain     elbeir
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
     c                   eval      elbeiu_ebgf = d1ebgf
     c                   eval      elbeiu_ebgt = d1ebgt
     c     elbeiu_key    chain     elbeiur
     c                   delete    elbeiur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_ebgf = k1ebgf
     c                   eval      s_ebgt = k1ebgt
      *
      * Fyller kopierings-bildet
      *
     c                   eval      elbeir_ebgf = k1ebgf
     c                   eval      elbeir_ebgt = k1ebgt
     c     elbeir_key    chain     elbeir
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   exsr      spØrring
     c                   goto      k1taga
     c                   endif
      *
      * Validering
      *
     c                   if        k1ebgf > k1ebgt
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      elbeir_ebgf = k1ebgf
     c                   eval      elbeir_ebgt = k1ebgt
     c     elbeir_key    chain     elbeir
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1ebgf = s_ebgf
     c                   eval      c1ebgt = s_ebgt
      *
     c                   exsr      xc2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      elbei1                                 15
      *
     c                   if        *in15                                        EOF
     c                   leave
     c                   endif
      *
     c                   if        eleble <> p_blev or
     c                             eleldo <> p_ldor or
     c                             eleogr <> p_ogrp or
     c                             elehgr <> p_hgrp or
     c                             eleugr <> p_ugrp or
     c                             elemod <> p_modn or
     c                             elevar <> p_vare or
     c                             eleenh <> p_enhe or
     c                             elelet <> p_lety or
     c                             elebot <> p_boty or
     c                             elefda <> p_fdat or
     c                             eletda <> p_tdat
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1ebgf = elebgf
     c                   eval      b1ebgt = elebgt
     c                   eval      b1ebpr = elebpr
     c                   eval      b1ebbe = elebbe
     c                   eval      b1eben = eleben
     c                   eval      b1emrk = elemrk
     c                   eval      b1ektb = elektb
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_blev
     c                   parm                    p_ldor
     c                   parm                    p_ogrp
     c                   parm                    p_hgrp
     c                   parm                    p_ugrp
     c                   parm                    p_modn
     c                   parm                    p_vare
     c                   parm                    p_enhe
     c                   parm                    p_lety
     c                   parm                    p_boty
     c                   parm                    p_fdat
     c                   parm                    p_tdat
      *
      * Key for oppslag på leverandør bonuselement
      *
     c     elbei1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_blev
     c                   kfld                    w_ldor
     c                   kfld                    w_ogrp
     c                   kfld                    w_hgrp
     c                   kfld                    w_ugrp
     c                   kfld                    w_modn
     c                   kfld                    w_vare
     c                   kfld                    w_enhe
     c                   kfld                    w_lety
     c                   kfld                    w_boty
     c                   kfld                    w_fdat
     c                   kfld                    w_tdat
     c                   kfld                    elbei1_ebgf
     c                   kfld                    elbei1_ebgt
      *
      * Key for oppslag på leverandør bonuselement
      *
     c     elbeir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_blev
     c                   kfld                    w_ldor
     c                   kfld                    w_ogrp
     c                   kfld                    w_hgrp
     c                   kfld                    w_ugrp
     c                   kfld                    w_modn
     c                   kfld                    w_vare
     c                   kfld                    w_enhe
     c                   kfld                    w_lety
     c                   kfld                    w_boty
     c                   kfld                    w_fdat
     c                   kfld                    w_tdat
     c                   kfld                    elbeir_ebgf
     c                   kfld                    elbeir_ebgt
      *
      * Key for oppdatering av leverandør bonuselement
      *
     c     elbeiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_blev
     c                   kfld                    w_ldor
     c                   kfld                    w_ogrp
     c                   kfld                    w_hgrp
     c                   kfld                    w_ugrp
     c                   kfld                    w_modn
     c                   kfld                    w_vare
     c                   kfld                    w_enhe
     c                   kfld                    w_lety
     c                   kfld                    w_boty
     c                   kfld                    w_fdat
     c                   kfld                    w_tdat
     c                   kfld                    elbeiu_ebgf
     c                   kfld                    elbeiu_ebgt
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på enhet
      *
     c     venhl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    venhl1_enhe
      *
      * Key for oppslag på leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
      *
      * Key for oppslag på bonustype
      *
     c     vbotl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vbotl1_tbot
      *
      * Key for oppslag på vare (VA)
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      *
      * Henter parametre, og fyller opp skjermbilde og subfile
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_blev = p_blev
     c                   eval      w_ldor = p_ldor
     c                   eval      w_ogrp = p_ogrp
     c                   eval      w_hgrp = p_hgrp
     c                   eval      w_ugrp = p_ugrp
     c                   eval      w_modn = p_modn
     c                   eval      w_vare = p_vare
     c                   eval      w_enhe = p_enhe
     c                   eval      w_lety = p_lety
     c                   eval      w_boty = p_boty
     c                   eval      w_fdat = p_fdat
     c                   eval      w_tdat = p_tdat
      *
     c                   eval      b2ldor = p_ldor
     c                   eval      b2ogrp = p_ogrp
     c                   eval      b2hgrp = p_hgrp
     c                   eval      b2ugrp = p_ugrp
     c                   eval      b2modn = p_modn
     c                   eval      b2vare = p_vare
     c                   eval      b2enhe = p_enhe
     c                   eval      b2lety = p_lety
     c                   eval      b2boty = p_boty
     c                   eval      b2fdat = 0
     c                   eval      b2tdat = 0
      *
      * Leverandør
      *
     c                   if        b2ldor <> 0
     c                   eval      rlevl1_levr = b2ldor
     c     rlevl1_key    chain     rlevl1
     c                   if        %found
     c                   eval      b2lnav = rlnavn
     c                   endif
     c                   endif
      *
      * Overgruppe
      *
     c                   if        b2ogrp <> 0 and
     c                             b2hgrp = 0
     c                   eval      vogrl1_oogr = b2ogrp
     c     vogrl1_key    chain     vogrl1
     c                   if        %found
     c                   eval      b2gtxt = vgotxt
     c                   endif
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        b2hgrp <> 0 and
     c                             b2ugrp = 0
     c                   eval      vhgrl1_hogr = b2ogrp
     c                   eval      vhgrl1_hhgr = b2hgrp
     c     vhgrl1_key    chain     vhgrl1
     c                   if        %found
     c                   eval      b2gtxt = vghtxt
     c                   endif
     c                   endif
      *
      * Undergruppe
      *
     c                   if        b2ugrp <> 0
     c                   eval      vugrl1_uogr = b2ogrp
     c                   eval      vugrl1_uhgr = b2hgrp
     c                   eval      vugrl1_uugr = b2ugrp
     c     vugrl1_key    chain     vugrl1
     c                   if        %found
     c                   eval      b2gtxt = vgutxt
     c                   endif
     c                   endif
      *
      * Modul
      *
     c                   if        b2modn <> 0
     c                   call      'FO714C'
     c                   parm                    w_firm
     c                   parm                    b2modn
     c                   parm                    b2mtxt
     c                   endif
      *
      * Varenummer
      *
     c                   if        b2vare <> *blank
     c                   eval      vvarl1_vare = b2vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      b2vtxt = vvtek1
     c                   else
     c                   eval      jvarl1_vare = b2vare
     c     jvarl1_key    chain     jvarl1
     c                   if        %found
     c                   eval      b2vtxt = jvtek1
     c                   endif
     c                   endif
     c                   endif
      *
      * Enhet
      *
     c                   if        b2enhe <> *blank
     c                   eval      venhl1_enhe = b2enhe
     c     venhl1_key    chain     venhl1
     c                   if        %found
     c                   eval      b2etxt = vaetxt
     c                   endif
     c                   endif
      *
      * Leveringstype
      *
     c                   if        b2lety <> 0
     c                   eval      vltpl1_lety = b2lety
     c     vltpl1_key    chain     vltpl1
     c                   if        %found
     c                   eval      b2ltxt = vybesk
     c                   endif
     c                   endif
      *
      * Bonustype
      *
     c                   if        b2boty <> *blank
     c                   eval      vbotl1_tbot = b2boty
     c     vbotl1_key    chain     vbotl1
     c                   if        %found
     c                   eval      b2btxt = vatbes
     c                   endif
     c                   endif
      *
     c                   if        p_fdat <> *loval
     c     *dmy          move      p_fdat        b2fdat
     c                   endif
     c                   if        p_tdat <> *loval
     c     *dmy          move      p_tdat        b2tdat
     c                   endif
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     elbei1_key    setll     elbei1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
