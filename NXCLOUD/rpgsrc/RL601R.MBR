     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RL601R                                        *
      * Beskrivelse . . : Utskrift av dubletter i leverandørregisteret  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
T5.00 * V5.40     120705  Nytt program                             KOB      L *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frlevl2    if   e           k disk    rename(rlevpfr:rlevl2r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
T5.20f                                     prefix(x:1)
     frl601p    o    e             printer oflind(*in30)
      *
      * Definering av variable i nøkler
      *
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av Local data area
      *
     d                uds
T5.00d l_navn                300    300
T5.00d l_gate                301    301
T5.00d l_ponr                302    302
T5.00d l_tlfn                303    303
T5.00d l_mobn                304    304
      *
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d ant             s              2  0
      *
     d w_ikke          s              1
      *
     d w_anta          s              5  0
     d w_firm          s                   like(l_firm)
     d w_levr          s                   like(rllevr)
     d w_alfa          s                   like(rlalfa)
     d w_navn          s                   like(rlnavn)
     d w_gate          s                   like(rlgate)
     d w_ponr          s                   like(rlponr)
     d w_tlfn          s                   like(rltlfn)
     d w_mobn          s                   like(rlmobn)
      *
      * Les leverandørregister
      *
     c     rlevl2_key    setll     rlevl2                                 90                 .
     c     rlevl2_key    reade     rlevl2                                 90
      *
     c                   dow       *in90 = *off
      *
     c                   if        rlalfa = w_alfa
     c                   exsr      xsrkont
      *
     c                   if        w_ikke = ' '
     c                   eval      ant = ant + 1
     c                   else
     c                   eval      ant = 1
     c                   endif
      *
     c                   else
      *
     c                   eval      ant = 1
     c                   endif
      *
     c                   select
     c                   when      ant = 1
     c                   eval      w_levr = rllevr
     c                   eval      w_navn = rlnavn
     c                   eval      w_gate = rlgate
     c                   eval      w_ponr = rlponr
     c                   eval      w_tlfn = rltlfn
     c                   eval      w_mobn = rlmobn
      *
     c                   when      ant = 2
     c                   eval      d1levr = w_levr
     c                   exsr      utskrift
     c                   eval      d1levr = rllevr
     c                   exsr      utskrift
      *
     c                   when      ant > 2
     c                   eval      d1levr = rllevr
     c                   exsr      utskrift
     c                   endsl
      *
     c                   eval      w_alfa = rlalfa
      *
     c     rlevl2_key    reade     rlevl2                                 90
     c                   enddo
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utskrift      begsr
      *
      * Sjekker om overflow
      *
     c                   if        *in30 = *on                                               .
     c                   write     a1hdr                                                     .
     c                   eval      *in30 = *off                                              .
     c                   endif                                                           .....
      *
     c                   eval      rlevl1_levr = d1levr
     c     rlevl1_key    chain     rlevl1                             90
      *
     c                   eval      d1navn = xlnavn
     c                   eval      d1gate = xlgate
     c                   eval      d1sted = xlsted
     c                   eval      d1tlfn = xltlfn
     c                   eval      d1mobn = xlmobn
      *
     c                   write     d1det
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kontroll
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xsrkont       begsr
      *
     c                   eval      w_ikke = ' '
      *
      * Sjekk navn
      *
     c                   if        l_navn = 'J' and
     c                             rlnavn <> w_navn
     c                   eval      w_ikke = 'J'
     c                   endif
      *
      * Sjekk gateadresse
      *
     c                   if        l_gate = 'J' and
     c                             rlgate <> w_gate
     c                   eval      w_ikke = 'J'
     c                   endif
      *
      * Sjekk postnummer
      *
     c                   if        l_ponr = 'J' and
     c                             rlponr <> w_ponr
     c                   eval      w_ikke = 'J'
     c                   endif
      *
      * Sjekk telefonnummer
      *
     c                   if        l_tlfn = 'J' and
     c                             rltlfn <> w_tlfn
     c                   eval      w_ikke = 'J'
     c                   endif
      *
      * Sjekk mobilnummer
      *
     c                   if        l_mobn = 'J' and
     c                             rlmobn <> w_mobn
     c                   eval      w_ikke = 'J'
     c                   endif
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Key for leverandørregister
      *
     c     rlevl2_key    klist
     c                   kfld                    w_firm
      *
      * Key for leverandørregister
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      w_alfa = ' '
     c                   eval      w_levr = 0
     c                   eval      ant = 0
      *
      * Skriv heading på første side
      *
     c                   write     a1hdr
      *
     c                   endsr
      *
