      /TITLE  Initiering av tape for nattrutine
     h datedit(*dmy)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
      * System. . . . . : ASPGPL
      * Program . . . . : AN070R
      * Beskrivelse . . : Initiering av tape for nattjobb/systembackup
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Referanse  Dato   Endringsbeskrivelse                      Sign
      * --------- ------  -------------------------------------------------
      *
      *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
      *
     faverl1    if   e           k disk
     f                                     rename(averpfr:averl1r)
     fan070d    cf   e             workstn
      *
      * Definering av variable i nøkler
      *
     d averl1_vkey     s                   like(avvkey)
      *
      * Definering av variable
      *
     d w_dens          s                   like(avdens)
     d w_kode          s              1
     d w_volu          s              6
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedrutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     m1taga        tag
     c                   eval      m1svar = '1'
     c                   eval      wactli = 4
     c                   eval      wactpo = 21
      *
     c                   write     m1win
     c                   exfmt     m1win
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      xslutt
     c                   when      *inkl
     c                   goto      xslutt
     c                   endsl
      *
      * Legg inn riktig navn på tapen (Vol.id)
      *
     c                   if        m1svar = '1'
     c                   eval      w_volu = 'ASBACK'
     c                   endif
      *
     c                   if        m1svar = '2'
     c                   eval      w_volu = 'SAVSYS'
     c                   endif
      *
      * Hent inn density for tape fra nattjobb 1 (ASNAT1)
      *
     c                   eval      averl1_vkey = 1
     c     averl1_key    chain     averl1                             60
      *
      * Varsle dersom det ikke er lagt opp nattjobb 1
      *
     c                   if        *in60  = *on
     c                   eval      w_kode = '1'                                 Nattjobb ikke def
     c                   endif
      *
      * Flytt over density til parameter
      *
     c                   if        *in60  = *off
     c                   eval      w_dens = avdens                              Density
     c                   endif
      *
      *  Kaller opp 'CL-prog' for initiering
      *
     c                   call      'AN070C'
     c                   parm                    w_kode
     c                   parm                    w_dens
     c                   parm                    w_volu
      *
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * End og job
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
      *
     c                   return
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Definering av felter som benyttes i systemet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c                   eval      w_dens = *blank
     c                   eval      w_kode = *blank
      *
      * Key for file med verdier fra nattrutine
      *
     c     averl1_key    klist
     c                   kfld                    averl1_vkey
      *
     c                   endsr
