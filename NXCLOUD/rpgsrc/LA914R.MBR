     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LA914R
      *  Beskrivelse . . : Årsoppgaver, Nullstiller lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign
      * --------- ------  -------------------------------------------------
      *           160307  Nullstiller ant. ganger tellet           bof
      * 6.10      260609  Oppdatert med sporingsinformasjon        BSA
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
      *
     fvlaglu    uf   e           k disk    rename(vlagpfr:vlaglur)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
      *
      * Parameter-inn
      *
     d                 ds
     d d_prec                        64
     d  d_val1                        1  0 overlay(d_prec)
     d  d_val2                        1  0 overlay(d_prec:2)
     d  d_val3                        1  0 overlay(d_prec:3)
     d  d_val4                        1  0 overlay(d_prec:4)
      *
      * Local Data Area
      *
     d                uds
6.10 d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d vlaglu_vare     s                   like(vlvare)
     d vlaglu_lage     s                   like(vllage)
     d vlagl1_vare     s                   like(vlvare)
     d vlagl1_lage     s                   like(vllage)
      *
      * Definering av variable
      *
     d w_firm          s                   like(laafir)
     d w_parm          s             64
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandle utplukk, Setter inn riktige parameterverdier
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      vlagl1_vare = *blank
     c                   eval      vlagl1_lage = 0
     c     vlagl1_key    setll     vlagl1r
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser poster fra register for utplukks-test
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_neste     tag
      *
     C                   read      vlagl1r                                90
     c                   if        *in90 = *on
     c                   goto      end_pgm
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppdater lager-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      vlaglu_vare = vlvare
     c                   eval      vlaglu_lage = vllage
     c     vlaglu_key    chain     vlaglu                             90
     c                   if        *in90 = *off
     c                   eval      vlinng = 0
     c                   eval      vlutta = 0
     c                   eval      vljust = 0
     c                   eval      vlsalg = 0
     c                   eval      vlretu = 0
     c                   eval      vlbeve = 0
     c                   eval      vltell = 0
     c                   eval      vlatel = 0
      *
     c                   if        d_val3 = 1
     c                   eval      vlb101 = vlbehl
     c                   endif
      *
6.10 c                   time                    vledat
6.10 c                   time                    vletim
6.10 c                   eval      vleusr = l_user
     c                   update    vlaglur
     c                   endif
      *
      *  Velg neste
      *
     c                   goto      les_neste
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for LAGER-STATUS
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for les av LAGER
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
      *
      *  Key for oppdatering av LAGER
      *
     c     vlaglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglu_vare
     c                   kfld                    vlaglu_lage
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    w_parm
     c                   eval      d_prec = w_parm
      *
      *  Legge inn firmanummer i nøklene
      *
     c                   eval      w_firm = l_firm
      *
      *  Hent opplysninger fra LAGER-STATUS-KODER
      *
     c     lstsl1_key    chain     lstsl1r                            90
      *
     c                   endsr
