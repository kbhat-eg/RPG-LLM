     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : ND105R
      * Beskrivelse . . : Ekstern ordre-linje, hent feilstatus
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       081002 HKO  Nytt program
      *       260107 HKO  Test på at enhet er salgsenhet
      * 6.31  050716 HKO  Lagt inn lager i parameter, og tester på
      *                   varetpe S i lagerregisteret.
6.32  * 6.30  180618 bsa  Flagger ikke feil for tjenestevarer.
6.33  * 6.30  061218 bsa  Sjekker mot lagerrregister
7.01  * 7.00  100419 ask  Overstyrer test mot lagervare dersom det er transer fra Winner
      *
8.01  * 8.xx  100222 bsa  Vi skal akseptere skaffevarer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
6.31 fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
6.31 ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vvfirm)
     d p_stat          s              1  0
6.31 d p_lage          s                   like(vllage)
     d p_vare          s                   like(vvvare)
     d p_tek1          s                   like(vvtek1)
     d p_anta          s             11  3
     d p_enhe          s                   like(veenhe)
     d p_lety          s                   like(vylety)
     d p_ldat          s               d   datfmt(*iso)
7.00 d p_fsys          s              3
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vvenl1_vare     s                   like(vevare)
     d vvenl1_enhe     s                   like(veenhe)
     d vltpl1_lety     s                   like(vylety)
6.31 d vlagl1_vare     s                   like(vlvare)
6.31 d vlagl1_lage     s                   like(vllage)
      *
      * Definering av variable
      *
     d w_stat          s              1
     d w_ltxt          s             20
     d w_ldat          s               d   datfmt(*iso)
     d w_udat          s               d   datfmt(*iso)
      *
     d w_firm          s                   like(vyfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      p_stat = 0
      *
      * Melding/tekstlinje
      *
     c                   if        p_vare = *blank and
     c                             p_tek1 <> *blank
     c                   goto      avslutt
     c                   endif
      *
      * Varenummer
      *
     c                   if        p_vare = *blank
     c                   eval      p_stat = 1
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   eval      p_stat = 1
     c                   goto      avslutt
     c                   endif
      *
      * Antall
      *
     c                   if        p_anta <= 0
     c                   eval      p_stat = 2
     c                   goto      avslutt
     c                   endif
      *
      * Enhet
      *
     c                   eval      vvenl1_vare = p_vare
     c                   eval      vvenl1_enhe = p_enhe
     c     vvenl1_key    chain     vvenl1
     c                   if        not %found or
     c                             vesaen = 0
     c                   eval      p_stat = 3
     c                   goto      avslutt
     c                   endif
      *
      * Leveringstype
      *
     c                   if        p_lety <> 0
     c                   eval      vltpl1_lety = p_lety
     c     vltpl1_key    chain     vltpl1
     c                   if        not %found
     c                   eval      p_stat = 5
     c                   goto      avslutt
     c                   endif
     c                   endif
      *
      * Leveringsdato
      *
     c                   if        p_ldat <> *loval
     c                   time                    w_udat
     c                   if        p_ldat < w_udat
     c                   eval      p_stat = 6
     c                   goto      avslutt
     c                   endif
     c                   endif
      *
6.31  * Lagervare
6.31  *
7.01 c                   if        p_fsys <> 'WIN'
6.31 c                   if        faalag = 1                                   Lagerstyring
6.31 c                   eval      vlagl1_vare = p_vare
6.31 c                   eval      vlagl1_lage = p_lage
6.31 c     vlagl1_key    chain     vlagl1
6.31 c                   if        %found and
6.31 c                             vlvtyp <> 'L' and
8.01 c                             vlvtyp <> 'S' and
6.31 c                             vlvtyp <> 'T'
6.31 c                   eval      p_stat = 8
6.31 c                   goto      avslutt
6.32 c                   else
6.33 c                   if        vlvtyp <> 'L' and
8.01 c                             vlvtyp <> 'S' and
6.33 c                             vlvtyp <> 'T'
6.32 c                   eval      p_stat = 8
6.32 c                   goto      avslutt
6.31 c                   endif
6.32 c                   endif
6.31 c                   else                                                   Ikke lagerstyring
6.32 c                   if        vvvtyp <> 'L' and
8.01 c                             vlvtyp <> 'S' and
6.32 c                             vvvtyp <> 'T'
6.31 c                   eval      p_stat = 8
6.31 c                   goto      avslutt
6.31 c                   endif
6.31 c                   endif
7.01 c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_stat
6.31 c                   parm                    p_lage
     c                   parm                    p_vare
     c                   parm                    p_tek1
     c                   parm                    p_anta
     c                   parm                    p_enhe
     c                   parm                    p_lety
     c                   parm                    p_ldat
7.01 c                   parm                    p_fsys
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
      *
      * Key for oppslag på leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
      *
6.31  * Key for oppslag på vare-lager
6.31  *
6.31 c     vlagl1_key    klist
6.31 c                   kfld                    w_firm
6.31 c                   kfld                    vlagl1_vare
6.31 c                   kfld                    vlagl1_lage
      *
6.31  * Key for oppslag på ofak koderegister
6.31  *
6.31 c     fstsl1_key    klist
6.31 c                   kfld                    w_firm
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
6.31  * Henter kode-informasjon
6.31  *
6.31 c     fstsl1_key    chain     fstsl1
      *
     c                   endsr
