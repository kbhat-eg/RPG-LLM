     h datedit(*dmy.) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RH966R                                              *
      * Beskrivelse . . : Hovedbok - omkjøring av journal - utskrift          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
T5.40 * V5.40     220306  Sideteller 0-stilt ved ny journal              HFL  *
pdf   * R6.10     250213  Innført PDF utskrift. Innebærer primært å finne BSA *
pdf   *                   generert spool, og kall av AP620R med dette som     *
pdf   *                   parametre.                                          *
6.30  * R6.30 180816 bsa  Rokkert på rekkefølge av close printerfile
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frhwapf    ip   e           k disk
     fraa1l1    if   e           k disk    rename(raa1pfr:raa1l1r)
     frh966p    o    e             printer oflind(*in30)
pdf  f                                     usropn
      *
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      * LOCAL DATA
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      *
      *  Parameter i LDA
      *
     d                uds
     d rhpkda                300    305  0
     d rhprÅr                         4  0
     d rhpper                         2  0
     d rhppef                         2  0
     d rhppet                         2  0
     d rhpavf                         4  0
     d rhpavt                         4  0
     d rhpjof                         5  0
     d rhpjot                         5  0
     d rhpsrt                         1
     d rhpsml                         1
     d rhpavv                         1
     d rhpkgs                         1
     d rhpkks                         1
     d rhpsps                         1
     d rhpktu                         1
pdf  d l_poff                584    584
pdf  d l_bach                595    635
pdf  d l_skje                637    637  0
      *
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d l1anta          s              9  0
     d l1debt          s                   like(rbneto)
     d l1jour          s                   like(rbneto)
     d l1kred          s                   like(rbneto)
     d l3anta          s              9  0
     d l3debt          s                   like(rbneto)
     d l3jour          s                   like(rbneto)
     d l3kred          s                   like(rbneto)
      *
     d w_firm          s                   like(rbfirm)
     d w_kund          s              6  0
     d w_levr          s              6  0
      *
     d p_stat          s              1
     d p_kont          s                   like(rbkont)
     d p_spes          s                   like(rbspes)
     d p_avde          s                   like(rbavde)
     d p_text          s             30
pdf  d p_btyp          s            100
      *
pdf  d splfname2       s             10a
pdf  d jobname2        s             10a
pdf  d username2       s             10a
pdf  d jobnbr2         s              6a
pdf  d splfnbr2        s             10i 0
pdf  d splfname3       s             10a
pdf  d jobname3        s             10a
pdf  d username3       s             10a
pdf  d jobnbr3         s              6a
pdf  d splfnbr3        s             10i 0
pdf  d p_tagg0         s             20
pdf  d p_tagg0val      s             50
pdf  d p_tagg1         s             20
pdf  d p_tagg1val      s             50
pdf  d p_tagg2         s             20
pdf  d p_tagg2val      s             50
pdf  d p_tagg3         s             20
pdf  d p_tagg3val      s             50
pdf  d p_tagg4         s             20
pdf  d p_tagg4val      s             50
pdf  d p_tagg5         s             20
pdf  d p_tagg5val      s             50
pdf  d p_tagg6         s             20
pdf  d p_tagg6val      s             50
pdf  d p_tagg7         s             20
pdf  d p_tagg7val      s             50
pdf  d p_tagg8         s             20
pdf  d p_tagg8val      s             50
pdf  d p_tagg9         s             20
pdf  d p_tagg9val      s             50
pdf  d p_refn          s             50
pdf  d p_dspl          s            100
pdf   *
pdf  d qsprilsp        pr                  extpgm('QSPRILSP')
pdf  d rcvvar                     32767a   options(*varsize)
pdf  d rcvvarlen                     10i 0 const
pdf  d format                         8a   const
pdf  d errorcode                  32767a   options(*varsize)
pdf   *
pdf  d sprl0100        ds
pdf  d  bytesrtn                     10i 0
pdf  d  bytesavl                     10i 0
pdf  d  splfname                     10a
pdf  d  jobname                      10a
pdf  d  username                     10a
pdf  d  jobnbr                        6a
pdf  d  splfnbr                      10i 0
pdf  d  systemname                    8a
pdf  d  createdate                    7a
pdf  d                                1a
pdf  d  createtime                    6a
pdf   *
pdf  d errorcode       ds
pdf  d  bytesprov                    10i 0 inz(0)
pdf  d  byteavail                    10i 0 inz(0)
      *
     irhwapfr
      *  HOVEDBOKSTRANSER
     i                                          rbfirm        l3
     i                                          rbjour        l1
      *
      * Les transaksjoner
      *
     c     les           tag
      *
     c                   if        *inl3 = *on and
     c                             *in18 = *off
     c                   write     head1
     c                   endif
      *
     c                   if        *inl3 = *off and
     c                             *inl1 = *off and
     c                             *in30 = *on
     c                   write     head1
     c                   eval      *in30 = *off
     c                   endif
      *
     c                   if        *inl3 = *on
     c                   eval      l3jour = 0
     c                   eval      l3debt = 0
     c                   eval      l3kred = 0
     c                   eval      l3anta = 0
     c                   endif
      *
     c                   if        *inl1 = *on and
     c                             *in18 = *on
     c                   eval      *in30 = *on
T5.40c                   eval      page  = *zero
     c                   write     head1
     c                   eval      *in30 = *off
     c                   endif
      *
     c                   if        *inl1 = *on
     c                   eval      l1jour = 0
     c                   eval      l1debt = 0
     c                   eval      l1kred = 0
     c                   eval      l1anta = 0
     c                   eval      l1kund = *zero
     c                   eval      l1levr = *zero
     c                   endif
      *
     c                   eval      w_debe = *zero
     c                   eval      w_kred = *zero
     c                   eval      *in81  = *off
     c                   eval      *in82  = *off
     c                   eval      *in83  = *off
      *
      * Summer
      *
     c                   eval      l1jour = l1jour + rbneto
      *
     c                   if        rbneto < 0
     c                   eval      w_kred = 0 - rbneto
     c                   eval      l1kred = l1kred - rbneto
     c                   else
     c                   eval      w_debe = rbneto
     c                   eval      *in81  = *on
     c                   eval      l1debt = l1debt + rbneto
     c                   endif
      *
     c                   eval      l1anta = l1anta + 1
      *
     c                   eval      l3jour = l3jour + rbneto
      *
     c                   if        rbneto < 0
     c                   eval      l3kred = l3kred - rbneto
     c                   else
     c                   eval      l3debt = l3debt + rbneto
     c                   endif
      *
     c                   eval      w_text = rbtext
     c     *dmy          move      rbbdat        w_bdat
     c                   eval      w_post = rbneto + rbmvab - rbinvb
      *
     c                   select
     c                   when      rbkon6 <= ra1hkt
     c                   move      rbkon6        p_kont
     c                   move      rbspes        p_spes
     c                   move      rbavde        p_avde
      *                                                                         Hent
     c                   call      'RS740R'                                      kontoplan-
     c                   parm                    p_stat
     c                   parm                    p_kont
     c                   parm                    p_spes
     c                   parm                    p_avde
     c                   parm                    p_text
      *                                                                         Hent
     c                   movel     p_text        w_txt1                          finnes
      *
     c                   when      rbkon6 <= ra1hrs
     c                   eval      *in82  = *on
     c                   eval      w_kund = rbkon6
     c                   call      'RS750R'                                      kontoplan-
     c                   parm                    p_stat                           info
     c                   parm                    w_kund                           info
     c                   parm                    p_text                           info
      *
     c                   movel     p_text        w_txt1                          finnes
      *
     c                   when      rbkon6 > ra1hrs
     c                   eval      *in83  = *on
     c                   eval      w_levr = rbkon6
     c                   call      'RS760R'                                      kontoplan-
     c                   parm                    p_stat                           info
     c                   parm                    w_levr                           info
     c                   parm                    p_text                           info
      *
     c                   movel     p_text        w_txt1                          finnes
      *
     c                   endsl
      *
      * Summerer kunde- og leverandørrreskontro
      *
     c                   if        *in82 = *on
     c                   eval      l1kund = l1kund + rbneto
     c                   else
     c                   if        *in83 = *on
     c                   eval      l1levr = l1levr + rbneto
     c                   endif
     c                   endif
      *
     c                   eval      l3anta = l3anta + 1
     c                   if        *in18 = *on
     c                   write     linje
     c                   endif
      *
     c     slutt         tag
      *
     cl1                 write     linl1
pdf   * Generer xml for videre utskrift
pdf  clr                 if        l_poff = '1'
pdf  clr                 exsr      spoolnbr
6.30 clr                 close     rh966p
6.30 clr                 exsr      xml_create
pdf  clr                 exsr      pdf_preview
pdf   * Avslutt jobbiden
pdf  clr                 call      'AB710R'
pdf  c                   parm                    l_bach
pdf  clr                 endif
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Finner spool som er generert av jobben
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     spoolnbr      begsr
pdf   /Free
pdf
pdf            QSPRILSP( SPRL0100
pdf                    : %size(SPRL0100)
pdf                    : 'SPRL0100'
pdf                    : errorcode );
pdf   /End-free
pdf  c*
pdf   *
pdf  c                   endsr
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Kaller eget program for generering av xml fil. Eget pgm         *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     xml_create    begsr
pdf   *
pdf  c                   eval      splfname2 = *blanks
pdf  c                   eval      splfname3 = *blanks
pdf  c                   eval      jobname2 = *blanks
pdf  c                   eval      jobname3 = *blanks
pdf  c                   eval      username2 = *blanks
pdf  c                   eval      username3 = *blanks
pdf  c                   eval      jobnbr2 = *blanks
pdf  c                   eval      jobnbr3 = *blanks
pdf  c                   eval      splfnbr2 = *zeros
pdf  c                   eval      splfnbr3 = *zeros
pdf  c                   eval      p_btyp = 'JOURNALLISTE'
pdf  c                   eval      p_dspl = 'Journalliste - nummer: '+
pdf  c                                      %char(rbjour)+
pdf  c                                      '   Periode ' +
pdf  c                                      %char(rbrper)+'/'+%char(rbraar)
pdf  c                   eval      p_refn = %char(rbraar) + %char(rbjour)
pdf  c                   eval      p_tagg0 = 'JOURNAL'
pdf  c                   eval      p_tagg0val = %char(rbjour)
pdf  c                   eval      p_tagg1 = *blanks
pdf  c                   eval      p_tagg1val = *blanks
pdf  c                   eval      p_tagg2 = 'KONTROLLSUM'
pdf  c                   eval      p_tagg2val = %char(l3debt)
pdf  c                   eval      p_tagg3 = *blanks
pdf  c                   eval      p_tagg3val = *blanks
pdf  c                   eval      p_tagg4 = *blanks
pdf  c                   eval      p_tagg4val = *blanks
pdf  c                   eval      p_tagg5 = *blanks
pdf  c                   eval      p_tagg5val = *blanks
pdf  c                   eval      p_tagg6 = *blanks
pdf  c                   eval      p_tagg6val = *blanks
pdf  c                   eval      p_tagg7 = *blanks
pdf  c                   eval      p_tagg7val = *blanks
pdf  c                   eval      p_tagg8 = *blanks
pdf  c                   eval      p_tagg8val = *blanks
pdf  c                   eval      p_tagg9 = *blanks
pdf  c                   eval      p_tagg9val = *blanks
pdf   *
pdf  c                   call      'AP627R'
pdf  c                   parm                    splfname
pdf  c                   parm                    jobname
pdf  c                   parm                    username
pdf  c                   parm                    jobnbr
pdf  c                   parm                    splfnbr
pdf  c                   parm                    splfname2
pdf  c                   parm                    jobname2
pdf  c                   parm                    username2
pdf  c                   parm                    jobnbr2
pdf  c                   parm                    splfnbr2
pdf  c                   parm                    splfname3
pdf  c                   parm                    jobname3
pdf  c                   parm                    username3
pdf  c                   parm                    jobnbr3
pdf  c                   parm                    splfnbr3
pdf  c                   parm                    p_tagg0
pdf  c                   parm                    p_tagg0val
pdf  c                   parm                    p_tagg1
pdf  c                   parm                    p_tagg1val
pdf  c                   parm                    p_tagg2
pdf  c                   parm                    p_tagg2val
pdf  c                   parm                    p_tagg3
pdf  c                   parm                    p_tagg3val
pdf  c                   parm                    p_tagg4
pdf  c                   parm                    p_tagg4val
pdf  c                   parm                    p_tagg5
pdf  c                   parm                    p_tagg5val
pdf  c                   parm                    p_tagg6
pdf  c                   parm                    p_tagg6val
pdf  c                   parm                    p_tagg7
pdf  c                   parm                    p_tagg7val
pdf  c                   parm                    p_tagg8
pdf  c                   parm                    p_tagg8val
pdf  c                   parm                    p_tagg9
pdf  c                   parm                    p_tagg9val
pdf  c                   parm                    l_bach
pdf  c                   PARM                    p_btyp
pdf  c                   PARM                    p_refn
pdf  c                   PARM                    p_dspl
pdf   *
pdf  c                   endsr
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Subrutiner for XML/jasper integrasjon - Skjermvisning           *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     pdf_preview   begsr
pdf  c                   if        l_skje = 1
pdf   * Vi kaller program for launch av PDF i browser
pdf  c                   call      'AP621R'
pdf  c                   parm                    l_bach
pdf   *
pdf  c                   endif
pdf   *
pdf  c                   endsr
pdf   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Key for statusopplysninger - koderegister
      *
     c     raa1l1_key    klist
     c                   kfld                    w_firm
      *
     c                   eval      rhpsrt = 'A'
     c                   if        rhpsrt = 'A'                                          .....
     c                   move      *on           *in18                                       .
     c                   endif                                                           .....
      *
     c                   move      *off          *in19
     c                   if        rhpsml = 'S'                                          .....
     c                   move      *on           *in19                                       .
     c                   endif                                                           .....
      *
     c                   move      l_firm        w_firm
pdf  c                   open      rh966p
      *
     c     raa1l1_key    chain     raa1l1                             91
      *
     c                   endsr
