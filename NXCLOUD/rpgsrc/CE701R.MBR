     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . :
      * Program . . . . : CE701R
      * Beskrivelse . . : EDI innkjøpsstatistikk, eksport til fil
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       130116 HKO  Nytt program
      *       260117 HKO  Utvidet funksjonalitet
      *       070517 HKO  Endret fra å lese en måned tilbake i tid til
      *                   et halvt år tilbake i tid
6.31  *       110919 HKO  Henter navn på innkjøper
7.00  *       240420 HKO  Blanker ugyldige tegn for å forhindre stopp i
      *                   eksport til NX
7.01  *       121020 HKO  Fjerner utlegging av varetekst-1
8.01  *       220621 bsa  Utvidet meldingsnummer fra 6-8 siffer
8.02  *       181122 bof  feil tegn i lev.navn
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsistl1    if   e           k disk    rename(sistpfr:sistl1r)
6.31 fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fjlevl3    if   e           k disk    rename(jlevpfr:jlevl3r)
     fllogl2    if   e           k disk    rename(llogpfr:llogl2r)
     fcelgl2    if   e           k disk    rename(celgpfr:celgl2r)
     fledil2    if   e           k disk    rename(ledipfr:ledil2r)
     flbedl1    if   e           k disk    rename(lbedpfr:lbedl1r)
     fsihel1    if   e           k disk    rename(sihepfr:sihel1r)
     fsidtl1    if   e           k disk    rename(sidtpfr:sidtl1r)
      *
     fcedipf    uf a e           k disk    rename(cedipfr:cedipfr)
      *
      * Definering av parametre
      *
     d p_list          s              8
      *
      * Datastruktur for parameterliste -inn
      *
     d                 ds
     d d_list                         8
     d  d_firm                        3  0 overlay(d_list:1)
     d  d_avde                        4  0 overlay(d_list:4)
      *
      * Definering av variable i nøkkel
      *
     d sistl1_paar     s                   like(sipaar)
6.31 d ra09l1_ikod     s                   like(raikod)
     d vlagl1_vare     s                   like(vlvare)
     d vlagl1_lage     s                   like(vllage)
     d rlevl1_levr     s                   like(rllevr)
     d jlevl3_enum     s                   like(jlenum)
     d llogl2_numm     s                   like(lmnumm)
     d llogl2_suff     s                   like(lmsuff)
     d celgl2_gind     s                   like(logind)
     d celgl2_gtyp     s                   like(logtyp)
     d celgl2_paar     s                   like(clpaar)
     d celgl2_benr     s                   like(clbenr)
     d ledil2_type     s                   like(litype)
     d ledil2_ldor     s                   like(lildor)
     d ledil2_numm     s                   like(linumm)
     d lbedl1_type     s                   like(xbtype)
     d lbedl1_aarr     s                   like(xbaarr)
     d lbedl1_numm     s                   like(xbnumm)
     d sihel1_aarr     s                   like(shaarr)
     d sihel1_binr     s                   like(shbinr)
     d sihel1_numm     s                   like(shnumm)
     d sihel1_suff     s                   like(shsuff)
     d sidtl1_aarr     s                   like(slaarr)
     d sidtl1_binr     s                   like(slbinr)
     d sidtl1_numm     s                   like(slnumm)
     d sidtl1_suff     s                   like(slsuff)
     d sidtl1_ktxt     s                   like(slktxt)
     d sidtl1_line     s                   like(slline)
      *
      * Definering av variable
      *
     d w_firm          s                   like(sifirm)
     d w_avde          s                   like(sikavd)
     d w_bida          s                   like(sibida)
     d w_paar          s                   like(sipaar)
      *
     d w_dat8          s              8
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter dato et halvt år tilbake i tid
      *
     c                   eval      w_bida = %date
     c***  w_bida        subdur    13:*m         w_bida
     c     w_bida        subdur    6:*m          w_bida
      *
      * Utleder år/periode
      *
     c     *iso0         move      w_bida        w_dat8
     c                   movel     w_dat8        w_paar
      *
      * Leser innkjøpsstatistikk fra et halvt år tilbake i tid
      *
     c                   eval      sistl1_paar = w_paar
     c     sistl1_key    setll     sistl1
     c     sistl1_key2   reade     sistl1
     c                   dow       not %eof
     c                   exsr      behandling
     c     sistl1_key2   reade     sistl1
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
      * Behandler kjøp for periode for avdeling som parameter
      *
     c                   if        sibida < w_bida or
     c                             sikavd <> w_avde or
     c                             sivare = *blank
     c                   leavesr
     c                   endif
      *
      * Initierer informasjon
      *
     c                   clear                   cedipfr
      *
      * Blanker ugyldige tegn for å forhindre stopp i eksport til NIB
      *
7.00 c     x'05':' '     xlate     sitek1        sitek1
7.00 c     x'1C':' '     xlate     sitek1        sitek1
      *
      * Innkjøpsstatistikk-informasjon
      *
     c                   eval      cefirm = w_firm                              Firma
     c                   eval      cepaar = sipaar                              Periode-år
     c                   eval      cepmnd = sipmnd                              Periode-mnd
     c                   eval      celadr = siladr                              Lev.adresse
     c                   eval      celety = silety                              Leveringstype
     c                   eval      cevare = sivare                              Varenr
7.01 c***                eval      cetek1 = %trim(sitek1)                       Varetekst-1
7.01 c                   eval      cetek1 = *blank                              Varetekst-1
     c                   eval      cenobb = sinobb                              Nobbnr
     c                   eval      celvar = silvar                              Lev.varenr
     c                   eval      cekavd = sikavd                              Avdeling (kjøp)
     c                   eval      celage = silage                              Lager
     c                   eval      cebinr = sibinr                              Bilagsnr
     c                   eval      celine = siline                              Bilagslinjenr
     c                   eval      cebida = sibida                              Bilagsdato
     c                   eval      cebtyp = siotyp                              Bestillingstype
     c                   eval      cebenr = siornr                              Bestillingsnr
     c                   eval      cebesu = siorsu                              Best. suffix
     c                   eval      cebeli = siorli                              Best. linjenr
     c                   eval      cebeda = siorda                              Best. dato
     c                   eval      ceornr = siordr                              Ordrenr
     c                   eval      ceorsu = siosuf                              Ordre suffix
     c                   eval      ceorli = siolin                              Ordre linjenr
     c                   eval      ceanta = sianta                              Antall
     c                   eval      cevenh = sivenh                              Enhet fra vare
     c                   eval      cebant = sioant                              Best. antall
     c                   eval      cebenh = sioenh                              Best. enhet
     c                   eval      cefakr = sifakr                              Debit/kredit
     c                   eval      cesumo = sisumo                              Sum opprinnelig
     c                   eval      cesumb = sisumb                              Sum brutto
     c                   eval      cesumn = sisumb - sirab1 -                   Sum netto
     c                                      sirab2 - sirabo
     c                   eval      cesumk = sisumk                              Sum kost
     c                   eval      cerab1 = sirab1                              Sum rab.1
     c                   eval      cerab2 = sirab2                              Sum rab.2
     c                   eval      cerabo = sirabo                              Sum ordrerab.
     c                   eval      celebo = silebo                              Lev.bonus
     c                   eval      cekres = sikres                              Restkode
     c                   eval      celdat = sildat                              Ønsket lev.dato
     c                   eval      cemoda = simoda                              Bekreftet lev.dato
     c                   eval      cevida = sivida                              Virkelig lev.dato
     c                   eval      cepuke = sipuke                              Prognose uke
     c                   eval      ceodat = siodat                              Oppr.dato
      *
     c                   eval      cekild = *blank                              Avventer Bård...
6.31 c                   eval      ceinkj = %char(siselg)
      *
6.31 c                   if        siselg <> 0
6.31 c                   eval      ra09l1_ikod = siselg
6.31 c     ra09l1_key    chain     ra09l1
6.31 c                   if        %found
6.31 c                   eval      ceinkj = %trim(raitxt)
6.31 c                   endif
6.31 c                   endif
      *
     c                   if        siordr <> 0
     c                   eval      ceskaf = 1
     c                   endif
      *
      * Henter varetype fra lager (eller fra bestilling/ordre-linje..?
      *
     c                   eval      vlagl1_vare = sivare
     c                   eval      vlagl1_lage = silage
     c     vlagl1_key    chain     vlagl1
     c                   if        %found
     c                   eval      cevtyp = vlvtyp
     c                   endif
      *
      * Leverandør- og fakturahode-informasjon
      *
     c                   eval      cevlev = sivaln                              Vareleverandør
      *
     c                   if        sivaln <> 0
     c                   eval      rlevl1_levr = sivaln
     c     rlevl1_key    chain     rlevl1
     c                   if        %found
     c                   eval      cevlor = rlftnr
8.02 c     x'05':' '     xlate     rlnavn        rlnavn
8.02 c     x'1C':' '     xlate     rlnavn        rlnavn
     c                   eval      cevlna = rlnavn
     c                   eval      jlevl3_enum = sivaln
     c     jlevl3_key    chain     jlevl3
     c                   if        %found
     c                   eval      cevnle = jlldor
     c                   endif
     c                   endif
     c                   endif
      *
     c                   eval      sihel1_aarr = sipaar
     c                   eval      sihel1_binr = sibinr
     c                   eval      sihel1_numm = siornr
     c                   eval      sihel1_suff = siorsu
     c     sihel1_key    chain     sihel1
     c                   if        %found
     c                   eval      ceflev = shflev
     c                   eval      cefemn = shmnum
     c                   if        shmnum <> 0
     c                   eval      cefedi = 1
     c                   endif
     c                   if        shflev <> 0
     c                   eval      rlevl1_levr = shflev
     c     rlevl1_key    chain     rlevl1
     c                   if        %found
     c                   eval      ceflor = rlftnr
     c                   eval      ceflna = rlnavn
     c                   endif
     c                   endif
     c                   endif
      *
      * Sjekker om bestilling er sendt på EDI
      *
     c                   eval      llogl2_numm = siornr
     c                   eval      llogl2_suff = siorsu
     c     llogl2_key    setll     llogl2
     c     llogl2_key    reade     llogl2
     c                   dow       not %eof
     c                   if        lmdate >= siorda and
     c                             lmkode = '2'
     c                   eval      cebedi = 1
     c                   leave
     c                   endif
     c     llogl2_key    reade     llogl2
     c                   enddo
      *
     c                   if        cebedi = 0
     c                   eval      llogl2_suff = 0
     c     llogl2_key    setll     llogl2
     c     llogl2_key    reade     llogl2
     c                   dow       not %eof
     c                   if        lmdate >= siorda and
     c                             lmkode = '2'
     c                   eval      cebedi = 1
     c                   leave
     c                   endif
     c     llogl2_key    reade     llogl2
     c                   enddo
     c                   endif
      *
     c                   if        cebedi = 0
     c                   eval      celgl2_gind = 'O'
     c                   eval      celgl2_gtyp = 'ORDR'
     c                   eval      celgl2_paar = sipaar
     c                   eval      celgl2_benr = siornr
     c     celgl2_key    chain     celgl2
     c                   if        %found
     c                   eval      cebedi = 1
     c                   endif
     c                   endif
      *
      * Henter EDI meldingsnr og status fra EDI melding
      *
     c                   eval      ledil2_type = 'OBKR'                         Ordrebekreftelse
     c                   eval      ledil2_ldor = cevlev
     c                   eval      ledil2_numm = siornr
     c     ledil2_key    setll     ledil2
     c     ledil2_key    reade     ledil2
     c                   dow       not %eof
     c                   eval      ceoedi = 1
     c                   eval      ceoemn = limnum
     c                   if        liline = siorli
     c                   eval      ceoems = listat
     c                   leave
     c                   endif
     c     ledil2_key    reade     ledil2
     c                   enddo
      *
     c                   if        ceoedi = 0
     c                   eval      celgl2_gind = 'I'
     c                   eval      celgl2_gtyp = 'OBKR'
     c                   eval      celgl2_paar = sipaar
     c                   eval      celgl2_benr = siornr
     c     celgl2_key    chain     celgl2
     c                   if        %found
     c                   eval      ceoedi = 1
     c                   endif
     c                   endif
      *
     c                   eval      ledil2_type = 'FAKT'                         Faktura
     c                   eval      ledil2_ldor = cevlev
     c                   eval      ledil2_numm = siornr
     c     ledil2_key    setll     ledil2
     c     ledil2_key    reade     ledil2
     c                   dow       not %eof
     c                   eval      cefemn = limnum
     c                   if        lisuff = siorsu and
     c                             liline = siorli
     c                   eval      cefems = listat
     c                   leave
     c                   endif
     c     ledil2_key    reade     ledil2
     c                   enddo
      *
     c                   if        cefedi = 0
     c                   eval      celgl2_gind = 'I'
     c                   eval      celgl2_gtyp = 'FAKT'
     c                   eval      celgl2_paar = sipaar
     c                   eval      celgl2_benr = siornr
     c     celgl2_key    chain     celgl2
     c                   if        %found
     c                   eval      cefedi = 1
     c                   endif
     c                   endif
      *
      * Henter status på inngående EDI-meldinger
      *
     c                   eval      lbedl1_type = 'OBKR'                         Ordrebekreftelse
     c                   eval      lbedl1_aarr = sipaar
     c                   eval      lbedl1_numm = siornr
     c     lbedl1_key    setll     lbedl1
     c     lbedl1_key    reade     lbedl1
     c                   dow       not %eof
     c                   eval      ceoedi = 1
     c                   select
     c                   when      xbstat = '3'                                 Avvik bestilling
     c                   eval      ceoavb = 1
     c                   when      xbstat = '5'                                 Avvik totalsum
     c                   eval      ceoavt = 1
     c                   endsl
     c     lbedl1_key    reade     lbedl1
     c                   enddo
      *
     c                   eval      lbedl1_type = 'FAKT'                         Faktura
     c                   eval      lbedl1_aarr = sipaar
     c                   eval      lbedl1_numm = siornr
     c     lbedl1_key    setll     lbedl1
     c     lbedl1_key    reade     lbedl1
     c                   dow       not %eof
     c                   eval      cefedi = 1
     c                   select
     c                   when      xbstat = '3'                                 Avvik bestilling
     c                   eval      cefavb = 1
     c                   when      xbstat = '5'                                 Avvik totalsum
     c                   eval      cefavt = 1
     c                   endsl
     c     lbedl1_key    reade     lbedl1
     c                   enddo
      *
      * Faktura-linje informasjon
      *
     c                   eval      sidtl1_aarr = sipaar
     c                   eval      sidtl1_binr = sibinr
     c                   eval      sidtl1_numm = siornr
     c                   eval      sidtl1_suff = siorsu
     c                   eval      sidtl1_ktxt = 3
     c                   eval      sidtl1_line = siorli
     c     sidtl1_key    chain     sidtl1
     c                   if        %found
     c                   eval      celvre = sllvre
     c                   endif
      *
      * Skriver til fil
      *
     c                   write     cedipfr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for les på innkjøpsstatistikk
      *
     c     sistl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_paar
      *
     c     sistl1_key2   klist
     c                   kfld                    w_firm
      *
6.31  * Key for oppslag på selger
6.31  *
6.31 c     ra09l1_key    klist
6.31 c                   kfld                    w_firm
6.31 c                   kfld                    ra09l1_ikod
      *
      * Key for oppslag på lager
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
      *
      * Key for oppslag leverandør (EVR)
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag leverandør (VA)
      *
     c     jlevl3_key    klist
     c                   kfld                    jlevl3_enum
      *
      * Key for oppslag på EDI forsendelseslogg
      *
     c     llogl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    llogl2_numm
     c                   kfld                    llogl2_suff
      *
      * Key for oppslag på EDI forsendelseslogg
      *
     c     celgl2_key    klist
     c                   kfld                    celgl2_gind
     c                   kfld                    celgl2_gtyp
     c                   kfld                    w_firm
     c                   kfld                    celgl2_paar
     c                   kfld                    celgl2_benr
      *
      * Key for oppslag på EDI melding
      *
     c     ledil2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ledil2_type
     c                   kfld                    ledil2_ldor
     c                   kfld                    ledil2_numm
      *
      * Key for oppslag på EDI ingående meldingslogg
      *
     c     lbedl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbedl1_type
     c                   kfld                    lbedl1_aarr
     c                   kfld                    lbedl1_numm
      *
      * Key for oppslag innkjøpsfaktura-hode
      *
     c     sihel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sihel1_aarr
     c                   kfld                    sihel1_binr
     c                   kfld                    sihel1_numm
     c                   kfld                    sihel1_suff
      *
      * Key for oppslag innkjøpsfaktura-linje
      *
     c     sidtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sidtl1_aarr
     c                   kfld                    sidtl1_binr
     c                   kfld                    sidtl1_numm
     c                   kfld                    sidtl1_suff
     c                   kfld                    sidtl1_ktxt
     c                   kfld                    sidtl1_line
      *
      *
      * Kopierer parameter til datastruktur
      *
     c                   eval      d_list = p_list
      *
     c                   eval      w_firm = d_firm
     c                   eval      w_avde = d_avde
      *
     c                   endsr
