      /TITLE  Kontroll av gyldig mengde-kode
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RS715R                                              *
      * Beskrivelse . . : Kontroll av gyldig mengde-kode                      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      *           020100  Nytt program                                   KOB  *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                                                       *
      *  BRUK AV INDIKATORER                                                  *
      *                                                                       *
      *    60-61 = Fileindikatorer chain etc.                                 *
      *                                                                       *
      *  BRUK AV RETURKODE                                                    *
      *                                                                       *
      *  Kode i statusfelt(p_stat = N) : post finnes ikke                     *
      *                                                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fra15l1    if   e           k disk    rename(ra15pfr:ra15l1r)
      *
     d w_firm          s                   like(raofir)
      *
     d p_stat          s              1
     d p_okod          s                   like(raokod)
     d p_otxt          s                   like(raotxt)
      *
     d ra15l1_okod     s                   like(raokod)
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Sjekke kode
      *
     c                   eval      ra15l1_okod = p_okod                                      .
     c     ra15l1_key    chain     ra15l1r                            60
      *
     c                   if        *in60 = *on                                           .....
     c                   eval      p_stat = 'N'                                              .
     c                   eval      p_otxt = 'Koden er ugyldig  '                             .
     c                   else                                                            .....
     c                   eval      p_stat = *blank                                           .
     c                   eval      p_otxt = raotxt                                           .
     c                   endif                                                           .....
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     slutt         tag
     c                   eval      *inlr = *on
     c                   return
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av prograo                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     *inzsr        begsr
      *
     c                   eval      w_firm = l_firm
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_stat
     c                   parm                    p_okod
     c                   parm                    p_otxt
      *
      * Key for kode
      *
     c     ra15l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra15l1_okod
      *
     c                   endsr
