     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : NW860R
      * Beskrivelse . . : WAP, API - ordreinngang
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       201103 HKO  Nytt program
      *       180304 HKO  Tilpasset nye feltlenger
      * 6.10  250609 BSA  Oppdatert med sporingsinformasjon
6.nu  * 6.30  160614 bof  Endring vedr. nummerutvidelse
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fnohel6    if   e           k disk    rename(nohepfr:nohel6r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fnohelu    uf a e           k disk    rename(nohepfr:nohelur)
     fnodtlu    uf a e           k disk    rename(nodtpfr:nodtlur)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
     fnkeol1    if   e           k disk    rename(nkeopfr:nkeol1r)
      *
6.10  * Definering av Local data area
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_avde          s              4  0
     d p_uniq          s             15
     d p_dtqi          s             10
     d p_dtqo          s             10
     d p_bibl          s             10
     d p_feil          s              1
     d p_inpl          s              5p 0
     d p_outl          s              5p 0
     d p_wait          s              5p 0
     d p_keyo          s              2
     d p_keyl          s              3p 0
     d p_sndl          s              3p 0
     d p_sndi          s             36
      *
      * Definering av datastrukturer
      *
      * Datakø - inn
      *
     d                 ds
     d d_inpu                       499
      *
      * Datakø - inn (ordre-hode)
      *
     d                 ds
5.10 d d_hode                       499
     d  d_kund                        9  0 overlay(d_hode:  1)
     d  d_kpro                       12    overlay(d_hode: 10)
     d  d_refn                       15    overlay(d_hode: 22)
     d  d_ladr                       35    overlay(d_hode: 37)
     d  d_dref                       35    overlay(d_hode: 72)
     d  d_rekv                       20    overlay(d_hode:107)
     d  d_hlma                       20    overlay(d_hode:127)
     d  d_hlda                        8    overlay(d_hode:147)
     d  d_unav                       35    overlay(d_hode:155)
     d  d_umai                       50    overlay(d_hode:190)
     d  d_hmld                      256    overlay(d_hode:240)
      *
      * Datakø - inn (ordre-linje)
      *
     d                 ds
     d d_linj                       128
     d  d_vare                       16    overlay(d_linj: 1)
     d  d_enhe                        3    overlay(d_linj:17)
     d  d_anta                       11  3 overlay(d_linj:20)
     d  d_llma                       20    overlay(d_linj:31)
     d  d_llda                        8    overlay(d_linj:51)
     d  d_lmld                       70    overlay(d_linj:59)
      *
      * Definering av variable i nøkler
      *
     d nohel6_fsys     s                   like(nofsys)
     d nohel6_nref     s                   like(nonref)
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d vvarl1_vare     s                   like(vvvare)
     d nkeol1_kode     s                   like(nkkode)
      *
      * Definering av variable
      *
     d b_frst          s              1    inz(*off)
      *
     d w_dato_6        s              6  0
      *
     d w_firm          s                   like(nofirm)
     d w_fsys          s                   like(nofsys)
     d w_line          s                   like(ndline)
      *
     d w_kpro          s              6  0
     d w_tkod          s              1    inz('0')
     d w_fell          s              6
     d w_ttyp          s              2    inz(*blank)
     d w_fast          s             10    inz(*blank)
6.nu d w_numm          s              8  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      w_fsys = 'WAP'
      *
     c                   eval      b_frst = *on
      *
      * Leser og behandler datakø (input)
      *
     c                   dou       d_inpu = *blank
      *
     c                   eval      d_inpu = *blank
      *
     c                   call      'QRCVDTAQ'
     c                   parm                    p_dtqi
     c                   parm                    p_bibl
     c                   parm                    p_inpl
     c                   parm                    d_inpu
     c                   parm                    p_wait
     c                   parm                    p_keyo
     c                   parm                    p_keyl
     c                   parm                    p_uniq
     c                   parm                    p_sndl
     c                   parm                    p_sndi
      *
     c                   if        d_inpu <> *blank
     c                   exsr      behandling
     c                   endif
      *
     c                   enddo
      *
      * Sender melding til ordrebehandlere
      *
     c***                exsr      send_melding
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av ordreinngang
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   if        b_frst = *on
     c                   exsr      behandl_hode
     c                   else
     c                   exsr      behandl_linje
     c                   endif
      *
     c                   eval      b_frst = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av ordre-hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_hode  begsr
      *
     c                   eval      d_hode = d_inpu
      *
     c                   movel     d_kpro        w_kpro
      *
      * Avryter dersom referanse finnes fra før
      *
     c                   eval      nohel6_fsys = w_fsys
     c                   eval      nohel6_nref = d_refn
     c     nohel6_key    chain     nohel6
     c                   if        %found
     c                   eval      p_feil = '2'
     c                   goto      avslutt
     c                   endif
      *
      * Initierer linjenummer
      *
     c                   eval      w_line = 0
      *
      * Skriver til ekstern ordre-hode
      *
     c                   eval      nofirm = w_firm
5.10 c                   eval      nofsys = w_fsys
5.10 c                   eval      notell = 0
     c                   eval      nonref = d_refn
     c                   eval      nofrsp = 0
     c                   eval      nolage = 0
     c                   eval      nouser = *blank
     c                   eval      nokund = d_kund
     c                   eval      noknav = *blank
     c                   eval      nokpro = w_kpro
     c                   eval      nopnav = *blank
     c                   eval      nonavn = %trim(d_ladr)
     c                   eval      noadr1 = *blank
     c                   eval      noadr2 = *blank
     c                   eval      noponr = *blank
     c                   eval      nosted = *blank
     c                   eval      nolety = 0
     c                   eval      noldat = *loval
     c                   eval      nodref = %trim(d_dref)
     c                   eval      norekv = %trim(d_rekv)
     c                   eval      nomeld = %trim(%trim(d_hlma) + ' ' +
     c                                            %trim(d_hmld))
     c                   eval      nounav = %trim(d_unav)
     c                   eval      noutlf = *blank
     c                   eval      noumob = *blank
     c                   eval      noufax = *blank
     c                   eval      noumai = %trim(d_umai)
     c                   eval      nonumm = 0
     c                   eval      noeusr = *blank
      *
     c                   time                    nobdat
     c                   time                    noodat
     c                   time                    nootim
     c                   time                    noedat
     c                   time                    noetim
      *
     c                   eval      rkunl1_kund = nokund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      noknav = rknavn
     c                   endif
      *
     c                   if        nokpro <> 0
     c                   eval      fkprl1_kund = nokund
     c                   eval      fkprl1_kpro = nokpro
     c     fkprl1_key    chain     fkprl1
     c                   if        %found
     c                   eval      nopnav = flnavn
     c                   endif
     c                   endif
      *
     c                   if        d_hlda <> *blank
     c                   move      d_hlda        w_dato_6
     c     *ymd          move      w_dato_6      noldat
     c                   endif
      *
      * Henter neste nummer fra nummer-serie
      *
     c                   eval      nkeol1_kode = %trim(w_fsys)
     c     nkeol1_key    chain     nkeol1r
     c                   if        %found
     c                   eval      w_fell = %trim(nkteln)
     c                   else
     c                   eval      w_fell = 'FWAPOR'
     c                   endif
      *
     c                   call      'AS100R'
     c                   parm                    w_tkod
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_ttyp
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
     c                   eval      notell = w_numm
      *
      * Skriver til fil
      *
6.10 c                   time                    noodat
6.10 c                   time                    nootim
6.10 c                   eval      noousr = l_user
6.10 c                   time                    noedat
6.10 c                   time                    noetim
6.10 c                   eval      noeusr = l_user
     c                   write     nohelur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av ordre-linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_linje begsr
      *
     c                   eval      d_linj = d_inpu
      *
      * Skriver til ekstern ordre-linje
      *
     c                   eval      w_line = w_line + 1
      *
     c                   eval      ndfirm = w_firm
     c                   eval      ndfsys = nofsys
     c                   eval      ndtell = notell
     c                   eval      ndnref = nonref
     c                   eval      ndline = w_line
     c                   eval      ndvare = d_vare
     c                   eval      ndtek1 = *blank
     c                   eval      ndtek2 = *blank
     c                   eval      ndanta = d_anta
     c                   eval      ndenhe = %trim(d_enhe)
     c                   eval      ndntpr = 0
     c                   eval      ndlety = 0
     c                   eval      ndldat = *loval
      *
     c                   time                    ndodat
6.10 c                   time                    ndotim
6.10 c                   eval      ndousr = l_user
     c                   time                    ndedat
     c                   time                    ndetim
6.10 c                   eval      ndeusr = l_user
      *
     c                   eval      vvarl1_vare = ndvare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      ndtek1 = vvtek1
     c                   eval      ndtek2 = vvtek2
     c                   endif
      *
     c                   if        d_llda <> *blank
     C                   move      d_llda        w_dato_6
     c     *ymd          move      w_dato_6      ndldat
     c                   endif
      *
     c                   write     nodtlur
      *
      * Dersom melding, legges denne ut som egen linje
      *
     c                   if        d_lmld <> *blank
      *
     c                   eval      d_lmld = %trim(d_lmld)
      *
     c                   eval      w_line = w_line + 1
      *
     c                   eval      d_lmld = %trim(%trim(d_llma) + ' ' +
     c                                            %trim(d_lmld))
      *
     c                   eval      ndline = w_line
     c                   eval      ndvare = *blank
     c                   eval      ndtek1 = %subst(d_lmld:1:35)
     c                   eval      ndtek2 = %subst(d_lmld:36:35)
     c                   eval      ndanta = 0
     c                   eval      ndenhe = *blank
     c                   eval      ndntpr = 0
     c                   eval      ndlety = 0
     c                   eval      ndldat = *loval
      *
     c                   time                    ndodat
6.10 c                   time                    ndotim
6.10 c                   eval      ndousr = l_user
     c                   time                    ndedat
     c                   time                    ndetim
6.10 c                   eval      ndeusr = l_user
      *
     c                   write     nodtlur
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sende melding til ordrebehandlere
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     send_melding  begsr
      *
     c     fusrl1_key    setll     fusrl1
     c     fusrl1_key    reade     fusrl1
     c                   dow       not %eof
      *
     c                   if        fbko10 = 1
     c                   call      'NN821C'
     c                   parm                    fbuser
     c                   endif
      *
     c     fusrl1_key    reade     fusrl1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      p_feil = '1'
     c                   goto      avslutt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_avde
     c                   parm                    p_uniq
     c                   parm                    p_dtqi
     c                   parm                    p_dtqo
     c                   parm                    p_bibl
     c                   parm                    p_feil
     c                   parm                    p_inpl
     c                   parm                    p_outl
     c                   parm                    p_wait
     c                   parm                    p_keyo
     c                   parm                    p_keyl
     c                   parm                    p_sndl
     c                   parm                    p_sndi
      *
      * Key for oppslag på ekstern ordre-hode
      *
     c     nohel6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    nohel6_fsys
     c                   kfld                    nohel6_nref
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på ekstern kode
      *
     c     nkeol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    nkeol1_kode
      *
      * Key for oppslag på ofak-bruker
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
      *
      *
      * Hent firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
