     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JM113R
      * Beskrivelse . . : Vedlikehold av kampanjepriser, påslag til
      *                   kostpris
      *
      * Spesialversjon. :
      * Tilpasset for . :
7.01  * Tilpasset for . : Mestergruppen
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       131097 HKO  Nytt program
7.xx  * 7.00  171116 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fjkavl2    if   e           k disk    rename(jkavpfr:jkavl2r)
     fjkavlu    uf   e           k disk    rename(jkavpfr:jkavlur)
     fjvarlr    if   e           k disk    rename(jvarpfr:jvarlrr)
      *
     fjm113d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_kamp          s              5
     d p_user          s             10
      *
      * Definering av variabler i nøkler
      *
     d jlevl1_ldor     s                   like(jlldor)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d jkavl2_vkam     s                   like(jmvkam)
7.01 d jkavlu_vprg     s                   like(jmvprg)
     d jkavl2_vldo     s                   like(jmvldo)
     d jkavlu_vkam     s                   like(jmvkam)
     d jkavlu_vvar     s                   like(jmvvar)
     d jvarlr_vare     s                   like(jvvare)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s              3  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandler bildet
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * F3=Avslutt
      *
     c                   if        *inkc or *inkl
     c                   goto      avslutt
     c                   endif
      *
      * F04=Spørring
      *
     c                   if        *inkd
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endif
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_inp
     c                   if        b_feil = *on
     c                   eval      b_feil = *off
     c                   goto      b1taga
     c                   endif
      *
      * Oppdaterer kampanje-vare
      *
     c                   exsr      oppdater
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av spørringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Prisgivende leverandør
      *
     c                   if        w_afld = 'B1PLDO'
     c                   call      'JL500R'
     c                   parm                    b1pldo
     c                   parm                    b1lnav
     c                   goto      end_spØrr
     c                   endif
      *
      * Varegruppe
      *
     c                   if        w_afld = 'B1OGRP'
     c                   eval      b1hgrp = 0
     c                   eval      b1ugrp = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1utxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'B1HGRP'
     c                   eval      b1ugrp = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    b1utxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'B1UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    b1ugrp
     c                   parm                    b1utxt
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_inp     begsr
      *
     c                   eval      b_feil = *off
      *
      * Leverandør
      *
     c                   eval      b1lnav = *blank
     c                   if        b1pldo = 0
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   eval      jlevl1_ldor = b1pldo
     c     jlevl1_key    chain     jlevl1                             32
     c                   if        *in32  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1lnav = jlnavn
      *
      * Overgruppe
      *
     c                   eval      b1utxt = *blank
     c                   if        b1ogrp <> 0 and
     c                             b1hgrp =  0 and
     c                             b1ugrp =  0
     c                   eval      vogrl1_oogr = b1ogrp
     c     vogrl1_key    chain     vogrl1                             33
     c                   if        *in33  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1utxt = vgotxt
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        b1hgrp <> 0 and
     c                             b1ugrp =  0
     c                   eval      vhgrl1_hogr = b1ogrp
     c                   eval      vhgrl1_hhgr = b1hgrp
     c     vhgrl1_key    chain     vhgrl1                             34
     c                   if        *in34  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1utxt = vghtxt
     c                   endif
      *
      * Undergruppe
      *
     c                   if        b1ugrp <> 0
     c                   eval      vugrl1_uogr = b1ogrp
     c                   eval      vugrl1_uhgr = b1hgrp
     c                   eval      vugrl1_uugr = b1ugrp
     c     vugrl1_key    chain     vugrl1                             35
     c                   if        *in35  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1utxt = vgutxt
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Leser vareregisteret, og kaller fellesprogram for oppretting/
      * oppdatering av kampanjevarer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
      * Leser kampanje-vareregister for leverandør
      *
     c                   eval      jkavl2_vkam = p_kamp
     c                   eval      jkavl2_vldo = b1pldo
     c     jkavl2_key    setll     jkavl2
     c     jkavl2_key    reade     jkavl2                                 90
     c                   dow       *in90 = *off
      *
      *  Sjekker om vare skal være med i utplukket
      *
     c                   eval      jvarlr_vare = jmvvar
     c     jvarlr_key    chain     jvarlr                             91
     c     *in91         ifeq      *on
     c                   goto      les_neste
     c                   endif
      *
     c                   if        b1ogrp <> 0 and
     c                             b1ogrp <> jvogrp
     c                   goto      les_neste
     c                   endif
      *
     c                   if        b1hgrp <> 0 and
     c                             b1hgrp <> jvhgrp
     c                   goto      les_neste
     c                   endif
      *
     c                   if        b1ugrp <> 0 and
     c                             b1ugrp <> jvugrp
     c                   goto      les_neste
     c                   endif
      *
      *  Oppdaterer kostpris på kampanjevare
      *
     c                   eval      jkavlu_vkam = jmvkam
7.01 c                   eval      jkavlu_vprg = jmvprg
     c                   eval      jkavlu_vvar = jmvvar
     c     jkavlu_key    chain     jkavlur                            92
     c     *in92         ifeq      *off
     c                   eval      jmvkpr = jmvkai + (jmvkai * b1pslk / 100)
     c                   eval      jmveus = p_user
     c                   time                    jmveti
     c                   time                    jmveda
     c                   update    jkavlur
     c                   endif
      *
     c     les_neste     tag
      *
     c     jkavl2_key    reade     jkavl2                                 90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kamp
     c                   parm                    p_user
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for les på kampanje-vare
      *
     c     jkavl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jkavl2_vkam
     c                   kfld                    jkavl2_vldo
      *
      * Key for oppdatering av kampanje-vare
      *
     c     jkavlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jkavlu_vkam
7.01 c                   kfld                    jkavlu_vprg
     c                   kfld                    jkavlu_vvar
      *
      * Key for les på vare
      *
     c     jvarlr_key    klist
     c                   kfld                    jvarlr_vare
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
