     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : NM700R
      * Beskrivelse . . : Nettbutikk, eksport av kundeinformasjon
      *
      * Spesialversjon. : Utvidet kundeinformasjon - kopi av NN700R
      * Tilpasset for . : Mestergruppen
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       150316 ASK  Nytt program
9.01  *  9.01 311218 ASK  Lagt inn oppslag mot RKBPPF for å hente proff-
      *                   portal tilgang.
9.02  *  9.02 050319 bsa  Sender prosjekt uten utfylt fradato
9.04  *  9.04 050521 hko  Setter kunde mail-adresse lik kontaktperson
9.04  *                   mail-adresse dersom denne er utfylt
9.05  *  8.00 110423 sst  Endret Ehandel til EHANDEL vedr roller
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
9.01 frkbplr    if   e           k disk    rename(rkbppfr:rkbplrr)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     ffkmalr    if   e           k disk    rename(fkmapfr:fkmalrr)
E5.50frukpl6    if   e           k disk    rename(rukppfr:rukpl6r)
     fapospf    if   e           k disk    rename(apospfr:apospfr)
     fanbll3    if   e           k disk    rename(anblpfr:anbll3r)
     fanbhl1    if   e           k disk    rename(anbhpfr:anbhl1r)
6.13 fekbtir    if   e           k disk    rename(ekbtst:ekbtirr)
     fckgrl2    if   e           k disk    rename(ckgrpfr:ckgrl2r)
     fckkllr    if   e           k disk    rename(ckklpfr:ckkllrr)
      *
     fnetkund   uf a e           k disk
     fnetkrapp  o  a e             disk    rename(netkrapp:netkrappr)
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_fgr                 931    933
      *
      * Definering av datastrukturer
      *
      * Recordbeskrivelse - meldingshode
      *
     d                 ds
     d d_unh                         41
     d  d_unhrcd                      3    overlay(d_unh: 1) inz('UNH')
     d  d_unhfill1                    1    overlay(d_unh: 4) inz(';')
     d  d_unhrefno                   26    overlay(d_unh: 5)
     d  d_unhfill2                    1    overlay(d_unh:31) inz(';')
     d  d_unhtypeid                   6    overlay(d_unh:32) inz('IMLIMP')
     d  d_unhfill3                    1    overlay(d_unh:38) inz(';')
     d  d_unhversion                  3    overlay(d_unh:39) inz('1.0')
      *
      * Recordbeskrivelse - meldingsstart
      *
     d                 ds
     d d_bgm                         16
     d  d_bgmrcd                      3    overlay(d_bgm: 1) inz('BGM')
     d  d_bgmfill1                    1    overlay(d_bgm: 4) inz(';')
     d  d_bgmetypekod                 9    overlay(d_bgm: 5) inz('IMLCUSIMP')
     d  d_bgmfill2                    2    overlay(d_bgm:14) inz(';;')
     d  d_bgmfunction                 1    overlay(d_bgm:16) inz('1')
      *
      * Recordbeskrivelse - organisasjon
      *
     d                 ds
     d d_org                        126
     d  d_orgrcd                      3    overlay(d_org: 1) inz('ORG')
     d  d_orgfill1                    1    overlay(d_org: 4) inz(';')
     d  d_orgid                      30    overlay(d_org: 5)
     d  d_orgfill2                    1    overlay(d_org:35) inz(';')
     d  d_orgstoreid                 30    overlay(d_org:36)
     d  d_orgfill3                    1    overlay(d_org:66) inz(';')
     d  d_orgname                    60    overlay(d_org:67)
      *
      * Recordbeskrivelse - kunde
      *
     d                 ds
     d d_cus                        650
     d  d_cusrcd                      3    overlay(d_cus:  1) inz('CUS')
     d  d_cusfill1                    1    overlay(d_cus:  4) inz(';')
     d  d_cusnum                      6  0 overlay(d_cus:  5)
     d  d_cusfill2                    1    overlay(d_cus: 11) inz(';')
     d  d_cusorg                      9  0 overlay(d_cus: 12)
     d  d_cusfill3                    1    overlay(d_cus: 21) inz(';')
     d  d_cusnam                     35    overlay(d_cus: 22)
     d  d_cusfill4                    2    overlay(d_cus: 57) inz(';;')
     d  d_cusad1                     35    overlay(d_cus: 59)
     d  d_cusfill5                    1    overlay(d_cus: 94) inz(';')
     d  d_cusad2                     35    overlay(d_cus: 95)
     d  d_cusfill6                    1    overlay(d_cus:130) inz(';')
     d  d_cuspon                      4  0 overlay(d_cus:131)
     d  d_cusfill7                    1    overlay(d_cus:135) inz(';')
     d  d_cuspad                     35    overlay(d_cus:136)
     d  d_cusfill8                    1    overlay(d_cus:171) inz(';')
     d  d_cuscty                      2    overlay(d_cus:172)
     d  d_cusfill9                    1    overlay(d_cus:174) inz(';')
     d  d_cusmai                    100    overlay(d_cus:175)
     d  d_cusfill10                   1    overlay(d_cus:275) inz(';')
     d  d_custlf                     15    overlay(d_cus:276)
     d  d_cusfill11                   1    overlay(d_cus:291) inz(';')
     d  d_cusmob                     15    overlay(d_cus:292)
     d  d_cusfill12                   1    overlay(d_cus:307) inz(';')
     d  d_cusfax                     15    overlay(d_cus:308)
     d  d_cusfill13                   1    overlay(d_cus:323) inz(';')
     d  d_cuscre                      1  0 overlay(d_cus:324)
     d  d_cusfill14                   1    overlay(d_cus:325) inz(';')
     d  d_cuscop                     35    overlay(d_cus:326)
     d  d_cusfill15                   1    overlay(d_cus:361) inz(';')
     d  d_cuscml                    100    overlay(d_cus:362)
     d  d_cusfill16                   1    overlay(d_cus:462) inz(';')
     d  d_cusctl                     15    overlay(d_cus:463)
     d  d_cusfill17                   1    overlay(d_cus:478) inz(';')
     d  d_cuscmo                     15    overlay(d_cus:479)
     d  d_cusfill18                   1    overlay(d_cus:494) inz(';')
     d  d_cuscfx                     15    overlay(d_cus:495)
     d  d_cusfill19                   1    overlay(d_cus:510) inz(';')
     d  d_cuslty                      1    overlay(d_cus:511)
     d  d_cusfill20                   1    overlay(d_cus:512) inz(';')
     d  d_cuslag                      2  0 overlay(d_cus:513)
     d  d_cusfill21                   1    overlay(d_cus:515) inz(';')
     d  d_cuskkat                     4  0 overlay(d_cus:516)
     d  d_cusfill22                   1    overlay(d_cus:520) inz(';')
     d  d_cusselg                     4  0 overlay(d_cus:521)
     d  d_cusfill23                   1    overlay(d_cus:525) inz(';')
     d  d_cussort                    15    overlay(d_cus:526)
     d  d_cusfill24                   1    overlay(d_cus:541) inz(';')
     d  d_cusdist                     4  0 overlay(d_cus:542)
     d  d_cusfill25                   1    overlay(d_cus:546) inz(';')
     d  d_cusbsal                     7  0 overlay(d_cus:547)
     d  d_cusfill26                   1    overlay(d_cus:554) inz(';')
     d  d_cusbdat                     6  0 overlay(d_cus:555)
     d  d_cusfill27                   1    overlay(d_cus:561) inz(';')
     d  d_cusgln                     13  0 overlay(d_cus:562)
     d  d_cusfill28                   1    overlay(d_cus:575) inz(';')
     d  d_cusclcde                    2  0 overlay(d_cus:576)
     d  d_cusfill29                   1    overlay(d_cus:578) inz(';')
     d  d_cuscltxt                   30    overlay(d_cus:579)
     d  d_cusfill30                   1    overlay(d_cus:609) inz(';')
9.02 d  d_cusorgid                   25    overlay(d_cus:610)
9.03 d  d_cusfill31                   1    overlay(d_cus:635) inz(';')
9.03 d  d_cusacces                    2    overlay(d_cus:636)
      *
      * Recordbeskrivelse - kundeprosjekt
      *
     d                 ds
     d d_cpr                        460
     d  d_cprrcd                      3    overlay(d_cpr:  1) inz('CPR')
     d  d_cprfill1                    1    overlay(d_cpr:  4) inz(';')
     d  d_cprcun                      6  0 overlay(d_cpr:  5)
     d  d_cprfill2                    1    overlay(d_cpr: 11) inz(';')
     d  d_cprprn                      6  0 overlay(d_cpr: 12)
     d  d_cprfill3                    1    overlay(d_cpr: 18) inz(';')
     d  d_cprnam                     35    overlay(d_cpr: 19)
     d  d_cprfill4                    1    overlay(d_cpr: 54) inz(';')
     d  d_cprad1                     35    overlay(d_cpr: 55)
     d  d_cprfill5                    1    overlay(d_cpr: 90) inz(';')
     d  d_cprad2                     35    overlay(d_cpr: 91)
     d  d_cprfill6                    1    overlay(d_cpr:126) inz(';')
     d  d_cprpon                      4  0 overlay(d_cpr:127)
     d  d_cprfill7                    1    overlay(d_cpr:131) inz(';')
     d  d_cprpad                     35    overlay(d_cpr:132)
     d  d_cprfill8                    1    overlay(d_cpr:167) inz(';')
     d  d_cprcop                     35    overlay(d_cpr:168)
     d  d_cprfill9                    1    overlay(d_cpr:203) inz(';')
     d  d_cprcml                    100    overlay(d_cpr:204)
     d  d_cprfill10                   1    overlay(d_cpr:304) inz(';')
     d  d_cprctl                     15    overlay(d_cpr:305)
     d  d_cprfill11                   1    overlay(d_cpr:320) inz(';')
     d  d_cprcmo                     15    overlay(d_cpr:321)
     d  d_cprfill12                   1    overlay(d_cpr:336) inz(';')
     d  d_cprcfx                     15    overlay(d_cpr:337)
     d  d_cprfill13                   1    overlay(d_cpr:352) inz(';')
     d  d_cprkom                      4  0 overlay(d_cpr:353)
     d  d_cprfill14                   1    overlay(d_cpr:357) inz(';')
     d  d_cprgdn                      5  0 overlay(d_cpr:358)
     d  d_cprfill15                   1    overlay(d_cpr:363) inz(';')
     d  d_cprbrn                      5  0 overlay(d_cpr:364)
     d  d_cprfill16                   1    overlay(d_cpr:369) inz(';')
     d  d_cprfen                      5  0 overlay(d_cpr:370)
     d  d_cprfill17                   1    overlay(d_cpr:375) inz(';')
     d  d_cprsen                      5  0 overlay(d_cpr:376)
     d  d_cprfill18                   1    overlay(d_cpr:381) inz(';')
     d  d_cprben                      5    overlay(d_cpr:382)
     d  d_cprfill19                   1    overlay(d_cpr:387) inz(';')
     d  d_cprkpn                     15    overlay(d_cpr:388)
     d  d_cprfill20                   1    overlay(d_cpr:403) inz(';')
     d  d_cprfda                      6  0 overlay(d_cpr:404)
     d  d_cprfill21                   1    overlay(d_cpr:410) inz(';')
     d  d_cprtda                      6  0 overlay(d_cpr:411)
     d  d_cprfill22                   1    overlay(d_cpr:417) inz(';')
     d  d_cprbmn                     15    overlay(d_cpr:418)
     d  d_cprfill23                   1    overlay(d_cpr:433) inz(';')
     d  d_cprdiv                      1    overlay(d_cpr:434)
     d  d_cprfill24                   1    overlay(d_cpr:435) inz(';')
9.02 d  d_cprorgid                   25    overlay(d_cpr:436)
      *
      * Recordbeskrivelse - meldingshale
      *
     d                 ds
     d d_unt                         43
     d  d_untrcd                      3    overlay(d_unt: 1) inz('UNT')
     d  d_untfill1                    1    overlay(d_unt: 4) inz(';')
     d  d_untnosegmen                12  0 overlay(d_unt: 5)
     d  d_untfill2                    1    overlay(d_unt:17) inz(';')
     d  d_untrefno                   26    overlay(d_unt:18)
      *
      * Definering av variabler i nøkler
      *
     d fkprl1_kund     s                   like(flkund)
9.01 d rkbplr_bkun     s                   like(rkbkun)
     d fkmalr_kund     s                   like(flmkun)
     d fkmalr_pros     s                   like(flmkpr)
E5.50d rukpl6_kund     s                   like(rukund)
     d apospf_ponr     s                   like(apponr)
     d anbll3_fgr      s                   like(aolfgr)
     d anbll3_fir      s                   like(aolfir)
6.13 d ekbtir_tbku     s                   like(ektbku)
     d ckgrl2_gkat     s                   like(ckgkat)
     d ckkllr_kkla     s                   like(ckkkla)
      *
      * Definering av variable
      *
     d w_tmst          s               z
     d w_tell          s             12  0
     d w_ponr          s              4
      *
     d w_firm          s                   like(rkfirm)
     d w_dato          s                   like(flfdat)
      *
      * Definering av parametre
      *
     d p_stat          s              1
      *
      * Definering av konstanter
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøå'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅ'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      p_stat = *blank
      *
      * Henter data om nettbutikk og eier. Avslutter hvis ingen finnes.
      *
     c                   eval      anbll3_fgr = l_fgr
     c                   eval      anbll3_fir = l_firm
     c     anbll3_key    chain     anbll3r
     c                   if        not %found
     c                   eval      p_stat = 'E'
     c                   goto      avslutt
     c                   endif
     c     aoleie        chain     anbhl1r
     c                   if        not %found
     c                   eval      p_stat = 'E'
     c                   goto      avslutt
     c                   endif
      *
      * Meldingshode
      *
     c                   time                    w_tmst
     c                   time                    w_dato
     c                   move      w_tmst        d_unhrefno
      *
     c                   eval      data = d_unh
     c                   write     netkundr
      *
      * Meldingsstart
      *
     c                   eval      data = d_bgm
     c                   write     netkundr
      *
      * Organisasjon
      *
     c                   eval      d_orgid = aoheie
     c                   eval      d_orgstoreid = aolbut
     c                   eval      d_orgname = aohnav
      *
     c                   eval      data = d_org
     c                   write     netkundr
      *
      * Kunde / kundeprosjekt / kontaktperson
      *
     c     rkunl1_key    setll     rkunl1
     c     rkunl1_key    reade     rkunl1
     c                   dow       %eof = *off
      *
9.01  * Slår opp mot tilleggsregister for å finne tilgangskode
9.01  *
9.01 c                   eval      rkbplr_bkun = rkkund
9.01 c     rkbplr_key    chain     rkbplrr
9.01 c                   if        not %found
9.01 c                   eval      rkprti = 0
9.01 c                   endif
9.01 c                   if        rkprti > 0 and
     c                             rkemal <> *blank
      *
     c                   if        rkftnr = 0
     c                   eval      SrcDta = %trim(rknavn) +
     c                             ' mangler ' +
     c                             'org-nr. Kunden opprettes ' +
     c                             'som privatkunde.'
     c                   write     netkrappr
     c                   eval      p_stat = 'R'
     c                   endif
      *
     c                   if        %subst(rksted:6:25) = *blank
     c                   eval      SrcDta = %trim(rknavn) +
     c                             ' mangler ' +
     c                             'postadresse, og utelates'
     c                   write     netkrappr
     c                   eval      p_stat = 'R'
     c     rkunl1_key    reade     rkunl1
     c                   iter
     c                   endif
      *
     c                   eval      w_tell = w_tell + 1
      *
     c                   eval      d_cusnum = rkkund
     c                   eval      d_cusorg = rkftnr
     c                   eval      d_cusnam = rknavn
     c                   eval      d_cusad1 = rkgate
     c                   eval      d_cusad2 = rkadr2
     c                   eval      d_cuspon = rkponr
     c                   eval      d_cuspad = %subst(rksted:6:25)
     c                   eval      d_cuscty = 'NO'
     c     up:lo         xlate     rkemal        rkemal
     c                   eval      d_cusmai = rkemal
     c                   eval      d_custlf = rktlfn
     c                   eval      d_cusmob = rkmobn
     c                   eval      d_cusfax = rktlfx
     c                   eval      d_cuscre = 0
     c                   eval      d_cuslty = '1'
     c                   eval      d_cuslag = rklagr
     c                   eval      d_cuskkat = rkkatg
     c                   eval      d_cusselg = rkslgr
     c                   eval      d_cussort = *blank
     c                   eval      d_cusdist = rkdist
     c                   eval      d_cusgln  = rkeank
9.02 c                   eval      d_cusorgid = aoheie
9.01 c                   movel     rkprti        d_cusacces
      *
      * Finner overordnet kundeklasifisering
      *
     c                   eval      ckgrl2_gkat = rkkatg
     c     ckgrl2_key    chain     ckgrl2
     c                   if        %found(ckgrl2)
     c                   eval      ckkllr_kkla = ckgkla
     c     ckkllr_key    chain     ckkllr
     c                   eval      d_cusclcde = ckgkla
     c                   eval      d_cuscltxt = ckktxt
     c                   endif
      *
     c                   if        rkland <> *blank
     c                   eval      d_cuscty = rkland
     c                   endif
      *
     c                   exsr      kontpers
     c                   exsr      kunbonus
     c                   eval      data = d_cus
     c                   write     netkundr
      *
     c                   eval      fkprl1_kund = rkkund
     c     fkprl1_key    setll     fkprl1
     c     fkprl1_key    reade     fkprl1
     c                   dow       %eof = *off
      *
     c                   if        fltdat >= w_dato
     c                   eval      w_tell = w_tell + 1
      *
     c                   eval      d_cprcun = flkund
     c                   eval      d_cprprn = flkpro
     c                   eval      d_cprnam = flnavn
     c                   eval      d_cprad1 = fladr1
     c                   eval      d_cprad2 = fladr2
     c                   eval      d_cprpon = flponr
     c                   eval      d_cprpad = *blank
     c                   eval      d_cprcop = flkprs
     c     up:lo         xlate     flmail        flmail
     c                   eval      d_cprcml = flmail
     c                   eval      d_cprctl = fltlfa
     c                   eval      d_cprcmo = fltlfm
     c                   eval      d_cprcfx = *blank
     c                   eval      d_cprkpn = flepro
9.02 c                   if        flfdat <> *loval
     c     *ymd          move      flfdat        d_cprfda
9.02 c                   else
9.02 c                   eval      d_cprfda = 0
9.02 c                   endif
     c     *ymd          move      fltdat        d_cprtda
      *
     c                   if        flponr <> 0
     c                   eval      apospf_ponr = flponr
     c     apospf_key    chain     apospf
     c                   if        %found
     c                   eval      d_cprpad = apsted
     c                   endif
     c                   endif
      *
      * Leser tilleggsfil med matrikkelinformasjon
      *
     c                   eval      fkmalr_kund = flkund
     c                   eval      fkmalr_pros = flkpro
     c     fkmalr_key    chain     fkmalr
     c                   if        %found
     c                   eval      d_cprbmn = flbonr
     c                   eval      d_cprkom = flmkom
     c                   eval      d_cprgdn = flmgdn
     c                   eval      d_cprbrn = flmbrn
     c                   eval      d_cprfen = flmfen
     c                   eval      d_cprsen = flmsen
     c                   eval      d_cprben = flmben
     c                   else
     c                   eval      d_cprbmn = *blank
     c                   eval      d_cprkom = 0
     c                   eval      d_cprgdn = 0
     c                   eval      d_cprbrn = 0
     c                   eval      d_cprfen = 0
     c                   eval      d_cprsen = 0
     c                   eval      d_cprben = *blank
     c                   endif
      *
      * Sjekker om diverseprosjekt
      *
     c                   if        flkpro > 900000
     c                   eval      d_cprdiv = 'D'
     c                   else
     c                   eval      d_cprdiv = ' '
     c                   endif
9.02 c                   eval      d_cprorgid = aoheie
      *
     c                   eval      data = d_cpr
     c                   write     netkundr
     c                   endif
      *
     c     fkprl1_key    reade     fkprl1
     c                   enddo
      *
     c                   endif
     c                   if        rkemal = *blank and
     c                             rkprti > 0
     c                   eval      SrcDta = %trim(rknavn) +
     c                             ' mangler ' +
     c                             'E-post adresse, og utelates'
     c                   write     netkrappr
     c                   eval      p_stat = 'R'
     c                   endif
      *
     c     rkunl1_key    reade     rkunl1
     c                   enddo
      *
      * Meldingshale
      *
     c                   eval      d_untnosegmen = w_tell
     c                   eval      d_untrefno = d_unhrefno
      *
     c                   eval      data = d_unt
     c                   write     netkundr
      *
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekk og utlegg av kontaktperson Ehandel
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kontpers      begsr
      *
     c                   eval      d_cuscop = *blank
     c                   eval      d_cuscml = *blank
     c                   eval      d_cusctl = *blank
     c                   eval      d_cuscmo = *blank
     c                   eval      d_cuscfx = *blank
      *
E5.50c                   eval      rukpl6_kund = rkkund
E5.50c     rukpl6_key    setll     rukpl6
      *
E5.50c     rukpl6_key    reade     rukpl6
     c                   dow       not %eof
9.05 c                   if        rukrol = 'EHANDEL'
     c                   eval      d_cuscop = %trim(ruenav) + ', '
     c                                      + %trim(rufnav)
9.04 c***                eval      d_cuscml = rumail
9.04 c                   eval      d_cuscml = %trim(rumail)
9.04 c                   if        rumail <> *blank
9.04 c                   eval      d_cusmai = %trim(rumail)
9.04 c                   endif
     c                   eval      d_cusctl = ruktlf
     c                   eval      d_cuscmo = rumtlf
     c                   eval      d_cuscfx = rukfax
     c                   goto      end_kont
     c                   endif
      *
E5.50c     rukpl6_key    reade     rukpl6
     c                   enddo
      *
     c     end_kont      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekk og utlegg av kundebonus
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kunbonus      begsr
      *
     c                   eval      d_cusbsal = 0
     c                   eval      d_cusbdat = 0
      *
6.13 c                   eval      ekbtir_tbku = rkkund
6.13 c     ekbtir_key    setll     ekbtir
6.13 c     ekbtir_key    reade     ekbtir
      *
6.13 c                   dow       not %eof(ekbtir)
6.13 c                   eval      d_cusbsal = d_cusbsal + ektsbo
6.13 c                   if        ektbda <> *loval
6.13 c     *dmy          move      ektbda        d_cusbdat
     c                   endif
      *
6.13 c     ekbtir_key    reade     ekbtir
     c                   enddo
      *
     c     end_bonus     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_stat
      *
      * Key for les på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
9.01  *
9.01  * Key for les på kunde tilleggsregister
9.01  *
9.01 c     rkbplr_key    klist
9.01 c                   kfld                    w_firm
9.01 c                   kfld                    rkbplr_bkun
      *
      * Key for les på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
      *
      * Key for les på kundeprosjekt matrikkelinfo
      *
     c     fkmalr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkmalr_kund
     c                   kfld                    fkmalr_pros
      *
E5.50 * Key for les av kontaktperson på kundenummer
      *
E5.50c     rukpl6_key    klist
     c                   kfld                    w_firm
E5.50c                   kfld                    rukpl6_kund
      *
      * Key for oppslag på poststed-register
      *
     c     apospf_key    klist
     c                   kfld                    apospf_ponr
      *
      * Key for oppslag på nettbutikk
      *
     c     anbll3_key    klist
     c                   kfld                    anbll3_fgr
     c                   kfld                    anbll3_fir
6.13  *
6.13  * Key for les på kunde bonus
6.13  *
6.13 c     ekbtir_key    klist
6.13 c                   kfld                    w_firm
6.13 c                   kfld                    ekbtir_tbku
      *
      * Key for les på kobling kundekategori - kjedekaegori
      *
     c     ckgrl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ckgrl2_gkat
      *
      * Key for les på kjedekategori
      *
     c     ckkllr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ckkllr_kkla
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
