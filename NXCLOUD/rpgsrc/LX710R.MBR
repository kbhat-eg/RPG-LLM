     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LX710R
      * Beskrivelse . . : Flytter lagerinformasjon mellom lagre
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       260107 ASK  Nytt program
      * 6.10  260609 BSA  Oppdatert med sporingsinformasjon
6.20  *       310812 bof  Nytt felt, endringsdato for faste data som vises
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvlagl2    if   e           k disk    rename(vlagpfr:vlagl2r)
     fvlaglu    uf a e           k disk    rename(vlagpfr:vlaglur)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av variable i nøkler
      *
     d vlagl2_lage     s                   like(vllage)
     d vlaglu_vare     s                   like(vlvare)
     d vlaglu_lage     s                   like(vllage)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vlfirm)
     d w_mini          s                   like(vlmini)
     d w_maks          s                   like(vlmaks)
     d w_ltid          s                   like(vlltid)
     d w_bpun          s                   like(vlbpun)
     d w_bmin          s                   like(vlbmin)
     d w_bmen          s                   like(vlbmen)
     d w_sikl          s                   like(vlsikl)
     d w_vare          s                   like(vlvare)
     d w_enhl          s                   like(vlenhl)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   exsr      flytt_lager
      *
      * Avslutt program
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å flytte poster på lager til annet lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     flytt_lager   begsr
      *
     c                   eval      vlagl2_lage = 99
     c     vlagl2_key    setll     vlagl2
     c     vlagl2_key    reade     vlagl2
     c                   dow       not %eof
      *
     c                   if        vlmini = 0 and
     c                             vlmaks = 0 and
     c                             vlltid = 0 and
     c                             vlbpun = 0 and
     c                             vlbmin = 0 and
     c                             vlbmen = 0 and
     c                             vlsikl = 0
     c                   goto      les_neste
     c                   endif
      *
     c                   eval      w_mini = vlmini
     c                   eval      w_maks = vlmaks
     c                   eval      w_ltid = vlltid
     c                   eval      w_bpun = vlbpun
     c                   eval      w_bmin = vlbmin
     c                   eval      w_bmen = vlbmen
     c                   eval      w_sikl = vlsikl
     c                   eval      w_vare = vlvare
     c                   eval      w_enhl = vlenhl
      *
     c                   eval      vlaglu_vare = vlvare
     c                   eval      vlaglu_lage = 02
     c     vlaglu_key    chain     vlaglu
     c                   if        %found
     c                   if        vlmini = 0 and
     c                             vlmaks = 0 and
     c                             vlltid = 0 and
     c                             vlbpun = 0 and
     c                             vlbmin = 0 and
     c                             vlbmen = 0 and
     c                             vlsikl = 0
     c                   eval      vlmini = w_mini
     c                   eval      vlmaks = w_maks
     c                   eval      vlltid = w_ltid
     c                   eval      vlbpun = w_bpun
     c                   eval      vlbmin = w_bmin
     c                   eval      vlbmen = w_bmen
     c                   eval      vlsikl = w_sikl
6.10 c                   time                    vledat
6.10 c                   time                    vletim
6.10 c                   eval      vleusr = l_user
6.20 c                   time                    vlfeda
6.20 c                   time                    vlfeti
6.20 c                   eval      vlfeus = l_user
     c                   update    vlaglur
     c                   endif
     c                   else
     c                   clear                   vlaglur
     c                   eval      vlfirm = w_firm
     c                   eval      vllage = 02
     c                   eval      vlvare = w_vare
     c                   eval      vlenhl = w_enhl
     c                   eval      vlmini = w_mini
     c                   eval      vlmaks = w_maks
     c                   eval      vlltid = w_ltid
     c                   eval      vlbpun = w_bpun
     c                   eval      vlbmin = w_bmin
     c                   eval      vlbmen = w_bmen
     c                   eval      vlsikl = w_sikl
6.10 c                   time                    vlodat
6.10 c                   time                    vlotim
6.10 c                   eval      vlousr = l_user
6.10 c                   time                    vledat
6.10 c                   time                    vletim
6.10 c                   eval      vleusr = l_user
     c                   write     vlaglur
     c                   endif
      *
     c     les_neste     tag
     c                   reade     vlagl2
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les på lagerregister
      *
     c     vlagl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl2_lage
      *
      *  Key for oppdatering av LAGER
      *
     c     vlaglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglu_vare
     c                   kfld                    vlaglu_lage
      *
      * Henter firma
      *
     c                   eval      w_firm = 000
      *
     c                   endsr
