      /TITLE  Leverandør, spørring
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RL502R                                              *
      * Beskrivelse . . : Leverandør, spørring                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
T5.00 * V5.00     131202  Flere param. i call RS718R.                    HFL  *
T5.00 * V5.00     180803  Unngår decimat-data error på RLEDIT.           HFL  *
T5.10 * V5.30     060404  Lagt inn vårt kundenummer hos leverandør       KOB  *
T5.30 * V5.30     250804  Saksbehandler er nummerisk (=selger).          HFL  *
T5.30 * V5.30     040205  Lagt inn merke når notatblokk på leverandøren  HFL
T5.33 * V5.40     250505  Lagt inn visning av besøksadresse              KOB
T5.40 * V5.40     170605  Leser RKLAL2 i stedet for RKLALU.              HFL
T5.40 * V5.40     030805  Utvidet parameterliste for RF020R              HFL
T5.40 * V5.50     310107  Lagt ut dato og bruker for opprettelse i F2    KOB
T5.50 * V5.50     160707  Testet på endret dato og la ut opprettet dato. HFL
6.20  * V6.20     110612  Vårt kundenr skal ikke være input i videprog.  bsa
6.20  *                   Kun kompilert. Endring i skjermbildet.         bsa
6.21  * R6.20     030113  Notatblokk ble ikke vist                       nho  *
6.22  * R6.20     100314  Posisjonering av popup gir JIT Gui                  *
      *                                                                 *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KC = F3  = Exit                                           *
      *       KE = F5  = Forny                                          *
      *       KL = F12 = Annuller                                       *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       10 = Roll                                                 *
      *       12 = Sfldsp                                               *
      *       13 = Sfldspctl                                            *
      *       14 = Sflclr                                               *
      *       15 = Sflend                                               *
      *       16 = Readc 16 on, no more records                         *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frlbapf    if   e           k disk    rename(rlbapfr:rlbapfr)
     frlbalu    uf a e           k disk    rename(rlbapfr:rlbalur)
E5.40frklal2    if   e           k disk    rename(rklapfr:rklal2r)
     fraa1l1    if   e           k disk    rename(raa1pfr:raa1l1r)
     fapospf    if   e           k disk
     frl502d    cf   e             workstn
      *
     d wpf             s              1    dim(30)
      *
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      * LOCAL DATA
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Parameter - program RF020R
      *                                                                -----
     i/COPY QCPYLESRC,RF020PAR
6.20 d*                ds
6.20 d*rxulda                       175
6.20 d* rxstat                        1    overlay(rxulda)
6.20 d* rxfirm                        3  0 overlay(rxulda:2)
6.20 d* rxsyst                        1    overlay(rxulda:5)
6.20 d* rxpros                        6  0 overlay(rxulda:6)
6.20 d* rxakti                        3    overlay(rxulda:12)
6.20 d* rxf001                        3    overlay(rxulda:15)
6.20 d* rxkont                        6  0 overlay(rxulda:18)
6.20 d* rxspes                        4  0 overlay(rxulda:24)
6.20 d* rxavde                        4  0 overlay(rxulda:28)
6.20 d* rxlinj                        4  0 overlay(rxulda:32)
6.20 d* rxtgrp                        3    overlay(rxulda:36)
6.20 d* rxtext                       78    overlay(rxulda:39)
6.20 d* rxf002                       13    overlay(rxulda:117)
6.20 d* rxvalg                        1  0 overlay(rxulda:130)
6.20 d* rxtist                         z   overlay(rxulda:131)
6.20 d* rxbiln                        6  0 overlay(rxulda:158)
6.20 d* rxrper                        2  0 overlay(rxulda:164)
6.20 d* rxraar                        4  0 overlay(rxulda:166)
6.20 d* rxonr                         6  0 overlay(rxulda:170)
E5.30d rxupar          s                   like(rxulda)
      *
      * Parametre
      *
     d p_stat          s              1
     d p_bkod          s                   like(rlbetm)
     d p_ckod          s                   like(rlbetb)
     d p_fkod          s                   like(rlrabk)
     d p_gkod          s                   like(rlavde)
     d p_hkod          s                   like(rldist)
     d p_jkod          s                   like(rllagr)
     d p_lkod          s                   like(rlland)
     d p_nkod          s                   like(rlkatg)
     d p_pkod          s                   like(rlvalk)
     d p_qkod          s                   like(rlform)
     d p_rkod          s                   like(rllbet)
     d p_zkod          s                   like(rlsaks)
     d p_tx20          s             20
     d p_tx25          s             25
T5.00d p_tx2           s             25
T5.00d p_tx3           s             25
     d p_tx30          s             30
6.NU d p_biln          s              8  0
     d p_rest          s             11  2
      *
     d wactli          s              2  0
     d wactpo          s              3  0
     d werror          s              1  0
     d whalfa          s                   like(rlalfa)
     d wpakti          s              3
     d wpantp          s              2  0
     d wpavde          s              4  0
     d wpfunk          s              1  0
     d wpponr          s                   like(rlponr)
6.21 d*wppros          s              6  0
6.21 d wppros          s              6
     d wpretk          s              1
E5.30d wpspes          s              4  0
     d wpsyst          s              1
     d wpvalg          s              1  0
     d wp4lnr          s              6  0
     d wseqen          s              1
     d wwalfa          s                   like(rlalfa)
6.NU d wwbiln          s              8  0
     d wwedat          s                   like(rledat)
     d w_firm          s                   like(rlfirm)
     d w_levr          s                   like(rllevr)
5.50 d w_kund          s              6  0 inz(0)
     d wwrest          s             11  2
     d ww1bel          s                   like(ra1bel)
     d ww1bkl          s                   like(ra1bkl)
     d apospf_ponr     s                   like(apponr)
E5.40d rklal2_syst     s                   like(rxsyst)
E5.40d rklal2_pros     s                   like(rxpros)
E5.40d rklal2_akti     s                   like(rxakti)
E5.40d rklal2_levr     s                   like(rxkont)
      *
      /eject
     c     dummy         tag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av felter                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * C2BLD  - C2 BILDE for å vise
      * F2WIN  - F2 Vindu for å vise OFAK-koder
      * G2WIN  - G2 Vindu for å vise Fakturakunde/EAN-kunde
      * ASUME  -    Record for å styre at vindu legges over
      *             eksisterende data.
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * HOVEDRUTINE                                                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * C2BLD Behandle bilde for ny - endring - vise                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
     c     rlevl1_key    chain     rlevl1                             60
      *
     c                   if        *in60 = *off
     c                   move      rllevr        c2levr
     c                   move      rlalfa        c2alfa
     c                   move      rlnavn        c2navn
     c                   move      rlgate        c2gate
     c                   move      rladr2        c2adr2
     c                   move      rlsted        c2sted
     c                   move      rlponr        c2ponr
     c                   move      rlbetb        c2betb
     c                   move      rlsaks        c2saks
     c                   move      rlbetm        c2betm
     c                   move      rlkatg        c2katg
     c                   move      rlland        c2land
S5.50c*                  move      rlpers        c2pers
     c                   move      rlsprk        c2sprk
     c                   move      rltlfn        c2tlfn
     c                   move      rlmobn        c2mobn
     c                   move      rltlfx        c2tlfx
     c                   move      rlbgir        c2bgir
     c                   move      rlkunr        c2kunr
     c                   move      rlftnr        c2ftnr
     c                   move      rlmkod        c2mkod
     c                   move      rlvalk        c2valk
     c                   move      rlavde        c2avde
     c                   move      rlrabk        c2rabk
     c                   move      rlorab        c2orab
     c                   move      rldist        c2dist
     c                   move      rllagr        c2lagr
     c                   move      rlform        c2form
     c                   move      rllbet        c2lbet
     c                   move      rlfrie        c2frie
     c                   move      rlemal        c2emal
T5.00c                   if        rledat = *loval
T5.00c                   move      *zeros        c2edat
T5.00c                   else
     c     *dmy          move      rledat        c2edat
T5.00c                   endif
     c                   move      rlesig        c2esig
      *
     c                   move      rllevr        e2levr
     c                   move      rlnavn        e2navn
     c                   move      rlbgat        e2bgat
     c                   move      rlbadr        e2badr
     c                   move      rlbpnr        e2bpnr
     c                   move      rlweba        e2weba
      *
      * Hent poststed
      *
     c                   move      rlbpnr        apospf_ponr
     c     apospf_key    chain     apospfr
      *                                                                 .
     c                   if        %found
     c                   move      apsted        e2bste
     c                   endif
      *                                                                 .
     c                   move      rlrest        f2rest
     c                   move      rlpakk        f2pakk
     c                   move      rlbonu        f2bonu
     c                   move      rlavts        f2avts
     c                   move      rlpurr        f2purr
     c                   move      rlspes        f2spes
     c                   move      rlprio        f2prio
     c                   move      rlorsk        f2orsk
     c                   move      rlstko        f2stko
     c                   move      rlkpri        f2kpri
     c                   move      rlppro        f2ppro
     c                   move      rlremk        f2remk
     c                   move      rlslet        f2slet
     c                   move      rlhyle        f2hyle
     c                   move      rllvko        f2lvko
      *
E5.50c                   if        rlndat = *loval
     c                   move      *zeros        f2ndat
     c                   else
     c     *dmy          move      rlndat        f2ndat
     c                   endif
      *
     c                   move      rlnsig        f2nsig
      *                                                                 .
     c                   move      rlfact        g2fact
     c                   move      rlfacn        g2facn
     c                   move      rledik        g2edik
     c                   move      rleanl        g2eanl
      *                                                                 .
      * Hent tekster fra koderegister                                   .
      *                                                                 .
     c                   exsr      xsr020
      *                                                                 .
     c                   endif
      *
     c     c2tagb        tag
      *
      * Posisjonere cursor
      *
     c                   if        *in32 = *off and
     c                             *in33 = *off and
     c                             *in34 = *off and
     c                             *in35 = *off and
     c                             *in36 = *off and
     c                             *in37 = *off and
     c                             *in38 = *off and
     c                             *in39 = *off and
     c                             *in40 = *off and
     c                             *in41 = *off and
     c                             *in42 = *off and
     c                             *in43 = *off and
     c                             *in44 = *off and
     c                             *in45 = *off
     c                   move      *on           *in31
     c                   endif
      *
T5.30 * Finnes notatblokk på leverandøren?
      *
E5.40c                   eval      rklal2_syst = 'L'
6.21 c*                  eval      rklal2_pros = *zeros
6.21 c                   eval      rklal2_pros = *blanks
E5.40c                   eval      rklal2_akti = *blanks
E5.40c                   eval      rklal2_levr = c2levr
E5.40c     rklal2_key    setll     rklal2                                 94
      *
T5.30c                   if        *in94 = *on
T5.30c                   eval      C2notb = 'NOTATARK'
T5.30c                   endif
      *
     c                   exfmt     c2bld
     c                   exsr      xnulle
      *
      * Oppkall av notatark.
      *
     c                   if        *inkb = *on
     c                   move      w_firm        rxfirm
     c                   move      wpsyst        rxsyst
E5.40c                   move      wppros        rxpros
     c                   move      wpakti        rxakti
     c                   move      w_levr        rxkont
     c                   move      wpspes        rxspes
     c                   move      wpavde        rxavde
     c                   eval      rxlinj = *zeros
     c                   eval      rxtext = *blank
     c                   move      wpvalg        rxvalg
T5.40c                   move      *loval        rxtist
T5.40c                   eval      rxbiln = *zero
T5.40c                   eval      rxrper = *zero
T5.40c                   eval      rxraar = *zero
T5.40c                   eval      rxonr  = *zero
     c                   move      rxulda        rxupar
     c                   call      'RF020R'
     c                   parm                    rxupar
     c                   move      rxupar        rxulda
     c                   goto      c2tagb
     c                   endif
      *
     c     *inkc         cabeq     *on           c2tag1
     c     *inkl         cabeq     *on           c2tag1
      *
      * Call program for posteringer
      *
     c                   if        *inke = *on
     c                   exsr      xe2bld
     c                   goto      c2tagb
     c                   endif
      *
      * Call program for OFAK-koder
      *
     c                   if        *inkf = *on
     c                   exsr      xf2win
     c                   goto      c2tagb
     c                   endif
      *
      * Call program for EAN-koder
      *
     c                   if        *inkg = *on
     c                   exsr      xg2win
     c                   goto      c2tagb
     c                   endif
      *
      * Call program for utenlandsk bank
      *
     c                   if        *inkh = *on
     c                   exsr      xh2win
     c                   goto      c2tagb
     c                   endif
5.50  *
5.50  * Call program for kontaktperson
5.50  *
5.50 c                   if        *inkj = *on
      *
5.50 c                   call      'RF210R'
5.50 c                   parm                    w_firm
5.50 c                   parm                    w_kund
5.50 c                   parm                    w_levr
      *
5.50 c                   goto      c2tagb
5.50 c                   endif
      *
      * Call program for Besøksadresse
      *
     c                   if        *inkk = *on
     c                   exsr      xe2win
     c                   goto      c2tagb
     c                   endif
      *
     c     c2tag1        tag
      *
     c                   goto      xslutt
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * E2BLD Vise posteringer                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xe2bld        begsr
      *
     c                   eval      wwbiln = *zeros
     c                   eval      wwrest = *zeros
      *
     c                   call      'RL512R'
     c                   parm                    wpvalg
     c                   parm                    w_levr
     c                   parm                    p_biln
     c                   parm                    p_rest
      *
     c                   move      *on           *in30
      *
     c                   move      *on           *in31
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * E2WIN Behandle bilde for vising besøkadresse                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xe2win        begsr
      *
     c                   exfmt     e2win
      *
     c     *inkc         cabeq     *on           e2tag1
     c     *inkl         cabeq     *on           e2tag1
      *
     c     e2tag1        tag
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * F2WIN Behandle bilde                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xf2win        begsr
      *
6.22 c**                 eval      wactli = 4
6.22 c                   eval      wactli = 1
6.22 c**                 eval      wactpo = 22
6.22 c                   eval      wactpo = 2
      *
     c                   exfmt     f2win
      *
     c     *inkc         cabeq     *on           f2tag1
     c     *inkl         cabeq     *on           f2tag1
      *
      *
      *
     c     f2tag1        tag
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * G2WIN Behandle bilde                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xg2win        begsr
      *
     c                   eval      wactli = 6
     c                   eval      wactpo = 15
      *
     c                   exfmt     g2win
      *
     c     *inkc         cabeq     *on           g2tag1
     c     *inkl         cabeq     *on           g2tag1
      *
     c     g2tag1        tag
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * H2WIN Behandle bilde                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xh2win        begsr
      *
     c     rlbapf_key    chain     rlbapf                             60
      *
     c                   if        *in60 = *off
     c                   move      rvhswa        h2hswa
     c                   move      rvhba1        h2hba1
     c                   move      rvhba2        h2hba2
     c                   move      rvhba3        h2hba3
     c                   move      rvhba4        h2hba4
     c                   move      rvhbko        h2hbko
     c                   move      rvhbku        h2hbku
     c                   move      rvhbki        h2hbki
     c                   move      rvhbvk        h2hbvk
     c                   endif
      *
     c                   eval      wactli = 6
     c                   eval      wactpo = 15
      *
     c     h2tagb        tag
      *
     c                   exfmt     h2win
      *
     c     *inkc         cabeq     *on           h2tag1
     c     *inkl         cabeq     *on           h2tag1
      *
     c     h2tag1        tag
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * 0-Stilling av arbeidsindikatorer og arbeidsfelt                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xnulle        begsr
      *
     c                   eval      *in31 = *off
     c                   eval      *in32 = *off
     c                   eval      *in33 = *off
     c                   eval      *in34 = *off
     c                   eval      *in35 = *off
     c                   eval      *in36 = *off
     c                   eval      *in37 = *off
     c                   eval      *in38 = *off
     c                   eval      *in39 = *off
     c                   eval      *in40 = *off
     c                   eval      *in41 = *off
     c                   eval      *in42 = *off
     c                   eval      *in43 = *off
     c                   eval      *in44 = *off
     c                   eval      *in45 = *off
     c                   eval      *in46 = *off
     c                   eval      werror = *zero
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Kontroll  C2 bilde (endringer)                                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xsr020        begsr
      *
      * Sjekke om navn er utfyllt
      *
     c                   if        c2navn = *blank
     c                   move      *on           *in45
     c                   move      *on           *in81
     c                   move      1             werror
     c                   goto      xa2010
     c                   endif
      *
      * Sjekke om postnummer er gyldig
      *
     c                   if        c2ponr <> *zero
     c                   move      c2ponr        apospf_ponr
     c     apospf_key    chain     apospfr                            62
      *                                                                 .
     c                   if        *in62 = *on
     c                   move      *on           *in32
     c                   move      *on           *in81
     c                   move      1             werror
     c                   goto      xa2010
     c                   else
     c                   eval      c2sted = *blank
     c                   movel     apponr        c2sted
     c                   move      apsted        c2sted
     c                   endif
     c                   endif
      *
      * Sjekke om betalingsmåte er gyldig
      *
     c                   if        c2betm <> *zero
     c                   move      c2betm        p_bkod
     c                   call      'RS702R'
     c                   parm                    p_stat
     c                   parm                    p_bkod
     c                   parm                    p_tx20
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in33
     c                   move      *on           *in81
     c                   move      1             werror
     c                   goto      xa2010
     c                   else
     c                   move      p_tx20        c2btxt
     c                   endif
     c                   endif
      *
      * Sjekke om betalingsbetingelse er gyldig
      *
     c                   if        c2betb <> *zero
     c                   move      c2betb        p_ckod
     c                   call      'RS703R'
     c                   parm                    p_stat
     c                   parm                    p_ckod
     c                   parm                    p_tx20
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in34
     c                   move      *on           *in81
     c                   move      1             werror
     c                   goto      xa2010
     c                   else
     c                   move      p_tx20        c2ctxt
     c                   endif
     c                   endif
      *
E5.30 * Sjekke om saksbehandler er gyldig (selger)
      *
E5.30c                   if        c2saks <> *zero
     c                   move      c2saks        p_zkod
E5.30c                   call      'RS709R'
     c                   parm                    p_stat
     c                   parm                    p_zkod
     c                   parm                    p_tx20
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in46
     c                   move      *on           *in81
     c                   move      1             werror
     c                   goto      xa2010
     c                   else
     c                   movel     p_tx20        c2ztxt
     c                   endif
     c                   endif
      *
      * Sjekke om rabattkategori er gyldig
      *
     c                   if        c2rabk <> *zero
     c                   move      c2rabk        p_fkod
     c                   call      'RS706R'
     c                   parm                    p_stat
     c                   parm                    p_fkod
     c                   parm                    p_tx20
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in35
     c                   move      *on           *in81
     c                   move      1             werror
     c                   goto      xa2010
     c                   else
     c                   move      p_tx20        c2ftxt
     c                   endif
     c                   endif
      *
      * Sjekke om avdeling er gyldig
      *
     c                   if        c2avde <> *zero
     c                   move      c2avde        p_gkod
     c                   call      'RS707R'
     c                   parm                    p_stat
     c                   parm                    p_gkod
     c                   parm                    p_tx20
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in36
     c                   move      *on           *in81
     c                   move      1             werror
     c                   goto      xa2010
     c                   else
     c                   move      p_tx20        c2gtxt
     c                   endif
     c                   endif
      *
      * Sjekke om distrikt er gyldig
      *
     c                   if        c2dist <> *zero
     c                   move      c2dist        p_hkod
     c                   call      'RS708R'
     c                   parm                    p_stat
     c                   parm                    p_hkod
     c                   parm                    p_tx20
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in37
     c                   move      *on           *in81
     c                   move      1             werror
     c                   goto      xa2010
     c                   else
     c                   move      p_tx20        c2htxt
     c                   endif
     c                   endif
      *
      * Sjekke om lager er gyldig
      *
     c                   if        c2lagr <> *zero
     c                   move      c2lagr        p_jkod
     c                   call      'RS710R'
     c                   parm                    p_stat
     c                   parm                    p_jkod
     c                   parm                    p_tx20
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in39
     c                   move      *on           *in81
     c                   move      1             werror
     c                   goto      xa2010
     c                   else
     c                   move      p_tx20        c2jtxt
     c                   endif
     c                   endif
      *
      * Sjekke om leverandørkategori er gyldig
      *
     c                   if        c2katg <> *zero
     c                   move      c2katg        p_nkod
     c                   call      'RS714R'
     c                   parm                    p_stat
     c                   parm                    p_nkod
     c                   parm                    p_tx20
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in40
     c                   move      *on           *in81
     c                   move      1             werror
     c                   goto      xa2010
     c                   else
     c                   move      p_tx20        c2ntxt
     c                   endif
     c                   endif
      *
      * Sjekke om land er gyldig
      *
     c                   if        c2land <> *blank
     c                   move      c2land        p_lkod
     c                   call      'RS712R'
     c                   parm                    p_stat
     c                   parm                    p_lkod
     c                   parm                    p_tx20
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in41
     c                   move      *on           *in81
     c                   move      1             werror
     c                   goto      xa2010
     c                   else
     c                   move      p_tx20        c2ltxt
     c                   endif
     c                   endif
      *
      * Sjekke om valuta er gyldig
      *
     c                   if        c2valk <> *blank
     c                   move      c2valk        p_pkod
     c                   call      'RS716R'
     c                   parm                    p_stat
     c                   parm                    p_pkod
     c                   parm                    p_tx20
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in42
     c                   move      *on           *in81
     c                   move      1             werror
     c                   goto      xa2010
     c                   else
     c                   move      p_tx20        c2pmna
     c                   endif
     c                   endif
      *
      * Sjekke om forsendelsesmåte er gyldig
      *
     c                   if        c2form <> *zeros
     c                   move      c2form        p_qkod
     c                   call      'RS717R'
     c                   parm                    p_stat
     c                   parm                    p_qkod
     c                   parm                    p_tx20
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in43
     c                   move      *on           *in81
     c                   move      1             werror
     c                   goto      xa2010
     c                   else
     c                   move      p_tx20        c2qtxt
     c                   endif
     c                   endif
      *
      * Sjekke om leveringsbetingelse er gyldig
      *
     c                   if        c2lbet <> *zeros
     c                   move      c2lbet        p_rkod
     c                   call      'RS718R'
     c                   parm                    p_stat
     c                   parm                    p_rkod
     c                   parm                    p_tx25
T5.00c                   parm                    p_tx2
T5.00c                   parm                    p_tx3
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in44
     c                   move      *on           *in81
     c                   move      1             werror
     c                   goto      xa2010
     c                   else
     c                   move      p_tx25        c2rtx1
     c                   endif
     c                   endif
      *
     c     xa2010        tag
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   eval      p_biln = 0
     c                   eval      p_rest = 0
     c                   eval      wactli = 0
     c                   eval      wactpo = 0
     c                   eval      werror = *zero
     c                   eval      wpfunk = *zero
     c                   eval      wpantp = *zero
     c                   eval      wseqen = *blank
      *
      * Parametre til Notatark.
      *
     c                   move      'L'           wpsyst
     c                   eval      wpspes = 0
     c                   eval      wpavde = 0
6.21 c*                  eval      wppros = 0
6.21 c                   eval      wppros = *blanks
     c                   eval      wpakti = *blanks
      *
      *
      *
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    w_levr
     c                   parm                    wpvalg
      *
      * Legge inn firmanummer
      *
     c                   move      l_firm        w_firm
     c                   move      w_levr        c2levr
      *
      * Sperre for registrering ved spørring
      *
     c                   if        wpvalg = 5
     c                   move      *on           *in30
     c                   endif
      *
      * Hent leverandør
      *
     c     rlevl1_key    chain     rlevl1                             61
      *
      * Hent statuskode 1
      *
     c     raa1l1_key    chain     raa1l1                             61
      *
     c                   if        *in61 = *off
     c                   move      ra1bel        ww1bel
     c                   move      ra1bkl        ww1bkl
     c                   else
     c                   move      30            ww1bel
     c                   move      0             ww1bkl
     c                   endif
      *
      **   Dato + klokkeslett
      *
     c                   time                    wwedat
     c                   time                    rletim
     c                   move      l_user        rlesig
      *
      * Key for leverandørregisteret via leverandørnummer
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_levr
      *
      * Key for bankregisteret via leverandørnummer
      *
     c     rlbapf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_levr
      *
T5.30 * Key for notatblokk
      *
E5.40c     rklal2_key    klist
T5.30c                   kfld                    w_firm
E5.40c                   kfld                    rklal2_syst
E5.40c                   kfld                    rklal2_pros
E5.40c                   kfld                    rklal2_akti
E5.40c                   kfld                    rklal2_levr
      *
      * Key for statuskode 1
      *
     c     raa1l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for postnummer-register
      *
     c     apospf_key    klist
     c                   kfld                    apospf_ponr
      *
     c                   endsr
