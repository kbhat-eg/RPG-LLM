     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
      *                                                                       *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RE620R                                              *
      * Beskrivelse . . : Resultatrapport til PC - parameter                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     fre620d    cf   e             workstn
      *
     d                uds
     d pfirm                 300    302  0                                      Firma
     d pnavn                         30                                         Navn
     d puser                         10                                         BruerI
     d pdato                          6  0                                      Pr. da
     d praar                          4  0                                      Pr. år
     d pperf                          2  0                                      Period
     d ppert                          2  0                                      Period
     d pavdf                          4  0                                      Avd. f
     d pavdt                          4  0                                      Avd. t
     d prapp                          2  0                                      Rappor
     d psumm                          1                                         Avd.su
     d psums                          1                                         Avd.su
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d p_stat          s              1
     d p_gkod          s              4  0
     d p_gtxt          s             20
      *
     d w_aar           s              4  0
     d w_mnd           s              2  0
     d w_in03          s              1
     d w_syst          s              4
      *
     c/eject
      *
     c     a2taga        tag
      *
     c                   exfmt     a2bld
      *
     c                   eval      *in31 = *off
     c                   eval      *in32 = *off
     c                   eval      *in33 = *off
     c                   eval      *in34 = *off
     c                   eval      *in35 = *off
     c                   eval      *in36 = *off
      *
      * Gå ut
      *
     c                   if        *inkc = *on                                  .....
     c                   eval      w_in03 = '1'                                     .
     c                   goto      slutt                                            .
     c                   endif                                                  .....
      *
      * Sjekke om kjøredato er gyldig
      *
     c                   if        a2pkda = *zero                                        .....
     c                   move      udate         a2pkda                                      .
     c                   endif                                                           .....
      *
     c     *dmy          test(d)                 a2pkda                 33
     c                   if        *in33 = *on                                           .....
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      * Kontroll av REGNSKAPSÅR
      *
     c                   if        a2prÅr > *year                                        .....
     c                   move      *on           *in32                          Pos.cur.     .
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      * Kontroll av REGNSKAPSPERIODE
      *
     c                   if        a2pper <= *zero                                       .....
     c                   move      *on           *in35                          Pos.cur.     .
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
     c                   if        a2pper > 13                                           .....
     c                   move      *on           *in35                          Pos.cur.     .
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      * Kontroll av AVDELING
      *
     c                   if        a2pavf = a2pavt
     c                   move      a2pavf        p_gkod
      *
     c                   call      'RS707R'                                      avdelings-
     c                   parm                    p_stat
     c                   parm                    p_gkod
     c                   parm                    p_gtxt
      *
     c                   if        p_stat = 'N'                                 Kode
     c                   move      *on           *in33                           finnes
     c                   goto      a2taga                                         ikke
     c                   endif
     c                   endif
      *
      * Kontroll av SUMMERINGSKODE
      *
     c                   if        a2psal <> 'N' and                                     .....
     c                             a2psal <> 'J'                                             .
     c                   move      *on           *in36                          Pos.cur.     .
     c                   goto      a2taga                                                    .
     c                   endif                                                              ..
      *
      *  PARAMETER - OK
      *
     c                   move      l_firm        pfirm
     c                   move      l_fnav        pnavn
     c                   move      l_user        puser
     c                   move      a2pkda        pdato
     c                   move      a2prÅr        praar
     c                   move      *zeros        pperf
     c                   move      a2pper        ppert
     c                   move      a2pavf        pavdf
     c                   move      a2pavt        pavdt
     c                   move      a2prap        prapp
     c                   move      a2psal        psumm
      *
     c     slutt         tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    w_syst
     c                   parm                    w_in03
      *
     c                   if        w_syst = 'PC  '
     c                   eval      *in13 = *on
     c                   endif
      *
     c                   if        w_syst = 'AKEL'
     c                   eval      *in14 = *on
     c                   endif
      *
     c                   move      udate         a2pkda
      *
      * Beregner periode
      *
     c                   eval      w_aar= *year
      *
     c     umonth        sub       1             w_mnd
     c                   if        w_mnd= *zero                                          .....
     c                   movel     12            w_mnd                                       .
     c     w_aar         sub       1             w_aar                                       .
     c                   move      w_aar         a2prÅr                                      .
     c                   else                                                                .
     c                   move      w_aar         a2prÅr                                      .
     c                   endif                                                           .....
      *
     c                   move      w_mnd         a2pper
     c                   move      9999          a2pavt
     c                   move      'J'           a2psal
      *
      * Parameterliste
      *
     c                   endsr
      *
