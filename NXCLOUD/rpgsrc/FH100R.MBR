     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FH100R
      * Beskrivelse . . : Kunde distribusjonsliste, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       270599 HKO  Nytt program
      *       260606 HKO  Lagt inn valg av leveringstype
      * 6.10  220609 BSA  Oppdatert med sporingsinformasjon
6.20  *       121110 bof  Utvidet med lager på varesortiment
7.xx  * 7.00  240216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  *k00  030620 BOF NXS-1195  Utvidet med parm inkl skaffvare / tilbudspris
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffkdilr    if   e           k disk    rename(fkdipfr:fkdilrr)
     ffkdilu    uf a e           k disk    rename(fkdipfr:fkdilur)
     ffkdil1    if   e           k disk    rename(fkdipfr:fkdil1r)
     ffkdil2    if   e           k disk    rename(fkdipfr:fkdil2r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fvshel1    if   e           k disk    rename(vshepfr:vshel1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
     ffprcl1    if   e           k disk    rename(fprcpfr:fprcl1r)
      *
     ffh100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d fkdilr_kund     s                   like(fhkund)
     d fkdilr_kpro     s                   like(fhkpro)
     d fkdil2_kpro     s                   like(fhkpro)
     d fkdilu_kund     s                   like(fhkund)
     d fkdilu_kpro     s                   like(fhkpro)
     d fkdil1_kund     s                   like(fhkund)
     d fkdil1_kpro     s                   like(fhkpro)
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d vshel1_vsor     s                   like(vcvsor)
6.20 d vshel1_lage     s                   like(vclage)
     d vltpl1_lety     s                   like(vylety)
     d fprcl1_rfmt     s                   like(fcrfmt)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(fhfirm)
     d w_knav          s                   like(rknavn)
     d w_pnav          s                   like(flnavn)
6.20 d w_lage          s                   like(vclage)
      *
     d s_kund          s                   like(fhkund)
     d s_kpro          s                   like(fhkpro)
      *
      * Konstanter
      *
     d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for å opprette ny rad
      * C1MSG  - C1 Meleingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile                                  CRT SFL
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on                                  CRT SFL
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2kund <> 0 or
     c                             b2kpro <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   eval      fkdil2_kpro = b1kpro
     c     fkdil2_key    setll     fkdil2
     c                   other
     c                   eval      fkdil1_kund = b1kund
     c                   eval      fkdil1_kpro = b1kpro
     c     fkdil1_key    setll     fkdil1
     c                   endsl
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjonerer på kundenummer
      *
     c                   if        b2kund <> 0
     c                   eval      w_seqe = *blank
     c                   eval      fkdil1_kund = b2kund
     c                   eval      fkdil1_kpro = b2kpro
     c     fkdil1_key    setll     fkdil1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjonerer på kundeprosjekt
      *
     c                   if        b2kpro <> 0
     c                   eval      w_seqe = 'L'
     c                   eval      fkdil2_kpro = b2kpro
     c     fkdil2_key    setll     fkdil2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2kund = 0
     c                   eval      b2kpro = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1kund = b1kund
     c                   eval      c1kpro = b1kpro
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1kund = b1kund
     c                   eval      k1kpro = b1kpro
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1kund = b1kund
     c                   eval      d1kpro = b1kpro
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1kund = b1kund
     c                   eval      c1kpro = b1kpro
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1kund = b2kund
     c                   eval      c1kpro = b2kpro
     c                   eval      c1knav = *blank
     c                   eval      c1lnav = *blank
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      c1taga
     c                   endsl
      *
     c                   if        c1kund = 0 and
     c                             c1kpro = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
     c                   if        c1kund = 0
     c                   eval      *in31  = *on
     c                   goto      c1taga
     c                   endif
      *
     c                   eval      c1knav = *blank
     c                   eval      rkunl1_kund = c1kund
     c     rkunl1_key    chain     rkunl1                             90
     c                   if        *in90
     c                   eval      *in32 = *on
     c                   goto      c1taga
     c                   endif
     c                   eval      c1knav = rknavn
      *
     c                   eval      c1lnav = *blank
     c                   if        c1kpro <> 0
     c                   eval      fkprl1_kund = c1kund
     c                   eval      fkprl1_kpro = c1kpro
     c     fkprl1_key    chain     fkprl1                             90
     c                   if        *in90
     c                   eval      *in33 = *on
     c                   goto      c1taga
     c                   endif
     c                   eval      c1lnav = flnavn
     c                   endif
     c
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      fkdilr_kund = c1kund
     c                   eval      fkdilr_kpro = c1kpro
     c     fkdilr_key    chain     fkdilr                             90
     c                   if        *in90 = *off
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1kund = 0
     c                   eval      c1kpro = 0
     c                   eval      c1knav = *blank
     c                   eval      c1lnav = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2kund = 0
     c                   eval      b2kpro = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbilde legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle skjermbilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      fkdilr_kund = c1kund
     c                   eval      fkdilr_kpro = c1kpro
     c     fkdilr_key    chain     fkdilr                             90
      *
     c                   if        *in90 = *off
     c                   eval      c2pbok = fhpbok
     c                   eval      c2pfil = fhpfil
     c                   eval      c2vsor = fhvsor
     c                   eval      c2vsos = fhvsos
     c                   eval      c2lety = fhlety
     c                   eval      c2rfmt = fhrfmt
     c                   eval      c2endr = fhendr
7.01 c                   eval      c2sval = fhsval
7.01 c                   eval      c2tval = fhtval
     c                   eval      c2eusr = fheusr
     c                   eval      c2dato = 0
     c                   if        fhdato <> *loval
     c     *dmy          move      fhdato        c2dato
     c                   endif
     c     *dmy          move      fhodat        c2odat
     c     *dmy          move      fhedat        c2edat
     c                   else
     c                   eval      c2pbok = 0
     c                   eval      c2pfil = 0
     c                   eval      c2vsor = *blank
     c                   eval      c2vsos = *blank
     c                   eval      c2lety = 0
     c                   eval      c2rfmt = *blank
     c                   eval      c2endr = 0
7.01 c                   eval      c2sval = 0
7.01 c                   eval      c2tval = 0
     c                   eval      c2dato = 0
     c                   eval      c2odat = 0
     c                   eval      c2edat = 0
     c                   eval      c2eusr = *blank
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1kund = k1kund
     c                   eval      c1kpro = k1kpro
     c                   eval      c2dato = 0
     c                   eval      fhdato = *loval
     c                   eval      fhbtis = *loval
     c                   eval      fhftis = *loval
     c                   endif
      *
      * Henter beskrivelse av felter
      *
     c                   eval      c1knav = *blank
     c                   eval      c1lnav = *blank
     c                   eval      c2vbes = *blank
     c                   eval      c2vbe2 = *blank
     c                   eval      c2lbes = *blank
     c                   eval      c2rbes = *blank
      *
     c                   eval      rkunl1_kund = c1kund
     c     rkunl1_key    chain     rkunl1                             91
     c                   if        *in91 = *off
     c                   eval      c1knav = rknavn
     c                   endif
      *
     c                   if        c1kpro <> 0
     c                   eval      fkprl1_kund = c1kund
     c                   eval      fkprl1_kpro = c1kpro
     c     fkprl1_key    chain     fkprl1                             91
     c                   if        *in91 = *off
     c                   eval      c1lnav = flnavn
     c                   endif
     c                   endif
      *
     c                   if        c2vsor <> *blank
     c                   eval      vshel1_vsor = c2vsor
6.20 c                   eval      vshel1_lage = w_lage
     c     vshel1_key    chain     vshel1                             91
     c                   if        *in91 = *off
     c                   eval      c2vbes = vcbesk
     c                   endif
     c                   endif
      *
     c                   if        c2vsos <> *blank
     c                   eval      vshel1_vsor = c2vsos
6.20 c                   eval      vshel1_lage = w_lage
     c     vshel1_key    chain     vshel1                             91
     c                   if        *in91 = *off
     c                   eval      c2vbe2 = vcbesk
     c                   endif
     c                   endif
      *
     c                   eval      vltpl1_lety = c2lety
     c     vltpl1_key    chain     vltpl1
     c                   if        %found
     c                   eval      c2lbes = vybesk
     c                   endif
      *
     c                   if        c2rfmt <> *blank
     c                   eval      fprcl1_rfmt = c2rfmt
     c     fprcl1_key    chain     fprcl1                             91
     c                   if        *in91 = *off
     c                   eval      c2rbes = fcbesk
     c                   endif
     c                   endif
      *
      * Dersom 'vise' settes protect-indikatoer
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     C     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
      *
     c                   if        w_afld = 'C2VSOR'
     c                   call      'VC500R'
     c                   parm                    w_firm
     c                   parm                    c2vsor
     c                   parm                    w_lage
     c                   parm                    c2vbes
6.20 c                   eval      w_lage = 0
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        w_afld = 'C2VSOS'
     c                   call      'VC500R'
     c                   parm                    w_firm
     c                   parm                    c2vsos
     c                   parm                    c2vbe2
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        w_afld = 'C2LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    c2lety
     c                   parm                    c2lbes
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        w_afld = 'C2RFMT'
     c                   call      'FC500R'
     c                   parm                    w_firm
     c                   parm                    c2rfmt
     c                   parm                    c2rbes
     c                   goto      c2tagb
     c                   endif
      *
     c                   endif
      *
      * Validering
      *
     c                   if        c2vsor = *blank
     c                   eval      *in31 = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   eval      vshel1_vsor = c2vsor
     c                   eval      vshel1_lage = w_lage
     c     vshel1_key    chain     vshel1                             91
     c                   if        *in91 = *on
     c                   eval      *in32 = *on
     c                   goto      c2tagb
     c                   endif
     c                   eval      c2vbes = vcbesk
      *
     c                   if        c2vsos <> *blank
     c                   eval      vshel1_vsor = c2vsos
     c                   eval      vshel1_lage = w_lage
     c     vshel1_key    chain     vshel1                             91
     c                   if        *in91 = *on
     c                   eval      *in33 = *on
     c                   goto      c2tagb
     c                   endif
     c                   eval      c2vbe2 = vcbesk
     c                   endif
      *
     c                   if        c2lety <> 0
     c                   eval      vltpl1_lety = c2lety
     c     vltpl1_key    chain     vltpl1
     c                   if        not %found
     c                   eval      *in34 = *on
     c                   goto      c2tagb
     c                   endif
     c                   eval      c2lbes = vybesk
     c                   endif
      *
     c                   if        c2pfil = 1 and
     c                             c2rfmt = *blank
     c                   eval      *in35 = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        c2rfmt <> *blank
     c                   eval      fprcl1_rfmt = c2rfmt
     c     fprcl1_key    chain     fprcl1                             91
     c                   if        *in91 = *on
     c                   eval      *in36 = *on
     c                   goto      c2tagb
     c                   endif
     c                   eval      c2rbes = fcbesk
     c                   endif
      *
      * Oppdaterer tabell
      *
     c                   clear                   fkdilur
      *
     c                   eval      fkdilu_kund = c1kund
     c                   eval      fkdilu_kpro = c1kpro
     c     fkdilu_key    chain     fkdilur                            90
      *
     c                   eval      fhpbok = c2pbok
     c                   eval      fhpfil = c2pfil
     c                   eval      fhvsor = c2vsor
     c                   eval      fhvsos = c2vsos
     c                   eval      fhlety = c2lety
     c                   eval      fhrfmt = c2rfmt
     c                   eval      fhendr = c2endr
7.01 c                   eval      fhsval = c2sval
7.01 c                   eval      fhtval = c2tval
     c                   eval      fheusr = l_user
      *
     c                   time                    fhedat
     c                   time                    fhetim
6.10 c                   eval      fheusr = l_user
      *
     c                   if        *in90 = *off
     c                   update    fkdilur
     c                   else
     c                   eval      fhfirm = w_firm
     c                   eval      fhkund = c1kund
     c                   eval      fhkpro = c1kpro
     c                   eval      fhbtis = *loval
     c                   eval      fhftis = *loval
     c                   time                    fhodat
6.10 c                   time                    fhotim
6.10 c                   eval      fhousr = l_user
     c                   write     fkdilur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1pbok = fhpbok
     c                   eval      b1pfil = fhpfil
     c                   eval      b1vsor = fhvsor
     c                   eval      b1rfmt = fhrfmt
     c                   eval      b1endr = fhendr
     c                   eval      b1dato = 0
     c                   if        fhdato <> *loval
     c     *dmy          move      fhdato        b1dato
     c                   endif
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      fkdilr_kund = d1kund
     c                   eval      fkdilr_kpro = d1kpro
     c     fkdilr_key    chain     fkdilr                             90
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
     c                   eval      fkdilu_kund = fkdilr_kund
     c                   eval      fkdilu_kpro = fkdilr_kpro
     c     fkdilu_key    chain     fkdilur                            90
     c                   delete    fkdilur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_kund = k1kund
     c                   eval      s_kpro = k1kpro
      *
      * Fyller kopierings-bildet
      *
     c                   eval      fkdilr_kund = k1kund
     c                   eval      fkdilr_kpro = k1kpro
     C     fkdilr_key    chain     fkdilr                             90
      *
     c     k1tagb        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   exsr      spØrring
     c                   goto      k1tagb
     c                   endif
      *
      * Validering
      *
     c                   if        k1kund = 0
     c                   eval      *in31 = *on
     c                   goto      k1tagb
     c                   endif
      *
     c                   eval      rkunl1_kund = k1kund
     c     rkunl1_key    chain     rkunl1                             90
     c                   if        *in90
     c                   eval      *in32 = *on
     c                   goto      k1tagb
     c                   endif
      *
     c                   if        k1kpro <> 0
     c                   eval      fkprl1_kund = k1kund
     c                   eval      fkprl1_kpro = k1kpro
     c     fkprl1_key    chain     fkprl1                             90
     c                   if        *in90
     c                   eval      *in33 = *on
     c                   goto      k1tagb
     c                   endif
     c                   endif
     c
      * Sjekker om rad finnes fra før
      *
     c                   eval      fkdilr_kund = k1kund
     c                   eval      fkdilr_kpro = k1kpro
     c     fkdilr_key    chain     fkdilr                             90
     c                   if        *in90 = *off
     c                   eval      *in34 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1kund = s_kund
     c                   eval      c1kpro = s_kpro
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   read      fkdil2
     c                   other
     c                   read      fkdil1
     c                   endsl
      *
     c                   if        %eof or                                      EOF
     c                             fhfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   eval      fkdil2_kpro = fhkpro
     c                   other
     c                   eval      fkdil1_kund = fhkund
     c                   eval      fkdil1_kpro = fhkpro
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1kund = fhkund
     c                   eval      b1kpro = fhkpro
     c                   eval      b1pbok = fhpbok
     c                   eval      b1pfil = fhpfil
     c                   eval      b1vsor = fhvsor
     c                   eval      b1rfmt = fhrfmt
     c                   eval      b1endr = fhendr
     c                   eval      b1dato = 0
      *
     c                   eval      b1knav = *blank
     c                   eval      rkunl1_kund = b1kund
     c     rkunl1_key    chain     rkunl1                             91
     c                   if        *in91 = *off
     c                   eval      b1knav = rknavn
     c                   endif
      *
     c                   if        fhdato <> *loval
     c     *dmy          move      fhdato        b1dato
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'L'
     c     fkdil2_key    setgt     fkdil2
     c                   readp     fkdil2
     c                   other
     c     fkdil1_key    setgt     fkdil1
     c                   readp     fkdil1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   readp     fkdil2
     c                   other
     c                   readp     fkdil1
     c                   endsl
      *
     c                   if        %eof or
     c                             fhfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   eval      fkdil2_kpro = fhkpro
     c                   other
     c                   eval      fkdil1_kund = fhkund
     c                   eval      fkdil1_kpro = fhkpro
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'L'
     c     fkdil2_key    setll     fkdil2
     c                   other
     c     fkdil1_key    setll     fkdil1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Kunde
      *
     c                   if        w_afld = 'B2KUND'
     c                   eval      b2kpro = 0
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    b2kund
     c                   parm                    w_knav
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1KUND'
     c                   eval      c1kpro = 0
     c                   eval      c1lnav = *blank
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    c1kund
     c                   parm                    c1knav
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1KUND'
     c                   eval      k1kpro = 0
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    k1kund
     c                   parm                    w_knav
     c                   goto      end_spØrr
     c                   endif
      *
      * Kundeprosjekt
      *
     c                   if        w_afld = 'B2KPRO'
     c                   call      'FL500R'
     c                   parm                    w_firm
     c                   parm                    b2kund
     c                   parm                    b2kpro
     c                   parm                    w_knav
     c                   parm                    w_pnav
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1KPRO'
     c                   call      'FL500R'
     c                   parm                    w_firm
     c                   parm                    c1kund
     c                   parm                    c1kpro
     c                   parm                    c1knav
     c                   parm                    c1lnav
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1KPRO'
     c                   call      'FL500R'
     c                   parm                    w_firm
     c                   parm                    k1kund
     c                   parm                    k1kpro
     c                   parm                    w_knav
     c                   parm                    w_pnav
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på kunde distribusjonsregister
      *
     c     fkdilr_key    klist
     C                   kfld                    w_firm
     c                   kfld                    fkdilr_kund
     c                   kfld                    fkdilr_kpro
      *
      * Key for oppdatering av kunde distribusjonsregister
      *
     c     fkdilu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkdilu_kund
     c                   kfld                    fkdilu_kpro
      *
      * Key for posisjonering på kunde
      *
     c     fkdil1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkdil1_kund
     c                   kfld                    fkdil1_kpro
      *
      * Key for posisjonering på kundeprosjekt
      *
     c     fkdil2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkdil2_kpro
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag på varesortiment-hode
      *
     c     vshel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vshel1_vsor
6.20 c                   kfld                    vshel1_lage
      *
      * Key for oppslag på leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
      *
      * Key for oppslag på pris recordformat
      *
     c     fprcl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fprcl1_rfmt
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      * her kan man ikke bruker sortiment med lager
6.20 c                   eval      w_lage = 0
      *
      * Initierer skjermbilde
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     fkdil1_key    setll     fkdil1
     c                   exsr      crt_subfile
      *
     c                   endsr
