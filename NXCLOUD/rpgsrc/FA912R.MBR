      /TITLE  ASOFAK: Subprogram for avrunding av beløp på formen 9:2
     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FA912R
      * Beskrivelse . . : Subprogram for avrunding av salgspris inkl mva
      *                   Her sendes 1 enhet og en pris.
      *                   Tatt utgangspunkt i FA911R
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       230915 bof  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     fjav1l1    if   e           k disk    rename(jav1pfr:jav1l1r)
     fjav3l1    if   e           k disk    rename(jav3pfr:jav3l1r)
6.31 fjlevl3    if   e           k disk    rename(jlevpfr:jlevl3r)
      *
      * Definering av variable i nøkler
      *
     d jav1l1_firm     s                   like(ja1fir)
     d jav1l1_ogrp     s                   like(ja1ogr)
     d jav1l1_hgrp     s                   like(ja1hgr)
     d jav1l1_ugrp     s                   like(ja1ugr)
     d jav1l1_levr     s                   like(ja1lev)
     d jav1l1_modu     s                   like(ja1mod)
     d jav1l1_vare     s                   like(ja1vnr)
     d jav3l1_firm     s                   like(jafirm)
     d jav3l1_regl     s                   like(jaregl)
     d jav3l1_frpr     s                   like(jafrpr)
6.31 d levl3_enum      s                   like(jlenum)
      *
      * Definering av variable
      *
6.32 d w_pris          s              9  2 inz(19)
      *
6.32 d w_fraa          s              9  0
6.32 d w_utgp          s              9  0
6.32 d w_utgpd         s              9  2
6.32 d w_gren          s              9  2
6.32 d w_frpr          s              9  2
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_ogrp          s              2  0
     d p_hgrp          s              2  0
     d p_ugrp          s              3  0
     d p_enum          s              6  0
     d p_modu          s              8  0
     d p_vare          s             15
6.31 d p_frpr          s              9  2
6.31 d p_s1im          s              9  2
6.31 d p_enh1          s              3
6.31 d p_type          s              1  0
      *
6.33 d b_pris          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *
6.31 c                   eval      levl3_enum  = p_enum
6.31 c     jlevl3_key    chain     jlevl3
6.31 c                   if        not %found(jlevl3)
6.31 c                   goto      avslutt
6.31 c                   endif
      *
      *
      *  Finn riktig regel utfra OGR, HGR, UGR, Lev, Modul, Varenr
      *
      * Overgruppe
      * | Hovedgruppe
      * | | Undergruppe
      * | | | Leverandør
      * | | | | Modul
      * | | | | | Vare
      * | | | | | |
      * ___________________________________________
      * - - - x - x      1. leverandør, vare
      * - - - - - x      2. vare
      * - - - x x -      3. leverandør, modul
      * - - - - x -      4. modul
      * x x x x - -      5. ogr,hgr,ugr, leverandør
      * x x - x - -      6. ogr,hgr,     leverandør
      * x - - x - -      7. ogr,         leverandør
      * - - - x - -      8.              leverandør
      * x x x - -        9. ogr,hgr,ugr
      * x x - - -       10. ogr,hgr
      * x - - - -       11. ogr
      * ___________________________________________
      * - - - x - x      1. leverandør, vare
     c                   eval      jav1l1_firm = p_firm
     c                   eval      jav1l1_ogrp = *zero
     c                   eval      jav1l1_hgrp = *zero
     c                   eval      jav1l1_ugrp = *zero
     c                   eval      jav1l1_levr = jlldor
     c                   eval      jav1l1_modu = *zero
     c                   eval      jav1l1_vare = p_vare
     c     jav1l1_key    chain     jav1l1
     c                   if        not %found(jav1l1)
      * - - - - - x      2. vare
     c                   eval      jav1l1_levr = *zero
     c     jav1l1_key    chain     jav1l1
     c                   endif
     c                   if        not %found(jav1l1)
      * - - - x x -      3. leverandør, modul
     c                   eval      jav1l1_levr = jlldor
     c                   eval      jav1l1_modu = p_modu
     c                   eval      jav1l1_vare = *blank
     c     jav1l1_key    chain     jav1l1
     c                   endif
     c                   if        not %found(jav1l1)
      * - - - - x -      4. modul
     c                   eval      jav1l1_levr = *zero
     c     jav1l1_key    chain     jav1l1
     c                   endif
     c                   if        not %found(jav1l1)
      * x x x x - -      5. ogr,hgr,ugr, leverandør
     c                   eval      jav1l1_ogrp = p_ogrp
     c                   eval      jav1l1_hgrp = p_hgrp
     c                   eval      jav1l1_ugrp = p_ugrp
     c                   eval      jav1l1_levr = jlldor
     c                   eval      jav1l1_modu = *zero
     c                   eval      jav1l1_vare = *blank
     c     jav1l1_key    chain     jav1l1
     c                   endif
     c                   if        not %found(jav1l1)
      * x x - x - -      6. ogr,hgr,     leverandør
     c                   eval      jav1l1_ugrp = *zero
     c     jav1l1_key    chain     jav1l1
     c                   endif
     c                   if        not %found(jav1l1)
      * x - - x - -      7. ogr,         leverandør
     c                   eval      jav1l1_hgrp = *zero
     c     jav1l1_key    chain     jav1l1
     c                   endif
     c                   if        not %found(jav1l1)
      * - - - x - -      8.              leverandør
     c                   eval      jav1l1_ogrp = *zero
     c     jav1l1_key    chain     jav1l1
     c                   endif
     c                   if        not %found(jav1l1)
      * x x x - - -      9. ogr,hgr,ugr
     c                   eval      jav1l1_ogrp = p_ogrp
     c                   eval      jav1l1_hgrp = p_hgrp
     c                   eval      jav1l1_ugrp = p_ugrp
     c                   eval      jav1l1_levr = *zero
     c                   eval      jav1l1_modu = *zero
     c                   eval      jav1l1_vare = *blank
     c     jav1l1_key    chain     jav1l1
     c                   endif
     c                   if        not %found(jav1l1)
      * x x - - - -     10. ogr,hgr
     c                   eval      jav1l1_ugrp = *zero
     c     jav1l1_key    chain     jav1l1
     c                   endif
     c                   if        not %found(jav1l1)
      * x - - - - -     11. ogr,
     c                   eval      jav1l1_hgrp = *zero
     c     jav1l1_key    chain     jav1l1
     c                   endif
      *    Hvis ingen nøkkel finnes blir det ingen avrunding
     c                   if        not %found(jav1l1) or
     c                             ja1rgl = *zero
     c                   goto      avslutt
     c                   endif
6.31  *
6.31  * Sjekker om kalkulere fra nobb. Hvis kalk fra nobb og parm = N avslutt
6.31  *
6.31 c                   if        jlkkal = 1 and
6.31 c                             ja1nob = 0
6.31 c                   goto      avslutt
6.31 c                   endif
6.31  *
6.31  * Sjekker om avrund på enhet, hvis enhet er blant enhet 1 - 3,så bruk
6.31  * tilsvarende nobb-pris - overstyr p_frpf
6.31 c                   if        ja1enh <> *blank
6.31 c                   if        ja1enh = p_enh1
6.31 c                   eval      w_frpr = p_s1im
6.31 c                   eval      p_type = 1
6.31 c                   endif
6.31 c                   else
6.31 c                   eval      w_frpr = p_s1im
6.31 c                   eval      p_type = 1
6.31 c                   endif
6.31  *
      *
      * Finn parameter utfra funnet regel og pris
      *
     c                   move      p_firm        jav3l1_firm
     c                   eval      jav3l1_regl = ja1rgl
     c                   eval      jav3l1_frpr = w_frpr
      *
     c     jav3l1_key    setll     jav3l1
     c                   readp     jav3l1
     c                   if        not %eof(jav3l1)     and
     c                             jafrpr < jav3l1_frpr and
     c                             jaregl = jav3l1_regl and
     c                             jafirm = jav3l1_firm
     c                   eval(h)   w_fraa=(jav3l1_frpr-jafrpr)/jaokni-0,5
     c                   eval(h)   w_utgp=jafrpr+jaokni*w_fraa
6.20 c                   eval(h)   w_utgpd=jafrpr+jaokni*w_fraa
     c                   eval(h)   w_gren=w_utgp+jaokni*((100-jaavrp)/100)
     c                   if        jav3l1_frpr < w_gren
6.20 c                   eval      w_pris=w_utgpd
6.33 c                   eval      b_pris = *on
     c                   else
6.20 c                   eval      w_pris =w_utgpd + jaokni
6.33 c                   eval      b_pris = *on
     c                   endif
     c                   endif
      *
6.31 c                   if        ja1enh <> *blank
6.33 c                   if        b_pris = *on
6.31 c                   eval      p_s1im = w_pris
6.33 c                   endif
6.31 c                   else
6.33 c                   if        b_pris = *on
6.31 c                   eval      p_s1im = w_pris
6.33 c                   endif
6.31 c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ogrp
     c                   parm                    p_hgrp
     c                   parm                    p_ugrp
     c                   parm                    p_enum
     c                   parm                    p_modu
     c                   parm                    p_vare
6.31 c                   parm                    p_s1im
6.31 c                   parm                    p_enh1
6.31 c                   parm                    p_type
      *
6.20  *  Key for les  av avrudningsparameter
6.20  *
6.20 c     jav3l1_key    klist
6.20 c                   kfld                    jav3l1_firm
6.20 c                   kfld                    jav3l1_regl
6.20 c                   kfld                    jav3l1_frpr
      *
6.20  *  Key for les  av avrudningsregle
6.20  *
6.20 c     jav1l1_key    klist
6.20 c                   kfld                    jav1l1_firm
6.20 c                   kfld                    jav1l1_ogrp
6.20 c                   kfld                    jav1l1_hgrp
6.20 c                   kfld                    jav1l1_ugrp
6.20 c                   kfld                    jav1l1_levr
6.20 c                   kfld                    jav1l1_modu
6.20 c                   kfld                    jav1l1_vare
6.31  *
6.31  * Key for oppslag på leverandør
6.31  *
6.31 c     jlevl3_key    klist
6.31 c                   kfld                    levl3_enum
      *
     c                   endsr
