     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JP506R
      * Beskrivelse . . : Betingelse, velge betingelse i ordre innenfor nivå
      *
      *                   Programmet returnerer betingelsesnivå, betingelse
      *                   og innkjøpsprisfaktor/nettopris
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       120804 HKO  Nytt program
      *       200905 HKO  Utvidet input-parameter med leveringstype og
      *                   output-parameter med varetype og leveringstype
6.3x  *       230915 bof  Utvidet med prisgruppe på betingelse og frakt
6.3y  *       190118 nho  Lagt inn prisgruppe i parameter til JP510R
7.xx  * K000  010720 bsa  Takler brede skjermbilder
8.01  *PRG2   140622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjbetlb    if   e           k disk    rename(jbetpfr:jbetlbr)
     fjbetlc    if   e           k disk    rename(jbetpfr:jbetlcr)
     fjbetl1    if   e           k disk    rename(jbetpfr:jbetl1r)
     fjbetlr    if   e           k disk    rename(jbetpfr:jbetlrr)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fjmodl1    if   e           k disk    rename(jmodpfr:jmodl1r)
     fjraelr    if   e           k disk    rename(jraepfr:jraelrr)
      *
     fjp506d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(jpfirm)
     d p_ldor          s                   like(jpldor)
     d p_vare          s                   like(jpvare)
6.3x d p_prgr          s                   like(jpprgr)
     d p_lety          s                   like(jplety)
     d p_bldo          s                   like(jpldor)
     d p_bogr          s                   like(jpogrp)
     d p_bhgr          s                   like(jphgrp)
     d p_bugr          s                   like(jpugrp)
     d p_bmod          s                   like(jpmodn)
     d p_bvar          s                   like(jpvare)
     d p_bvty          s                   like(jpvtyp)
     d p_blet          s                   like(jplety)
     d p_bbet          s                   like(jpbeti)
     d p_ntpr          s              9  2
     d p_ifak          s              8  6
      *
      * Definering av Local data area
      *
     d                uds
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d jbetlb_vare     s                   like(jpvare)
6.3x d jbetlb_prgr     s                   like(jpprgr)
     d jbetlb_vtyp     s                   like(jpvtyp)
     d jbetlb_lety     s                   like(jplety)
6.3x d jbetlc_prgr     s                   like(jpprgr)
     d jbetlc_modn     s                   like(jpmodn)
     d jbetlc_vare     s                   like(jpvare)
     d jbetlc_vtyp     s                   like(jpvtyp)
     d jbetlc_lety     s                   like(jplety)
6.3x d jbetl1_prgr     s                   like(jpprgr)
     d jbetl1_ogrp     s                   like(jpogrp)
     d jbetl1_hgrp     s                   like(jphgrp)
     d jbetl1_ugrp     s                   like(jpugrp)
     d jbetl1_modn     s                   like(jpmodn)
     d jbetl1_vare     s                   like(jpvare)
     d jbetl1_vtyp     s                   like(jpvtyp)
     d jbetl1_lety     s                   like(jplety)
     d jbetlr_ldor     s                   like(jpldor)
6.3x d jbetlr_prgr     s                   like(jpprgr)
     d jbetlr_ogrp     s                   like(jpogrp)
     d jbetlr_hgrp     s                   like(jphgrp)
     d jbetlr_ugrp     s                   like(jpugrp)
     d jbetlr_modn     s                   like(jpmodn)
     d jbetlr_vare     s                   like(jpvare)
     d jbetlr_vtyp     s                   like(jpvtyp)
     d jbetlr_lety     s                   like(jplety)
     d jbetlr_rsts     s                   like(jprsts)
     d jbetlr_line     s                   like(jpline)
     d jvarl1_vare     s                   like(jvvare)
     d jlevl1_ldor     s                   like(jlldor)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d jmodl1_modn     s                   like(jumodn)
     d jraelr_eldo     s                   like(jpeldo)
6.3x d jraelr_eprg     s                   like(jpeprg)
     d jraelr_eogr     s                   like(jpeogr)
     d jraelr_ehgr     s                   like(jpehgr)
     d jraelr_eugr     s                   like(jpeugr)
     d jraelr_emod     s                   like(jpemod)
     d jraelr_evar     s                   like(jpevar)
     d jraelr_evty     s                   like(jpevty)
     d jraelr_elet     s                   like(jpelet)
     d jraelr_ebrs     s                   like(jpebrs)
     d jraelr_ebli     s                   like(jpebli)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_valg_ok       s              1    inz(*off)
      *
     d b_feil          s              1    inz(*off)
     d b_visn          s              1    inz(*off)
      *
     d w_firm          s                   like(jpfirm)
     d w_ldor          s                   like(jpldor)
     d w_vare          s                   like(jpvare)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(5)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for vise
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile                                  CRT SFL
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   if        b_valg_ok = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c     jbetl1_key    setll     jbetl1
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Velg betingelse
      *
     c                   if        b1valg = 1
     c                   eval      p_bldo = b2ldor
     c                   eval      p_bogr = b2ogrp
     c                   eval      p_bhgr = b2hgrp
     c                   eval      p_bugr = b2ugrp
     c                   eval      p_bmod = b2modn
     c                   eval      p_bvar = b2vare
     c                   eval      p_bvty = b2vtyp
     c                   eval      p_blet = b2lety
     c                   eval      p_bbet = b1beti
     c                   eval      p_ntpr = b1ntpr
     c                   eval      p_ifak = b1fakt
     c                   eval      b_valg_ok = *on
     c                   leave
     c                   endif
      *
      * Vis betingelse
      *
     c                   if        b1valg = 5
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis rabattelement
      *
     c                   if        b1valg = 7
     c                   eval      w_vare = b2vare
     c                   call      'JP510R'
     c                   parm                    w_firm
     c                   parm                    b2ldor
6.3y c                   parm                    b2prgr
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    b2ugrp
     c                   parm                    b2modn
     c                   parm                    w_vare
     c                   parm                    b2vtyp
     c                   parm                    b2lety
     c                   parm                    b1rsts
     c                   parm                    b1line
     c                   eval      b1valg = 0
     c                   exsr      finn_fant
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle skjermbilde for å vise betingelsesinfo
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      jbetlr_ldor = b2ldor
6.3x c                   eval      jbetlr_prgr = b2prgr
     c                   eval      jbetlr_ogrp = b2ogrp
     c                   eval      jbetlr_hgrp = b2hgrp
     c                   eval      jbetlr_ugrp = b2ugrp
     c                   eval      jbetlr_modn = b2modn
     c                   eval      jbetlr_vare = b2vare
     c                   eval      jbetlr_vtyp = b2vtyp
     c                   eval      jbetlr_lety = b2lety
     c                   eval      jbetlr_rsts = b1rsts
     c                   eval      jbetlr_line = b1line
     c     jbetlr_key    chain     jbetlr
      *
     c                   eval      c2ldor = jpldor
6.3x c                   eval      c2prgr = jpprgr
     c                   eval      c2ogrp = jpogrp
     c                   eval      c2hgrp = jphgrp
     c                   eval      c2ugrp = jpugrp
     c                   eval      c2modn = jpmodn
     c                   eval      c2vare = jpvare
     c                   eval      c2vtyp = jpvtyp
     c                   eval      c2lety = jplety
      *
     c                   eval      c2beti = jpbeti
     c                   eval      c2eusr = jpeusr
     c                   eval      c2hgrp = 0
     c                   eval      c2ugrp = 0
     c                   eval      c2utxt = *blank
     c                   eval      c2nobb = 0
     c                   eval      c2fdat = 0
     c                   eval      c2tdat = 0
     c                   eval      c2dato = 0
     c                   eval      c2edat = 0
     c                   if        jpfdat <> *loval
     c     *dmy          move      jpfdat        c2fdat
     c                   endif
     c                   if        jptdat <> *loval
     c     *dmy          move      jptdat        c2tdat
     c                   endif
     c                   if        jpdato <> *loval
     c     *dmy          move      jpdato        c2dato
     c                   endif
      *
     c     *dmy          move      jpodat        c2odat
     c     *dmy          move      jpedat        c2edat
      *
      * Henter beskrivelse
      *
     c                   eval      c2lnav = b2lnav
     c                   eval      c2mte1 = b2mte1
     c                   eval      c2nobb = b2nobb
      *
     c                   eval      c2otxt = *blank
     c                   eval      c2htxt = *blank
     c                   eval      c2utxt = *blank
     c                   eval      c2vtek = *blank
      *
     c                   if        c2ogrp <> 0
     c                   eval      vogrl1_oogr = c2ogrp
     c     vogrl1_key    chain     vogrl1
     c                   eval      c2otxt = vgotxt
     c                   endif
      *
     c                   if        c2hgrp <> 0
     c                   eval      vhgrl1_hogr = c2ogrp
     c                   eval      vhgrl1_hhgr = c2hgrp
     c     vhgrl1_key    chain     vhgrl1
     c                   eval      c2htxt = vghtxt
     c                   endif
      *
     c                   if        c2ugrp <> 0
     c                   eval      vugrl1_uogr = c2ogrp
     c                   eval      vugrl1_uhgr = c2hgrp
     c                   eval      vugrl1_uugr = c2ugrp
     c     vugrl1_key    chain     vugrl1
     c                   eval      c2utxt = vgutxt
     c                   endif
      *
     c                   if        c2vare <> *blank
     c                   eval      c2vtek = b2vte2
     c                   endif
      *
     c                   exfmt     c2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c     jbetl1_key    reade     jbetl1                                 15
      *
     c                   if        *in15                                        EOF
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1rsts = jprsts
     c                   eval      b1line = jpline
     c                   eval      b1beti = jpbeti
6.3x c                   eval      b1prgr = jpprgr
     c                   eval      b1akti = *blank
     c                   eval      b1rkod = *blank
      *
     c                   if        jpakti = 1
     c                   eval      b1akti = 'A'
     c                   endif
      *
     c                   if        jprsts = 1
     c                   eval      b1rkod = 'L'
     c                   endif
      *
      * Finner nettopris eller rabatt fra rabattelementene
      *
     c                   exsr      finn_fant
      *
      * Skriver til subfile
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å hente betingelsesnivå
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_nivÅ     begsr
      *
6.3x  * * prisgruppe **
6.3x c                   eval      jbetlb_prgr = p_prgr
6.3x c                   eval      jbetl1_prgr = p_prgr
6.3x c                   eval      jbetlc_prgr = p_prgr
      * Vare
      *
     c                   eval      jbetlb_vare = p_vare
     c                   eval      jbetlb_vtyp = 'S'
     c                   eval      jbetlb_lety = p_lety
      *
     c     jbetlb_key    chain     jbetlb
     c                   if        %found
     c                   leavesr
     c                   endif
      *
     c                   if        p_lety <> 0
     c                   eval      jbetlb_lety = 0
     c     jbetlb_key    chain     jbetlb
     c                   if        %found
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   eval      jbetlb_vtyp = *blank
     c                   eval      jbetlb_lety = p_lety
     c     jbetlb_key    chain     jbetlb
     c                   if        %found
     c                   leavesr
     c                   endif
      *
     c                   if        p_lety <> 0
     c                   eval      jbetlb_vtyp = *blank
     c                   eval      jbetlb_lety = 0
     c     jbetlb_key    chain     jbetlb
     c                   if        %found
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Modul
      *
     c                   if        jvmodn <> 0
      *
     c                   eval      jbetlc_modn = jvmodn
     c                   eval      jbetlc_vare = *blank
     c                   eval      jbetlc_vtyp = 'S'
     c                   eval      jbetlc_lety = p_lety
      *
     c     jbetlc_key    chain     jbetlc
     c                   if        %found
     c                   leavesr
     c                   endif
      *
     c                   if        p_lety <> 0
     c                   eval      jbetlc_lety = 0
     c     jbetlc_key    chain     jbetlc
     c                   if        %found
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   eval      jbetlc_vtyp = *blank
     c                   eval      jbetlc_lety = p_lety
     c     jbetlc_key    chain     jbetlc
     c                   if        %found
     c                   leavesr
     c                   endif
      *
     c                   if        p_lety <> 0
     c                   eval      jbetlc_vtyp = *blank
     c                   eval      jbetlc_lety = 0
     c     jbetlc_key    chain     jbetlc
     c                   if        %found
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Overgruppe/hovedgruppe/undergruppe
      *
     c                   eval      jbetl1_ogrp = jvogrp
     c                   eval      jbetl1_hgrp = jvhgrp
     c                   eval      jbetl1_ugrp = jvugrp
     c                   eval      jbetl1_modn = 0
     c                   eval      jbetl1_vare = *blank
     c                   eval      jbetl1_vtyp = 'S'
     c                   eval      jbetl1_lety = p_lety
      *
     c     jbetl1_key    chain     jbetl1
     c                   if        %found
     c                   leavesr
     c                   endif
      *
     c                   if        p_lety <> 0
     c                   eval      jbetl1_lety = 0
     c     jbetl1_key    chain     jbetl1
     c                   if        %found
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   eval      jbetl1_vtyp = *blank
     c                   eval      jbetl1_lety = p_lety
     c     jbetl1_key    chain     jbetl1
     c                   if        %found
     c                   leavesr
     c                   endif
      *
     c                   if        p_lety <> 0
     c                   eval      jbetl1_vtyp = *blank
     c                   eval      jbetl1_lety = 0
     c     jbetl1_key    chain     jbetl1
     c                   if        %found
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Overgruppe/hovedgruppe
      *
     c                   eval      jbetl1_ugrp = 0
     c                   eval      jbetl1_vtyp = 'S'
     c                   eval      jbetl1_lety = p_lety
      *
     c     jbetl1_key    chain     jbetl1
     c                   if        %found
     c                   leavesr
     c                   endif
      *
     c                   if        p_lety <> 0
     c                   eval      jbetl1_lety = 0
     c     jbetl1_key    chain     jbetl1
     c                   if        %found
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   eval      jbetl1_vtyp = *blank
     c                   eval      jbetl1_lety = p_lety
     c     jbetl1_key    chain     jbetl1
     c                   if        %found
     c                   leavesr
     c                   endif
      *
     c                   if        p_lety <> 0
     c                   eval      jbetl1_vtyp = *blank
     c                   eval      jbetl1_lety = 0
     c     jbetl1_key    chain     jbetl1
     c                   if        %found
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Overgruppe
      *
     c                   eval      jbetl1_hgrp = 0
     c                   eval      jbetl1_vtyp = 'S'
     c                   eval      jbetl1_lety = p_lety
      *
     c     jbetl1_key    chain     jbetl1
     c                   if        %found
     c                   leavesr
     c                   endif
      *
     c                   if        p_lety <> 0
     c                   eval      jbetl1_lety = 0
     c     jbetl1_key    chain     jbetl1
     c                   if        %found
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   eval      jbetl1_vtyp = *blank
     c                   eval      jbetl1_lety = p_lety
     c     jbetl1_key    chain     jbetl1
     c                   if        %found
     c                   leavesr
     c                   endif
      *
     c                   if        p_lety <> 0
     c                   eval      jbetl1_vtyp = *blank
     c                   eval      jbetl1_lety = 0
     c     jbetl1_key    chain     jbetl1
     c                   if        %found
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Generell (på leverandør)
      *
     c                   eval      jbetl1_ogrp = 0
     c                   eval      jbetl1_vtyp = 'S'
     c                   eval      jbetl1_lety = p_lety
      *
     c     jbetl1_key    chain     jbetl1
     c                   if        %found
     c                   leavesr
     c                   endif
      *
     c                   if        p_lety <> 0
     c                   eval      jbetl1_lety = 0
     c     jbetl1_key    chain     jbetl1
     c                   if        %found
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   eval      jbetl1_vtyp = *blank
     c                   eval      jbetl1_lety = p_lety
     c     jbetl1_key    chain     jbetl1
     c                   if        %found
     c                   leavesr
     c                   endif
      *
     c                   if        p_lety <> 0
     c                   eval      jbetl1_vtyp = *blank
     c                   eval      jbetl1_lety = 0
     c     jbetl1_key    chain     jbetl1
     c                   if        %found
     c                   leavesr
     c                   endif
     c                   endif
      *
6.3x  * Uten Prisgruppe
6.3x c                   eval      jbetl1_prgr = *blank
6.3x c                   eval      jbetlb_prgr = *blank
6.3x c                   eval      jbetlc_prgr = *blank
6.3x  *
6.3x c                   eval      jbetlb_vare = p_vare
6.3x c                   eval      jbetlb_vtyp = 'S'
6.3x c                   eval      jbetlb_lety = p_lety
6.3x  *
6.3x c     jbetlb_key    chain     jbetlb
6.3x c                   if        %found
6.3x c                   leavesr
6.3x c                   endif
6.3x  *
6.3x c                   if        p_lety <> 0
6.3x c                   eval      jbetlb_lety = 0
6.3x c     jbetlb_key    chain     jbetlb
6.3x c                   if        %found
6.3x c                   leavesr
6.3x c                   endif
6.3x c                   endif
6.3x  *
6.3x c                   eval      jbetlb_vtyp = *blank
6.3x c                   eval      jbetlb_lety = p_lety
6.3x c     jbetlb_key    chain     jbetlb
6.3x c                   if        %found
6.3x c                   leavesr
6.3x c                   endif
6.3x  *
6.3x c                   if        p_lety <> 0
6.3x c                   eval      jbetlb_vtyp = *blank
6.3x c                   eval      jbetlb_lety = 0
6.3x c     jbetlb_key    chain     jbetlb
6.3x c                   if        %found
6.3x c                   leavesr
6.3x c                   endif
6.3x c                   endif
6.3x  *
6.3x  * Modul
6.3x  *
6.3x c                   if        jvmodn <> 0
6.3x  *
6.3x c                   eval      jbetlc_modn = jvmodn
6.3x c                   eval      jbetlc_vare = *blank
6.3x c                   eval      jbetlc_vtyp = 'S'
6.3x c                   eval      jbetlc_lety = p_lety
6.3x  *
6.3x c     jbetlc_key    chain     jbetlc
6.3x c                   if        %found
6.3x c                   leavesr
6.3x c                   endif
6.3x  *
6.3x c                   if        p_lety <> 0
6.3x c                   eval      jbetlc_lety = 0
6.3x c     jbetlc_key    chain     jbetlc
6.3x c                   if        %found
6.3x c                   leavesr
6.3x c                   endif
6.3x c                   endif
6.3x  *
6.3x c                   eval      jbetlc_vtyp = *blank
6.3x c                   eval      jbetlc_lety = p_lety
6.3x c     jbetlc_key    chain     jbetlc
6.3x c                   if        %found
6.3x c                   leavesr
6.3x c                   endif
6.3x  *
6.3x c                   if        p_lety <> 0
6.3x c                   eval      jbetlc_vtyp = *blank
6.3x c                   eval      jbetlc_lety = 0
6.3x c     jbetlc_key    chain     jbetlc
6.3x c                   if        %found
6.3x c                   leavesr
6.3x c                   endif
6.3x c                   endif
6.3x  *
6.3x c                   endif
6.3x  *
6.3x  * Overgruppe/hovedgruppe/undergruppe
6.3x  *
6.3x c                   eval      jbetl1_ogrp = jvogrp
6.3x c                   eval      jbetl1_hgrp = jvhgrp
6.3x c                   eval      jbetl1_ugrp = jvugrp
6.3x c                   eval      jbetl1_modn = 0
6.3x c                   eval      jbetl1_vare = *blank
6.3x c                   eval      jbetl1_vtyp = 'S'
6.3x c                   eval      jbetl1_lety = p_lety
6.3x  *
6.3x c     jbetl1_key    chain     jbetl1
6.3x c                   if        %found
6.3x c                   leavesr
6.3x c                   endif
6.3x  *
6.3x c                   if        p_lety <> 0
6.3x c                   eval      jbetl1_lety = 0
6.3x c     jbetl1_key    chain     jbetl1
6.3x c                   if        %found
6.3x c                   leavesr
6.3x c                   endif
6.3x c                   endif
6.3x  *
6.3x c                   eval      jbetl1_vtyp = *blank
6.3x c                   eval      jbetl1_lety = p_lety
6.3x c     jbetl1_key    chain     jbetl1
6.3x c                   if        %found
6.3x c                   leavesr
6.3x c                   endif
6.3x  *
6.3x c                   if        p_lety <> 0
6.3x c                   eval      jbetl1_vtyp = *blank
6.3x c                   eval      jbetl1_lety = 0
6.3x c     jbetl1_key    chain     jbetl1
6.3x c                   if        %found
6.3x c                   leavesr
6.3x c                   endif
6.3x c                   endif
6.3x  *
6.3x  * Overgruppe/hovedgruppe
6.3x  *
6.3x c                   eval      jbetl1_ugrp = 0
6.3x c                   eval      jbetl1_vtyp = 'S'
6.3x c                   eval      jbetl1_lety = p_lety
6.3x  *
6.3x c     jbetl1_key    chain     jbetl1
6.3x c                   if        %found
6.3x c                   leavesr
6.3x c                   endif
6.3x  *
6.3x c                   if        p_lety <> 0
6.3x c                   eval      jbetl1_lety = 0
6.3x c     jbetl1_key    chain     jbetl1
6.3x c                   if        %found
6.3x c                   leavesr
6.3x c                   endif
6.3x c                   endif
6.3x  *
6.3x c                   eval      jbetl1_vtyp = *blank
6.3x c                   eval      jbetl1_lety = p_lety
6.3x c     jbetl1_key    chain     jbetl1
6.3x c                   if        %found
6.3x c                   leavesr
6.3x c                   endif
6.3x  *
6.3x c                   if        p_lety <> 0
6.3x c                   eval      jbetl1_vtyp = *blank
6.3x c                   eval      jbetl1_lety = 0
6.3x c     jbetl1_key    chain     jbetl1
6.3x c                   if        %found
6.3x c                   leavesr
6.3x c                   endif
6.3x c                   endif
6.3x  *
6.3x  * Overgruppe
6.3x  *
6.3x c                   eval      jbetl1_hgrp = 0
6.3x c                   eval      jbetl1_vtyp = 'S'
6.3x c                   eval      jbetl1_lety = p_lety
6.3x  *
6.3x c     jbetl1_key    chain     jbetl1
6.3x c                   if        %found
6.3x c                   leavesr
6.3x c                   endif
6.3x  *
6.3x c                   if        p_lety <> 0
6.3x c                   eval      jbetl1_lety = 0
6.3x c     jbetl1_key    chain     jbetl1
6.3x c                   if        %found
6.3x c                   leavesr
6.3x c                   endif
6.3x c                   endif
6.3x  *
6.3x c                   eval      jbetl1_vtyp = *blank
6.3x c                   eval      jbetl1_lety = p_lety
6.3x c     jbetl1_key    chain     jbetl1
6.3x c                   if        %found
6.3x c                   leavesr
6.3x c                   endif
6.3x  *
6.3x c                   if        p_lety <> 0
6.3x c                   eval      jbetl1_vtyp = *blank
6.3x c                   eval      jbetl1_lety = 0
6.3x c     jbetl1_key    chain     jbetl1
6.3x c                   if        %found
6.3x c                   leavesr
6.3x c                   endif
6.3x c                   endif
6.3x  *
6.3x  * Generell (på leverandør)
6.3x  *
6.3x c                   eval      jbetl1_ogrp = 0
6.3x c                   eval      jbetl1_vtyp = 'S'
6.3x c                   eval      jbetl1_lety = p_lety
6.3x  *
6.3x c     jbetl1_key    chain     jbetl1
6.3x c                   if        %found
6.3x c                   leavesr
6.3x c                   endif
6.3x  *
6.3x c                   if        p_lety <> 0
6.3x c                   eval      jbetl1_lety = 0
6.3x c     jbetl1_key    chain     jbetl1
6.3x c                   if        %found
6.3x c                   leavesr
6.3x c                   endif
6.3x c                   endif
6.3x  *
6.3x c                   eval      jbetl1_vtyp = *blank
6.3x c                   eval      jbetl1_lety = p_lety
6.3x c     jbetl1_key    chain     jbetl1
6.3x c                   if        %found
6.3x c                   leavesr
6.3x c                   endif
6.3x  *
6.3x c                   if        p_lety <> 0
6.3x c                   eval      jbetl1_vtyp = *blank
6.3x c                   eval      jbetl1_lety = 0
6.3x c     jbetl1_key    chain     jbetl1
6.3x c                   if        %found
6.3x c                   leavesr
6.3x c                   endif
6.3x c                   endif
6.3x c                   endsr
6.3x  *
6.3x  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne nettopris eller rabatt fra rabattelementene
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_fant     begsr
      *
     c                   eval      b1fakt = 1
     c                   eval      b1ntpr = 0
     c                   eval      b1raba = 0
      *
     c                   eval      jraelr_eldo = b2ldor
6.3x c                   eval      jraelr_eprg = b2prgr
     c                   eval      jraelr_eogr = b2ogrp
     c                   eval      jraelr_ehgr = b2hgrp
     c                   eval      jraelr_eugr = b2ugrp
     c                   eval      jraelr_emod = b2modn
     c                   eval      jraelr_evar = b2vare
     c                   eval      jraelr_evty = b2vtyp
     c                   eval      jraelr_elet = b2lety
     c                   eval      jraelr_ebrs = b1rsts
     c                   eval      jraelr_ebli = b1line
     c     jraelr_key    setll     jraelr
     c     jraelr_key    reade     jraelr
     c                   dow       not %eof
     c                   select
     c                   when      jpereg = '1'
     c                   eval      b1fakt = b1fakt - (jpever / 100 * b1fakt)
     c                   when      jpereg = '2'
     c                   eval      b1fakt = b1fakt - (jpever / 100)
     c                   when      jpereg = 'N'
     c                   eval      b1ntpr = b1ntpr + jpever
     c                   endsl
     c     jraelr_key    reade     jraelr
     c                   enddo
      *
     c                   eval      b1raba = (1 - b1fakt) * 100
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
6.3x c                   parm                    p_prgr
     c                   parm                    p_vare
     c                   parm                    p_lety
     c                   parm                    p_bldo
     c                   parm                    p_bogr
     c                   parm                    p_bhgr
     c                   parm                    p_bugr
     c                   parm                    p_bmod
     c                   parm                    p_bvar
     c                   parm                    p_bvty
     c                   parm                    p_blet
     c                   parm                    p_bbet
     c                   parm                    p_ntpr
     c                   parm                    p_ifak
      *
      * Key for les på betingelse
      *
     c     jbetl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
6.3x c                   kfld                    jbetl1_prgr
     c                   kfld                    jbetl1_ogrp
     c                   kfld                    jbetl1_hgrp
     c                   kfld                    jbetl1_ugrp
     c                   kfld                    jbetl1_modn
     c                   kfld                    jbetl1_vare
     c                   kfld                    jbetl1_vtyp
     c                   kfld                    jbetl1_lety
      *
      * Key for oppslag på betingelse
      *
     c     jbetlb_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    jbetlb_vare
6.3x c                   kfld                    jbetlb_prgr
     c                   kfld                    jbetlb_vtyp
     c                   kfld                    jbetlb_lety
      *
     c     jbetlc_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ldor
     c                   kfld                    jbetlc_modn
     c                   kfld                    jbetlc_vare
6.3x c                   kfld                    jbetlc_prgr
     c                   kfld                    jbetlc_vtyp
     c                   kfld                    jbetlc_lety
      *
     c     jbetlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jbetlr_ldor
6.3x c                   kfld                    jbetlr_prgr
     c                   kfld                    jbetlr_ogrp
     c                   kfld                    jbetlr_hgrp
     c                   kfld                    jbetlr_ugrp
     c                   kfld                    jbetlr_modn
     c                   kfld                    jbetlr_vare
     c                   kfld                    jbetlr_vtyp
     c                   kfld                    jbetlr_lety
     c                   kfld                    jbetlr_rsts
     c                   kfld                    jbetlr_line
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på modul
      *
     c     jmodl1_key    klist
     c                   kfld                    jmodl1_modn
      *
      * Key for les på rabattelementer
      *
     c     jraelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jraelr_eldo
6.3x c                   kfld                    jraelr_eprg
     c                   kfld                    jraelr_eogr
     c                   kfld                    jraelr_ehgr
     c                   kfld                    jraelr_eugr
     c                   kfld                    jraelr_emod
     c                   kfld                    jraelr_evar
     c                   kfld                    jraelr_evty
     c                   kfld                    jraelr_elet
     c                   kfld                    jraelr_ebrs
     c                   kfld                    jraelr_ebli
      *
      *
      * Henter firma,leverandør og varenummer fra parameter, og initierer
      * skjermbildet
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_ldor = p_ldor
      *
     c                   eval      b2var2 = p_vare
      *
      * Vareinformasjon (parameter)
      *
     c                   eval      jvarl1_vare = p_vare
     c     jvarl1_key    chain     jvarl1
     c                   eval      b2vte2 = jvtek1
      *
      * Henter betingelsesnivå
      *
     c                   exsr      hent_nivÅ
      *
     c                   eval      b2ldor = jpldor
     c                   eval      b2ogrp = jpogrp
     c                   eval      b2hgrp = jphgrp
     c                   eval      b2ugrp = jpugrp
     c                   eval      b2modn = jpmodn
     c                   eval      b2vare = jpvare
     c                   eval      b2vtyp = jpvtyp
     c                   eval      b2lety = jplety
6.3x c                   eval      b2prgr = jpprgr
      *
      * Dersom betingelsesnivå ble funnet hentes beskrivelse av nivået
      *
     c                   if        jpldor <> 0
      *
     c                   eval      jlevl1_ldor = b2ldor
     c     jlevl1_key    chain     jlevl1
     c                   eval      b2lnav = jlnavn
      *
     c                   if        b2ogrp <> 0
     c                   eval      vogrl1_oogr = b2ogrp
     c     vogrl1_key    chain     vogrl1
     c                   eval      b2utxt = vgotxt
     c                   endif
      *
     c                   if        b2hgrp <> 0
     c                   eval      vhgrl1_hogr = b2ogrp
     c                   eval      vhgrl1_hhgr = b2hgrp
     c     vhgrl1_key    chain     vhgrl1
     c                   eval      b2utxt = vghtxt
     c                   endif
      *
     c                   if        b2ugrp <> 0
     c                   eval      vugrl1_uogr = b2ogrp
     c                   eval      vugrl1_uhgr = b2hgrp
     c                   eval      vugrl1_uugr = b2ugrp
     c     vugrl1_key    chain     vugrl1
     c                   eval      b2utxt = vgutxt
     c                   endif
      *
     c                   if        b2modn <> 0
     c                   eval      jmodl1_modn = c2modn
     c     jmodl1_key    chain     jmodl1
     c                   eval      b2mte1 = jumte1
     c                   endif
      *
     c                   if        b2vare <> *blank
     c                   eval      b2nobb = jvnobb
     c                   endif
      *
     c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      jbetl1_ogrp = b2ogrp
     c                   eval      jbetl1_hgrp = b2hgrp
     c                   eval      jbetl1_ugrp = b2ugrp
     c                   eval      jbetl1_modn = b2modn
     c                   eval      jbetl1_vare = b2vare
     c                   eval      jbetl1_vtyp = b2vtyp
     c                   eval      jbetl1_lety = b2lety
6.3x c                   eval      jbetl1_prgr = b2prgr
     c     jbetl1_key    setll     jbetl1
      *
     c                   eval      *in22 = *on
     c                   exsr      crt_subfile
      *
     c                   endsr
