     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK618R                                              *
      * Beskrivelse . . : Utskrift av bankgiro                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
6.nu  * R6.20     280515  Utvidelse av kort kid                    nho   NHO  *
      * --------- ------  ---------------------------------------------- ---- *
      *                                                                       *
      *                                                                       *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frk618d    cf   e             workstn
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     faforl1    if   e           k disk    rename(aforpfr:aforl1r)
     frk618p    o    e             printer oflind(*in30)
      *
      * Kroner i alfafelt
      *
     d akr             s              1    dim(9)
      *
      * - - - - - - - - - - - - - -
      * Henter inn data fra LDA
      * - - - - - - - - - - - - - -
      *
     d                uds
     d l_ruti                800    809
     d l_hex1                760    779
     d l_hex2                780    799
     d l_sfir                845    845
     d l_alin                856    858  0
     d l_kidk                883    883  0
     d l_ocrk                885    885  0
     d l_firm                944    946  0
      *
      * Datastruktur for kid-feltet
      *
     d d_kkid          ds            25
     d  d_kkus                       24
     d  d_kmod                        1  0
      *
     d n               s              2  0
     d w_pkid          s             25
     d w_firm          s              3  0
     d w_avde          s              4  0
     d w_ruti          s             10
     d w_aprt          s              1  0
      *
     d w_prfi          s              1  0
     d w_ffir          s              3  0
     d w_fnav          s             30
     d w_fad1          s             30
     d w_fad2          s             30
     d w_fste          s             30
     d w_fftx          s             30
     d w_tfax          s              8
     d w_ftlf          s             11
     d w_ffax          s             11
     d w_tbnk          s             10
     d w_fbgi          s             13
      *
6.nu d*w_kidr          s              6  0
6.nu d w_kidr          s              8  0
     d w_ksif          s              1  0
     d w_kkus          s             24
     d w_kkid          s             21
      *
     c/space 3
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av formater                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede formater
      * - - - - - - - - - -
      *
      * P1GIR  - Bankgiro hode beløpet gjelder
      * P2GIR  - Bankgiro hode gebyr
      * P3GIR  - Bankgiro hode rabatt
      * P4GIR  - Bankgiro hode navn/adresse
      * P5GIR  - Bankgiro beløpslinje
      *
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Styre-rutine                                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Behandle girotransaksjoner
      *
     c     a2tagb        tag
      *
     c                   exfmt     a2bld
      *
     c                   if        *inkc
     c                   goto      xslutt
     c                   endif
      *
     c                   move      a2fdat        a1fdat
      *
      *
      * Tekstlinjer
      *
     c                   eval      a1txt1 = a2txt1
     c                   eval      a1txt2 = a2txt2
     c                   eval      a1txt3 = a2txt3
     c                   eval      a1txt4 = a2txt4
     c                   eval      a1txt5 = a2txt5
     c                   eval      a1txt6 = a2txt6
      *
      * Navn/Adresse betaler
      *
     c                   move      a2bnav        a4navn
     c                   move      a2bgat        a4gate
     c                   move      a2badr        a4adr2
     c                   move      a2bste        a4sted
      *
      * Navn/Adresse mottaker
      *
     c                   eval      a5fbgi = a2mbgi
     c                   eval      a6fbgi = a2mbgi
      *
     c                   eval      a4fnav = a2mnav
     c                   eval      a4fad1 = a2mgat
     c                   eval      a4fad2 = a2madr
     c                   eval      a4fste = a2mste
      *
      * Beløpslinje
      *
     c                   exsr      xajour
      *
      * Skriv giro
      *
     c                   eval      n = 0
      *
     c                   dow       n < a2anta
     c                   add       1             n
     c                   exsr      xprint
     c                   enddo
      *
     c                   goto      a2tagb
      *
     c/space 3
      *
      * Ferdig
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
      *
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * XAJOUR Klargjør data for print                                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xajour        begsr
      *
      * Klargjøre kidfelt
      *
      * Bygger opp kiddfelt
      *
     c                   eval      w_kkus = a2kkid
     c                   eval      w_ksif = *zero
      *
      * Beregning av sjekksiffer på kiddfelt
      *
     c                   call      'AK720R'
     c                   parm                    w_kkus
     c                   parm                    w_ksif
      *
     c                   eval      d_kmod = w_ksif
     c                   eval      d_kkus = w_kkus
     c                   eval      w_kkid = d_kkid
      *
     c                   select
     c                   when      l_kidk = 0
     c                   move      *blank        a5kkid
      *
     c                   when      l_kidk = 1
     c                   eval      a5kkid = %trim(w_kkid)
      *
     c                   endsl
      *
      * Beregne modulus 10 på beløpsfeltet
      *
     c                   move      *blank        d_kkus
     c                   move      a2belp        w_kkus
      *
     c                   call      'AK720R'
     c                   parm                    w_kkus
     c                   parm                    w_ksif
      *
     c                   move      w_ksif        a5bsif
      *
      * Splitte kr/øre
      *
     c                   movel     a2belp        a5kron
     c                   move      a2belp        a5Ører
      *
      * Fjerne foranstilte nuller
      * i et alfanummerisk felt
      *
     c                   movea     a5kron        akr
     c                   z-add     1             n
      *
     c                   dow       akr(n) = '0'
     c                   move      ' '           akr(n)
     c                   add       1             n
     c     n             cabgt     9             xsorti
     c                   enddo
      *
     c     xsorti        tag
      *
     c                   movea     akr(1)        a5kron
      *
     c                   endsr
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * XPRINT Skriver bankgiro                                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xprint        begsr
      *
      * Skriv beløpet gjelder
      *
     c                   write     p1gir                                30
      *
      * Skriv firmanavn
      *
     c                   write     p4gir                                30
      *
      * Skrive giro H-linje dersom ikke OCR
      *
     c                   if        l_ocrk = 0
     c                   write     p5gir                                30
     c                   endif
      *
      * Skrive giro H-linje dersom OCR og 4224
      *
     c                   if        l_ocrk = 1
     c                   write     p6gir                                30
     c                   endif
      *
      * Skrive giro H-linje dersom OCR og Axix-boks
      *
     c                   if        l_ocrk = 2
     c                   move      l_hex1        p1hex1
     c                   move      l_hex2        p1hex2
     c                   write     p7gir                                30
     c                   endif
      *
      * Skrive bankgiro H-linje dersom 5224 skriver
      *
     c                   if        l_ocrk = 3
     c                   write     p8gir                                30
     c                   endif
      *
      * Skrive bankgiro H-linje dersom OCR og Nett-print
      *
     c                   if        l_ocrk = 4
     c                   move      l_hex1        p1hex1
     c                   move      l_hex2        p1hex2
     c                   write     p9gir                                30
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Key for firmaregister
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for formular-register
      *
     c     aforl1_key    klist
     c                   kfld                    w_ruti
      *
     c                   eval      w_firm = l_firm
     c                   move      *zeros        w_pkid
     c                   eval      n = *zero
     c                   eval      a2kkid = *blank
      *
      * Hente inn formular-register
      *
     c                   eval      w_ruti = 'RK618 '
     c     aforl1_key    chain     aforl1r
      *
     c                   if        not%found
     c                   eval      afinst = 1
     c                   eval      afprfi = 1
     c                   endif
      *
      *  Hent inn firma/avd. opplysninger dersom
      *  kode for det er satt i rutineregister
      *
     c                   if        afprfi = 1 or
     c                             afprfi = 2
     c                   eval      w_prfi = afprfi
     c                   eval      w_ffir = w_firm
     c                   eval      w_avde = 0
      *
     c                   call      'RÅ707R'
     c                   parm                    w_prfi
     c                   parm                    w_ffir
     c                   parm                    w_avde
     c                   parm                    w_fnav
     c                   parm                    w_fad1
     c                   parm                    w_fad2
     c                   parm                    w_fste
     c                   parm                    w_fftx
     c                   parm                    w_tfax
     c                   parm                    w_ftlf
     c                   parm                    w_ffax
     c                   parm                    w_tbnk
     c                   parm                    w_fbgi
      *
     c                   eval      a2mbgi = w_fbgi
      *
     c                   eval      a2mnav = w_fnav
     c                   eval      a2mgat = w_fad1
     c                   eval      a2madr = w_fad2
     c                   eval      a2mste = w_fste
     c                   endif
      *
      * Skal firmanavn droppes på bankgiro ?
      *
     c                   if        w_aprt = 2
     c                   eval      a2mnav = *blank
     c                   eval      a2mgat = *blank
     c                   eval      a2madr = *blank
     c                   eval      a2mste = *blank
     c                   endif
      *
     c                   endsr
