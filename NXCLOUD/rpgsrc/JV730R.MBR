     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JV730R
      * Beskrivelse . . : Batch program som oppdaterer vareinformasjon i
      *                   EVR på varer som passer til utvalgskriteriene
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       180197 HKO  Nytt program
      *       171097 HKO  Kaller fellesprogram for oppdatering av
      *                   vareinformasjon i EVR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvarl5    if   e           k disk    rename(jvarpfr:jvarl5r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_ldor          s                   like(jvldor)
     d p_ogrp          s                   like(jvogrp)
     d p_hgrp          s                   like(jvhgrp)
     d p_ugrp          s                   like(jvugrp)
      *
      * Definering av variabler i nøkler
      *
     d jvarl5_ldor     s                   like(jvldor)
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Kaller behandlingsrutine for selektering og behandling av varer
      * i EVR
      *
     c                   exsr      behandling
      *
      * Avslutt
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av poster som matcher utplukkskriteriene
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   eval      jvarl5_ldor = p_ldor
     c     jvarl5_key    setll     jvarl5
     c     jvarl5_key    reade     jvarl5                                 90
     c                   dow       *in90 = *off
      *
      * Sjekker om varene skal være med på utplukket
      *
     c                   if        p_ogrp <> 0 and
     c                             p_ogrp <> jvogrp
     c                   goto      les_neste
     c                   endif
      *
     c                   if        p_hgrp <> 0 and
     c                             p_hgrp <> jvhgrp
     c                   goto      les_neste
     c                   endif
      *
     c                   if        p_ugrp <> 0 and
     c                             p_ugrp <> jvugrp
     c                   goto      les_neste
     c                   endif
      *
     c                   eval      vvarl1_vare = jvvare
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *on
     c                   goto      les_neste
     c                   endif
      *
      * Kaller fellesprogram for oppdatering av vareinformasjon i EVR
      *
     c                   call      'JV755R'
     c                   parm                    w_firm
     c                   parm                    jvvare
      *
      * Leser neste post og tester utvalg
      *
     c     les_neste     tag
      *
     c     jvarl5_key    reade     jvarl5                                 90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
     c                   parm                    p_ogrp
     c                   parm                    p_hgrp
     c                   parm                    p_ugrp
      *
      * Key for les på vare
      *
     c     jvarl5_key    klist
     c                   kfld                    jvarl5_ldor
      *
      * Key for oppslag EVR-vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
