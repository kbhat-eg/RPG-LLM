     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VE711R
      * Beskrivelse . . : Henter ean.nr ut fra gitt varenr + enhet
      *                   bruker nobb-lev. som ligger på vvarpf
      *                   Henter kun fra VVEPPF dvs. med enhet
      *
      *                   Input : firma, varenr., enhet
      *                   Output: ean.nr  status (1 = ok)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       080709 bof  Nytt program
6.10  *       171210 bsa  Sjekker om det finnes GTIN nummer uavhengig av NOBB leverandør
6.11  *       120120 bof  Justert en test om varen finnes
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarlr    if   e           k disk    rename(vvarpfr:vvarlrr)
     fvveplr    if   e           k disk    rename(vveppfr:vveplrr)
      *
      * Definering av variable i nøkler
      *
     d vvarlr_firm     s                   like(vvfirm)
     d vvarlr_vare     s                   like(vvvare)
     d vveplr_pfir     s                   like(vepfir)
     d vveplr_pvar     s                   like(vepvar)
     d vveplr_penh     s                   like(vepenh)
     d vveplr_pldo     s                   like(vepldo)
6.10 d vvepl1_pfir     s                   like(vepfir)
6.10 d vvepl1_pvar     s                   like(vepvar)
6.10 d vvepl1_penh     s                   like(vepenh)
      *
      * Definering av pamametre
      *
     d p_firm          s                   like(vepfir)
     d p_eann          s                   like(vepgti)
     d p_vare          s                   like(vepvar)
     d p_enhe          s                   like(vepenh)
     d p_stat          s              1
      *
      *
     d                uds
     d l_user                911    920
     d l_sfir                944    946  0
     d l_savd                947    950  0
     d l_lagr               1001   1002  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer output-parametre
      *
     c                   eval      p_eann = 0
     c                   eval      p_stat = *blank
      *
     c                   eval      vvarlr_firm = p_firm
     c                   eval      vvarlr_vare = p_vare
     c     vvarlr_key    chain     vvarlr
6.11 c                   if        not %found(vvarlr)
     c                   goto      avslutt
     c                   endif
      *
      * Henter eannr.
      *
     c                   eval      vveplr_pfir = p_firm
     c                   eval      vveplr_pvar = p_vare
     c                   eval      vveplr_penh = p_enhe
     c                   eval      vveplr_pldo = vvldor
     c     vveplr_key    chain     vveplr
     c                   if        %found(vveplr) and
     c                             vepgti <> 0
     c                   eval      p_eann = vepgti
     c                   eval      p_stat = '1'
6.10 c                   goto      avslutt
     c                   endif
6.10  *
6.10  * Sjekk en gang til på VVEP uavhengig av leverandør, men med angitt enhet
6.10  *
6.10 c                   eval      vvepl1_pfir = p_firm
6.10 c                   eval      vvepl1_pvar = p_vare
6.10 c                   eval      vvepl1_penh = p_enhe
6.10 c     vvepl1_key    setll     vveplr
6.10 c     vvepl1_key    reade     vveplr                                 91
6.10 c                   dow       *in91 = *off
6.10  * Har vi match på enhet ?
6.10 c                   if        p_enhe = vepenh and
6.10 c                             vepgti <> 0
6.10 c                   eval      p_eann = vepgti
6.10 c                   eval      p_stat = '1'
6.10 c                   goto      avslutt
6.10 c                   endif
6.10 c     vvepl1_key    reade     vveplr                                 91
6.10 c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_enhe
     c                   parm                    p_eann
     c                   parm                    p_stat
      *
      * Key for les på vareenhets-register
      *
     c     vveplr_key    klist
     c                   kfld                    vveplr_pfir
     c                   kfld                    vveplr_pvar
     c                   kfld                    vveplr_penh
     c                   kfld                    vveplr_pldo
6.10  *
6.10  * Key for les på VVEP-register
6.10  *
6.10 c     vvepl1_key    klist
6.10 c                   kfld                    vvepl1_pfir
6.10 c                   kfld                    vvepl1_pvar
6.10 c                   kfld                    vvepl1_penh
      *
      * Key for les på vareregister
      *
     c     vvarlr_key    klist
     c                   kfld                    vvarlr_firm
     c                   kfld                    vvarlr_vare
      *
      *
     c                   endsr
