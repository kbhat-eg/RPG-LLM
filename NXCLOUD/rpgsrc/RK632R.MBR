      /TITLE  Utskrift av saldoliste
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK632R                                              *
      * Beskrivelse . . : Utskrift av aldersfordelt saldoliste - kunder       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * V5.00     300702  Lagt om til ekstern definert rapport            KOB *
      * V5.00     150802  Lagt om strukturen i programmet noe             KOB *
      * V5.00     100902  Redusert navnefeltet til 18 pos av plasshensyn  KOB *
T5.00 * V5.00     040403  Ny param. Kun konti m/poster i kol. 1-4.        HFL *
T5.00 * V5.40     151105  Utvidet antall posisjoner total kjøp i år       KOB *
T5.00 * V5.40     100206  Korrigert datastruktur                          KOB *
      * V5.40     020306  Tar hensyn til at kredittdager < -999           HFL *
      * V5.40     180506  Kjøp i år summeres ved brudd (bare comp.)       HFL *
      * V5.50     220107  Endret antall siffer i kjøp i år-felt           KOB *
pdf   * R6.10     070610  Innført PDF utskrift. Innebærer primært å finne BSA *
pdf   *                   generert spool, og kall av AP620R med dette som     *
pdf   *                   parametre.                                          *
6.11  * R6.10 071010 BSA  Overfører bacthtype som en del parameterlista. Viser
6.11  *                   hva slags rutine som kjøres. Legges med i arkivet.
6.20  * R6.20 080911 NHO  Rekompilert pga endring i RPARRKDS            *NHO
6.21  * 6.20  061212 bsa  Tillegg for å berike metataggger
6.30  * R6.30 180816 bsa  Rokkert på rekkefølge av close printerfile
7.01  * 7.01  220620 bsa  To formater på detaljer. Skilles med antall mellomrom.
7.01  *                   MGR har 3 og XL Nett hare ett. Switchstyres.
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frkw1l1    if   e           k disk    rename(rkw1pfr:rkw1l1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fraa3l1    if   e           k disk    rename(raa3pfr:raa3l1r)
      *
     frk632p    o    e             printer oflind(*in30)
pdf  f                                     usropn
      *
6.11 d p_btyp          s            100
      *
     d bel             s             11  2 dim(6)
V500 d*txt             s             20    dim(6) ctdata perrcd(1)
      *
      /eject
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      * Data struktur
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      *
     d                 ds
     d wkwrec                  2    192
     d  wkfirm                        3  0 overlay(wkwrec)
     d  wkkund                        6  0 overlay(wkwrec:4)
V500 d  wknavn                       18    overlay(wkwrec:10)
     d  wkkper                       30    overlay(wkwrec:40)
     d  wktlfn                       11    overlay(wkwrec:70)
     d  wksald                       11  2 overlay(wkwrec:81)
     d  wkbel0                       11  2 overlay(wkwrec:92)
     d  wkbel1                       11  2 overlay(wkwrec:103)
     d  wkbel2                       11  2 overlay(wkwrec:114)
     d  wkbel3                       11  2 overlay(wkwrec:125)
     d  wkbel4                       11  2 overlay(wkwrec:136)
     d  wkbel5                       11  2 overlay(wkwrec:147)
     d  wkinka                       11  2 overlay(wkwrec:158)
     d  wkkjop                       12  2 overlay(wkwrec:169)
     d  wkotid                        6  0 overlay(wkwrec:181)
      *
     d bbel1           s                   like(wksald)
     d bbel2           s                   like(wksald)
     d bbel3           s                   like(wksald)
     d bbel4           s                   like(wksald)
     d bbel5           s                   like(wksald)
     d bbel6           s                   like(wksald)
E5.50d bkjop           s             12  2
     d bkrtid          s              9  0
     d brsum           s              1
     d brtxt           s             18
     d bsntid          s              9  0
     d btink           s                   like(wksald)
     d btsal           s                   like(wksald)
     d kat             s              4  0
     d key             s             25
     d kksant          s              6  0
     d kptxt           s             20
     d msal            s              6  0
     d nsal            s              6  0
     d usal            s              6  0
     d pavdsx          s              3
     d pkatsx          s              3
     d pkompx          s              3
     d pkperx          s              3
     d psaldx          s              3
     d pkjopx          s              3
     d pselsx          s              3
     d psortx          s             10
     d ptlfx           s              3
     d sel             s              4  0
     d tbel1           s                   like(wksald)
     d tbel2           s                   like(wksald)
     d tbel3           s                   like(wksald)
     d tbel4           s                   like(wksald)
     d tbel5           s                   like(wksald)
     d tbel6           s                   like(wksald)
     d tink            s                   like(wksald)
E5.50d tkjop           s             12  2
     d tkrtid          s              9  0
     d tksant          s              6  0
     d tkun            s              6  0
     d tltxt           s             20
     d tsald           s                   like(wksald)
     d msald           s                   like(wksald)
     d nsald           s                   like(wksald)
     d tsntid          s              9  0
      *
     d w_firm          s                   like(ra3fir)
     d w_3ka1          s                   like(ra3ka1)
     d w_3ka2          s                   like(ra3ka2)
     d w_3ka3          s                   like(ra3ka3)
     d w_kniv          s                   like(rwkniv)
      *
     d p_stat          s              1
     d p_gkod          s              4  0
     d p_ikod          s              4  0
     d p_kkod          s              4  0
     d p_tx20          s             20
      *
pdf  d splfname2       s             10a
pdf  d jobname2        s             10a
pdf  d username2       s             10a
pdf  d jobnbr2         s              6a
pdf  d splfnbr2        s             10i 0
pdf  d splfname3       s             10a
pdf  d jobname3        s             10a
pdf  d username3       s             10a
pdf  d jobnbr3         s              6a
pdf  d splfnbr3        s             10i 0
6.21 d p_tagg0         s             20
6.21 d p_tagg0val      s             50
6.21 d p_tagg1         s             20
6.21 d p_tagg1val      s             50
6.21 d p_tagg2         s             20
6.21 d p_tagg2val      s             50
6.21 d p_tagg3         s             20
6.21 d p_tagg3val      s             50
6.21 d p_tagg4         s             20
6.21 d p_tagg4val      s             50
6.21 d p_tagg5         s             20
6.21 d p_tagg5val      s             50
6.21 d p_tagg6         s             20
6.21 d p_tagg6val      s             50
6.21 d p_tagg7         s             20
6.21 d p_tagg7val      s             50
6.21 d p_tagg8         s             20
6.21 d p_tagg8val      s             50
6.21 d p_tagg9         s             20
6.21 d p_tagg9val      s             50
6.21 d p_refn          s             50
6.21 d p_dspl          s            100
pdf   *
pdf  d qsprilsp        pr                  extpgm('QSPRILSP')
pdf  d rcvvar                     32767a   options(*varsize)
pdf  d rcvvarlen                     10i 0 const
pdf  d format                         8a   const
pdf  d errorcode                  32767a   options(*varsize)
pdf   *
pdf  d sprl0100        ds
pdf  d  bytesrtn                     10i 0
pdf  d  bytesavl                     10i 0
pdf  d  splfname                     10a
pdf  d  jobname                      10a
pdf  d  username                     10a
pdf  d  jobnbr                        6a
pdf  d  splfnbr                      10i 0
pdf  d  systemname                    8a
pdf  d  createdate                    7a
pdf  d                                1a
pdf  d  createtime                    6a
pdf   *
pdf  d errorcode       ds
pdf  d  bytesprov                    10i 0 inz(0)
pdf  d  byteavail                    10i 0 inz(0)
pdf   *
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      * LOCAL DATA
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     d/COPY QCPYLESRC,RPARRKDS
pdf  d l_poff                584    584
pdf  d l_utqm                585    594
pdf  d l_bach                595    635
pdf  d l_arch                636    636  0
pdf  d l_skje                637    637  0
pdf  d l_land                638    638  0
pdf  d l_exlp                639    639  0
pdf  d l_mail                640    640  0
pdf  d l_outq                810    819
7.01 d l_filg                931    933
T5.00d pkoll                 501    501
      /eject
      *
7.01   dcl-s co402_verdi1  char(1);
7.01   dcl-s co402_verdi2  char(2048);
7.01   DCL-PR CO402R EXTPGM('CO402R');
7.01     p_filgrp            char(3)     const;
7.01     p_firm              packed(3:0) const;
7.01     p_lager             packed(2:0) const;
7.01     p_nokkel            char(50)    const;
7.01     p_verdi1            char(1);
7.01     p_verdi2            char(2048);
7.01   END-PR;
7.01 d u_701           s              1    inz(*off)
      *
     c     nyles         tag
      *
     c                   read      rkw1l1                                 99
      *
     c                   if        *in99 = *on
     c                   eval      *inlr = *on
     c                   goto      slutt
     c                   endif
      *
     c                   move      rwkrec        wkwrec
      *
      * Telle kunder
      *
     c                   eval      tkun = tkun + 1
      *
     c                   select
     c                   when      wksald > *zeros
     c                   eval      msal = msal + 1
     c                   when      wksald = *zeros
     c                   eval      usal = usal + 1
     c                   when      wksald < *zeros
     c                   eval      nsal = nsal + 1
     c                   endsl
      *
      * BRUDDSUM ?
      *
     c                   if        btsal <> *zeros and
     c                             brsum <> *blank
     c                   exsr      brudd
     c                   exsr      nyside
     c                   else
     c                   eval      w_kniv = rwkniv
     c                   endif
      *
     c                   move      wkbel0        bel01
     c                   move      wkbel1        bel02
     c                   move      wkbel2        bel03
     c                   move      wkbel3        bel04
     c                   move      wkbel4        bel05
     c                   move      wkbel5        bel06
      *
     c                   if        psald <> *blank and
     c                             wksald = *zeros
     c                   goto      nl2
     c                   endif
T5.00 * Skriver kun konti hvor aldersfordelt kolonne n eller eldre
T5.00 * har poster.
T5.00c                   if        pkoll <> *blank
T5.00c                   if        pkoll = '4' and
T5.00c                             wkbel5 = *zeros
T5.00c                   goto      nl2
T5.00c                   endif
      *
T5.00c                   if        pkoll = '3' and
T5.00c                             wkbel4 = *zeros and
T5.00c                             wkbel5 = *zeros
T5.00c                   goto      nl2
T5.00c                   endif
      *
T5.00c                   if        pkoll = '2' and
T5.00c                             wkbel3 = *zeros and
T5.00c                             wkbel4 = *zeros and
T5.00c                             wkbel5 = *zeros
T5.00c                   goto      nl2
T5.00c                   endif
      *
T5.00c                   if        pkoll = '1' and
T5.00c                             wkbel2 = *zeros and
T5.00c                             wkbel3 = *zeros and
T5.00c                             wkbel4 = *zeros and
T5.00c                             wkbel5 = *zeros
T5.00c                   goto      nl2
T5.00c                   endif
T5.00c                   endif
      *
      * Bruddsummering
      *
     c                   eval      btsal = btsal + wksald
     c                   eval      btink = btink + wkinka
     c                   eval      bkjop = bkjop + wkkjop
     c                   eval      bbel6 = bbel6 + bel06
     c                   eval      bbel5 = bbel5 + bel05
     c                   eval      bbel4 = bbel4 + bel04
     c                   eval      bbel3 = bbel3 + bel03
     c                   eval      bbel2 = bbel2 + bel02
     c                   eval      bbel1 = bbel1 + bel01
     c                   eval      bkrtid = bkrtid + wkotid
     c                   eval      bsntid = bsntid + wkotid
     c                   eval      kksant = kksant + 1
      *
      * Totaler LR
      *
     c                   select
     c                   when      wksald > *zeros
     c                   eval      msald = msald + wksald
     c                   when      wksald < *zeros
     c                   eval      nsald = nsald + wksald
     c                   endsl
      *
     c                   eval      tsald = tsald + wksald
     c                   eval      tink = tink + wkinka
     c                   eval      tkjop = tkjop + wkkjop
     c                   eval      tbel6 = tbel6 + bel06
     c                   eval      tbel5 = tbel5 + bel05
     c                   eval      tbel4 = tbel4 + bel04
     c                   eval      tbel3 = tbel3 + bel03
     c                   eval      tbel2 = tbel2 + bel02
     c                   eval      tbel1 = tbel1 + bel01
     c                   eval      tkrtid = tkrtid + wkotid
      *
     c                   if        wkotid > 999
     c                   eval      wkvtid = 999
     c                   else
T5.40c                   if        wkotid < -999
T5.40c                   eval      wkvtid = -999
T5.40c                   else
     c                   eval      wkvtid = wkotid
T5.40c                   endif
     c                   endif
      *
     c                   eval      tsntid = tsntid + wkotid
     c                   eval      tksant = tksant + 1
      *
      * Alderfordelt linje pr. kunde
      *
7.01 c                   if        u_701 = *off
     c                   write     d101
7.01 c                   else
7.01 c                   write     d102
7.01 c                   endif
      *
      * Kun kontaktperson ønskes
      *
     c                   if        pkper <> *blank and
     c                             ptlf = *blank and
     c                             wkkper <> *blank
     c                   write     h10d1
     c                   endif
      *
      * Kun telefonnummer ønskes
      *
     c                   if        ptlf <> *blank and
     c                             pkper = *blank and
     c                             wktlfn <> *blank
     c                   write     h10d3
     c                   endif
      *
      * Kontaktperson og telefonnummer ønskes
      *
     c                   if        pkper <> *blank and
     c                             ptlf <> *blank
      *
     c                   if        wkkper <> *blank and
     c                             wktlfn <> *blank
     c                   write     h10d2
     c                   endif
      *
     c                   if        wkkper <> *blank and
     c                             wktlfn = *blank
     c                   write     h10d1
     c                   endif
      *
     c                   if        wkkper = *blank and
     c                             wktlfn <> *blank
     c                   write     h10d3
     c                   endif
      *
     c                   endif
      *
     c     nl2           tag
      *
     c                   exsr      nyside
      *
     c                   goto      nyles
      *
     c     slutt         tag
      *
      /eject
      * LR
      * BRUDDSUM ?
      *
     clr                 if        brsum <> *blank
      *
     clr                 if        rwkniv = *zeros
     clr                 eval      rwkniv = 1
     clr                 endif
      *
     clr                 eval      rwkniv = rwkniv * -1
     clr                 exsr      brudd
     clr                 exsr      nyside
     clr                 endif
      *
     clr                 if        tksant <> 0
     clr                 eval      tkrtid = tkrtid / tksant
     clr                 eval      tsntid = tsntid / tksant
     clr                 else
     clr                 eval      tkrtid = *zeros
     clr                 eval      tsntid = *zeros
     clr                 endif
      *
     clr                 write     h101
     clr                 write     t101
     clr                 write     t102
pdf   * Generer xml for videre utskrift
pdf  clr                 if        l_poff = '1'
pdf  clr                 exsr      spoolnbr
6.30 clr                 close     rk632p
6.30 clr                 exsr      xml_create
pdf  clr                 exsr      pdf_preview
pdf   * Avslutt jobbiden
pdf  clr                 call      'AB710R'
pdf  c                   parm                    l_bach
pdf  clr                 endif
      *
      /eject
      *
      * Subrutine for behandling av brudd
      *
     c     brudd         begsr
      *
     c                   if        rwkniv <> w_kniv
      *
      * Avdeling?
      *
     c                   if        pavds <> *blank
     c                   move      w_kniv        p_gkod
     c                   call      'RS707R'
     c                   parm                    p_stat
     c                   parm                    p_gkod
     c                   parm                    p_tx20
      *
     c                   movel     p_tx20        brtxt
      *
     c                   endif
      *
      * Kategori?
      *
     c                   if        pkats <> *blank
     c                   move      w_kniv        kat
     c                   move      w_kniv        p_kkod
     c                   call      'RS711R'
     c                   parm                    p_stat
     c                   parm                    p_kkod
     c                   parm                    p_tx20
      *
     c                   movel     p_tx20        brtxt
      *
     c                   endif
      *
      * Selger?
      *
     c                   if        psels <> *blank
     c                   move      w_kniv        sel
     c                   move      w_kniv        p_ikod
     c                   call      'RS709R'
     c                   parm                    p_stat
     c                   parm                    p_ikod
     c                   parm                    p_tx20
      *
     c                   movel     p_tx20        brtxt
      *
     c                   endif
      *
     c                   if        kksant <> 0
     c                   eval      bkrtid = bkrtid / kksant
     c                   eval      bsntid = bsntid / kksant
     c                   else
     c                   eval      bkrtid = *zeros
     c                   eval      bsntid = *zeros
     c                   endif
      *
     c                   write     b101
      *
      * Ny side ved brudd - avdeling/selger
      *
     c                   if        pavds <> *blank or
     c                             psels <> *blank
     c                   write     h101
     c                   write     h102
     c                   endif
      *
     c                   eval      btsal = *zeros
     c                   eval      btink = *zeros
     c                   eval      bkjop = *zeros
     c                   eval      bbel6 = *zeros
     c                   eval      bbel5 = *zeros
     c                   eval      bbel4 = *zeros
     c                   eval      bbel3 = *zeros
     c                   eval      bbel2 = *zeros
     c                   eval      bbel1 = *zeros
     c                   eval      brtxt = *blank
     c                   eval      bkrtid = *zeros
     c                   eval      bsntid = *zeros
     c                   eval      kksant = *zeros
     c                   endif
      *
     c                   eval      w_kniv = rwkniv
      *
     c                   endsr
      *
      /eject
      *
      * Subrutine for ny side
      *
     c     nyside        begsr
      *
     c                   if        *in30 = *on
     c                   write     h101
     c                   write     h102
     c                   eval      *in30 = *off
     c                   endif
      *
     c                   endsr
      *
      /eject
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Finner spool som er generert av jobben
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     spoolnbr      begsr
pdf   /Free
pdf
pdf            QSPRILSP( SPRL0100
pdf                    : %size(SPRL0100)
pdf                    : 'SPRL0100'
pdf                    : errorcode );
pdf   /End-free
pdf  c*
pdf   *
pdf  c                   endsr
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Kaller eget program for generering av xml fil. Eget pgm         *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     xml_create    begsr
pdf   *
pdf  c                   eval      splfname2 = *blanks
pdf  c                   eval      splfname3 = *blanks
pdf  c                   eval      jobname2 = *blanks
pdf  c                   eval      jobname3 = *blanks
pdf  c                   eval      username2 = *blanks
pdf  c                   eval      username3 = *blanks
pdf  c                   eval      jobnbr2 = *blanks
pdf  c                   eval      jobnbr3 = *blanks
pdf  c                   eval      splfnbr2 = *zeros
pdf  c                   eval      splfnbr3 = *zeros
6.11 c                   eval      p_btyp = 'KUNDER SALDOLISTE'
6.21 c                   eval      p_dspl = 'Aldersfordelt saldoliste '+
6.21 c                                      'kunder, kjørt dato: '+
6.21 c                                      %char(udate)
6.21 c                   eval      p_refn = 'KSL-' + %char(udate)
6.21 c                   eval      p_tagg0 = *blanks
6.21 c                   eval      p_tagg0val = *blanks
6.21 c                   eval      p_tagg1 = *blanks
6.21 c                   eval      p_tagg1val = *blanks
6.21 c                   eval      p_tagg2 = *blanks
6.21 c                   eval      p_tagg2val = *blanks
6.21 c                   eval      p_tagg3 = *blanks
6.21 c                   eval      p_tagg3val = *blanks
6.21 c                   eval      p_tagg4 = *blanks
6.21 c                   eval      p_tagg4val = *blanks
6.21 c                   eval      p_tagg5 = *blanks
6.21 c                   eval      p_tagg5val = *blanks
6.21 c                   eval      p_tagg6 = *blanks
6.21 c                   eval      p_tagg6val = *blanks
6.21 c                   eval      p_tagg7 = *blanks
6.21 c                   eval      p_tagg7val = *blanks
6.21 c                   eval      p_tagg8 = *blanks
6.21 c                   eval      p_tagg8val = *blanks
6.21 c                   eval      p_tagg9 = *blanks
6.21 c                   eval      p_tagg9val = *blanks
pdf   *
6.21 c**                 call      'AP620R'
6.21 c                   call      'AP627R'
pdf  c                   PARM                    splfname
pdf  c                   PARM                    jobname
pdf  c                   PARM                    username
pdf  c                   PARM                    jobnbr
pdf  c                   PARM                    splfnbr
pdf  c                   PARM                    splfname2
pdf  c                   PARM                    jobname2
pdf  c                   PARM                    username2
pdf  c                   PARM                    jobnbr2
pdf  c                   PARM                    splfnbr2
pdf  c                   PARM                    splfname3
pdf  c                   PARM                    jobname3
pdf  c                   PARM                    username3
pdf  c                   PARM                    jobnbr3
pdf  c                   PARM                    splfnbr3
6.21 c                   parm                    p_tagg0
6.21 c                   parm                    p_tagg0val
6.21 c                   parm                    p_tagg1
6.21 c                   parm                    p_tagg1val
6.21 c                   parm                    p_tagg2
6.21 c                   parm                    p_tagg2val
6.21 c                   parm                    p_tagg3
6.21 c                   parm                    p_tagg3val
6.21 c                   parm                    p_tagg4
6.21 c                   parm                    p_tagg4val
6.21 c                   parm                    p_tagg5
6.21 c                   parm                    p_tagg5val
6.21 c                   parm                    p_tagg6
6.21 c                   parm                    p_tagg6val
6.21 c                   parm                    p_tagg7
6.21 c                   parm                    p_tagg7val
6.21 c                   parm                    p_tagg8
6.21 c                   parm                    p_tagg8val
6.21 c                   parm                    p_tagg9
6.20 c                   parm                    p_tagg9val
pdf  c                   PARM                    l_bach
6.11 c                   PARM                    p_btyp
6.21 c                   PARM                    p_refn
6.21 c                   PARM                    p_dspl
pdf   *
pdf  c                   endsr
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Subrutiner for XML/jasper integrasjon - Skjermvisning           *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     pdf_preview   begsr
pdf  c                   if        l_skje = 1
pdf   * Vi kaller program for launch av PDF i browser
pdf  c                   call      'AP621R'
pdf  c                   parm                    l_bach
pdf   *
pdf  c                   endif
pdf   *
pdf  c                   endsr
pdf   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
pdf  c                   open      rk632p
      *
     c                   eval      key = *blank
     c                   eval      brtxt = *blank
     c                   eval      wkkund = 0
     c                   eval      rwkniv = 0
     c                   eval      w_kniv = 0
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
     c     raa3l1_key    klist
     c                   kfld                    w_firm
      *
     c     rkw1l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rwkniv
     c                   kfld                    rwksal
     c                   kfld                    rwkalf
     c                   kfld                    rwkknr
      *
      * Henter firmaopplysninger
      *
     c                   movel     l_firm        w_firm
     c     afirl1_key    chain     afirl1                             60
      *
     c     raa3l1_key    chain     raa3l1                             99
      *
     c                   if        *in60 = *on
     c                   eval      w_3ka1 = 31
     c                   eval      w_3ka2 = 61
     c                   eval      w_3ka3 = 91
     c                   else
     c                   eval      w_3ka1 = ra3ka1 + 1
     c                   eval      w_3ka2 = ra3ka2 + 1
     c                   eval      w_3ka3 = ra3ka3 + 1
     c                   endif
      *
      * Behandler parameter
      *
      * Sjekker om det ønskes bruddsum
      *
     c                   if        pavds <> *blank
     c                   move      'X'           brsum
     c                   endif
      *
     c                   if        pkats <> *blank
     c                   move      'X'           brsum
     c                   endif
      *
     c                   if        psels <> *blank
     c                   move      'X'           brsum
     c                   endif
      *
      * Kontaktperson
      *
     c                   if        pkper = *blank
     c                   movel     'Nei'         pkperx
     c                   eval      kptxt = *blanks
     c                   else
     c                   movel     'Ja '         pkperx
S500 c*                  move      txt(5)        kptxt
T500 c                   eval      kptxt = 'Kontaktperson'
     c                   endif
      *
      * Telefonnummer
      *
     c                   if        ptlf = *blank
     c                   movel     'Nei'         ptlfx
     c                   eval      tltxt = *blanks
     c                   else
     c                   movel     'Ja '         ptlfx
S500 c*                  move      txt(6)        tltxt
T500 c                   eval      tltxt = 'Telefon-nr.'
     c                   endif
      *
      * Bare kunder med saldo
      *
     c                   if        psald = *blank
     c                   movel     'Nei'         psaldx
     c                   else
     c                   movel     'Ja '         psaldx
     c                   endif
      *
      * Alle med kjøp i år
      *
     c                   if        pkjop = *blank
     c                   movel     'Nei'         pkjopx
     c                   else
     c                   movel     'Ja '         pkjopx
     c                   endif
      *
      * Sum pr. avdeling
      *
     c                   if        pavds = *blank
     c                   movel     'Nei'         pavdsx
     c                   else
     c                   movel     'Ja '         pavdsx
     c                   endif
      *
      * Sum pr. kategori
      *
     c                   if        pkats = *blank
     c                   movel     'Nei'         pkatsx
     c                   else
     c                   movel     'Ja '         pkatsx
     c                   endif
      *
      * Sum pr. selger
      *
     c                   if        psels = *blank
     c                   movel     'Nei'         pselsx
     c                   else
     c                   movel     'Ja '         pselsx
     c                   endif
      *
      * Sortering
      *
     c                   if        psort = ' '
     c                   movel     'Nummeris'    psortx
     c                   move      'k '          psortx
     c                   else
     c                   movel     'Alfabeti'    psortx
     c                   move      'sk'          psortx
     c                   endif
      *
      * Skriver header 1. side
      *
     c                   write     h101
     c                   write     h102
7.01  *
7.01  * Endring 7.01
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'RK632_701':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_701 = *on;
7.01   endif;
      *
     c                   endsr
      *
