     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JM730R
      * Beskrivelse . . : Oppdater fra NIB, kampanje
      *
      *                   Oppdaterer/oppretter kampanjer fra
      *                   Norgros/NIB (XML-format)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       181105 GRo  Nytt program
6.30  *       140414 bof  utvidet med fra /til innkjøpsdato
6.31  *       130514 bof  Farveringen - hvis lev = 212588, sett til 214384
6.32  *       070714 bkv  Håndterer oppdatering (var slett og insert)
6.33  *       280415 bof  Feilrettning vedr. fra innkjøpsdato
6.34  *       020318 bof  Skal ta innkj.dato fra dato fra vare-del
7.01  *       251119 bsa  Utvidet med pris gruppe på VA kampanje
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fnxkapf    uf   e           k disk
     fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
     fjkahlu    uf a e           k disk    rename(jkahpfr:jkahlur)
     fjkavlu    uf a e           k disk    rename(jkavpfr:jkavlur)
     fjstsl1    if   e           k disk    rename(jstspfr:jstsl1r)
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
      *
      * Definering av Local Data Area
      *
     d                uds
     d l_user                911    920
      *
      *  Dataelementer kampanjehode
      *
     d                 ds
     d d_hrec                       120
     d  d_kamp                        5    overlay(d_hrec:1)
     d  d_besk                       30    overlay(d_hrec:6)
     d  d_fdat                       10    overlay(d_hrec:36)
     d   d_fdat_iso                    d   overlay(d_fdat:1) datfmt(*iso)
     d  d_tdat                       10    overlay(d_hrec:46)
     d   d_tdat_iso                    d   overlay(d_tdat:1) datfmt(*iso)
     d  d_ansv                       10    overlay(d_hrec:56)
     d  d_beda                       10    overlay(d_hrec:66)
     d   d_beda_iso                    d   overlay(d_beda:1) datfmt(*iso)
     d  d_spda                       10    overlay(d_hrec:76)
     d   d_spda_iso                    d   overlay(d_spda:1) datfmt(*iso)
     d  d_pala                        1    overlay(d_hrec:86)
     d   d_pala_num                   1  0 overlay(d_pala:1)
     d  d_odat                       10    overlay(d_hrec:87)
     d   d_odat_iso                    d   overlay(d_odat:1) datfmt(*iso)
     d  d_edat                       10    overlay(d_hrec:97)
     d   d_edat_iso                    d   overlay(d_edat:1) datfmt(*iso)
     d  d_etim                       10    overlay(d_hrec:107)
      *
      *  Dataelementer kampanjevare
      *
     d                 ds
6.34 d d_vrec                       260
     d  d_vare                        8    overlay(d_vrec:1)
     d  d_nobb                        8    overlay(d_vrec:9)
     d   d_nobb_num                   8  0 overlay(d_nobb:1)
     d  d_vtxt                       35    overlay(d_vrec:17)
     d  d_levn                        6    overlay(d_vrec:52)
     d   d_levn_num                   6  0 overlay(d_levn:1)
     d  d_lnav                       35    overlay(d_vrec:58)
     d  d_inkp                       10    overlay(d_vrec:93)
     d   d_inkp_kx                    7    overlay(d_inkp:1)
     d   d_inkp_kr                    7  0 overlay(d_inkp_kx:1)
     d   d_inkp_Ørx                   2    overlay(d_inkp:9)
     d   d_inkp_Øre                   2  2 overlay(d_inkp_Ørx:1)
     d  d_salp                       10    overlay(d_vrec:103)
     d   d_salp_kx                    7    overlay(d_salp:1)
     d   d_salp_kr                    7  0 overlay(d_salp_kx:1)
     d   d_salp_Ørx                   2    overlay(d_salp:9)
     d   d_salp_Øre                   2  2 overlay(d_salp_Ørx:1)
     d  d_palp                       10    overlay(d_vrec:113)
     d   d_palp_kx                    7    overlay(d_palp:1)
     d   d_palp_kr                    7  0 overlay(d_palp_kx:1)
     d   d_palp_Ørx                   2    overlay(d_palp:9)
     d   d_palp_Øre                   2  2 overlay(d_palp_Ørx:1)
     d  d_bsen                        1    overlay(d_vrec:123)
     d   d_bsen_num                   1  0 overlay(d_bsen:1)
     d  d_lbet                       50    overlay(d_vrec:124)
     d  d_anmk                       50    overlay(d_vrec:174)
6.34 d  d_bedf                       10    overlay(d_vrec:224)
6.34 d   d_bedf_iso                    d   overlay(d_bedf:1) datfmt(*iso)
      *
      * Definering av variable i nøkler
      *
     d jkahlu_kamp     s                   like(jmkamp)
     d jkavlu_vkam     s                   like(jmvkam)
7.01 d jkavlu_vprg     s                   like(jmvprg)
     d jvarl3_nobb     s                   like(jvnobb)
6.32 d jkavlu_vvar     s                   like(jmvvar)
      *
      * Definering av variable
      *
     d b_inlr          s              1
     d b_kild          s              1    inz(*off)
     d b_kfel          s              1    inz(*off)
     d b_blnk          s              1    inz(*off)
6.32 d b_ny            s              1    inz(*off)
      *
     d w_stda          s              3  0
     d w_slda          s              3  0
     d w_welm          s            400
     d w_wlen          s              3  0
      *
     d w_decp          s              3  0
      *
     d w_inkp          s                   like(jmvkai)
     d w_salp          s                   like(jmvkaa)
     d w_palp          s                   like(jmvkau)
6.30 d w_tdat          s                   like(jmvidt)
6.32 d w_dato          s                   like(jmvidt)
      *
     d w_str_i         s            400
     d w_str_u         s            400
     d w_posi          s              4  0
     d w_posu          s              4  0
     d w_str_x         s              1
      *
     d w_firm          s              3  0
      *
      *  Konstanter
      *
     d c_apos          c                   x'7D'
     d c_digits        c                   ' 0123456789'
     d c_nsep          c                   ','
     d c_pris_max      c                   9999999,99
      *
     d c_NIBk          c                   '0'
     d c_NVk           c                   '3'
      *
      * XML string konstanter
      *
     d c_xsle          c                   '</'
     d c_xend          c                   '>'
      *
      * XML feltnavn - meldingsspesifikke
      *
      * Hovedstruktur - Avsender
      *
     d c_xstr          c                   '<Avsender>'
     d c_kild          c                   '<Kilde>'
      *
      * Substruktur #1 - Kampanje
      *
     d c_kamp_s        c                   '<Kampanje>'
     d c_kamp_e        c                   '</Kampanje>'
     d c_navn          c                   '<Navn>'
     d c_besk          c                   '<Beskrivelse>'
     d c_fdat          c                   '<Fra_dato>'
     d c_tdat          c                   '<Til_dato>'
     d c_ansv          c                   '<Ansvarlig>'
     d c_bdat          c                   '<Best_dato>'
     d c_sdat          c                   '<Sperret_dato>'
     d c_pala          c                   '<Palagt>'
     d c_opda          c                   '<Oppr_dato>'
     d c_enda          c                   '<Endr_dato>'
     d c_entd          c                   '<Endr_tidspunkt>'
      *
      * Substruktur #2 - Kampanje/vare
      *
     d c_vare_s        c                   '<Vare>'
     d c_vare_e        c                   '</Vare>'
     d c_nibn          c                   '<NIBnr>'
     d c_nobn          c                   '<NOBBnr>'
     d c_teks          c                   '<Tekst>'
     d c_levn          c                   '<Lev>'
     d c_lnav          c                   '<Lev_navn>'
     d c_inpr          c                   '<Innkjopspris>'
     d c_sapr          c                   '<Salgspris>'
     d c_papr          c                   '<Palagt_pris>'
     d c_blag          c                   '<Best_sentrallager>'
     d c_lbet          c                   '<Leveringsbet>'
     d c_anmk          c                   '<Anmerkning>'
6.34 d c_bedf          c                   '<Best_dato_vare>'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      b_inlr = *on
      *
      * Leser XML-fil kampanje
      *
     c                   read      nxkapfr
     c                   dow       not %eof(nxkapf)
      *
     c                   if        b_kild = *on
     c                   if        %subst(nxkamp:1:10) = c_kamp_s
     c                   exsr      beh_kamp
     c                   endif
     c                   endif
      *
      *  Finn gyldig avsender/kilde
      *
     c                   eval      w_stda = %scan(c_xend:nxkamp:1)
     c                   eval      w_slda = %scan(c_xsle:nxkamp:w_stda)
     c                   eval      w_stda = w_stda + 1
      *
     c                   eval      w_wlen = *zero
     c                   eval      w_welm = *blank
     c                   if        w_slda > w_stda
     c                   eval      w_wlen = w_slda - w_stda
     c                   eval      w_welm = %subst(nxkamp:w_stda:w_wlen)
     c                   endif
      *
     c                   select
     c                   when      %subst(nxkamp:1:7) = c_kild
     c                   exsr      uttr_kild
     c                   endsl
      *
     c                   delete    nxkapfr
      *
     c                   read      nxkapfr
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av avsenderelement : Kilde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_kild     begsr
      *
     c                   eval      b_kild = *off
      *
     c                   if        %trim(w_welm) = c_NIBk
     c                   eval      b_kild = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av kampanjeinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beh_kamp      begsr
      *
     c                   eval      b_kfel = *off
     c                   exsr      kamp_init
      *
     c                   dow       not %eof(nxkapf)
      *
      *  Start på vare
      *
     c                   if        %subst(nxkamp:1:6) = c_vare_s
     c                   if        b_kfel = *off
     c                   exsr      beh_vare
     c                   endif
     c                   endif
      *
      *  Slutt på kampanjeelement
      *
     c                   if        %subst(nxkamp:1:11) = c_kamp_e
6.32 c                   if        b_ny = *on
     c                   exsr      skr_kamp
6.32 c                   else
6.32 c                   exsr      upd_kamp
6.32 c                   endif
     c                   leave
     c                   endif
      *
      *  Enkeltelement kampanjenivå herfra
      *
     c                   eval      w_stda = %scan(c_xend:nxkamp:1)
     c                   eval      w_slda = %scan(c_xsle:nxkamp:w_stda)
     c                   eval      w_stda = w_stda + 1
      *
     c                   eval      w_wlen = *zero
     c                   eval      w_welm = *blank
     c                   if        w_slda > w_stda
     c                   eval      w_wlen = w_slda - w_stda
     c                   eval      w_welm = %subst(nxkamp:w_stda:w_wlen)
     c                   endif
      *
     c                   select
     c                   when      %subst(nxkamp:1:6) = c_navn
     c                   exsr      uttr_navn
     c                   when      %subst(nxkamp:1:13) = c_besk
     c                   exsr      uttr_besk
     c                   when      %subst(nxkamp:1:10) = c_fdat
     c                   exsr      uttr_fdat
     c                   when      %subst(nxkamp:1:10) = c_tdat
     c                   exsr      uttr_tdat
     c                   when      %subst(nxkamp:1:11) = c_ansv
     c                   exsr      uttr_ansv
     c                   when      %subst(nxkamp:1:11) = c_bdat
     c                   exsr      uttr_bdat
     c                   when      %subst(nxkamp:1:14) = c_sdat
     c                   exsr      uttr_sdat
     c                   when      %subst(nxkamp:1:8) = c_pala
     c                   exsr      uttr_pala
     c                   when      %subst(nxkamp:1:11) = c_opda
     c                   exsr      uttr_opda
     c                   when      %subst(nxkamp:1:11) = c_enda
     c                   exsr      uttr_enda
     c                   when      %subst(nxkamp:1:16) = c_entd
     c                   exsr      uttr_entd
     c                   endsl
      *
     c                   delete    nxkapfr
      *
     c                   read      nxkapfr
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av kampanjeinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kamp_init     begsr
      *
     c                   eval      d_hrec = *blank
     c                   eval      d_fdat_iso = *loval
     c                   eval      d_tdat_iso = *loval
     c                   eval      d_beda_iso = *loval
6.34 c                   eval      d_bedf_iso = *loval
     c                   eval      d_spda_iso = *loval
     c                   eval      d_pala_num = *zero
     c                   eval      d_odat_iso = *loval
     c                   eval      d_edat_iso = *loval
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av kampanjeelement : Navn
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_navn     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_kamp = %trim(w_str_u)
      *
     c                   if        d_kamp = *blank
     c                   eval      b_kfel = *on
     c                   else
6.32 c                   exsr      sjekk_kamp
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av kampanjeelement : Beskrivelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_besk     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_besk = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av kampanjeelement : Fra-dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_fdat     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_fdat = %trim(w_welm)
     c                   else
     c                   eval      d_fdat_iso = *loval
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av kampanjeelement : Til-dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_tdat     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_tdat = %trim(w_welm)
     c                   else
     c                   eval      d_tdat_iso = *loval
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av kampanjeelement : Ansvarlig
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_ansv     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_ansv = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av kampanjeelement : Best.dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_bdat     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_beda = %trim(w_welm)
     c                   else
     c                   eval      d_beda_iso = *loval
     c                   endif
      *
     c                   endsr
6.34  *
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6 34  * Subrutine for behandling av kampanjeelement : Best.dato på linje
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34  *
6.34 c     uttr_bedf     begsr
6.34  *
6.34 c                   if        w_wlen = 10
6.34 c                   eval      d_bedf = %trim(w_welm)
6.34 c                   else
6.34 c                   eval      d_bedf_iso = *loval
6.34 c                   endif
6.34  *
6.34 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av kampanjeelement : Sperret dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_sdat     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_spda = %trim(w_welm)
     c                   else
     c                   eval      d_spda_iso = *loval
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av kampanjeelement : Pålagt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_pala     begsr
      *
     c                   eval      d_pala_num = *zero
      *
     c                   if        w_wlen = 1
     c                   if        %check(c_digits : w_welm) = 0
     c                   eval      d_pala = %trim(w_welm)
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av kampanjeelement : Opprettet dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_opda     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_odat = %trim(w_welm)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av kampanjeelement : Endr.dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_enda     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_edat = %trim(w_welm)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av kampanjeelement : Endr.tidspunkt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_entd     begsr
      *
     c                   if        w_wlen = 8
     c                   eval      d_etim = %trim(w_welm)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Nytt kampanjehode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_kamp      begsr
      *
     c                   if        b_kfel = *on
     c                   leavesr
     c                   endif
      *
      * Skriver til kampanje-hode
      *
     c                   clear                   jkahlur
      *
     c                   eval      jmfirm = w_firm
     c                   eval      jmkamp = d_kamp
     c                   eval      jmbesk = d_besk
     c                   eval      jmansv = d_ansv
6.32 c                   eval      jmfoda = *loval
     c                   eval      jmodat = *loval
     c                   eval      jmeusr = l_user
      *
     c                   eval      jmfdat = d_fdat_iso
     c                   eval      jmtdat = d_tdat_iso
      *
6.32 c                   time                    jmooda
     c                   time                    jmedat
     c                   time                    jmetim
      *
     c                   write     jkahlur
6.34  * 6.34 * fjerner dette, skal bruke best.f.dato fra varel.
6.34  * oppdaterer varen på kampanje med fra innkjøpsdato
6.34  *
6.34 c*                  eval      jkavlu_vkam = d_kamp
6.34 c*    jkavlu_key    setll     jkavlu
6.34 c*    jkavlu_key    reade     jkavlur
6.34 c*                  dow       not %eof
6.34 c*                  eval      jmvidf = d_fdat_iso
6.34 c*    jmvidf        adddur    -14:*d        w_dato
6.34 c*                  eval      jmvidf = w_dato
6.34 c*                  update    jkavlur
6.34 c*    jkavlu_key    reade     jkavlur
6.34 c*                  enddo
      *
      *
     c                   endsr
      *
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * Oppdaterer kampanjehode
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     upd_kamp      begsr
6.32  *
6.32 c                   if        b_kfel = *on
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32  * Skriver til kampanje-hode
6.32  *
6.32 c                   eval      jkahlu_kamp = d_kamp
6.32 c     jkahlu_key    chain     jkahlur
6.32  *
6.32 c                   eval      jmbesk = d_besk
6.32 c                   eval      jmansv = d_ansv
6.32 c                   eval      jmeusr = l_user
6.32  *
6.32 c                   eval      jmfdat = d_fdat_iso
6.32 c                   eval      jmtdat = d_tdat_iso
6.32  *
6.32 c                   eval      jmodat = *loval
6.32 c                   time                    jmedat
6.32 c                   time                    jmetim
6.32  *
6.32 c                   update    jkahlur
6.32  *
6.32 c                   endsr
      *
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * Sletter eksisterende kampanje
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c*    slett_kamp    begsr
6.32  *
6.32 c*                  eval      jkahlu_kamp = d_kamp
6.32 c*    jkahlu_key    chain     jkahlur
6.32 c*                  if        %found
6.32 c*                  delete    jkahlur
6.32 c*                  eval      jkavlu_vkam = d_kamp
6.32 c*    jkavlu_key    setll     jkavlu
6.32 c*    jkavlu_key    reade     jkavlur
6.32 c*                  dow       not %eof
6.32 c*                  delete    jkavlur
6.32 c*    jkavlu_key    reade     jkavlur
6.32 c*                  enddo
6.32 c*                  endif
6.32  *
6.32 c*                  endsr
      *
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * Sjekk eksisterende kampanje
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     sjekk_kamp    begsr
6.32  *
6.32 c                   eval      jkahlu_kamp = d_kamp
6.32 c     jkahlu_key    chain     jkahlur
6.32 c                   if        not %found
6.32 c                   eval      b_ny = *on
6.32 c                   endif
6.32  *
6.32 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av kampanje/vareinfo.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beh_vare      begsr
      *
     c                   eval      b_kfel = *off
     c                   exsr      vare_init
      *
     c                   dow       not %eof(nxkapf)
      *
      *  Slutt på vareelement
      *
     c                   if        %subst(nxkamp:1:7) = c_vare_e
6.32  * Finner varenr på bakgrunn av varenr./NOBB-nr
6.32  *
6.32 c                   eval      jvarl3_nobb = d_nobb_num
6.32 c     jvarl3_key    chain     jvarl3
6.32 c                   if        not %found
6.32 c                   movel     d_vare        jvvare
6.32 c                   endif
6.32  *
6.32 c                   eval      jkavlu_vkam = d_kamp
7.01 c                   eval      jkavlu_vprg = *blank
6.32 c                   eval      jkavlu_vvar = jvvare
6.32 c     jkavlu_key    chain     jkavlur
6.32 c                   if        %found
      * oppdaterer kun hvis ikke låst
     c                   if        jmvlas = 0
6.32 c                   exsr      opd_vare
     c                   endif
6.32 c                   else
6.32 c                   exsr      skr_vare
6.32 c                   endif
6.32 c                   leave
     c                   endif
      *
      *  Enkeltelement kampanjevare herfra
      *
     c                   eval      w_stda = %scan(c_xend:nxkamp:1)
     c                   eval      w_slda = %scan(c_xsle:nxkamp:w_stda)
     c                   eval      w_stda = w_stda + 1
      *
     c                   eval      w_wlen = *zero
     c                   eval      w_welm = *blank
     c                   if        w_slda > w_stda
     c                   eval      w_wlen = w_slda - w_stda
     c                   eval      w_welm = %subst(nxkamp:w_stda:w_wlen)
     c                   endif
      *
     c                   select
     c                   when      %subst(nxkamp:1:7) = c_nibn
     c                   exsr      uttr_nibn
     c                   when      %subst(nxkamp:1:8) = c_nobn
     c                   exsr      uttr_nobb
     c                   when      %subst(nxkamp:1:7) = c_teks
     c                   exsr      uttr_teks
     c                   when      %subst(nxkamp:1:5) = c_levn
     c                   exsr      uttr_levn
     c                   when      %subst(nxkamp:1:10) = c_lnav
     c                   exsr      uttr_lnav
     c                   when      %subst(nxkamp:1:14) = c_inpr
     c                   exsr      uttr_inpr
     c                   when      %subst(nxkamp:1:11) = c_sapr
     c                   exsr      uttr_sapr
     c                   when      %subst(nxkamp:1:13) = c_papr
     c                   exsr      uttr_papr
     c                   when      %subst(nxkamp:1:19) = c_blag
     c                   exsr      uttr_blag
     c                   when      %subst(nxkamp:1:14) = c_lbet
     c                   exsr      uttr_lbet
     c                   when      %subst(nxkamp:1:12) = c_anmk
     c                   exsr      uttr_anmk
6.34 c                   when      %subst(nxkamp:1:16) = c_bedf
6.34 c                   exsr      uttr_bedf
     c                   endsl
      *
     c                   delete    nxkapfr
      *
     c                   read      nxkapfr
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av vareinfo.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vare_init     begsr
      *
     c                   eval      d_vrec = *blank
     c                   eval      d_nobb_num = *zero
     c                   eval      d_levn_num = *zero
     c                   eval      d_inkp_kr  = *zero
     c                   eval      d_inkp_Øre = *zero
     c                   eval      d_salp_kr  = *zero
     c                   eval      d_salp_Øre = *zero
     c                   eval      d_palp_kr  = *zero
     c                   eval      d_palp_Øre = *zero
     c                   eval      d_bsen_num = *zero
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vareelement : NIB-nr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_nibn     begsr
      *
     c                   if        w_wlen = 8
     c                   if        %check(c_digits : w_welm) = 0
     c                   eval      d_vare = %trim(w_welm)
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vareelement : NOBB-nr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_nobb     begsr
      *
     c                   eval      d_nobb_num = *zero
      *
     c                   if        w_wlen = 8
     c                   if        %check(c_digits : w_welm) = 0
     c                   eval      d_nobb = %trim(w_welm)
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vareelement : Tekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_teks     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_vtxt = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vareelement : Lev.nr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_levn     begsr
      *
     c                   if        %check(c_digits : w_welm) = 0
     c                   evalr     d_levn = %trim(w_welm)
     c                   eval      d_levn = %xlate(' ':'0':d_levn)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vareelement : Lev.navn
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_lnav     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_lnav = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vareelement : Innkjøpspris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_inpr     begsr
      *
     c                   if        w_wlen = 0
     c                   eval      d_inkp_kr  = *zero
     c                   eval      d_inkp_Øre = *zero
     c                   eval      w_inkp = *zero
     c                   leavesr
     c                   endif
      *
     c                   eval      w_decp = %scan(c_nsep:w_welm:1)
     c                   if        w_decp = 0
     c                   evalr     d_inkp_kx  = %trim(%subst(w_welm:1))
     c                   eval      d_inkp_kx  = %xlate(' ':'0':d_inkp_kx)
     c                   eval      d_inkp_Øre = *zero
     c                   eval      w_inkp = d_inkp_kr
     c                   else
     c                   evalr     d_inkp_kx  = %trim(%subst(w_welm:1:w_decp-1))
     c                   eval      d_inkp_kx  = %xlate(' ':'0':d_inkp_kx)
     c                   evalr     d_inkp_Ørx = %trim(%subst(w_welm:w_decp+1))
     c                   eval      d_inkp_Ørx = %xlate(' ':'0':d_inkp_Ørx)
     c                   if        (d_inkp_kr + d_inkp_Øre) > c_pris_max
     c                   eval      w_inkp = *zero
     c                   else
     c                   eval(h)   w_inkp = (d_inkp_kr + d_inkp_Øre)
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vareelement : Salgspris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_sapr     begsr
      *
     c                   if        w_wlen = 0
     c                   eval      d_salp_kr  = *zero
     c                   eval      d_salp_Øre = *zero
     c                   eval      w_salp = *zero
     c                   leavesr
     c                   endif
      *
     c                   eval      w_decp = %scan(c_nsep:w_welm:1)
     c                   if        w_decp = 0
     c                   evalr     d_salp_kx  = %trim(%subst(w_welm:1))
     c                   eval      d_salp_kx  = %xlate(' ':'0':d_salp_kx)
     c                   eval      d_salp_Øre = *zero
     c                   eval      w_salp = d_salp_kr
     c                   else
     c                   evalr     d_salp_kx  = %trim(%subst(w_welm:1:w_decp-1))
     c                   eval      d_salp_kx  = %xlate(' ':'0':d_salp_kx)
     c                   evalr     d_salp_Ørx = %trim(%subst(w_welm:w_decp+1))
     c                   eval      d_salp_Ørx = %xlate(' ':'0':d_salp_Ørx)
     c                   if        (d_salp_kr + d_salp_Øre) > c_pris_max
     c                   eval      w_salp = *zero
     c                   else
     c                   eval(h)   w_salp = (d_salp_kr + d_salp_Øre)
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vareelement : Pålagt pris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_papr     begsr
      *
     c                   if        w_wlen = 0
     c                   eval      d_palp_kr  = *zero
     c                   eval      d_palp_Øre = *zero
     c                   eval      w_palp = *zero
     c                   leavesr
     c                   endif
      *
     c                   eval      w_decp = %scan(c_nsep:w_welm:1)
     c                   if        w_decp = 0
     c                   evalr     d_palp_kx  = %trim(%subst(w_welm:1))
     c                   eval      d_palp_kx  = %xlate(' ':'0':d_palp_kx)
     c                   eval      d_palp_Øre = *zero
     c                   eval      w_palp = d_palp_kr
     c                   else
     c                   evalr     d_palp_kx  = %trim(%subst(w_welm:1:w_decp-1))
     c                   eval      d_palp_kx  = %xlate(' ':'0':d_palp_kx)
     c                   evalr     d_palp_Ørx = %trim(%subst(w_welm:w_decp+1))
     c                   eval      d_palp_Ørx = %xlate(' ':'0':d_palp_Ørx)
     c                   if        (d_palp_kr + d_palp_Øre) > c_pris_max
     c                   eval      w_palp = *zero
     c                   else
     c                   eval(h)   w_palp = (d_palp_kr + d_palp_Øre)
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vareelement : Best. sentrallager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_blag     begsr
      *
     c                   eval      d_bsen_num = *zero
      *
     c                   if        w_wlen = 1
     c                   if        %check(c_digits : w_welm) = 0
     c                   eval      d_bsen = %trim(w_welm)
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vareelement : Leveringsbet.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_lbet     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_lbet = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vareelement : Anmerkning
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_anmk     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_anmk = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Skriver kampanjevareelement
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_vare      begsr
      *
     c                   if        b_kfel = *on
     c                   leavesr
     c                   endif
      *
      *
      * Skriver til kampanje-vare
      *
     c                   clear                   jkavlur
      *
     c                   eval      jmvfir = w_firm
     c                   eval      jmvkam = d_kamp
7.01 c                   eval      jmvprg = *blank
     c                   eval      jmvvar = jvvare
     c                   eval      jmvldo = d_levn_num
6.31 c                   if        jmvldo = 212588
6.31 c                   eval      jmvldo = 214384
6.31 c                   endif
     c                   eval      jmvkai = d_inkp_kr + d_inkp_Øre
     c                   eval      jmvkpr = 0
     c                   eval      jmvkaa = d_salp_kr + d_salp_Øre
     c                   eval      jmvkau = d_palp_kr + d_palp_Øre
     c                   eval      jmvans = *blank
     c                   eval      jmveus = l_user
6.34 c                   eval      jmvidf = d_bedf_iso
6.30 c                   eval      jmvidt = d_tdat_iso
6.33 c     jmvidt        adddur    7:*d          w_dato
6.33 c                   eval      jmvidt = w_dato
6.30 c****               eval      jmvidt = w_tdat
6.32 c                   eval      jmvlas = 0
      *
6.32 c                   time                    jmvoda
     c                   time                    jmveda
     c                   time                    jmveti
      *
     c                   write     jkavlur
      *
     c                   endsr
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * Oppdaterer kampanjevareelement
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     opd_vare      begsr
6.32  *
6.32 c                   if        b_kfel = *on
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32  * Oppdaterer kampanje-vare
6.32  *
6.32  *
6.32 c                   eval      jmvfir = w_firm
6.32 c                   eval      jmvkam = d_kamp
6.32 c                   eval      jmvvar = jvvare
6.32 c                   eval      jmvldo = d_levn_num
6.32 c                   if        jmvldo = 212588
6.32 c                   eval      jmvldo = 214384
6.32 c                   endif
6.32 c                   eval      jmvkai = d_inkp_kr + d_inkp_Øre
6.32 c                   eval      jmvkpr = 0
6.32 c                   eval      jmvkaa = d_salp_kr + d_salp_Øre
6.32 c                   eval      jmvkau = d_palp_kr + d_palp_Øre
6.32 c                   eval      jmvans = *blank
6.32 c                   eval      jmveus = l_user
6.34 c                   eval      jmvidf = d_bedf_iso
6.32 c                   eval      jmvidt = d_tdat_iso
6.32 c     jmvidt        adddur    7:*d          w_tdat
6.32 c                   eval      jmvidt = w_tdat
6.32  *
6.32 c                   time                    jmveda
6.32 c                   time                    jmveti
6.32  *
6.32 c                   update    jkavlur
6.32  *
6.32 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tilbakestilling av XML-escape tegn i tekstfelt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     konv_XML_esc  begsr
      *
     c                   eval      w_posi = 0
     c                   eval      w_posu = 0
     c                   eval      w_str_u = *blank
     c                   eval      b_blnk = *off
      *
     c                   dow       w_posi < 395 and
     c                             b_blnk = *off
     c                   eval      w_posi = w_posi + 1
     c                   if        %subst(w_str_i:w_posi) = *blank
     c                   eval      b_blnk = *on
     c                   else
     c                   eval      w_str_x = %subst(w_str_i:w_posi:1)
     c                   if        w_posu < 395
     c                   select
     c                   when      %subst(w_str_i:w_posi:5) = '&amp;'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = '&'
     c                   eval      w_posi = w_posi + 4
      *
     c                   when      %subst(w_str_i:w_posi:4) = '&lt;'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = '<'
     c                   eval      w_posi = w_posi + 3
      *
     c                   when      %subst(w_str_i:w_posi:4) = '&gt;'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = '>'
     c                   eval      w_posi = w_posi + 3
      *
     c                   when      %subst(w_str_i:w_posi:6) = '&apos;'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = c_apos
     c                   eval      w_posi = w_posi + 5
      *
     c                   when      %subst(w_str_i:w_posi:6) = '&quot;'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = '"'
     c                   eval      w_posi = w_posi + 5
      *
     c                   other
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = w_str_x
      *
     c                   endsl
     c                   endif
     c                   endif
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
      *
      * Key for oppslag på vare
      *
     c     jvarl3_key    klist
     c                   kfld                    jvarl3_nobb
      *
      * Key for oppdatering av kampanje-hode
      *
     c     jkahlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jkahlu_kamp
      *
      * Key for oppdatering av kampanje-vare
      *
     c     jkavlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jkavlu_vkam
7.01 c                   kfld                    jkavlu_vprg
6.32 c                   kfld                    jkavlu_vvar
      *
      * Key for oppslag på status
      *
     c     jstsl1_key    klist
     c                   kfld                    w_firm
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c     jstsl1_key    chain     jstsl1
      *
     c                   endsr
