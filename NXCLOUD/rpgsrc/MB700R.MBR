     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLOGI
      * Program . . . . : MB700R
      * Beskrivelse . . : Starter MB701R
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       260405 bø   Nytt program
      * 6.10  180609 BSA  Oppdatert med sporingsinformasjon
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     fmabcl1    if   e           k disk    rename(mabcpfr:mabcl1r)
     fmabdl2    if   e           k disk    rename(mabdpfr:mabdl2r)
     fmabclu    uf   e           k disk    rename(mabcpfr:mabclur)
      *
     d a_lage          s              2  0 DIM(10)
     d a_ltyp          s              1  0 DIM(10)
      *
     d mabdl2_lagk     s                   like(mblagk)
     d mabdl2_vsor     s                   like(mbvsor)
     d mabdl2_ogrp     s                   like(mbogrp)
     d mabdl2_hgrp     s                   like(mbhgrp)
     d mabdl2_ugrp     s                   like(mbugrp)
     d mabdl2_kabc     s                   like(mbkabc)
     d mabclu_lagk     s                   like(malagk)
     d mabclu_vsor     s                   like(mavsor)
     d mabclu_ogrp     s                   like(maogrp)
     d mabclu_hgrp     s                   like(mahgrp)
     d mabclu_ugrp     s                   like(maugrp)
      *
     d p_listp         s            128
     d w_totax         s             11  0
     d w_totay         s             11  0
     d w_totaz         s             11  0
     d w_totbx         s             11  0
     d w_totby         s             11  0
     d w_totbz         s             11  0
     d w_totcx         s             11  0
     d w_totcy         s             11  0
     d w_totcz         s             11  0
     d w_lagk          s                   like(malagk)
     d w_vsor          s                   like(mavsor)
     d w_ogrp          s                   like(maogrp)
     d w_hgrp          s                   like(mahgrp)
     d w_ugrp          s                   like(maugrp)
     d w_firm          s                   like(mafirm)
      *
     d                 ds
     d d_list                       128
     d  d_fvar                        8  0 overlay(d_list)
     d  d_tvar                        8  0 overlay(d_list:9)
     d  d_flag                        2  0 overlay(d_list:17)
     d  d_tlag                        2  0 overlay(d_list:19)
     d  d_fogr                        2  0 overlay(d_list:21)
     d  d_togr                        2  0 overlay(d_list:23)
     d  d_fhgr                        2  0 overlay(d_list:25)
     d  d_thgr                        2  0 overlay(d_list:27)
     d  d_fugr                        3  0 overlay(d_list:29)
     d  d_tugr                        3  0 overlay(d_list:32)
     d  d_levn                        6  0 overlay(d_list:35)
     d  d_selg                        4  0 overlay(d_list:41)
     d  d_faar                        4  0 overlay(d_list:45)
     d  d_fmnd                        2  0 overlay(d_list:49)
     d  d_taar                        4  0 overlay(d_list:51)
     d  d_tmnd                        2  0 overlay(d_list:55)
     d  d_aint                        2  0 overlay(d_list:57)
     d  d_bint                        2  0 overlay(d_list:59)
     d  d_cint                        2  0 overlay(d_list:61)
     d  d_xint                        2  0 overlay(d_list:63)
     d  d_yint                        2  0 overlay(d_list:65)
     d  d_zint                        2  0 overlay(d_list:67)
     d  d_sorb                        1  0 overlay(d_list:69)
     d  d_sorn                        1  0 overlay(d_list:70)
     d  d_sora                        1  0 overlay(d_list:71)
     d  d_oppd                        1  0 overlay(d_list:72)
     d  d_lt01                        1  0 overlay(d_list:73)
     d  d_lt02                        1  0 overlay(d_list:74)
     d  d_lt03                        1  0 overlay(d_list:75)
     d  d_lt04                        1  0 overlay(d_list:76)
     d  d_lt05                        1  0 overlay(d_list:77)
     d  d_lt06                        1  0 overlay(d_list:78)
     d  d_lt07                        1  0 overlay(d_list:79)
     d  d_lt08                        1  0 overlay(d_list:80)
     d  d_lt09                        1  0 overlay(d_list:81)
     d  d_lt10                        1  0 overlay(d_list:82)
     d  d_vtyp                        1    overlay(d_list:83)
     d  d_fvso                       10    overlay(d_list:84)
     d  d_tvso                       10    overlay(d_list:94)
     d  d_lager                      20  0 overlay(d_list:104)
     d  d_lagk                        2    overlay(d_list:124)
      *
     d                 ds
     d d_fper                         6
     d  d_fpar                        4  0 overlay(d_fper)
     d  d_fpmd                        2  0 overlay(d_fper:5)
     d                 ds
     d d_tper                         6
     d  d_tpar                        4  0 overlay(d_tper)
     d  d_tpmd                        2  0 overlay(d_tper:5)
      *
      *
     d d_lagr          ds
     d  d_la01                        2  0
     d  d_la02                        2  0
     d  d_la03                        2  0
     d  d_la04                        2  0
     d  d_la05                        2  0
     d  d_la06                        2  0
     d  d_la07                        2  0
     d  d_la08                        2  0
     d  d_la09                        2  0
     d  d_la10                        2  0
      *
     d d_ltyp          ds
     d  d_lty1                        1  0
     d  d_lty2                        1  0
     d  d_lty3                        1  0
     d  d_lty4                        1  0
     d  d_lty5                        1  0
     d  d_lty6                        1  0
     d  d_lty7                        1  0
     d  d_lty8                        1  0
     d  d_lty9                        1  0
     d  d_lty0                        1  0
      *
      *
      * Local Data Area
      *
     d                uds
      *
6.10 d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
      *
      * Definering av variable
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     *
     c     mabcl1_key    setll     mabcl1
     c     mabcl1_key    reade     mabcl1
     c                   dow       not %eof
     c                   exsr      behandle
     c     mabcl1_key    reade     mabcl1
     c                   enddo
     *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sette paramter og kjøre analyse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandle      begsr
     c                   move      malagr        d_lagr
     c                   if        malagr = 0
     c                   eval      d_flag = 0
     c                   eval      d_tlag = 99
     c                   else
     c                   eval      d_flag = 0
     c                   eval      d_tlag = 0
     c                   endif
      *
     c                   eval      a_lage(1) = d_la01
     c                   eval      a_lage(2) = d_la02
     c                   eval      a_lage(3) = d_la03
     c                   eval      a_lage(4) = d_la04
     c                   eval      a_lage(5) = d_la05
     c                   eval      a_lage(6) = d_la06
     c                   eval      a_lage(7) = d_la07
     c                   eval      a_lage(8) = d_la08
     c                   eval      a_lage(9) = d_la09
     c                   eval      a_lage(10) = d_la10
     c                   move      maltyp        d_ltyp
     c                   eval      a_ltyp(1) = d_lty1
     c                   eval      a_ltyp(2) = d_lty2
     c                   eval      a_ltyp(3) = d_lty3
     c                   eval      a_ltyp(4) = d_lty4
     c                   eval      a_ltyp(5) = d_lty5
     c                   eval      a_ltyp(6) = d_lty6
     c                   eval      a_ltyp(7) = d_lty7
     c                   eval      a_ltyp(8) = d_lty8
     c                   eval      a_ltyp(9) = d_lty9
     c                   eval      a_ltyp(10) = d_lty0
      *
     c                   move      mafaar        d_fpar
     c                   movel     mafmnd        d_fpmd
     c                   move      mataar        d_tpar
     c                   movel     matmnd        d_tpmd
     c                   eval      d_lager = malagr
     c                   eval      d_fvso = mavsor
     c                   eval      d_fogr = maogrp
     c                   eval      d_fhgr = mahgrp
     c                   eval      d_fugr = maugrp
     c                   eval      d_levn = 0
     c                   eval      d_selg = 0
     c                   eval      d_faar = d_fpar
     c                   eval      d_fmnd = d_fpmd
     c                   eval      d_taar = d_tpar
     c                   eval      d_tmnd = d_tpmd
     c                   eval      d_aint = maabap
     c                   eval      d_bint = maabbp
     c                   eval      d_cint = maabcp
     c                   eval      d_xint = maxyxp
     c                   eval      d_yint = maxyyp
     c                   eval      d_zint = maxyzp
     c                   eval      d_sorb = 0
     c                   eval      d_sorn = 0
     c                   eval      d_sora = 0
     c                   eval      d_oppd = 0
     c                   eval      d_lt01 = a_ltyp(1)
     c                   eval      d_lt02 = a_ltyp(2)
     c                   eval      d_lt03 = a_ltyp(3)
     c                   eval      d_lt04 = a_ltyp(4)
     c                   eval      d_lt05 = a_ltyp(5)
     c                   eval      d_lt06 = a_ltyp(6)
     c                   eval      d_lt07 = a_ltyp(7)
     c                   eval      d_lt08 = a_ltyp(8)
     c                   eval      d_lt09 = a_ltyp(9)
     c                   eval      d_lt10 = a_ltyp(10)
     c                   eval      d_vtyp = *blank
      *
     c                   call      'MB701C'
     c                   parm                    d_list
      *
     c                   eval      w_vsor = mavsor
     c                   eval      w_ogrp = maogrp
     c                   eval      w_hgrp = mahgrp
     c                   eval      w_ugrp = maugrp
     c                   exsr      upd_sum
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer antall abc / xyz
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     upd_sum       begsr
      *
     c                   eval      w_totax  =  0
     c                   eval      w_totay  =  0
     c                   eval      w_totaz  =  0
     c                   eval      w_totbx  =  0
     c                   eval      w_totby  =  0
     c                   eval      w_totbz  =  0
     c                   eval      w_totcx  =  0
     c                   eval      w_totcy  =  0
     c                   eval      w_totcz  =  0
     *
     c                   eval      mabdl2_lagk = malagk
     c                   eval      mabdl2_vsor = mavsor
     c                   eval      mabdl2_ogrp = maogrp
     c                   eval      mabdl2_hgrp = mahgrp
     c                   eval      mabdl2_ugrp = maugrp
     c     mabdl2_key    setll     mabdl2
     c     mabdl2_key    reade     mabdl2
     c                   dow       not %eof
     c                   if        mbkabc = 'AX'
     c                   eval      w_totax = w_totax + 1
     c                   endif
     c                   if        mbkabc = 'AY'
     c                   eval      w_totay = w_totay + 1
     c                   endif
     c                   if        mbkabc = 'AZ'
     c                   eval      w_totaz = w_totaz + 1
     c                   endif
     c                   if        mbkabc = 'BX'
     c                   eval      w_totbx = w_totbx + 1
     c                   endif
     c                   if        mbkabc = 'BY'
     c                   eval      w_totby = w_totby + 1
     c                   endif
     c                   if        mbkabc = 'BZ'
     c                   eval      w_totbz = w_totbz + 1
     c                   endif
     c                   if        mbkabc = 'CX'
     c                   eval      w_totcx = w_totcx + 1
     c                   endif
     c                   if        mbkabc = 'CY'
     c                   eval      w_totcy = w_totcy + 1
     c                   endif
     c                   if        mbkabc = 'CZ'
     c                   eval      w_totcz = w_totcz + 1
     c                   endif
     c     mabdl2_key    reade     mabdl2
     c                   enddo
      *
     c                   eval      mabclu_vsor = mavsor
     c                   eval      mabclu_ogrp = maogrp
     c                   eval      mabclu_hgrp = mahgrp
     c                   eval      mabclu_ugrp = maugrp
     c                   eval      mabclu_lagk = malagk
     c     mabclu_key    chain     mabclu
     c                   if        %found(mabclu)
     c                   eval      maanax = w_totax
     c                   eval      maanay = w_totay
     c                   eval      maanaz = w_totaz
     c                   eval      maanbx = w_totbx
     c                   eval      maanby = w_totby
     c                   eval      maanbz = w_totbz
     c                   eval      maancx = w_totcx
     c                   eval      maancy = w_totcy
     c                   eval      maancz = w_totcz
date c                   move      *date         makdat
6.10 c                   time                    maedat
6.10 c                   time                    maetim
6.10 c                   eval      maeusr = l_user
     c                   update    mabclur
     c                   endif
      *
     c                   endsr
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les på abc parameter
      *
     c     mabcl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les på av abc detaljer i abc-kode rekkefølge
      *
     c     mabdl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mabdl2_lagk
     c                   kfld                    mabdl2_vsor
     c                   kfld                    mabdl2_ogrp
     c                   kfld                    mabdl2_hgrp
     c                   kfld                    mabdl2_ugrp
      *
      * Key for å slette detaljer
      *
     c     mabclu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mabclu_vsor
     c                   kfld                    mabclu_ogrp
     c                   kfld                    mabclu_hgrp
     c                   kfld                    mabclu_ugrp
     c                   kfld                    mabclu_lagk
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
