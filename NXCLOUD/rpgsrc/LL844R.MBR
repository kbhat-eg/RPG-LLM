      /TITLE  ASLAGR: Sletter lagerposter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LL844R
      *  Beskrivelse . . : Sletter lagerposter, lagerregister
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign
      * --------- ------  -------------------------------------------------
5.70  *  PRGR     151208  Utvidet med prisgruppe /varetype         bof
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvlaglu    uf   e           k disk    rename(vlagpfr:vlaglur)
      *
      * Parameter-inn
      *
     d                 ds
     d d_prec                        64
     d  d_val1                        1  0 overlay(d_prec)
     d  d_val2                        1  0 overlay(d_prec:2)
     d  d_val3                        1  0 overlay(d_prec:3)
     d  d_val4                        1  0 overlay(d_prec:4)
     d  d_lage                        2  0 overlay(d_prec:5)
      *
      * Local Data Area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d vlagl1_lage     s                   like(vllage)
     d vlagl1_vare     s                   like(vlvare)
     d vvarl1_vare     s                   like(vvvare)
     d vlaglu_lage     s                   like(vllage)
     d vlaglu_vare     s                   like(vlvare)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vlfirm)
     d w_parm          s             64
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandle utplukk, Setter inn riktige parameterverdier
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   if        d_val1 <> 1 and
     c                             d_val2 <> 1 and
     c                             d_val3 <> 1
     c                   goto      end_pgm
     c                   endif
      *
     c                   eval      vlagl1_vare = *blank
     c                   eval      vlagl1_lage = d_lage
     c     vlagl1_key    setll     vlagl1r
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser poster fra register for utplukks-test
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   read      vlagl1r                                90
     c                   dow       *in90 = *off
      *
     c                   if        vlfirm <> w_firm
     c                   goto      end_pgm
     c                   endif
      *
     c                   if        vllage <> vlagl1_lage
     c                   goto      les_neste
     c                   endif
      *
      *  Sjekk om sletting av alle poster i lager
      *
     c                   if        d_val3 = 1
     c                   goto      slett_post
     c                   endif
      *
      *  Sjekk om bare sletting poster som ikke er er varetype L
      *
     c                   if        d_val1 = 1
     c                   eval      vvarl1_vare = vlvare
     c     vvarl1_key    chain     vvarl1                             93
     c                   if        *in93 = *off and
5.70 c                             vlvtyp <> 'L'
     c                   goto      slett_post
     c                   endif
     c                   endif
      *
      *  Sjekk om bare sletting poster som ikke finnes i vareregister
      *
     c                   if        d_val2 = 1
     c                   eval      vvarl1_vare = vlvare
     c     vvarl1_key    chain     vvarl1                             93
     c                   if        *in93 = *on
     c                   goto      slett_post
     c                   endif
     c                   endif
      *
     c                   goto      les_neste
      *
      *  Sletter lagerpost på valgt record
      *
     c     slett_post    tag
     c                   eval      vlaglu_vare = vlvare
     c                   eval      vlaglu_lage = vllage
     c     vlaglu_key    chain     vlaglu                             94
     c                   if        *in94 = *off
     c                   delete    vlaglur
     c                   endif
     c     les_neste     tag
     c                   read      vlagl1r                                90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *  Key for les av LAGER
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
      *
      *  Key for oppdatering av LAGER
      *
     c     vlaglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglu_vare
     c                   kfld                    vlaglu_lage
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    w_parm
     c                   eval      d_prec = w_parm
      *
      *  Legge inn firmanummer i nøklene
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
