     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : HR200R
      * Beskrivelse . . : Radioterminal trans, kalkulator
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       061000 HKO  Nytt program
      *       131200 HKO  Lagt inn "field exit" og bedret logikk ved
      *                   bytting av enhet
      *       020805 HKO  Endret les mot vare-enhet for å finne salgsenheter
6.21  * 6.20 101110 bof  Utvidet med leverandør på lager
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
      *
     fhr200d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vvfirm)
     d p_vare          s                   like(vvvare)
     d p_anta          s             11  3
6.21  *
6.21  * Definering av Local data area
6.21  *
6.21 d                uds
6.21 d l_lage               1001   1002  0
      *
      * Definering av variabler i nøkler
      *
     d vvenl2_saen     s                   like(vesaen)
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d x               s              1  0
      *
     d w_firm          s                   like(vvfirm)
     d w_vare          s                   like(vvvare)
     d w_omr1          s                   like(veomre)
     d w_omr2          s                   like(veomre)
     d w_omr3          s                   like(veomre)
     d w_enhe          s                   like(veenhe)
6.12 d w_lage          s              2  0
      *
     d w_enhe_gml      s                   like(veenhe)
6.21 d w_ldor          s                   like(vvldor)
6.21 d b_inlr          s              1    inz(*off)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1BLD Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     b1taga        tag
      *
      * Henter vareinformasjon
      *
     c     vvarl1_key    chain     vvarl1                             90
     c                   if        *in90 = *on
     c                   goto      avslutt
     c                   endif
6.21  *
6.21  * henter leverandør fra lager
6.21  *
6.21 c                   eval      w_lage = l_lage
6.21 c                   call      'VL721R'
6.21 c                   parm                    w_firm
6.21 c                   parm                    vvvare
6.21 c                   parm                    w_lage
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
      *
     c                   eval      vvtek1 = %trim(vvtek1)
     c                   eval      b1tek1 = %subst(vvtek1:1:19)
     c                   eval      b1tek2 = %subst(vvtek1:20:16)
      *
     c                   eval      b1lnav = *blank
      *
6.21 c                   if        w_ldor <> 0
6.21 c                   eval      rlevl1_levr = w_ldor
     c     rlevl1_key    chain     rlevl1                             90
     c                   if        *in90 = *off
     c                   eval      b1lnav = rlnavn
     c                   endif
     c                   endif
      *
      * Henter tre salgsenheter
      *
     c                   eval      *in80 = *on
     c                   eval      *in81 = *off
     c                   eval      *in82 = *off
      *
     c                   eval      x = 0
     c                   eval      b1enh1 = *blank
     c                   eval      b1enh2 = *blank
     c                   eval      b1enh3 = *blank
      *
     c                   eval      vvenl2_saen = 1
     c     vvenl2_key    setll     vvenl2
     c     vvenl2_key2   reade     vvenl2                                 90
     c                   dow       *in90 = *off and
     c                             x < 3
      *
     c                   eval      x = x + 1
      *
     c                   select
     c                   when      x = 1
     c                   eval      b1enh1 = veenhe
     c                   eval      w_omr1 = veomre
     c                   when      x = 2
     c                   eval      b1enh2 = veenhe
     c                   eval      w_omr2 = veomre
     c                   when      x = 3
     c                   eval      b1enh3 = veenhe
     c                   eval      w_omr3 = veomre
     c                   endsl
      *
     c     vvenl2_key2   reade     vvenl2                                 90
     c                   enddo
      *
     c                   eval      w_enhe = b1enh1
     c                   eval      b1anta = 0
      *
     c                   eval      w_enhe_gml = b1enh1
      *
      * Viser bildet
      *
     c     b1tagb        tag
      *
     c                   exfmt     b1bld
      *
     c                   if        *inkc
     c                   goto      avslutt
     c                   endif
      *
      * F1=Bytting av enhet
      *
     c                   if        *inka
     c                   exsr      bytt_enhet
     c                   goto      b1tagb
     c                   endif
      *
      * F5=Forny
      *
     c                   if        *inke
     c                   goto      b1taga
     c                   endif
      *
     c                   goto      b1tagb
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      p_anta = b1anta
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Kalkulerer og henter antall mellom enheter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bytt_enhet    begsr
      *
     c                   select
     c                   when      *in80 = *on and
     c                             b1enh2 <> *blank
     c                   eval      *in80 = *off
     c                   eval      *in81 = *on
     c                   when      *in81 = *on
     c                   eval      *in81 = *off
     c                   if        b1enh3 <> *blank
     c                   eval      *in82 = *on
     c                   else
     c                   eval      *in80 = *on
     c                   endif
     c                   when      *in82 = *on
     c                   eval      *in82 = *off
     c                   eval      *in80 = *on
     c                   endsl
      *
     c                   select
     c                   when      *in80 = *on
     c                   eval      w_enhe = b1enh1
     c                   when      *in81 = *on
     c                   eval      w_enhe = b1enh2
     c                   when      *in82 = *on
     c                   eval      w_enhe = b1enh3
     c                   endsl
      *
     c                   if        w_enhe <> w_enhe_gml
      *
     c                   select
     c                   when      w_enhe_gml = b1enh1
     c                   eval(h)   b1anta = b1anta / w_omr2
     c                   when      w_enhe_gml = b1enh2
     c                   if        *in82 = *on
     c                   eval(h)   b1anta = b1anta * w_omr2 / w_omr3
     c                   else
     c                   eval(h)   b1anta = b1anta * w_omr2 / w_omr1
     c                   endif
     c                   when      w_enhe_gml = b1enh3
     c                   eval(h)   b1anta = b1anta * w_omr3 / w_omr1
     c                   endsl
      *
     c                   endif
      *
     c                   eval      w_enhe_gml = w_enhe
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      *in31 = *on
     c                   goto      b1tagb
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_anta
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      * Key for les på vare-enhet-register
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
     c                   kfld                    vvenl2_saen
      *
     c     vvenl2_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      *
      * Henter firma og vare fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_vare = p_vare
      *
      * Initierer parametre
      *
     c                   eval      p_anta = 0
      *
     c                   endsr
