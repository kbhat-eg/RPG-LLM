      /TITLE  Fordeling utfra budsjett
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RH196R                                              *
      * Beskrivelse . . : Fordeling utfra budsjett                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * R5.30     241104  År ble ikke lagt ut i RBUNPF                   KOB  *
6.20  * R6.20     180412  Flytter blank til RTPROS                       NHO  *
6.31  *           040719  utvidet tabell i hht. utvidelse i RHSAPF       bof
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frhsal1    if   e           k disk    rename(rhsapfr:rhsal1r)
     frbunlu    uf a e           k disk    rename(rbunpfr:rbunlur)
     frtrapf    o  a e             disk    rename(rtrapfr:rtrapfr)
      *
6.31 d sal             s             15  2 dim(14)                              Saldo
     d txt             s             30    dim(1) ctdata perrcd(1)
      *
610  d rbunlu_nivo     s                   like(rsnivo)
610  d rbunlu_memb     s                   like(rsmemb)
610  d w_mem1          s                   like(rsmemb)
610  d w_mem2          s                   like(rsmemb)
     d                uds
     d rhpkda                300    305  0
     d rhprÅr                         4  0
     d rhpper                         2  0
     d rhppef                         2  0
     d rhppet                         2  0
     d rhpavf                         4  0
     d rhpavt                         4  0
     d rhpktf                         5  0
     d rhpktt                         5  0
     d rhpsrt                         1
     d rhpsml                         1
     d rhpavv                         1
     d rhpkgs                         1
     d rhpkks                         1
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d w_biln          s                   like(rtbiln)
     d w_bunt          s                   like(rsbunt)
     d w_fØrs          s                   like(rsfØrs)
     d w_firm          s                   like(rsfirm)
     d w_linj          s              5  0
     d w_pdeb          s                   like(rspdeb)
     d w_pkre          s                   like(rspkre)
     d w_psum          s                   like(rspsum)
     d w_rper          s                   like(rsrper)
     d w_sist          s                   like(rssist)
     d x               s                   like(rsrper)
      *
      *  Saldo-register - Kontonr.rekkefølge
      *
     irhsal1r
      *
     i              risa01                      sal(01)
     i              risa02                      sal(02)
     i              risa03                      sal(03)
     i              risa04                      sal(04)
     i              risa05                      sal(05)
     i              risa06                      sal(06)
     i              risa07                      sal(07)
     i              risa08                      sal(08)
     i              risa09                      sal(09)
     i              risa10                      sal(10)
     i              risa11                      sal(11)
     i              risa12                      sal(12)
     i              risa13                      sal(13)
     i              risa00                      sal(14)
      *
      /eject
      *
     c     les           tag
      *
     c                   eval      *in98 = *off
     c                   read      rhsal1                                 98
      *
     c                   if        *in98 = *on
     c                   goto      slutt
     c                   endif
      *
      * Kontroll av firma
      *
     c                   if        rifirm > l_firm
     c                   goto      slutt
     c                   endif
      *
      * Kontroll av regnskapsår
      *
     c                   if        riraar <> rhprÅr
     c                   goto      neste
     c                   endif
      *
      * Kontroll av rekordtype
      *
     c                   if        ritype <> 3
     c                   goto      neste
     c                   endif
      *
      * Kontroll av KONTO FRA - TIL
      *
     c                   if        rikont < rhpktf
     c                             or rikont > rhpktt
     c                   goto      neste
     c                   endif
      *
      * Kontroll av AVDELING FRA - TIL
      *
     c                   if        riavde < rhpavf
     c                             or riavde > rhpavt
     c                   goto      neste
     c                   endif
      *
      * Beregn BELØP
      *
     c                   eval      rtbelØ = 0
     c                   move      rhppef        x
     c                   dou       rhppet < x
     c                   eval      rtbelØ = rtbelØ + sal(x)
     c                   eval      x = x + 1
     c                   enddo
      *
      * Kontroll av beløp = 0
      *
     c                   if        rtbelØ = *zero
     c                   goto      neste
     c                   endif
      *
      * Summer posteringer til buntsum
      *
     c                   eval      w_sist = w_sist + 1
     c                   eval      w_linj = w_linj + 1
      *
      * Legg ut postering
      *
     c                   move      'T'           rtreid
     c                   move      w_firm        rtfirm
     c                   move      w_bunt        rtbunt
     c                   move      w_linj        rtlinj
     c                   eval      rtstat = *blank
     c     *dmy          move      rhpkda        rtbdat
     c                   eval      rtrper = *zero
     c                   move      w_biln        rtbiln
     c                   eval      rtdavd = *zeros
     c                   eval      rtdkto = *zeros
     c                   eval      rtdsps = *zeros
     c                   eval      rtdmva = *blank
     c                   eval      rtcavd = *zeros
     c                   eval      rtckto = *zeros
     c                   eval      rtcsps = *zeros
     c                   eval      rtcmva = *blank
      *
     c                   if        rtbelØ > *zero
     c                   move      riavde        rtdavd
     c                   move      rikont        rtdkto
     c                   move      rispes        rtdsps
     c                   eval      rtdmva = *zero
     c                   eval      w_psum = w_psum + rtbelØ
     c                   eval      w_pdeb = w_pdeb + rtbelØ
      *
     c                   else
      *
     c                   eval      rtbelØ = 0 - rtbelØ
     c                   move      riavde        rtcavd
     c                   move      rikont        rtckto
     c                   move      rispes        rtcsps
     c                   eval      rtcmva = *zeros
     c                   eval      w_pkre = w_pkre + rtbelØ
     c                   endif
      *
6.20 c*                  eval      rtpros = *zeros
6.20 c                   eval      rtpros = *blank
     c                   eval      rtrefn = *zeros
     c                   eval      rtfdat = *loval
     c                   eval      rtkrab = *zero
     c                   eval      rtvalk = *blank
     c                   eval      rtvalb = *zeros
     c                   eval      rtakti = *blank
     c                   eval      rtmngd = *zeros
     c                   eval      rtmngk = *zeros
     c                   eval      rtresn = *zeros
     c                   eval      rtvirk = *blank
     c                   eval      rtatte = *zeros
     c                   eval      rtkrys = *blank
     c                   eval      rtkidi = *blank
     c                   move      '85'          rtbilk
     c                   movel     txt(1)        rttext
     c                   eval      rtbetb = 0
     c                   eval      rtdeck = *blank
      *
610  c                   eval      rtnivo = *blank
610  c                   eval      rtmemb = w_mem1
610  c                   time                    rtedat
610  c                   time                    rtetim
610  c                   eval      rteusr = l_user
     c                   write     rtrapfr                                       SKRIV POSTERING
      *
     c     neste         tag
      *
     c                   goto      les
      *
      /eject
      *
     c     slutt         tag
      *
      *  Oppdater buntrecord i posteringsfile
      *
     c                   move      w_firm        rsfirm
     c                   move      w_bunt        rsbunt
     c                   eval      rsstat = *blank
     c                   move      rhprÅr        rsraar
     c                   movel     rhpper        rsrper
     c                   move      w_fØrs        rsfØrs
     c                   move      w_sist        rssist
     c     *dmy          move      rhpkda        rspdat
     c                   move      w_psum        rsbsum
     c                   move      w_psum        rspsum
     c                   move      w_pdeb        rspdeb
     c                   move      w_pkre        rspkre
      *
610  c                   eval      rsnivo = *blank
610  c                   eval      rsmemb = w_mem1
610  c                   time                    rsedat
610  c                   time                    rsetim
610  c                   eval      rseusr = l_user
     c                   write     rbunlur                                       SKRIV BUNT
      *
     c                   eval      *inlr = *on
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
610  c     *entry        plist
610  c                   parm                    w_mem1                         user
610  c                   parm                    w_mem2                         user
      *
     c                   eval      w_linj = 1
6.NU c                   eval      w_biln = 99999999
      *
     c                   move      l_firm        w_firm
      *
      *  Finn ledig buntnr. for IB-poster
      *
610  c                   eval      rbunlu_nivo = *blank
610  c                   eval      rbunlu_memb = w_mem1
     c                   move      l_firm        rsfirm                         Firma
     c                   move      99999         rsbunt                         Type
     c     keybun        setgt     rbunlu
     c                   readp     rbunlu                                 10
      *
     c                   if        *in10 = *on
     c                   eval      w_bunt = 1
     c                   endif
      *
     c                   if        *in10 = *off
     c                   eval      w_bunt = rsbunt + 1
     c                   endif
      *
     c                   eval      w_sist = w_linj
     c                   eval      w_psum = 0                                                .
     c                   eval      w_fØrs = 2                                                .
     c                   eval      w_sist = 1                                                .
      *
     c                   move      w_firm        rifirm
     c                   eval      rikont = *zeros
     c                   eval      rispes = *zeros
     c                   eval      riavde = *zeros
     c                   eval      riraar = *zeros
     c     rikey         setll     rhsal1r
      *
      * Key for SALDOREGISTER
      *
     c     rikey         klist
     c                   kfld                    rifirm
     c                   kfld                    rikont
     c                   kfld                    rispes
     c                   kfld                    riavde
     c                   kfld                    riraar
     c                   kfld                    ritype
      *
      * Key for file RBUNL1 nummerisk
      *
     c     keybun        klist
610  c                   kfld                    rbunlu_nivo
610  c                   kfld                    rbunlu_memb
     c                   kfld                    rsfirm
     c                   kfld                    rsbunt
      *
      * Key for statuskode
      *
     c     keya4         klist
     c                   kfld                    w_firm
      *
     c                   endsr
      *
**
Budsjettfordeling
