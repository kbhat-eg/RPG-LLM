     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASPMAL
      * Program . . . . : JV113R
      * Beskrivelse . . : Alternativt varenr., vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringbeskrrivelse
      * ----- ------ ---- -------------------------------------------------
      *       200809 bof  Nytt program
7.xx  * 7.00  060616 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  141020 bof  endring i d1btxt og k2btxt pga komp.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
     fjvall1    if   e           k disk    rename(jvalpfr:jvall1r)
     fjvall2    if   e           k disk    rename(jvalpfr:jvall2r)
     fjvallr    if   e           k disk    rename(jvalpfr:jvallrr)
     fjvallu    uf a e           k disk    rename(jvalpfr:jvallur)
      *
     fJV113d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d jvall1_btyp     s                   like(jvbtyp)
     d jvall2_bvar     s                   like(jvbvar)
     d jvallr_btyp     s                   like(jvbtyp)
     d jvallr_bvar     s                   like(jvbvar)
     d jvallu_btyp     s                   like(jvbtyp)
     d jvallu_bvar     s                   like(jvbvar)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_bnob          s                   like(jvbnob)
      *
     d p_bnob          s                   like(jvbnob)
     d p_valg          s              2  0
      *
     d s_btyp          s                   like(jvbtyp)
     d s_bvar          s                   like(jvbvar)
      *
      * Definering av konstanter
      *
7.xx d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2btyp <> *blank or
     c                             b2bvar <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      jvall2_bvar = b1bvar
     c     jvall2_key    setll     jvall2
     c                   other
     c                   eval      jvall1_btyp = b1btyp
     c     jvall1_key    setll     jvall1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Type
      *
     c                   if        b2btyp <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      jvall1_btyp = b2btyp
     c     jvall1_key    setll     jvall1
     c                   goto      end_pos
     c                   endif
      *
      * Alternativ
      *
     c                   if        b2bvar <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      jvall2_bvar = b2bvar
     c     jvall2_key    setll     jvall2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2btyp = *blank
     c                   eval      b2bvar = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1btyp = b1btyp
     c                   eval      c1bvar = b1bvar
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1btyp = b1btyp
     c                   eval      k1bvar = b1bvar
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1btyp = b1btyp
     c                   eval      d1bvar = b1bvar
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1btyp = b1btyp
     c                   eval      c1bvar = b1bvar
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1btyp = b2btyp
     c                   eval      c1bvar = b2bvar
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1btyp = *blank
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
     c                   if        c1btyp <> 'T' and
     c                             c1btyp <> 'F'
     c                   eval      *in31  = *on
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1bvar = *blank
     c                   eval      *in32  = *on
     c                   goto      c1taga
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      jvallr_btyp = c1btyp
     c                   eval      jvallr_bvar = c1bvar
     c     jvallr_key    chain     jvallr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1btyp = *blank
     c                   eval      c1bvar = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2btyp = *blank
     c                   eval      b2bvar = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      jvallr_btyp = c1btyp
     c                   eval      jvallr_bvar = c1bvar
     c     jvallr_key    chain     jvallr
      *
     c                   if        %found
     c                   eval      c2btxt = jvbtxt
     c                   eval      c2busr = jvbusr
     c                   if        jvboda <> *loval
     c     *dmy          move      jvboda        c2boda
     c                   else
     c                   eval      c2boda = 0
     c                   endif
     c                   if        jvbeda <> *loval
     c     *dmy          move      jvbeda        c2beda
     c                   else
     c                   eval      c2beda = 0
     c                   endif
     c                   if        jvbnod <> *loval
     c     *dmy          move      jvbnod        c2bnod
     c                   else
     c                   eval      c2bnod = 0
     c                   endif
     c                   if        jvbned <> *loval
     c     *dmy          move      jvbned        c2bnod
     c                   else
     c                   eval      c2bned = 0
     c                   endif
     c                   else
     c                   eval      c2btxt = *blank
     c                   eval      c2busr = *blank
     c                   eval      c2boda = 0
     c                   eval      c2beda = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1btyp = k1btyp
     c                   eval      c1bvar = k1bvar
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2btxt = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   jvallur
      *
     c                   eval      jvallu_btyp = c1btyp
     c                   eval      jvallu_bvar = c1bvar
     c     jvallu_key    chain     jvallur
      *
     c                   eval      jvbtxt = c2btxt
      *
     c                   time                    jvbeda
     c                   time                    jvbeti
     c                   eval      jvbusr = l_user
      *
     c                   if        %found
     c                   update    jvallur
     c                   else
     c                   eval      jvbnob = w_bnob
     c                   eval      jvbtyp = c1btyp
     c                   eval      jvbvar = c1bvar
     c                   time                    jvboda
     c                   write     jvallur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1btxt = jvbtxt
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      jvallr_btyp = d1btyp
     c                   eval      jvallr_bvar = d1bvar
     c     jvallr_key    chain     jvallr
      *
7.01 c                   eval      d1btxt = jvbtxt
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      jvallu_btyp = d1btyp
     c                   eval      jvallu_bvar = d1bvar
     c     jvallu_key    chain     jvallur
      *
     c                   delete    jvallur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_btyp = k1btyp
     c                   eval      s_bvar = k1bvar
      *
      * Fyller kopierings-bildet
      *
     c                   eval      jvallr_btyp = k1btyp
     c                   eval      jvallr_bvar = k1bvar
     c     jvallr_key    chain     jvallr
      *
     c     k1taga        tag
      *
7.01 c                   eval      k2btxt = jvbtxt

     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Validering
      *
     c                   if        k1btyp <> 'T' and
     c                             k1btyp <> 'F'
     c                   eval      *in31  = *on
     c                   goto      k1taga
     c                   endif
      *
     c                   if        k1bvar = *blank
     c                   eval      *in32  = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      jvallr_btyp = k1btyp
     c                   eval      jvallr_bvar = k1bvar
     c     jvallr_key    chain     jvallr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1btyp = s_btyp
     c                   eval      c1bvar = s_bvar
     c                   exsr      xc2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      jvall2
     c                   other
     c                   read      jvall1
     c                   endsl
      *
     c                   if        %eof or
     c                             jvbnob <> w_bnob
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      jvall2_bvar = jvbvar
     c                   other
     c                   eval      jvall1_btyp = jvbtyp
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1btyp = jvbtyp
     c                   eval      b1bvar = jvbvar
     c                   eval      b1btxt = jvbtxt
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     jvall2_key    setgt     jvall2
     c                   readp     jvall2
     c                   other
     c     jvall1_key    setgt     jvall1
     c                   readp     jvall1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     jvall2
     c                   other
     c                   readp     jvall1
     c                   endsl
      *
     c                   if        %eof or
     c                             jvbnob <> w_bnob
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      jvall2_bvar = jvbvar
     c                   other
     c                   eval      jvall1_btyp = jvbtyp
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     jvall2_key    setll     jvall2
     c                   other
     c     jvall1_key    setll     jvall1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_bnob
     c                   parm                    p_valg
      *
      * Parametre
      *
      * Key for posisjonering på alt.varenr
      *
     c     jvall1_key    klist
     c                   kfld                    w_bnob
     c                   kfld                    jvall1_btyp
      *
      * Key for posisjonering på alt.varnr
      *
     c     jvall2_key    klist
     c                   kfld                    w_bnob
     c                   kfld                    jvall2_bvar
      *
      * Key for les på alt.varenr
      *
     c     jvallr_key    klist
     c                   kfld                    w_bnob
     c                   kfld                    jvallr_btyp
     c                   kfld                    jvallr_bvar
      *
      * Key for oppdatering av alt.varenr
      *
     c     jvallu_key    klist
     c                   kfld                    w_bnob
     c                   kfld                    jvallu_btyp
     c                   kfld                    jvallu_bvar
      *
      * Key for les av vare
      *
     c     jvarl3_key    klist
     c                   kfld                    w_bnob
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_bnob = p_bnob
     c     jvarl3_key    chain     jvarl3
     c                   eval      b2nobb = p_bnob
     c                   eval      b2vte2 = jvtek1
     c                   eval      c1nobb = p_bnob
     c                   eval      c1vte2 = jvtek1
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      jvall1_btyp = *blank
     c     jvall1_key    setll     jvall1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
