      /TITLE  Oppdatering av leveringstid - oppdatering
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : MB103R
      * Beskrivelse . . : Oppdatering av ledetid           - oppdatering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.
      * --------- ------  --------------------------------------------------
      *           270804  Nytt program                             bø
5.50  *           291106  Justert reade
5.60  *           080808  Benytte ABC-kode på lager                bø
5.70  * PRGR      151208  Har vurdert pgm. mht. varetype, kan være som det er
      * 6.10      180609  Oppdatert med sporingsinformasjon        BSA
6.21  * 6.20      310812  Nytt felt, endringsdato for faste data som vises
6.22  * 6.20      040913  Kun endring hvis reell endring.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  BRUK AV INDIKATORER
      *
      *       KC = F3  = Exit
      *
      *       LR = Avslutt program
      *       30 = Protect av felter ved vise
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer
      *    70-79 = Arbeidsindikatorer i sub-rutiner
      *    80-89 = Arbeidsindikatorer ordinære
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl8    if   e           k disk    rename(vvarpfr:vvarl8r)
     fvvarla    if   e           k disk    rename(vvarpfr:vvarlar)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvlaglu    uf   e           k disk    rename(vlagpfr:vlaglur)
     fmltilu    uf   e           k disk    rename(mltipfr:mltilur)
      *
     fmc103p    o    e             printer
      *
      *  Local Data Area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      *  Parameter-rec
      *
     d                 ds
     d p_prec                       128
     d  p_fvar                        8  0 overlay(p_prec)
     d  p_tvar                        8  0 overlay(p_prec:9)
     d  p_flag                        2  0 overlay(p_prec:17)
     d  p_tlag                        2  0 overlay(p_prec:19)
     d  p_fogr                        2  0 overlay(p_prec:21)
     d  p_togr                        2  0 overlay(p_prec:23)
     d  p_fhgr                        2  0 overlay(p_prec:25)
     d  p_thgr                        2  0 overlay(p_prec:27)
     d  p_fugr                        3  0 overlay(p_prec:29)
     d  p_tugr                        3  0 overlay(p_prec:32)
     d  p_levn                        6  0 overlay(p_prec:35)
     d  p_sakb                        4    overlay(p_prec:41)
     d  p_sort                        1  0 overlay(p_prec:45)
     d  p_brud                        1  0 overlay(p_prec:46)
     d  p_dage                        3  0 overlay(p_prec:47)
     d  p_crdo                        1  0 overlay(p_prec:50)
      *
      * Definering av variable i nøkler
      *
     d vvarla_ldor     s                   like(vvldor)
     d vvarlx_ogrp     s                   like(vvogrp)
     d vvarly_ogrp     s                   like(vvogrp)
     d vvarly_hgrp     s                   like(vvhgrp)
     d vvarlz_ogrp     s                   like(vvogrp)
     d vvarlz_hgrp     s                   like(vvhgrp)
     d vvarlz_ugrp     s                   like(vvugrp)
     d vlagl1_vare     s                   like(vlvare)
     d vlaglu_vare     s                   like(vlvare)
     d vlaglu_lage     s                   like(vllage)
      *
     d vvarl1_vare     s                   like(vvvare)
     d mltilu_ogrp     s                   like(mcogrp)
     d mltilu_hgrp     s                   like(mchgrp)
     d mltilu_ugrp     s                   like(mcugrp)
     d mltilu_ldor     s                   like(mcldor)
     d mltilu_vare     s                   like(mcvare)
     d mltilu_lage     s                   like(mclage)
     d mltilu_crdo     s                   like(mccrdo)
      *
      * Definering av variable
      *
     d b_ok            s              1    inz(*off)
     d b_head          s              1    inz(*off)
     d w_firm          s              3  0
     d w_parm          s            128
     d w_dato          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * A1BLD Behandle bilde for valg av utskriftskriteria
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     a1taga        tag
      *
     c                   if        p_fvar <> 0
     c                   exsr      les_vare
     c                   goto      end_pgm
     c                   endif
      *
     c                   if        p_fugr <> 0
     c                   exsr      les_ugr
     c                   goto      end_pgm
     c                   endif
      *
     c                   if        p_fhgr <> 0
     c                   exsr      les_hgr
     c                   goto      end_pgm
     c                   endif
      *
     c                   if        p_fogr <> 0
     c                   exsr      les_ogr
     c                   goto      end_pgm
     c                   endif
      *
     c                   if        p_levn <> 0
     c                   exsr      les_lev
     c                   goto      end_pgm
     c                   endif
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser en vare kaller oppdatering av lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_vare      begsr
      *
     c                   movel     p_fvar        vvarl1_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found(vvarl1)
     c                   exsr      rec_ktrl
     c                   if        b_ok = *on
     c                   exsr      upd_lager
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser tom ugr på vareregister kaller oppdatering av lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_ugr       begsr
      *
     c                   eval      vvarlz_ogrp = p_fogr
     c                   eval      vvarlz_hgrp = p_fhgr
     c                   eval      vvarlz_ugrp = p_fugr
     c     vvarlz_key    setll     vvarl8
     c     vvarlz_key    reade     vvarl8
     c                   dow       not %eof(vvarl8)
     c                   exsr      rec_ktrl
     c                   if        b_ok = *on
     c                   exsr      upd_lager
     c                   endif
     c     vvarlz_key    reade     vvarl8
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser tom ugr på vareregister kaller oppdatering av lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_hgr       begsr
      *
     c                   eval      vvarly_ogrp = p_fogr
     c                   eval      vvarly_hgrp = p_fhgr
     c     vvarly_key    setll     vvarl8
     c     vvarly_key    reade     vvarl8
     c                   dow       not %eof(vvarl8)
     c                   exsr      rec_ktrl
     c                   if        b_ok = *on
     c                   exsr      upd_lager
     c                   endif
     c     vvarly_key    reade     vvarl8
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser tom ogr på vareregister kaller oppdatering av lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_ogr       begsr
      *
     c                   eval      vvarlx_ogrp = p_fogr
     c     vvarlx_key    setll     vvarl8
     c     vvarlx_key    reade     vvarl8
     c                   dow       not %eof(vvarl8)
     c                   exsr      rec_ktrl
     c                   if        b_ok = *on
     c                   exsr      upd_lager
     c                   endif
     c     vvarlx_key    reade     vvarl8
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser tom leverandør på vareregister kaller oppdatering av lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_lev       begsr
      *
     c                   eval      vvarla_ldor = p_levn
     c     vvarla_key    setll     vvarla
     c     vvarla_key    reade     vvarla
     c                   dow       not %eof(vvarla)
     c                   exsr      rec_ktrl
     c                   if        b_ok = *on
     c                   exsr      upd_lager
     c                   endif
     c     vvarla_key    reade     vvarla
     c                   enddo
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for kontroll mot parameter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     rec_ktrl      begsr
      *
     c                   eval      b_ok = *off
      *
     c                   if        vvfirm <> l_firm
     c                   leavesr
     c                   endif
      *
     c                   if        vvvtyp <> 'L' and
     c                             vvvtyp <> 'S'
     c                   leavesr
     c                   endif
      *
      * Sjekk om riktig ogrp
      *
     c                   if        p_fogr <> *zero
     c                             and p_fogr <> vvogrp
     c                   leavesr
     c                   endif
      *
      * Sjekk om riktig hgrp
      *
     c                   if        p_fhgr <> *zero
     c                             and p_fhgr <> vvhgrp
     c                   leavesr
     c                   endif
      *
      * Sjekk om riktig ugrp
      *
     c                   if        p_fugr <> *zero
     c                             and p_fugr <> vvugrp
     c                   leavesr
     c                   endif
      *
      * Sjekk om riktig leverandør - hvis leverandør er valgt
      *
     c                   if        p_levn <> *zero
     c                             and p_levn <> vvldor
     c                   leavesr
     c                   endif
      *
      * Sjekk om cross docking
      *
     c                   if        p_crdo <> *zero
     c                   if        p_crdo = 1
     c                             and vvcrdo <> 1
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Sett OK-indikator - record skal være med
      *
     c                   eval      b_ok = *on
      *
     c     end_ktrl      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppdaterer lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     upd_lager     begsr
      *
     c                   if        p_flag <> 0
      * oppdaterer et bestemt lager for en vare
     c                   eval      vlaglu_lage = p_flag
     c                   eval      vlaglu_vare = vvvare
     c     vlaglu_key    chain     vlaglu
     c                   if        %found(vlaglu)
6.22 c                   if        vlltid <> p_dage
     c                   eval      vlltid  =  p_dage
6.10 c                   time                    vledat
6.10 c                   time                    vletim
6.10 c                   eval      vleusr = l_user
6.21 c                   time                    vlfeda
6.21 c                   time                    vlfeti
6.21 c                   eval      vlfeus = l_user
     c                   update    vlaglur
6.22 c                   endif
     c                   endif
      *
     c                   else
      * leser alle lager for denne varen
     c                   eval      vlagl1_vare = vvvare
     c     vlagl1_key    setll     vlagl1
     c     vlagl1_key    reade     vlagl1
     c                   dow       not %eof(vlagl1)
     c                   eval      vlaglu_lage = vllage
     c                   eval      vlaglu_vare = vvvare
     c     vlaglu_key    chain     vlaglu
     c                   if        %found(vlaglu)
6.22 c                   if        vlltid <> p_dage
     c                   eval      vlltid  =  p_dage
6.10 c                   time                    vledat
6.10 c                   time                    vletim
6.10 c                   eval      vleusr = l_user
6.21 c                   time                    vlfeda
6.21 c                   time                    vlfeti
6.21 c                   eval      vlfeus = l_user
     c                   update    vlaglur
6.22 c                   endif
     c                   endif
     c     vlagl1_key    reade     vlagl1
     c                   enddo
     c
     c                   endif
      *
     c                   if        vvcrdo = 1
     c                   exsr      skriv
     c                   endif
      *
      * oppdaterer kjøredato
      *
     c                   eval      mltilu_ogrp = p_fogr
     c                   eval      mltilu_hgrp = p_fhgr
     c                   eval      mltilu_ugrp = p_fugr
     c                   eval      mltilu_ldor = p_levn
     c                   if        p_fvar <> 0
     c                   movel     p_fvar        mltilu_vare
     c                   else
     c                   eval      mltilu_vare = *blank
     c                   endif
     c                   eval      mltilu_lage = p_flag
     c                   eval      mltilu_crdo = p_crdo
     c     mltilu_key    chain     mltilur
     c                   if        %found(mltilu)
     c                   time                    mckdat
6.10 c                   time                    mcedat
6.10 c                   time                    mcetim
6.10 c                   eval      mceusr = l_user
     c                   update    mltilur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Skriver cross-docking varer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv         begsr
      *
     c                   if        b_head = *off
     c                   write     a1hdr                                30
     c                   endif
      *
     c                   eval      d1vare = vvvare
     c                   eval      d1text = vvtek1
     c                   if        vlkabc <> *blank
     c                   eval      d1kabc = vlkabc
     c                   else
     c                   eval      d1kabc = vvkabc
     c                   endif
     c                   eval      d1ltid = vlltid
      *
     c                   write     d1det                                30
      *
     c                   if        *in30 = *on
     c                   write     a1hdr                                30
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      *  Key for les av vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *  Key for les av vareregister
      *
     c     vvarla_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarla_ldor
      *
      *  Key for les av vareregister
      *
     c     vvarlx_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlx_ogrp
      *
      *  Key for les av vareregister
      *
     c     vvarly_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarly_ogrp
     c                   kfld                    vvarly_hgrp
      *
      *  Key for les av vareregister
      *
     c     vvarlz_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlz_ogrp
     c                   kfld                    vvarlz_hgrp
     c                   kfld                    vvarlz_ugrp
      *
      *  Key for les av alle lager på en vare
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
      *
      *  Key for oppdatering av lager
      *
     c     vlaglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglu_vare
     c                   kfld                    vlaglu_lage
      *
      * Key for oppdatering av kjøredato på leveringstids-parameter
      *
     c     mltilu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mltilu_ogrp
     c                   kfld                    mltilu_hgrp
     c                   kfld                    mltilu_ugrp
     c                   kfld                    mltilu_ldor
     c                   kfld                    mltilu_vare
     c                   kfld                    mltilu_lage
     c                   kfld                    mltilu_crdo
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    w_parm
      *
     c                   eval      p_prec = w_parm
     c                   eval      w_firm = l_firm
      *
      *
     c                   time                    w_dato
     c                   time                    w_time
     c                   eval      a1user = l_user
     c     *dmy          move      w_dato        a1date
     c     *hms          move      w_time        a1time
     c                   eval      a1ogrp = p_fogr
     c                   eval      a1hgrp = p_fhgr
     c                   eval      a1ugrp = p_fugr
     c                   eval      a1wsid = l_wsid
     c                   eval      a1firm = l_firm
     c                   eval      a1fnav = l_fnav
      *
     c                   endsr
