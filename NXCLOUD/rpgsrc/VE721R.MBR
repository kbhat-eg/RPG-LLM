     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VE721R
      * Beskrivelse . . : Henter gtin pr. leverandør fra VA til EVR
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       230512 bof  Nytt program
6.20  * 6.20  240114 bsa  Sletter først alle gtin nummer på valgt
6.20  *                   leverandør og vare, før det legges ut nye.
6.21  * 6.20  060514 bsa  Feil bruk av nøkler
6.2a  * 6.20  120214 bof  Feilretting, legger på firma når write
6.30  * 6.30  260113 bof  Omskriving i forb. endring pakning VA
6.32  *       140314 bof  Lagt inn test på fra /til dato på pakning
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl5    if   e           k disk    rename(vvarpfr:vvarl5r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fvvepl1    if   e           k disk    rename(vveppfr:vvepl1r)
6.20 fvvepl3    uf   e           k disk    rename(vveppfr:vvepl3r)
     fjlevl3    if   e           k disk    rename(jlevpfr:jlevl3r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjvpkl1    if   e           k disk    rename(jvpkpfr:jvpkl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvveplu    uf a e           k disk    rename(vveppfr:vveplur)
     fve721d    cf   e             workstn
      *
     fve721p    o    e             printer
      *
      * Definering av LDA
      *
     d                uds
     d l_wsid                901    910
     d  l_user               911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable i nøkler
      *
     d vvarl5_ldor     s                   like(vvldor)
     d vvenl1_vare     s                   like(vevare)
     d vveplu_pvar     s                   like(vepvar)
     d vveplu_penh     s                   like(vepenh)
     d vveplu_pldo     s                   like(vepldo)
6.20 d vvepl3_pvar     s                   like(vepvar)
6.20 d vvepl3_pldo     s                   like(vepldo)
     d
     d jlevl3_enum     s                   like(jlenum)
     d jvarl1_vare     s                   like(jwvare)
     d jvpkl1_vare     s                   like(jwvare)
     d jvpkl1_ldor     s                   like(jwldor)
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d b_inlr          s              1    inz(*off)
     d b_uskr          s              1    inz(*off)
6.32 d b_pakn          s              1    inz(*off)
      *
     d w_firm          s                   like(vvfirm)
5.64 d w_udat          s               d   datfmt(*iso)
5.64 d w_stat          s              1
6.32 d w_dato          s               d   datfmt(*iso)
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1BLD  - B1 Skjermbilde for parametervalg
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     start_pgm     tag
      *
     c                   eval      b1ldor = 0
      *
     c     bilde_ut      tag
     c                   exfmt     b1bld
      *
      *  F3=Avslutt program
      *
     c                   if        *inkc = *on
     c                   goto      avslutt
     c                   endif
      *
      *  F4=Spørring
      *
     c                   if        *inkd = *on
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    b1ldor
     c                   parm                    b1ltek
     c                   goto      bilde_ut
     c                   endif
      *
      *  Er gyldig leverandør angitt
      *
     c                   eval      rlevl1_levr = b1ldor
     c     rlevl1_key    chain     rlevl1
     c                   if        not %found
     c                   eval      *in33 = *on
     c                   goto      bilde_ut
     c                   endif
      *
      *  Sjekker om leverandør er knyttet i VA
      *
     c                   eval      jlevl3_enum = b1ldor
     c     jlevl3_key    chain     jlevl3
     c                   if        not %found
     c                   eval      *in34 = *on
     c                   goto      bilde_ut
     c                   endif
      *
      * starter behandling
      *
     c                   eval      jlevl3_enum = b1ldor
     c     jlevl3_key    chain     jlevl3
     c                   if        %found(jlevl3)
      *
     c                   eval      vvarl5_ldor = b1ldor
     c     vvarl5_key    setll     vvarl5
     c     vvarl5_key    reade     vvarl5
     c                   dow       not %eof(vvarl5)
      *
6.20 c                   exsr      rmv_gtin
      *
     c                   exsr      behandle
      *
     c     vvarl5_key    reade     vvarl5r
     c                   enddo
      *
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for finne gtin pr. leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandle      begsr
      *
      * leser VA varen
      *
     c                   eval      jvarl1_vare = vvvare
     c     jvarl1_key    chain     jvarl1
     c                   if        %found(jvarl1)
      *
      * leser alle enheter på varen
      *
     c                   eval      vvenl1_vare = vvvare
     c     vvenl1_key    setll     vvenl1
     c     vvenl1_key    reade     vvenl1
     c                   dow       not %eof(vvenl1)
6.11  *
6.11  * finner alle gtin på vare - enhet og legger ut pr. prisgiver
6.11  *
6.11 c                   eval      jvpkl1_vare = vevare
6.30 c                   eval      jvpkl1_ldor = jlldor
6.30 c     jvpkl1_key    setll     jvpkl1
6.30 c     jvpkl1_key    reade     jvpkl1
6.30 c                   dow       not %eof(jvpkl1)
6.32  *
6.32  * sjekker fra / tildato
6.32 c                   exsr      sjekk_pkdato
6.32 c                   if        b_pakn = *on
6.30  *
6.30 c                   if        jwenhe = veenhe and
6.30 c                             jwpakn <> '000' and
6.30 c                             jwgtin <> 0
      *
     c                   exsr      sjk_dupl
     c                   if        w_stat = *blank
6.30 c                   eval      vveplu_pvar = jwvare
6.11 c                   eval      vveplu_penh = jwenhe
6.11 c                   eval      vveplu_pldo = b1ldor
6.11 c     vveplu_key    chain     vveplu
6.30 c                   eval      vepgti = jwgtin
6.30 c                   eval      vepbes = jwbest
6.11 c                   time                    vepeda
6.11 c                   time                    vepeti
6.11 c                   eval      vepeus = l_user
6.11 c                   if        %found(vveplu)
6.11 c                   update    vveplur
6.11 c                   else
     c                   time                    vepoda
     c                   time                    vepoti
6.11 c                   eval      vepous = l_user
6.2a c                   eval      vepfir = w_firm
6.11 c                   eval      vepvar = vveplu_pvar
6.11 c                   eval      vepenh = vveplu_penh
6.11 c                   eval      vepldo = vveplu_pldo
6.11 c                   write     vveplur
6.11 c                   endif
6.11 c                   endif
      *
     c                   endif
      *
6.32 c                   endif
6.11 c
6.11 c     jvpkl1_key    reade     jvpkl1
6.11 c                   enddo
      *
     c     vvenl1_key    reade     vvenl1
     c                   enddo
      *
     c                   endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke duplikat
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_dupl      begsr
      *
     c                   call      'VE718R  '
6.30 c                   parm                    jwgtin
     c                   parm                    vvvare
     c                   parm                    w_stat
      *
     c                   if        w_stat = '1'
      * duplikat finnes - skriver logg
      *
     c                   if        b_uskr = *off
     c                   exsr      skr_rapph
     c                   endif
      *
     c                   exsr      skr_rappl
      *
     c                   endif
      *
     c                   endsr
      *
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  * Subrutine for å fjerne GTIN numre pr. leverandør før oppdat.
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     rmv_gtin      begsr
6.20  *
6.20  * leser alle gtin nummer på vare og leverandør, og sletter disse
6.20  *
6.20 c                   eval      vvepl3_pvar = vvvare
6.20 c                   eval      vvepl3_pldo = b1ldor
6.20 c     vvepl3_key    setll     vvepl3
6.20 c     vvepl3_key    reade     vvepl3
6.20 c                   dow       not %eof(vvepl3)
6.20 c                   delete    vvepl3r
6.20 c     vvepl3_key    reade     vvepl3
6.20 c                   enddo
6.20  *
6.20 c                   endsr
6.20  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for skriving av rapportheader v/ utskrift valgt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_rapph     begsr
      *
     c                   eval      a1firm = l_firm
     c                   eval      a1fnav = l_fnav
     c                   eval      a1wsid = l_wsid
     c                   time                    a1time
5.64 c     *dmy          move      w_udat        a1date
     c                   eval      a1user = l_user
      *
      *
     c                   write     a1hdr                                30
      *
     c                   eval      b_uskr = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for skriving av rapportlinje v/ utskrift valgt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_rappl     begsr
      *
     c                   eval      a1pvar = vvvare
6.30 c                   eval      a1pgti = jwgtin
     c                   eval      a1text = 'GTIN-nr finnes på minst -
     c                             en annen vare'
      *
     c                   write     a1det                                30
      *
      * Er det overflow ?
      *
     c                   select
     c                   when      *in30 = *on
     c                   exsr      xovrfl
     c                   endsl
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for overflow                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xovrfl        begsr
      *
     c                   write     a1hdr                                30
      *
     c                   endsr
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * Subrutine for å sjekke om dato er innenfor fra /til dato
6.32  * hvis *loval i begge datoer = ok
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     sjekk_pkdato  begsr
6.32  *
6.32 c                   eval      b_pakn = *on
6.32 c                   time                    w_dato
6.32  *
6.32 c                   if        jwfdat = *loval and
6.32 c                             jwtdat = *loval
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        jwfdat <= w_dato and
6.32 c                             jwtdat >  w_dato
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        jwfdat = *loval  and
6.32 c                             jwtdat >  w_dato
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   if        jwfdat <= w_dato and
6.32 c                             jwtdat =  *loval
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   eval      b_pakn = *off
6.32  *
6.32 c                   endsr
6.32  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les på vare
      *
     c     vvarl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl5_ldor
      *
      * Key for les på leverandør
      *
     c     jlevl3_key    klist
     c                   kfld                    jlevl3_enum
      *
      * Key for les på vvare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for les på enhet VA
      *
     c     jvpkl1_key    klist
     c                   kfld                    jvpkl1_vare
6.30 c                   kfld                    jvpkl1_ldor
      *
      * Key for oppdatering av vare-enhhet-prisgiver EVR
      *
     c     vveplu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vveplu_pvar
     c                   kfld                    vveplu_penh
     c                   kfld                    vveplu_pldo
6.20  *
6.20  * Key for oppdatering av vare-enhhet-prisgiver EVR
6.20  *
6.20 c     vvepl3_key    klist
6.20 c                   kfld                    w_firm
6.21 c                   kfld                    vvepl3_pvar
6.21 c                   kfld                    vvepl3_pldo
      *
      * Key for les av vareenhet i evr
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
5.64 c                   time                    w_udat
      *
     c                   endsr
