     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FA922R
      * Beskrivelse . . : Oppdatering av memosaldo i kunde-register
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       080904 HKO  Lagt inn firma som parameter
5.60  * R5.60 061107 AMD  Lagt inn løsning for kontroll av grense for
      *                   almenningsrabatter
6.10  * V6.10 230909 BSA  Lagt inn sporingsfelter for utvalgte OKO filer.
6.11  * V6.10 070510 BSA  Endret loggemåte
6.20  * V6.20 140611 BSA  Memosaldo lagt ut på egen file
6.21  * V6.20 180612 BSA  Hvis ikke kunde finnes fra før, opprettes denne
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.20 f*rkunlu    uf   e           k disk    rename(rkunpfr:rkunlur)
6.20 frkmelu    uf a e           k disk    rename(rkmepfr:rkmelur)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
      *
      * Definering av parametere
      *
     d p_firm          s                   like(rkfirm)
     d p_kund          s                   like(rkkund)
     d p_otyp          s                   like(vaotyp)
     d p_sald          s                   like(rkmems)
5.60 d p_sal2          s                   like(rkmema)
      *
      * Definering av variable i nøkler
      *
     d rkunlu_kund     s                   like(rkkund)
     d votyl1_otyp     s                   like(vaotyp)
      *
      * Definering av variable
      *
     d w_firm          s                   like(rkfirm)
      *
      * Definering av Local data area
      *
6.10 d                uds
6.10 d l_user                911    920
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdater memosaldo i KUNDE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Hent opplysninger fra ORDRETYPE-REGISTER
      *
     c                   eval      votyl1_otyp = p_otyp
     c     votyl1_key    chain     votyl1r                            90
      *
      * Oppdaterer i henhold til kode for ordreverdi på ordretype
      *
     c                   if        vaoogr = 0
     c                   goto      xslutt
     c                   endif
      *
      * Snur fortegn
      *
     c                   if        vaokre <> *blank
     c                   eval      p_sald = p_sald * -1
5.60 c                   eval      p_sal2 = p_sal2 * -1
     c                   endif
      *
      * Oppdaterer KUNDE-REGISTER
      *
     c                   eval      rkunlu_kund = p_kund
6.20 c**   rkunlu_key    chain     rkunlu                             90
6.20 c     rkunlu_key    chain     rkmelu                             90
     c                   if        *in90 = *off
     c                   eval      rkmems = rkmems + p_sald
5.60 c                   eval      rkmema = rkmema + p_sal2
6.20 c                   time                    rkmeda
6.20 c                   time                    rkmeti
6.20 c                   eval      rkmeus = l_user
6.20 c**                 time                    rk2dpt
6.20 c**                 eval      rkepgm = 'FA922R    '
6.20 c**                 update    rkunlur
6.20 c                   update    rkmelur
6.21  * Hvsi posten ikke finnes, må den opprettes
6.21 c                   else
6.21 c                   clear                   rkmelur
6.21 c                   eval      rkfirm = w_firm
6.21 c                   eval      rkkund = p_kund
6.21 c                   eval      rkmems = rkmems + p_sald
6.21 c                   eval      rkmema = rkmema + p_sal2
6.21 c                   time                    rkmeda
6.21 c                   time                    rkmoda
6.21 c                   time                    rkmeti
6.21 c                   time                    rkmoti
6.21 c                   eval      rkmeus = l_user
6.21 c                   write     rkmelur
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for file : KUNDE-REGISTER
      *
     c     rkunlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunlu_kund
      *
      * Key for file : ORDRETYPE-REGISTER
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_otyp
     c                   parm                    p_kund
     c                   parm                    p_sald
5.60 c                   parm                    p_sal2
      *
      * Legger inn firmanummer i nøklene
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
