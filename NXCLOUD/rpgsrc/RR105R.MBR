     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RR105R                                              *
      * Beskrivelse . . : Resultatrapport, vedlikehold rapportlinje           *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       150902 KOB  Nytt program
T5.00 * R5.00 250303 HFL  Timestampfeil rettet
7.xx  * 7.xx  120516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frrf5l1    if   e           k disk    rename(rrf5pfr:rrf5l1r)
     frrf5l2    if   e           k disk    rename(rrf5pfr:rrf5l2r)
     frrf5lu    uf a e           k disk    rename(rrf5pfr:rrf5lur)
     frrf5lr    if   e           k disk    rename(rrf5pfr:rrf5lrr)
     frrf8l1    if   e           k disk    rename(rrf8pfr:rrf8l1r)
     frrf8lu    uf a e           k disk    rename(rrf8pfr:rrf8lur)
      *
     frr105d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d rrf5l1_5rap     s                   like(rr5rap)
     d rrf5l1_5lin     s                   like(rr5lin)
     d rrf5l2_5rap     s                   like(rr5rap)
     d rrf5l2_5txt     s                   like(rr5txt)
     d rrf5lr_5rap     s                   like(rr5rap)
     d rrf5lr_5lin     s                   like(rr5lin)
     d rrf5lu_5rap     s                   like(rr5rap)
     d rrf5lu_5lin     s                   like(rr5lin)
      *
     d rrf8l1_8rap     s                   like(rr8rap)
     d rrf8l1_8lin     s                   like(rr8lin)
     d rrf8lu_8rap     s                   like(rr8rap)
     d rrf8lu_8lin     s                   like(rr8lin)
     d rrf8lu_8lØp     s                   like(rr8lØp)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d p_0rap          s                   like(rr5rap)
     d p_0txt          s                   like(rr5txt)
     d p_5rap          s                   like(rr5rap)
     d p_5lin          s                   like(rr5lin)
     d p_5txt          s                   like(rr5txt)
     d p_5kod          s                   like(rr5kod)
     d p_valg          s                   like(b1valg)
      *
     d w_firm          s                   like(rr5fir)
      *
     d s_5rap          s                   like(rr5rap)
     d s_5lin          s                   like(rr5lin)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(12)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Siden som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b25lin <> 0 or
     c                             b25txt <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      rrf5l2_5txt = b15txt
     c     rrf5l2_key    setll     rrf5l2
     c                   other
     c                   eval      rrf5l1_5lin = b15lin
     c     rrf5l1_key    setll     rrf5l1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Rapport
      *
     c                   if        b25lin <> 0
     c                   eval      w_seqe = *blank
     c                   eval      rrf5l1_5lin = b25lin
     c     rrf5l1_key    setll     rrf5l1
     c                   goto      end_pos
     c                   endif
      *
      * Beskrivelse
      *
     c                   if        b25txt <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      rrf5l2_5txt = b25txt
     c     rrf5l2_key    setll     rrf5l2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b25lin = 0
     c                   eval      b25txt = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c15lin = b15lin
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k15lin = b15lin
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d15lin = b15lin
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c15lin = b15lin
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vedlikeholde konto/linje
      *
     c                   if        b1valg = 7                                          ..... .
     c                   eval      p_5rap = b15rap                                         . .
     c                   eval      p_5lin = b15lin                                         . .
     c                   eval      p_5txt = b15txt                                         . .
     c                   eval      p_5kod = b15kod                                         . .
     c                   eval      p_valg = b1valg                                         . .
      *
     c                   call      'RR108R'
     c                   parm                    p_0rap
     c                   parm                    p_0txt
     c                   parm                    p_5lin
     c                   parm                    p_5txt
     c                   parm                    p_5kod
     c                   parm                    p_valg
      *
     c                   eval      b1valg = 0                                              . .
     c                   update    b1sfl
     c                   iter                                                              . .
     c                   endif                                                         ..... .
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c15lin = b25lin
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c15lin = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      rrf5lr_5lin = c15lin
     c     rrf5lr_key    chain     rrf5lr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c15lin = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b25lin = 0
     c                   eval      b25txt = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      rrf5lr_5lin = c15lin
     c     rrf5lr_key    chain     rrf5lr
      *
     c                   if        %found
     c                   eval      c25txt = rr5txt
     c                   eval      c25txt = rr5txt
     c                   eval      c25kod = rr5kod
     c                   eval      c25sln = rr5sln
     c                   eval      c25nul = rr5nul
     c                   eval      c25abl = rr5abl
     c                   eval      c2eusr = rr5usr
T5.00c                   if        rr5dat = *loval
T5.00c                   move      *zeros        c2edat
T5.00c                   else
     c     *dmy          move      rr5dat        c2edat
T5.00c                   endif
     c                   else
     c                   eval      c25txt = *blank
     c                   eval      c25kod = *blank
     c                   eval      c25sln = 0
     c                   eval      c25nul = *blank
     c                   eval      c25abl = *blank
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c15lin = k15lin
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c                   if        c25txt = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   rrf5lur
      *
     c                   eval      rrf5lu_5lin = c15lin
     c     rrf5lu_key    chain     rrf5lur
      *
     c                   eval      rr5txt = c25txt
     c                   eval      rr5kod = c25kod
     c                   eval      rr5sln = c25sln
     c                   eval      rr5nul = c25nul
     c                   eval      rr5abl = c25abl
      *
     c                   time                    rr5dat
     c                   time                    rr5tim
     c                   eval      rr5usr = l_user
      *
     c                   if        %found
     c                   update    rrf5lur
     c                   else
     c                   eval      rr5fir = w_firm
     c                   eval      rr5rap = p_0rap
     c                   eval      rr5lin = c15lin
     c                   write     rrf5lur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b15txt = rr5txt
     c                   eval      b15kod = rr5kod
     c                   eval      b15sln = rr5sln
     c                   eval      b15nul = rr5nul
     c                   eval      b15abl = rr5abl
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      rrf5lr_5lin = d15lin
     c     rrf5lr_key    chain     rrf5lr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Slett fra-til begrep
      *
     c                   eval      rrf8l1_8lin = d15lin
     c     rrf8l1_key    setll     rrf8l1r                                60
     c     rrf8l1_key    reade     rrf8l1r                                60
      *
     c                   dow       *in60 = *off                                 EOF
      *
     c                   eval      rrf8lu_8lin = rr8lin
     c                   eval      rrf8lu_8lØp = rr8lØp
     c     rrf8lu_key    chain     rrf8lur                            61
      *
     c                   if        %found
     c                   delete    rrf8lur
     c                   endif
      *
     c     rrf8l1_key    reade     rrf8l1r                                60
      *
     c                   enddo
      *
      * Sletter rad
      *
     c                   eval      rrf5lu_5lin = d15lin
     c     rrf5lu_key    chain     rrf5lur
      *
     c                   delete    rrf5lur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_5lin = k15lin
      *
      * Fyller kopierings-bildet
      *
     c                   eval      rrf5lr_5lin = k15lin
     c     rrf5lr_key    chain     rrf5lr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k15lin = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      rrf5lr_5lin = k15lin
     c     rrf5lr_key    chain     rrf5lr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kopier fra-til begrep
      *
     c                   eval      rrf8l1_8rap = s_5rap
     c     rrf8l1_key    setll     rrf8l1r
     c     rrf8l1_key    reade     rrf8l1r                                60
      *
     c                   dow       *in60 = *off                                 EOF
      *
     c                   eval      rrf8lu_8rap = s_5rap
     c                   eval      rrf8lu_8lin = rr8lin
     c                   eval      rrf8lu_8lØp = rr8lØp
     c     rrf8lu_key    chain     rrf8lur
      *
     c                   if        %found
     c                   eval      rr8rap = k15lin
     c                   write     rrf8lur
     c                   endif
      *
     c     rrf8l1_key    reade     rrf8l1r                                60
      *
     c                   enddo
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c15lin = s_5lin
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      rrf5l2
     c                   other
     c                   read      rrf5l1
     c                   endsl
      *
     c                   if        %eof or
     c                             rr5fir <> w_firm or
     c                             rr5rap <> p_0rap
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      rrf5l2_5txt = rr5txt
     c                   other
     c                   eval      rrf5l1_5lin = rr5lin
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b15rap = rr5rap
     c                   eval      b15lin = rr5lin
     c                   eval      b15txt = rr5txt
     c                   eval      b15kod = rr5kod
     c                   eval      b15sln = rr5sln
     c                   eval      b15nul = rr5nul
     c                   eval      b15abl = rr5abl
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   select
     c                   when      w_seqe = 'B' and
     c                             %eof(rrf5l2)
     c     rrf5l2_key    setgt     rrf5l2
     c                   readp     rrf5l2
     c                   when      w_seqe = *blank and
     c                             %eof(rrf5l1)
     c     rrf5l1_key    setgt     rrf5l1
     c                   readp     rrf5l1
     c                   endsl
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     rrf5l2
     c                   other
     c                   readp     rrf5l1
     c                   endsl
      *
     c                   if        %eof or
     c                             rr5fir <> w_firm
     c                   leave
     c                   endif
      *
     c                   if        rr5rap <> p_0rap
     c                   iter
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      rrf5l2_5txt = rr5txt
     c                   other
     c                   eval      rrf5l1_5lin = rr5lin
     c                   endsl
      *
     c                   enddo
      *
     c                   select
     c                   when      w_seqe = 'B' and
     c                             %eof(rrf5l2)
     c     rrf5l2_key    setll     rrf5l2
     c                   when      w_seqe = *blank and
     c                             %eof(rrf5l1)
     c     rrf5l1_key    setll     rrf5l1
     c                   endsl
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_0rap
     c                   parm                    p_0txt
      *
      * Key for posisjonering på linje
      *
     c     rrf5l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rrf5l1_5rap
     c                   kfld                    rrf5l1_5lin
      *
      * Key for posisjonering på beskrivelse
      *
     c     rrf5l2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rrf5l2_5rap
     c                   kfld                    rrf5l2_5txt
      *
      * Key for les på rapport
      *
     c     rrf5lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rrf5lr_5rap
     c                   kfld                    rrf5lr_5lin
      *
      * Key for oppdatering av rapport
      *
     c     rrf5lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rrf5lu_5rap
     c                   kfld                    rrf5lu_5lin
      *
      * Key for les på fra-til begrep
      *
     c     rrf8l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rrf8l1_8rap
     c                   kfld                    rrf8l1_8lin
      *
      * Key for oppdatering av fra-til begrep
      *
     c     rrf8lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rrf8lu_8rap
     c                   kfld                    rrf8lu_8lin
     c                   kfld                    rrf8lu_8lØp
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
     c                   eval      b20rap = p_0rap
     c                   eval      b20txt = p_0txt
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rrf5l1_5rap = p_0rap
     c                   eval      rrf5l2_5rap = p_0rap
     c                   eval      rrf5lu_5rap = p_0rap
     c                   eval      rrf5lr_5rap = p_0rap
     c                   eval      rrf8l1_8rap = p_0rap
     c                   eval      rrf8lu_8rap = p_0rap
     c                   eval      rrf5l1_5lin = 0
     c     rrf5l1_key    setll     rrf5l1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
