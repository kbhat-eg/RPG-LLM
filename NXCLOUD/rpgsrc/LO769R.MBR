     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LO767R
      *  Beskrivelse . . : Generere ordrebekreftelse i EDIFAKT-format
      *                    Uten elektronisk mottatt bestilling.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  -------------------------------------------------
      * K000  060122 bsa  Nytt program
8.01  * K000  140122 bsa  Justeringer etter tilbakeldinger. Koder orgnr i
8.01  * K000              og rekvisisjonsnummer.
8.02  * K000  180122 bsa  Legge ut eksternt prosjektnummer.
8.03  * K000  270122 bsa  GLN segmentet på ED05 skal være 14 siffer
8.04  * K000  101022 bsa  Legger på suffiks på ordrereferanse.
8.05  * K000  171123 bsa  Sjekker mot eksternkode for å sjekke om ordre-
8.05  * K000              referanse er relevant.
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *    50-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fraa8l1    if   e           k disk    rename(raa8pfr:raa8l1r)
     fra05l1    if   e           k disk    rename(ra05pfr:ra05l1r)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
     fra09lr    if   e           k disk    rename(ra09pfr:ra09lrr)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffotxl1    if   e           k disk    rename(fotxpfr:fotxl1r)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffodtlr    if   e           k disk    rename(fodtpfr:fodtlrr)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
8.02 ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     flwedpf    o    e           k disk
     flwelpf    o    e           k disk
      *
      *  Kommunikasjons-linje til IBM
      *
     d                 ds
     d d_ed99                        80
     d  d_fmsr99                      5    overlay(d_ed99)     inz('FMS01')
     d  d_avsn99                      8    overlay(d_ed99:6)
     d  d_skil99                      4    overlay(d_ed99:14)  inz('    ')
     d  d_mott99                      8    overlay(d_ed99:18)  inz('NOIBMDIN')
      *
      *  Definering av datastrukturer for EDI-format
      *
     d                 ds
     d d_ed00                        80
     d  d_id00                        4    overlay(d_ed00)     inz('ED00')
     d  d_decs00                      1    overlay(d_ed00:5)   inz(',')
     d  d_send00                     13  0 overlay(d_ed00:6)
8.01 d**d_mott00                     13  0 overlay(d_ed00:19)
8.01 d  d_pref00                      4    overlay(d_ed00:19)
8.01 d  d_mott00                     13    overlay(d_ed00:19)
     d  d_gdat00                      6    overlay(d_ed00:32)
     d  d_gtim00                      4    overlay(d_ed00:38)
     d  d_test00                      1    overlay(d_ed00:42)  inz('0')
     d* d_kva100                      3    overlay(d_ed00:43)  inz('137')
     d* d_mdat00                     12    overlay(d_ed00:46)
     d* d_for100                      3    overlay(d_ed00:58)  inz('203')
     d* d_kva200                      3    overlay(d_ed00:61)  inz('2  ')
     d* d_levd00                      8    overlay(d_ed00:64)
     d* d_for200                      3    overlay(d_ed00:72)  inz('102')
      *
     d                 ds
     d d_ed01                        80
     d  d_id01                        4    overlay(d_ed01)     inz('ED01')
     d  d_mref01                     14    overlay(d_ed01:5)
     d  d_mtyp01                      6    overlay(d_ed01:19)  inz('ORDRSP')
     d  d_mver01                      3    overlay(d_ed01:25)  inz('D  ')
     d  d_mrel01                      3    overlay(d_ed01:28)  inz('93A')
     d  d_mkon01                      2    overlay(d_ed01:31)  inz('UN')
     d  d_mfor01                      6    overlay(d_ed01:33)  inz('NEB01 ')
     d  d_mkod01                      3    overlay(d_ed01:39)  inz('231')
     d  d_benr01                     35    overlay(d_ed01:42)
     d* d_aksp01                      3    overlay(d_ed01:77)  inz('4  ')
     d  d_aksp01                      3    overlay(d_ed01:77)  inz('   ')
     d  d_resp01                      3    overlay(d_ed01:73)
      *
     d                 ds
     d d_ed02                        80
     d  d_id02                        4    overlay(d_ed02)     inz('ED02')
     d  d_kval02                      3    overlay(d_ed02:5)
     d  d_dato02                     12    overlay(d_ed02:8)
     d  d_dfor02                      3    overlay(d_ed02:20)  inz('102')
      *
     d                 ds
     d d_ed03                        80
     d  d_id03                        4    overlay(d_ed03)     inz('ED03')
     d  d_ftxq03                      3    overlay(d_ed03:5)   inz('SUR')
     d  d_ftxt03                     70    overlay(d_ed03:8)
      *
     d                 ds
     d d_ed04                        80
     d  d_id04                        4    overlay(d_ed04)     inz('ED04')
     d  d_refq04                      3    overlay(d_ed04:5)
     d  d_refn04                     70    overlay(d_ed04:8)
      *
     d                 ds
     d d_ed05                        80
     d  d_id05                        4    overlay(d_ed05)     inz('ED05')
     d  d_parq05                      3    overlay(d_ed05:5)
8.03 d  d_part05                     14  0 overlay(d_ed05:8)
8.03 d  d_part05a                    14    overlay(d_ed05:8)
8.03 d  d_pako05                      3    overlay(d_ed05:22)  inz('   ')
8.03 d  d_porg05                      3    overlay(d_ed05:25)  inz('9  ')
8.03 d  d_refq05                      3    overlay(d_ed05:28)  inz('VA ')
8.03 d  d_fore05                     16    overlay(d_ed05:31)
8.03 d  d_knfu05                      3    overlay(d_ed05:47)
8.03 d  d_kntp05                     30    overlay(d_ed05:50)
      *
     d                 ds
     d d_ed06                        80
     d  d_id06                        4    overlay(d_ed06)     inz('ED06')
     d  d_pnav06                     35    overlay(d_ed06:5)
     d  d_pad106                     35    overlay(d_ed06:40)
      *
     d                 ds
     d d_ed07                        80
     d  d_id07                        4    overlay(d_ed07)     inz('ED07')
     d  d_pad207                     35    overlay(d_ed07:5)
     d  d_ppnu07                      4    overlay(d_ed07:40)
     d  d_ppst07                     30    overlay(d_ed07:44)
     d  d_plkd07                      3    overlay(d_ed07:74)
      *
     d                 ds
     d d_ed08                        80
     d  d_id08                        4    overlay(d_ed08)     inz('ED08')
     d  d_valk08                      3    overlay(d_ed08:5)   inz('NOK')
     d  d_trko08                      3    overlay(d_ed08:8)
     d  d_fmko08                      3    overlay(d_ed08:11)
     d  d_lebq08                      3    overlay(d_ed08:14)  inz('6  ')
     d  d_levb08                      3    overlay(d_ed08:17)
     d  d_lbst08                     13    overlay(d_ed08:20)
     d  d_lsko08                      3    overlay(d_ed08:33)
      *
      * Foreløpig løages ikke 09 record
      *
     d                 ds
     d d_ed09                        80
     d  d_id09                        4    overlay(d_ed09)     inz('ED09')
     d  d_omkq09                      3    overlay(d_ed09:5)
     d  d_sekv09                      3    overlay(d_ed09:8)
     d  d_omkt09                      3    overlay(d_ed09:11)
     d  d_omkx09                     35    overlay(d_ed09:14)
     d  d_omkb09                     15  2 overlay(d_ed09:49)
     d  d_omks09                      5  2 overlay(d_ed09:64)
      *
     d                 ds
     d d_ed10                        80
     d  d_id10                        4    overlay(d_ed10)     inz('ED10')
     d  d_vlin10                      6  0 overlay(d_ed10:5)
     d  d_aksk10                      3    overlay(d_ed10:11)
     d  d_eanr10                     15    overlay(d_ed10:14)
     d  d_eank10                      3    overlay(d_ed10:29)  inz('EN ')
     d  d_piq110                      3    overlay(d_ed10:32)  inz('1  ')
     d  d_alt110                     15    overlay(d_ed10:35)
     d  d_alk110                      3    overlay(d_ed10:50)  inz('NO1')
     d  d_piq210                      3    overlay(d_ed10:53)  inz('1  ')
     d  d_alt210                     15    overlay(d_ed10:56)
     d  d_alk210                      3    overlay(d_ed10:71)  inz('SA ')
      *
     d                 ds
     d d_ed11                        80
     d  d_id11                        4    overlay(d_ed11)     inz('ED11')
     d  d_txk111                      3    overlay(d_ed11:5)   inz('F  ')
     d  d_txt111                     35    overlay(d_ed11:8)
     d  d_txt211                     35    overlay(d_ed11:43)
      *
     d                 ds
     d d_ed12                        80
     d  d_id12                        4    overlay(d_ed12)     inz('ED12')
     d  d_kvaq12                      3    overlay(d_ed12:5)   inz('21 ')
     d  d_kvah12                     11  0 overlay(d_ed12:8)
     d  d_kvas12                      1    overlay(d_ed12:19)  inz(',')
     d  d_kvad12                      3  3 overlay(d_ed12:20)
     d  d_benh12                      3    overlay(d_ed12:23)
      *
     d                 ds
     d d_ed13                        80
     d  d_id13                        4    overlay(d_ed13)     inz('ED13')
     d  d_datq13                      3    overlay(d_ed13:5)
     d  d_dato13                     10    overlay(d_ed13:8)
     d  d_datk13                      3    overlay(d_ed13:18)  inz('102')
      *
     d                 ds
     d d_ed14                        80
     d  d_id14                        4    overlay(d_ed14)     inz('ED14')
     d  d_lbeq14                      3    overlay(d_ed14:5)   inz('203')
     d  d_lbeh14                     11  0 overlay(d_ed14:8)
     d  d_lbde14                      1    overlay(d_ed14:19)  inz(',')
     d  d_lbed14                      3  3 overlay(d_ed14:20)
      *
     d                 ds
     d d_ed15                        80
     d  d_id15                        4    overlay(d_ed15)     inz('ED15')
     d  d_lftq15                      3    overlay(d_ed15:5)   inz('SUR')
     d  d_lftx15                     70    overlay(d_ed15:8)
      *
     d                 ds
     d d_ed16                        80
     d  d_id16                        4    overlay(d_ed16)     inz('ED16')
     d  d_priq16                      3    overlay(d_ed16:5)   inz('AAB')
     d  d_pris16                     11  0 overlay(d_ed16:8)
     d  d_prit16                      1    overlay(d_ed16:19)  inz(',')
     d  d_prid16                      3  3 overlay(d_ed16:20)
     d  d_prib16                      9    overlay(d_ed16:23)  inz('000000000')
     d  d_penh16                      3    overlay(d_ed16:32)
      *
     d                 ds
     d d_ed17                        80
     d  d_id17                        4    overlay(d_ed17)     inz('ED17')
     d  d_kreq17                      3    overlay(d_ed17:5)   inz('ON ')
     d  d_kbes17                     35    overlay(d_ed17:8)
     d  d_kbel17                      6    overlay(d_ed17:43)
      *
     d                 ds
     d d_ed18                        80
     d  d_id18                        4    overlay(d_ed18)     inz('ED18')
     d  d_taxq18                      3    overlay(d_ed18:5)   inz('7  ')
     d  d_taxt18                      3    overlay(d_ed18:8)   inz('VAT')
     d  d_taxs18                     17    overlay(d_ed18:11)
      *
     d                 ds
     d d_ed19                        80
     d  d_id19                        4    overlay(d_ed19)    inz('ED19')
     d  d_fraq19                      3    overlay(d_ed19:5)  inz('A  ')
     d  d_frat19                      3    overlay(d_ed19:8)  inz('PAR')
     d  d_frtx19                     35    overlay(d_ed19:11) inz('Rabatt')
     d  d_fras19                      3    overlay(d_ed19:46)
     d  d_frak19                     11    overlay(d_ed19:49) inz('00000000000')
     d  d_frsk19                      1    overlay(d_ed19:60) inz(',')
     d  d_frad19                      3    overlay(d_ed19:61) inz('000')
     d  d_fren19                      3    overlay(d_ed19:64)
      *
     d                 ds
     d d_ed20                        80
     d  d_id20                        4    overlay(d_ed20)    inz('ED20')
     d  d_fprh20                      5  0 overlay(d_ed20:5)
     d  d_fprs20                      1    overlay(d_ed20:10) inz(',')
     d  d_fprd20                      2  2 overlay(d_ed20:11)
     d  d_fbeh20                     15  0 overlay(d_ed20:13)
     d  d_fbes20                      1    overlay(d_ed20:28) inz(',')
     d  d_fbed20                      2  2 overlay(d_ed20:29)
     d  d_fsth20                     12  0 overlay(d_ed20:31)
     d  d_fsts20                      1    overlay(d_ed20:43) inz(',')
     d  d_fstd20                      2  2 overlay(d_ed20:44)
      *
     d                 ds
     d d_ed21                        80
     d  d_id21                        4    overlay(d_ed21)     inz('ED21')
     d  d_tbeq21                      3    overlay(d_ed21:5)
     d  d_tbeh21                     12  0 overlay(d_ed21:8)
     d  d_tbes21                      1    overlay(d_ed21:20)  inz(',')
     d  d_tbed21                      2  2 overlay(d_ed21:21)
      *
      * Datastruktur for mottakers lokasjonsnummer
      *
     d d_lknr          ds
     d  d_eana                       13
     d    d_eann                     13  0 overlay(d_eana:1)
     d  d_fill                        7
      *
      * Definering av LDA
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fotxl1_numm     s                   like(ftnumm)
     d fotxl1_suff     s                   like(ftsuff)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d fodtl1_line     s                   like(fdline)
     d fodtlr_numm     s                   like(fdnumm)
     d fodtlr_suff     s                   like(fdsuff)
     d rkunl1_kund     s                   like(rkkund)
     d vvarl1_vare     s                   like(vvvare)
     d ra09lr_ikod     s                   like(raikod)
     d vltyl1_ltyp     s                   like(valtyp)
     d ra05l1_ekod     s                   like(raekod)
8.02 d fkprl1_kund     s                   like(flkund)
8.02 d fkprl1_kpro     s                   like(flkpro)
      *
      * Definering av øvrige variable
      *
     d w_firm          s                   like(fofirm)
     d w_dalf          s              8
     d w_balf          s              6
     d w_salf          s              2
     d w_levb          s              3
     d w_tr01          s              1  0
     d w_refn          s             14    inz('1             ')
     d w_udat          s               d   datfmt(*iso)
     d w_utim          s               t   timfmt(*iso)
     d w_dato_8        s              8  0
     d w_time_6        s              6  0
     d w_dato_alf      s              8
     d w_time_alf      s              4
     d w_dat6_alf      s              6
     d w_tim4_alf      s              4
     d w_pibm          s              8
     d w_hean          s             13  0
     d w_fean          s             13  0
     d w_lean          s             13  0
     d w_sean          s             13  0
     d w_east          s              1
     d w_eann          s             14  0
     d w_lnvn          s             30
     d w_lgte          s             30
     d w_lad2          s             30
     d w_lpnr          s              4  0
     d w_lstd          s             30
     d w_flvn          s             30
     d w_flte          s             30
     d w_fld2          s             30
     d w_flnr          s              4  0
     d w_fltd          s             30
     d w_ftnr          s              9  0
     d w_iftn          s              9  0
     d w_pers          s             30
     d w_sums          s             11  2
8.04 d w_numm          s             15
      *
     d p_numm          s                   like(fonumm)
     d p_suff          s                   like(fosuff)
     d p_brto          s                   like(fotots)
     d p_raba          s                   like(fotots)
     d p_neto          s                   like(fotots)
     d p_mvag          s                   like(fotots)
     d p_mvab          s                   like(fotots)
     d p_avru          s                   like(fotots)
     d p_otot          s                   like(fotots)
      *
     d b_text          s              1    inz(*off)
     d b_lnut          s              1    inz(*off)
     d b_ed10          s              1    inz(*off)
     d b_logp          s              1    inz(*off)
     d b_dlev          s              1    inz(*off)
      *
     d digits          c                   ' 0123456789'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Uthenting av kode-, ordrehode-, leverandør- og lageropplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Hent faste data
      *
     c     raa8l1_key    chain     raa8l1
     c                   if        not %found
     c                   goto      end_pgm
     c                   endif
      *
     c                   eval      w_pibm = ra8ibm
     c                   eval      w_hean = ra8ean
     c                   eval      w_fean = 0
     c                   eval      w_lean = w_hean
      *
      * Hent ordrebekreftelse
      *
     c     fohel1_key    chain     fohel1r
     c                   if        not %found
     c                   goto      end_pgm
     c                   endif
     c                   eval      w_levb = %subst(folbtx:1:3)
      *
      * Hent kundeinformasjon
      *
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1
     c                   if        not %found
     c                   goto      end_pgm
     c                   endif
      * Kundeinfo
     c                   eval      w_sean = rkeank
     c                   eval      w_lnvn = rknavn
     c                   eval      w_lgte = rkgate
     c                   eval      w_lad2 = rkadr2
     c                   eval      w_lpnr = rkponr
     c                   eval      w_lstd = rksted
     c                   eval      w_ftnr = rkftnr
      * Fakturakunde
     c                   eval      w_flvn = rknavn
     c                   eval      w_flte = rkgate
     c                   eval      w_fld2 = rkadr2
     c                   eval      w_flnr = rkponr
     c                   eval      w_fltd = rksted
     c                   eval      w_iftn = rkftnr
     c                   eval      w_pers = fodref
      *
     c                   if        ra8tst <> *blank or
     c                             rkedik <> *blank
     c                   eval      d_test00 = '1'
     c                   endif
      * Sjekk fakturakunde
     c                   if        rkfaku <> 0
     c                   eval      rkunl1_kund = rkfaku
     c     rkunl1_key    chain     rkunl1
     c                   if        %found and
     c                             rkeank <> 0
     c                   eval      w_fean = rkeank
     c                   eval      w_flvn = rknavn
     c                   eval      w_flte = rkgate
     c                   eval      w_fld2 = rkadr2
     c                   eval      w_flnr = rkponr
     c                   eval      w_fltd = rksted
     c                   eval      w_iftn = rkftnr
     c                   eval      w_pers = rkpers
     c                   endif
     c                   endif
      *
      * Hent selgerinformasjon  (e-mail)
      *
     c                   eval      ra09lr_ikod = foselg
     c     ra09lr_key    chain     ra09lr
     c                   if        not %found
     c                   eval      raikod = 0
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Utlegging av kommunikasjonsinfo (IBM-FMS)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * ED99
      *
     c*                  if        lpmott = 'IBM            '
     c*                  eval      d_avsn99 = w_pibm
      *
     c*                  if        lptype = 'FTP      '
     c*                  eval      edilin = d_ed99
     c*                  write     lwedpfr
     c*                  eval      logrec = d_ed99
     c*                  write     lwelpfr
     c*                  endif
     c*                  endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Utlegging av hode-linjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * ED00  - Info til tjenestesegment
      *
     c                   eval      d_send00 = w_hean
      *
8.01 c*                  if        d_eana <> *blank and
8.01 c*                            %len(%trim(d_eana)) = 13 and
8.01 c*                            %check(digits : d_eana) = 0
8.01 c*                  eval      d_mott00 = d_eann
8.01 c*                  else
8.01 c*                  eval      d_mott00 = w_sean
8.01 c*                  endif
8.01  *
8.01  * Hvis det ligger orgnr isteden for GLN, legges nummer foran med EEEE
8.01  *
8.01 c                   eval      d_mott00 = *blanks
8.01 c                   move      w_sean        d_mott00
8.01 c                   if        w_sean < 1000000000
8.01 c                   eval      d_pref00 = 'EEEE'
8.01 c                   endif
8.01  *
     c                   eval      d_gdat00 = w_dat6_alf
     c                   eval      d_gtim00 = w_tim4_alf
     c*                  eval      d_mdat00 = w_dato_alf + w_time_alf
      *
     c*                  if        foldat <> *loval
     c*    *iso          move      foldat        w_dato_8
     c*                  move      w_dato_8      d_levd00
     c*                  else
     c*                  eval      d_levd00 = w_dato_alf
     c*                  endif
      *
     c                   eval      edilin = d_ed00
     c                   write     lwedpfr
     c                   eval      logrec = d_ed00
     c                   write     lwelpfr
      *
      * ED01 - Meldings start informasjon
      *
      * Siden vi ikke har mottatt bestilling elektronisk, flagg avvik
      *
     c                   eval      d_aksp01 = '4  '
      *
     c                   eval      d_mref01 = w_refn
8.04 c                   if        fosuff > 0
8.04 c                   eval      w_numm = *blanks
8.04 c                   eval      w_numm = %trim(%char(fonumm) + '-' +
8.04 c                             %char(fosuff))
8.04 c                   movel     w_numm        d_benr01
8.04 c                   else
     c                   movel     fonumm        d_benr01
8.04 c                   endif
     c                   eval      edilin = d_ed01
     c                   write     lwedpfr
     c                   eval      logrec = d_ed01
     c                   write     lwelpfr
      *
      * ED02 - Datoinformasjon
      *
     c                   move      w_dato_alf    d_dato02
     c                   eval      d_kval02 = '137'
     c                   eval      edilin = d_ed02
     c                   write     lwedpfr
     c                   eval      logrec = d_ed02
     c                   write     lwelpfr
      *
     c                   if        foldat <> *loval
     c     *iso          move      foldat        w_dato_8
     c                   move      w_dato_8      d_dato02
     c                   eval      d_kval02 = '69 '
     c                   eval      edilin = d_ed02
     c                   write     lwedpfr
     c                   eval      logrec = d_ed02
     c                   write     lwelpfr
     c                   endif
      *
      * ED03 - Fritekst på hodenivå
      *
     c                   eval      fotxl1_numm = fonumm
     c                   eval      fotxl1_suff = fosuff
     c     fotxl1_key    setll     fotxl1
     c     fotxl1_key    reade     fotxl1
      *
     c                   dow       not %eof
     c                   eval      d_ftxt03 = fttext
     c                   eval      edilin = d_ed03
     c                   write     lwedpfr
     c                   eval      logrec = d_ed03
     c                   write     lwelpfr
      *
     c     fotxl1_key    reade     fotxl1
     c                   enddo
      *
      * ED04 - Referanse fra bestilling og evt. kundeordre
      *
     c                   eval      d_refn04 = *blanks
8.05 c                   if        fofsys <> 'AOR' and
8.05 c                             fonref <> *blanks
     c                   eval      d_refq04 = 'ON '
     c                   movel     fonref        d_refn04
     c                   eval      edilin = d_ed04
     c                   write     lwedpfr
     c                   eval      logrec = d_ed04
     c                   write     lwelpfr
     c                   endif
8.01  * Legger ut rekvisisjonsnummer
8.01 c                   eval      d_refn04 = *blanks
8.01 c                   if        forekv <> *blanks
8.01 c                   eval      d_refq04 = 'CR '
8.01 c                   movel     forekv        d_refn04
8.01 c                   eval      edilin = d_ed04
8.01 c                   write     lwedpfr
8.01 c                   eval      logrec = d_ed04
8.01 c                   write     lwelpfr
8.01 c                   endif
8.02  * Legger ut eksternt prosjektnummer
8.02 c                   if        fokpro <> 0
8.02 c                   if        folkun <> 0 and
8.02 c                             folkun <> fokund
8.02 c                   eval      fkprl1_kund = folkun
8.02 c                   else
8.02 c                   eval      fkprl1_kund = fokund
8.02 c                   endif
8.02 c                   eval      fkprl1_kpro = fokpro
8.02 c     fkprl1_key    chain     fkprl1
8.02 c                   if        %found
8.02 c                   eval      d_refn04 = *blanks
8.02 c                   if        flepro <> *blanks
8.02 c                   eval      d_refq04 = 'AEP'
8.02 c                   movel     flepro        d_refn04
8.02 c                   eval      edilin = d_ed04
8.02 c                   write     lwedpfr
8.02 c                   eval      logrec = d_ed04
8.02 c                   write     lwelpfr
8.02 c                   endif
8.02 c                   endif
8.02 c                   endif
      *
     c                   eval      d_refn04 = *blanks
     c                   eval      d_refq04 = 'VN '
8.04 c                   if        fosuff > 0
8.04 c                   eval      w_numm = *blanks
8.04 c                   eval      w_numm = %trim(%char(fonumm) + '-' +
8.04 c                             %char(fosuff))
8.04 c                   movel     w_numm        d_refn04
8.04 c                   else
     c                   movel     fonumm        d_refn04
8.04 c                   endif
     c                   eval      edilin = d_ed04
     c                   write     lwedpfr
     c                   eval      logrec = d_ed04
     c                   write     lwelpfr
      *
      * ED05 - ED07 - Partsidentifisering
      *
      * SU -selger
      *
     c                   eval      d_parq05 = 'SU '
     c                   eval      d_part05 = w_hean
     c                   eval      d_fore05 = %char(aafftn)
     c                   eval      d_kntp05 = fovref
     c                   eval      edilin = d_ed05
     c                   write     lwedpfr
     c                   eval      logrec = d_ed05
     c                   write     lwelpfr
      *
     c                   eval      d_pnav06 = aafnav
     c                   eval      d_pad106 = aafad1
     c                   eval      edilin = d_ed06
     c                   write     lwedpfr
     c                   eval      logrec = d_ed06
     c                   write     lwelpfr
      *
     c                   eval      d_pad207 = aafad2
     c                   eval      d_ppnu07 = %subst(aafste:1:4)
     c                   eval      d_ppst07 = %subst(aafste:6)
     c                   eval      d_plkd07 = 'NO '
     c                   eval      edilin = d_ed07
     c                   write     lwedpfr
     c                   eval      logrec = d_ed07
     c                   write     lwelpfr
      *
      * BY -kjøper
      *
     c                   eval      d_parq05 = 'BY '
     c                   eval      d_part05 = w_sean
     c                   eval      d_fore05 = %char(w_ftnr)
     c                   eval      d_kntp05 = fodref
     c                   eval      edilin = d_ed05
     c                   write     lwedpfr
     c                   eval      logrec = d_ed05
     c                   write     lwelpfr
      *
     c                   eval      d_pnav06 = w_lnvn
     c                   eval      d_pad106 = w_lgte
     c                   eval      edilin = d_ed06
     c                   write     lwedpfr
     c                   eval      logrec = d_ed06
     c                   write     lwelpfr
      *
     c                   eval      d_pad207 = w_lad2
     c                   eval      d_ppnu07 = %char(w_lpnr)
     c                   eval      d_ppst07 = w_lstd
     c                   eval      d_plkd07 = 'NO '
     c                   eval      edilin = d_ed07
     c                   write     lwedpfr
     c                   eval      logrec = d_ed07
     c                   write     lwelpfr
      *
      * IV -fakturammotager
      *
     c                   eval      d_parq05 = 'IV '
     c                   if        w_fean <> 0
     c                   eval      d_part05 = w_fean
     c                   else
     c                   eval      d_part05 = w_sean
     c                   endif
     c                   eval      d_fore05 = %char(w_iftn)
     c                   eval      d_kntp05 = w_pers
     c                   eval      edilin = d_ed05
     c                   write     lwedpfr
     c                   eval      logrec = d_ed05
     c                   write     lwelpfr
      *
     c                   eval      d_pnav06 = w_flvn
     c                   eval      d_pad106 = w_flte
     c                   eval      edilin = d_ed06
     c                   write     lwedpfr
     c                   eval      logrec = d_ed06
     c                   write     lwelpfr
      *
     c                   eval      d_pad207 = w_fld2
     c                   eval      d_ppnu07 = %char(w_flnr)
     c                   eval      d_ppst07 = w_fltd
     c                   eval      d_plkd07 = 'NO '
     c                   eval      edilin = d_ed07
     c                   write     lwedpfr
     c                   eval      logrec = d_ed07
     c                   write     lwelpfr
      *
      * DP - mottakeradresse. Legg ut adressen fra ordren
      *   (Kun hvis avvikende fra kundenavn og adresse)
      *
     c                   if        %trim(fonavn) <> %trim(w_lnvn) or
     c                             %trim(foadr1) <> %trim(w_lgte) or
     c                             %trim(foadr2) <> %trim(w_lad2) or
     c                             %trim(fosted) <> %trim(w_lstd)
     c                   eval      d_parq05 = 'DP '
     c                   eval      d_part05 = w_sean
     c                   eval      d_fore05 = %char(w_ftnr)
     c                   eval      d_kntp05 = fodref
     c                   eval      edilin = d_ed05
     c                   write     lwedpfr
     c                   eval      logrec = d_ed05
     c                   write     lwelpfr
      *
     c                   eval      d_pnav06 = fonavn
     c                   eval      d_pad106 = foadr1
     c                   eval      edilin = d_ed06
     c                   write     lwedpfr
     c                   eval      logrec = d_ed06
     c                   write     lwelpfr
      *
     c                   eval      d_pad207 = foadr2
     c                   eval      d_ppnu07 = %subst(fosted:1:4)
     c                   eval      d_ppst07 = %subst(fosted:6)
     c                   eval      d_plkd07 = 'NO '
     c                   eval      edilin = d_ed07
     c                   write     lwedpfr
     c                   eval      logrec = d_ed07
     c                   write     lwelpfr
     c                   endif
      *
      * ED08 - Transport og leveringsinformasjon
      *
     c                   move      folmat        w_tr01
     c                   movel     w_tr01        d_trko08
     c**                 movel     w_tr01        d_fmko08
      *
     c                   if        folmat = 4
     c                   eval      d_lebq08 = '4  '
     c                   endif
      *
     c                   if        w_levb = 'FCA' or
     c                             w_levb = 'FAS' or
     c                             w_levb = 'FOB' or
     c                             w_levb = 'CFR' or
     c                             w_levb = 'CIF' or
     c                             w_levb = 'CPT' or
     c                             w_levb = 'CIP' or
     c                             w_levb = 'DES' or
     c                             w_levb = 'DEQ' or
     c                             w_levb = 'DDU' or
     c                             w_levb = 'DDP' or
     c                             w_levb = 'EXW' or
     c                             w_levb = 'DAF'
     c                   eval      d_levb08 = w_levb
     c                   else
     c                   eval      d_levb08 = 'EXW'
     c                   endif
      *
     c                   eval      d_lbst08 = *blank
      *
     c                   eval      edilin = d_ed08
     c                   write     lwedpfr
     c                   eval      logrec = d_ed08
     c                   write     lwelpfr
      *
      * Foreløpig legger vi ikke ut segment ED09, da vi er usikre på hva som bør legges der.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Utlegging av detalj-linjer, tekstlinjer blir fritekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     fodtlr_key    setll     fodtlrr
     c     fodtlr_key    reade     fodtlrr
      *
     c                   dow       not %eof
      *
      * Intern linjetype?
      *
     c                   exsr      sjekk_type
     c                   if        b_lnut = *off
     c                   goto      neste_linje
     c                   endif
      *
      * ED15 - Fritekstlinje
      *
     c                   if        b_text = *on
     c                   if        fdtek1 <> *blank or
     c                             fdtek2 <> *blank
     c                   eval      d_lftx15 = fdtek1 + fdtek2
     c**                 if        b_ed10 = *on
     c                   eval      edilin = d_ed15
     c                   write     lwedpfr
     c                   eval      logrec = d_ed15
     c                   write     lwelpfr
     c                   eval      d_lftx15 = *blank
     c**                 endif
     c                   endif
     c                   goto      neste_linje
     c                   endif
      *
      * ED10 - Nummer identifikasjon
      *
     c                   eval      d_aksk10 = *blanks
     c                   eval      d_vlin10 = fdline
      *
      * Kode alle varelinjer som "nye".
      *
     c                   eval      d_aksk10 = '1  '
      *
     c                   exsr      finn_varenr
      *
     c                   eval      edilin = d_ed10
     c                   write     lwedpfr
     c                   eval      logrec = d_ed10
     c                   write     lwelpfr
     c                   eval      b_ed10 = *on
      *
      * ED11 - Varebeskrivelse
      *
     c                   eval      d_txt111 = fdtek1
     c                   eval      d_txt211 = fdtek2
     c                   eval      edilin = d_ed11
     c                   write     lwedpfr
     c                   eval      logrec = d_ed11
     c                   write     lwelpfr
      *
      * ED12 - Mengde/enhetsangivelse
      *
     c*                  eval      d_kvaq12 = '21 '
     c*                  eval      d_kvah12 = edanta
     c*                  eval      d_kvad12 = edanta - d_kvah12
     c*                  eval      d_benh12 = edenhe
     c*                  eval      edilin = d_ed12
     c*                  write     lwedpfr
     c*                  eval      logrec = d_ed12
     c*                  write     lwelpfr
      * Levert kvantum
     c                   eval      d_kvaq12 = '113'
     c                   eval      d_kvah12 = fdanta
     c                   eval      d_kvad12 = fdanta - d_kvah12
     c                   eval      d_benh12 = fdenh1
     c                   eval      edilin = d_ed12
     c                   write     lwedpfr
     c                   eval      logrec = d_ed12
     c                   write     lwelpfr
      *
      *  ED13 - Datokvalifikatorer - leveringsdatoer
      *
      *  Legg ut bekreftet leveringsdato fra ordren
      *
     c                   if        fdldat <> *loval
     c     *iso          move      fdldat        w_dato_8
     c                   move      w_dato_8      d_dato13
     c                   eval      d_datq13 = '69 '
     c                   eval      edilin = d_ed13
     c                   write     lwedpfr
     c                   eval      logrec = d_ed13
     c                   write     lwelpfr
     c                   else
     c     *iso          move      foldat        w_dato_8
     c                   move      w_dato_8      d_dato13
     c                   eval      d_datq13 = '69 '
     c                   eval      edilin = d_ed13
     c                   write     lwedpfr
     c                   eval      logrec = d_ed13
     c                   write     lwelpfr
     c                   endif
      *
      * ED14 - Varelinjebeløp  (netto)
      *
     c                   eval      w_sums = fdsums - fdsumr
     c                   eval      d_lbeh14 = w_sums
     c                   eval      d_lbed14 = w_sums - d_lbeh14
     c                   eval      edilin = d_ed14
     c                   write     lwedpfr
     c                   eval      logrec = d_ed14
     c                   write     lwelpfr
      *
      *  ED15 - Fritekst linje
      *
     c                   if        d_lftx15 <> *blank
     c                   eval      edilin = d_ed15
     c                   write     lwedpfr
     c                   eval      logrec = d_ed15
     c                   write     lwelpfr
     c                   eval      d_lftx15 = *blank
     c                   endif
      *
      * ED16 - Pris
      *
     c                   eval      d_priq16 = 'AAB'
     c                   if        foneto = 1
     c                   eval      d_priq16 = 'AAA'
     c                   endif
     c                   eval      d_pris16 = fdsapr
     c                   eval      d_pris16 = fdsapr
     c                   eval      d_prid16 = fdsapr - d_pris16
     c                   eval      d_penh16 = fdenh1
     c                   eval      edilin = d_ed16
     c                   write     lwedpfr
     c                   eval      logrec = d_ed16
     c                   write     lwelpfr
      *
      * ED17 - Kjøper referansenummer
      *
     c                   eval      d_kbes17 = fonref
     c                   eval      d_kbel17 = '000000'
     c                   eval      edilin = d_ed17
     c                   write     lwedpfr
     c                   eval      logrec = d_ed17
     c                   write     lwelpfr
      *
      * ED18 - Avgifter
      *
     c                   if        fdomva <> *blanks and
     c                             fomkod =  0
     c                   eval      ra05l1_ekod = fdomva
     c     ra05l1_key    chain     ra05l1
     c                   if        %found
     c                   movel     raepro        d_taxs18
     c                   else
     c                   movel     '0000'        d_taxs18
     c                   endif
     c                   else
     c                   movel     '0000'        d_taxs18
     c                   endif
      *
     c                   eval      edilin = d_ed18
     c                   write     lwedpfr
     c                   eval      logrec = d_ed18
     c                   write     lwelpfr
      *
      * ED19/ED20 - Rabatter
      * Rabatt 1
     c                   if        fdrab1 <> 0
     c                   eval      d_fras19 = '1  '
     c                   eval      edilin = d_ed19
     c                   write     lwedpfr
     c                   eval      logrec = d_ed19
     c                   write     lwelpfr
      *
     c                   eval      d_fprh20 = fdrab1
     c                   eval      d_fprd20 = fdrab1 - d_fprh20
     c                   eval      d_fbeh20 = fdrkr1
     c                   eval      d_fbed20 = fdrkr1 - d_fbeh20
     c                   eval      d_fsth20 = 0
     c                   eval      d_fstd20 = 0
     c                   eval      edilin = d_ed20
     c                   write     lwedpfr
     c                   eval      logrec = d_ed20
     c                   write     lwelpfr
     c                   endif
      * Rabatt2
     c                   if        fdrab2 <> 0
     c                   eval      d_fras19 = '2  '
     c                   eval      edilin = d_ed19
     c                   write     lwedpfr
     c                   eval      logrec = d_ed19
     c                   write     lwelpfr
      *
     c                   eval      d_fprh20 = fdrab2
     c                   eval      d_fprd20 = fdrab2 - d_fprh20
     c                   eval      d_fbeh20 = fdrkr2
     c                   eval      d_fbed20 = fdrkr2 - d_fbeh20
     c                   eval      d_fsth20 = 0
     c                   eval      d_fstd20 = 0
     c                   eval      edilin = d_ed20
     c                   write     lwedpfr
     c                   eval      logrec = d_ed20
     c                   write     lwelpfr
     c                   endif
      *
     c     neste_linje   tag
      *
     c     fodtlr_key    reade     fodtlrr
     c                   enddo
      *
      * ED21 - Totaler
      *
      * Kall program for beregning av ordretotaler
      *
     c                   eval      p_neto = 0
     c                   eval      p_brto = 0
     c                   eval      p_raba = 0
     c                   eval      p_mvag = 0
     c                   eval      p_mvab = 0
     c                   eval      p_avru = 0
     c                   eval      p_otot = 0
     c                   call      'FO702R'
     c                   parm                    w_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_brto
     c                   parm                    p_raba
     c                   parm                    p_neto
     c                   parm                    p_mvag
     c                   parm                    p_mvab
     c                   parm                    p_avru
     c                   parm                    p_otot
      * Bruttobeløp
     c                   eval      d_tbeq21 = '86 '
     c                   eval      d_tbeh21 = p_brto
     c                   eval      d_tbed21 = p_brto - d_tbeh21
     c                   eval      edilin = d_ed21
     c                   write     lwedpfr
     c                   eval      logrec = d_ed21
     c                   write     lwelpfr
      * Rabatt
     c                   eval      d_tbeq21 = '131'
     c                   eval      d_tbeh21 = p_raba
     c                   eval      d_tbed21 = p_raba - d_tbeh21
     c                   if        d_tbed21 <> 0
     c                   eval      edilin = d_ed21
     c                   write     lwedpfr
     c                   eval      logrec = d_ed21
     c                   write     lwelpfr
     c                   endif
      * Mva beløp
     c                   eval      d_tbeq21 = '124'
     c                   eval      d_tbeh21 = p_mvab
     c                   eval      d_tbed21 = p_mvab - d_tbeh21
     c                   eval      edilin = d_ed21
     c                   write     lwedpfr
     c                   eval      logrec = d_ed21
     c                   write     lwelpfr
      * Netto
     c                   eval      d_tbeq21 = '203'
     c                   eval      d_tbeh21 = p_otot
     c                   eval      d_tbed21 = p_otot - d_tbeh21
     c                   eval      edilin = d_ed21
     c                   write     lwedpfr
     c                   eval      logrec = d_ed21
     c                   write     lwelpfr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne ean-nummer og nobb-nummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_varenr   begsr
      *
     c                   eval      d_eanr10 = *blank
     c                   eval      d_alt110 = *blank
     c                   eval      d_alt210 = *blank
      *
      * Kaller program for henting av GTIN nr
      *
     c                   if        fdeann = 0
     c                   call      'VE713R'
     c                   parm                    w_firm
     c                   parm                    fdldor
     c                   parm                    fdvare
     c                   parm                    fdenh1
     c                   parm                    w_eann
     c                   parm                    w_east
     c                   if        w_east = '1' and
     c                             w_eann <> 0
     c                   movel     w_eann        d_eanr10
     c                   else
     c                   movel     fdeann        d_eanr10
     c                   endif
     c                   else
     c                   movel     fdeann        d_eanr10
     c                   endif
      * NOBB nummer
     c                   if        fdnobb = 0
     c                   eval      vvarl1_vare = fdvare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   movel     vvnobb        d_alt110
     c                   eval      d_alt210 = vvlvar
     c                   endif
     c                   else
     c                   movel     fdnobb        d_alt110
     c                   eval      d_alt210 = fdlvar
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å sjekke om fritekstlinje som skal sendes
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_type    begsr
      *
     c                   eval      b_text = *off
     c                   eval      b_lnut = *on
      *
      * Les linjetype
      *
     c                   eval      vltyl1_ltyp = fdltyp
     c     vltyl1_key    chain     vltyl1
     c                   if        %found and
     c                             valktx = 1
     c                   eval      b_text = *on
     c                   endif
      *
      * Ikke skrives ut?
      *
     c                   if        footyp = valo01 or
     c                             footyp = valo02 or
     c                             footyp = valo03 or
     c                             footyp = valo04
     c                   eval      b_lnut = *off
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    p_numm
     c                   parm                    p_suff
      *
      *  Key for file les FIRMA-OPPLYSNINGER
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for file les FASTE DATA
      *
     c     raa8l1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for file les LAGER-KODE
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for les av Ordrehode
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      *  Key for les av Ordretekst register
      *
     c     fotxl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fotxl1_numm
     c                   kfld                    fotxl1_suff
      *
      *  Key for file les Ordrelinje register
      *
     c     fodtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlr_numm
     c                   kfld                    fodtlr_suff
      *
      *  Key for file les Ordrelinje register
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
     c                   kfld                    fodtl1_line
      *
      *  Key for les av LEVERANDØR
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for les av vare-register
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les av innkjøper
      *
     c     ra09lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra09lr_ikod
      *
      * Key for oppslag på linjetype
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
      * Key for les av vare-register
      *
     c     ra05l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra05l1_ekod
8.02  *
8.02  *  Key for les av kundeprosjekt
8.02  *
8.02 c     fkprl1_key    klist
8.02 c                   kfld                    w_firm
8.02 c                   kfld                    fkprl1_kund
8.02 c                   kfld                    fkprl1_kpro
      *
      *  Legger inn verdier i nøklene
      *
     c                   eval      w_firm = l_firm
     c                   eval      fohel1_numm = p_numm
     c                   eval      fohel1_suff = p_suff
     c                   eval      fodtl1_numm = p_numm
     c                   eval      fodtl1_suff = p_suff
     c                   eval      fodtlr_numm = p_numm
     c                   eval      fodtlr_suff = p_suff
      *
      *  Hent opplysninger fra FIRMA og LAGER-KODE
      *
     c     lstsl1_key    chain     lstsl1r
      *
     c     afirl1_key    chain     afirl1r
      *
      *  Legger inn dagens dato
      *
     C                   time                    w_udat
     C                   time                    w_utim
      *
     c     *iso          move      w_udat        w_dato_8
     c     *hms          move      w_utim        w_time_6
     c                   move      w_dato_8      w_dato_alf
     c                   movel     w_time_6      w_time_alf
     c                   move      w_dato_8      w_dat6_alf
     c                   movel     w_time_6      w_tim4_alf
      *
      *  Bygger nøkkel for loginformasjon
      *
     c                   eval      logind = 'O'
     c                   eval      logtyp = 'OBKR'
     c                   eval      logdat = w_udat
     c                   eval      logtid = w_utim
      *
     c                   endsr
