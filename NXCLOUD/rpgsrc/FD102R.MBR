     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FD102R
      * Beskrivelse . . : Ordre-linje, spørring/registrering av varelinjer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       050502 HKO  Nytt program
      *       240203 HKO  Returnerer til frisøk etter å ha valg vare
      *       161003 HKO  Ekskluderer/inkluderer skaffe-varer
      *                   Beholder posisjonering i subfile etter forny
      *       251103 HKO  Viser ** UTGÅTT ** i vareteksten på utgåtte varer
      *       140504 HKO  Søk blir med over ved overgang til skafferegister
      *       020805 HKO  Endret les mot vare-enhet for å finne salgsenheter
      * 5.50  200307 KOB  Lagt inn pos. til frisøk hvis ingen treff
prg   * 5.70  290908 sst  Prisgruppe
6.11  * 6.10  201109 bof  Utvidet parameter til FD105R
6.12  * 6.10  090810 bof  Sjekke lagerbeh. sender til det.reg.bildet
      *                   Dette skal parameterstyres pr. nå hardkodes det
      *                   til 0, dvs. ingen sjekk.
6.20  * 6.20  021110 bof  Utvidet med utg.dato pr. lager
6.21  * 6.20  091110 bof  Utvidet med status for C-sentrallagerv. P=periodev.
6.22  * 6.20  051210 bof  Hvis sentral-lagervare, settes farge lik rød
6.23  * 6.20  150211 bof  Sjekker om beh.på lager ihht. til kode på status
6.24  * 6.20  150211 bof  Sentral-lagervare kode for styring av farge
6.25  * 6.20  250511 bof  Hvis nobb utg.dato på varen,så vises *UTG.NOBB*
6.26  * 6.20  220212 bof  Kontroll på at varen finne på vlagpf hvis lagersjekk
6.27  * 6.20  180412 bof  Korrigert tekster i forb. med utg.dato
6.28  * 6.20  270512 bsa  Feil ved bakover scroll, pga parameterliste til VL710R
6.28  *                   Rettet i 6.33
6.30  * 6.30  060114 bof  Vise tilgjengelig beholdning i spørring.
6.31  * 6.30  260114 bof  Hvis vare finnes på annet tilbud, sett pris rød
6.32  * 6.30  260314 bof  vedr.6.31,hvis kunde = kont.kund-styrt ikke behandle
6.32  * 6.30  270314 bof  Må også sjekke at varetype ikke er T eller D
6.33  * 6.30  020414 bkv  Endret på test av tilbud utgår
6.34  * 6.30  140414 bof  Justering på hvordan beholdning vises i bildet
6.NU  * R6.30 230514 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.35  * 6.30  060814 bsa  Programmet kan initieres med verdi, da skal
6.35  *                   det fungere som varesøk.
6.36  *       130115 bof  Fjernet RKU2PF, sydd inn i rkunpf.
6.37  *       180515 BKV  Endret LVAR pga økt fra 15 til 20 tegn
6.3q  *       260515 bof  Datoformat sql
6.38  * 6.30  040716 bof  Skal ikke vise std.varenr fra SHOP.
6.39  * 6.30  081216 bof  Justering på beregning av behold.nytt VL716R t
6.3a  * 6.30  120417 bof  Skal returnere fra program med F3.           t
6.3b  * 6.30  170918 bkv  Utvidet med trelast sortiment
      *
7.xx  * 7.00  220116 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.00  * 7.00  120616 bkv  VVTEK2 som hidden felt
7.01  * 7.00  191218 bkv  Posisjonerer seg på frisøk etter søk.
Q.01  * 7.00  080219 bsa  Quick wins - skrevet om til å bruke 27x132 størrelse
Q.02  * 7.00  020419 bsa  Quick wins - synker ordrelinje mot bestilling
7.02  * 9.01  260619 bsa  Kaller program for kalkulering av priser ved
7.02  *                   registrering av varelinjer.
7.03  *       151019 bkv  Utvidet til 132 bilde
7.04  *       030220 bkv  Støtte for Solve F8
7.05  * 7.05  230320 ask  Lagt inn fargestyring basert på kode i sortimentfeltet på VVARPF
7.05  *                   Dette er switch-styrt med koden kode "FARGE_SKAFF_SORTIMENT"
7.05  *                   Denne koden overstyrer fargestyring i 6.24
7.06  * K000  060720 bsa  Utvidet søk i bildet
7.07  * K000  170920 bsa  Hvis søket ikke er endret, hopp videre.
      *
7.fb  * K000  050517 bof  Utivdet parameter fd105R, erstatte varelinj.
7.f1  * K000  300320 bsa  Ny parameter som hindrer nye linjer hvis
7.f1  *                   forskuddsbetaling.
      *
8.01  * K000  270123 bsa  Hvis angitt leverandør, sjekk om leverandøren
8.01  * K000              er angitt på lager, ikke bare varen.
8.02  * K000  150223 sst  Åpne for registrering av Div.varer selv om Depositum
8.03  * K000  100323 sst  Endret vedr lov å registrere ved depositum betalt/ikke
8.04  * K000  050723 bsa  Ved F8 til Solve, kalles eget program for rekalk av
8.04  * K000              ordre (FO731). Hentet fra FD100R.
8.05  *PRG2   080622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
8.06  * K000  040324 bsa  Innført nytt progranm for beregning av disponibel beholdning.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvsopf    if   e           k disk
     fvvarlr    if   e           k disk    rename(vvarpfr:vvarlrr)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarl2    if   e           k disk    rename(vvarpfr:vvarl2r)
     fvvarl3    if   e           k disk    rename(vvarpfr:vvarl3r)
     fvvarl4    if   e           k disk    rename(vvarpfr:vvarl4r)
     fvvarl6    if   e           k disk    rename(vvarpfr:vvarl6r)
     fvvarl7    if   e           k disk    rename(vvarpfr:vvarl7r)
     fvvarl8    if   e           k disk    rename(vvarpfr:vvarl8r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
6.12 fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
6.12 fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
6.38 fbstsl1    if   e           k disk    rename(bstspfr:bstsl1r)
6.3b fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
7.06 frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
8.03 fsdepl1    if   e           k disk    rename(sdeppfr:sdepl1r)
      *
     ffd102d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_kode          s              1
     d p_firm          s                   like(fofirm)
     d p_numm          s                   like(fonumm)
     d p_suff          s                   like(fosuff)
     d p_line          s                   like(fdline)
3.31 d p_kost          s              1
6.35 d p_vare          s             35
7.f1 d p_depo          s              1
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
7.02 d l_filg                931    933
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d vvarlr_vare     s                   like(vvvare)
     d vvarl1_vare     s                   like(vvvare)
     d vvarl2_tek1     s                   like(vvtek1)
     d vvarl3_nobb     s                   like(vvnobb)
     d vvarl4_lvar     s                   like(vvlvar)
     d vvarl6_ogrp     s                   like(vvogrp)
     d vvarl7_ogrp     s                   like(vvogrp)
     d vvarl7_hgrp     s                   like(vvhgrp)
     d vvarl8_ogrp     s                   like(vvogrp)
     d vvarl8_hgrp     s                   like(vvhgrp)
     d vvarl8_ugrp     s                   like(vvugrp)
     d fusrl1_user     s                   like(fbuser)
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d vltpl1_lety     s                   like(vylety)
     d vvenl2_vare     s                   like(vevare)
     d vvenl2_saen     s                   like(vesaen)
6.12 d vlagl1_vare     s                   like(vlvare)
6.12 d vlagl1_lage     s                   like(vllage)
6.12 d votyl1_otyp     s                   like(vaotyp)
7.06 d rlevl1_levr     s                   like(rllevr)
8.03 d sdepl1_numm     s                   like(sknumm)
8.03 d sdepl1_kund     s                   like(skkund)
8.03 d sdepl1_tell     s                   like(sktell)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              2
     d b_feil          s              1    inz(*off)
     d b_open_sok      s              1    inz(*off)
     d b_vare          s              1    inz(*off)
     d b_enhe          s              1    inz(*off)
6.12 d b_lagr          s              1    inz(*off)
6.21 d b_sess          s              1    inz(*off)
6.21 d b_sent          s              1    inz(*off)
6.21 d b_inlr          s              1    inz(*off)
6.32 d b_kont          s              1    inz(*off)
6.3a d b_dirs          s              1    inz(*off)
7.06 d b_reset         s              1    inz(*off)
7.07 d b_regl          s              1    inz(*off)
8.01 d b_levr          s              1    inz(*off)
8.04 d b_solv          s              1    inz(*off)
      *
6.20 d w_ddat          s               d   datfmt(*iso)
6.20 d w_udat          s               d   datfmt(*iso)
6.20 d w_ldor          s                   like(vvldor)
6.3b d w_lv_nobb       s                   like(vvlnob)
6.3b d w_lv_modn       s                   like(vvlmod)
6.3b d w_lv_lldo       s                   like(vvlnle)
6.3b d w_lv_llva       s                   like(vvllva)
6.3b d w_undv          s              8  0
6.3b d w_logl          s             15
6.3b d w_jvqvnr        s                   like(vvvare)
6.33 d w_jvquno        s              8  0
6.30 d w_behl          s                   like(vlbehl)
6.30 d w_behl0         s              5  0
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
     d w_kode          s              1  0
     d w_ste1          s             35
     d w_ste2          s             35
     d w_ste3          s             35
     d w_ste4          s             35
     d w_ste5          s             35
     d w_posi          s              3  0
     d w_otxt          s             35
     d w_htxt          s             35
     d w_utxt          s             35
5.70 d w_prgr          s                   like(fdprgr)
6.11 d w_eann          s                   like(fdeann)
7.fb d w_erst          s              1  0
7.f1 d w_mld1          s             50
8.04 d w_enor          s              1
      *
     d w_btim          s               t   timfmt(*iso)
      *
     d s_stek          s             35
7.06 d s_levr          s                   like(rllevr)
      *
     d w_firm          s                   like(vvfirm)
     d w_numm          s                   like(fonumm)
     d w_suff          s                   like(fosuff)
     d w_line          s                   like(fdline)
      *
     d w_vare          s                   like(vvvare)
     d w_anta          s                   like(fdanta)
     d w_bupr          s                   like(fdsapr)
     d w_tipr          s                   like(fdsapr)
     d w_tots          s                   like(fdsums)
     d w_totr          s                   like(fdsumr)
     d w_ldat          s                   like(fdldat)
5.70 d w_vtyp          s                   like(vvvtyp)
6.12 d w_absj          s              1  0
6.31 d w_antt          s              6  0
6.31 d w_tvar          s                   like(vvvare)
6.31 d w_bdat          s                   like(fobdat)
6.31 d w_kund          s                   like(fokund)
7.03 d w_len           s              3  0
7.03 d w_teller        s              3  0
      *
     d s_ogrp          s                   like(vvogrp)
     d s_hgrp          s                   like(vvhgrp)
     d s_ugrp          s                   like(vvugrp)
7.05 d u_fsor          s              1    inz(*off)                            Fargekode fra sortim
7.05 d w_fso1          s             10
7.05 d w_fso2          s             10
7.00  *
7.00  * Definering av eksterne prg
7.00  * BKV
7.00   dcl-s co402_verdi1  char(1);
7.00   dcl-s co402_verdi2  char(2048);
7.00   DCL-PR CO402R EXTPGM('CO402R');
7.00     p_filgrp            char(3)     const;
7.00     p_firm              packed(3:0) const;
7.00     p_lager             packed(2:0) const;
7.00     p_nokkel            char(50)    const;
7.00     p_verdi1            char(1);
7.00     p_verdi2            char(2048);
7.00   END-PR;
7.02 d u_702           s              1    inz(*off)
8.06 d u_disp          s              1    inz(*off)                            Disponibel beholdnin
      *
      * Konstanter
      *
7.03 d c_sfil          s              2  0 inz(15)
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
6.3q C/End-Exec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2 Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
7.xx c*                  when      *in21
7.xx c*                  eval      *in22 = *on
7.xx c*                  goto      b2taga
     c                   endsl
      *
7.06 c                   if        b2null = 1
7.06 c                   eval      b2ogrp = 0
7.06 c                   eval      s_ogrp = 0
7.06 c                   eval      b2hgrp = 0
7.06 c                   eval      s_hgrp = 0
7.06 c                   eval      b2ugrp = 0
7.06 c                   eval      s_ugrp = 0
7.06 c                   eval      b2levr = 0
7.06 c                   eval      s_levr = 0
7.06 c                   eval      b2lnvn = *blanks
7.06 c                   eval      b2stek = *blank
7.06 c                   eval      s_stek = *blank
7.06 c                   eval      w_seqe = *blank
7.06 c                   eval      b_reset = *on
7.06 c                   eval      b2null = 0
7.06 c                   eval      b1tek1 = *blanks
7.06 c                   exsr      forny
7.06 c                   goto      b2tagb
7.06 c                   endif
7.06  *
7.06  * Behandling av leverandør
7.06  *
7.06 c                   if        b2levr <> s_levr
7.06  *
7.06 c                   eval      b2lnvn = *blanks
7.06 c                   if        b2levr <> 0
7.06 c                   eval      rlevl1_levr = b2levr
7.06 c     rlevl1_key    chain     rlevl1
7.06 c                   if        not %found
7.06 c                   eval      b2lnvn = 'Koden er ugyldig'
7.06 c                   goto      b2tagb
7.06 c                   else
7.06 c                   eval      s_levr = b2levr
7.06 c                   eval      b2lnvn = rlnavn
7.06 c                   exsr      forny
7.06 c                   goto      b2taga
7.06 c                   endif
7.06 c                   endif
7.06  *
7.06 c                   endif
      *
      * F2=Registrering av tekstlinjer
      *
     c                   if        *inkb = *on
     c                   eval      w_line = w_line + 10
     c                   call      'FD110R'
     c                   parm                    p_kode
     c                   parm                    w_firm
     c                   parm                    w_numm
     c                   parm                    w_suff
     c                   parm                    w_line
     c                   parm                    faalty
Q.02  *
Q.02 c                   if        p_kode = *on
Q.02  *
Q.02  * Kall program for synking med bestilling
Q.02  *
Q.02 c                   call      'QD100R'
Q.02 c                   parm                    w_firm
Q.02 c                   parm                    w_numm
Q.02 c                   parm                    w_suff
Q.02 c                   parm                    w_line
Q.02  *
Q.02 c                   endif
Q.02  *
     c                   goto      b2tagb
     c                   endif
      *
      * F8=Skaffevareregister (overgang til vareadministrasjon)
      *
     c                   if        *inkh = *on
     c                   call      'JV160R'
     c                   parm                    p_kode
     c                   parm                    w_firm
     c                   parm                    w_numm
     c                   parm                    w_suff
     c                   parm                    w_line
     c                   parm                    faalty
     c                   parm                    folety
     c                   parm                    s_ogrp
     c                   parm                    s_hgrp
     c                   parm                    s_ugrp
     c                   parm                    s_stek
7.f1 c                   parm                    p_depo
     c                   goto      avslutt
     c                   endif
      *
      * F9=Gjenta søk
      *
     c                   if        *inki = *on
     c                   eval      *in22 = *on
     c                   eval      b2ogrp = s_ogrp
     c                   eval      b2hgrp = s_hgrp
     c                   eval      b2ugrp = s_ugrp
     c                   eval      b2stek = s_stek
7.06 c                   eval      b2levr = s_levr
     c                   goto      b2tagb
     c                   endif
      *
      * F13=Varelinjene som registreres, skal alltid vises
      *
     c                   if        *inkm = *on
     c                   if        b_vare = *off
     c                   eval      b_vare = *on
     c                   else
     c                   eval      b_vare = *off
     c                   endif
     c                   goto      b2tagb
     c                   endif
      *
      * F14=Vis kunde-/kundeprosjekt-informasjon
      *
     c                   if        *inkn = *on
     c                   call      'FL510R'
     c                   parm                    w_firm
     c                   parm                    fokund
     c                   parm                    fokpro
     c                   goto      b2tagb
     c                   endif
      *
      * F15=Rabattmatrise for kunde/kundeprosjekt
      *
     c                   if        *inkp = *on
     c                   call      'FR510R'
     c                   parm                    w_firm
     c                   parm                    fokund
     c                   parm                    fokpro
     c                   goto      b2tagb
     c                   endif
      *
      * F16=Spesialpriser for kunde/kundeprosjekt
      *
     c                   if        *inkq = *on
     c                   call      'FP510R'
     c                   parm                    w_firm
     c                   parm                    fokund
     c                   parm                    fokpro
     c                   goto      b2tagb
     c                   endif
      *
      * F21=Bytt mellom å utelate og ta med skaffevarer
      *
     c                   if        *inkv = *on
      *
     c                   if        *in83 = *off
     c                   eval      *in83 = *on
     c                   else
     c                   eval      *in83 = *off
     c                   endif
      *
     c***                exsr      hent_total
     c                   exsr      forny
      *
     c                   goto      b2taga
      *
     c                   endif
      *
      * F22=Bytt mellom å vise salgspris eksl. mva og inkl. mva
      *
     c                   if        *inkw = *on
      *
     c                   if        *in84 = *off
     c                   eval      *in84 = *on
     c                   else
     c                   eval      *in84 = *off
     c                   endif
      *
     c                   exsr      hent_total
     c                   exsr      forny
      *
     c                   goto      b2taga
      *
     c                   endif
      *
      * F23=Ny posisjonering (Varenr/Nobbnr/Lev.varenr)
      *
     c                   if        *inkx = *on
      *
     c                   if        *in80 = *on
     c                   eval      *in80 = *off
     c                   eval      *in81 = *on
     c                   exsr      forny
     c                   goto      b2taga
     c                   endif
      *
     c                   if        *in81 = *on
     c                   eval      *in81 = *off
     c                   eval      *in82 = *on
     c                   exsr      forny
     c                   goto      b2taga
     c                   endif
      *
     c                   if        *in82 = *on
     c                   eval      *in82 = *off
     c                   eval      *in80 = *on
     c                   exsr      forny
     c                   goto      b2taga
     c                   endif
      *
     c                   endif
      *
      * Søking
      *
7.07 c                   eval      b_regl = *off
     c                   if        b2ogrp <> 0 or
     c                             b2hgrp <> 0 or
     c                             b2ugrp <> 0 or
7.06 c                             b2stek <> *blank or
7.06 c                             b2levr <> 0
     c                   exsr      sØk
7.07 c                   if        b_regl = *on
7.07 c                   goto      xs_subfile
7.07 c                   endif
     c                   goto      b2tagb
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2tek1 <> *blank or
     c                             b2vare <> 0 or
     c                             b2nobb <> 0 or
     c                             b2lvar <> *blank
     c                   exsr      posisjoner
     c                   goto      b2taga
     c                   endif
      *
      * Behandling av subfile
      *
7.07 c     xs_subfile    tag
     c                   exsr      subfile
7.f1 c                   if        b_dirs = *off or
7.f1 c                             *in33 = *on
6.3a c                   goto      b2taga
6.3a c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *lukker filer
6.30 c                   eval      b_inlr = *off
6.39 c                   call      'VL716R'
6.30 c                   parm                    w_firm
6.30 c                   parm                    vvvare
6.30 c                   parm                    folage
6.39 c                   parm                    b1enhe
6.30 c                   parm                    w_vtyp
6.30 c                   parm                    w_udat
6.30 c                   parm                    w_ldor
6.30 c                   parm                    w_behl
6.30 c                   parm                    b_inlr
      *
8.04  * Hvis Solve VA, rekalkuler ordre ved avslutning.
      *
8.04 c                   if        b_solv = *on
8.04 c                   eval      w_enor = ' '
8.04 c                   call      'FO731R'
8.04 c                   parm                    w_enor
8.04 c                   parm                    w_firm
8.04 c                   parm                    w_numm
8.04 c                   parm                    w_suff
8.04 c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
7.06  *
7.06  * Det er valgt nullstilling av filter. Må starte på nytt
7.06  *
7.06 c                   if        b_reset = *on
7.06 c                   eval      b1tek1 = *blanks
7.06 c                   eval      b_reset = *off
7.06 c                   endif
      *
     c                   select
     c                   when      w_seqe = 'O'
     c     vvarl6_key    setll     vvarl6
     c                   when      w_seqe = 'H'
     c     vvarl7_key    setll     vvarl7
     c                   when      w_seqe = 'U'
     c     vvarl8_key    setll     vvarl8
     c                   when      w_seqe = 'SO' or
     c                             w_seqe = 'SH' or
     c                             w_seqe = 'SU' or
     c                             w_seqe = 'S '
     c                   eval      b_open_sok = *on
     c                   when      w_seqe = 'V'
     c                   eval      vvarl1_vare = b1vare
     c     vvarl1_key    setll     vvarl1
     c                   when      w_seqe = 'N'
     c                   eval      vvarl3_nobb = b1nobb
     c     vvarl3_key    setll     vvarl3
     c                   when      w_seqe = 'L'
     c                   eval      vvarl4_lvar = b1lvar
     c     vvarl4_key    setll     vvarl4
     c                   other
     c                   eval      vvarl2_tek1 = b1tek1
     c     vvarl2_key    setll     vvarl2
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for søking i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sØk           begsr
      *
7.07  * Hvis samme søk, og ikke nullstilt, sjekk om det er registrert antall
      *
7.07 c                   if        b2ogrp = s_ogrp and
7.07 c                             b2hgrp = s_hgrp and
7.07 c                             b2ugrp = s_ugrp and
7.07 c                             b2stek = s_stek and
7.07 c                             b2levr = s_levr
7.07 c                   eval      b_regl = *on
7.07 c                   goto      end_sØk2
7.07 c                   endif
      *
      * Validering av varegruppe
      *
     c                   if        b2ogrp = 0 and
     c                             b2hgrp <> 0
     c                   eval      *in31 = *on
     c                   goto      end_sØk2
     c                   endif
      *
     c                   if        b2hgrp = 0 and
     c                             b2ugrp <> 0
     c                   eval      *in32 = *on
     c                   goto      end_sØk2
     c                   endif
      *
      * Tar vare på søk
      *
     c                   eval      s_ogrp = b2ogrp
     c                   eval      s_hgrp = b2hgrp
     c                   eval      s_ugrp = b2ugrp
     c                   eval      s_stek = b2stek
7.06 c                   eval      s_levr = b2levr
      *
      * Søk via søketekst
      *
     c                   if        b2stek <> *blank
      *
     c                   eval      b_open_sok = *on
      *
     c                   select
     c                   when      b2ogrp = 0 and
     c                             b2hgrp = 0 and
     c                             b2ugrp = 0
     c                   eval      w_seqe = 'S '
     c                   when      b2ogrp <> 0 and
     c                             b2hgrp = 0 and
     c                             b2ugrp = 0
     c                   eval      w_seqe = 'SO'
     c                   when      b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp = 0
     c                   eval      w_seqe = 'SH'
     c                   when      b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp <> 0
     c                   eval      w_seqe = 'SU'
     c                   endsl
      *
      * Utleder søketekster ut av søketekst
      *
     c                   call      'AS700R'
     c                   parm                    b2stek
     c                   parm                    w_ste1
     c                   parm                    w_ste2
     c                   parm                    w_ste3
     c                   parm                    w_ste4
     c                   parm                    w_ste5
      *
     c                   goto      end_sØk
      *
     c                   endif
      *
      * Søk på overgruppe
      *
     c                   if        b2ogrp <> 0 and
     c                             b2hgrp =  0 and
     c                             b2ugrp =  0
     c                   eval      w_seqe = 'O'
     c                   eval      vvarl6_ogrp = b2ogrp
     c     vvarl6_key    setll     vvarl6
     c                   goto      end_sØk
     c                   endif
      *
      * Søk på hovedgruppe
      *
     c                   if        b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp =  0
     c                   eval      w_seqe = 'H'
     c                   eval      vvarl7_ogrp = b2ogrp
     c                   eval      vvarl7_hgrp = b2hgrp
     c     vvarl7_key    setll     vvarl7
     c                   goto      end_sØk
     c                   endif
      *
      * Søk på undergruppe
      *
     c                   if        b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp <> 0
     c                   eval      w_seqe = 'U'
     c                   eval      vvarl8_ogrp = b2ogrp
     c                   eval      vvarl8_hgrp = b2hgrp
     c                   eval      vvarl8_ugrp = b2ugrp
     c     vvarl8_key    setll     vvarl8
     c                   goto      end_sØk
     c                   endif
7.06  *
7.06  * "Søk på leverandør"
7.06  *
7.06 c                   if        b2levr <> 0
7.06 c                   eval      w_seqe = *blank
7.06 c                   eval      vvarl2_tek1 = *blanks
7.06 c     vvarl2_key    setll     vvarl2
7.06 c                   goto      end_sØk
7.06 c                   endif
      *
     c     end_sØk       tag
      *
      * Blanker subfile, og fyller opp i.h.t. søk
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker søkefelt
      *
7.06 c                   if        b2null = 1
     c                   eval      b2ogrp = 0
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   eval      b2stek = *blank
7.06 c                   eval      b2levr = 0
7.06 c                   eval      b2lnvn = *blanks
7.06 c                   endif
      *
     c     end_sØk2      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Varetekst
      *
     c                   if        b2tek1 <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      vvarl2_tek1 = b2tek1
     c     vvarl2_key    setll     vvarl2
     c                   goto      end_pos
     c                   endif
      *
      * Varenummer
      *
     c                   if        b2vare <> 0
     c                   eval      w_seqe = 'V'
     c                   eval      *in80 = *on
     c                   eval      *in81 = *off
     c                   eval      *in82 = *off
     c                   movel     b2vare        vvarl1_vare
     c     vvarl1_key    setll     vvarl1
     c                   goto      end_pos
     c                   endif
      *
      * Nobbnummer
      *
     c                   if        b2nobb <> 0
     c                   eval      w_seqe = 'N'
     c                   eval      *in80 = *off
     c                   eval      *in81 = *on
     c                   eval      *in82 = *off
     c                   eval      vvarl3_nobb = b2nobb
     c     vvarl3_key    setll     vvarl3
     c                   goto      end_pos
     c                   endif
      *
      * Lev. varenummer
      *
     c                   if        b2lvar <> *blank
     c                   eval      w_seqe = 'L'
     c                   eval      *in80 = *off
     c                   eval      *in81 = *off
     c                   eval      *in82 = *on
     c                   eval      vvarl4_lvar = b2lvar
     c     vvarl4_key    setll     vvarl4
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2tek1 = *blank
     c                   eval      b2vare = 0
     c                   eval      b2nobb = 0
     c                   eval      b2lvar = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
7.f1 c                   eval      *in33 = *off
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Antall er utfylt, linje skal registreres
      *
     c                   if        b1anta <> 0
7.f1  * Hvis det ikke er tjenestevare, skal det ikke være mulig å registrere ny
7.f1  * vare.
7.f1 c
8.03 c**                 if        b1vtyp <> 'T'
8.03 c**                           and b1vtyp <> 'D'
8.03 c**                           and p_depo = *on
8.03 c                   if        sk100p = 1
8.03 c                             and skdepb = skdepo
8.03 c                   eval      w_mld1 = 'Depositum er betalt. Kan +
8.03 c                             ikke endre linje'
8.03 c**                 eval      w_mld1 = 'Depositum finnes, kun +
8.03 c**                                tjen./div. vare kan registr.'
7.f1 c                   call      'AA005R'
7.f1 c                   parm                    w_mld1
7.f1 c                   eval      *in33 = *on
7.f1 c                   eval      b1anta = 0
7.f1 c                   update    b1sfl
7.f1 c                   leavesr
7.f1 c                   endif
     c                   eval      *in22 = *on
     c                   exsr      registrer
     c                   move      '->'          b1tek1
     c                   eval      b1anta = 0
6.3b  * sjekk farge
6.3b c                   eval      w_jvqvnr = b1vare
6.3b c                   exsr      finn_logivar
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for registering av vare i ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     registrer     begsr
      *
     c                   eval      w_line = w_line + 10
     c                   eval      w_vare = b1vare
     c                   eval      w_anta = b1anta
      *
      * Sjekker om vare har flere salgsenheter
      *
     c                   eval      b_enhe = *off
      *
     c                   eval      vvenl2_vare = w_vare
     c                   eval      vvenl2_saen = 1
     c     vvenl2_key    setll     vvenl2
     c     vvenl2_key2   reade     vvenl2
     c                   if        not %eof
     c     vvenl2_key2   reade     vvenl2
     c                   if        not %eof
     c                   eval      b_enhe = *on
     c                   endif
     c                   endif
6.12  *
6.12  * Sjekker om antall overskrider lagerbeholdning
6.12  *
6.12 c                   if        w_absj > 0
6.12 c                   eval      b_lagr = *off
6.12 c                   if        faalag = 1 and
6.12 c                             b1vtyp = 'L' and
6.12 c                             vaoakk > 0
6.12 c                   eval      vlagl1_vare = b1vare
6.12 c                   eval      vlagl1_lage = folage
6.12 c     vlagl1_key    chain     vlagl1
6.26 c                   if        not %found(vlagl1)
6.26 c                   eval      b_lagr = *on
6.26 c                   else
6.26 c                   if        vlbehl < b1anta
6.12 c                   eval      b_lagr = *on
6.26 c                   endif
6.12 c                   endif
6.12 c                   endif
     c                   endif
      *
      * Viser registeringbildet dersom
      * - 'vis vare' er valgt
      * - valgt av bruker
      * - varen har flere enheter
      * - vare er skaffevare, diverse-vare eller tjeneste
      * - salgspris <= 0,02
      *
     c                   if        fbko05 <> 0 or
     c                             b_vare = *on or
     c                             b_enhe = *on or
6.12 c                             b_lagr = *on or
     c                             b1vtyp = 'S' or
     c                             b1vtyp = 'D' or
     c                             b1vtyp = 'T' or
     c                             b1sapr <= 0,02
      *
6.11 c                   eval      w_eann = 0
7.02  * Kall program for kalkulering av priser
7.02 c                   if        u_702 = *on
7.02 c                   call      'ZI110R'
7.02 c                   parm                    fokund
7.02 c                   parm                    w_numm
7.02 c                   parm                    folage
7.02 c                   parm                    foavde
7.02 c                   parm                    w_vare
7.02 c                   parm                    b1enhe
     c                   endif
      *
     c                   call      'FD105R'
     c                   parm                    p_kode
     c                   parm                    w_firm
     c                   parm                    w_numm
     c                   parm                    w_suff
     c                   parm                    w_line
     c                   parm                    faalty
     c                   parm                    folety
     c                   parm                    w_vare
     c                   parm                    w_anta
     c                   parm                    b1enhe
     c                   parm                    p_kost
6.11 c                   parm                    w_eann
7.fb c                   parm                    w_erst
      *
     c                   else
      *
     c                   eval      w_ldat = *loval
      *
     c                   call      'FD106R'
     c                   parm                    p_kode
     c                   parm                    w_firm
     c                   parm                    w_numm
     c                   parm                    w_suff
     c                   parm                    w_line
     c                   parm                    faalty
     c                   parm                    folety
     c                   parm                    w_vare
     c                   parm                    w_anta
     c                   parm                    b1enhe
     c                   parm                    w_ldat
      *
     c                   endif
Q.02  *
Q.02 c                   if        p_kode = *on
Q.02  *
Q.02  * Kall program for synking med bestilling
Q.02  *
Q.02 c                   call      'QD100R'
Q.02 c                   parm                    w_firm
Q.02 c                   parm                    w_numm
Q.02 c                   parm                    w_suff
Q.02 c                   parm                    w_line
Q.02  *
Q.02 c                   endif
Q.02  *
      * Henter total sum for ordre
      *
     c                   exsr      hent_total
      *
     c     end_reg       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
      * Initierer SQL for søk
      *
     c                   if        b_open_sok = *on
      *
     c                   select
     c                   when      w_seqe = 'SO'
     c/exec sql
     c+                  declare sok_o cursor for
     c+                  select vvsfir,
     c+                         vvsogr,
     c+                         vvstek,
     c+                         vvsvar
     c+                  from   vvsopf
     c+                  where  vvsfir = :w_firm and
     c+                         vvsogr = :b2ogrp and
     c+                         vvstek like :w_ste1
     c+                  order by vvsfir, vvsogr, vvstek
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_o
     c/end-exec
     c/exec sql
     c+                  open sok_o
     c/end-exec
      *
     c                   when      w_seqe = 'SH'
     c/exec sql
     c+                  declare sok_h cursor for
     c+                  select vvsfir,
     c+                         vvsogr,
     c+                         vvshgr,
     c+                         vvstek,
     c+                         vvsvar
     c+                  from   vvsopf
     c+                  where  vvsfir = :w_firm and
     c+                         vvsogr = :b2ogrp and
     c+                         vvshgr = :b2hgrp and
     c+                         vvstek like :w_ste1
     c+                  order by vvsfir, vvsogr, vvshgr, vvstek
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_h
     c/end-exec
     c/exec sql
     c+                  open sok_h
     c/end-exec
      *
     c                   when      w_seqe = 'SU'
     c/exec sql
     c+                  declare sok_u cursor for
     c+                  select vvsfir,
     c+                         vvsogr,
     c+                         vvshgr,
     c+                         vvsugr,
     c+                         vvstek,
     c+                         vvsvar
     c+                  from   vvsopf
     c+                  where  vvsfir = :w_firm and
     c+                         vvsogr = :b2ogrp and
     c+                         vvshgr = :b2hgrp and
     c+                         vvsugr = :b2ugrp and
     c+                         vvstek like :w_ste1
     c+                  order by vvsfir, vvsogr, vvshgr, vvsugr, vvstek
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_u
     c/end-exec
     c/exec sql
     c+                  open sok_u
     c/end-exec
      *
     c                   when      w_seqe = 'S '
     c/exec sql
     c+                  declare sok cursor for
     c+                  select vvsfir,
     c+                         vvstek,
     c+                         vvsvar
     c+                  from   vvsopf
     c+                  where  vvsfir = :w_firm and
     c+                         vvstek like :w_ste1
     c+                  order by vvsfir, vvstek
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok
     c/end-exec
     c/exec sql
     c+                  open sok
     c/end-exec
      *
     c                   endsl
      *
     c                   endif
      *
      * Fyller opp subfile
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'O'
     c     vvarl6_key    reade     vvarl6                                 15
     c                   when      w_seqe = 'H'
     c     vvarl7_key    reade     vvarl7                                 15
     c                   when      w_seqe = 'U'
     c     vvarl8_key    reade     vvarl8                                 15
     c                   when      w_seqe = 'SO'
     c/exec sql
     c+                  fetch next
     c+                  from  sok_o
     c+                  into  :vvsfir,
     c+                        :vvsogr,
     c+                        :vvstek,
     c+                        :vvsvar
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'SH'
     c/exec sql
     c+                  fetch next
     c+                  from  sok_h
     c+                  into  :vvsfir,
     c+                        :vvsogr,
     c+                        :vvshgr,
     c+                        :vvstek,
     c+                        :vvsvar
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'SU'
     c/exec sql
     c+                  fetch next
     c+                  from  sok_u
     c+                  into  :vvsfir,
     c+                        :vvsogr,
     c+                        :vvshgr,
     c+                        :vvsugr,
     c+                        :vvstek,
     c+                        :vvsvar
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'S '
     c/exec sql
     c+                  fetch next
     c+                  from  sok
     c+                  into  :vvsfir,
     c+                        :vvstek,
     c+                        :vvsvar
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'V'
     c                   read      vvarl1                                 15
     c                   when      w_seqe = 'N'
     c                   read      vvarl3                                 15
     c                   when      w_seqe = 'L'
     c                   read      vvarl4                                 15
     c                   other
     c                   read      vvarl2                                 15
     c                   endsl
      *
     c                   if        *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        w_seqe = 'SO' or
     c                             w_seqe = 'SH' or
     c                             w_seqe = 'SU' or
     c                             w_seqe = 'S '
      *
     c                   if        w_ste2 <> *blank
      *
     c                   eval      w_posi = %scan(%trim(w_ste2):%trim(vvstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
      *
     c                   if        w_ste3 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste3):%trim(vvstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste4 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste4):%trim(vvstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste5 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste5):%trim(vvstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c                   eval      vvarlr_vare = vvsvar
     c     vvarlr_key    chain     vvarlr                             91
     c                   if        *in91 = *on
     c                   iter
     c                   endif
      *
     c                   endif
      *
     c                   if        vvfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *finner varetype
6.39  *
6.39 c                   call      'VL710R'
6.39 c                   parm                    w_firm
6.39 c                   parm                    vvvare
6.39 c                   parm                    folage
6.39 c                   parm                    w_vtyp
6.39 c                   parm                    w_udat
6.39 c                   parm                    w_ldor
6.39 c                   parm                    b_inlr
6.3b c                   parm                    w_lv_nobb
6.3b c                   parm                    w_lv_modn
6.3b c                   parm                    w_lv_lldo
6.3b c                   parm                    w_lv_llva
      *
5.70 c                   if        *in83 = *off  and
5.70 c                             w_vtyp = 'S'
     c                   iter
5.70 c                   endif
6.38  *
6.38  * ikke lov å bruke std. varenr fra shop
6.38  *
6.38 c                   if         vvvare = baavar or
6.38 c                              vvvare = baagva or
6.38 c                              vvvare = baadva
6.38 c                   iter
6.38 c                   endif
7.06  *
7.06  * Sjekk mot angitt leverandør.                                   å de
7.06  *
8.01 c                   eval      b_levr = *off
8.01 c                   if        b2levr <> 0
8.01 c*                            b2levr <> vvldor
8.01 c*                  iter
8.01  *
8.01 c                   if        folage <> 0
8.01 c                   eval      vlagl1_vare = vvvare
8.01 c                   eval      vlagl1_lage = folage
8.01 c     vlagl1_key    chain     vlagl1
8.01 c                   if        %found and
8.01 c                             vlbldo = b2levr
8.01 c                   eval      b_levr = *on
8.01 c                   endif
8.01 c                   endif
8.01 c                   if        vvldor = b2levr
8.01 c                   eval      b_levr = *on
8.01 c                   endif
8.01  *
8.01 c                   if        b_levr = *off
8.01 c                   iter
8.01 c                   endif
8.01  *
7.06 c                   endif
      *
6.31  * sjekker om det finne tilbudsordre på denne varen.
6.31  *
6.32 c                   eval      *in51 = *off
6.32 c                   if        b_kont = *off and
6.32 c                             w_vtyp <> 'T' and
6.32 c                             w_vtyp <> 'D'
6.31 c                   exsr      sjekk_tilb
6.31 c                   if        w_antt <> 0
6.31 c                   eval      *in51 = *on
6.31 c                   else
6.31 c                   eval      *in51 = *off
6.31 c                   endif
6.32 c                   endif
      * Skriver til subfile
      *
     c                   eval      w_srrn = w_srrn + 1
      *
6.21 c                   eval      b1sess = *blank
6.21 c                   eval      b1sent = *blank
6.21 c                   eval      b1stat = *blank
6.21 c                   exsr      sjekk_vsor
6.21 c                   if        b_sess = *on
6.21 c                   eval      b1sess = 'P'
6.21 c                   endif
6.21 c                   if        b_sent = *on
6.21 c                   eval      b1sent = 'C'
6.21 c                   endif
7.03 c*                   eval      b1tek1 = vvtek1
7.03 c                   eval      b1tek1 = %trim(vvtek1)+' : '+%trim(vvtek2)
     c                   eval      b1kpri = vvkpri
5.70 c                   eval      b1vtyp = w_vtyp
7.00 c                   eval      b1tek2 = vvtek2
     c                   eval      b1vare = vvvare
6.3b c                   if        w_lv_nobb > 0
6.3b c                   eval      b1nobb = w_lv_nobb
6.3b c                   eval      b1lvar = w_lv_llva
6.3b c                   else
     c                   eval      b1nobb = vvnobb
     c                   eval      b1lvar = vvlvar
     c                   endif
6.30  *
6.30 c                   if        %abs(w_behl) < 100000
6.30 c                   eval(h)   w_behl0 = w_behl
6.30 c                   else
6.30 c                   eval      w_behl0 = 99999
6.30 c                   endif
      *
6.21  * legger alle koder i et felt
6.21 c                   eval      %subst(b1stat:1:1) = b1sent
6.21 c                   eval      %subst(b1stat:2:1) = b1sess
6.21 c                   eval      %subst(b1stat:3:1) = b1kpri
6.21 c                   eval      %subst(b1stat:4:1) = b1vtyp
      *
6.22 c                   eval      *in50  = *off
6.20  *
6.25 c                   if        w_udat <> *loval
6.25 c                   if        w_udat < w_ddat
     c                   eval      b1tek1 = %subst(vvtek1:1:22) +
     c                                      ' ** UTGÅTT **'
6.25 c                   else
6.25 c                   eval      b1tek1 = %subst(vvtek1:1:22) +
6.25 c                                      ' ** UTGÅR  **'
6.25 c                   endif
6.27 c                   endif
6.25 c                   if        vvnuda <> *loval and
6.25 c                             vvnuda <= w_ddat
6.25 c                   eval      b1tek1 = %subst(vvtek1:1:22) +
6.25 c                                      ' **NOBB.UTG**'
6.25 c                   endif
6.27  *
7.05 c                   if        u_fsor = *off
6.22 c                   if        b_sent = *on
6.22 c                   eval      b1sent = 'C'
6.24 c                   if        faafsl = 1
6.22 c                   eval      *in50  = *on
6.24 c                   endif
6.22 c                   endif
7.05 c                   else
7.05 c                   eval      *in50  = *off
7.05 c                   eval      *in53  = *off
7.05 c                   if        vvsort = w_fso1
7.05 c                   eval      *in50 = *on
7.05 c                   endif
7.05 c                   if        vvsort = w_fso2
7.05 c                   eval      *in53 = *on
7.05 c                   endif
7.05 c                   endif
6.27  *
      *
     c                   call      'VP710R'
     c                   parm                    w_firm
     c                   parm                    vvvare
5,70 c                   parm                    w_prgr
     c                   parm                    folety
     c                   parm                    fobdat
     c                   parm                    w_btim
     c                   parm                    b1enhe
     c                   parm                    b1sapr
     c                   parm                    w_bupr
     c                   parm                    w_tipr
8.06  *
8.06  * henter varetype og beholdning
8.06  *
6.39 c                   eval      b_inlr = *on
8.06 c                   if        u_disp = *off
8.06 c                   call      'VL719R'
8.06 c                   parm                    w_firm
8.06 c                   parm                    vvvare
8.06 c                   parm                    folage
8.06 c                   parm                    b1enhe
8.06 c                   parm                    w_vtyp
8.06 c                   parm                    w_udat
8.06 c                   parm                    w_ldor
8.06 c                   parm                    w_behl
8.06 c                   parm                    b_inlr
8.06  *
8.02 c                   else
6.39  *
6.39 c                   call      'VL716R'
6.39 c                   parm                    w_firm
6.39 c                   parm                    vvvare
6.39 c                   parm                    folage
6.39 c                   parm                    b1enhe
6.39 c                   parm                    w_vtyp
6.39 c                   parm                    w_udat
6.39 c                   parm                    w_ldor
6.39 c                   parm                    w_behl
6.39 c                   parm                    b_inlr
6.33  *
8.06 c                   endif
6.39  *
6.39 c                   if        %abs(w_behl) < 100000
6.39 c                   eval(h)   w_behl0 = w_behl
6.39 c                   else
6.39 c                   eval      w_behl0 = 99999
6.39 c                   endif
      *
     c                   if        w_bupr <> 0
     c                   eval      b1kpri = '*'
     c                   eval      b1sapr = w_bupr
     c                   endif
      *
     c                   if        w_tipr <> 0
     c                   if        w_bupr = 0 or
     c                             w_tipr <= w_bupr
     c                   eval      b1kpri = 'T'
     c                   eval      b1sapr = w_tipr
     c                   endif
     c                   endif
      *
6.34 c                   eval      b1nypo = *blank
     c                   select
     c                   when      *in80 = *on
6.34 c                   eval      b1nypo = vvvare
     c                   when      *in81 = *on
6.3b c*                   eval      b1nypo = %editc(vvnobb : 'Z')
6.3b c                   eval      b1nypo = %editc(b1nobb : 'Z')
     c                   when      *in82 = *on
6.37 c*                  eval      b1nypo = vvlvar
6.37 c                   movel     vvlvar        b1nypo
     c                   endsl
6.34 c                   eval      b1behl = w_behl0
      *
     c                   if        *in84 = *on
     c                   eval      b1sapr = b1sapr * w_mvat
     c                   eval      w_kode = 4
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    b1vare
     c                   parm                    w_kode
     c                   parm                    b1sapr
     c                   endif
      *
6.3b  * sjekk farge
6.3b c                   eval      w_jvqvnr = vvvare
6.3b c                   exsr      finn_logivar
      *
7.03 c                   exsr      newlo_vask
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   eval      b_open_sok = *off
      *
5.50 c                   if        w_srrn = 0
5.50 c                   eval      *in22 = *on
5.50 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        w_seqe = 'O'  or
     c                             w_seqe = 'H'  or
     c                             w_seqe = 'U'  or
     c                             w_seqe = 'SO' or
     c                             w_seqe = 'SH' or
     c                             w_seqe = 'SU' or
     c                             w_seqe = 'S '
     c                   exsr      forny
     c                   goto      end_bck
     c                   endif
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'V'
     c     vvarl1_key    setgt     vvarl1
     c                   readp     vvarl1
     c                   when      w_seqe = 'N'
     c     vvarl3_key    setgt     vvarl3
     c                   readp     vvarl3
     c                   when      w_seqe = 'L'
     c     vvarl4_key    setgt     vvarl4
     c                   readp     vvarl4
     c                   other
     c     vvarl2_key    setgt     vvarl2
     c                   readp     vvarl2
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'V'
     c                   readp     vvarl1
     c                   when      w_seqe = 'N'
     c                   readp     vvarl3
     c                   when      w_seqe = 'L'
     c                   readp     vvarl4
     c                   other
     c                   readp     vvarl2
     c                   endsl
      *
     c                   if        %eof or
     c                             vvfirm <> w_firm
     c                   leave
     c                   endif
      * finner varetype
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    folage
5.70 c                   parm                    w_vtyp
6.33 c                   parm                    w_udat
6.33 c                   parm                    w_ldor
6.33 c                   parm                    b_inlr
6.3b c                   parm                    w_lv_nobb
6.3b c                   parm                    w_lv_modn
6.3b c                   parm                    w_lv_lldo
6.3b c                   parm                    w_lv_llva
      *
     c                   if        *in83 = *off  and
5.70 c                             w_vtyp = 'S'
     c                   iter
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'V'
     c                   eval      vvarl1_vare = vvvare
     c                   when      w_seqe = 'N'
     c                   eval      vvarl3_nobb = vvnobb
     c                   when      w_seqe = 'L'
     c                   eval      vvarl4_lvar = vvlvar
     c                   other
     c                   eval      vvarl2_tek1 = vvtek1
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'V'
     c     vvarl1_key    setll     vvarl1
     c                   when      w_seqe = 'N'
     c     vvarl3_key    setll     vvarl3
     c                   when      w_seqe = 'L'
     c     vvarl4_key    setll     vvarl4
     c                   other
     c     vvarl2_key    setll     vvarl2
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_bck       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Overgruppe
      *
     c                   if        w_afld = 'B2OGRP'
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    w_otxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_afld = 'B2HGRP'
     c                   eval      b2ugrp = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    w_htxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_afld = 'B2UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    b2ugrp
     c                   parm                    w_utxt
     c                   goto      end_spØrr
     c                   endif
7.06  *
7.06  * Leverandør
7.06  *
7.06 c                   if        w_afld = 'B2LEVR'
7.06 c                   call      'RL500R'
7.06 c                   parm                    w_firm
7.06 c                   parm                    b2levr
7.06 c                   parm                    b2lnvn
7.06 c                   goto      end_spØrr
7.06 c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av ordretotal
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_total    begsr
      *
     c                   eval      w_tots = 0
     c                   eval      w_totr = 0
      *
     c     fodtl1_key    setll     fodtl1
     c     fodtl1_key    reade     fodtl1
     c                   dow       not %eof
     c                   eval      w_tots = w_tots + fdsums
     c                   eval      w_totr = w_totr + fdsumr
     c     fodtl1_key    reade     fodtl1
     c                   enddo
      *
     c                   eval      b2tots = w_tots - w_totr
      *
     c                   endsr
6.21  *
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  * Subrutine for å sjekke om varen er i sortiment for C - sentrallagervarer
6.21  *                           eller i et sortiment for P - periodestyrt varer
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  *
6.21 c     sjekk_vsor    begsr
6.21  *
6.21 c                   eval      b_sent = *off
6.21 c                   eval      b_sess = *off
6.21  * sjekker sentral-lager-vare
6.21  *
6.21 c                   call      'VD771R'
6.21 C                   parm                    w_firm
6.21 C                   parm                    folage
6.21 C                   parm                    vvvare
6.21 C                   parm                    b_sent
6.21 C                   parm                    b_inlr
6.21  * sjekker periodestyrt
6.21  *
6.21 c                   call      'VD772R'
6.21 C                   parm                    w_firm
6.21 C                   parm                    folage
6.21 C                   parm                    vvvare
6.21 C                   parm                    b_sess
6.21 C                   parm                    b_inlr
6.21  *
6.21 c                   endsr
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  * Subrutine for sjekke om det finnes ordre med tilbud på denne vare/kund
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *
6.31 c     sjekk_tilb    begsr
6.31  *
6.31 c                   eval      w_tvar = vvvare
     c                   time                    w_bdat
     c     w_bdat        subdur    6:*months     w_bdat
6.31 c                   eval      w_kund = fokund
6.31  *
6.31 c/exec sql
6.31 c+                  declare tilb cursor for
6.31 c+                  select count(*) as w_antt
6.31 c+                  from   fodtpf
6.31 c+                  where  fdfirm = :w_firm and
6.31 c+                         fdvare = :w_tvar and
6.31 c+                         fdnumm <> :p_numm and
6.31 c+                         fdnumm in (select fonumm from fohepf
6.31 c+                         where fokund = :w_kund and
6.31 c+                               fobdat > :w_bdat and
6.31 c+                               ((fotdat = :w_ldat) or
6.31 c+                               (fotdat <> :w_ldat and
6.33 c+                               fotdat > :w_ddat))  and
6.31 c+                               footyp in (select vaotyp from votypf
6.31 c+                                      where vaoakk = 0) and
6.31 c+                               fonumm not in (select fpnumm from fpripf)))
6.31 c+                  for read only
6.31 c/end-exec
6.31  *
6.31 c/exec sql
6.31 c+ open tilb
6.31 c/end-exec
6.31  *
6.31 c/exec sql
6.31 c+ fetch tilb
6.31 c+ into  :w_antt
6.31 c/end-exec
6.31  *
6.31 c/exec sql
6.31 c+ close tilb
6.31 c/end-exec
6.31  *
6.31 c                   endsr
7.03  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.03  * Newlook vask av data
7.03  *  Fjerner tegn som gjør at Newlook feiltolker
7.03  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.03  *
7.03   begsr newlo_vask;
7.03     for w_teller = 1 to 5 ;
7.03       w_len = %len(%trim(b1tek1));
7.03       if (w_len > 0);
7.03       b1tek1 = %ScanRpl('.':'':b1tek1:w_len); // newlook tåler ikke . på slutten
7.03       endif;
7.03       w_len = %len(%trim(b1tek1));
7.03       if (w_len > 0);
7.03       b1tek1 = %ScanRpl(':':'':b1tek1:w_len); // newlook tåler ikke : på slutten
7.03       endif;
7.03     endfor ;
7.03   endsr;
6.3b  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.3b  * Finn info om logistikkvare for riktig prisgiver
6.3b  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.3b  *
6.3b c     finn_logivar  begsr
6.3b  *
6.3b c                   eval      *in51 = *off
6.3b c                   eval      *in52 = *off
6.3b c
6.3b c                   if        %trim(w_jvqvnr) = ''
6.3b c                   leavesr
6.3b c                   endif
6.3b c
6.3b c                   eval      w_logl = ''
6.3b c                   eval      w_undv = 0
     c                   movel     w_jvqvnr      w_jvquno
6.3b c/exec sql
6.3b c+                  select jvquno, jvqvnr
6.3b c+                  into :w_undv, :w_logl
6.3b c+                  from jvklpf
6.3b c+                  where (jvquno = :w_jvquno or
6.3b c+                  jvqvnr = :w_jvqvnr)
6.3b c+                  and jvqfir = :w_firm
6.3b c+                  fetch first 1 rows only
6.3b c/end-exec
6.3b c                   if        w_logl = w_jvqvnr
6.3b c                   eval      *in52 = *on
6.3b c                   elseif    w_undv > 0
6.3b c                   eval      *in51 = *on
6.3b c                   endif
6.3b c
6.3b c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_kode
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_line
3.31 c                   parm                    p_kost
6.35 c                   parm                    p_vare
7.f1 c                   parm                    p_depo
      *
      * Key for oppslag på vare
      *
     c     vvarlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlr_vare
      *
      * Key for posisjonering på varenummer
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for posisjonering på varetekst
      *
     c     vvarl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl2_tek1
      *
      * Key for posisjonering på nobb-nummer
      *
     c     vvarl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl3_nobb
      *
      * Key for posisjonering på leverandørs varenummer
      *
     c     vvarl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl4_lvar
      *
      * Key for søk på overgruppe
      *
     c     vvarl6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl6_ogrp
      *
      * Key for søk på overgruppe/hovedgruppe
      *
     c     vvarl7_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl7_ogrp
     c                   kfld                    vvarl7_hgrp
      *
      * Key for søk på overgruppe/hovedgruppe/undergruppe
      *
     c     vvarl8_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl8_ogrp
     c                   kfld                    vvarl8_hgrp
     c                   kfld                    vvarl8_ugrp
      *
      * Key for oppslag på status-register
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på bruker
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrl1_user
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag på leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
      *
      * Key for les på vare-enhet (salgsenheter)
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
     c                   kfld                    vvenl2_saen
      *
     c     vvenl2_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
      *
      * Key for oppslag på ordre-hode
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for les på ordre-linje
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
6.12  *
6.12  * Key for les på lager
6.12  *
6.12 c     vlagl1_key    klist
6.12 c                   kfld                    w_firm
6.12 c                   kfld                    vlagl1_vare
6.12 c                   kfld                    vlagl1_lage
6.12  *
6.12  * Key for ordretyperegister
6.12  *
6.12 c     votyl1_key    klist
6.12 c                   kfld                    w_firm
6.12 c                   kfld                    votyl1_otyp
6.38  *
6.38  * Key for oppslag på shop-koderegister
6.38  *
6.38 c     bstsl1_key    klist
6.38 c                   kfld                    w_firm
7.06  *
7.06  * Key for oppslag på leverandør
7.06  *
7.06 c     rlevl1_key    klist
7.06 c                   kfld                    w_firm
7.06 c                   kfld                    rlevl1_levr
8.03  *
8.03  * Key for oppslag på depositum-fil
8.03  *
8.03 c     sdepl1_key    klist
8.03 c                   kfld                    w_firm
8.03 c                   kfld                    sdepl1_numm
8.03 c                   kfld                    sdepl1_kund
8.03 c                   kfld                    sdepl1_tell
      *
      * Henter parametre
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_numm = p_numm
     c                   eval      w_suff = p_suff
     c                   eval      w_line = p_line
7.02  *
7.02  * Endring 7.02
7.02  *
7.02   CallP CO402R(l_filg:w_firm:0:'FD102R_702':
7.02                    co402_verdi1:co402_verdi2);
7.02   if co402_verdi1 = '1';
7.02     u_702 = *on;
7.02   endif;
7.04  *
7.04  * Endring 7.04
7.04  *
7.04   *in56 = *off;
7.04   CallP CO402R(l_filg:w_firm:0:'SOLVE_VA':
7.04                    co402_verdi1:co402_verdi2);
7.04   if co402_verdi1 = '1';
7.04     *in56 = *on;
7.04   endif;
7.05  *
7.05  * Test om fargekode fra sortimentskode
7.05  *
7.05   CallP CO402R(l_filg:w_firm:0:'FARGE_SKAFF_SORTIMENT':
7.05                    co402_verdi1:co402_verdi2);
7.05   if co402_verdi1 = '1';
7.05     u_fsor = *on;
7.05     w_fso1 = %subst(co402_verdi2:3:%scan('B=':co402_verdi2) - 3);
7.05     w_fso2 = %subst(co402_verdi2:%scan('B=':co402_verdi2) + 2:10);
7.05   endif;
      *
      * Henter dagens dato
      *
     c                   time                    w_ddat
      *
      * Hent opplysninger fra ofak koderegister
      *
     c     fstsl1_key    chain     fstsl1r                            90
6.12  *
6.23  * lager blir sjekket i hht. kode på status
6.23  * 0=ingen sjekk
6.23  * 1=sjekkes, men du kan gå videre
6.23  * 2=sjekkes, går ikke videre med neg. behold.
6.23  *
6.23 c                   eval      w_absj = faabeh
      *
      * Henter opplysninger fra ofak bruker-register
      *
     c                   eval      fusrl1_user = l_user
     c     fusrl1_key    chain     fusrl1                             90
      *
      * Hent opplysninger fra ordre-hode
      *
     c     fohel1_key    chain     fohel1r                            90
      *
6.20 c                   eval      votyl1_otyp = footyp
6.20 c     votyl1_key    chain     votyl1r
8.03  *
8.03  * Hent opplysninger vedr depositum
8.03  *
8.03 c                   eval      sdepl1_numm = fonumm
8.03 c                   eval      sdepl1_kund = fokund
8.03 c                   eval      sdepl1_tell = 0
8.03 c     sdepl1_key    chain     sdepl1
      *
     c                   eval      w_btim = *loval
7.fb c                   eval      w_erst = 0
      *
      * Finner moms-sats
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    fobdat
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
      *
      * Henter total sum for ordre
      *
     c                   exsr      hent_total
      *
      * Fyller opp skjermbildet for første visning
      *
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1                             90
     c                   eval      b2kund = fokund
     c                   eval      b2knav = rknavn
6.36  *
6.36  * sjekker om kunde har kontant-kunde satt på
6.36  *
6.36 c                   eval      b_kont = *off
6.36 c                   if        rkkoti = 1
6.36 c                   eval      b_kont = *on
6.36 c                   endif
      *
     c                   if        fokpro <> 0
     c                   eval      fkprl1_kund = fokund
     c                   eval      fkprl1_kpro = fokpro
     c     fkprl1_key    chain     fkprl1                             90
     c                   eval      b2kpro = fokpro
     c                   eval      b2pnav = flnavn
     c                   endif
      *
     c                   eval      vltpl1_lety = folety
     c     vltpl1_key    chain     vltpl1                             90
     c                   eval      b2ltxt = vybesk
6.38  *
6.38  * Les av status shop
6.38  *
6.38 c     bstsl1_key    chain     bstsl1
      *
     c                   eval      *in22 = *on
     c                   eval      *in80 = *on
      *
     c     vvarl2_key    setll     vvarl2
      *
5.70  * henter prisgruppe
5.70  *
5.70 c                   call      'VL712R  '
5.70 c                   parm                    w_firm
5.70 c                   parm                    folage
5.70 c                   parm                    foavde
5.70 c                   parm                    w_prgr
      *
6.35 c                   if        p_vare = *blanks
     c                   exsr      crt_subfile
6.35 c                   else
6.3a c                   eval      b_dirs = *on
6.35 c                   eval      b2stek = p_vare
6.35 c                   exsr      sØk
6.35 c                   endif
8.04  *
8.04  * Endring 8.04
8.04  *
8.04   CallP CO402R(l_filg:w_firm:0:'SOLVE_VA':
8.04                    co402_verdi1:co402_verdi2);
8.04   if co402_verdi1 = '1';
8.04     b_solv = *on;
8.04   endif;
8.06  *
8.06  * Test om fargekode fra sortimentskode
8.06  *
8.06   CallP CO402R(l_filg:w_firm:0:'DISPONIBEL_BEREGNING':
8.06                    co402_verdi1:co402_verdi2);
8.06   if co402_verdi1 = '1';
8.06     u_disp = *on;
8.06   endif;
      *
     c                   endsr
