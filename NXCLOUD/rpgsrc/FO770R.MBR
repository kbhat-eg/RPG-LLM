      /TITLE  ASOFAK Kunde, bytting av rabattkategori på kunder og ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO770R
      * Beskrivelse . . : Kunde, bytting av rabattkategori på kunder og
      *                   ordre
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       210699 HKO  Nytt program
6.10  * 6.10  240609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  070510 BSA  Endret i sporingsinformasjon
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra06pf    if   e           k disk    rename(ra06pfr:ra06pfr)
     frkunlu    uf   e           k disk    rename(rkunpfr:rkunlur)
     ffohelu    uf   e           k disk    rename(fohepfr:fohelur)
      *
     ffo770d    cf   e             workstn
      *
      * Defiering av Local data area
      *
     d                uds
6.10 d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d ra06pf_fkod     s                   like(rafkod)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(rkfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandling av skjermbilde
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
      * Oppdaterer kunderegister med ny rabattkategori
      *
     c     key01         setll     rkunlu
     c     key01         reade     rkunlur                                91
     c                   dow       *in91 = *off
     c                   if        rkrabk = b1rabg
     c                   eval      rkrabk = b1rabn
6.10 c                   time                    rkedat
6.10 c                   time                    rketim
6.10 c                   eval      rkesig = l_user
6.11 c                   time                    rk2dpt
6.11 c                   eval      rkepgm = 'FO770R    '
     c                   update    rkunlur
     c                   else
     c                   unlock    rkunlu
     c                   endif
     c     key01         reade     rkunlur                                91
     c                   enddo
      *
      * Oppdaterer ordre-hode med ny rabattkategori
      *
     c     key01         setll     fohelur
     c     key01         reade     fohelur                                91
     c                   dow       *in91 = *off
     c                   if        forkat = b1rabg
     c                   eval      forkat = b1rabn
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   update    fohelur
     c                   else
     c                   unlock    fohelu
     c                   endif
     c     key01         reade     fohelur                                91
     c                   enddo
      *
      * Blanker ny rabattkategori, og sender opp bildet på nytt
      *
     c                   eval      b1rabn = 0
      *
     c                   goto      b1taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
     c                   if        b1rabg = 0 or
     c                             b1rabn = 0
     c                   eval      *in31 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif                                                           .....
      *
     c                   eval      ra06pf_fkod = b1rabn
     c     ra06pf_key    chain     ra06pf                             32
     c                   if        *in32 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på rabattkategori
      *
     c     ra06pf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra06pf_fkod
      *
      * Key for les på vare tilhørende firma
      *
     c     key01         klist
     c                   kfld                    w_firm
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
