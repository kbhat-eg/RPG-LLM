      /TITLE  Spesial program - lageroppdatering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System  . . . . : ASLAGR
      *  Program . . . . : LX700R
      *  Beskrivelse . . : Oppdatering av informasjon på lagerregister
      *                    fra ekstern fil.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. : Excel-fil med lagerinfo
      *  Tilpasset for . : Zeiner
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      * --------- ------  --------------------------------------------------
      *           300800  Nytt program                             ASK
      *   6.10    260609  Oppdatert med sporingsinformasjon        BSA
6.20  *           310812  Nytt felt, endringsdato for faste data som vises
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flwexpf    if   e           k disk
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvlaglu    uf a e           k disk    rename(vlagpfr:vlaglur)
      *
      *  Datastruktur - excel-fil
      *
     d                 ds
     d d_ex                         512
     d  d_ean1                       13    overlay(d_ex)
     d  d_ean2                        8    overlay(d_ex:14)
     d  d_maxl                        7    overlay(d_ex:70)
     d  d_minl                        7    overlay(d_ex:77)
     d  d_bmen                        7    overlay(d_ex:87)
      *
      *  UDS Local Data Area
      *
     d                uds
6.10 d l_user                911    920
     d l_firm                944    946  0
      *
      *  Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vlaglu_lage     s                   like(vllage)
     d vlaglu_vare     s                   like(vlvare)
      *
      *  Definering av variable
      *
     d w_firm          s                   like(vvfirm)
     d w_lage          s                   like(vllage)
     d w_bpun          s              7  0
     d w_bmen          s              7  0
     d w_mini          s              7  0
     d w_maks          s              7  0
6.11 d w_eann          s             14  0
6.11 d w_eava          s                   like(vvvare)
6.11 d w_eaen          s                   like(vvenhe)
6.11 d w_east          s              1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Lese ekstern fil for å hente neste vare for behandling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      w_lage = 00
      *
      *  Lese file
      *
     c                   read      lwexpf                                 90
     c                   dow       *in90 = *off
     c                   eval      d_ex = exclin
      *
      *  Sjekk at ikke heading-rad er lest inn
      *
     c                   if        %subst(d_ean1:1:2) <> 'ID' and
     c                             %subst(d_ean1:1:3) <> '   '
     c                   exsr      behand
     c                   endif
      *
     c
      *
     c                   read      lwexpf                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppdatering av LAGER-informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behand        begsr
      *
      * Finn vare via ean-nummer
      *
     c                   if        %subst(d_ean1:1:1) <> '?'
6.11 c                   movel     d_ean1        w_eann
     c                   else
     c                   if        %subst(d_ean2:1:1) <> '?'
6.11 c                   movel     d_ean2        w_eann
     c                   else
     c                   goto      behand_ut
     c                   endif
     c                   endif
      *
6.11 c                   call      'VE710R  '
6.11 c                   parm                    w_firm
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_eava
6.11 c                   parm                    w_eaen
6.11 c                   parm                    w_east
6.11 c                   if        w_east = '1'
6.11 c                   eval      vvarl1_vare = w_eava
     c     vvarl1_key    chain     vvarl1r                            92
     c                   if        *in92 = *off
      *
     c                   eval      vlaglu_vare = vvvare
     c                   eval      vlaglu_lage = w_lage
     c     vlaglu_key    chain     vlaglur                            93
      *
     c                   eval      vlenhl = vvenhl
     c*                  eval      vlplas = d_loka
     c                   move      d_minl        w_mini
     c                   move      d_maxl        w_maks
     c                   move      d_minl        w_bpun
     c                   move      d_bmen        w_bmen
     c                   eval      vlmini = w_mini
     c                   eval      vlmaks = w_maks
     c                   eval      vlbpun = w_bpun
     c                   eval      vlbmen = w_bmen
      *
6.10 c                   time                    vledat
6.10 c                   time                    vletim
6.10 c                   eval      vleusr = l_user
6.20 c                   time                    vlfeda
6.20 c                   time                    vlfeti
6.20 c                   eval      vlfeus = l_user
      *
     c                   if        *in93 = *off
     c                   update    vlaglur
     c                   else
     c                   eval      vlfirm = w_firm
     c                   eval      vlvare = vvvare
     c                   eval      vllage = w_lage
6.10 c                   time                    vlodat
6.10 c                   time                    vlotim
6.10 c                   eval      vlousr = l_user
     c                   write     vlaglur
     c                   endif
      *
     c                   endif
     c                   endif
     c     behand_ut     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *  Key for oppdatering av LAGER
      *
     c     vlaglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglu_vare
     c                   kfld                    vlaglu_lage
      *
      *  Legge inn opplysninger i nøklene
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
