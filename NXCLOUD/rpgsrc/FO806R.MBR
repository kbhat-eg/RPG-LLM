      /TITLE  ASOFAK: Inntasting av parameter for ordre-utskrift
     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO806R
      * Beskrivelse . . : Inntasting av parameter for ordre-utskrift
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * 8.xx  020522 bsa  Nytt program
8.01  *K0000  251124 bsa  Må legge rutineregister ut i LDA.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvbldl1    if   e           k disk    rename(vbldpfr:vbldl1r)
     ffohelu    uf   e           k disk    rename(fohepfr:fohelur)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffplolu    uf a e           k disk    rename(fplopfr:fplolur)
     ffplol1    if   e           k disk    rename(fplopfr:fplol1r)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
      *
      * Definering av local data area
      *
8.01 dlda             uds
     d l_poff                584    584
     d l_ruti                800    809
     d l_wsid                901    910
     d l_user                911    920
     d l_filg                931    933
     d l_sfir                944    946  0
     d l_avde                947    950  0
      *
      * Definering av parametere
      *
     d p_firm          s                   like(fofirm)
     d p_numm          s                   like(fonumm)
     d p_suff          s                   like(fosuff)
     d p_ipor          s                   like(w_alf1)
     d p_ruti          s                   like(l_ruti)
     d p_pdf           s              1
      *
      * Definering av variable i nøkler
      *
     d w_firm          s                   like(fofirm)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fplol1_numm     s                   like(fkponr)
     d fplol1_suff     s                   like(fkpsuf)
     d votyl1_otyp     s                   like(vaotyp)
     d vbldl1_balt     s                   like(vabalt)
     d fusrl1_user     s                   like(fbuser)
      *
     d                 ds
     d d_urec                        80
     d  d_firm                        3  0 overlay(d_urec)
     d  d_aarr                        4  0 overlay(d_urec:4)
     d  d_numm                        8  0 overlay(d_urec:8)
     d  d_suff                        2  0 overlay(d_urec:16)
     d  d_kode                        1    overlay(d_urec:18)
     d  d_date                         d   overlay(d_urec:19) datfmt(*iso)
     d  d_time                         t   overlay(d_urec:29) timfmt(*iso)
     d  d_user                       10    overlay(d_urec:37)
     d  d_wsid                       10    overlay(d_urec:47)
      *
      * Definering av variable
      *
     d w_alf1          s              1
     d w_skor          s              1
     d w_sjek          s              1
     d w_outq          s             10
     d w_otyp          s                   like(footyp)
     d w_palt          s                   like(fkpalt)
     d w_svar          s                   like(fkpdir)
     d w_ruti          s                   like(l_ruti)
     d w_stat          s              1
     d w_memb          s             10
     d w_stmp_alf      s             26
     d w_stmp          s               z
     d w_fgrp          s              3
     d w_user          s             10
     d w_aktp          s             15
     d w_sgru          s             60
     d w_slko          s              2  0
     d w_kkko          s              2  0
      *
      * Eksternt program
       dcl-s co402_verdi1  char(1);
       dcl-s co402_verdi2  char(2048);
       DCL-PR CO402R EXTPGM('CO402R');
         p_filgrp            char(3)     const;
         p_firm              packed(3:0) const;
         p_lager             packed(2:0) const;
         p_nokkel            char(50)    const;
         p_verdi1            char(1);
         p_verdi2            char(2048);
       END-PR;
     d u_702           s              1    inz(*off)
     d u_707           s              1    inz(*off)
     d u_709           s              1    inz(*off)
     d u_710           s              1    inz(*off)                            Nulle fakturatype
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Velg utfra ORDRETYPE hvilket bilde som skal vises
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      *  Hent opplysninger fra ORDRE-HODE-REGISTER
      *
     c     fohel1_key    chain     fohel1r                            90
     c                   if        *in90 = *off
     c                   eval      w_otyp = footyp
     c                   endif
      *
      *  Hent opplysninger fra UTPLUKK-PARAM-REGISTER
      *       (Ordretype hentes kun om den finnes plukket tidligere)
      *
     c     fplol1_key    chain     fplol1r                            90
     c                   if        *in90 = *off
     c                   eval      w_otyp = fkpoty
     c                   endif
      *
      *  Hent opplysninger fra ORDRETYPE-REGISTER
      *
     c                   eval      votyl1_otyp = w_otyp
     c     votyl1_key    chain     votyl1r                            90
      *
      *  Utfra ordretype (akk.type) hopp til riktig bilde
      *
     c     vaoakk        cabeq     0             e2tag
     c     vaoakk        cabeq     1             e3tag
     c     vaoakk        cabeq     2             e4tag
     c     vaoakk        cabeq     3             e5tag
      *
     c                   goto      xslutt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  E2 Behandler valg for utskrift av TILBUD
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     e2tag         tag
      *
     c                   eval      w_ruti = 'FTILB    '
     c                   exsr      sjk_proa
      *
     c                   eval      w_palt = 1
     c                   eval      w_svar = 1
     c                   eval      w_outq = 'PDFPRINT'
      *
      *  Nullstiller felter som benyttes
      *
      *  Dersom ordretype T logges oppretting av tilbud i loggfile
      *
     c                   if        footyp = 'T '
     c                   eval      w_aktp = 'UTSKRIFT '
     c                   eval      w_slko = 0
     c                   eval      w_kkko = 0
     c                   eval      w_sgru = *blank
      *
     c                   call      'XL700R'
     c                   parm                    fonumm
     c                   parm                    w_aktp
     c                   parm                    w_slko
     c                   parm                    w_kkko
     c                   parm                    w_sgru
     c                   endif
      *
     c                   goto      start
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  E3 Behandler valg for utskrift av ORDREBEKREFTELSE/PLUKKLISTE
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     e3tag         tag
      *
     c                   eval      w_ruti = 'FOBEK    '
     c                   exsr      sjk_proa
      *
      *  Legger opplysninger over i felles-felter
      *
     c                   eval      w_palt = vaofdj
     c                   eval      w_svar = 1
     c                   eval      w_outq = 'PDFPRINT'
      *
     c                   goto      start
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  E4 Behandler valg for utskrift av PAKKSEDDEL
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     e4tag         tag
      *
     c                   eval      w_ruti = 'FPAKK    '
     c                   exsr      sjk_proa
      *
      *  Legger opplysninger over i felles-felter
      *
     c                   eval      w_palt = *zero
     c                   eval      w_svar = 1
     c                   eval      w_outq = 'PDFPRINT'
      *
     c                   goto      start
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  E5 Behandler valg for utskrift av FAKTURA/KREDITNOTA
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     e5tag         tag
      *
     c                   eval      w_ruti = 'FFAKK    '
     c                   exsr      sjk_proa
      *
      *  Hvis direkte utskrift,skal fakturatype settes lik 0
      *
     c                   if        u_710 = *on
     c     fohel1_key    chain     fohelur
     c                   if        %found(fohelu)
     c                   eval      fofaty = 0
     c                   update    fohelur
     c                   endif
     c                   endif
      *
      *  Legger opplysninger over i felles-felter
      *
     c                   eval      w_palt = *zero
     c                   eval      w_svar = 1
     c                   eval      w_outq = 'PDFPRINT'
      *
     c                   goto      start
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  START Behandler oppdatering av ORDRE-HODE-REGISTER
      *        samt utlegging/oppdatering av UTPLUKK-PARAM-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     start         tag
      *
      *  Oppdater ORDRE-HODE-REGISTER, ordren er nå til behandling
      *
     c                   if        fokode <> 3
     c                   call      'FO709R'
     c                   parm                    fohel1_numm
     c                   parm                    fohel1_suff
     c                   endif
      *
      *  Dersom ordren allerede er plukket, skal ikke koden endres
      *
     c     fohel1_key    chain     fohelur                            90
     c                   if        *in90  = *off
      *
     c                   if        fokode <> 3
     c                   eval      fokode = 2
     c                   endif
      *
     c                   time                    foedat
     c                   time                    foetim
     c                   eval      foeusr = l_user
     c                   update    fohelur
     c                   endif
      *
      *  Legg ut nye records til  ORDRE-PLUKK-HODE-REGISTER
      *  Eventuelt oppdater, dersom ordren allerede er plukket
      *
     c     fplol1_key    chain     fplolur                            90
     c                   if        *in90  = *on
     c                   eval      fkpfir = w_firm
     c                   eval      fkponr = fplol1_numm
     c                   eval      fkpsuf = fplol1_suff
     c                   eval      fkpoty = w_otyp
     c                   eval      fkpwsd = l_wsid
     c                   eval      fkpusr = l_user
     c                   eval      fkpalt = w_palt
     c                   eval      fkpdir = w_svar
     c                   write     fplolur
     c                   else
     c                   eval      fkpalt = w_palt
     c                   eval      fkpdir = w_svar
     c                   update    fplolur
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Kaller opp utskriftsprogrammet, dersom direkte utskrift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   if        w_svar = 1 or
     c                             w_svar = 2
     c                   call      'FO807R'
     c                   parm                    w_skor
     c                   parm                    w_firm
     c                   parm                    fplol1_numm
     c                   parm                    fplol1_suff
     c                   parm                    w_otyp
     c                   parm                    w_svar
     c                   parm                    w_outq
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
      *
8.01 c                   eval      l_ruti = w_ruti
8.01 c                   out       *dtaara
      *
     c                   eval      *inlr = *on
     c                   return
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_proa      begsr
      *
     c                   eval      *in36 = *off
     c                   eval      p_pdf ='0'
     c                   eval      p_ruti= w_ruti
      * Sjekk om PROA er aktivert
     c                   call      'AP608R'
     c                   parm                    p_ruti
     c                   parm                    p_pdf
      * p_pdf = '1' = PROA aktivert
     c                   if        p_pdf = '1'
     c                   eval      *in35 = *on
     c                   endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     c                   eval      w_skor = *blank
     c                   eval      w_sjek = *blank
     c                   eval      w_outq = *blank
     c                   eval      *in35 = *off
      *
      * Key for file : ORDRE-TYPE-REGISTER
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for file : BILDE-ALTERNATIVER
      *
     c     vbldl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vbldl1_balt
      *
      * Key for file : ORDRE-HODE-REGISTER
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for file : ORDRE-PLUKK-HODE-REGISTER
      *
     c     fplol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fplol1_numm
     c                   kfld                    fplol1_suff
      *
      * Key for brukerregister
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrl1_user
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_ipor
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
      *
8.01 c     *dtaara       define    *lda          lda
      *
      *  Legger inn firmanummer i nøkklene
      *
     c                   eval      w_firm      = p_firm
      *
      *  Legger inn ordrenummer og suffix i nøkklene
      *
     c                   eval      fohel1_numm = p_numm
     c                   eval      fohel1_suff = p_suff
     c                   eval      fplol1_numm = p_numm
     c                   eval      fplol1_suff = p_suff
      *
      * Hente inn brukerregister
      *
     c                   eval      fusrl1_user = l_user
     c     fusrl1_key    chain     fusrl1r                            90
      *
      * Nullstiller returvalg
      *
     c                   eval      p_ipor = *blank
      *
      * Endring 7.02
      *
       CallP CO402R(l_filg:w_firm:0:'FO600R_702':
                        co402_verdi1:co402_verdi2);
       if co402_verdi1 = '1';
         u_702 = *on;
         *in40 = *on;
       endif;
      *
      * Endring 7.07 - Ordrebekreftelse uten pris
      *
       CallP CO402R(l_filg:w_firm:0:'FO600R_707':
                        co402_verdi1:co402_verdi2);
       if co402_verdi1 = '1';
         u_707 = *on;
       endif;
      *
      * Endring 7.09 - Legger default ut verdi 1 som valg på utskrift av tilbud
      *
       CallP CO402R(l_filg:w_firm:0:'FO600R_709':
                        co402_verdi1:co402_verdi2);
       if co402_verdi1 = '1';
         u_709 = *on;
       endif;
      *
      * Hvis faktura/kreditnota og direkte skal sette fakturatype = 0
      *
       CallP CO402R(l_filg:w_firm:0:'FO600_710':
       co402_verdi1:co402_verdi2);
       if co402_verdi1 = '1';
         u_710 = *on;
       endif;
      *
     c                   endsr
