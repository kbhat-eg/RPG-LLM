      /TITLE  ASOKON Henter type kommunikasjon:
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * System  . . . . : ASOKON                                        *
      * Program . . . . : GL119R                                        *
      * Beskrivelse . . : Henter kommunikasjon.                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      *           090295  Nytt program februar-1995                RH   *
      *                                                                 *
      * 19091     190901  Utvidet parameterlisten til også å       RH   *
      *                   returnere type bank.  Tidligere ble      RH   *
      *                   kun type kommunikasjon returnert.        RH   *
      *                                                                 *
      * 09065     090605  Utvidet p.liste til IFS-mappe.           RH   *
      * 09065     090605  A1win skal kun sendes opp hvis in03      RH   *
      *                   er "0".                                  RH   *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *    60-69 = Fileindikatorer chain etc.                           *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fgfaspf    if   e           k disk
     fgl119d    cf   e             workstn
      *
      /eject
      *
      *
      * Datastruktur for klokkeslett:
      *
     d                 ds
     d dsklok                         6  0
     d  dstime                        2  0 overlay(dsklok)
     d  dsminu                        2  0 overlay(dsklok:3)
     d  dsseku                        2  0 overlay(dsklok:5)
      *
      * Henter firmanavn.
      *
     d                uds
     d l_firm                944    946  0
      *
     d w_arow          s              2  0
     d w_acol          s              3  0
     d w_bank          s              2
     d w_in03          s              1  0
     d w_komm          s              4
     d w_vent          s              6  0
09065d w_pifs          s             45
      *
      * Sender angrebilde:
      *
     c                   eval      w_arow = w_arow + 2
     c                   eval      w_acol = w_acol - 4
09065c                   if        w_in03 <> 2
     c                   exfmt     a1win
09065c                   endif
     c                   if        *inkc = *on
     c                   eval      w_in03 = 1
     c                   goto      xslutt
     c                   endif
      *
      * Henter type kommunikasjon firma skal benytte:
      *
     c     l_firm        chain     gfaspf                             60
      *
     c                   if        *in60 = *off
     c                   movel     gefkom        w_komm
19091c                   movel     gefban        w_bank
09065c                   movel     gefifs        w_pifs
     c                   endif
      *
      * Ved ICF-kommunikasjon vil oppkobling av kommunikasjon
      * starte umiddelbart:
      *
     c                   if        w_komm = 'ICF '
     c                   eval      w_arow = w_arow + 2
     c                   eval      w_acol = w_acol - 4
     c                   write     c1win
      * Venter i 5 sekunder:                                        .
     c                   time                    dsklok
     c                   eval      dsseku = dsseku + 5
     c                   if        dsseku > 59
     c                   eval      dsseku = dsseku - 59
     c                   eval      dsminu = dsminu + 1
     c                   if        dsminu > 59
     c                   eval      dsminu = dsminu - 59
     c                   eval      dstime = dstime + 1
     c                   if        dstime > 23
     c                   eval      dstime = dstime - 23
     c                   endif
     c                   endif
     c                   endif
      *                                                             .
     c                   time                    w_vent
     c                   dow       w_vent < dsklok
     c                   time                    w_vent
     c                   enddo
     c                   endif
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     *inzsr        begsr
      *
     c                   eval      w_vent = 0
      *
      * Parameterliste returnerer type kommunikasjon, tilbake til
      * kallende program:
      *
     c     *entry        plist
     c                   parm                    w_komm
19091c                   parm                    w_bank
     c                   parm                    w_in03
     c                   parm                    w_arow
     c                   parm                    w_acol
09065c                   parm                    w_pifs
     c                   endsr
      *
