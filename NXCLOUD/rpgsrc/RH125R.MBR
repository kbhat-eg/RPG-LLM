      /TITLE  Overføring av saldo/budsjett - parameter
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Program . . . . : RH125R                                        *
      * Beskrivelse . . : Overføring av saldo/budsjett - parameter      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      *                   Nytt program november 94                  KOB *
      *                                                                 *
      *                                                                 *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frh125d    cf   e             workstn
      *
     d                uds
     d rhpfÅr                300    303  0
     d rhptÅr                         4  0
     d rhpavf                         4  0
     d rhpavt                         4  0
     d rhpspf                         4  0
     d rhpspt                         4  0
     d rhpktf                         5  0
     d rhpktt                         5  0
     d rhptyp                         1
     d rhpbfa                         1
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d wpakod          s              2  0
     d p_in03          s              1
     d w_firm          s                   like(l_firm)
     d w_user          s                   like(l_user)
     d w_navn          s                   like(l_fnav)
      *
     c     dummy         tag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av felter                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * A2BLD  - A2 Bilde for valg av parametre
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A2BLD Behandle bilde for valg av utskriftskriteria              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     a2taga        tag
      *
      * Bare data
      *
     c     a2tagb        tag
      *
     c                   exfmt     a2bld
      *
      * Gå ut
      *
     c                   if        *inkc = *on                                  .....
     c                   eval      p_in03 = '1'                                     .
     c                   goto      xslutt                                           .
     c                   endif                                                  .....
      *
      *  Kontroll avdeling
      *
     c                   if        a2pavf > a2pavt                                       .....
     c                   eval      *in31 = *on                                               .
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      *  Kontroll kostnadsbærer
      *
     c                   if        a2pspf > a2pspt                                       .....
     c                   eval      *in33 = *on                                               .
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      *  Kontroll konto
      *
     c                   if        a2pktf > a2pktt                                       .....
     c                   eval      *in32 = *on                                               .
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      *  Kontroll av år fra - til
      *
     c                   if        a2pfÅr > a2ptÅr                                       .....
     c                   eval      *in34 = *on                                               .
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      * Skriv parameter til LDA
      *
     c                   move      a2pfÅr        rhpfÅr
     c                   move      a2ptÅr        rhptÅr
     c                   move      a2pavf        rhpavf
     c                   move      a2pavt        rhpavt
     c                   move      a2pspf        rhpspf
     c                   move      a2pspt        rhpspt
     c                   move      a2pktf        rhpktf
     c                   move      a2pktt        rhpktt
     c                   move      a2ptyp        rhptyp
     c                   move      a2pbfa        rhpbfa
      *
      /space 3
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   eval      wpakod = *zero
      *
      * Legge inn firma-
      * nummer og navn
      * fra parameter
      *
     c                   move      l_user        w_user
     c                   move      l_firm        w_firm
     c                   move      l_fnav        w_navn
      *
     c                   eval      a2pavf = *zeros
     c                   eval      a2pavt = 9999
     c                   eval      a2pspf = *zeros
     c                   eval      a2pspt = 9999
     c                   eval      a2pktf = *zeros
     c                   eval      a2pktt = 99999
     c                   eval      a2ptyp = 'B'
     c                   eval      a2pbfa = 'N'
      *
      * Beregner år fra - til
      *
     c                   move      *year         a2pfÅr
     c                   eval      a2ptÅr = a2pfÅr + 1
      *
     c                   eval      p_in03 = ' '
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_in03
      *
     c                   endsr
