     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : MO701R
      * Beskrivelse . . : Overvåking - danner grunndata for type 1
      *                   Endret varetekst på lagervare ?
      *                   Salgsstat, innkjøp, ordre
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       141206 bø   Nytt program
      *       110907 bø   Fra ordre skal ikke tilbud komme med.
5.70  *       151208 bø   Varetype fra lager
6.21  * 6.20  190511 bø   Utvidet vl710r med div. info
6.22  * 6.20  250716 bkv  Rettet feil i les av ordrelinjer
6.nu  * 6.30  230614 bof  sjekket mtp. nummerutvidelse
6.31  * 6.30  030118 bof  Gen. lwo1pf,lwo2pf,lwo3pf,lwo4pf,lwo7pf (num=8)
6.32  * 6.30  170918 bkv  Utvidet med trelast sortiment
8.01  *PRG2   220622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsstal1    if   e           k disk    rename(sstapfr:sstal1r)
     fsistl1    if   e           k disk    rename(sistpfr:sistl1r)
     ffoheld    if   e           k disk    rename(fohepfr:foheldr)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvotylr    if   e           k disk    rename(votypfr:votylrr)
     flwo1lu    uf a e           k disk    rename(lwo1pfr:lwo1lur)
6.32 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
      *
     d sstal1_paar     s                   like(sfpaar)
     d sistl1_paar     s                   like(sfpaar)
     d foheld_bdat     s                   like(fobdat)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d vvarl1_vare     s                   like(vvvare)
     d lwo1lu_odat     s                   like(mwodat)
     d lwo1lu_osys     s                   like(mwosys)
     d lwo1lu_oonr     s                   like(mwoonr)
     d lwo1lu_osuf     s                   like(mwosuf)
     d lwo1lu_olin     s                   like(mwolin)
     d lwo1lu_ovnr     s                   like(mwovnr)
     d lwo1lx_odat     s                   like(mwodat)
     d votylr_otyp     s                   like(vaotyp)
      *
     d w_firm          s                   like(vvfirm)
     d w_vare          s                   like(vvvare)
5.70 d w_lage          s                   like(sflage)
     d w_dato          s                   like(vvedat)
     d w_tek1          s                   like(sftek1)
     d w_tek2          s                   like(sftek2)
     d w_aar           s              4  0
     d p_dato          s                   like(vvedat)
     d w_usr           s                   like(fouser)
     d w_oda           s                   like(fodate)
     d w_tim           s                   like(fotime)
     d w_bong          s                   like(fobong)
     d w_avde          s                   like(foavde)
5.70 d w_vtyp          s                   like(vvvtyp)
6.21 d w_udat          s                   like(vvudat)
6.21 d w_ldor          s                   like(vvldor)
6.32 d w_lv_nobb       s                   like(vvlnob)
6.32 d w_lv_modn       s                   like(vvlmod)
6.32 d w_lv_lldo       s                   like(vvlnle)
6.32 d w_lv_llva       s                   like(vvllva)
      *
6.21 d b_inlr          s              1    inz(*off)
     d b_endr          s              1    inz(*off)
      *
      * Local Data Area
      *
     d                uds
      *
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
      *
      * Definering av variable
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * sletter først for dato som det skal kjøres for.
      *
     c                   eval      lwo1lx_odat = w_dato
readec     lwo1lx_key    setll     lwo1lu
readec     lwo1lx_key    reade     lwo1lu
readec                   dow       not %eof(lwo1lu)
     c                   delete    lwo1lur
readec     lwo1lx_key    reade     lwo1lu
     c                   enddo
      *
     c*                  eval      w_aar       = *year
     c                   eval      w_aar = %subdt(w_dato:*years)
     c                   exsr      les_sstat
      *
     c                   exsr      les_istat
      *
     c                   exsr      les_ordre
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     avslutt       tag
     c                   eval      *inlr = *on
      *
     c                   return
      *
      * --------------------------------------------------------------
      *  Leser salgstatistikk - legger ut varer med endret varetekst
      * --------------------------------------------------------------
      *
     c     les_sstat     begsr
      *
     c                   eval      sstal1_paar = w_aar
readec     sstal1_key    setll     sstal1
readec     sstal1_key    reade     sstal1
readec                   dow       not %eof(sstal1)
     c                   if        sfbida = w_dato
      *
     c                   eval      w_vare = sfvare
5.70 c                   eval      w_lage = sflage
     c                   eval      w_tek1 = sftek1
     c                   eval      w_tek2 = sftek2
     c                   exsr      ctrl_vare
     c                   if        b_endr = *on
     c                   eval      lwo1lu_odat = sfbida
     c                   eval      lwo1lu_osys = 'S'
     c                   eval      lwo1lu_oonr = sfornr
     c                   eval      lwo1lu_osuf = sforsu
     c                   eval      lwo1lu_olin = sforli
     c                   eval      lwo1lu_ovnr = sfvare
     c                   eval      w_usr       = sfuser
     c                   eval      w_oda       = sfdate
     c                   eval      w_tim       = sftime
     c                   eval      w_bong      = sfbong
     c                   eval      w_avde      = sfsavd
     c                   exsr      skriv_logg
     c                   endif
     c                   endif
      *
readec     sstal1_key    reade     sstal1
readec                   enddo
      *
     c                   endsr
      *
      * --------------------------------------------------------------
      *  Leser innkjøpsstatistikk - logger ut varer med endret varetekst
      * --------------------------------------------------------------
      *
     c     les_istat     begsr
      *
     c                   eval      sistl1_paar = w_aar
readec     sistl1_key    setll     sistl1
readec     sistl1_key    reade     sistl1
readec                   dow       not %eof
     c                   if        sfbida = w_dato
     c                   eval      w_vare = sivare
5.70 c                   eval      w_lage = silage
     c                   eval      w_tek1 = sitek1
     c                   eval      w_tek2 = *blank
     c                   exsr      ctrl_vare
     c                   if        b_endr = *on
     c                   eval      lwo1lu_odat = sibida
     c                   eval      lwo1lu_osys = 'I'
     c                   eval      lwo1lu_oonr = siornr
     c                   eval      lwo1lu_osuf = siorsu
     c                   eval      lwo1lu_olin = siorli
     c                   eval      lwo1lu_ovnr = sivare
     c                   eval      w_usr       = siwsid
     c                   eval      w_oda       = *loval
     c                   eval      w_tim       = sitime
     c                   eval      w_bong      = *blank
     c                   eval      w_avde      = sikavd
     c                   exsr      skriv_logg
     c                   endif
     c                   endif
      *
readec     sistl1_key    reade     sistl1
readec                   enddo
      *
     c                   endsr
      *
      * --------------------------------------------------------------
      *  Leser ordre - logger ut varer med endret varetekst
      * --------------------------------------------------------------
      *
     c     les_ordre     begsr
      *
     c                   eval      foheld_bdat = w_dato
readec     foheld_key    setll     foheld
readec     foheld_key    reade     foheld
readec                   dow       not %eof(foheld)
      * det gjelder ikke behandlingskode 0
     c                   eval      votylr_otyp = footyp
     c     votylr_key    chain     votylr
     c                   if        %found(votylr) and
     c                             vaoakk <> 0
      * ikke tilbud
      *
     c                   eval      fodtl1_numm = fonumm
     c                   eval      fodtl1_suff = fosuff
readec     fodtl1_key    setll     fodtl1
6.22 c     fodtl1_key    reade     fodtl1
readec                   dow       not %eof(fodtl1)
     c
     c                   eval      w_vare = fdvare
5.70 c                   eval      w_lage = fdlage
     c                   eval      w_tek1 = fdtek1
     c                   eval      w_tek2 = fdtek2
     c                   exsr      ctrl_vare
     c                   if        b_endr = *on
     c                   eval      lwo1lu_odat = fobdat
     c                   eval      lwo1lu_osys = 'O'
     c                   eval      lwo1lu_oonr = fdnumm
     c                   eval      lwo1lu_osuf = fdsuff
     c                   eval      lwo1lu_olin = fdline
     c                   eval      lwo1lu_ovnr = fdvare
     c                   eval      w_usr       = fouser
     c                   eval      w_oda       = fodate
     c                   eval      w_tim       = fotime
     c                   eval      w_bong      = fobong
     c                   eval      w_avde      = foavde
     c                   exsr      skriv_logg
     c                   endif
readec     fodtl1_key    reade     fodtl1
readec                   enddo
      *
     c                   endif
      *
readec     foheld_key    reade     foheld
readec                   enddo
      *
     c                   endsr
      *
      * --------------------------------------------------------------
      *  Skriver logg ved feil-situasjon
      * --------------------------------------------------------------
      *
     c     skriv_logg    begsr
      *
     c                   clear                   lwo1lur
     c     lwo1lu_key    chain     lwo1lu
     c                   eval      mwotxo = w_tek1
     c                   eval      mwot2o = w_tek2
     c                   eval      mwotxv = vvtek1
     c                   eval      mwot2v = vvtek2
     c                   eval      mwovtx = vvtek1
     c                   eval      mwoogr = vvogrp
     c                   eval      mwohgr = vvhgrp
     c                   eval      mwougr = vvugrp
     c                   eval      mwousr = w_usr
     c                   eval      mwooda = w_oda
     c                   eval      mwotim = w_tim
     c                   eval      mwobng = w_bong
     c                   eval      mwoavd = w_avde
     c                   if        %found(lwo1lu)
     c                   update    lwo1lur
     c                   else
     c                   eval      mwofir = w_firm
     c                   eval      mwodat = lwo1lu_odat
     c                   eval      mwosys = lwo1lu_osys
     c                   eval      mwoonr = lwo1lu_oonr
     c                   eval      mwosuf = lwo1lu_osuf
     c                   eval      mwovnr = lwo1lu_ovnr
     c                   eval      mwolin = lwo1lu_olin
     c                   write     lwo1lur
     c                   endif
      *
     c                   endsr
      *
      * --------------------------------------------------------------
      *  Tester om atistikk - legger ut varer med endret varetekst
      * --------------------------------------------------------------
      *
     c     ctrl_vare     begsr
      *
      * henter varetype
      *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    w_vare
5.70 c                   parm                    w_lage
5.70 c                   parm                    w_vtyp
6.21 c                   parm                    w_udat
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.32 c                   parm                    w_lv_nobb
6.32 c                   parm                    w_lv_modn
6.32 c                   parm                    w_lv_lldo
6.32 c                   parm                    w_lv_llva
      *
     c                   eval      b_endr = *off
      *
     c                   eval      vvarl1_vare = w_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found(vvarl1) and
5.70 c                             w_vtyp = 'L'
     c                   if        w_tek2 <> *blank and
     c                             vvtek2 <> *blank
     c                   if        vvtek1 <> w_tek1 or
     c                             vvtek2 <> w_tek2
     c                   eval      b_endr = *on
     c                   endif
     c                   else
     c                   if        vvtek1 <> w_tek1
     c                   eval      b_endr = *on
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av salgsstatistikk
      *
     c     sstal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sstal1_paar
      *
      *  Key for les av innkjøpsstatikk
      *
     c     sistl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sistl1_paar
      *
      *  Key for les av vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *  Key for les av ordre
      *
     c     foheld_key    klist
     c                   kfld                    w_firm
     c                   kfld                    foheld_bdat
      *
      *  Key for les av ordre
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
      *
      * Key for oppdatering av overvåking
      *
     c     lwo1lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwo1lu_odat
     c                   kfld                    lwo1lu_osys
     c                   kfld                    lwo1lu_oonr
     c                   kfld                    lwo1lu_osuf
     c                   kfld                    lwo1lu_olin
     c                   kfld                    lwo1lu_ovnr
      *
      * Key for slett av overvåking på den dato man vil kjøre på.
      *
     c     lwo1lx_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwo1lx_odat
      *
      *  Key for les av ordretype
      *
     c     votylr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votylr_otyp
      *
     c     *entry        plist
     c                   parm                    p_dato
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_dato = p_dato
      *
     c                   endsr
