      /TITLE  ASOFAK: Start utkjøring av ordrepapirer
     h datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
Q.01  * Program . . . . : QO601R
      * Beskrivelse . . : Start utkjøring av ordrepapirer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       120219 bsa  Nytt program
7.01  *       310519 bsa  Må sjekke om du rettigheter til å skrive ut
7.01  *                   pakkseddel på nytt.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
7.01 ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
      *
      * Definering av LDA
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av parametere
      *
     d p_numm          s              8  0
     d p_suff          s              2  0
     d p_retu          s              1
     d p_acti          s              1
     d p_stat          s              1
      *
      * Definering av variable i nøkler
      *
     d votyl1_otyp     s                   like(vaotyp)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
7.01 d fusrl1_user     s                   like(fbuser)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vaofir)
     d w_numm          s              8  0
     d w_suff          s              2  0
     d w_retu          s              1
     d w_acti          s              1
7.01 d w_mld01         s             30
7.01 d w_mld02         s             30
7.01 d w_ctli          s              2  0
7.01 d w_ctpo          s              3  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Gjør noen sjekker før vi går videre
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * 1. Sjekk at ordren finnes
      *
     c                   eval      fohel1_numm = p_numm
     c                   eval      fohel1_suff = p_suff
     c     fohel1_key    chain     fohel1
     c                   if        not %found
     c                   eval      p_stat = 'O'
7.01 c                   goto      error
     c                   endif
      *
      * Ordretypen kan ikke være faktura/kreditnota
      *
     c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1
     c                   if        vaoakk = 3
     c                   eval      p_stat = 'T'
7.01 c                   goto      error
     c                   endif
7.01  *
7.01  *  Kontroll om bruker har lov til å starte
7.01  *  gjenutskrift av pakkseddel
7.01  *
7.01 c                   if        fbko09 <> 1 and
7.01 c                             fopaks =  1
7.01 c                   eval      p_stat = 'X'
7.01 c                   goto      error
7.01 c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Så kan vi starte dokument handling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      w_retu = *blanks
     c                   eval      w_numm = p_numm
     c                   eval      w_suff = p_suff
     c                   eval      w_acti = p_acti
      *
     c                   call      'QO600R'
     c                   parm                    w_retu
     c                   parm                    w_firm
     c                   parm                    w_numm
     c                   parm                    w_suff
     c                   parm                    w_acti
      *
7.01 c                   goto      avslutt
      *
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Send feilmelding
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01 c     error         tag
7.01  *
7.01 c                   eval      w_ctli = 12
7.01 c                   eval      w_ctpo = 42
7.01 c                   if        p_stat = 'X'
7.01 c                   eval      w_mld01 = 'Har ikke rettigheter til å    '
7.01 c                   eval      w_mld02 = 'skrive ut pakkseddel på nytt  '
7.01 c                   elseif    p_stat = 'T'
7.01 c                   eval      w_mld01 = 'Ordren kan ikke være av type  '
7.01 c                   eval      w_mld02 = 'faktura/kreditnota            '
7.01 c                   elseif    p_stat = 'O'
7.01 c                   eval      w_mld01 = 'Ordren finnes ikke ????       '
7.01 c                   eval      w_mld02 = '                              '
7.01 c                   else
7.01 c                   eval      w_mld01 = 'Ukjent feilstatus  ????       '
7.01 c                   eval      w_mld02 = '                              '
7.01 c                   endif
7.01  *
7.01 c                   call      'AA006C'
7.01 c                   parm                    w_ctli
7.01 c                   parm                    w_ctpo
7.01 c                   parm                    w_mld01
7.01 c                   parm                    w_mld02
7.01  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *inzsr        begsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     *entry        plist
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_acti
     c                   parm                    p_stat
      *
      * Key for file : Ordretype
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for file : Ordrehode
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
7.01  *
7.01  * Key for brukerregister
7.01  *
7.01 c     fusrl1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    fusrl1_user
      *
      * Legger inn firmanummer i nøkklene
      *
     c                   eval      w_firm = l_firm
     c                   eval      p_stat = *blanks
7.01  *
7.01  * Hente inn brukerregister
7.01  *
7.01 c                   eval      fusrl1_user = l_user
7.01 c     fusrl1_key    chain     fusrl1r                            90
      *
     c                   endsr
      *
