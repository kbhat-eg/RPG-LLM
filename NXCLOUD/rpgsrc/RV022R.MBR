     h option(*nodebugio) datedit(*dmy)
     h decedit('0.')
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : RV002R
      * Beskrivelse . . : Leser linjer fra BOVF til kommaseparert til Duett
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * ----- ------ ---- -------------------------------------------------
      *       100209 nho  Visma  / Global
6.21  * V6.20 030912 bof  Sjekker om det er kort kid + litt rydding
6.22  * V6.20 050814 nho  Må sjekke om navnefeltet inneholder ','
6.23  *       141014 nho  Test på firma
6.24  *       171214 nho  Blanker felt RKMKOD
6.25  * V6.20 280515 nho  Alfafelt var for lagnt for å sjekke ','
6.30  * V6.30 040517 nho  Endring vedr. mvakode
7.00  * V7.00 260419 nho  Henter inn konvertering av bilagskode
6.nu  * R6.20     280515  Utvidelse av kort kid                          NHO  *
7.01  * V7.00 270421 sst  Nytt format mot Duett med utgnagspunkt i Visma
7.01  *                   Skal ha med telefonnr og mailadresse i tillegg
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     fbovfl1    ip   e           k disk
     fsohel1    if   e           k disk    rename(sohepfr:sohel1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
6.21 fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
6.21 faforl1    if   e           k disk    rename(aforpfr:aforl1r)
7.00 frb10l1    if   e           k disk    rename(rb10pfr:rb10l1r)
      *
      *
     frv02pf    o    e           k disk
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
6.11  *
6.11 d w_pste          s             30
7.00 d xxbilk          s              5  0
      *
     d w_dato_alf      s              6
     d w_dato_al1      s              6
     d w_dato_al2      s              6
      * Datastruktur for forfallsdato
     d                 ds
     d d_fdat                  1      6  0
     d  d_fa1                  1      1  0
     d  d_fag                  1      2  0
     d  d_fnd                  3      4  0
     d  d_far                  5      6  0
      *
     d                 ds
     d d_fdati                 1     10
     d  d_fa                   1      4
     d  d_f1                   5      5
     d  d_fn                   6      7
     d  d_f2                   8      8
     d  d_fd                   9     10
     d*
      *
      * Datastruktur for bilagsdato
     d                 ds
     d d_date                  1      6  0
     d  d_da1                  1      1  0
     d  d_dag                  1      2  0
     d  d_mnd                  3      4  0
     d  d_aar                  5      6  0
      *
     d                 ds
     d d_dati                  1     10
     d  d_aa                   1      4
     d  d_a1                   5      5
     d  d_mn                   6      7
     d  d_a2                   8      8
     d  d_dd                   9     10
      *
6.11  * Datastruktur for postted
6.11 d                 ds
6.11 d d_pste                  1     30
6.11 d  d_ps1                  6     30
      *
6.21  * Variabler for nøkler
6.21  *
6.21 d votyl1_otyp     s                   like(vaotyp)
6.21 d aforl1_ruti     s                   like(afruti)
7.00 d rb10l1_gkod     s                   like(raakod)
     d rkunl1_kund     s                   like(rkkund)
      *
6.12 d w_kund          s                   like(rkkund)
6.12 d w_navn          s                   like(rknavn)
6.12 d w_gate          s                   like(rkgate)
6.12 d w_adr2          s                   like(rkadr2)
6.12 d w_tlfn          s                   like(rktlfn)
6.12 d w_tlfx          s                   like(rktlfx)
6.12 d w_alfa          s                   like(rkalfa)
6.12 d w_bgir          s                   like(rkbgir)
6.12  *
6.12 d w_firm          s                   like(bffirm)
     d w_aarr          s                   like(soaarr)
     d w_binr          s                   like(sobinr)
     d w_avde          s                   like(bfdavd)
6.30 d w_mkod          s                   like(bfdmom)
     d w_fnutt         s              1    inz('"')
     d w_ponr          s              4
     d w_grns          s             11  2
     d*
6.22 d wkalfa          s                   like(rkalfa)
6.22 d w2navn          s                   like(rknavn)
6.22 d w2fnav          s                   like(rknavn)
6.22 d wknavu          s                   like(rknavn)
6.22 d w_posm          s              2  0
6.22 d*w_lnavn         s                   like(rknavn)
6.22 d w_lnav          s                   like(rknavn)
     d w_ladr1         s                   like(rkgate)
     d w_ladr2         s                   like(rkadr2)
     d w_lposn         s              4
     d w_lposs         s                   like(rksted)
6.nu d*w_kidr          s              6  0
6.nu d w_kidr          s              8  0
6.21 d w_kidd          s                   like(sokidd)
6.21 d w_kid2          s                   like(sokidd)
      *
6.21 d b_kkid          s              1    inz(*off)
      *
      * ---------------------------------------------------------------------------*
      *
6.23 c                   if        l_firm <> bffirm
6.23 c                   goto      slutt
6.23 c                   endif
6.23  *
     c* Blanker felt
     c                   eval      d_dati  = *blanks
     c                   eval      d_fdati = *blanks
6.22 c**                 eval      w_lnavn = *blanks
6.22 c                   eval      w_lnav  = *blanks
6.22 c                   eval      wknavu  = *blanks
6.22 c                   eval      w_posm  = *zeros
6.22 c                   eval      w2fnav  = *blanks
6.22 c                   eval      wkalfa  = *blanks
6.22 c                   eval      w2navn  = *blanks
     c                   eval      w_ladr1 = *blanks
     c                   eval      w_ladr2 = *blanks
     c                   eval      w_lposn = *blanks
     c                   eval      w_lposs = *blanks
      *
6.12 c                   eval      w_navn = *blanks
6.12 c                   eval      w_adr2 = *blanks
6.12 c                   eval      w_gate = *blanks
6.12 c                   eval      w_tlfn = *blanks
6.12 c                   eval      w_tlfx = *blanks
6.12 c                   eval      w_alfa = *blanks
6.12 c                   eval      w_bgir = *zeros
6.12 c                   eval      w_pste = *blanks
6.12 c                   eval      d_pste = *blanks
6.12 c                   eval      w_ponr = *blanks
6.12 c                   eval      w_grns = *zeros
6.12 c                   eval      w_kund = *zeros
6.30  * Moms kode
6.30 c                   if        bfdmom = *blank
6.30 c                   eval      w_mkod = bfcmom
6.30 c                   else
6.30 c                   eval      w_mkod = bfdmom
6.30 c                   endif
     c*
     c                   move      bfbdat        d_dati
     c                   move      d_aa          d_aar
     c                   move      d_mn          d_mnd
     c                   move      d_dd          d_dag
     c                   move      d_date        w_dato_al1
     c*
     c**********
     c                   move      bffdat        d_fdati
     c                   move      d_fa          d_far
     c                   move      d_fn          d_fnd
     c                   move      d_fd          d_fag
     c*
     c*
     c                   move      d_fdat        w_dato_alf
      *
     c                   move      udate         w_dato_al2
      *
6.24 c                   eval      rkmkod = 0
      *
      * Finner data fra faktura-hode og kundereg
     c                   exsr      control_init
     c*
7.00  * Sjekker om bilagstype skal konverteres
7.00 c                   eval      xxbilk = bfbilk
7.00 c                   eval      rb10l1_gkod = bfbilk
7.00 c     rb10l1_key    chain     rb10l1
7.00 c                   if        %found
7.00 c                   eval      xxbilk = rankod
7.00 c                   endif
7.00  *
     c                   if        bfdavd = *zeros
     c                   eval      w_avde = bfcavd
     c                   else
     c                   eval      w_avde = bfdavd
     c                   endif
     c*
6.12 c*
6.12 c                   if        bfdkto > 9999
6.12 c                   eval      rkunl1_kund = bfdkto
6.12 c     rkunl1_key    chain     rkunl1                             90
6.12 c                   if        *in90 = '0'
6.12 c                   exsr      w_flytt
6.12 c                   endif
6.12 c                   else
6.12 c                   if        bfckto > 9999
6.12 c                   eval      rkunl1_kund = bfckto
6.12 c     rkunl1_key    chain     rkunl1                             90
6.12 c                   if        *in90 = '0'
6.12 c                   exsr      w_flytt
6.12 c                   endif
6.12 c                   else
6.12 c                   endif
6.12 c                   endif
     c*
     c*
     c**********
     c                   eval      rvdata = *blank
      * Trans.type
     c                   eval      rvdata =  %char(1)        + ',' +
      * Post.dato
     c                                     %trim(w_dato_al2) + ',' +
      * Bilagsnr.
     c                                     %char(bfbiln)     + ',' +
      * Bil.dato
     c                                     %trim(w_dato_al1) + ',' +
      * Bil.art ??????
7.00 c**                                   %char(bfbilk)     + ',' +
7.00 c                                     %char(xxbilk)     + ',' +
      * Bil.tekst
     c                               '"'+  %trim(bftext) + '"' + ',' +
      * Avdeling
     c                                     %char(w_avde)     + ',' +
      * Prosjektnr.
     c                                     %trim(bfproj)     + ',' +
      * Deb.konto
     c                                     %char(bfdkto)     + ',' +
      * Kred.konto
     c                                     %char(bfckto)     + ',' +
     c*
      * MVA-kode deb???????
6.30 c                                     %trim(w_mkod)     + ',' +
      * Valutakode
     c                                     %char(0)          + ',' +
      * Valutakurs
     c                                     %char(0)          + ',' +
      * Valutabeløp
     c                                     %char(bfvalb)     + ',' +
      * Beløp på linje
     c                                     %char(bfbelp)     + ',' +
      * Motbilagsnr ???????
     c                                     %char(0)          + ',' +
      * Forf.dato
     c                                     %trim(w_dato_alf) + ',' +
      * Kjedenummer
     c                                     %char(0)          + ',' +
      * Mengde
     c                                     %char(bfmngd)     + ',' +
      * Kid nr ???????
     c                               '"'+  %trim(sokidd) + '"' + ',' +
      * Avgifts klasse ???????
     c                                     %char(0)          + ',' +
      * Hovedbokskto kunde ????
     c                                     %char(bfkund)     + ',' +
      * Lev.fakturanr.
     c                               '"'+  %trim(w_fnutt)    + ',' +
      * Remittance no.
     c                                     %char(0)          + ',' +
      * Produktnr.
     c                                     %char(0)          + ',' +
      * Medarbeidernr.
     c                                     %char(0)          + ',' +
      * Ekstra 1 nr
     c                                     %char(0)          + ',' +
      * Ekstra 2 nr
     c                                     %char(0)          + ',' +
      * Ekstra 3 nr
     c                                     %char(0)          + ',' +
      * Ekstra 4 nr
     c                                     %char(0)          + ',' +
      * Kundenr
6.12 c                                     %char(w_kund)     + ',' +
      * Kjedenr
     c                                     %char(0)          + ',' +
6.12  * Navn
6.12 c                               '"'+  %trim(w_navn) + '"' + ',' +
6.12  * Adresse 1
6.12 c                               '"'+  %trim(w_gate) + '"' + ',' +
6.12  * Adresse 2
6.12 c                               '"'+  %trim(w_adr2) + '"' + ',' +
6.12  * Postnr
     c                               '"'+  %trim(w_ponr) + '"' + ',' +
      * Poststed
6.11 c                               '"'+  %trim(w_pste) + '"' + ',' +
      * Skipalfa
     c                               '"'+  %trim(w_fnutt)    + ','   +
      * Telefon
6.12 c                               '"'+  %trim(w_tlfn) + '"' + ',' +
      * Telefax
6.12 c                               '"'+  %trim(w_tlfx) + '"' + ',' +
      * Alfa sort. navn
6.12 c                               '"'+  %trim(w_alfa) + '"' + ',' +
      * Postkto nr.
     c                               '"'+  %trim(w_fnutt)    + ','   +
      * Bankkonto ?????????
6.12 c***                            '"'+  %char(w_bgir)    + ','   +
     c                               '"'+  %char(w_bgir) + '"' + ','    +
      * Kredittgrense
     c                                     %char(w_grns)     + ','   +
      * Lev.navn  ?????????
6.22 c**                             '"'+  %trim(w_lnavn) + '"' + ','   +
6.22 c                               '"'+  %trim(w_lnav) + '"' + ','    +
      * Lev.adr.1 ?????????
     c                               '"'+  %trim(w_ladr1) + '"' + ','   +
      * Lev.adr.2 ?????????
     c                               '"'+  %trim(w_ladr2) + '"' + ','   +
      * Lev.postn ?????????
     c                               '"'+  %trim(w_lposn) + '"'  + ','  +
      * Lev.poststed ??????
     c                               '"'+  %trim(w_lposs) + '"'  + ','   +
      * Medarb.nr på kunden???
     c                                     %char(0)          + ',' +
      * Distrikt
     c                                     %char(rkdist)     + ',' +
      * Betalingsbet
     c                                     %char(rkbetb)     + ',' +
      * Skipnum????
     c                                     %char(0)          + ',' +
      * Kundetype
     c                                     %char(0)          + ',' +
      * Rabattgr./bet.profiler
     c                                     %char(rkrabk)     + ',' +
      * Behandlingsprofiler
7.01 c                                     %char(0)          + ',' +
7.01  * emailadresse
7.01 c                                     %trim(rkemal)     + ','
     c************************************************************************
     c*    'Æ':X'46'     xlate     nsdata        nsdata
     c*    'æ':X'A0'     xlate     nsdata        nsdata
     c*    'Ø':X'77'     xlate     nsdata        nsdata
     c*    'ø':X'90'     xlate     nsdata        nsdata
     c*    'Å':X'2A'     xlate     nsdata        nsdata
     c*    'å':X'EF'     xlate     nsdata        nsdata
     c                   write     rv02pfr
      *
6.23 c     slutt         tag
      *
     c     dd999         tag
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     control_init  begsr
      *
     c                   eval      w_firm = bffirm
     c                   eval      w_binr = bfbiln
     c                   move      d_aa          w_aarr
      * Finner siste faktura-hode for faktura
      *
     c     sohel1_key    setll     sohel1
     c     sohel1_key    reade     sohel1                                 90
     c                   if        *in90 = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   dow       *in90 = *off
     c     sohel1_key    reade     sohel1                                 90
     c                   enddo
      *
     c     avslutt       tag
      * Henter lev.adresse dersom fyllt ut
     c                   if        solkun  <> *zeros
     c                   eval      rkunl1_kund = solkun
     c     rkunl1_key    chain     rkunl1                             90
6.22  * Sjekker om det ligger ',' i leveringsnavn
6.22 c                   eval      w_posm = 0
6.22 c                   eval      w_posm = %scan(',':rknavn)
6.22  *
6.22 c                   if        w_posm > 0 and
6.22 c                             w_posm < 29
6.22 c                   eval      w2navn = %subst(rknavn:1:w_posm - 1)
6.22 c                   eval      w2fnav = %subst(rknavn:w_posm + 2)
6.22 c                   eval      w_lnav = %trim(w2navn) + ' ' + %trim(w2fnav)
6.22 c                   else
6.22 c                   eval      w_lnav = rknavn
6.22 c                   endif
6.22 c**                 eval      w_lnavn     = rknavn
     c                   eval      w_ladr1     = rkgate
     c                   eval      w_ladr2     = rkadr2
     c                   move      rkponr        w_lposn
     c                   eval      w_lposs     = rksted
     c                   endif
      * Henter kundenavn
      *
     c                   eval      rkunl1_kund = sokund
     c     rkunl1_key    chain     rkunl1                             90
6.21  *
6.21  * Henter rutine fra ordretype
6.21  *
6.21 c                   eval      b_kkid = *off
6.21 c                   eval      votyl1_otyp = sootyp
6.21 c     votyl1_key    chain     votyl1
6.21 c                   if        %found(votyl1)   and
6.21 c                             vaorut <> *blank
6.21 c                   eval      aforl1_ruti = vaorut
6.21 c     aforl1_key    chain     aforl1
6.21 c                   if        %found(aforl1) and
6.21 c                             afkidk = 2
6.21 c                   eval      b_kkid = *on
6.21 c                   endif
6.21 c                   endif
6.21 c                   if        b_kkid = *on
6.21 c                   movel     sokidr        w_kidr
6.21 c                   call      'AK710R'
6.21 c                   parm                    w_firm
6.21 c                   parm                    w_kidr
6.21 c                   parm                    w_kidd
6.21 c                   parm                    w_kid2
6.21 c                   movel     w_kid2        sokidd
6.21 c                   endif
6.21  *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.12  *
6.12 c     w_flytt       begsr
6.12  *
6.12 c                   eval      w_kund = rkkund
6.22  *
6.22  * Sjekker om det ligger ',' i kundenavn
6.22 c                   eval      w_posm = 0
6.22 c                   eval      w_posm = %scan(',':rknavn)
6.22  *
6.22 c                   if        w_posm > 0 and
6.22 c                             w_posm < 29
6.22 c                   eval      w2navn = %subst(rknavn:1:w_posm - 1)
6.22 c                   eval      w2fnav = %subst(rknavn:w_posm + 2)
6.22 c                   eval      w_navn = %trim(w2navn) + ' ' + %trim(w2fnav)
6.22 c                   else
6.22 c                   eval      w_navn = rknavn
6.22 c                   endif
6.22  *
6.12 c                   eval      w_gate = rkgate
6.12 c                   eval      w_adr2 = rkadr2
6.12 c                   eval      w_tlfn = rktlfn
6.22  * Sjekker om det ligger ',' i alfafelt
6.22 c                   eval      w_posm = 0
6.22 c                   eval      w_posm = %scan(',':rkalfa)
6.22  *
6.22 c                   if        w_posm > 0 and
6.22 c* 6.25                       w_posm < 29
6.25 c                             w_posm < 17
6.22 c                   eval      w2navn = %subst(rkalfa:1:w_posm - 1)
6.22 c                   eval      w2fnav = %subst(rkalfa:w_posm + 2)
6.22 c                   eval      w_alfa = %trim(w2navn) + ' ' + %trim(w2fnav)
6.22 c                   else
6.22 c                   eval      w_alfa = rkalfa
6.22 c                   endif
6.22  *
6.12 c                   eval      w_bgir = rkbgir
6.11 c                   eval      d_pste = rksted
6.11 c                   eval      w_pste = d_ps1
6.12 c                   move      rkponr        w_ponr
6.12 c                   z-add     rkgrns        w_grns
6.11 c*
6.12  *
6.12 c                   endsr
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  * Subrutine for initiering av program
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  *
6.21 c     *inzsr        begsr
6.21  *
6.21  * Key for oppslag på faktura-hode-register
6.21  *
6.21 c     sohel1_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    w_aarr
6.21 c                   kfld                    w_binr
6.21  *
6.21  * Key for oppslag på kunde
6.21  *
6.21 c     rkunl1_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    rkunl1_kund
6.21  *
6.21  * Key for les av ordretype
6.21  *
6.21 c     votyl1_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    votyl1_otyp
6.21  *
6.21  * Key for les av rutine-register
6.21  *
6.21 c     aforl1_key    klist
6.21 c                   kfld                    aforl1_ruti
      *
7.00  * Key for posisjonering på bilagskoder
7.00  *
7.00 c     rb10l1_key    klist
7.00 c                   kfld                    w_firm
7.00 c                   kfld                    rb10l1_gkod
7.00  *
6.21  *
     c                   eval      w_firm = l_firm
6.21  *
6.21 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
