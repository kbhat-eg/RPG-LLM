     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : HR111R
      * Beskrivelse . . : Radioterminal trans, bestilling - registrer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       121000 HKO  Nytt program
      *       131200 HKO  Lagt inn "field exit"
      *       230301 HKO  Tar vare på søkestring mot vare
      *       150601 HKO  Rettet feil ved endring av enhet på salg
      *       210604 bø   Tilpasset mot nye bestillingsforsl.-rutiner
      *       010904 bø   Justert parameter mot status best.forslag
      *       201204 HKO  Lagt inn ny logikk for scanning
      *       221204 bø   Justert feilkode håndtering til best.forslag
      *       150305 bø   Parameter for styre om status brukes
      *                   Midlertidig hardkodet, legge kode på kodereg. v.540
      *       020805 HKO  Endret les mot vare-enhet for å finne innkjøps-
      *                   enheter og salgenheter
      *       080805 HKO  Henter vekt fra vare-enhet
      *       240805 bø   Kode for å styre status på plass
      *       290506 bø   Tar med firma over til HR112R
      *       270606 bø   Rettet feil vedr. enhet
      *       270606 bø   Rettet feil  vare ligger på bestilling fra før
      *       060307 bø   Hvis enhet på ean - skal denne foreslås.
      *       150307 bø   Tar aldri best.pkt,min,mm. fra varereg. kun lager
      * V5.60 230308 KOB  Lagt inn oppdateering av selger i bestillingsforslag f
5.70  * PRGR  021208 BOF  Utvidet med prisgruppe /varetype
      * 6.10  170609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  130709 bof  Utivdet eannr til 14 og brukt std. oppslag
6.12  * 6.10  021009 bof  Bestillbar enhet pr. leverandør - ekstra test
6.20  * 6.20  021110 bof  Utvidet med utg.dato pr. lager
6.21  * 6.20  101110 bof  Utvidet med leverandør på lager
6.22  * 6.20  190511 bof  Utvidet vl710r med div. info
6.23  * 6.20  151113 bsa  Bruker innkjøpspris og ikke kostpris ved ut-
6.23  *                   legg av verdi på bestillingsforslag.
6.24  * 6.20  030614 bkv  Innkjøpspris: bruker VP753R i stedet for VP750R
6.nu  * 6.30  060614 bof  endringer ihht. nummerserieutvidelse
6.31  * 6.30  180816 bof  Hente kampanjekost hvis kampanje.
6.32  * 6.30  170918 bkv  Utvidet med trelast sortiment
7.01  *       180820 bof  Utvidet parm til JM591R med prisgruppe
8.01  *PRG2   130622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fhrtrl2    if   e           k disk    rename(hrtrpfr:hrtrl2r)
     fhrtrlr    if   e           k disk    rename(hrtrpfr:hrtrlrr)
     fhrtrlu    uf a e           k disk    rename(hrtrpfr:hrtrlur)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarl4    if   e           k disk    rename(vvarpfr:vvarl4r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fvvenl4    if   e           k disk    rename(vvenpfr:vvenl4r)
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
6.12 fvvepl3    if   e           k disk    rename(vveppfr:vvepl3r)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     flodtlv    if   e           k disk    rename(lodtpfr:lodtlvr)
     flohelr    if   e           k disk    rename(lohepfr:lohelrr)
     fvotylr    if   e           k disk    rename(votypfr:votylrr)
     flfhelu    uf a e           k disk    rename(lfhepfr:lfhelur)
     flfdtlu    uf a e           k disk    rename(lfdtpfr:lfdtlur)
6.32 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
     fhr111d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(hrfirm)
     d p_kode          s                   like(hrkode)
     d p_tims          s                   like(hrtims)
     d p_otyp          s                   like(lhotyp)
     d p_ldor          s                   like(lfldor)
     d p_lage          s                   like(lflage)
     d p_bdat          s                   like(lhbdat)
     d p_ldat          s                   like(lhldat)
     d p_avde          s                   like(lhavde)
V5.60d p_selg          s                   like(lhselg)
     d p_numm          s                   like(lhnumm)
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
      *
      * Datastruktur - EAN128
      *
     d                 ds
     d d_e128                        26
6.11 d  d_fast                        2  0 overlay(d_e128)
6.11 d  d_eann                       14  0 overlay(d_e128:3)
     d  d_kode                        4  0 overlay(d_e128:17)
     d  d_anta                        6  2 overlay(d_e128:21)
      *
      * Definering av variabler i nøkler
      *
     d hrtrlr_vare     s                   like(hrvare)
     d hrtrlr_enhe     s                   like(hrenhe)
     d hrtrlu_vare     s                   like(hrvare)
     d hrtrlu_enhe     s                   like(hrenhe)
     d vvarl1_vare     s                   like(vvvare)
     d vvarl4_lvar     s                   like(vvlvar)
     d rlevl1_levr     s                   like(rllevr)
     d vvenl1_vare     s                   like(vevare)
     d vvenl1_enhe     s                   like(veenhe)
     d vvenl4_vare     s                   like(vevare)
     d vvenl4_inen     s                   like(veinen)
     d vvenl2_vare     s                   like(vevare)
     d vvenl2_saen     s                   like(vesaen)
6.12 d vvepl3_pvar     s                   like(vepvar)
6.12 d vvepl3_pldo     s                   like(vepldo)
     d vlagl1_vare     s                   like(vlvare)
     d vlagl1_lage     s                   like(vllage)
     d lodtlv_vare     s                   like(ldvare)
     d lfhelu_numm     s                   like(lhnumm)
     d lohelr_numm     s                   like(lonumm)
     d lohelr_suff     s                   like(losuff)
     d votylr_otyp     s                   like(vaotyp)
      *
      * Definering av variable
      *
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_scan          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_oppr          s              1    inz(*off)
     d b_inlr          s              1    inz(*off)
      *
     d w_udat          s               d   datfmt(*iso)
     d w_scan          s             26
     d w_leng          s              2  0
     d w_posi          s              2  0
     d w_vare          s             15
6.11 d w_eanx          s             14
     d w_kod2          s              1
     d w_fell          s              6
     d w_type          s              2
     d w_fast          s             10
     d w_stek          s             35
     d x               s              1  0
8.01 d w_prgr          s              2
      *
     d w_firm          s                   like(hrfirm)
     d w_kode          s                   like(hrkode)
     d w_tims          s                   like(hrtims)
     d w_enhe          s                   like(hrenhe)
6.31 d w_enhx          s                   like(hrenhe)
     d w_anta          s                   like(hranta)
     d w_dato          s                   like(hrdato)
     d w_time          s                   like(hrtime)
     d w_anta_kalk     s                   like(hranta)
     d w_bmen          s                   like(vlbmen)
     d w_bmin          s                   like(vlbmin)
     d w_stat          s                   like(lfstat)
     d w_hnum          s                   like(lhnumm)
     d w_112           s             11  2
     d w_tbel          s                   like(lhtbel)
     d w_tvkt          s             11  3
     d w_xenhe         s                   like(hrenhe)
6.11 d w_eann          s             14  0
6.11 d w_eava          s                   like(vvvare)
6.11 d w_eaen          s                   like(vvenhe)
6.11 d w_east          s              1
      *
     d w_lety          s              1  0
     d w_kopr          s              9  2
     d w_inpr          s              9  2
     d w_sapr          s              9  2
     d w_bupr          s              9  2
     d w_tipr          s              9  2
     d w_inlr          s              1    inz(*off)
     d w_ldor          s                   like(lhldor)
6.32 d w_lv_nobb       s                   like(vvlnob)
6.32 d w_lv_modn       s                   like(vvlmod)
6.32 d w_lv_lldo       s                   like(vvlnle)
6.32 d w_lv_llva       s                   like(vvllva)
6.32 d w_lv_vare       s                   like(vvvare)
     d w_lsor          s                   like(lhlsor)
5.70 d p_vtyp          s                   like(vvvtyp)
6.20 d p_udat          s                   like(vvudat)
6.20 d w_retk          s              1
6.23 d w_kost          s              9  2
6.23 d w_kamp          s              5
6.nu d w_numm          s              8  0
6.31  *
6.31 d w_enhk          s                   like(ldenh1)
6.31 d w_enh1          s                   like(ldenh1)
6.31 d w_inp1          s                   like(ldipny)
6.31 d w_kop1          s                   like(ldkpny)
6.31 d w_enh2          s                   like(ldenh1)
6.31 d w_inp2          s                   like(ldipny)
6.31 d w_kop2          s                   like(ldkpny)
6.31 d w_enh3          s                   like(ldenh1)
6.31 d w_inp3          s                   like(ldipny)
6.31 d w_kop3          s                   like(ldkpny)
6.31 d w_omr2          s             12  6
6.31 d w_omr3          s             12  6
6.31 d w_vkai          s              9  2
6.31 d w_vkpr          s              9  2
6.31  *
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(6)
     d c_digi          c                   '0123456789 '
     d c_null          c                   '0000000000000'
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * wvisrn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1BLD  - D1 Skjermbilde for slette (Delete)
      * E1BLD  - E1 Skjermbilde for innlegging av tilleggsinformasjon
      * F1BLD  - F1 Skjermbilde for avslutting av bestilling
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32 C/Exec SQL
6.32 C+  Set Option DatFmt=*ISO
6.32 C/End-Exec
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   eval      *in22 = *on
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   eval      *in22 = *on
     c                   goto      b2tagb
     c                   endsl
      *
      * F4=Spørring på vare
      *
     c                   if        *inkd
     c                   eval      b2sca1 = *blank
     c                   eval      b2sca2 = *blank
     c                   call      'VV580R'
     c                   parm                    w_firm
     c                   parm                    b2sca1
     c                   parm                    w_stek
     c                   eval      *in22 = *on
     c                   if        b2sca1 = *blank
     c                   goto      b2tagb
     c                   endif
     c                   endif
      *
      * Behandling av scanning
      *
     c                   if        b2sca1 <> *blank or
     c                             b2sca2 <> *blank
     c                   exsr      scanning
     c                   eval      *in22 = *on
     c                   if        b_feil = *on
     c                   goto      b2tagb
     c                   else
     c                   goto      b2taga
     c                   endif
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
      * Oppretter bestilling i bestillingsforslag
      *
     c                   exsr      opprett
     c                   if        b_oppr = *off
     c                   goto      b2taga
     c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av scanning
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     scanning      begsr
      *
      * Identifisering av vare foregår etter følgende rekkefølge, hvor
      * lengden på varenummeret, og om varenummer er alfa eller numerisk
      * er avgjørende:
      *
      * 1) alfa:
      *      - Lev. varenummer (første 15 pos.)
      * 2) num + lengde = 26:
      *      - EAN-128
      * 3) num + lengde <= 8:
      *      - Varenr
      * 4) num + lengde <= 13:
      *      - EAN-nummer
      * 5) num + lengde < 26:
      *      - Lev. varenummer (første 15 pos.)
      *
     c                   eval      b_feil = *off
     c                   eval      b_scan = *on
      *
     c                   eval      c1enhe = *blank
     c                   eval      w_anta = 0
      *
     c                   eval      w_scan = %trim(b2sca1) + %trim(b2sca2)
      *
     c                   eval      w_leng = %len(%trim(w_scan))
      *
      * Sjekker om scannefelt inneholder alfa-verdi
      *
     c     c_digi        check     w_scan        w_posi
      *
      * Behandling dersom alfa
      *
     c                   if        w_posi > 0
6.32  * sjekker om logistikk-vare
6.32 c                   eval      w_lv_vare = *blank
6.32 c/exec sql
6.32 c+ select vvlvnr
6.32 c+ into   :w_lv_vare
6.32 c+ from   vvlepf
6.32 c+ where  vvllva = :w_scan
6.32 c+ fetch first 1 rows only
6.32 c/end-exec
6.32 c                   if        w_lv_vare <> *blank
6.32 c                   movel     w_lv_vare     c1vare
6.32 c                   exsr      xc2bld
6.32 c                   goto      end_scan
6.32 c                   endif
      *
     c                   eval      vvarl4_lvar = w_scan
     c     vvarl4_key    chain     vvarl4
     c                   if        %found
     c***                eval      c1vare = vvvare
     c                   movel     vvvare        c1vare
     c                   exsr      xc2bld
     c                   goto      end_scan
     c                   endif
      *
     c                   eval      b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      end_scan
      *
     c                   endif
      *
      * Behandling dersom lengde = 26
      *
     c                   if        w_leng = 26
      *
     c                   eval      w_xenhe = *blank
     c                   eval      d_e128 = w_scan
6.11 c                   eval      w_eann = d_eann
6.11 c                   call      'VE710R  '
6.11 c                   parm                    w_firm
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_eava
6.11 c                   parm                    w_eaen
6.11 c                   parm                    w_east
6.11 c                   if        w_east = '1'
6.11 c                   eval      w_xenhe     = w_eaen
6.11 c                   eval      vvarl1_vare = w_eava
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c***                eval      c1vare = vvvare
     c                   movel     vvvare        c1vare
     c                   eval      w_anta = d_anta
     c                   exsr      xc2bld
     c                   goto      end_scan
     c                   endif
     c                   endif
      *
     c                   eval      b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      end_scan
      *
     c                   endif
      *
      * Behandling dersom lengde <= 8
      *
     c                   if        w_leng <= 8
      *
     c                   eval      w_vare = %subst(c_null:1:8-w_leng) +
     c                                      w_scan
      *
     c                   eval      vvarl1_vare = w_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c***                eval      c1vare = vvvare
     c                   movel     vvvare        c1vare
     c                   exsr      xc2bld
     c                   goto      end_scan
     c                   endif
      *
     c                   endif
      *
      * Behandling dersom lengde <= 13
      *
6.11 c                   if        w_leng <= 14
      *
6.11 c                   eval      w_eanx = %subst(c_null:1:14-w_leng) +
     c                                      w_scan
      *
     c                   eval      w_xenhe = *blank
6.11 c                   movel     w_eanx        w_eann
6.11 c                   call      'VE710R  '
6.11 c                   parm                    w_firm
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_eava
6.11 c                   parm                    w_eaen
6.11 c                   parm                    w_east
6.11 c                   if        w_east = '1'
6.11 c                   eval      w_xenhe     = w_eaen
6.11 c                   eval      vvarl1_vare = w_eava
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c***                eval      c1vare = vvvare
     c                   movel     vvvare        c1vare
     c                   exsr      xc2bld
     c                   goto      end_scan
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Behandling dersom lengde < 26 (samlepost)
      *
6.32  * sjekker om logistikk-vare
6.32 c                   eval      w_lv_vare = *blank
6.32 c/exec sql
6.32 c+ select vvlvnr
6.32 c+ into   :w_lv_vare
6.32 c+ from   vvlepf
6.32 c+ where  vvllva = :w_scan
6.32 c+ fetch first 1 rows only
6.32 c/end-exec
6.32 c                   if        w_lv_vare <> *blank
6.32 c                   movel     w_lv_vare     c1vare
6.32 c                   exsr      xc2bld
6.32 c                   goto      end_scan
6.32 c                   endif
      *
     c                   eval      vvarl4_lvar = w_scan
     c     vvarl4_key    chain     vvarl4
     c                   if        %found
     c***                eval      c1vare = vvvare
     c                   movel     vvvare        c1vare
     c                   exsr      xc2bld
     c                   goto      end_scan
     c                   endif
      *
      * Setter feil-indikator dersom vare ikke ble funnet
      *
     c                   eval      b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      end_scan
      *
     c     end_scan      tag
      *
     c                   eval      b_scan = *off
      *
     c                   if        b_feil = *off
     c                   eval      b2sca1 = *blank
     c                   eval      b2sca2 = *blank
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   eval      b2sca1 = *blank
     c                   eval      b2sca2 = *blank
      *
     c     hrtrl2_key    setll     hrtrl2
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        wcurrn > 0
     c                   eval      wvisrn = wcurrn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      wvisrn = w_srrn
     c                   eval      *in22 = *off
      *
      * Endre post
      *
     c                   if        b1valg = 2
     c                   eval      c1vare = b1vare
     c                   eval      c1enhe = b1enhe
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slette post
      *
     c                   if        b1valg = 4
     c                   eval      d1vare = b1vare
     c                   eval      d1enhe = b1enhe
     c                   exsr      xd1bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_enhe = c1enhe
      *
      * Henter vareinformasjon
      *
     c***                eval      vvarl1_vare = c1vare
     c                   eval      vvarl1_vare = *blank
     c                   movel     c1vare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   leavesr
     c                   endif
6.21 c                   eval      w_ldor = vvldor
6.21  *
6.22  * henter div. info fra lager
6.22  *
6.22 c                   call      'VL710R'
6.22 c                   parm                    w_firm
6.22 c                   parm                    vvvare
6.22 c                   parm                    p_lage
6.22 c                   parm                    p_vtyp
6.22 c                   parm                    p_udat
6.22 c                   parm                    w_ldor
6.22 c                   parm                    b_inlr
6.32 c                   parm                    w_lv_nobb
6.32 c                   parm                    w_lv_modn
6.32 c                   parm                    w_lv_lldo
6.32 c                   parm                    w_lv_llva
      *
     c                   eval      vvtek1 = %trim(vvtek1)
     c                   eval      c2tek1 = %subst(vvtek1:1:19)
     c                   eval      c2tek2 = %subst(vvtek1:20:16)
      *
     c                   eval      c2lnav = *blank
      *
6.21 c                   if        w_ldor <> 0
6.21 c                   eval      rlevl1_levr = w_ldor
     c     rlevl1_key    chain     rlevl1                             90
     c                   if        *in90 = *off
     c                   eval      c2lnav = rlnavn
     c                   endif
     c                   endif
      *
6.12  * Prøver å først å finne bestillbare enheter pr. leverandør
6.12  *
6.12  *
6.12 c                   eval      x = 0
6.12 c                   eval      c2enh1 = *blank
6.12 c                   eval      c2enh2 = *blank
6.12 c                   eval      c2enh3 = *blank
6.12 c                   eval      *in80 = *on
6.12 c                   eval      *in81 = *off
6.12 c                   eval      *in82 = *off
6.12  *
6.12 c                   if        p_ldor <> 0
6.12 c                   eval      vvepl3_pvar = vvvare
6.12 c                   eval      vvepl3_pldo = p_ldor
6.12 c     vvepl3_key    setll     vvepl3
6.12 c     vvepl3_key    reade     vvepl3
6.12 c                   dow       not %eof(vvepl3)
6.12 c                   if        vepbes = 1
6.12 c                   eval      x = x + 1
6.12  *
6.12 c                   select
6.12 c                   when      x = 1
6.12 c                   eval      c2enh1 = vepenh
6.12 c                   when      x = 2
6.12 c                   eval      c2enh2 = vepenh
6.12 c                   when      x = 3
6.12 c                   eval      c2enh3 = vepenh
6.12 c                   endsl
6.12 c                   endif
6.12 c     vvepl3_key    reade     vvepl3
6.12 c                   enddo
6.12 c                   endif
      * Henter tre innkjøpsenheter. Dersom innkjøpsenhet ikke ble funnet,
      * hentes tre salgsenheter
      *
6.12 c                   if        x = 0
      *
     c                   eval      vvenl4_vare = vvvare
     c                   eval      vvenl4_inen = 1
     c     vvenl4_key    setll     vvenl4
     c     vvenl4_key2   reade     vvenl4
     c                   dow       not %eof and
     c                             x < 3
      *
     c                   eval      x = x + 1
      *
     c                   select
     c                   when      x = 1
     c                   eval      c2enh1 = veenhe
     c                   when      x = 2
     c                   eval      c2enh2 = veenhe
     c                   when      x = 3
     c                   eval      c2enh3 = veenhe
     c                   endsl
      *
     c     vvenl4_key2   reade     vvenl4
     c                   enddo
      *
6.12 c                   endif
      *
     c                   if        x = 0
      *
     c                   eval      vvenl2_vare = vvvare
     c                   eval      vvenl2_saen = 1
     c     vvenl2_key    setll     vvenl2
     c     vvenl2_key2   reade     vvenl2
     c                   dow       not %eof and
     c                             x < 3
      *
     c                   eval      x = x + 1
      *
     c                   select
     c                   when      x = 1
     c                   eval      c2enh1 = veenhe
     c                   when      x = 2
     c                   eval      c2enh2 = veenhe
     c                   when      x = 3
     c                   eval      c2enh3 = veenhe
     c                   endsl
      *
     c     vvenl2_key2   reade     vvenl2
     c                   enddo
      *
     c                   endif
      *
     c                   if        w_xenhe <> *blank
     c                   select
     c                   when      w_xenhe = c2enh1
     c                   eval      *in80  = *on
     c                   eval      *in81  = *off
     c                   eval      *in82  = *off
     c                   when      w_xenhe = c2enh2
     c                   eval      *in80  = *off
     c                   eval      *in81  = *on
     c                   eval      *in82  = *off
     c                   when      w_xenhe = c2enh3
     c                   eval      *in80  = *off
     c                   eval      *in81  = *off
     c                   eval      *in82  = *on
     c                   endsl
     c                   endif
      *
      * Antall
      *
     c                   eval      c2anta = w_anta
      *
      * Bestilling
      *
     c                   if        b_scan = *off
      *
     c                   eval      hrtrlr_vare = c1vare
     c                   eval      hrtrlr_enhe = c1enhe
     c     hrtrlr_key    chain     hrtrlr                             90
     c                   if        *in90 = *off
      *
     c                   eval      *in80 = *off
      *
     c                   select
     c                   when      hrenhe = c2enh1
     c                   eval      *in80 = *on
     c                   when      hrenhe = c2enh2
     c                   eval      *in81 = *on
     c                   when      hrenhe = c2enh3
     c                   eval      *in82 = *on
     c                   endsl
      *
     c                   eval      c2anta = hranta
      *
     c                   endif
     c                   endif
      *
      * Viser bildet
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc
     c                   leavesr
     c                   endif
      *
      * F1=Bytting av enhet
      *
     c                   if        *inka
     c                   select
     c                   when      *in80 = *on and
     c                             c2enh2 <> *blank
     c                   eval      *in80 = *off
     c                   eval      *in81 = *on
     c                   when      *in81 = *on
     c                   eval      *in81 = *off
     c                   if        c2enh3 <> *blank
     c                   eval      *in82 = *on
     c                   else
     c                   eval      *in80 = *on
     c                   endif
     c                   when      *in82 = *on
     c                   eval      *in82 = *off
     c                   eval      *in80 = *on
     c                   endsl
     c                   goto      c2tagb
     c                   endif
      *
      * F2=Kalkulator
      *
     c                   if        *inkb
     c                   call      'HR200R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    w_anta_kalk
     c                   goto      c2tagb
     c                   endif
      *
      * F5=Forny
      *
     c                   if        *inke
     c                   eval      *in80 = *on
     c                   eval      *in81 = *off
     c                   eval      *in82 = *off
     c                   eval      c2anta = 0
     c                   goto      c2tagb
     c                   endif
      *
      * Validering av input
      *
      *
5.70 c                   if        p_vtyp =  'S'
     c                   eval      *in34 = *on
     c                   goto      c2tagb
     c                   endif
      *
      *
5.70 c                   if        p_vtyp <> 'L'
     c                   eval      *in33 = *on
     c                   goto      c2tagb
     c                   endif
      *
      * sjekk om det er en periodestyrt vare
      *
     c                   call      'VD772R '
6.21 c                   parm                    w_firm
6.21 c                   parm                    p_lage
6.21 c                   parm                    vvvare
6.21 c                   parm                    w_retk
6.21 c                   parm                    b_inlr
6.21 c                   if        w_retk = '1'
6.21 c                   eval      *in35 = *on
6.21 c                   goto      c2tagb
6.21 c                   endif
      *
     c                   if        c2anta <= 0
     c                   eval      *in31 = *on
     c                   goto      c2tagb
     c                   endif
      *
6.20 c                   if        p_udat <> *loval and
6.20 c                             p_udat <= w_udat
     c                   eval      *in32 = *on
     c                   goto      c2tagb
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   hrtrlur
      *
     c                   select
     c                   when      *in80 = *on
     c                   eval      c1enhe = c2enh1
     c                   when      *in81 = *on
     c                   eval      c1enhe = c2enh2
     c                   when      *in82 = *on
     c                   eval      c1enhe = c2enh3
     c                   endsl
      *
     c                   if        b1valg = 2 and
     c                             w_enhe <> c1enhe
     c                   eval      hrtrlu_vare = c1vare
     c                   eval      hrtrlu_enhe = w_enhe
     c     hrtrlu_key    chain     hrtrlur                            90
     c                   if        *in90 = *off
     c                   delete    hrtrlur
     c                   endif
     c                   endif
      *
     c                   eval      hrtrlu_vare = c1vare
     c                   eval      hrtrlu_enhe = c1enhe
     c     hrtrlu_key    chain     hrtrlur                            90
      *
     c                   eval      hrvare = c1vare
     c                   eval      hrenhe = c1enhe
      *
     c                   if        *in90 = *off and
     c                             b_scan = *on
     c                   eval      hranta = hranta + c2anta
     c                   else
     c                   eval      hranta = c2anta
     c                   endif
      *
     c                   time                    hrdato
     c                   time                    hrtime
      *
     c                   if        *in90 = *off
     c                   update    hrtrlur
     c                   else
     c                   eval      hrfirm = w_firm
     c                   eval      hrkode = w_kode
     c                   eval      hrtims = w_tims
     c                   write     hrtrlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1enhe = hrenhe
     c     hranta        mult      1             b1anta
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (d1bld)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1bld        begsr
      *
     c                   eval      hrtrlr_vare = d1vare
     c                   eval      hrtrlr_enhe = d1enhe
     c     hrtrlr_key    chain     hrtrlr                             90
      *
     c                   eval      d1anta = hranta
     c                   eval      d1tek1 = *blank
     c                   eval      d1tek2 = *blank
      *
     c***                eval      vvarl1_vare = hrvare
     c                   eval      vvarl1_vare = *blank
     c                   movel     hrvare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1                             90
     c                   if        *in90 = *off
     c                   eval      d1tek1 = %subst(vvtek1:1:19)
     c                   eval      d1tek2 = %subst(vvtek1:20:16)
     c                   endif
      *
     c                   exfmt     d1bld
      *
     c                   if        *inkc
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      hrtrlu_vare = d1vare
     c                   eval      hrtrlu_enhe = d1enhe
     c     hrtrlu_key    chain     hrtrlur
      *
     c                   delete    hrtrlur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c     hrtrl2_key    reade     hrtrl2                                 15
      *
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1tek1 = *blank
     c                   eval      b1enhe = hrenhe
     c                   eval      b1anta = 0
     c                   eval      b1vare = hrvare
      *
     c     hranta        mult      1             b1anta
      *
     c***                eval      vvarl1_vare = hrvare
     c                   eval      vvarl1_vare = *blank
     c                   movel     hrvare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      b1tek1 = vvtek1
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      wvisrn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av bestilling i bestillingsforlag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opprett       begsr
      *
     c                   eval      b_oppr = *off
      *
     c     hrtrl2_key    chain     hrtrl2                             90
     c                   if        *in90 = *on
     c                   eval      b_oppr = *on
     c                   leavesr
     c                   endif
      *
      * Tilleggsbilde
      *
     c     e1taga        tag
      *
     c                   exfmt     e1bld
      *
     c                   if        *inkc
     c                   leavesr
     c                   endif
      *
     c                   if        *inke
     c                   goto      e1taga
     c                   endif
      *
      * Avslutning
      *
      * Henter neste bestillingsforslag-nummer
      *
     c                   eval      w_kod2 = '0'
     c                   eval      w_fell = laafor
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
6.nu c                   eval      w_numm = 0
      *
     c                   call      'AS100R'
     c                   parm                    w_kod2
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
6.nu c                   parm                    w_numm
6.nu c                   eval      f1numm  = w_numm
      *
      * Oppretter bestillingsforslag-hode
      *
     c                   eval      lhfirm = w_firm
     c                   eval      lhnumm = f1numm
     c                   eval      lhotyp = p_otyp
     c                   eval      lhlage = p_lage
     c                   eval      lhavde = p_avde
V5.60c                   eval      lhselg = p_selg
     c                   eval      lhbdat = p_bdat
     c                   eval      lhldat = p_ldat
     c                   eval      lhwsid = l_wsid
     c                   eval      lhuser = l_user
     c                   eval      lhoppr = 'R'
     c                   if        p_ldor <> 0
     c                   eval      lhldor = p_ldor
     c                   eval      rlevl1_levr = p_ldor
     c     rlevl1_key    chain     rlevl1
     c                   if        %found(rlevl1)
     c                   movel     rlalfa        lhlsor
     c                   endif
     c                   endif
      *
     c                   time                    lhdate
     c                   time                    lhtime
      *
     c                   if        lhbdat = *loval
     c                   time                    lhbdat
     c                   endif
      *
6.10 c                   eval      lhousr = l_user
6.10 c                   time                    lhedat
6.10 c                   time                    lhetim
6.10 c                   eval      lheusr = l_user
     c                   write     lfhelur
      *
      * Flytter bestilling fra radioterminal til bestillingsforslag-linje
      *
     c     hrtrlu_key2   setll     hrtrlu
     c     hrtrlu_key2   reade     hrtrlur                                90
     c                   dow       *in90 = *off
      *
     c***                eval      vvarl1_vare = hrvare
     c                   eval      vvarl1_vare = *blank
     c                   movel     hrvare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1
6.21 c                   eval      w_ldor = vvldor
6.21  *
6.21  * henter leverandør fra lager
6.21  *
6.21 c                   call      'VL721R'
6.21 c                   parm                    w_firm
6.21 c                   parm                    vvvare
6.21 c                   parm                    p_lage
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
      *
6.32  * henter div. info fra lager
6.32  *
6.32 c                   call      'VL710R'
6.32 c                   parm                    w_firm
6.32 c                   parm                    vvvare
6.32 c                   parm                    p_lage
6.32 c                   parm                    p_vtyp
6.32 c                   parm                    p_udat
6.32 c                   parm                    w_ldor
6.32 c                   parm                    b_inlr
6.32 c                   parm                    w_lv_nobb
6.32 c                   parm                    w_lv_modn
6.32 c                   parm                    w_lv_lldo
6.32 c                   parm                    w_lv_llva
      *
     c                   eval      vvenl1_vare = *blank
     c                   movel     hrvare        vvenl1_vare
     c                   eval      vvenl1_enhe = hrenhe
     c     vvenl1_key    chain     vvenl1
      *
     c                   exsr      finn_pris
      *
     c                   eval      lffirm = hrfirm
     c                   eval      lfnumm = f1numm
     c                   eval      lfline = lfline + 10
     c                   eval      lfltyp = *blank
     c                   eval      lfldor = 0
     c                   eval      lflage = p_lage
     c                   eval      lfogrp = vvogrp
     c                   eval      lfhgrp = vvhgrp
     c                   eval      lfugrp = vvugrp
     c                   eval      lflvar = *blank
     c                   eval      lfvare = vvvare
     c                   eval      lfenh1 = hrenhe
     c                   eval      lftek1 = vvtek1
     c                   eval      lftek2 = vvtek2
     c                   eval      lfanta = hranta
     c                   eval      lfvekt = vevekt
     c                   eval      lflbel = w_kopr
     c                   exsr      finn_varelag
     c                   exsr      sjekk_status
     c                   if        laabst = 1
     c                   eval      lfstat = w_stat
     c                   else
     c                   eval      lfstat = '0'
     c                   endif
      *
     c                   if        p_ldor = 0 or
6.21 c                             p_ldor = w_ldor
6.21 c                   eval      lfldor = w_ldor
6.32 c                   if        w_lv_nobb > 0
6.32 c                   eval      lflvar = w_lv_llva
6.32 c                   else
     c                   eval      lflvar = vvlvar
6.32 c                   endif
     c                   else
     c                   eval      lfldor = p_ldor
     c                   endif
6.21 c                   eval      lfvlev = w_ldor
      *
6.10 c                   time                    lfodat
6.10 c                   time                    lfotim
6.10 c                   eval      lfousr = l_user
6.10 c                   time                    lfedat
6.10 c                   time                    lfetim
6.10 c                   eval      lfeusr = l_user
     c                   write     lfdtlur
      *
     c                   delete    hrtrlur
      *
     c     hrtrlu_key2   reade     hrtrlur                                90
     c                   enddo
      *
      * finner status for heading - oppdaterer
      *
     c                   eval      lfhelu_numm = f1numm
     c     lfhelu_key    chain     lfhelu
     c                   if        %found(lfhelu)
     c                   eval      w_hnum = f1numm
     c                   eval      w_tbel = 0
     c                   eval      w_tvkt = 0
     c                   eval      w_stat = *blank
     c                   call      'LH714R '
     c                   parm                    w_hnum
     c                   parm                    w_stat
     c                   parm                    w_tbel
     c                   parm                    w_tvkt
     c                   eval      lhvekt = w_tvkt
     c                   eval      lhtbel = w_tbel
     c                   eval      lhstat = w_stat
6.10 c                   time                    lhedat
6.10 c                   time                    lhetim
6.10 c                   eval      lheusr = l_user
     c                   update    lfhelur
     c                   endif
      *
      * Hvis leverandør ikke er bestemt fra rad.term.,så sjekkes det om
      * bestillingsforslag må splittes på flere leverandører.
      *
     c                   if        p_ldor = 0
     c                   eval      p_numm = f1numm
     c                   call      'HR112R '
     c                   parm                    w_firm
     c                   parm                    p_numm
     c                   endif
      *
      * Viser avslutingsbildet
      *
     c                   eval      b_oppr = *on
      *
     c                   exfmt     f1bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Finne prisen på en vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_pris     begsr
      *
      *
     c                   eval      w_lety = 0
     c                   time                    w_dato
     c                   time                    w_time
     c                   eval      w_inlr = *off
      *
     c                   call      'VP700R'
     c                   parm                    w_firm
     c                   parm                    vvvare
5.70 c                   parm                    w_prgr
     c                   parm                    hrenhe
     c                   parm                    w_lety
     c                   parm                    w_dato
     c                   parm                    w_time
     c                   parm                    w_inlr
     c                   parm                    w_sapr
     c                   parm                    w_bupr
     c                   parm                    w_tipr
     c                   parm                    w_kopr
6.23  *
6.23  * Henter innkjøpspris, slik at den benyttes i verdiberegning
6.23  *
6.23 c                   eval      w_kost = *zeros
6.23 c                   eval      w_inpr = *zeros
6.23 c                   eval      w_kamp = *blanks
6.31 c                   eval      w_enhx = hrenhe
6.23  *
6.24 c                   call      'VP753R'
6.23 c                   parm                    w_firm
6.23 c                   parm                    vvvare
6.23 c                   parm                    w_prgr
6.31 c                   parm                    w_enhx
6.24 c                   parm                    w_lety
6.23 c                   parm                    w_dato
6.24 c                   parm                    w_kost
6.23 c                   parm                    w_inpr
6.23 c                   parm                    w_kamp
      * Hvis innkjøpspris finnes brukes denne, hvis ikke kost
6.23 c                   if        w_inpr <> 0
6.23 c                   eval      w_kopr = w_inpr
6.23 c                   endif
6.31  *
6.31 c                   exsr      sjek_kamp
6.31  *
6.31  * hvis det finnes en kostpris fra kampanje
6.31 c                   if        w_vkai <> 0
6.31 c                   select
6.31 c                   when      w_enh1 = w_enhx
6.31 c                   eval      w_inpr = w_inp1
6.31 c                   eval      w_kopr = w_kop1
6.31 c                   when      w_Enh2 = w_enhx
6.31 c                   eval      w_inpr = w_inp2
6.31 c                   eval      w_kopr = w_kop2
6.31 c                   when      w_Enh3 = w_enhx
6.31 c                   eval      w_inpr = w_inp3
6.31 c                   eval      w_kopr = w_kop3
6.31 c                   endsl
6.31 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av vare/lager informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_varelag  begsr
      *
     c                   eval      w_bmin = 0
     c                   eval      w_bmen = 0
      *
     c                   eval      vlagl1_vare = lfvare
     c                   eval      vlagl1_lage = lflage
      *
     c     vlagl1_key    chain     vlagl1
     c                   if        not %found(vlagl1)
     c                   leavesr
     c                   endif
      *
      * Verdier fra lager eller vare?
      * fom. v 5.50 tas dette kun fra lager
      *
     c                   eval      w_bmen = vlbmen
     c                   eval      w_bmin = vlbmin
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker mulige statuskoder
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_status  begsr
      *
      * Her sjekkes det for status 4 - 6.
      *
     c                   eval      w_stat = '0'
     c                   if        w_bmen <> 0 and
     c                             hranta <> w_bmen
     c                   eval      w_stat = '4'
     c                   endif
      *
     c                   if        w_bmin <> 0
     c                   eval      w_112  = hranta / w_bmin
     c                   move      w_112         w_20              2 0
     c                   if        w_20 > 0
     c                   eval      w_stat = '5'
     c                   endif
     c                   endif
      *
      * Sjekk mot bestilling /restleveranse
      *
     c                   exsr      sjk_best
      *
      * Sjekk om det er en cross-docking vare
      *
     c                   if        vvcrdo = 1
     c                   eval      w_stat = '7'
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_best      begsr
      *
readec                   eval      lodtlv_vare = vvvare
readec     lodtlv_key    setll     lodtlv
     c     lodtlv_key    reade     lodtlv
     c                   dow       not %eof(lodtlv)
     c                   if        ldanta > 0 and
     c                             ldlage = lhlage
      *
     c                   eval      lohelr_numm = ldnumm
     c                   eval      lohelr_suff = ldsuff
     c     lohelr_key    chain     lohelr
     c                   if        %found(lohelr)
     c                   eval      votylr_otyp = lootyp
     c     votylr_key    chain     votylr
     c                   if        %found(votylr) and
     c                             vaolag = 1  and
     c                             vaoakk = 1
     c                   eval      w_stat = '6'
     c                   leavesr
     c                   endif
     c                   endif
     c                   endif
     c     lodtlv_key    reade     lodtlv
     c                   enddo
      *
     c                   endsr
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6 31  * Subrutine for å sjekke om vare ligger på kampanje med
6.31  * aktuell innkjøpsperiode, bruker i så fall kostpris / innkjøpspris
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *
6.31 c     sjek_kamp     begsr
6.31  *
6.31  * Kaller program for henting av pris-informasjon på tre enheter
6.31  *
6.31 C                   eval      w_kamp = *blank
6.31 c                   call      'VP755R'
6.31 c                   parm                    w_firm
6.31 c                   parm                    vvvare
6.31 c                   parm                    w_prgr
6.31 c                   parm                    w_lety
6.31 c                   parm                    w_dato
6.31 c                   parm                    w_enh1
6.31 c                   parm                    w_kop1
6.31 c                   parm                    w_inp1
6.31 c                   parm                    w_enh2
6.31 c                   parm                    w_kop2
6.31 c                   parm                    w_inp2
6.31 c                   parm                    w_omr2
6.31 c                   parm                    w_enh3
6.31 c                   parm                    w_kop3
6.31 c                   parm                    w_inp3
6.31 c                   parm                    w_omr3
6.31 c                   parm                    w_kamp
6.31 c                   parm                    w_ldor
6.31 c                   parm                    p_lage
5.37  *
6.31 c                   eval      w_vkai = 0
6.31 c                   eval      w_vkpr = 0
6.31 c                   eval      w_enhk = *blank
6.31 c                   eval      w_kamp = *blank
6.31 c                   call      'JM591R  '
6.31 c                   parm                    w_firm
6.31 c                   parm                    vvvare
6.31 c                   parm                    w_ldor
6.31 c                   parm                    w_vkai
6.31 c                   parm                    w_vkpr
6.31 c                   parm                    w_enhk
6.31 c                   parm                    w_kamp
7.01 c                   parm                    w_prgr
6.31  *
6.31 c                   if        w_vkai <> 0 and
6.31 c                             w_enhk <> *blank
6.31 c                   if        w_enhk = w_enh1 or
6.31 c                             w_enhk = w_enh2 or
6.31 c                             w_enhk = w_enh3
6.31 c                   eval      w_vkpr = w_vkai
6.31 c                   exsr      just_enhe
6.31 c                   endif
6.31 c                   endif
6.31  *
6.31 c                   endsr
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  * Subrutine for justere kampanjepris hentet fra VA til riktig enhet
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *
6.31 c     just_enhe     begsr
6.31  *
6.31 c                   select
6.31 c                   when      w_enh1 = w_enhk
6.31 c                   eval      w_inp1 = w_vkai
6.31 c                   eval      w_kop1 = w_vkpr
6.31 c                   eval      w_inp2 = w_inp1 * w_omr2 / 1
6.31 c                   eval      w_inp3 = w_inp1 * w_omr3 / 1
6.31 c                   eval      w_kop2 = w_kop1 * w_omr2 / 1
6.31 c                   eval      w_kop3 = w_kop1 * w_omr3 / 1
6.31 c                   when      w_enh2 = w_enhk
6.31 c                   eval      w_inp2 = w_vkai
6.31 c                   eval      w_kop2 = w_vkpr
6.31 c                   eval      w_inp1 = w_inp2 * 1/ w_omr2
6.31 c                   eval      w_inp3 = w_inp2 * w_omr3 / w_omr2
6.31 c                   eval      w_kop1 = w_kop2 * 1/ w_omr2
6.31 c                   eval      w_kop3 = w_kop2 * w_omr3 / 1
6.31 c                   when      w_enh3 = w_enhk
6.31 c                   eval      w_inp3 = w_vkai
6.31 c                   eval      w_kop3 = w_vkpr
6.31 c                   eval      w_inp1 = w_inp3 * 1/ w_omr3
6.31 c                   eval      w_inp2 = w_inp3 * w_omr2 / w_omr3
6.31 c                   eval      w_kop1 = w_kop3 * 1/ w_omr3
6.31 c                   eval      w_kop2 = w_kop3 * w_omr2 / w_omr3
6.31 c                   endsl
6.31  *
6.31 c                   endsr

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kode
     c                   parm                    p_tims
     c                   parm                    p_otyp
     c                   parm                    p_ldor
     c                   parm                    p_lage
     c                   parm                    p_bdat
     c                   parm                    p_ldat
     c                   parm                    p_avde
     c                   parm                    p_selg
      *
      * Key for les på radioterminal-trans
      *
     c     hrtrl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_kode
     c                   kfld                    w_tims
      *
      * Key for oppslag på radioterminal-trans
      *
     c     hrtrlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_kode
     c                   kfld                    w_tims
     c                   kfld                    hrtrlr_vare
     c                   kfld                    hrtrlr_enhe
      *
      * Key for oppdatering av radioterminal-trans
      *
     c     hrtrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_kode
     c                   kfld                    w_tims
     c                   kfld                    hrtrlu_vare
     c                   kfld                    hrtrlu_enhe
      *
     c     hrtrlu_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    w_kode
     c                   kfld                    w_tims
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på vare m/lev.varenummer
      *
     c     vvarl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl4_lvar
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
      *
      * Key for les på vare-enhet
      *
     c     vvenl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl4_vare
     c                   kfld                    vvenl4_inen
      *
     c     vvenl4_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl4_vare
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
     c                   kfld                    vvenl2_saen
      *
     c     vvenl2_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
6.12  *
6.12  * Key for les på vare-enhet-prisguver
6.12  *
6.12 c     vvepl3_key    klist
6.12 c                   kfld                    w_firm
6.12 c                   kfld                    vvepl3_pvar
6.12 c                   kfld                    vvepl3_pldo
      *
      * Key for oppslag på lager status-koder
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for les av LAGER
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
      *
      *  Key for les av bestillinger
      *
     c     lodtlv_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlv_vare
      *
      *  Key for les av bestilling hode
      *
     c     lohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelr_numm
     c                   kfld                    lohelr_suff
      *
      *  Key for les av ordretype
      *
     c     votylr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votylr_otyp
      *
      *  Key for oppdatering av FORSLAG-HODE
      *
     c     lfhelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lfhelu_numm
      *
      * Henter firma og timestamp fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_kode = p_kode
     c                   eval      w_tims = p_tims
      *
     c                   time                    w_udat
      *
5.70  * Henter prisgruppe
5.70  *
5.70 c                   call      'VL712R  '
5.70 c                   parm                    w_firm
5.70 c                   parm                    p_lage
5.70 c                   parm                    p_avde
5.70 c                   parm                    w_prgr
      *
      * Hent opplysninger fra lager status-koder
      *
     c     lstsl1_key    chain     lstsl1r                            90
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      *in22 = *on
      *
     c     hrtrl2_key    setll     hrtrl2
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
