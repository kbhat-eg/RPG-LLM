     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : NN810R
      * Beskrivelse . . : Nettbutikk, hent pris
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       160502 HKO  Nytt program
      *       161203 HKO  Endret parameter ved kall til VP900R
      *       120804 HKO  Endret parameter ved kall til VP900R
      *       041104 HKO  Endret parameter ved kall til VP900R
      *       020805 HKO  Endret les mot vare-enhet for å finne salgsenheter
5.70  * PRGR  231008 sst  Prisgruppe
      *       140609 ASK  Kaller program for oppsett av riktig lager i LDA
5.71  *       160609 bof  Endret kundenr i input til alfa og gjør om til num.
6.21  * 6.20  190511 bø   Utvidet vl710r med div. info
6.nu  *       160614 bof  Endret ihht. nummerutvidelse
6.30  * 6.30  170918 bkv  Utvidet med trelast sortiment
      *
8.01  *PRG2   160622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
     fvpkol1    if   e           k disk    rename(vpkopfr:vpkol1r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
6.30 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
      * Definering av parametre
      *
     d p_uniq          s             15
     d p_dtqi          s             10
     d p_dtqo          s             10
     d p_bibl          s             10
     d p_feil          s              1
     d p_firm          s              3  0
     d p_lage          s              2
     d p_inpl          s              5p 0
     d p_outl          s              5p 0
     d p_wait          s              5p 0
     d p_keyo          s              2
     d p_keyl          s              3p 0
     d p_sndl          s              3p 0
     d p_sndi          s             36
5.70 d p_vtyp          s                   like(vvvtyp)
      *
      * Definering av datastrukturer
      *
      * Datakø - inn
      *
     d                 ds
     d d_inpu                        21
5.71 d  d_kunx                        6    overlay(d_inpu: 1)
     d  d_kprx                        6    overlay(d_inpu: 7)
     d  d_vare                        8    overlay(d_inpu:13)
     d  d_lety                        1  0 overlay(d_inpu:21)
      *
      * Datakø - ut
      *
     d                 ds
     d d_outp                       150
     d  d_vark                        8    overlay(d_outp:  1)
     d  d_ltyk                        1  0 overlay(d_outp:  9)
     d  d_enh1                        3    overlay(d_outp: 10)
     d  d_pri1                       10    overlay(d_outp: 13)
     d  d_rab1                        6    overlay(d_outp: 23)
     d  d_lty1                        1  0 overlay(d_outp: 29)
     d  d_pko1                       10    overlay(d_outp: 30)
     d  d_enh2                        3    overlay(d_outp: 40)
     d  d_pri2                       10    overlay(d_outp: 43)
     d  d_rab2                        6    overlay(d_outp: 53)
     d  d_lty2                        1  0 overlay(d_outp: 59)
     d  d_pko2                       10    overlay(d_outp: 60)
     d  d_enh3                        3    overlay(d_outp: 70)
     d  d_pri3                       10    overlay(d_outp: 73)
     d  d_rab3                        6    overlay(d_outp: 83)
     d  d_lty3                        1  0 overlay(d_outp: 89)
     d  d_pko3                       10    overlay(d_outp: 90)
     d  d_enhl                        3    overlay(d_outp:100)
     d  d_behl                       12    overlay(d_outp:103)
     d  d_bbes                       12    overlay(d_outp:115)
     d  d_bord                       12    overlay(d_outp:127)
     d  d_disp                       12    overlay(d_outp:139)
      *
      * Datastruktur - parametre inn til VP900R
      *
     d                 ds
     d hirec                        120
     d  hifirm                        3  0 overlay(hirec)
     d  hirkat                        3  0 overlay(hirec:4)
     d  hikund                        6  0 overlay(hirec:7)
     d  hikpro                        6  0 overlay(hirec:13)
     d  hivare                       15    overlay(hirec:19)
     d  hilety                        1  0 overlay(hirec:34)
     d  hienhe                        3    overlay(hirec:35)
     d  hidato                         d   overlay(hirec:38) datfmt(*iso)
     d  hitime                         t   overlay(hirec:48) timfmt(*iso)
6.nu d  hinumm                        8  0 overlay(hirec:56)
6.nu d  hikode                        1    overlay(hirec:64)
6.nu d  hidekn                        5  2 overlay(hirec:65)
6.nu d  hiavgf                        1  0 overlay(hirec:70)
6.nu d  hipriv                        1  0 overlay(hirec:71)
6.nu d  hitilb                        1    overlay(hirec:72)
6.nu d  hiskaf                        1    overlay(hirec:73)
6.nu d  hildor                        6  0 overlay(hirec:74)
6.nu d  hisapr                        9  2 overlay(hirec:80)
6.nu d  hikopr                        9  2 overlay(hirec:89)
8.01 d  hiprgr                        2    overlay(hirec:98)
     d  hiinlr                        1    overlay(hirec:120)
      *
      * Datastruktur - parametre ut fra VP900R
      *
     d                 ds
     d horec                         80
     d  holety                        1  0 overlay(horec)
     d  hokpri                        1    overlay(horec:2)
     d  hosapr                        9  2 overlay(horec:12)
     d  horab1                        5  2 overlay(horec:30)
     d  horab2                        5  2 overlay(horec:35)
8.01 d  hoprgr                        2    overlay(horec:71)
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vvenl2_vare     s                   like(vevare)
     d vvenl2_saen     s                   like(vesaen)
     d vpkol1_pkod     s                   like(vapkod)
     d vlagl1_vare     s                   like(vlvare)
     d vlagl1_lage     s                   like(vllage)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vvfirm)
     d w_enhe          s                   like(veenhe)
     d w_behl          s                   like(vlbehl)
     d w_bbes          s                   like(vlbbes)
     d w_bord          s                   like(vlpakk)
     d w_disp          s                   like(vlbehl)
      *
     d w_dato          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_pris          s              9  2
     d w_raba          s              5  2
8.01 d w_prgr          s              2
5.71 d w_kund          s              6  0
5.71 d w_kpro          s              6  0
5.71 d w_vare          s             15
5.71 d w_lage          s              2  0
6.21 d w_udat          s                   like(vvudat)
6.21 d w_ldor          s                   like(vvldor)
6.21 d b_inlr          s              1    inz(*off)
6.30 d w_lv_nobb       s                   like(vvlnob)
6.30 d w_lv_modn       s                   like(vvlmod)
6.30 d w_lv_lldo       s                   like(vvlnle)
6.30 d w_lv_llva       s                   like(vvllva)
      *
     d w_pris_alf      s             10
     d w_raba_alf      s              6
     d w_behl_alf      s             12
     d w_bbes_alf      s             12
     d w_bord_alf      s             12
     d w_disp_alf      s             12
      *
     d                uds
     d l_lagr               1001   1002  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser og behandler datakø (input)
      *
     c                   dou       d_inpu = *blank
      *
     c                   eval      d_inpu = *blank
      *
     c                   call      'QRCVDTAQ'
     c                   parm                    p_dtqi
     c                   parm                    p_bibl
     c                   parm                    p_inpl
     c                   parm                    d_inpu
     c                   parm                    p_wait
     c                   parm                    p_keyo
     c                   parm                    p_keyl
     c                   parm                    p_uniq
     c                   parm                    p_sndl
     c                   parm                    p_sndi
      *
     c                   if        d_inpu <> *blank
5.71  *
5.71 c     ' ':'0'       xlate     d_kunx        d_kunx
5.71 c                   move      d_kunx        w_kund
5.71 c     ' ':'0'       xlate     d_kprx        d_kprx
5.71 c                   move      d_kprx        w_kpro
     c                   movel     d_vare        w_vare
      *
      * Kaller opp program for uthenting av lager fra kunde
      * Dette legges ut i LDA for å gi riktig pris i VP900R
      *
     c                   call      'NN899C'
5.71 c                   parm                    w_kund
5.71  *
5.71 c*                  eval      w_lage = l_lagr
5.70  *
5.70  * Henter prisgruppe
5.70  *
5.70 c                   call      'VL714R   '
5.70 c                   parm                    w_prgr
5.70 c                   parm                    w_lage
      *
     c                   exsr      behandling
     c                   endif
      *
     c                   enddo
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av prisforespørsel
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
      * Avbryter dersom vare ikke finnes
      *
     c**                 eval      vvarl1_vare = w_vare
     c**   vvarl1_key    chain     vvarl1
     c**                 if        not %found
     c**                 eval      p_feil = *on
     c**                 goto      avslutt
     c**                 endif
      *
      * Initierer output datakø
      *
     c                   eval      d_outp = *blank
      *
     c                   eval      d_vark = w_vare
     c                   eval      d_ltyk = d_lety
      *
     c                   eval      d_pri1 = '0000000,00'
     c                   eval      d_rab1 = '000,00'
     c                   eval      d_lty1 = 0
     c                   eval      d_pri2 = '0000000,00'
     c                   eval      d_rab2 = '000,00'
     c                   eval      d_lty2 = 0
     c                   eval      d_pri3 = '0000000,00'
     c                   eval      d_rab3 = '000,00'
     c                   eval      d_lty3 = 0
     c                   eval      d_enhl = *blank
     c                   eval      d_behl = '00000000,000'
     c                   eval      d_bbes = '00000000,000'
     c                   eval      d_bord = '00000000,000'
     c                   eval      d_disp = '00000000,000'
      *
      * Henter pris på tre salgsenheter
      *
     c                   eval      vvenl2_vare = w_vare
     c                   eval      vvenl2_saen = 1
     c     vvenl2_key    setll     vvenl2
     c     vvenl2_key2   reade     vvenl2
     c                   dow       %eof = *off
      *
     c                   eval      w_enhe = veenhe
      *
     c                   select
     c                   when      d_enh1 = *blank
     c                   exsr      hent_pris
     c                   eval      d_enh1 = w_enhe
     c                   eval      d_pri1 = w_pris_alf
     c                   eval      d_rab1 = w_raba_alf
     c                   eval      d_lty1 = holety
     c                   eval      d_pko1 = hokpri
     c                   when      d_enh1 <> *blank and
     c                             d_enh2 = *blank
     c                   exsr      hent_pris
     c                   eval      d_enh2 = w_enhe
     c                   eval      d_pri2 = w_pris_alf
     c                   eval      d_rab2 = w_raba_alf
     c                   eval      d_lty2 = holety
     c                   eval      d_pko2 = hokpri
     c                   when      d_enh2 <> *blank and
     c                             d_enh3 = *blank
     c                   exsr      hent_pris
     c                   eval      d_enh3 = w_enhe
     c                   eval      d_pri3 = w_pris_alf
     c                   eval      d_rab3 = w_raba_alf
     c                   eval      d_lty3 = holety
     c                   eval      d_pko3 = hokpri
     c                   other
     c                   leave
     c                   endsl
      *
     c     vvenl2_key2   reade     vvenl2
     c                   enddo
      *
      * Henter beskrivelse av priskode
      *
     c                   if        d_pko1 <> *blank
     c                   eval      vpkol1_pkod = d_pko1
     c     vpkol1_key    chain     vpkol1
     c                   if        %found
     c                   eval      d_pko1 = vaptxt
     c                   endif
     c                   endif
      *
     c                   if        d_pko2 <> *blank
     c                   eval      vpkol1_pkod = d_pko2
     c     vpkol1_key    chain     vpkol1
     c                   if        %found
     c                   eval      d_pko2 = vaptxt
     c                   endif
     c                   endif
      *
     c                   if        d_pko3 <> *blank
     c                   eval      vpkol1_pkod = d_pko3
     c     vpkol1_key    chain     vpkol1
     c                   if        %found
     c                   eval      d_pko3 = vaptxt
     c                   endif
     c                   endif
      *
      * Henter lagerbeholding
      *
     c                   exsr      hent_lager
      *
      * Skriver til output-datakø
      *
     c                   call      'QSNDDTAQ'
     c                   parm                    p_dtqo
     c                   parm                    p_bibl
     c                   parm                    p_outl
     c                   parm                    d_outp
     c                   parm                    p_keyl
     c                   parm                    p_uniq
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å hente prisinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_pris     begsr
      *
      * Kaller prisberegningsprogram
      *
     c                   eval      hifirm = w_firm
     c                   eval      hirkat = 0
5.71 c                   eval      hikund = w_kund
     c                   eval      hikpro = w_kpro
     c                   eval      hivare = w_vare
     c                   eval      hilety = d_lety
     c                   eval      hienhe = w_enhe
     c                   eval      hidato = w_dato
     c                   eval      hitime = w_time
5.70 c                   eval      hiprgr = w_prgr
     c                   eval      hinumm = 0
     c                   eval      hikode = *blank
     c                   eval      hidekn = 0
     c                   eval      hitilb = *blank
     c                   eval      hiavgf = 0
     c                   eval      hipriv = 0
     c                   eval      hiskaf = *off
     c                   eval      hildor = 0
     c                   eval      hisapr = 0
     c                   eval      hikopr = 0
     c                   eval      hiinlr = *blank
      *
     c                   call      'VP900R'
     c                   parm                    hirec
     c                   parm                    horec
      *
     c                   eval      w_pris = hosapr
      *
     c                   if        horab1 > 0
     c                   eval(h)   w_pris = w_pris - (w_pris * horab1 / 100)
     c                   endif
     c                   if        horab2 > 0
     c                   eval(h)   w_pris = w_pris - (w_pris * horab2 / 100)
     c                   endif
      *
     c                   eval      w_raba = horab1
      *
     c                   if        horab2 <> 0 and
     c                             w_pris <> 0
     c                   eval(h)   w_raba = (hosapr - w_pris) * 100 / hosapr
     c                   endif
      *
     c                   movel     w_pris        w_pris_alf
     c                   eval      w_pris_alf = %subst(w_pris_alf:1:7) + ',' +
     c                                          %subst(w_pris_alf:8:2)
      *
     c                   movel     w_raba        w_raba_alf
     c                   eval      w_raba_alf = %subst(w_raba_alf:1:3) + ',' +
     c                                          %subst(w_raba_alf:4:2)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å hente lagerbeholdning
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_lager    begsr
      *
     c                   if        faalag = 0
     c                   goto      end_lager
     c                   endif
      *
     c                   eval      vvarl1_vare = w_vare
     c     vvarl1_key    chain     vvarl1
5.70 *
5.70 * Kaller program for henting av varetype
5.70 *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    w_lage
5.70 c                   parm                    p_vtyp
6.21 c                   parm                    w_udat
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
     c                   if        not %found or
5.70 c                             p_vtyp <> 'L'
     c                   goto      end_lager
     c                   endif
      *
      * Henter beholdning
      *
     c                   eval      w_behl = 0
     c                   eval      w_bbes = 0
     c                   eval      w_bord = 0
     c                   eval      w_disp = 0
      *
     c                   eval      vlagl1_vare = w_vare
      *
     c                   if        p_lage <> *blank
     c                   move      p_lage        vlagl1_lage
     c     vlagl1_key    chain     vlagl1
     c                   if        %found
     c                   eval      w_behl = vlbehl
     c                   eval      w_bbes = vlbbes
     c                   eval      w_bord = vlpakk + vloord
     c                   endif
     c                   else
     c     vlagl1_key2   setll     vlagl1
     c     vlagl1_key2   reade     vlagl1
     c                   dow       %eof = *off
     c                   eval      w_behl = w_behl + vlbehl
     c                   eval      w_bbes = w_bbes + vlbbes
     c                   eval      w_bord = w_bord + vlpakk + vloord
     c     vlagl1_key2   reade     vlagl1
     c                   enddo
     c                   endif
      *
     c                   eval      w_disp = w_behl + w_bbes - w_bord
      *
     c                   movel     w_behl        w_behl_alf
     c                   eval      w_behl_alf = %subst(w_behl_alf:1:8) + ',' +
     c                                          %subst(w_behl_alf:9:3)
      *
     c                   movel     w_bbes        w_bbes_alf
     c                   eval      w_bbes_alf = %subst(w_bbes_alf:1:8) + ',' +
     c                                          %subst(w_bbes_alf:9:3)
      *
     c                   movel     w_bord        w_bord_alf
     c                   eval      w_bord_alf = %subst(w_bord_alf:1:8) + ',' +
     c                                          %subst(w_bord_alf:9:3)
      *
     c                   movel     w_disp        w_disp_alf
     c                   eval      w_disp_alf = %subst(w_disp_alf:1:8) + ',' +
     c                                          %subst(w_disp_alf:9:3)
      *
      * Legger ut i datakø
      *
     c                   eval      d_enhl = vvenhl
     c                   eval      d_behl = w_behl_alf
     c                   eval      d_bbes = w_bbes_alf
     c                   eval      d_bord = w_bord_alf
     c                   eval      d_disp = w_disp_alf
      *
     c     end_lager     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c*    *pssr         begsr
      *
     c*                  eval      p_feil = *on
     c*                  goto      avslutt
      *
     c*                  endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_uniq
     c                   parm                    p_dtqi
     c                   parm                    p_dtqo
     c                   parm                    p_bibl
     c                   parm                    p_feil
     c                   parm                    p_firm
     c                   parm                    p_lage
     c                   parm                    p_inpl
     c                   parm                    p_outl
     c                   parm                    p_wait
     c                   parm                    p_keyo
     c                   parm                    p_keyl
     c                   parm                    p_sndl
     c                   parm                    p_sndi
      *
      * Key for oppslag på status
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les på vare-enhet
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
     c                   kfld                    vvenl2_saen
      *
     c     vvenl2_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
      *
      * Key for oppslag på priskode
      *
     c     vpkol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vpkol1_pkod
      *
      * Key for oppslag på lager
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
      *
     c     vlagl1_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
      *
      *
      * Hent firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
      * Hent opplysninger fra ofak koderegister
      *
     c     fstsl1_key    chain     fstsl1r
      *
      * Henter current dato og tid
      *
     c                   time                    w_dato
     c                   time                    w_time
      *
5.70  *
     c                   endsr
