     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RC101R
      * Beskrivelse . . : Inkasso - vedlikehold faste data
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       230902 KOB  Nytt program
      *
7.00  *       010519 sst  Lagt inkassobyrå i RCA0PF + utelate bilagskoder
7.01  *       070519 sst  Åpne for firma 000
7.02  *       260919 sst  Legge inn bilagskoder på kreditnotaer
7.02  *       NB!!!!      Foreløpig *-ut det som går mot register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frca0l1    if   e           k disk    rename(rca0pfr:rca0l1r)
     frca0lr    if   e           k disk    rename(rca0pfr:rca0lrr)
     frca0lu    uf a e           k disk    rename(rca0pfr:rca0lur)
      *
     frc100d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d rca0l1_0fir     s                   like(rc0fir)
     d rca0lr_0fir     s                   like(rc0fir)
     d rca0lu_0fir     s                   like(rc0fir)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(rc0fir)
7.00 d w_mld1          s             50
      *
     d s_0fir          s                   like(rc0fir)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Siden som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b20fir <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   eval      rca0l1_0fir = b10fir
     c     rca0l1_key    setll     rca0l1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Firma
      *
     c                   if        b20fir <> 0
     c                   eval      w_seqe = *blank
     c                   eval      rca0l1_0fir = b20fir
     c     rca0l1_key    setll     rca0l1
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b20fir = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c10fir = b10fir
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k10fir = b10fir
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d10fir = b10fir
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c10fir = b10fir
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c10fir = b20fir
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
7.01 c*                  if        c10fir = 0
7.01 c*                  goto      end_c1bld
7.01 c*                  endif
      *
      * Validering
      *
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      rca0lr_0fir = c10fir
     c     rca0lr_key    chain     rca0lr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c10fir = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b20fir = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      rca0lr_0fir = c10fir
     c     rca0lr_key    chain     rca0lr
      *
     c                   if        %found
     c                   eval      c20txt = rc0txt
     c                   eval      c20cnk = rc0cnk
     c                   eval      c20rnt = rc0rnt
     c                   eval      c20inr = rc0inr
     c                   eval      c20bik = rc0bik
     c                   eval      c20kto = rc0kto
     c                   eval      c20avd = rc0avd
     c                   eval      c20sps = rc0sps
     c                   eval      c20prt = rc0prt
7.00 c                   eval      c2inkf = rc0inf
7.00 c                   eval      c2ubk1 = rcubk1
7.00 c                   eval      c2ubk2 = rcubk2
7.00 c                   eval      c2ubk3 = rcubk3
7.00 c                   eval      c2ubk4 = rcubk4
7.00 c                   eval      c2ubk5 = rcubk5
7.02 c                   eval      c2bkk1 = rcubk1
7.02 c                   eval      c2bkk2 = rcubk2
7.02 c                   eval      c2bkk3 = rcubk3
7.02 c                   eval      c2bkk4 = rcubk4
7.02 c                   eval      c2bkk5 = rcubk5
     c                   eval      c2eusr = rc0usr
     c     *dmy          move      rc0dat        c2edat
     c                   else
     c                   eval      c20txt = *blank
     c                   eval      c20cnk = *blank
     c                   eval      c20rnt = 0
     c                   eval      c20inr = 0
     c                   eval      c20bik = 0
     c                   eval      c20kto = 0
     c                   eval      c20avd = 0
     c                   eval      c20sps = 0
     c                   eval      c20prt = *blank
7.00 c                   eval      c2inkf = *blank
7.00 c                   eval      c2ubk1 = *zero
7.00 c                   eval      c2ubk2 = *zero
7.00 c                   eval      c2ubk3 = *zero
7.00 c                   eval      c2ubk4 = *zero
7.00 c                   eval      c2ubk5 = *zero
7.02 c                   eval      c2ubk1 = *zero
7.02 c                   eval      c2ubk2 = *zero
7.02 c                   eval      c2ubk3 = *zero
7.02 c                   eval      c2ubk4 = *zero
7.02 c                   eval      c2ubk5 = *zero
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c10fir = k10fir
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c                   if        c20txt = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
7.00  *   InkassoByrå
7.00 c                   if            c2inkf <> 'Lindorf'
7.00 c                             and c2inkf <> 'Svea   '
7.00 c                             and c2inkf <> 'Collectio'
7.00 c                   eval      w_mld1 = 'Ukjent Inkassobyrå   +
7.00 c                                                            +
7.00 c                                                '
7.00 c                   call      'AA005R'
7.00 c                   parm                    w_mld1
7.00 c                   goto      c2taga
7.00 c                   endif
      *
      * Oppdatering
      *
     c                   clear                   rca0lur
      *
     c                   eval      rca0lu_0fir = c10fir
     c     rca0lu_key    chain     rca0lur
      *
     c                   eval      rc0txt = c20txt
     c                   eval      rc0cnk = c20cnk
     c                   eval      rc0rnt = c20rnt
     c                   eval      rc0inr = c20inr
     c                   eval      rc0bik = c20bik
     c                   eval      rc0kto = c20kto
     c                   eval      rc0avd = c20avd
     c                   eval      rc0sps = c20sps
     c                   eval      rc0prt = c20prt
7.00 c                   eval      rc0inf = c2inkf
7.00 c                   eval      rcubk1 = c2ubk1
7.00 c                   eval      rcubk2 = c2ubk2
7.00 c                   eval      rcubk3 = c2ubk3
7.00 c                   eval      rcubk4 = c2ubk4
7.00 c                   eval      rcubk5 = c2ubk5
7.02 c                   eval      rcbkk1 = c2ubk1
7.02 c                   eval      rcbkk2 = c2ubk2
7.02 c                   eval      rcbkk3 = c2ubk3
7.02 c                   eval      rcbkk4 = c2ubk4
7.02 c                   eval      rcbkk5 = c2ubk5
      *
      *
     c                   time                    rc0dat
     c                   time                    rc0tim
     c                   eval      rc0usr = l_user
      *
     c                   if        %found
     c                   update    rca0lur
     c                   else
     c                   eval      rc0fir = c10fir
     c                   write     rca0lur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b10txt = rc0txt
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      rca0lr_0fir = d10fir
     c     rca0lr_key    chain     rca0lr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      rca0lu_0fir = d10fir
     c     rca0lu_key    chain     rca0lur
      *
     c                   delete    rca0lur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_0fir = k10fir
      *
      * Fyller kopierings-bildet
      *
     c                   eval      rca0lr_0fir = k10fir
     c     rca0lr_key    chain     rca0lr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k10fir = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      rca0lr_0fir = k10fir
     c     rca0lr_key    chain     rca0lr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c10fir = s_0fir
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      rca0l1
      *
     c                   if        %eof or
     c                             rc0fir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      rca0l1_0fir = rc0fir
      *
     c                   eval      b1valg = 0
     c                   eval      b10fir = rc0fir
     c                   eval      b10txt = rc0txt
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        %eof(rca0l1)
     c     rca0l1_key    setgt     rca0l1
     c                   readp     rca0l1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     rca0l1
      *
     c                   if        %eof or
     c                             rc0fir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      rca0l1_0fir = rc0fir
      *
     c                   enddo
      *
     c                   if        %eof(rca0l1)
     c     rca0l1_key    setll     rca0l1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på firma
      *
     c     rca0l1_key    klist
     c                   kfld                    rca0l1_0fir
      *
      * Key for les på firma
      *
     c     rca0lr_key    klist
     c                   kfld                    rca0lr_0fir
      *
      * Key for oppdatering av firma
      *
     c     rca0lu_key    klist
     c                   kfld                    rca0lu_0fir
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rca0l1_0fir = 0
     c     rca0l1_key    setll     rca0l1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
