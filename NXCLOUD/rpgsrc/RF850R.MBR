      /TITLE  ASOKON Spørring v.h.a. rapportter (Utvalg)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : AS???
      * Program . . . . : BYGGFTPR
      * Beskrivelse . . : Bygger FTP parametre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       111001 MHA  Nytt program
6.10  * 6.10  230611 bsa  Utvidet lengde på filnavn
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fafpapf    o  a e             disk    rename(afpapf:afpapfr)
     faftpl1    if a e           k disk    rename(aftppfr:aftpl1r)
     faftplu    uf a e           k disk    rename(aftppfr:aftplur)
      *
     dLinje1           ds
     d line1                   1     80
     d bruker                  1     36
     d passord                40     50
      *
     dLinje2           ds           200
     d line2                   1    200
      *
      * Definering av parametre
     d p_navn          s                   like(afnavn)
     d p_status        s              1
     d p_konvin        s                   like(afkoni)
     d p_konvinl       s                   like(afkoil)
     d p_konvut        s                   like(afkono)
     d p_konvutl       s                   like(afkool)
     d p_host          s                   like(afhost)
6.10 d*p_xnavn         s             20a
6.10 d p_xnavn         s             30a
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable
     d w_filn          s            150a
      *
     d w_bruker        s             10a
     d w_passord       s             10a
     d w_status        s              1
      *
     d w_teller        s              9a
     d w_kommando      s            200a
     d w_kommlen       s             15p 5 inz(200)
      *
     d aftpl1_navn     s                   like(afnavn)
     d aftplu_navn     s                   like(afnavn)
      *
     d w_firm          s                   like(affirm)
      *
      * Definering av konstanter
      *
      /eject
      *
      *
     c     start         tag
      * Sjekke om FTP forespørsel finnes
     c     aftpl1_key    chain     aftpl1r
     c                   if        not %found
     c                   eval      p_status = 'N'
     c                   goto      avslutt
     c                   endif
      *
      * Setter dato for kjøring
     c     aftplu_key    chain     aftplur
     c                   if        %found
     c                   time                    afkdat
     c                   time                    afktid
     c                   update    aftplur
     c                   endif
      *
     c                   if        aflibt = 'J'
     c                   select
     c                   when      %trim(aftype) = 'GET'
     c                   exsr      get_lib
     c                   goto      ret
     c                   when      %trim(aftype) = 'PUT'
     c                   exsr      put_lib
     c                   goto      ret
     c                   endsl
     c                   endif
      *
      *
      * Setter opp første linje med bruker og passord
     c                   eval      bruker = afuser
     c                   if        afprxu <> *blank
     c                   eval      bruker = %trim(bruker) + '@' +
     c                             %trim(afhost)
     c                   endif
     c                   if        afqyps = 'J'
     c                   eval      w_bruker = afuser
     c                   eval      w_passord = *blank
     c                   eval      w_status = *blank
     c                   call      'AF709R'
     c                   parm                    w_bruker
     c                   parm                    w_passord
     c                   parm                    w_status
     c                   if        w_status = *blank
     c                   eval      passord = w_passord
     c                   eval      bruker = w_bruker
     c                   else
     c                   eval      p_status = 'A'
     c                   goto      avslutt
     c                   endif
     c                   else
     c                   eval      passord = afpass
     c                   endif
     c                   eval      SrcDta = Linje1
     c                   write     afpapfr
      *
      * Setter riktig filformat
     c                   if        affils = 'A'
     c                   eval      line2 = 'namefmt 0'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   else
     c                   eval      line2 = 'namefmt 1'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   endif
      *
      * Setter riktig overføringstype Passiv/aktiv
     c                   if        afspas = 'A'
     c                   eval      line2 = 'sendpasv 0'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   else
     c                   eval      line2 = 'sendpasv 1'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   endif
      *
      * Setter muligens binær overføring
     c                   if        afbina = 'J'
     c                   eval      line2 = 'binary'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   endif
      *
      * Vi skriver inn kommandoen
     c                   select
     c                   when      aftype = 'MGET'
      * Setter lokal bane
     c                   eval      line2 = 'lcd ' + %trim(aflocd)
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   if        afxcm1 <> *blank
     c                   eval      line2 = %trim(afxcm1)
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   endif
     c                   if        afxcm2 <> *blank
     c                   eval      line2 = %trim(afxcm2)
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   endif
     c                   if        afxcm3 <> *blank
     c                   eval      line2 = %trim(afxcm3)
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   endif
     c                   if        afchdr <> *blank
     c                   eval      line2 = %trim(aftype) + ' ' +
     c                             %trim(afchdr) + '/' + %trim(affiln)
     c                   else
     c                   eval      line2 = %trim(aftype) + ' ' +
     c                             %trim(affiln)
     c                   endif
     c                   if        afrepl = 'J'
     c                   eval      line2 = %trim(line2) + ' (replace'
     c                   endif
     c                   when      %trim(aftype) = 'MPUT'
     c                   if        afrepl = 'J'
     c                   eval      line2 = 'SUnique 0'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   else
     c                   eval      line2 = 'SUnique 1'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   endif
      * Setter bane på host for put
     c                   eval      line2 = 'cd ' + %trim(afchdr)
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   if        afxcm1 <> *blank
     c                   eval      line2 = %trim(afxcm1)
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   endif
     c                   if        afxcm2 <> *blank
     c                   eval      line2 = %trim(afxcm2)
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   endif
     c                   if        afxcm3 <> *blank
     c                   eval      line2 = %trim(afxcm3)
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   endif
     c                   if        aflocd <> *blank
     c                   eval      line2 = %trim(aftype) + ' ' +
     c                             %trim(aflocd) + '/' + %trim(affiln)
     c                   else
     c                   eval      line2 = %trim(aftype) + ' ' +
     c                             %trim(affiln)
     c                   endif
     c                   when      %trim(aftype) = 'PUT'
      * Skal filen overskrives??
     c                   if        afrepl = 'J'
     c                   eval      line2 = 'SUnique 0'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   else
     c                   eval      line2 = 'SUnique 1'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   endif
      * Setter bane på host for put
     c                   eval      line2 = 'cd ' + %trim(afchdr)
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
      * Kjører ekstra brukerspesifiserte kommandoer
     c                   if        afxcm1 <> *blank
     c                   eval      line2 = %trim(afxcm1)
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   endif
     c                   if        afxcm2 <> *blank
     c                   eval      line2 = %trim(afxcm2)
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   endif
     c                   if        afxcm3 <> *blank
     c                   eval      line2 = %trim(afxcm3)
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   endif
      * Kjører kommando
     c                   if        aflocd <> *blank
     c                   eval      line2 = %trim(aftype) + ' ' +
     c                             %trim(aflocd) + '/' + %trim(affiln)
     c                   else
     c                   eval      line2 = %trim(aftype) + ' ' +
     c                             %trim(affiln)
     c                   endif
     c                   eval      w_filn = *blank
      * Hvis spesifisert til filnavn
     c                   if        affilt <> *blank
     c                   eval      w_filn = %trim(affilt)
     c                   endif
      * Hvis ekstra navn kommer som parameter
     c                   if        p_xnavn <> *blank
     c                   eval      w_filn = %trim(w_filn) +
     c                             %trim(p_xnavn)
     c                   endif
      * Hvis filen ikke skal hete noe annet etter PUT
     c                   if        w_filn = *blank
     c                   eval      w_filn = %trim(affiln)
     c                   endif
      * Hvis vi skal ha med teller
     c                   if        aftell = 'J'
     c     aftplu_key    chain     aftplur
     c                   if        %found
     c                   if        aftval < (10 ** aftlen - 1)
     c                   eval      aftval = aftval + 1
     c                   else
     c                   eval      aftval = 0
     c                   endif
     c                   update    aftplur
     c                   move      aftval        w_teller
     c                   eval      w_filn = %trim(w_filn) +
     c                             %trim(%subst(w_teller:10-aftlen))
     c                   endif
     c                   endif
     c                   eval      line2 = %trim(line2) +
     c                             ' ' + %trim(w_filn)
     c                   if        afrena <> *blank
     c                   eval      line2 = %trim(line2) +
     c                             %trim(afrena)
     c                   eval      SrcDta = Linje2
     c                   write     afpapfr
     c
     c                   eval      line2 = 'rename ' + %trim(w_filn) +
     c                             %trim(afrena) + ' ' +
     c                             %trim(w_filn)
     c                   endif
     c
     c                   endsl
     c                   eval      SrcDta = Linje2
     c                   write     afpapfr
      *
      * Skal filen(e) slettes??
     c                   if        afslet = 'J' and aftype = 'MGET'
     c                   if        afchdr <> *blank
     c                   eval      line2 = 'mdelete ' + %trim(afchdr) +
     c                             '/' + %trim(affiln)
     c                   else
     c                   eval      line2 = 'mdelete ' +
     c                             %trim(affiln)
     c                   endif
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   endif
      *
     c     ret           tag
      *
      * Vi legger inn slutt kommandoen
     c                   eval      line2 = 'quit'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
      *
      * Vi gir tilbake parametre ang. host etc...
     c                   if        afprxu <> *blank
     c                   eval      p_host = afprxu
     c                   else
     c                   eval      p_host = afhost
     c                   endif
     c                   eval      p_konvin = afkoni
     c                   eval      p_konvinl = afkoil
     c                   eval      p_konvut = afkono
     c                   eval      p_konvutl = afkool
      *
     c                   if        afbkup <> *blank
     c*                  eval      w_kommando = 'CPYF ' +
     c*                            'FROMFILE(' +
     c*                            %trim(affiln)
     c*                  call(e)   'QCMDEXC'
     c*                  parm                    w_cmdstr
     c*                  parm                    w_cmdlen
     c                   endif
      *
     c     avslutt       tag
     c                   eval      *inlr = *on
     c                   return
      *
      *
      * -------------------------------------------------------------
      *
     c     get_lib       begsr
      *
      * Fjerne gammel savefile
     c                   eval      w_kommando = 'DLTF FILE(' +
     c                             'ASP.SAVF/' + %trim(affiln) +
     c                             ')'
     c                   call(e)   'QCMDEXC'
     c                   parm                    w_kommando
     c                   parm                    w_kommlen
      * Lage ny savefile
     c                   eval      w_kommando = 'CRTSAVF FILE(' +
     c                             'ASP.SAVF/' + %trim(affiln) +
     c                             ')'
     c                   call(e)   'QCMDEXC'
     c                   parm                    w_kommando
     c                   parm                    w_kommlen
     c                   if        %error
     c                   goto      end_libget
     c                   endif
      *
      * Lage parameterfilen for overføring
      * Setter opp første linje med bruker og passord
     c                   eval      bruker = afuser
     c                   if        afprxu <> *blank
     c                   eval      bruker = %trim(bruker) + '@' +
     c                             %trim(afhost)
     c                   endif
     c                   if        afqyps = 'J'
     c                   eval      w_bruker = afuser
     c                   eval      w_passord = *blank
     c                   eval      w_status = *blank
     c                   call      'AF709R'
     c                   parm                    w_bruker
     c                   parm                    w_passord
     c                   parm                    w_status
     c                   if        w_status = *blank
     c                   eval      passord = w_passord
     c                   eval      bruker = w_bruker
     c                   else
     c                   eval      p_status = 'A'
     c                   goto      avslutt
     c                   endif
     c                   else
     c                   eval      passord = afpass
     c                   endif
     c                   eval      SrcDta = Linje1
     c                   write     afpapfr
      * Passiv/aktiv mode
     c                   if        afpass = 'P'
     c                   eval      line2 = 'sendpasv 1'
     c                   else
     c                   eval      line2 = 'sendpasv 0'
     c                   endif
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
      * AS/400 filformat
     c                   eval      line2 = 'namefmt 0'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
      * Binær overføring
     c                   eval      line2 = 'binary'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
      * Slette savefile på host
     c                   eval      line2 = 'QUOTE RCMD DLTF FILE(' +
     c                             'ASP.SAVF/' + %trim(affiln) +
     c                             ')'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
      * Lage savefile på host
     c                   eval      line2 = 'QUOTE RCMD CRTSAVF ' +
     c                             'FILE(ASP.SAVF/' +
     c                             %trim(affiln) + ')'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
      * Lagre bibliotek på host
     c                   eval      line2 = 'QUOTE RCMD SAVLIB ' +
     c                             'LIB(' + %trim(affiln) +
     c                             ') DEV(*SAVF) SAVF(ASP.SAVF/' +
     c                             %trim(affiln) + ') TGTRLS(' +
     c                             '*PRV)'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
      * Hente savefil fra host
     c                   eval      line2 = 'GET ASP.SAVF/' +
     c                             %trim(affiln) + ' ASP.SAVF/' +
     c                             %trim(affiln) + ' (replace'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
      * Slette savefile på host
     c                   eval      line2 = 'QUOTE RCMD DLTF FILE(' +
     c                             'ASP.SAVF/' + %trim(affiln) +
     c                             ')'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c
     c     end_libget    endsr
      *
      * ---------------------------------------------------------
      *
     c     put_lib       begsr
      *
      * Fjerne gammel savefile
     c                   eval      w_kommando = 'DLTF FILE(' +
     c                             'ASP.SAVF/' + %trim(affiln) +
     c                             ')'
     c                   call(e)   'QCMDEXC'
     c                   parm                    w_kommando
     c                   parm                    w_kommlen
      * Lage ny savefile
     c                   eval      w_kommando = 'CRTSAVF FILE(' +
     c                             'ASP.SAVF/' + %trim(affiln) +
     c                             ')'
     c                   call(e)   'QCMDEXC'
     c                   parm                    w_kommando
     c                   parm                    w_kommlen
     c                   if        %error
     c                   goto      end_putl
     c                   endif
      * Lagre bibliotek til savefile
     c                   eval      w_kommando = 'SAVLIB LIB(' +
     c                             %trim(affiln) + ') DEV(*SAVF) ' +
     c                             'SAVF(ASP.SAVF/' + %trim(affiln) +
     c                             ') TGTRLS(*PRV)'
     c                   call(e)   'QCMDEXC'
     c                   parm                    w_kommando
     c                   parm                    w_kommlen
     c                   if        %error
     c                   goto      end_putl
     c                   endif
      *
      * Kopiere savefile til IFS hvis Zipping er ønskelig
     c                   if        afzipl = 'J'
     c                   eval      w_kommando = 'CPYTOSTMF FROMMBR(''' +
     c                             '/QSYS.LIB/ASP.SAVF.LIB/' +
     c                             %trim(affiln) + '.FILE'') ' +
     c                             'TOSTMF(''/temp/' + %trim(affiln) +
     c                             ''') STMFOPT(*REPLACE)'
     c                   call(e)   'QCMDEXC'
     c                   parm                    w_kommando
     c                   parm                    w_kommlen
     c                   if        %error
     c                   goto      end_putl
     c                   endif
      * Zipper
     c                   eval      w_kommando = 'AZ700C FILE1(''' +
     c                             '/temp/' + %trim(affiln) +
     c                             ''') ZIPF(''/temp/' +
     c                             %trim(affiln) + '.zip'')'
     c                   call(e)   'QCMDEXC'
     c                   parm                    w_kommando
     c                   parm                    w_kommlen
     c                   if        %error
     c                   goto      end_putl
     c                   endif
     c                   endif
      *
      * Lage parameterfilen for overføring
      * Setter opp første linje med bruker og passord
     c                   eval      bruker = afuser
     c                   if        afprxu <> *blank
     c                   eval      bruker = %trim(bruker) + '@' +
     c                             %trim(afhost)
     c                   endif
     c                   if        afqyps = 'J'
     c                   eval      w_bruker = afuser
     c                   eval      w_passord = *blank
     c                   eval      w_status = *blank
     c                   call      'AF709R'
     c                   parm                    w_bruker
     c                   parm                    w_passord
     c                   parm                    w_status
     c                   if        w_status = *blank
     c                   eval      passord = w_passord
     c                   eval      bruker = w_bruker
     c                   else
     c                   eval      p_status = 'A'
     c                   goto      avslutt
     c                   endif
     c                   else
     c                   eval      passord = afpass
     c                   endif
     c                   eval      SrcDta = Linje1
     c                   write     afpapfr
      * Passiv/aktiv mode
     c                   if        afpass = 'P'
     c                   eval      line2 = 'sendpasv 1'
     c                   else
     c                   eval      line2 = 'sendpasv 0'
     c                   endif
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
      *
      * Hvis det skal brukes zipping må vi bruke en annen parm fil
      *
     c                   if        afzipl = 'J'
     c                   exsr      put_w_zip
     c                   goto      end_putl
     c                   endif
      *
      * AS/400 filformat
     c                   eval      line2 = 'namefmt 0'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
      * Binær overføring
     c                   eval      line2 = 'binary'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
      * Slette savefile på host
     c                   eval      line2 = 'QUOTE RCMD DLTF FILE(' +
     c                             'ASP.SAVF/' + %trim(affiln) +
     c                             ')'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
      * Lage savefile på host
     c                   eval      line2 = 'QUOTE RCMD CRTSAVF ' +
     c                             'FILE(ASP.SAVF/' +
     c                             %trim(affiln) + ')'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
      * Sette riktig bane på host
     c                   eval      line2 = 'CD ASP.SAVF'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
      * Overføring
     c                   eval      line2 = 'PUT ' +
     c                             'ASP.SAVF/' + %trim(affiln) +
     c                             ' ASP.SAVF/' + %trim(affiln)
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
      * Pakke ut savefile på host
     c                   eval      line2 = 'QUOTE RCMD RSTLIB ' +
     c                             'SAVLIB(' + %trim(affiln) +
     c                             ') DEV(*SAVF) SAVF(ASP.SAVF/' +
     c                             %trim(affiln) + ')'
     c                   if        affilt <> *blank
     c                   eval      line2 = %trim(line2) +
     c                             ' RSTLIB(' +
     c                             %trim(affilt) + ')'
     c                   endif
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
      *
      * Fjerne lokal savefile
     c*                  eval      w_kommando = 'DLTF FILE(' +
     c*                            'ASP.SAVF/' + %trim(affiln) +
     c*                            ')'
     c*                  call(e)   'QCMDEXC'
     c*                  parm                    w_kommando
     c*                  parm                    w_kommlen
      *
     c     end_putl      endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for pakking til ZIP
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     put_w_zip     begsr
      *
      * PC filformat
     c                   eval      line2 = 'namefmt 1'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
      * Binær overføring
     c                   eval      line2 = 'binary'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
      * Slette savefile på host
     c                   eval      line2 = 'QUOTE RCMD DLTF FILE(' +
     c                             'ASP.SAVF/' + %trim(affiln) +
     c                             ')'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
      * Lage savefile på host
     c                   eval      line2 = 'QUOTE RCMD CRTSAVF ' +
     c                             'FILE(ASP.SAVF/' +
     c                             %trim(affiln) + ')'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
      * Sette riktig bane på host
     c                   eval      line2 = 'CD /temp'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
      * Overføring
     c                   eval      line2 = 'PUT /temp/' +
     c                             %trim(affiln) + '.zip ' +
     c                             '/temp/' + %trim(affiln) +
     c                             '.zip'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
      * Zippe ut
     c                   eval      line2 = 'QUOTE RCMD ADDENVVAR ' +
     c                             'ENVVAR(CLASSPATH) VALUE(''' +
     c                             '/JAVA'')'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   eval      line2 = 'QUOTE RCMD AZ701C ' +
     c                             'ZIPF(''/temp/' +
     c                             %trim(affiln) + '.zip'') ' +
     c                             'DIR(''/temp/'')'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
      * Kopiere fra IFS til savefile
     c                   eval      line2 = 'QUOTE RCMD CPYFRMSTMF ' +
     c                             'FROMSTMF(''/temp/' +
     c                             %trim(affiln) + ''') TOMBR(''' +
     c                             '/qsys.lib/asp.savf.lib/' +
     c                             %trim(affiln) + '.file'') ' +
     c                             'MBROPT(*REPLACE)'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
      * Fjerne filer fra IFS
     c                   eval      line2 = 'QUOTE RCMD DEL ' +
     c                             'OBJLNK(''/temp/' +
     c                             %trim(affiln) + '.zip'')'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c                   eval      line2 = 'QUOTE RCMD DEL ' +
     c                             'OBJLNK(''/temp/' +
     c                             %trim(affiln) + ''')'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
      * Pakke ut savefile på host
     c                   eval      line2 = 'QUOTE RCMD RSTLIB ' +
     c                             'SAVLIB(' + %trim(affiln) +
     c                             ') DEV(*SAVF) SAVF(ASP.SAVF/' +
     c                             %trim(affiln) + ')'
     c                   if        affilt <> *blank
     c                   eval      line2 = %trim(line2) +
     c                             ' RSTLIB(' +
     c                             %trim(affilt) + ')'
     c                   endif
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
      * Slette savefile
     c                   eval      line2 = 'QUOTE RCMD DLTF ' +
     c                             'FILE(asp.savf/' +
     c                             %trim(affiln) + ')'
     c                   eval      SrcDta = linje2
     c                   write     afpapfr
     c
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     aftpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    aftpl1_navn
      *
     c     aftplu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    aftplu_navn
      *
     c                   eval      w_firm = l_firm
      *
     c     *entry        plist
     c                   parm                    p_navn
     c                   parm                    p_status
     c                   parm                    p_konvin
     c                   parm                    p_konvinl
     c                   parm                    p_konvut
     c                   parm                    p_konvutl
     c                   parm                    p_host
     c                   parm                    p_xnavn
      *
     c                   eval      aftpl1_navn = p_navn
     c                   eval      aftplu_navn = p_navn
      *
     c                   endsr
      *
