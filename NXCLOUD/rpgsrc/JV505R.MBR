     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JV505R
      * Beskrivelse . . : Vare, spørring - fra ordre
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       140504 HKO  Nytt program
      *       140905 HKO  Lagt inn frisøk
os71  * os71  161210 bsa  Lagt inn korreksjoner på SQL pga. OS/400 feil
os71  *                   Det er også lagt inn endring på SQL,
os71  *                   som er nødvendig ved overgang til v6.1
6.3q  *       260515 bof  Datoformat sql
6.31  *       041018 bof  Innføring av logistikk-varenr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvsost    if   e           k disk    rename(jvsost:jvsostr)
      *
     fjvarla    if   e           k disk    rename(jvarpfr:jvarlar)
     fjvarlb    if   e           k disk    rename(jvarpfr:jvarlbr)
     fjvarlc    if   e           k disk    rename(jvarpfr:jvarlcr)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjvarl2    if   e           k disk    rename(jvarpfr:jvarl2r)
     fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
     fjvarl4    if   e           k disk    rename(jvarpfr:jvarl4r)
     fjsokl3    if   e           k disk    rename(jsokpfr:jsokl3r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
     fjv505d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_ogrp          s                   like(jvogrp)
     d p_hgrp          s                   like(jvhgrp)
     d p_ugrp          s                   like(jvugrp)
     d p_stek          s             35
     d p_vare          s                   like(jvvare)
     d p_tek1          s                   like(jvtek1)
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variable i nøkler
      *
     d jvarla_ogrp     s                   like(jvogrp)
     d jvarlb_ogrp     s                   like(jvogrp)
     d jvarlb_hgrp     s                   like(jvhgrp)
     d jvarlc_ogrp     s                   like(jvogrp)
     d jvarlc_hgrp     s                   like(jvhgrp)
     d jvarlc_ugrp     s                   like(jvugrp)
     d jvarl1_vare     s                   like(jvvare)
     d jvarl2_tek1     s                   like(jvtek1)
     d jvarl3_nobb     s                   like(jvnobb)
     d jvarl4_lvar     s                   like(jvlvar)
     d jsokl3_vare     s                   like(jsvare)
     d jsokl3_stek     s                   like(jsstek)
     d jlevl1_ldor     s                   like(jlldor)
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              2
     d b_open_sok      s              1    inz(*off)
     d b_valg_ok       s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_ste1          s             35
     d w_ste2          s             35
     d w_ste3          s             35
     d w_ste4          s             35
     d w_ste5          s             35
     d w_leng          s              2  0
     d w_otxt          s             35
     d w_htxt          s             35
     d w_utxt          s             35
     d w_udat          s               d   datfmt(*iso)
6.3i d w_logi          s              9  0
      *
     d s_stek          s             35
     d s_ftek          s             35
      *
     d w_vare          s                   like(jvvare)
      *
     d s_vare          s                   like(jvvare)
     d s_ogrp          s                   like(jvogrp)
     d s_hgrp          s                   like(jvhgrp)
     d s_ugrp          s                   like(jvugrp)
      *
      * Konstanter
      *
     d c_sfil          s              2  0 inz(8)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO
6.3q C/End-Exec
      *
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * F9=Gjenta søk
      *
     c                   if        *inki = *on
     c                   eval      *in22 = *on
     c                   eval      b2ogrp = s_ogrp
     c                   eval      b2hgrp = s_hgrp
     c                   eval      b2ugrp = s_ugrp
     c                   eval      b2stek = s_stek
     c                   eval      b2ftek = s_ftek
     c                   goto      b2tagb
     c                   endif
      *
      * F23=Bytt informasjon (Varenr/Nobbnr/Lev.varenr)
      *
     c                   if        *inkx = *on
      *
     c                   if        *in80 = *on
     c                   eval      *in80 = *off
     c                   eval      *in81 = *on
     c                   exsr      forny                                        CRT SFL
     c                   goto      b2taga
     c                   endif
      *
     c                   if        *in81 = *on
     c                   eval      *in81 = *off
     c                   eval      *in82 = *on
     c                   exsr      forny                                        CRT SFL
     c                   goto      b2taga
     c                   endif
      *
     c                   if        *in82 = *on
     c                   eval      *in82 = *off
     c                   eval      *in80 = *on
     c                   exsr      forny                                        CRT SFL
     c                   goto      b2taga
     c                   endif
      *
     c                   endif
      *
      * Søking
      *
     c                   if        b2ogrp <> 0 or
     c                             b2hgrp <> 0 or
     c                             b2ugrp <> 0 or
     c                             b2stek <> *blank or
     c                             b2ftek <> *blank
     c                   exsr      sØk
     c                   goto      b2tagb
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2tek1 <> *blank or
     c                             b2vare <> 0 or
     c                             b2nobb <> 0 or
     c                             b2lvar <> *blank
     c                   exsr      posisjoner
     c                   goto      b2taga
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   if        b_valg_ok = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'O '
     c     jvarla_key    setll     jvarla
     c                   when      w_seqe = 'H '
     c     jvarlb_key    setll     jvarlb
     c                   when      w_seqe = 'U '
     c     jvarlc_key    setll     jvarlc
     c                   when      w_seqe = 'S ' or
     c                             w_seqe = 'SO' or
     c                             w_seqe = 'SH' or
     c                             w_seqe = 'SU' or
     c                             w_seqe = 'F ' or
     c                             w_seqe = 'FO' or
     c                             w_seqe = 'FH' or
     c                             w_seqe = 'FU'
     c                   eval      b_open_sok = *on
     c                   when      w_seqe = 'V '
     c                   eval      jvarl1_vare = b1vare
     c     jvarl1_key    setll     jvarl1
     c                   when      w_seqe = 'N '
     c                   eval      jvarl3_nobb = b1nobb
     c     jvarl3_key    setll     jvarl3
     c                   when      w_seqe = 'L '
     c                   eval      jvarl4_lvar = b1lvar
     c     jvarl4_key    setll     jvarl4
     c                   other
     c                   eval      jvarl2_tek1 = b1tek1
     c     jvarl2_key    setll     jvarl2
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for søking i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sØk           begsr
      *
      * Validering av varegruppe
      *
     c                   if        b2ogrp = 0 and
     c                             b2hgrp <> 0
     c                   eval      *in31 = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b2hgrp = 0 and
     c                             b2ugrp <> 0
     c                   eval      *in32 = *on
     c                   leavesr
     c                   endif
      *
      * Tar vare på søk
      *
     c                   eval      s_ogrp = b2ogrp
     c                   eval      s_hgrp = b2hgrp
     c                   eval      s_ugrp = b2ugrp
     c                   eval      s_stek = b2stek
     c                   eval      s_ftek = b2ftek
      *
      * Søk via søketekst
      *
     c                   if        b2stek <> *blank
      *
     c                   eval      b_open_sok = *on
      *
     c                   call      'AS702R'
     c                   parm                    b2stek
     c                   parm                    w_ste1
     c                   parm                    w_ste2
     c                   parm                    w_ste3
     c                   parm                    w_ste4
     c                   parm                    w_ste5
      *
     c                   select
     c                   when      b2ogrp = 0 and
     c                             b2hgrp = 0 and
     c                             b2ugrp = 0
     c                   eval      w_seqe = 'S '
     c                   when      b2ogrp <> 0 and
     c                             b2hgrp = 0 and
     c                             b2ugrp = 0
     c                   eval      w_seqe = 'SO'
     c                   when      b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp = 0
     c                   eval      w_seqe = 'SH'
     c                   when      b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp <> 0
     c                   eval      w_seqe = 'SU'
     c                   endsl
      *
     c                   goto      end_sØk
      *
     c                   endif
      *
      * Søk via fritekst
      *
     c                   if        b2ftek <> *blank
      *
     c                   eval      b_open_sok = *on
      *
     c                   call      'AS701R'
     c                   parm                    b2ftek
     c                   parm                    w_ste1
     c                   parm                    w_ste2
     c                   parm                    w_ste3
     c                   parm                    w_ste4
     c                   parm                    w_ste5
      *
     c                   select
     c                   when      b2ogrp = 0 and
     c                             b2hgrp = 0 and
     c                             b2ugrp = 0
     c                   eval      w_seqe = 'F '
     c                   when      b2ogrp <> 0 and
     c                             b2hgrp = 0 and
     c                             b2ugrp = 0
     c                   eval      w_seqe = 'FO'
     c                   when      b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp = 0
     c                   eval      w_seqe = 'FH'
     c                   when      b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp <> 0
     c                   eval      w_seqe = 'FU'
     c                   endsl
      *
     c                   goto      end_sØk
      *
     c                   endif
      *
      * Søk på overgruppe
      *
     c                   if        b2ogrp <> 0 and
     c                             b2hgrp =  0 and
     c                             b2ugrp =  0
     c                   eval      w_seqe = 'O '
     c                   eval      jvarla_ogrp = b2ogrp
     c     jvarla_key    setll     jvarla
     c                   goto      end_sØk
     c                   endif
      *
      * Søk på hovedgruppe
      *
     c                   if        b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp =  0
     c                   eval      w_seqe = 'H '
     c                   eval      jvarlb_ogrp = b2ogrp
     c                   eval      jvarlb_hgrp = b2hgrp
     c     jvarlb_key    setll     jvarlb
     c                   goto      end_sØk
     c                   endif
      *
      * Søk på undergruppe
      *
     c                   if        b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp <> 0
     c                   eval      w_seqe = 'U '
     c                   eval      jvarlc_ogrp = b2ogrp
     c                   eval      jvarlc_hgrp = b2hgrp
     c                   eval      jvarlc_ugrp = b2ugrp
     c     jvarlc_key    setll     jvarlc
     c                   goto      end_sØk
     c                   endif
      *
     c     end_sØk       tag
      *
      * Blanker subfile, og fyller opp i.h.t. søk
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker søkefelt
      *
     c                   eval      b2ogrp = 0
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   eval      b2stek = *blank
     c                   eval      b2ftek = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Varetekst
      *
     c                   if        b2tek1 <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      jvarl2_tek1 = b2tek1
     c     jvarl2_key    setll     jvarl2
     c                   goto      end_pos
     c                   endif
      *
      * Varenummer
      *
     c                   if        b2vare <> 0
     c                   eval      w_seqe = 'V '
     c                   eval      *in80 = *on
     c                   eval      *in81 = *off
     c                   eval      *in82 = *off
     c                   movel     b2vare        jvarl1_vare
     c     jvarl1_key    setll     jvarl1
     c                   goto      end_pos
     c                   endif
      *
      * Nobb-nummer
      *
     c                   if        b2nobb <> 0
     c                   eval      w_seqe = 'N '
     c                   eval      *in80 = *off
     c                   eval      *in81 = *on
     c                   eval      *in82 = *off
     c                   eval      jvarl3_nobb = b2nobb
     c     jvarl3_key    setll     jvarl3
     c                   goto      end_pos
     c                   endif
      *
      * Lev. varenummer
      *
     c                   if        b2lvar <> *blank
     c                   eval      w_seqe = 'L '
     c                   eval      *in80 = *off
     c                   eval      *in81 = *off
     c                   eval      *in82 = *on
     c                   eval      jvarl4_lvar = b2lvar
     c     jvarl4_key    setll     jvarl4
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2tek1 = *blank
     c                   eval      b2vare = 0
     c                   eval      b2nobb = 0
     c                   eval      b2lvar = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
     c                   movel     b1vare        w_vare
      *
      * Velg
      *
     c                   if        b1valg = 1
     c                   eval      p_vare = b1vare
     c                   eval      p_tek1 = b1tek1
     c                   eval      b_valg_ok = *on
     c                   leave
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   call      'JV510R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   eval      s_vare = *blank
      *
      * Initierer SQL for søk
      *
     c                   if        b_open_sok = *on
      *
     c                   select
     c                   when      w_seqe = 'SO'
     c/exec sql
     c+                  declare sok_so cursor for
os71 c+                  select s.jsvare,
os71 c+                         s.jsstek,
os71 c+                         v.jvvare,
os71 c+                         v.jvogrp,
os71 c+                         v.jvtek1,
os71 c+                         v.jvnobb,
os71 c+                         v.jvlvar,
os71 c+                         v.jvldor
os71 c+                  from   jsokpf as s inner join
os71 c+                         jvarpf as v on
os71 c+                         s.jsvare = v.jvvare
os71 c+                  where  v.jvogrp = :b2ogrp and
os71 c+                         v.jvstat <> 4 and
os71 c+                         v.jvudat < :w_udat and
6.31 c+                         s.jsstek like :w_ste1 and
6.31 c+                         v.jvnobb not in
6.31 c+                        (select jvquno from jvklpf
6.e1 c+                         where jvqfir = :w_firm)
os71 c+                  order by v.jvtek1, v.jvvare
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_so
     c/end-exec
     c/exec sql
     c+                  open sok_so
     c/end-exec
      *
     c                   when      w_seqe = 'SH'
     c/exec sql
     c+                  declare sok_sh cursor for
os71 c+                  select s.jsvare,
os71 c+                         s.jsstek,
os71 c+                         v.jvvare,
os71 c+                         v.jvogrp,
os71 c+                         v.jvhgrp,
os71 c+                         v.jvtek1,
os71 c+                         v.jvnobb,
os71 c+                         v.jvlvar,
os71 c+                         v.jvldor
os71 c+                  from   jsokpf as s inner join
os71 c+                         jvarpf as v on
os71 c+                         s.jsvare = v.jvvare
os71 c+                  where  v.jvogrp = :b2ogrp and
os71 c+                         v.jvhgrp = :b2hgrp and
os71 c+                         v.jvstat <> 4 and
os71 c+                         v.jvudat < :w_udat and
6.11 c+                         s.jsstek like :w_ste1 and
6.31 c+                         v.jvnobb not in
6.31 c+                        (select jvquno from jvklpf
6.e1 c+                         where jvqfir = :w_firm)
os71 c+                  order by v.jvtek1, v.jvvare
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_sh
     c/end-exec
     c/exec sql
     c+                  open sok_sh
     c/end-exec
      *
     c                   when      w_seqe = 'SU'
     c/exec sql
     c+                  declare sok_su cursor for
os71 c+                  select s.jsvare,
os71 c+                         s.jsstek,
os71 c+                         v.jvvare,
os71 c+                         v.jvogrp,
os71 c+                         v.jvhgrp,
os71 c+                         v.jvugrp,
os71 c+                         v.jvtek1,
os71 c+                         v.jvnobb,
os71 c+                         v.jvlvar,
os71 c+                         v.jvldor
os71 c+                  from   jsokpf as s inner join
os71 c+                         jvarpf as v on
os71 c+                         s.jsvare = v.jvvare
os71 c+                  where  v.jvogrp = :b2ogrp and
os71 c+                         v.jvhgrp = :b2hgrp and
os71 c+                         v.jvugrp = :b2ugrp and
os71 c+                         v.jvstat <> 4 and
os71 c+                         v.jvudat < :w_udat and
6.31 c+                         s.jsstek like :w_ste1  and
6.31 c+                         v.jvnobb not in
6.31 c+                        (select jvquno from jvklpf
6.e1 c+                         where jvqfir = :w_firm)
os71 c+                  order by v.jvtek1, v.jvvare
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_su
     c/end-exec
     c/exec sql
     c+                  open sok_su
     c/end-exec
      *
     c                   when      w_seqe = 'S '
     c/exec sql
     c+                  declare sok_s cursor for
os71 c+                  select s.jsvare,
os71 c+                         s.jsstek,
os71 c+                         v.jvvare,
os71 c+                         v.jvtek1,
os71 c+                         v.jvnobb,
os71 c+                         v.jvlvar,
os71 c+                         v.jvldor
os71 c+                  from   jsokpf as s inner join
os71 c+                         jvarpf as v on
os71 c+                         s.jsvare = v.jvvare
os71 c+                  where  v.jvstat <> 4 and
os71 c+                         v.jvudat < :w_udat and
6.31 c+                         s.jsstek like :w_ste1  and
6.31 c+                         v.jvnobb not in
6.31 c+                        (select jvquno from jvklpf
6.e1 c+                         where jvqfir = :w_firm)
os71 c+                  order by v.jvtek1, v.jvvare
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_s
     c/end-exec
     c/exec sql
     c+                  open sok_s
     c/end-exec
      *
     c                   when      w_seqe = 'FO'
     c/exec sql
     c+                  declare sok_fo cursor for
os71 c+                  select s.jvsvar,
os71 c+                         s.jvstek,
os71 c+                         v.jvvare,
os71 c+                         v.jvogrp,
os71 c+                         v.jvtek1,
os71 c+                         v.jvnobb,
os71 c+                         v.jvlvar,
os71 c+                         v.jvldor
os71 c+                  from   jvsost as s inner join
os71 c+                         jvarpf as v on
os71 c+                         s.jvsvar = v.jvvare
os71 c+                  where  v.jvogrp = :b2ogrp and
os71 c+                         v.jvstat <> 4 and
os71 c+                         v.jvudat < :w_udat and
os71 c+                         s.jvstek like :w_ste1 and
os71 c+                         s.jvstek like :w_ste2 and
os71 c+                         s.jvstek like :w_ste3 and
os71 c+                         s.jvstek like :w_ste4 and
6.31 c+                         s.jvstek like :w_ste5 and
6.31 c+                         v.jvnobb not in
6.31 c+                        (select jvquno from jvklpf
6.e1 c+                         where jvqfir = :w_firm)
os71 c+                  order by s.jvstek, s.jvsvar
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_fo
     c/end-exec
     c/exec sql
     c+                  open sok_fo
     c/end-exec
      *
     c                   when      w_seqe = 'FH'
     c/exec sql
     c+                  declare sok_fh cursor for
os71 c+                  select s.jvsvar,
os71 c+                         s.jvstek,
os71 c+                         v.jvvare,
os71 c+                         v.jvogrp,
os71 c+                         v.jvhgrp,
os71 c+                         v.jvtek1,
os71 c+                         v.jvnobb,
os71 c+                         v.jvlvar,
os71 c+                         v.jvldor
os71 c+                  from   jvsost as s inner join
os71 c+                         jvarpf as v on
os71 c+                         s.jvsvar = v.jvvare
os71 c+                  where  v.jvogrp = :b2ogrp and
os71 c+                         v.jvhgrp = :b2hgrp and
os71 c+                         v.jvstat <> 4 and
os71 c+                         v.jvudat < :w_udat and
os71 c+                         s.jvstek like :w_ste1 and
os71 c+                         s.jvstek like :w_ste2 and
os71 c+                         s.jvstek like :w_ste3 and
os71 c+                         s.jvstek like :w_ste4 and
6.31 c+                         s.jvstek like :w_ste5 and
6.31 c+                         v.jvnobb not in
6.31 c+                        (select jvquno from jvklpf
6.e1 c+                         where jvqfir = :w_firm)
os71 c+                  order by s.jvstek, s.jvsvar
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_fh
     c/end-exec
     c/exec sql
     c+                  open sok_fh
     c/end-exec
      *
     c                   when      w_seqe = 'FU'
     c/exec sql
     c+                  declare sok_fu cursor for
os71 c+                  select s.jvsvar,
os71 c+                         s.jvstek,
os71 c+                         v.jvvare,
os71 c+                         v.jvogrp,
os71 c+                         v.jvhgrp,
os71 c+                         v.jvugrp,
os71 c+                         v.jvtek1,
os71 c+                         v.jvnobb,
os71 c+                         v.jvlvar,
os71 c+                         v.jvldor
os71 c+                  from   jvsost as s inner join
os71 c+                         jvarpf as v on
os71 c+                         s.jvsvar = v.jvvare
os71 c+                  where  v.jvogrp = :b2ogrp and
os71 c+                         v.jvhgrp = :b2hgrp and
os71 c+                         v.jvugrp = :b2ugrp and
os71 c+                         v.jvstat <> 4 and
os71 c+                         v.jvudat < :w_udat and
os71 c+                         s.jvstek like :w_ste1 and
os71 c+                         s.jvstek like :w_ste2 and
os71 c+                         s.jvstek like :w_ste3 and
os71 c+                         s.jvstek like :w_ste4 and
6.31 c+                         s.jvstek like :w_ste5 and
6.31 c+                         v.jvnobb not in
6.31 c+                        (select jvquno from jvklpf
6.e1 c+                         where jvqfir = :w_firm)
os71 c+                  order by s.jvstek, s.jvsvar
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_fu
     c/end-exec
     c/exec sql
     c+                  open sok_fu
     c/end-exec
      *
     c                   when      w_seqe = 'F '
     c/exec sql
     c+                  declare sok_f cursor for
os71 c+                  select s.jvsvar,
os71 c+                         s.jvstek,
os71 c+                         v.jvvare,
os71 c+                         v.jvtek1,
os71 c+                         v.jvnobb,
os71 c+                         v.jvlvar,
os71 c+                         v.jvldor
os71 c+                  from   jvsost as s inner join
os71 c+                         jvarpf as v on
os71 c+                         s.jvsvar = v.jvvare
os71 c+                  where  v.jvstat <> 4 and
os71 c+                         v.jvudat < :w_udat and
os71 c+                         s.jvstek like :w_ste1 and
os71 c+                         s.jvstek like :w_ste2 and
os71 c+                         s.jvstek like :w_ste3 and
os71 c+                         s.jvstek like :w_ste4 and
6.31 c+                         s.jvstek like :w_ste5 and
6.31 c+                         v.jvnobb not in
6.31 c+                        (select jvquno from jvklpf
6.e1 c+                         where jvqfir = :w_firm)
os71 c+                  order by s.jvstek, s.jvsvar
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_f
     c/end-exec
     c/exec sql
     c+                  open sok_f
     c/end-exec
      *
     c                   endsl
      *
     c                   endif
      *
      * Fyller opp subfile
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'O '
     c     jvarla_key    reade     jvarla                                 15
     c                   when      w_seqe = 'H '
     c     jvarlb_key    reade     jvarlb                                 15
     c                   when      w_seqe = 'U '
     c     jvarlc_key    reade     jvarlc                                 15
     c                   when      w_seqe = 'SO'
     c/exec sql
     c+                  fetch next
     c+                  from  sok_so
     c+                  into  :jsvare,
     c+                        :jsstek,
     c+                        :jvvare,
     c+                        :jvogrp,
     c+                        :jvtek1,
     c+                        :jvnobb,
     c+                        :jvlvar,
     c+                        :jvldor
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'SH'
     c/exec sql
     c+                  fetch next
     c+                  from  sok_sh
     c+                  into  :jsvare,
     c+                        :jsstek,
     c+                        :jvvare,
     c+                        :jvogrp,
     c+                        :jvhgrp,
     c+                        :jvtek1,
     c+                        :jvnobb,
     c+                        :jvlvar,
     c+                        :jvldor
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'SU'
     c/exec sql
     c+                  fetch next
     c+                  from  sok_su
     c+                  into  :jsvare,
     c+                        :jsstek,
     c+                        :jvvare,
     c+                        :jvogrp,
     c+                        :jvhgrp,
     c+                        :jvugrp,
     c+                        :jvtek1,
     c+                        :jvnobb,
     c+                        :jvlvar,
     c+                        :jvldor
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'S '
     c/exec sql
     c+                  fetch next
     c+                  from  sok_s
     c+                  into  :jsvare,
     c+                        :jsstek,
     c+                        :jvvare,
     c+                        :jvtek1,
     c+                        :jvnobb,
     c+                        :jvlvar,
     c+                        :jvldor
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'FO'
     c/exec sql
     c+                  fetch next
     c+                  from  sok_fo
     c+                  into  :jvsvar,
     c+                        :jvstek,
     c+                        :jvvare,
     c+                        :jvogrp,
     c+                        :jvtek1,
     c+                        :jvnobb,
     c+                        :jvlvar,
     c+                        :jvldor
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'FH'
     c/exec sql
     c+                  fetch next
     c+                  from  sok_fh
     c+                  into  :jvsvar,
     c+                        :jvstek,
     c+                        :jvvare,
     c+                        :jvogrp,
     c+                        :jvhgrp,
     c+                        :jvtek1,
     c+                        :jvnobb,
     c+                        :jvlvar,
     c+                        :jvldor
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'FU'
     c/exec sql
     c+                  fetch next
     c+                  from  sok_fu
     c+                  into  :jvsvar,
     c+                        :jvstek,
     c+                        :jvvare,
     c+                        :jvogrp,
     c+                        :jvhgrp,
     c+                        :jvugrp,
     c+                        :jvtek1,
     c+                        :jvnobb,
     c+                        :jvlvar,
     c+                        :jvldor
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'F '
     c/exec sql
     c+                  fetch next
     c+                  from  sok_f
     c+                  into  :jvsvar,
     c+                        :jvstek,
     c+                        :jvvare,
     c+                        :jvtek1,
     c+                        :jvnobb,
     c+                        :jvlvar,
     c+                        :jvldor
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'V '
     c                   read      jvarl1                                 15
     c                   when      w_seqe = 'N '
     c                   read      jvarl3                                 15
     c                   when      w_seqe = 'L '
     c                   read      jvarl4                                 15
     c                   other
     c                   read      jvarl2                                 15
     c                   endsl
      *
     c                   if        *in15 = *on
     c                   leave
     c                   endif
6.3i c                   if        w_seqe = 'N' or
6.3i c                             w_seqe = 'L' or
6.3i c                             w_seqe = 'V' or
     c                             w_seqe = *blank
6.31 c                   eval      w_logi = 0
6.3i c/exec sql
6.3i c+ select count(*)
6.3i i+ into   :w_logi
6.3i c+ from   jvklpf
6.3i c+ where  jvquno = :jvvare and jvqfir = :w_firm
6.3i c/end-exec
6.3i c                   if        w_logi <> 0
6.3i c                   iter
6.3i c                   endif
6.3i c                   endif
      *
      * Behandling av søketekst
      *
     c                   if        w_seqe = 'SO' or
     c                             w_seqe = 'SH' or
     c                             w_seqe = 'SU' or
     c                             w_seqe = 'S '
      *
     c                   if        jsvare = s_vare
     c                   iter
     c                   endif
      *
     c                   if        w_ste2 <> *blank
     c                   eval      w_leng = %len(%trim(w_ste2))
     c                   eval      jsokl3_vare = jsvare
     c                   eval      jsokl3_stek = w_ste2
     c     jsokl3_key    setll     jsokl3                                       Søketekst
     c                   read      jsokl3
     c                   if        %eof or
     c                             jvvare <> jsvare or
     c                             w_ste2 <> %subst(jsstek:1:w_leng)
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste3 <> *blank
     c                   eval      w_leng = %len(%trim(w_ste3))
     c                   eval      jsokl3_vare = jsvare
     c                   eval      jsokl3_stek = w_ste3
     c     jsokl3_key    setll     jsokl3                                       Søketekst
     c                   read      jsokl3
     c                   if        %eof or
     c                             jvvare <> jsvare or
     c                             w_ste3 <> %subst(jsstek:1:w_leng)
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste4 <> *blank
     c                   eval      w_leng = %len(%trim(w_ste4))
     c                   eval      jsokl3_vare = jsvare
     c                   eval      jsokl3_stek = w_ste4
     c     jsokl3_key    setll     jsokl3                                       Søketekst
     c                   read      jsokl3
     c                   if        %eof or
     c                             jvvare <> jsvare or
     c                             w_ste4 <> %subst(jsstek:1:w_leng)
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste5 <> *blank
     c                   eval      w_leng = %len(%trim(w_ste5))
     c                   eval      jsokl3_vare = jsvare
     c                   eval      jsokl3_stek = w_ste5
     c     jsokl3_key    setll     jsokl3                                       Søketekst
     c                   read      jsokl3
     c                   if        %eof or
     c                             jvvare <> jsvare or
     c                             w_ste5 <> %subst(jsstek:1:w_leng)
     c                   iter
     c                   endif
     c                   endif
      *
     c                   eval      s_vare = jsvare
      *
     c                   endif
      *
     c                   if        w_seqe = *blank or
     c                             w_seqe = 'V ' or
     c                             w_seqe = 'N ' or
     c                             w_seqe = 'B '
     c                   if        jvstat = 4 or
     c                             jvudat <> *loval and
     c                             jvudat < w_udat
     c                   iter
     c                   endif
     c                   endif
      *
      * Test på at vare ikke finnes i EVR
      *
     c                   eval      vvarl1_vare = jvvare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   iter
     c                   endif
      *
      * Skriver til subfile
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = *blank
     c                   eval      jvarl2_tek1 = jvtek1
     c                   when      w_seqe = 'V '
     c                   eval      jvarl1_vare = jvvare
     c                   when      w_seqe = 'N '
     c                   eval      jvarl3_nobb = jvnobb
     c                   when      w_seqe = 'L '
     c                   eval      jvarl4_lvar = jvlvar
     c                   endsl
      *
     c                   eval      b1vare = jvvare
     c                   eval      b1nobb = jvnobb
     c                   eval      b1lvar = jvlvar
     c                   eval      b1tek1 = jvtek1
     c                   eval      b1nypo = *blank
     c                   eval      b1navn = *blank
      *
     c                   select
     c                   when      *in80 = *on
     c                   eval      b1nypo = jvvare
     c                   when      *in81 = *on
     c                   eval      b1nypo = %editc(jvnobb : 'Z')
     c                   when      *in82 = *on
     c                   eval      b1nypo = jvlvar
     c                   endsl
      *
     c                   eval      jlevl1_ldor = jvldor
     c     jlevl1_key    chain     jlevl1
     c                   if        %found
     c                   eval      b1navn = jlnavn
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   eval      b_open_sok = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        w_seqe = 'O ' or
     c                             w_seqe = 'H ' or
     c                             w_seqe = 'U ' or
     c                             w_seqe = 'S ' or
     c                             w_seqe = 'SO' or
     c                             w_seqe = 'SH' or
     c                             w_seqe = 'SU' or
     c                             w_seqe = 'F ' or
     c                             w_seqe = 'FO' or
     c                             w_seqe = 'FH' or
     c                             w_seqe = 'FU'
     c                   exsr      forny
     c                   leavesr
     c                   endif
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'V '
     c     jvarl1_key    setgt     jvarl1
     c                   readp     jvarl1
     c                   when      w_seqe = 'N '
     c     jvarl3_key    setgt     jvarl3
     c                   readp     jvarl3
     c                   when      w_seqe = 'L '
     c     jvarl4_key    setgt     jvarl4
     c                   readp     jvarl4
     c                   other
     c     jvarl2_key    setgt     jvarl2
     c                   readp     jvarl2
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'V '
     c                   readp     jvarl1
     c                   when      w_seqe = 'N '
     c                   readp     jvarl3
     c                   when      w_seqe = 'L '
     c                   readp     jvarl4
     c                   other
     c                   readp     jvarl2
     c                   endsl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   if        jvstat = 4 or
     c                             jvudat <> *loval and
     c                             jvudat < w_udat
     c                   iter
     c                   endif
      *
     c                   eval      vvarl1_vare = jvvare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   iter
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'V '
     c                   eval      jvarl1_vare = jvvare
     c                   when      w_seqe = 'N '
     c                   eval      jvarl3_nobb = jvnobb
     c                   when      w_seqe = 'L '
     c                   eval      jvarl4_lvar = jvlvar
     c                   other
     c                   eval      jvarl2_tek1 = jvtek1
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'V '
     c     jvarl1_key    setll     jvarl1
     c                   when      w_seqe = 'N '
     c     jvarl3_key    setll     jvarl3
     c                   when      w_seqe = 'L '
     c     jvarl4_key    setll     jvarl4
     c                   other
     c     jvarl2_key    setll     jvarl2
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Overgruppe
      *
     c                   if        w_afld = 'B2OGRP'
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    w_otxt
     c                   leavesr
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_afld = 'B2HGRP'
     c                   eval      b2ugrp = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    w_htxt
     c                   leavesr
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_afld = 'B2UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    b2ugrp
     c                   parm                    w_utxt
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ogrp
     c                   parm                    p_hgrp
     c                   parm                    p_ugrp
     c                   parm                    p_stek
     c                   parm                    p_vare
     c                   parm                    p_tek1
      *
      * Key for søk på overgruppe
      *
     c     jvarla_key    klist
     c                   kfld                    jvarla_ogrp
      *
      * Key for søk på hovedgruppe
      *
     c     jvarlb_key    klist
     c                   kfld                    jvarlb_ogrp
     c                   kfld                    jvarlb_hgrp
      *
      * Key for søk på undergruppe
      *
     c     jvarlc_key    klist
     c                   kfld                    jvarlc_ogrp
     c                   kfld                    jvarlc_hgrp
     c                   kfld                    jvarlc_ugrp
      *
      * Key for posisjonering på varetekst
      *
     c     jvarl2_key    klist
     c                   kfld                    jvarl2_tek1
      *
      * Key for posisjonering på varenummer
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for posisjonering på Nobb-nummer
      *
     c     jvarl3_key    klist
     c                   kfld                    jvarl3_nobb
      *
      * Key for posisjonering på lev. varenummer
      *
     c     jvarl4_key    klist
     c                   kfld                    jvarl4_lvar
      *
      * Key for oppslag på søketekst
      *
     c     jsokl3_key    klist
     c                   kfld                    jsokl3_vare
     c                   kfld                    jsokl3_stek
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på vare (EVR)
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   eval      *in22 = *on
     c                   eval      *in80 = *on
      *
     c                   time                    w_udat
      *
      * Posisjonerer databasen, og fyller opp subfile
      * Behandling avhengig om søk er fortatt eller ikke i ofak
      *
     c                   if        p_ogrp <> 0 or
     c                             p_stek <> *blank
     c                   eval      b2ogrp = p_ogrp
     c                   eval      b2hgrp = p_hgrp
     c                   eval      b2ugrp = p_ugrp
     c                   eval      b2stek = p_stek
     c                   exsr      sØk
     c                   else
     c                   eval      jvarl2_tek1 = *blank
     c     jvarl2_key    setll     jvarl2                                                    .
     c                   exsr      crt_subfile
     c                   endif
      *
     c                   endsr
