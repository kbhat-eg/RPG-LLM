     H/TITLE  ASSRVL Spørrevindu m/vedlikehold
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * System  . . . . : ASOKON                                        *
      * Program . . . . : RP603R                                        *
      * Beskrivelse . . : Spørrevindu m/vedlikehold mot ILONPF          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      *                   Nytt program April-2013                       *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KC = F3  = Exit                                           *
      *       KE = F5  = Forny                                          *
      *       KL = F12 = Annuller                                       *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       10 = Roll                                                 *
      *       12 = Sfldsp                                               *
      *       13 = Sfldspctl                                            *
      *       14 = Sflclr                                               *
      *       15 = Sflend                                               *
      *       16 = Readc 16 on, no more records                         *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     Filonl1    IF   E           K DISK
     F                                     RENAME(ilonpfr:ilonl1r)
     Filonl2    IF   E           K DISK
     F                                     RENAME(ilonpfr:ilonl2r)
     Filonlu    UF A E           K DISK
     F                                     RENAME(ilonpfr:ilonlur)
     Frp603d    CF   E             WORKSTN
     F                                     SFILE(B1WSF:SRRN01)
      *
     D MLD             S             11    DIM(4) CTDATA PERRCD(1)
      *
     d ilonl1_lnr      s                   like(ilrlnr)
     d ilonl2_nav      s                   like(ilrnav)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av felter                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
ad   d w_tilgko        s             10
ad   d w_rutine        s             10
ad   d w_adv           s              3
ad   d w_status        s             10
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      *     nn - er 01 for subfile 01,
      *             02 for subfile 02,
      *             03 for subfile 03, o.s.v.
      * SRRNnn - Relativt recordnummer i subfile
      * SPGEnn - Sidestørrelse i subfile (page)
      * SFRNnn - Første recordnummer på siden
      * SSRNnn - Siste  recordnummer på siden
      * SVRNnn - Vis den side som inneholder denne record.
      *
      * Forklaring på spesielle felter
      * som benyttes ved windows.
      * - - - - - - - - - - - - - - - - -
      *
      * WACTLI - Actual line, angir startlinje for vindu.
      * WACTPO - Actual posision, angir startposisjon for vindu.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * B1WFL  - B1 Subfile - vindu for valg av post
      * B2WCT  - B2 Subfile - vindu control record
      * B2CMD  - B2 Vindu for CMD-taster
      * C2WIN  - C2 Vindu for endre vise opprette
      * D1WIN  - D1 Vindu for slette (Delete)
      * ASUME  -    Record for å styre at vindu legges over
      *             eksisterende data.
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * B2WCT Behandle åpningsbildet med subfile                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Send Alt
      *
     C     B2TAGA        TAG
     C                   Z-ADD     4             WACTLI
     C                   Z-ADD     26            WACTPO
     C                   WRITE     B2CMD
      *
      * Bare data
      *
     C     B2TAGB        TAG
     C                   EXSR      XDSP01
      *
      * Avslutt program
      *
     C     *INKC         CABEQ     *ON           XSLUTT
      *
      * Forny
      *
     C     *INKE         IFEQ      *ON
     C     SFRN01        IFNE      *ZERO
     C     SFRN01        CHAIN     B1WSF                              60
     C                   END
     C                   MOVE      B1LNR         WWLNR
     C                   MOVEL     B1LNAV        WWLNAV
      *                                                                 .
     C     WSEQEN        IFEQ      'A'
     C     ilonl1_key    SETLL     ilonl1
     C                   ELSE
     C     ilonl2_key    SETLL     ilonl2
     C                   END
      *                                                                 .
     C                   EXSR      XCLR01
     C                   EXSR      XCRT01
     C                   GOTO      B2TAGB
     C                   ENDIF
      *
      * Ny post,
      *
     c                   if        *inkf = *on
ad   c                   eval      w_tilgko = l_user
ad   c                   eval      w_rutine = 'VDLPRL'
ad   c                   eval      w_status = *blank
ad   c                   call      'AD005R'
ad   c                   parm                    w_tilgko
ad   c                   parm                    w_rutine
ad   c                   parm                    w_adv
ad   c                   parm                    w_status
ad   c                   if        w_status = 'Sperret'
ad   c                   goto      b2tagb
ad   c                   endif
     c                   call      'RP150R'
     c**                 eval      *in31 = *on
     C                   if        sfrn01 = *zero
     C     sfrn01        chain     b1wsf                              60
     C                   endif
     C                   move      b1lnr         wwlnr
     C                   movel     b1lnav        wwlnav
      *                                                                 .
     C                   if        wseqen = 'A'
     C     ilonl1_key    setll     ilonl1
     C                   else
     C     ilonl2_key    setll     ilonl2
     C                   endif
     c                   exsr      xclr01
     c                   exsr      xcrt01
     c                   goto      b2tagb
     c                   endif
      *
      * Roll
      *
     C     *IN10         IFEQ      *ON
     C                   EXSR      XCRT01
     C                   GOTO      B2TAGB
     C                   ENDIF
      *
      * Posisjoner
      * startverdi
      * på konto
      *
     C     B2LNR         IFNE      *ZERO
     C                   MOVE      ' '           WSEQEN
     C                   MOVE      B2LNR         ilonl1_lnr
     C     ilonl1_key    SETLL     ilonl1
     C                   EXSR      XCLR01
     C                   EXSR      XCRT01
     C                   MOVE      *ZERO         B2LNR
     C                   GOTO      B2TAGB
     C                   ENDIF
      *
      * Posisjoner
      * startverdi
      * på alfafelt
      *
     C     B2LNAV        IFNE      *BLANK
     C                   MOVE      'A'           WSEQEN
     C                   MOVE      *ZERO         WWLNR
     C                   MOVEL     B2LNAV        ilonl2_nav
     C     ilonl2_key    SETLL     ilonl2
     C                   EXSR      XCLR01
     C                   EXSR      XCRT01
     C                   MOVE      *ZERO         B2LNR
     C                   MOVE      *BLANK        B2LNAV
     C                   GOTO      B2TAGB
     C                   ENDIF
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Behandling av subfile                                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Sjekk om valg
      * er foretatt i
      * subfile
      *
     C                   DO        SSRN01
     C                   READC     B1WSF                                  16
     C     *IN16         IFEQ      *ON
     C                   LEAVE
     C                   ENDIF
      *                                                                 .
      * Hente post                                                      .
      *                                                                 .
     C     B1VALG        IFEQ      '1'
     C                   MOVE      B1LNR         PALNR
     C                   MOVEl     B1LNav        PALna
     C                   GOTO      XSLUTT
     C                   ENDIF
      *                                                                 .
      * Endre post                                                      .
      *                                                                 .
     C     B1VALG        IFEQ      '2'
     C                   MOVE      B1LNR         C1LNR
     C                   eval      ilonl1_lnr = b1lnr
     C                   EXSR      XC2WIN
     c                   eval      b1valg = *blank
     c                   update    b1wsf
     C                   ITER
     C                   ENDIF
      *                                                                 .
      * Vise post                                                       .
      *                                                                 .
     C     B1VALG        IFEQ      '5'
     C                   MOVE      *ON           *IN30
     C                   MOVE      B1LNR         C1LNR
     C                   eval      ilonl1_lnr = b1lnr
     C                   EXSR      XC2WIN
     c                   eval      b1valg = *blank
     c                   update    b1wsf
     C                   ITER
     C                   ENDIF
      *                                                                 .
     C                   ENDDO
      *
     C                   GOTO      B2TAGA
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * C1BLD Behandle åpningsbildet for ny post                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C*    C1TAGA        TAG
     C*                  EXFMT     C2WIN
      *
     C*    *INKC         CABEQ     *ON           B2TAGA
     C*    *INKL         CABEQ     *ON           B2TAGA
      *
     C*                  MOVE      C1LNR         WWLNR
      *
     C*                  EXSR      XC2WIN
     C*                  GOTO      C1TAGA
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XSLUTT        TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * C2WIN Behandle bilde for ny - endring - vise                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XC2WIN        BEGSR
     C     ilonl1_key    CHAIN     ilonl1                             60
      *
     C     *IN60         IFEQ      *OFF
     C                   MOVEL     MLD(2)        C2NYEN
     c                   MOVE      ilrnav        C2LNAV
     c                   eval      c2ladr = ilradr
     c                   eval      c2ponr = ilrpnr
     c                   eval      c2pste = ilrste
     c                   eval      c2lavd = ilravd
     c                   eval      c2mobn = ilrmob
     C                   ELSE
     C                   MOVEL     MLD(1)        C2NYEN
     c                   eval      c2lnav  = *blank
     c                   eval      c2ladr  = *blank
     c                   eval      c2ponr  = *zero
     c                   eval      c2pste  = *blank
     c                   eval      c2lavd  = *zero
     c                   eval      c2mobn  = *blank
     C                   ENDIF
      *
     C     *IN30         IFEQ      *ON
     C                   MOVEL     MLD(3)        C2NYEN
     C                   ENDIF
      *
     C                   Z-ADD     7             WACTLI
     C                   Z-ADD     20            WACTPO
     C                   EXFMT     C2WIN
      *
     C     *INKC         CABEQ     *ON           C2TAG1
     C     *INKL         CABEQ     *ON           C2TAG1
     C     *IN30         CABEQ     *ON           C2TAG1
      *
     C     ilonl1_key    CHAIN     ilonlu                             60
      *
     c                   eval      ilrnav = c2lnav
     c                   eval      ilradr = c2ladr
     c                   eval      ilrpnr = c2ponr
     c                   eval      ilrste = c2pste
     c                   eval      ilravd = c2lavd
     c                   eval      ilrmob = c2mobn
      *
     C     *IN60         IFEQ      *OFF
     C                   UPDATE    ilonlur
     C                   ELSE
     C                   MOVE      C1LNR         ilrlnr
     C                   WRITE     ilonlur
     C                   ENDIF
      *
      * Oppdater subfile
      *
     C*    B1VALG        IFEQ      '2'
     C*                  MOVE      C1LNR         B1LNR
     C*                  MOVE      C2LNAV        B1LNAV
     C*                  ENDIF
      *
     C     C2TAG1        TAG
      *
     C*    B1VALG        IFEQ      '2'
     C*    B1VALG        OREQ      '5'
     C*                  MOVE      SRRN01        SVRN01
     C*                  MOVE      *BLANK        B1VALG
     C*                  UPDATE    B1WSF
     C*                  ENDIF
      *
     C                   MOVE      *BLANK        B2LNAV
     C                   MOVE      *ZERO         B2LNR
     C                   MOVE      *OFF          *IN30
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Tøm subfile                                                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XCLR01        BEGSR
     C                   MOVE      *ON           *IN14
     C                   WRITE     B2WCT
     C                   MOVE      *OFF          *IN14
      *
     C                   MOVE      *OFF          *IN15
     C                   Z-ADD     0             SRRN01
     C                   Z-ADD     0             SFRN01
     C                   Z-ADD     0             SSRN01
     C                   Z-ADD     0             SPGE01
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Fyll opp subfile                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -  - -*
      *
     C     XCRT01        BEGSR
     C                   move      *off          *in15
     C                   ADD       7             SPGE01
     C                   Z-ADD     SSRN01        SRRN01
      *
     C     SRRN01        DOUEQ     SPGE01
      *                                                                 .
     C     WSEQEN        IFEQ      'A'
     C                   READ      ilonl1                                 15
     C                   ELSE
     C                   READ      ilonl2                                 15
     C                   END
      *
     C                   if        *in15 = *off and
     C                             ilrkli <> wwfirm
     C                   move      *on           *in15
     C                   endif
      *
     C     *IN15         CABEQ     '1'           XEND01
     C                   ADD       1             SRRN01
     C                   MOVE      ilrlnr        B1LNR
     C                   MOVEL     ilrnav        B1LNAV
     C                   WRITE     B1WSF
     C                   ENDDO
      *
     C     XEND01        TAG
      *
     C     SRRN01        IFGT      SSRN01
     C     SSRN01        ADD       1             SFRN01
     C                   ENDIF
      *
     C                   Z-ADD     SRRN01        SSRN01
     C                   Z-ADD     SFRN01        SVRN01
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Display subfile                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XDSP01        BEGSR
      *
     C     SRRN01        IFNE      0
     C                   MOVE      *ON           *IN12
     C                   ENDIF
      *
     C                   MOVE      *ON           *IN13
     C                   EXFMT     B2WCT
     C                   MOVE      *OFF          *IN12
     C                   MOVE      *OFF          *IN13
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     *INZSR        BEGSR
      *
     C                   Z-ADD     0             SPGE01            3 0
     C                   Z-ADD     0             SRRN01            4 0
     C                   Z-ADD     0             WACTLI            2 0
     C                   Z-ADD     0             WACTPO            3 0
     C                   MOVE      *BLANK        WSEQEN            1
      *
     C     *LIKE         DEFINE    SRRN01        SFRN01
     C     *LIKE         DEFINE    SRRN01        SSRN01
     C     *LIKE         DEFINE    ilrkli        WWFIRM
     C     *LIKE         DEFINE    ilrkli        PAFIRM
     C     *LIKE         DEFINE    ilrlnr        WWLNR
     C     *LIKE         DEFINE    ilrnav        WWLNAV
     C     *LIKE         DEFINE    ilrlnr        PALNR
     C     *LIKE         DEFINE    ilrnav        PALNa
      *
      * Parameterliste
      *
     C     *ENTRY        PLIST
     C                   PARM                    PAFIRM
     C                   PARM                    PALNR
     C                   PARM                    PALna
      *
     C                   MOVE      PAFIRM        WWFIRM
      *
      * Key for file nummerisk
      *
     C     ilonl1_key    KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    ilonl1_lnr
      *
      * Key for file alfabetisk
      *
     C     ilonl2_key    KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    ilonl2_nav
      *
     C                   MOVE      PAFIRM        WWFIRM
     c                   eval      ilonl1_lnr = *zero
     c                   eval      ilonl2_nav = *blank
     C     WSEQEN        IFEQ      'A'
     C     ilonl2_key    SETLL     ilonl2
     C                   ELSE
     C     ilonl1_key    SETLL     ilonl1
     C                   END
      *
     C                   EXSR      XCRT01
      *
     C                   ENDSR
      *
**
  Ny post                  001
  Endring                  002
  Vise                     003
* Slettet *                004
