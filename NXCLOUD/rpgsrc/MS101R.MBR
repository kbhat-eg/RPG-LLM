     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLOGI
      * Program . . . . : MS101R
      * Beskrivelse . . : Syklisk telling - lister de uten abc-kode
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       251007 bø   Nytt program
5.70  *       171208 bof  Pristype på lager/ABC-kode på lager
      * 6.10  290609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  181109 BSA  Må ta høyde for lager, hvis dette er lagt inn
6.12  * 6.10  110610 bof  Gjort om masseoppd. fra sql til rpg
6.13  * 6.10  100311 bof  har lagt inn test på lager i sql-kall
6.21  * 6.20  310812 bof  Nytt felt, endringsdato for faste data som vises
6.3q  *       260515 bof  Datoformat sql
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = prutect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
5.70 fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
5.70 fvlaglu    uf   e           k disk    rename(vlagpfr:vlaglur)
6.12 fvlagl2    uf   e           k disk    rename(vlagpfr:vlagl2r)
      *
     fms101d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Informution Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
5.70 d vlaglu_vare     s                   like(vlvare)
5.70 d vlaglu_lage     s                   like(vllage)
5.70 d vlagl1_vare     s                   like(vlvare)
5.70 d vlagl1_lage     s                   like(vllage)
6.12 d vlagl2_lage     s                   like(vllage)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_open_sok      s              1    inz(*off)
     d b_abck          s              1    inz(*off)
     d b_blaf          s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_null          s              3  0 inz(000)
6.12 d w_kabc          s                   like(vlkabc)
      *
5.70 d p_lage          s                   like(vllage)
      *
     d s_vare          s                   like(vvvare)
     d s_tek1          s                   like(vvtek1)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO
6.3q C/End-Exec
      *
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inki
     c                   exsr      mass_oppd
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Søk - ved sql
      *

     c                   if        b2vare <> *blank or
     c                             b2tek1 <> *blank
     c                   exsr      sØk
     c                   goto      b2tagb
     c                   endif
      *
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      b_open_sok = *on
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for søking i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sØk           begsr
      *
     c                   eval      w_seqe = ' '
     c                   eval      b_open_sok = *on
     c                   eval      s_vare  = b2vare
     c                   eval      s_tek1  = b2tek1
      *
     c                   if        b2vare <> *blank
     c                   eval      w_seqe  = 'V'
     c                   goto      end_sok
     c                   endif
     c                   if        b2tek1 <> *blank
     c                   eval      w_seqe  = 'T'
     c                   goto      end_sok
     c                   endif
      *
     c     end_sok       tag
      *
      * Blanker subfile, og fyller opp i.h.t. søk
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker søkefelt
      *
     c                   eval      b2vare = *blank
     c                   eval      b2tek1 = *blank
      *
     c                   endsr
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      *
      * Vise detaljer på varer
      *
     c                   if        b1valg = 2
     c                   eval      c2vare = b1vare
     c                   eval      c2tek1=  b1tek1
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
      * Initierer SQL for søk
      *
      *
     c                   if        b_open_sok = *on
      *
     c                   select
     c                   when      w_seqe = 'V'
      * pr varenr
      *
     c/exec sql
     c+                  declare sok_v cursor for
5.70 c+                  select vlfirm,
5.70 c+                         vlvare,
5.70 c+                         vltabc,
6.12 c+                         vllage
5.70 c+                  from   vlagpf
5.70 c+                  where  vltabc = '  '  and
5.70 c+                         vlfirm = :w_firm and
6.13 c+                         vllage = :b2lage  and
5.70 c+                         vlvtyp = 'L' and
5.70 c+                         vlvare >= :b2vare
     c+                  order by vlvare
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_v
     c/end-exec
     c/exec sql
     c+                  open sok_v
     c/end-exec
     c                   when      w_seqe = 'T'
      *
5.70  * pr varetekst
      *
     c/exec sql
     c+                  declare sok_t cursor for
5.70 c+                  select vvfirm,
5.70 c+                         vvvare,
5.70 c+                         vvtek1
5.70 c+                  from   vvarpf
5.70 c+                  where  vvvare in
5.70 c+                 (select vlvare from vlagpf where vltabc = ' ' and
5.70 c+                                vllage = :b2lage  and
5.70 c+                                vlvtyp = 'L')     and
5.70 c+                       vvfirm = :w_firm and
5.70 c+                       vvtek1 >= :b2tek1
     c+                  order by vvtek1
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_t
     c/end-exec
     c/exec sql
     c+                  open sok_t
     c/end-exec
      *
     c                   endsl
      *
     c                   endif
      *
      * Fyller opp subfile
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'V'
      * varenr
     c/exec sql
     c+                  fetch next
     c+                  from  sok_v
5.70 c+                  into  :vlfirm,
5.70 c+                        :vlvare,
5.70 c+                        :vltabc,
6.12 c+                        :vllage
     c/end-exec
      *
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
5.70  *
5.70 c                   eval      vvarl1_vare = vlvare
5.70 c     vvarl1_key    chain     vvarl1
      *
     c                   when      w_seqe = 'T'
      * tekst
     c/exec sql
     c+                  fetch next
     c+                  from  sok_t
     c+                  into  :vvfirm,
     c+                        :vvvare,
     c+                        :vvtek1
     c/end-exec
      *
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   endsl
      *
     c                   if        *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        vvfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
6.11  *
6.11  * Sjekk at det kun blir med valgte lagre
6.11  *
6.11 c                   if        b2lage = *zero or
6.11 c                             b2lage = vllage
      *
      * Skriver til subfile
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1vare = vvvare
     c                   eval      b1tek1 = vvtek1
      *
     c                   write     b1sfl
      *
6.11 c                   endif
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   eval      b_open_sok = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   exsr      forny
     c                   goto      end_bck
      *
     c     end_bck       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
5.70 c                   eval      vlagl1_vare = b1vare
5.70 c                   eval      vlagl1_lage = b2lage
5.70 c     vlagl1_key    chain     vlagl1
5.70  *
5.70 c                   if        %found
5.70 c                   eval      c2vare = vlvare
5.70 c                   eval      c2lage = vllage
5.70 c                   eval      c2kabc = vltabc
5.70 c                   eval      vvarl1_vare = vlvare
5.70 c     vvarl1_key    chain     vvarl1
5.70 c                   eval      c2tek1 = vvtek1
      *
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl
     c                   goto      end_c2bld
     c                   endif
     c                   if        c2kabc <> 'AX' and
     c                             c2kabc <> 'AY' and
     c                             c2kabc <> 'AZ' and
     c                             c2kabc <> 'BX' and
     c                             c2kabc <> 'BY' and
     c                             c2kabc <> 'BZ' and
     c                             c2kabc <> 'CX' and
     c                             c2kabc <> 'CY' and
     c                             c2kabc <> 'CZ'
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
      *
     c                   if        c2kabc <> *blank
5.70 c                   eval      vlaglu_vare = b1vare
5.70 c                   eval      vlaglu_lage = b2lage
5.70 c     vlaglu_key    chain     vlaglur
5.70 c                   if        %found(vlaglu)
5.70 c                   eval      vltabc = c2kabc
6.10 c                   time                    vledat
6.10 c                   time                    vletim
6.10 c                   eval      vleusr = l_user
6.21 c                   time                    vlfeda
6.21 c                   time                    vlfeti
6.21 c                   eval      vlfeus = l_user
5.70 c                   update    vlaglur
     c                   eval      b_forn = *on
     c                   endif
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å masseoppdatering av abc-kode (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     mass_oppd     begsr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_mass
     c                   endif
6.12  *
6.12 c                   eval      w_kabc = d1kabc
6.12 c                   eval      vlagl2_lage = p_lage
6.12 c     vlagl2_key    setll     vlagl2
6.12 c     vlagl2_key    reade     vlagl2
6.12 c                   dow       not %eof(vlagl2)
6.12 c                   if        vltabc = *blank
6.12 c                   eval      vltabc = w_kabc
6.12 c                   update    vlagl2r
6.12 c                   endif
6.12 c     vlagl2_key    reade     vlagl2
6.12 c                   enddo
      *
     c     end_mass      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
5,70  *
5.70  * Parametre
5.70  *
5.70 c     *entry        plist
5.70 c                   parm                    p_lage
      *
      * Key for posisjonering på varereg.
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les på lagerreg.
      *
5.70 c     vlagl1_key    klist
5.70 c                   kfld                    w_firm
5.70 c                   kfld                    vlagl1_vare
5.70 c                   kfld                    vlagl1_lage
      *
      * Key for oppdatering på lager
      *
5.70 c     vlaglu_key    klist
     c                   kfld                    w_firm
5.70 c                   kfld                    vlaglu_vare
5.70 c                   kfld                    vlaglu_lage
6.12  *
6.12  * Key for masseoppdatering på lager
6.12  *
6.12 c     vlagl2_key    klist
6.12 c                   kfld                    w_firm
6.12 c                   kfld                    vlagl2_lage
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
      *
5.70 c                   eval      b2lage     = p_lage
5.70 c                   eval      w_firm     = l_firm
     c                   eval      b_open_sok = *on
     c                   eval      w_seqe     = 'V'
     c                   exsr      crt_subfile
      *
     c                   endsr
