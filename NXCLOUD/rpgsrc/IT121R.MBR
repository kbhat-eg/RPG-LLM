     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASNAVIOKO
      * Program . . . . : IT121R
      * Beskrivelse . . : Oppdaterer avdelinger fra NAV
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       260214 ask  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra07lu    uf a e           k disk    rename(ra07pfr:ra07lur)
      *
      * Parameter-inn
      *
     d p_firm          s                   like(ragfir)
     d p_comd          s             20
     d p_parm          s           2048
     d p_user          s             10
     d p_stat          s              1
      *
      * Definering av variabler i nøkler
      *
     d ra07lu_gkod     s                   like(ragkod)
      *
      *  Definering av datastrukturer for parameter
      *
     d d_parm          ds          2048
     d  d_gkod                        4
     d  d_besk                       40
      *
      * Definering av variable
      *
     d w_firm          s                   like(ragfir)
     d w_gkod          s                   like(ragkod)
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram - oppdaterer informasjon på gkod., setter statusflagg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Redigerer parameterdata før behandling
      *
     c                   eval      d_parm = p_parm
     c                   exsr      red_num
     c                   exsr      red_flt
      *
      * Velger oppdatering avhengig av kommando
      *
     c                   select
     c                   when      p_comd = 'INSERT'
     c                   exsr      nyrec
     c                   when      p_comd = 'UPDATE'
     c                   exsr      oppdrec
     c                   when      p_comd = 'DELETE'
     c                   exsr      sletrec
     c                   endsl
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slutt         tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Opprette kodepost
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     nyrec         begsr
      *
     c                   eval      w_firm = p_firm
     c                   eval      ra07lu_gkod = w_gkod
     c     ra07lu_key    chain     ra07lu
     c                   if        not %found
     c                   eval      ragfir = w_firm
     c                   eval      ragkod = w_gkod
     c                   eval      ragtxt = %subst(d_besk:1:30)
     c                   eval      ragdat = w_date
     c                   eval      ragtim = w_time
     c                   eval      ragusr = p_user
     c                   write     ra07lur
     c                   eval      p_stat = '1'
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatere kodepost
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdrec       begsr
      *
     c                   eval      w_firm = p_firm
     c                   eval      ra07lu_gkod = w_gkod
     c     ra07lu_key    chain     ra07lu
     c                   if        %found
     c                   eval      ragtxt = %subst(d_besk:1:30)
     c                   eval      ragdat = w_date
     c                   eval      ragtim = w_time
     c                   eval      ragusr = p_user
     c                   update    ra07lur
     c                   eval      p_stat = '1'
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Slett kodepost
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sletrec       begsr
      *
     c                   eval      w_firm = p_firm
     c                   eval      ra07lu_gkod = w_gkod
     c     ra07lu_key    chain     ra07lu
     c                   if        %found
     c                   delete    ra07lur
     c                   eval      p_stat = '1'
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Bytter ut blanke med nuller i numeriske felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     red_num       begsr
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Redigerer numeriske felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     red_flt       begsr
      *
      * Kodefelt
      *
     c                   eval      w_gkod = %dec(d_gkod:4:0)
      *
     c     end_flt       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      p_stat = ' '
     c                   eval      *inlr = *on
     c                   return
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_comd
     c                   parm                    p_parm
     c                   parm                    p_user
     c                   parm                    p_stat
      *
      * Key for posisjonering på valgt kode
      *
     c     ra07lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra07lu_gkod
      *
      * Setter "timestamp" for postoppdateringer
      *
     c                   time                    w_date
     c                   time                    w_time
      *
     c                   endsr
