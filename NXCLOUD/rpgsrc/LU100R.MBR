     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LU100R
      * Beskrivelse . . : Lagertelling, behandling av tellebunter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *  6.10  170609 BSA  Oppdatert med sporingsinformasjon
6.nu  *  6.30  170614 bof  Endring ihht. nummerutvidelse
7.xx  *  7.00  190516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  230119 bkv  Vasker vekk probl Newlook tegn
7.02  * 7.00  210619 bkv  Vasker vekk probl linjeskift
8.01  * 800   270122 ask  Lagt inn mulighet for "korreksjons overføring" til telleliste NXS-11668
8.02  * 800   200422 ask  Mulighet for å slette tellelinjer med 0 på telleliste
      *                   ved reversering av bunt på syklisk teling.
8.03  * 800  161024  ask  Lagt inn mulighet for å kalle opp pgm med batchid
8.04  * 800  221024  ask  Sjekk om hovedtelleliste er kjørt før overføring av tellebunt
8.05  * 800  221024  ask  Lagt inn mulighet for utskrft av lagerlister fra her
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       10 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     flbchl1    if   e           k disk    rename(lbchpfr:lbchl1r)
     flbhelr    if   e           k disk    rename(lbhepfr:lbhelrr)
     flbhel1    if   e           k disk    rename(lbhepfr:lbhel1r)
     flbhel2    if   e           k disk    rename(lbhepfr:lbhel2r)
     flbhelu    uf   e           k disk    rename(lbhepfr:lbhelur)
     flusrl1    if   e           k disk    rename(lusrpfr:lusrl1r)
      *
     flu100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Parameter for service programmer
      *
     d                 ds
     d d_prec                        20
     d d_firm                         3  0 overlay(d_prec)
     d d_batc                        10    overlay(d_prec:4)
     d d_numm                         6  0 overlay(d_prec:14)
     d d_kode                         1    overlay(d_prec:20)
      *
      * Parameter for registreringsprogrammer
      *
     d                 ds
     d d_rrec                        30
     d d_retu                         1    overlay(d_rrec)
     d d_rfir                         3  0 overlay(d_rrec:2)
     d d_rbat                        10    overlay(d_rrec:5)
     d d_rnum                         6  0 overlay(d_rrec:15)
     d d_rtyp                        10    overlay(d_rrec:21)
8.05  *
8.05  *  Parameter-rec
8.05  *
8.05 d                 ds
8.05 d p_prec                       128
8.05 d  p_fvar                        8  0 overlay(p_prec)
8.05 d  p_tvar                        8  0 overlay(p_prec:9)
8.05 d  p_flag                        2  0 overlay(p_prec:17)
8.05 d  p_tlag                        2  0 overlay(p_prec:19)
8.05 d  p_fogr                        2  0 overlay(p_prec:21)
8.05 d  p_togr                        2  0 overlay(p_prec:23)
8.05 d  p_fhgr                        2  0 overlay(p_prec:25)
8.05 d  p_thgr                        2  0 overlay(p_prec:27)
8.05 d  p_fugr                        3  0 overlay(p_prec:29)
8.05 d  p_tugr                        3  0 overlay(p_prec:32)
8.05 d  p_levn                        6  0 overlay(p_prec:35)
8.05 d  p_sakb                        4    overlay(p_prec:41)
8.05 d  p_sort                        1  0 overlay(p_prec:45)
8.05 d  p_type                        1  0 overlay(p_prec:46)
8.05 d  p_vlin                        1  0 overlay(p_prec:47)
8.05 d  p_batc                       10    overlay(p_prec:48)
8.05 d  p_dath                        6  0 overlay(p_prec:58)
8.05 d  p_gjko                        1  0 overlay(p_prec:64)
8.05 d  p_dumm                       10    overlay(p_prec:65)
      *
      * Local data area
      *
8.03 d lda            uds
     d l_prtf                800    809
     d l_chgv                844    844
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
8.03 d l_batc               1006   1015
8.03 d l_type               1016   1016
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d lbchl1_batc     s                   like(lcbatc)
     d lbhelr_batc     s                   like(lubatc)
     d lbhelr_numm     s                   like(lunumm)
     d lbhel1_batc     s                   like(lubatc)
     d lbhel1_numm     s                   like(lunumm)
     d lbhel2_batc     s                   like(lubatc)
     d lbhel2_numm     s                   like(lunumm)
     d lbhel2_type     s                   like(lutype)
     d lbhelu_batc     s                   like(lubatc)
     d lbhelu_numm     s                   like(lunumm)
     d lusrl1_user     s                   like(lbuser)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(lufirm)
     d w_batc          s             10
     d w_fast          s             10
     d w_fell          s              6
     d w_kode          s              1
6.nu d w_numm          s              8  0
     d w_retu          s              1
     d w_stat          s              1
     d w_type          s              2
     d w_valg          s              1  0
     d w_utsk          s              1  0
     d w_over          s              1  0
     d w_0tst          s              1  0
     d w_besk          s             30
     d w_latx          s             20
     d w_prec          s             20
     d w_rrec          s             30
7.01 d w_len           s              3  0
7.01 d w_teller        s              3  0
8.04 d w_lintel        s              6  0
8.05 d w_parm          s            128
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(13)
      *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * K1WIN  - K1 Bilde for å kopiere en bunt
      * D1WIN  - D1 Bilde for å bekrefte sletting av bunt
      * B8WIN  - B8 Bilde for å bekrefte overføring til tellefil
      * B9WIN  - B9 Bilde for å bekrefte sletting av overført bunt
      * H1WIN  - H1 Bilde for å velge blametoder
      *
     c/Exec SQL
     c+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR,
     c+             commit=*none
     c/End-Exec
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *   Kaller opp subprogram for inntasting av batchnummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     a1tag         tag
      *
8.03 c                   in        *dtaara
8.03 c                   if        l_batc <> ' '
8.03 c                   eval      w_batc = l_batc
8.03 c                   else
     c                   eval      w_retu = ' '
     c                   call      'LC500R'
     c                   parm                    w_retu
     c                   parm                    w_batc
      *
     c                   if        w_retu <> *blank
     c                   goto      end_pgm
     c                   endif
8.03 c                   endif
      *
     c                   eval      lbchl1_batc = w_batc
     c                   eval      lbhel1_batc = w_batc
     c                   eval      lbhelr_batc = w_batc
     c                   eval      lbhel2_batc = w_batc
      *
      *  Hent opplysninger fra BATCH-REGISTER
      *
     c     lbchl1_key    chain     lbchl1
     c                   if        not %found
     c                   goto      end_pgm
     c                   endif
      *
      *  Fyller opp skjermbildet for første visning
      *
     c     lbhel1_key    setll     lbhel1
     c                   eval      w_seqe = ' '
     c                   exsr      crt_subfile
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inka
     c                   exsr      bla_alt
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkc or *inkl
     c                   goto      end_pgm
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkd
     c                   exsr      sp_input
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      ny_bunt
     c                   exsr      forny
     c                   goto      b2taga
8.05 c                   when      *inkg
8.05 c                   exsr      skriv_avvik
8.05 c                   exsr      forny
8.05 c                   goto      b2taga
8.05 c                   when      *inkh
8.05 c                   exsr      skriv_verdi
8.05 c                   exsr      forny
8.05 c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2numm <> 0 or
     c                             b2type <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   select
     c                   when      w_seqe = ' '
     c     lbhel1_key    setll     lbhel1
     c                   when      w_seqe = 'T'
     c     lbhel2_key    setll     lbhel2
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på buntnummer
      *
     c                   if        b2numm <> 0
     c                   eval      w_seqe = ' '
     c                   eval      lbhel1_numm = b2numm
     c     lbhel1_key    setll     lbhel1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på bunttype
      *
     c                   if        b2type <> *blank
     c                   eval      w_seqe = 'T'
     c                   eval      lbhel2_type = b2type
     c     lbhel2_key    setll     lbhel2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2numm = 0
     c                   eval      b2type = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre post
      *
     c                   if        b1valg = 2
     c                   exsr      xb2win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopiere post
      *
     c                   if        b1valg = 3
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slette post
      *
     c                   if        b1valg = 4
     c                   eval      d1numm = b1numm
     c                   eval      d1text = b1text
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Skrive ut etiketter
      *
     c                   if        b1valg = 5
     c                   exsr      xb5win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Skrive ut post
      *
     c                   if        b1valg = 6
     c                   exsr      xb6win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Endre linjer
      *
     c                   if        b1valg = 7
     c                   exsr      xb7win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Overføre til tellefil
      *
     c                   if        b1valg = 8
     c                   exsr      xb8win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Reversering av tellebunt
      *
     c                   if        b1valg = 9
     c                   exsr      xb9win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
8.01  *
8.01  * Overføre korreksjonsbunt til tellefil
8.01  *
8.01 c                   if        b1valg = 10
8.01 c                   eval      b10num = b1numm
8.01 c                   eval      b10txt = b1text
8.01 c                   exsr      xb10win
8.01 c                   eval      b1valg = 0
8.01 c                   update    b1sfl
8.01 c                   iter
8.01 c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2  Subrutine for å kalle opp program for endring av tellebunt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb2win        begsr
      *
      * Overført ?
      *
     c                   if        b1kode = 'OF'
     c                   goto      end_xb2
     c                   endif
      *
     c                   call      'LU102R'
     c                   parm                    d_kode
     c                   parm                    w_firm
     c                   parm                    lbhel1_batc
     c                   parm                    b1numm
      *
     c                   eval      b_forn = *on
      *
     c     end_xb2       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * k1  Subrutine for å kopiere tellebunt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
     c                   eval      k1numm = b1numm
     c                   eval      k1text = b1text
     c                   eval      k1nonr = 0
     c                   eval      k1type = *blank
     c                   eval      w_awro = 0
     c                   eval      w_awco = 0
      *
     c     k1tagb        tag
     c                   exfmt     k1win
      *
      *  Behandle funksjonstaster
      *
     c                   if        *inkc = *on
     c                   goto      end_xk1
     c                   endif
      *
     c                   if        *inkd = *on
     c                   exsr      sp_input
     c                   goto      k1tagb
     c                   endif
      *
      *  Sjekk om bunt finnes fra før
      *
     c                   eval      lbhelr_numm = k1nonr
     c     lbhelr_key    chain     lbhelr
      *
     c                   if        %found
     c                   eval      *in31 = *on
     c                   goto      k1tagb
     c                   endif
      *
      *  Henter neste ledige lagerbuntnummer, dersom buntnummer er 0
      *
     c     k1tagx        tag
      *
     c                   if        k1nonr = 0
     c                   eval      w_kode = '0'
     c                   eval      w_numm = 0
     c                   exsr      hntnum
     c                   eval      lbhelr_numm = w_numm
     c                   endif
      *
     c     lbhelr_key    chain     lbhelrr
     c                   if        %found
     c                   goto      k1tagx
     c                   endif
      *
      *  Kaller opp subprogram for kopiering av bunt
      *
     c                   call      'LU300R'
     c                   parm                    w_stat
     c                   parm                    w_firm
     c                   parm                    lbhel1_batc
     c                   parm                    k1numm
     c                   parm                    lbhelr_numm
     c                   parm                    k1type
      *
     c                   eval      b1text = *blank
     c                   eval      b_forn = *on
      *
     c     end_xk1       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * d1  Subrutine for behandling av slette-bildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
      * Overført ?
      *
     c                   if        b1kode = 'OF'
     c                   goto      end_xd1
     c                   endif
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc = *on or
     c                             *inkl = *on
     c                   goto      end_xd1
     c                   endif
      *
      *  Kaller opp subprogrammet for sletting av lagerbunten
      *
     c                   call      'LU400R'
     c                   parm                    w_stat
     c                   parm                    w_firm
     c                   parm                    lbhel1_batc
     c                   parm                    b1numm
     c                   eval      b_forn = *on
      *
     c     end_xd1       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  B5 Utskrift av etiketter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb5win        begsr
      *
      *  Kaller opp etikettprogram
      *
     c                   call      'LV610C'
     c                   parm                    w_batc
     c                   parm                    b1numm
      *
     c     end_xb5       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  B6 Behandle bilde for utskrift av lagerbunt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb6win        begsr
      *
     c                   eval      d_batc = w_batc
     c                   eval      d_numm = b1numm
     c                   eval      w_kode = ' '
      *
      *  Kaller opp utskriftsprogram
      *
     c                   call      'LU601C'
     c                   parm                    d_prec
     c                   parm                    w_kode
      *
     c     end_xb6       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B7  Subrutine for å kalle opp program for endring av tellelinjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb7win        begsr
      *
      * Overført ?
      *
     c                   if        b1kode = 'OF'
     c                   goto      end_xb7
     c                   endif
      *
      *  Setter verdier i parameter-record
      *
     c                   eval      d_retu = *blank
     c                   eval      d_rfir = w_firm
     c                   eval      d_rbat = w_batc
     c                   eval      d_rnum = b1numm
     c                   eval      d_rtyp = b1type
     c                   eval      w_rrec = d_rrec
      *
     c                   call      'LV100R'
     c                   parm                    w_rrec
      *
     c     end_xb7       tag
     c                   eval      b_forn = *on
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  B8 Overføre lagerbunt til tellefil
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb8win        begsr
      *
      * Sjekk autorisasjon
      *
     c                   if        lbtans < 1
     c                   exfmt     a2win
     c                   goto      end_xb8
     c                   endif
      *
      * Overført ?
      *
     c                   if        b1kode = 'OF'
     c                   goto      end_xb8
     c                   endif
      *
     c                   eval      d_batc = w_batc
     c                   eval      d_numm = b1numm
     c                   eval      b_forn = *on
8.04  *
8.04  * Sjekk om telleliste er kjørt
8.04  *
8.04 c                   if        lcstel = 0
8.04         exec sql
8.04           select count(*)
8.04               into :w_lintel
8.04             from ltelpf
8.04             where lebatc = :w_batc;
8.04
8.04 c                   if        w_lintel = 0
8.04 c                   exfmt     a4win
8.04 c                   goto      end_xb8
8.04 c                   endif
8.04 c                   endif
      *
      *  Kaller opp oppdateringsprogram
      *
     c                   eval      w_kode = '1'
     c                   call      'LU601C'
     c                   parm                    d_prec
     c                   parm                    w_kode
      *
      * Markerer bunthode hvis oppdatert
      *
     c                   if        w_kode = '1'
     c                   eval      lbhelu_batc = w_batc
     c                   eval      lbhelu_numm = b1numm
     c     lbhelu_key    chain     lbhelu
     c                   eval      lukode = 1
     c                   if        %found
6.10 c                   time                    luedat
6.10 c                   time                    luetim
6.10 c                   eval      lueusr = l_user
     c                   update    lbhelur
     c                   endif
     c                   endif
      *
     c     end_xb8       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  B9 Reversering av tellebunt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xb9win        begsr
      *
      * Sjekk autorisasjon
      *
     c                   if        lbtans < 1
     c                   exfmt     a2win
     c                   goto      end_xb9
     c                   endif
      *
      * Sperre retur av diverse bunter
      *
     c                   if        b1type = 'DIVERSE'
     c                   exfmt     a3win
     c                   goto      end_xb9
     c                   endif
      *
     c                   if        b1kode <> 'OF'
     c                   goto      end_xb9
     c                   endif
      *
     c                   eval      b9numm = b1numm
     c                   eval      b9text = b1text
8.02 c                   if        lcstel = 1
8.02 c                   eval      *in30 = *on
8.02 c                   endif
     c                   exfmt     b9win
      *
     c                   if        *inkc = *on or
     c                             *inkl = *on
     c                   goto      end_xb9
     c                   endif
      *
     c                   eval      d_batc = w_batc
     c                   eval      d_numm = b1numm
     c                   eval      b_forn = *on
      *
      *  Kaller opp tilbakeføringsprogram
      *
     c                   call      'LE702R'
     c                   parm                    d_prec
8.02 c                   parm                    b9slet
      *
     c     end_xb9       endsr
8.01  *
8.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.01  *     Subrutine for overføring av korreksjonsbunt
8.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.01  *
8.01 c     xb10win       begsr
8.01  *
8.01  * Overført ?
8.01  *
8.01 c                   if        b1kode = 'OF'
8.01 c                   goto      end_xb10
8.01 c                   endif
8.01  *
8.01 c                   exfmt     b10win
8.01  *
8.01 c                   if        *inkc = *on or
8.01 c                             *inkl = *on
8.01 c                   goto      end_xb10
8.01 c                   endif
8.01  *
8.01  *  Kaller opp oppdateringsprogram med overskriving i LTELPF
8.01  *
8.01 c                   eval      w_kode = '1'
8.01 c                   eval      d_batc = w_batc
8.01 c                   eval      d_numm = b1numm
8.01 c                   call      'LU603C'
8.01 c                   parm                    d_prec
8.01 c                   parm                    w_kode
8.01  *
8.01  * Markerer bunthode hvis oppdatert
8.01  *
8.01 c                   if        w_kode = '1'
8.01 c                   eval      lbhelu_batc = w_batc
8.01 c                   eval      lbhelu_numm = b1numm
8.01 c     lbhelu_key    chain     lbhelu
8.01 c                   eval      lukode = 1
8.01 c                   if        %found
8.01 c                   time                    luedat
8.01 c                   time                    luetim
8.01 c                   eval      lueusr = l_user
8.01 c                   update    lbhelur
8.01 c                   endif
8.01 c                   endif
8.01 c                   eval      b_forn = *on
8.01  *
8.01 c     end_xb10      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = ' '
     c                   read      lbhel1
     c                   when      w_seqe = 'T'
     c                   read      lbhel2
     c                   endsl
      *
     c                   if        %eof or
     c                             lufirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        lubatc <> w_batc
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        h1type <> *blank and lutype <> h1type
     c                   iter
     c                   endif
      *
     c                   if        h1user <> *blank and luuser <> h1user
     c                   iter
     c                   endif
      *
     c                   if        h1lage <> 0 and lulage <> h1lage
     c                   iter
     c                   endif
      *
     c                   if        h1utsk = 1 and luskod <> 0
     c                   iter
     c                   endif
      *
     c                   if        h1utsk = 2 and luskod <> 1
     c                   iter
     c                   endif
      *
     c                   if        h1over = 1 and lukode <> 0
     c                   iter
     c                   endif
      *
     c                   if        h1over = 2 and lukode <> 1
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1numm = lunumm
     c                   eval      b1type = lutype
     c                   eval      b1lage = lulage
     c                   eval      b1text = lutext
     c                   eval      b1wsid = luwsid
     c                   eval      b1user = luuser
     c                   eval      b1kode = *blank
     c                   if        lukode <> 0
     c                   eval      b1kode = 'OF'
     c                   endif
      *
     c                   eval      b1vise = *blank
     c                   if        luskod <> 0
     c                   eval      b1vise = 'UT'
     c                   endif
      *
7.02 c     x'25':' '     xlate     b1text        b1text
7.01 c                   exsr      newlo_vask
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'T'
     c     lbhel2_key    setgt     lbhel2
     c                   readp     lbhel2
     c                   other
     c     lbhel1_key    setgt     lbhel1
     c                   readp     lbhel1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'T'
     c                   readp     lbhel2
     c                   other
     c                   readp     lbhel1
     c                   endsl
      *
     c                   if        %eof or
     c                             lufirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   if        lubatc <> w_batc
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'T'
     c                   eval      lbhel2_type = lutype
     c                   other
     c                   eval      lbhel1_numm = lunumm
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'T'
     c     lbhel2_key    setll     lbhel2
     c                   other
     c     lbhel1_key    setll     lbhel1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for registrering av ny bunt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ny_bunt       begsr
      *
      * Sjekk autorisasjon
      *
     c                   if        lbtans < 1
     c                   exfmt     a1win
     c                   goto      end_bunt
     c                   endif
      *
     c                   eval      lbhel1_numm = 0
     c                   call      'LU102R'
     c                   parm                    d_kode
     c                   parm                    w_firm
     c                   parm                    lbhel1_batc
     c                   parm                    lbhel1_numm
      *
     c     end_bunt      endsr
8.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.05  * Subrutine for utskrift av lageravviksliste
8.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.05  *
8.05 c     skriv_avvik   begsr
8.05  *
8.05 c                   eval      p_batc = l_batc
8.05 c                   call      'LE610R'
8.05 c                   parm                    p_batc
8.05  *
8.05 c     end_avvik     endsr
8.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.05  * Subrutine for utskrift av lageravviksliste
8.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.05  *
8.05 c     skriv_verdi   begsr
8.05  *
8.05 c                   eval      p_fogr = 0
8.05 c                   eval      p_fhgr = 0
8.05 c                   eval      p_fugr = 0
8.05 c                   eval      p_togr = 99
8.05 c                   eval      p_thgr = 99
8.05 c                   eval      p_tugr = 999
8.05 c                   eval      p_fvar = 0
8.05 c                   eval      p_tvar = 99999999
8.05 c                   eval      p_flag = 0
8.05 c                   eval      p_tlag = 99
8.05 c                   eval      p_levn = 0
8.05 c                   eval      p_sakb = ' '
8.05 c                   eval      p_sort = 1
8.05 c                   eval      p_vlin = 0
8.05 c                   eval      p_gjko = 0
8.05 c                   eval      p_type = 2
8.05 c                   eval      p_batc = l_batc
8.05 c                   eval      p_dath = 0
8.05 c                   eval      w_parm = p_prec
8.05 c                   eval      l_type = '2'
8.05 c                   out       *dtaara
8.05 c                   call      'LL605C'
8.05 c                   parm                    w_parm
8.05  *
8.05 c     end_verdi     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for spørring på inputfelter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp_input      begsr
      *
      * Spørring på type
      *
     c                   if        w_afld = 'H1TYPE'
     c                   call      'LU500R'
     c                   parm                    w_firm
     c                   parm                    h1type
     c                   parm                    w_besk
     c                   goto      sp_end
     c                   endif
      *
     c                   if        w_afld = 'K1TYPE'
     c                   call      'LU500R'
     c                   parm                    w_firm
     c                   parm                    k1type
     c                   parm                    w_besk
     c                   goto      sp_end
     c                   endif
      *
      * Spørring på lager
      *
     c                   if        w_afld = 'H1LAGE'
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    h1lage
     c                   parm                    w_latx
     c                   goto      sp_end
     c                   endif
      *
     c     sp_end        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for bla alternativer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bla_alt       begsr
      *
     c                   eval      w_awro = 0
     c                   eval      w_awco = 0
      *
     c     h1taga        tag
     c                   exfmt     h1win
      *
      *  Behandle funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_bla
     c                   when      *inkd
     c                   exsr      sp_input
     c                   goto      h1taga
     c                   endsl
      *
      * Gyldige valg?
      *
     c                   if        h1utsk > 2 or
     c                             h1over > 2
     c                   goto      h1taga
     c                   endif
      *
      * Lagre verdier
      *
     c                   eval      b2typv = h1type
     c                   eval      b2usrv = h1user
     c                   eval      b2lagv = h1lage
     c                   eval      w_utsk = h1utsk
     c                   eval      w_over = h1over
      *
     c     end_bla       tag
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter/Oppdatering av NUMMER-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hntnum        begsr
      *
     c                   eval      w_fell = laatbu
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
      *
      *  Kaller opp subprogram for henting av nummer fra NUMMER-REGISTER
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
     c                   endsr
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Newlook vask av data
7.01  *  Fjerner tegn som gjør at Newlook feiltolker
7.01  *  Looper 3 ganger i tilfelle ... på slutten av tekst
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01  /FREE
7.01
7.01   begsr newlo_vask;
7.01     for w_teller = 1 to 5 ;
7.01     w_len = %len(%trim(b1text));
7.01     if (w_len > 0);
7.01       b1text = %ScanRpl('.':'':b1text:w_len); // newlook tåler ikke . på slutten
7.01     endif;
7.01     w_len = %len(%trim(b1text));
7.01     if (w_len > 0);
7.01       b1text = %ScanRpl(':':'':b1text:w_len); // newlook tåler ikke : på slutten
7.01     endif;
7.01     endfor ;
7.01   endsr;
7.01  /END-FREE
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
8.03  *
8.03  * Definisjon av local data area for output
8.03  *
8.03 c     *dtaara       define    *lda          lda
      *
      *  Key for les av LAGER-KODE
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for les av BATCH-REGISTER
      *
     c     lbchl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbchl1_batc
      *
      *  Key for les av LAGERTELLINGSBUNT-HODE
      *
     c     lbhelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbhelr_batc
     c                   kfld                    lbhelr_numm
      *
      *  Key for oppdatering LAGERTELLEBUNT-HODE
      *
     c     lbhelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbhelu_batc
     c                   kfld                    lbhelu_numm
      *
      *  Key for les av LAGERTELLINGSBUNT-HODE (Sekv. på bunt)
      *
     c     lbhel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbhel1_batc
     c                   kfld                    lbhel1_numm
      *
      *  Key for les av LAGERTELLINGSBUNT-HODE (Sekv. på type)
      *
     c     lbhel2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbhel2_batc
     c                   kfld                    lbhel2_type
     c                   kfld                    lbhel2_numm
      *
      *  Key for les av BRUKER
      *
     c     lusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lusrl1_user
      *
      * Legger inn startverdier
      *
     c                   eval      w_firm = l_firm
     c                   eval      d_firm = l_firm
     c                   eval      *in22 = *on
      *
      *  Hent opplysninger fra LAGER-KODE-REGISTER
      *
     c     lstsl1_key    chain     lstsl1r
      *
      *  Hent opplysninger fra LAGER-BRUKER-REGISTER
      *
     c                   eval      lusrl1_user = l_user
     c     lusrl1_key    chain     lusrl1
     c                   if        not %found
     c                   eval      lbtans = 0
     c                   endif
      *
     c                   endsr
      *
