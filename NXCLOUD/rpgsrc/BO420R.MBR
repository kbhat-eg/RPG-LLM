     H/TITLE  ASSHOP Vedlikehold av butikk budsjett-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASSHOP
      * Program . . . . : BO420R
      * Beskrivelse . . : ASSHOP Vedlikehold av budsjett-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
7.xx  * 7.00  200516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  BRUK AV INDIKATORER
      *
      *       KC = F3  = Exit
      *       KE = F5  = Forny
      *       KL = F12 = Annuller
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       30 = Protect av felter ved vise
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer
      *    60-69 = Fileindikatorer chain etc.
      *    70-79 = Arbeidsindikatorer i sub-rutiner
      *    80-89 = Arbeidsindikatorer ordinære
      *    90-99 = Benyttes ved std. sub-rutiner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     FBBUDL2    IF   E           K DISK
     F                                     RENAME(BBUDPFR:BBUDL2R)
     FBBUDL1    IF   E           K DISK
     F                                     RENAME(BBUDPFR:BBUDL1R)
     FBBUDLU    UF A E           K DISK
     F                                     RENAME(BBUDPFR:BBUDLUR)
     FBO420D    CF   E             WORKSTN
     F                                     SFILE(B1SFL:SRRN01)
      *
     D MLD             S             11    DIM(4) CTDATA PERRCD(1)               MELDINGER
     D VIR             S              2  0 DIM(12)                               VIRKEDAGER
     D KRO             S             11  2 DIM(12)                               KRONER
      *
      * Hente inn firma fra local
      *
      *
     D                UDS
     D  DSFIRM               944    946  0
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av felter                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      *     nn - er 01 for subfile 01,
      *             02 for subfile 02,
      *             03 for subfile 03, o.s.v.
      * SRRNnn - Relativt recordnummer i subfile
      * SPGEnn - Sidestørrelse i subfile (page)
      * SFRNnn - Første recordnummer på siden
      * SSRNnn - Siste  recordnummer på siden
      * SVRNnn - Vis den side som inneholder denne record.
      *
      * Forklaring på spesielle felter
      * som benyttes ved windows.
      * - - - - - - - - - - - - - - - - -
      *
      * WACTLI - Actual line, angir startlinje for vindu.
      * WACTPO - Actual posision, angir startposisjon for vindu.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for Kontonummer
      * C2BLD  - C2 Skjermbilde for opprette/ endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      * S1WIN  - S1 Standardvindu som inneholder definisjon av
      *             størrelse og utseende.  Bildene D1WIN og K1WIN
      *             er referert til dette.
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * B2CTL Behandle åpningsbildet med subfile                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Send Alt
      *
     C     B2TAGA        TAG
     C                   WRITE     B2CMD
      *
      * Bare data
      *
     C     B2TAGB        TAG
     C                   EXSR      XDSP01                                       DSP SFL
      *
      * Avslutt program
      *
     C     *INKC         CABEQ     *ON           XSLUTT
      *
      * Forny
      *
     C     *INKE         IFEQ      *ON                                                   .....
     C     SFRN01        IFNE      *ZERO                                                     .
     C     SFRN01        CHAIN     B1SFL                              60                     .
     C                   END                                                                 .
     C                   MOVE      B1AARR        WWAARR                                      .
     C                   MOVE      *ZERO         WWMMND                                      .
     C     KEY01         SETLL     BBUDL2                                                    .
     C                   EXSR      XCLR01                                       TØM SFL      .
     C                   EXSR      XCRT01                                       CRT SFL      .
     C                   GOTO      B2TAGB                                                    .
     C                   ENDIF                                                           .....
      *
      * Roll
      *
     C     *IN10         IFEQ      *ON                                                   .....
     C                   EXSR      XCRT01                                       CRT SFL      .
     C                   GOTO      B2TAGB                                                    .
     C                   ENDIF                                                           .....
      *
      * Posisjoner
      * startverdi
      *
     C     B2VALG        IFEQ      *BLANK                                                .....
     C     B2AARR        IFNE      *ZERO                                                     .
     C                   MOVE      B2AARR        WWAARR                                      .
     C                   MOVE      *ZERO         WWMMND                                      .
     C     KEY01         SETLL     BBUDL1                                                    .
     C                   EXSR      XCLR01                                       TØM SFL      .
     C                   EXSR      XCRT01                                       CRT SFL      .
     C                   MOVE      *ZERO         B2AARR                                      .
     C                   GOTO      B2TAGB                                                    .
     C                   END                                                                 .
     C                   ENDIF                                                           .....
      *
      * Varsle om
      * gyldig konto
      *
     C     B2VALG        IFNE      ' '                                                   .....
     C     B2AARR        IFEQ      *ZERO                                                     .
     C                   MOVE      *ON           *IN31                                       .
     C                   GOTO      B2TAGB                                                    .
     C                   END                                                                 .
     C                   ENDIF                                                           .....
      *
      * Slå opp
      * i filen
      *
     C                   MOVE      B2AARR        WWAARR
     C                   MOVE      01            WWMMND
     C     KEY01         CHAIN     BBUDL2                             60
      *
      * Post finnes
      * ikke
      *
     C     *IN60         IFEQ      *ON                                                   .....
     C     B2VALG        IFEQ      '2'                                                       .
     C     B2VALG        OREQ      '3'                                                       .
     C     B2VALG        OREQ      '4'                                                       .
     C     B2VALG        OREQ      '5'                                                       .
     C                   MOVE      *ON           *IN32                                       .
     C                   GOTO      B2TAGB                                                    .
     C                   END                                                                 .
     C                   ENDIF                                                           .....
      *
      * Post finnes
      * fra før
      *
     C     *IN60         IFEQ      *OFF                                                  .....
     C     B2VALG        IFEQ      '1'                                                       .
     C                   MOVE      *ON           *IN33                                       .
     C                   GOTO      B2TAGB                                                    .
     C                   END                                                                 .
     C                   ENDIF                                                           .....
      *
      * Opprette
      *
     C     B2VALG        IFEQ      '1'                                                   .....
     C                   MOVE      B2AARR        C1AARR                                      .
     C                   GOTO      C1TAGA                                                    .
     C                   ENDIF                                                           .....
      *
      * Endre
      *
     C     B2VALG        IFEQ      '2'                                                   .....
     C                   MOVE      B2AARR        C1AARR                                      .
     C                   EXSR      XC2BLD                                                    .
     C                   GOTO      B2TAGA                                                    .
     C                   ENDIF                                                           .....
      *
      * Kopiere
      *
     C     B2VALG        IFEQ      '3'                                                   .....
     C                   MOVE      B2AARR        K0AARR                                      .
     C                   MOVE      B2AARR        K1AARR                                      .
     C                   EXSR      XK1WIN                                                    .
     C                   GOTO      B2TAGB                                                    .
     C                   ENDIF                                                           .....
      *
      * Slette
      *
     C     B2VALG        IFEQ      '4'                                                   .....
     C                   EXSR      XD1WIN                                                    .
      *                    GOTO B2TAGB                                  .
     C                   ENDIF                                                           .....
      *
      * Vise
      *
     C     B2VALG        IFEQ      '5'                                                   .....
     C                   MOVE      *ON           *IN30                                       .
     C                   MOVE      B2AARR        C1AARR                                      .
     C                   EXSR      XC2BLD                                                    .
     C                   GOTO      B2TAGA                                                    .
     C                   ENDIF                                                           .....
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Behandling av subfile                                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Sjekk om valg
      * er foretatt i
      * subfile
      *
     C                   DO        SSRN01                                                .....
     C                   READC     B1SFL                                  16                 .
     C     *IN16         IFEQ      *ON                                                       .
     C                   LEAVE                                                               .
     C                   ENDIF                                                               .
      *                                                                 .
      * Endre post                                                      .
      *                                                                 .
     C     B1VALG        IFEQ      '2'                                                       .
     C                   MOVE      B1AARR        C1AARR                                      .
     C                   MOVE      B1AARR        WWAARR                                      .
     C                   EXSR      XC2BLD                                                    .
     C                   ITER                                                                .
     C                   ENDIF                                                               .
      *                                                                 .
      * Kopiere post                                                    .
      *                                                                 .
     C     B1VALG        IFEQ      '3'                                                       .
     C                   MOVE      B1AARR        K0AARR                                      .
     C                   MOVE      B1AARR        K1AARR                                      .
     C                   MOVE      B1AARR        WWAARR                                      .
     C                   EXSR      XK1WIN                                                    .
     C                   ITER                                                                .
     C                   ENDIF                                                               .
      *                                                                 .
      * Slette post                                                     .
      *                                                                 .
     C     B1VALG        IFEQ      '4'                                                       .
     C                   MOVE      B1AARR        WWAARR                                      .
     C                   EXSR      XD1WIN                                                    .
     C                   ITER                                                                .
     C                   ENDIF                                                               .
      *                                                                 .
      * Vise post                                                       .
      *                                                                 .
     C     B1VALG        IFEQ      '5'                                                       .
     C                   MOVE      *ON           *IN30                                       .
     C                   MOVE      B1AARR        C1AARR                                      .
     C                   MOVE      B1AARR        WWAARR                                      .
     C                   EXSR      XC2BLD                                                    .
     C                   ITER                                                                .
     C                   ENDIF                                                               .
      *                                                                 .
     C                   ENDDO                                                           .....
      *
     C                   GOTO      B2TAGA
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * C1BLD Behandle åpningsbildet for ny post                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     C1TAGA        TAG
     C                   EXFMT     C1BLD
      *
     C     *INKC         CABEQ     *ON           B2TAGA
     C     *INKL         CABEQ     *ON           B2TAGA
      *
     C                   MOVE      C1AARR        WWAARR
      *
     C                   EXSR      XC2BLD
     C                   GOTO      C1TAGA
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XSLUTT        TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C/EJECT
     C* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     C* C2BLD Behandle bilde for ny - endring - vise                    *
     C* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     C*
     C     XC2BLD        BEGSR
     C                   MOVE      *ZERO         VIR
     C                   MOVE      *ZERO         KRO
     C                   MOVE      01            WWMMND
     C     KEY01         CHAIN     BBUDL2                             60
      *
     C     *IN60         IFEQ      *OFF                                                  .....
     C                   MOVEL     MLD(2)        C2NYEN                                      .
     C                   MOVE      BUTEXT        C2TEXT                                      .
     C                   ELSE                                                                .
     C                   MOVEL     MLD(1)        C2NYEN                                      .
     C                   MOVE      *BLANK        C2TEXT                                      .
     C                   ENDIF                                                           .....
      *
     C     1             DO        12            N                                       .....
     C                   MOVE      N             WWMMND                                      .
     C     KEY01         CHAIN     BBUDL1                             60                     .
     C     *IN60         IFEQ      *OFF                                                  ... .
     C                   Z-ADD     BUVDAG        VIR(N)                                    . .
     C                   Z-ADD     BUKRON        KRO(N)                                    . .
     C                   ENDIF                                                           ... .
     C                   ENDDO                                                           .....
      *
     C                   MOVE      VIR(1)        C2VI01
     C                   MOVE      VIR(2)        C2VI02
     C                   MOVE      VIR(3)        C2VI03
     C                   MOVE      VIR(4)        C2VI04
     C                   MOVE      VIR(5)        C2VI05
     C                   MOVE      VIR(6)        C2VI06
     C                   MOVE      VIR(7)        C2VI07
     C                   MOVE      VIR(8)        C2VI08
     C                   MOVE      VIR(9)        C2VI09
     C                   MOVE      VIR(10)       C2VI10
     C                   MOVE      VIR(11)       C2VI11
     C                   MOVE      VIR(12)       C2VI12
     C                   MOVE      KRO(1)        C2KR01
     C                   MOVE      KRO(2)        C2KR02
     C                   MOVE      KRO(3)        C2KR03
     C                   MOVE      KRO(4)        C2KR04
     C                   MOVE      KRO(5)        C2KR05
     C                   MOVE      KRO(6)        C2KR06
     C                   MOVE      KRO(7)        C2KR07
     C                   MOVE      KRO(8)        C2KR08
     C                   MOVE      KRO(9)        C2KR09
     C                   MOVE      KRO(10)       C2KR10
     C                   MOVE      KRO(11)       C2KR11
     C                   MOVE      KRO(12)       C2KR12
      *
     C     *IN30         IFEQ      *ON                                                   .....
     C                   MOVEL     MLD(3)        C2NYEN                                      .
     C                   ENDIF                                                           .....
      *
     C                   EXFMT     C2BLD
      *
     C     *INKC         CABEQ     *ON           C2TAG1
     C     *INKL         CABEQ     *ON           C2TAG1
     C     *IN30         CABEQ     *ON           C2TAG1
      *
     C                   MOVE      C2VI01        VIR(1)
     C                   MOVE      C2VI02        VIR(2)
     C                   MOVE      C2VI03        VIR(3)
     C                   MOVE      C2VI04        VIR(4)
     C                   MOVE      C2VI05        VIR(5)
     C                   MOVE      C2VI06        VIR(6)
     C                   MOVE      C2VI07        VIR(7)
     C                   MOVE      C2VI08        VIR(8)
     C                   MOVE      C2VI09        VIR(9)
     C                   MOVE      C2VI10        VIR(10)
     C                   MOVE      C2VI11        VIR(11)
     C                   MOVE      C2VI12        VIR(12)
     C                   MOVE      C2KR01        KRO(1)
     C                   MOVE      C2KR02        KRO(2)
     C                   MOVE      C2KR03        KRO(3)
     C                   MOVE      C2KR04        KRO(4)
     C                   MOVE      C2KR05        KRO(5)
     C                   MOVE      C2KR06        KRO(6)
     C                   MOVE      C2KR07        KRO(7)
     C                   MOVE      C2KR08        KRO(8)
     C                   MOVE      C2KR09        KRO(9)
     C                   MOVE      C2KR10        KRO(10)
     C                   MOVE      C2KR11        KRO(11)
     C                   MOVE      C2KR12        KRO(12)
      *
      * Oppdatering
      *
     C     1             DO        12            N                                       .....
     C                   MOVE      N             WWMMND                                      .
     C     KEY01         CHAIN     BBUDLUR                            60                     .
     C                   MOVE      C2TEXT        BUTEXT                                      .
     C                   Z-ADD     VIR(N)        BUVDAG                                      .
     C                   Z-ADD     KRO(N)        BUKRON                                      .
     C     *IN60         IFEQ      *OFF                                                  ... .
     C                   UPDATE    BBUDLUR                                                 . .
     C                   ELSE                                                              . .
     C                   MOVE      WWFIRM        BUFIRM                                    . .
     C                   MOVE      C1AARR        BUAARR                                    . .
     C                   MOVE      N             BUMMND                                    . .
     C                   MOVE      *ZERO         BUSALG                                    . .
     C                   MOVE      *ZERO         BUKOST                                    . .
     C                   WRITE     BBUDLUR                                                 . .
     C                   ENDIF                                                           ... .
     C                   ENDDO                                                           .....
      *
      * Oppdater subfile
      *
     C     B1VALG        IFEQ      '2'                                                   .....
     C                   MOVE      C1AARR        B1AARR                                      .
     C                   MOVE      C2TEXT        B1TEXT                                      .
     C                   ENDIF                                                           .....
      *
     C     C2TAG1        TAG
      *
     C     B1VALG        IFEQ      '2'                                                   .....
     C     B1VALG        OREQ      '5'                                                       .
     C                   MOVE      SRRN01        SVRN01                                      .
     C                   MOVE      *BLANK        B1VALG                                      .
     C                   UPDATE    B1SFL                                                     .
     C                   ENDIF                                                           .....
      *
     C                   MOVE      *BLANK        B2VALG
     C                   MOVE      *ZERO         B2AARR
     C                   MOVE      *OFF          *IN30
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * D1WIN Behandle bilde for slette                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XD1WIN        BEGSR
     C                   MOVE      01            WWMMND
     C     KEY01         CHAIN     BBUDL2                             60
      *
     C                   Z-ADD     4             WACTLI
     C                   Z-ADD     26            WACTPO
     C                   WRITE     S1WIN
      *
     C                   EXFMT     D1WIN
      *
     C     *INKC         CABEQ     *ON           D1TAG1
     C     *INKL         CABEQ     *ON           D1TAG1
      *
     C     KEY01         CHAIN     BBUDLUR                            60
      *
      * Sletting
      *
     C     1             DO        12            N                                       .....
     C                   MOVE      N             WWMMND                                      .
     C     KEY01         CHAIN     BBUDLUR                            60                     .
     C     *IN60         IFEQ      *OFF                                                  ... .
     C                   DELETE    BBUDLUR                                                 . .
     C                   ENDIF                                                           ... .
     C                   ENDDO                                                           .....
      *
      * Oppdater subfile
      *
     C     B1VALG        IFEQ      '4'                                                   .....
     C                   MOVE      *BLANK        B1TEXT                                      .
     C                   MOVEL     MLD(4)        B1TEXT                                      .
     C                   ENDIF                                                           .....
      *
     C     D1TAG1        TAG
      *
     C     B1VALG        IFEQ      '4'                                                   .....
     C                   MOVE      *BLANK        B1VALG                                      .
     C                   UPDATE    B1SFL                                                     .
     C                   ENDIF                                                           .....
      *
     C                   MOVE      *BLANK        B2VALG
     C                   MOVE      *ZERO         B2AARR
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * K1WIN Behandle bilde for kopiere                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XK1WIN        BEGSR
     C                   MOVE      01            WWMMND
     C     KEY01         CHAIN     BBUDL2                             60
      *
     C                   MOVE      BUTEXT        K1TEXT
     C                   Z-ADD     4             WACTLI
     C                   Z-ADD     26            WACTPO
     C                   WRITE     S1WIN
      *
     C     K1TAGB        TAG
      *
     C                   EXFMT     K1WIN
      *
     C     *INKC         CABEQ     *ON           K1TAG1
     C     *INKL         CABEQ     *ON           K1TAG1
      *
      * Slå opp
      * i filen
      *
     C                   MOVE      K1AARR        WWAARR
     C                   MOVE      01            WWMMND
     C     KEY01         CHAIN     BBUDL2                             60
      *
      * Post finnes
      * fra før
      *
     C     *IN60         IFEQ      *OFF                                                  .....
     C                   MOVE      *ON           *IN31                                       .
     C                   GOTO      K1TAGB                                                    .
     C                   ENDIF                                                           .....
      *
      * Oppdater file
      *
     C     1             DO        12            N                                       .....
     C                   MOVE      K0AARR        WWAARR                                      .
     C                   MOVE      N             WWMMND                                      .
     C     KEY01         CHAIN     BBUDL1                             60                     .
     C                   MOVE      K1AARR        WWAARR                                      .
     C                   MOVE      N             WWMMND                                      .
     C     KEY01         CHAIN     BBUDLUR                            60                     .
     C                   MOVE      K1AARR        BUAARR                                      .
     C                   MOVE      N             BUMMND                                      .
     C                   MOVE      K1TEXT        BUTEXT                                      .
     C                   MOVE      *ZERO         BUSALG                                      .
     C                   MOVE      *ZERO         BUKOST                                      .
     C     *IN60         IFEQ      *ON                                                   ... .
     C                   WRITE     BBUDLUR                                                 . .
     C                   ENDIF                                                           ... .
     C                   ENDDO                                                           .....
      *
     C     K1TAG1        TAG
      *
      * Oppdater subfile
      *
     C     B1VALG        IFEQ      '3'                                                   .....
     C                   MOVE      *BLANK        B1VALG                                      .
     C                   UPDATE    B1SFL                                                     .
     C                   ENDIF                                                           .....
      *
     C                   MOVE      *BLANK        B2VALG
     C                   MOVE      *ZERO         B2AARR
     C                   MOVE      *ZERO         K1AARR
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Tøm subfile                                                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XCLR01        BEGSR
     C                   MOVE      *ON           *IN14
     C                   WRITE     B2CTL
     C                   MOVE      *OFF          *IN14
      *
     C                   MOVE      *OFF          *IN15
     C                   Z-ADD     0             SRRN01
     C                   Z-ADD     0             SFRN01
     C                   Z-ADD     0             SSRN01
     C                   Z-ADD     0             SPGE01
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Fyll opp subfile                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XCRT01        BEGSR
     C                   ADD       10            SPGE01                         SFL END
     C                   Z-ADD     SSRN01        SRRN01
      *
     C     SRRN01        DOUEQ     SPGE01                                                .....
     C                   READ      BBUDL2                                 15                 .
     C     *IN15         CABEQ     '1'           XEND01                         EOF          .
     C                   ADD       1             SRRN01                                      .
     C                   MOVE      BUAARR        B1AARR                                      .
     C                   MOVE      BUTEXT        B1TEXT                                      .
     C                   WRITE     B1SFL                                                     .
     C                   ENDDO                                                           .....
      *
     C     XEND01        TAG
      *
     C     SRRN01        IFGT      SSRN01                                                .....
     C     SSRN01        ADD       1             SFRN01                                      .
     C                   ENDIF                                                           .....
      *
     C                   Z-ADD     SRRN01        SSRN01
     C                   Z-ADD     SFRN01        SVRN01
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Display subfile                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XDSP01        BEGSR
      *
     C     SRRN01        IFNE      0                                                     .....
     C                   MOVE      *ON           *IN12                                       .
     C                   ENDIF                                                           .....
      *
     C                   MOVE      *ON           *IN13
     C                   EXFMT     B2CTL
     C                   MOVE      *OFF          *IN12
     C                   MOVE      *OFF          *IN13
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     *INZSR        BEGSR
      *
     C                   Z-ADD     0             SPGE01            3 0
     C                   Z-ADD     0             SRRN01            4 0
     C                   Z-ADD     0             WACTLI            2 0
     C                   Z-ADD     0             WACTPO            3 0
     C                   Z-ADD     0             N                 2 0
      *
     C     *LIKE         DEFINE    SRRN01        SFRN01
     C     *LIKE         DEFINE    SRRN01        SSRN01
     C     *LIKE         DEFINE    BUFIRM        WWFIRM
     C     *LIKE         DEFINE    BUAARR        WWAARR
     C     *LIKE         DEFINE    BUMMND        WWMMND
      *
      * Legge inn key
      *
     C                   MOVE      DSFIRM        WWFIRM
      *
      * Key for file nummerisk
      *
     C     KEY01         KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    WWAARR
     C                   KFLD                    WWMMND
      *
     C     KEY01         SETLL     BBUDL2
     C                   EXSR      XCRT01
      *
     C                   ENDSR
      *
**
  Ny post                  001
  Endring                  002
  Vise                     003
* Slettet *                004
