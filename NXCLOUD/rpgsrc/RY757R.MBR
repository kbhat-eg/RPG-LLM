     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : RY757R
      * Beskrivelse . . : FIX: Leser fil med Nobbnr og frigir varer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       300505 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjrvapf    if   e           k disk
      *
     fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
     fjvdtlu    uf a e           k disk    rename(jvdtpfr:jvdtlur)
      *
      * Definering av LDA
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av recordbeskrivelse - Christ Engebretsen
      *
     d                 ds
     d d_inpu                         8
     d  d_nobb                        8    overlay(d_inpu:1)                    Artikkelnr
      *
      * Definering av variable i nøkler
      *
     d jvarl3_nobb     s                   like(jvnobb)
     d jvdtlu_dvar     s                   like(jvdvar)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_nobb          s              8  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser fil, og frigir varer
      *
     c                   read      jrvapf
     c                   dow       not %eof
      *
     c                   eval      d_inpu = jrvrad
      *
     c                   if        d_nobb <> *blank
      *
     c     ' ':'0'       xlate     d_nobb        d_nobb
     c                   move      d_nobb        w_nobb
      *
     c                   eval      jvarl3_nobb = w_nobb
     c     jvarl3_key    chain     jvarl3
     c                   if        %found
     c                   eval      jvdtlu_dvar = jvvare
     c     jvdtlu_key    chain     jvdtlur
     c                   if        %found
     c                   eval      jvdhko = 0
     c                   update    jvdtlur
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c                   read      jrvapf
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på vare
      *
     c     jvarl3_key    klist
     c                   kfld                    jvarl3_nobb
      *
      * Key for oppdatering av vare-detalj
      *
     c     jvdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jvdtlu_dvar
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
