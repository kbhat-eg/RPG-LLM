     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : RY825R
      * Beskrivelse . . : Fjerner moduler uten varer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       020406 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjmodlu    uf   e           k disk    rename(jmodpfr:jmodlur)
     fjvarlg    if   e           k disk    rename(jvarpfr:jvarlgr)
     fjmsdlu    uf   e           k disk    rename(jmsdpfr:jmsdlur)
     fjmstlu    uf   e           k disk    rename(jmstpfr:jmstlur)
     fjmtxlu    uf   e           k disk    rename(jmtxpfr:jmtxlur)
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d jvarlg_modn     s                   like(jvmodn)
      *
      * Definering av variabler
      *
     d w_modn          s                   like(jumodn)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Sletter moduler uten varer
      *
     c                   read      jmodlur
     c                   dow       not %eof
      *
     c                   eval      jvarlg_modn = jumodn
     c     jvarlg_key    chain     jvarlg
     c                   if        not %found
      *
     c                   delete    jmodlur
      *
     c                   eval      w_modn = jumodn
     c     key01         setll     jmsdlu
     c     key01         reade     jmsdlur
     c                   dow       not %eof
     c                   delete    jmsdlur
     c     key01         reade     jmsdlur
     c                   enddo
      *
     c     key01         setll     jmstlu
     c     key01         reade     jmstlur
     c                   dow       not %eof
     c                   delete    jmstlur
     c     key01         reade     jmstlur
     c                   enddo
      *
     c     key01         setll     jmtxlu
     c     key01         reade     jmtxlur
     c                   dow       not %eof
     c                   delete    jmtxlur
     c     key01         reade     jmtxlur
     c                   enddo
      *
     c                   endif
      *
     c                   read      jmodlur
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på vare
      *
     c     jvarlg_key    klist
     c                   kfld                    jvarlg_modn
      *
      * Key for sletting av modul
      *
     c     key01         klist
     c                   kfld                    w_modn
      *
     c                   endsr
