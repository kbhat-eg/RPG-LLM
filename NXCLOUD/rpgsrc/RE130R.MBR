     h/TITLE    Program : RE130R   ASOKON
     h decedit(',') datedit(*dmy.)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RE130R                                              *
      * Beskrivelse . . : Integrasjon Huldt&Lillevik lønn ==> ASOKON          *
      *                   Danner databasefil m/klient.                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * R3.40     290103  Nytt program                                   HFL  *
      * R5.20     260104  Tilpasset v5.20                                HFL  *
      * R5.30     181004  Kostnadsbærer er med.                          HFL  *
      * R5.30     230105  Bruker H&L standard inputfil m/ mvaanum. AKT.  HFL  *
      *                   Avd, prosj., spes. kan være uten foranstlt 0.  HFL  *
      *                   Aktivitet kan være num. eller alfanum.         HFL  *
6.20  *   sst     190312  Tilpasning til nytt prosjektsystem                  *
6.21  *   nho     130511  Endret startposisjon for momskode                   *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     freinpf    if   e           k disk
     freutpf    o  a e           k disk
      *
     d*---------------------------------------------------------------------
     d* Stand Alone Fields - TOP
     d*---------------------------------------------------------------------
     d p_flgr          s              3
     d p_firmx         s              3
     d w_firm          s              3  0
     d*---------------------------------------------------------------------
     d* Stand Alone Fields - BOTTOM
     d*---------------------------------------------------------------------
      * Definering av recordbeskrivelse - eksportfil fra H&L lønn
     d                 ds
     d d_inpu                       130
     d  d_kont                        6    overlay(d_inpu:3)
     d   d_kontn                      6  0 overlay(d_kont:1)
6.21 d* d_kmva                        1    overlay(d_inpu:9)
6.21 d  d_kmva                        1    overlay(d_inpu:10)
     d   d_kmvan                      1  0 overlay(d_kmva:1)
     d  d_avde                        4    overlay(d_inpu:15)
     d   d_avden                      4  0 overlay(d_avde:1)
     d  d_pros                        6    overlay(d_inpu:21)
     d   d_prosn                      6  0 overlay(d_pros:1)
     d  d_spes                        4    overlay(d_inpu:31)
     d   d_spesn                      4  0 overlay(d_spes:1)
     d  d_akt                         3    overlay(d_inpu:40)
     d  d_dato                        8  0 overlay(d_inpu:79)
     d   d_per                        2  0 overlay(d_dato:3)
     d   d_aar                        4  0 overlay(d_dato:5)
     d  d_bdat                        8  0 overlay(d_inpu:90)
     d   d_bdag                       2  0 overlay(d_bdat:1)
     d   d_bmnd                       2  0 overlay(d_bdat:3)
     d   d_baar                       4  0 overlay(d_bdat:5)
     d  d_ants                        1    overlay(d_inpu:98)
     d  d_anta                        9  2 overlay(d_inpu:99)
     d  d_sign                        1    overlay(d_inpu:118)
     d  d_bel                        10  2 overlay(d_inpu:119)
      *
      * Bilagsdato på format ÅÅÅÅMMDD
      *
     d                 ds
     dw_bdat                   1      8  0
     dtbaar                    1      4  0
     dtbmnd                    5      6  0
     dtbdag                    7      8  0
      *
      * Leser eksportfil/H&L lønn
      *
     c                   read      reinpf
     c                   dow       not %eof
      *
     c                   eval      d_inpu = reinrec
     c                   exsr      oppr_lpos
     c                   read      reinpf
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av lønnsposteringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppr_lpos     begsr
      *
     c                   clear                   reutpfr
      *
     c                   eval      d_avde = %xlate(' ':'0':d_avde)
     c                   eval      d_pros = %xlate(' ':'0':d_pros)
     c                   eval      d_spes = %xlate(' ':'0':d_spes)
      * Filen fra H&L må korrigeres når det ikke er aktivitet
     c                   if        d_akt  = '  0' or
     c                             d_akt  = '000'
     c                   eval      d_akt  = *blanks
     c                   endif
     c*                  eval      d_akt  = %xlate('+':'Å':d_akt)
      *
     c                   eval      reflgr = p_flgr
     c                   eval      refirm = w_firm
     c                   eval      rekont = d_kontn
     c                   eval      remvak = d_kmva
     c                   eval      reavde = d_avden
6.20 c                   eval      repros = d_pros
6.20 c                   if        d_prosn = *zero
6.20 c                   eval      repros = *blank
6.20 c                   endif
     c                   eval      respes = d_spesn
     c                   eval      reakti = d_akt
      *
     c                   if        d_ants = '-'
     c                   eval      remngd = 0 - d_anta
     c                   else
     c                   eval      remngd = d_anta
     c                   endif
      *
     c                   eval      rerper = d_per
     c                   eval      reraar = d_aar
     c                   eval      tbaar  = d_baar
     c                   eval      tbmnd  = d_bmnd
     c                   eval      tbdag  = d_bdag
     c                   move      w_bdat        rebdat
     c                   if        d_sign = '-'
     c                   z-sub     d_bel         rebelØ
     c                   else
     c                   eval      rebelØ = d_bel
     c                   endif
      *
      * Skriv post
      *
     c                   write     reutpfr
     c                   endsr
      * ----------------------------------------
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_flgr
     c                   parm                    p_firmx
      *
     c                   move      p_firmx       w_firm
      *
     c                   endsr
