     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LI900R
      * Beskrivelse . . : Sjekker dubletter av GTIN nummer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       230512 bsa  Nytt program
6.10  *       020712 bsa  Lagt inn ytterligere tester mot enheter
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvepl2    if   e           k disk    rename(vveppfr:vvepl2r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     f                                     prefix(xe:2)
     fli900p    o    e             printer
      *
      * Definering av parametre
      *
     d w_stat          s              1
     d w_pgti          s                   like(vepgti)
     d w_pvar          s                   like(vepvar)
      *
     d b_first         s              1    inz(*on)
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
      * Definering av variable
      *
     d w_firm          s                   like(vepfir)
      *
     d w_udat          s               d   datfmt(*iso)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      b_first = *on
      *
      * Leser VVEP for å finne GTIN dupbletter
      *
     c     vvepl2_key    setll     vvepl2
     c     vvepl2_key    reade     vvepl2
     c                   dow       not %eof
      *
     c                   if        vepgti <> 0
     c                   eval      w_pgti = vepgti
     c                   eval      w_pvar = vepvar
     c                   eval      w_stat = *blank
      *
      * Kall program for å sjekke dubletter
      *
     c                   call      'VE718R'
     c                   parm                    w_pgti
     c                   parm                    w_pvar
     c                   parm                    w_stat
      *
     c                   if        w_stat <> *blank
      * duplikat finnes - skriver logg
     c                   if        b_first = *on
     c                   exsr      skr_rapph
     c                   endif
      *
     c                   exsr      skr_rappl
      *
     c                   endif
     c                   endif
      *
     c     vvepl2_key    reade     vvepl2
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for skriving av rapportheader v/ utskrift valgt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_rapph     begsr
      *
     c                   eval      a1firm = l_firm
     c                   eval      a1fnav = l_fnav
     c                   eval      a1wsid = l_wsid
     c                   time                    a1time
     c     *dmy          move      w_udat        a1date
     c                   eval      a1user = l_user
      *
     c                   write     a1hdr                                30
      *
     c                   eval      b_first = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for skriving av rapportlinje v/ utskrift valgt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_rappl     begsr
      *
     c                   eval      a1pvar = vepvar
     c                   eval      a1pgti = vepgti
     c                   if        w_stat = '1'
     c                   eval      a1text = 'GTIN-nr finnes på minst -
     c                             en annen vare på VVEP'
6.10 c                   elseif    w_stat = '2'
     c                   eval      a1text = 'GTIN-nr finnes på minst -
     c                             en annen vare på VEAN'
6.10 c                   elseif    w_stat = '3'
6.10 c                   eval      a1text = 'Forskjellige enheter på -
6.10 c                             samme GTIN nummer'
     c                   endif
      *
     c                   write     a1det                                30
      *
      * Er det overflow ?
      *
     c                   select
     c                   when      *in30 = *on
     c                   exsr      xovrfl
     c                   endsl
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for overflow                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xovrfl        begsr
      *
     c                   write     a1hdr                                30
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c*    *entry        plist
     c*                  parm                    p_anta
      *
      * Key for les på EDI-melding
      *
     c     vvepl2_key    klist
     c                   kfld                    w_firm
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
     c                   time                    w_udat
      *
     c                   endsr
