      /TITLE  ASOKON Arbeid med returfiler
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * System  . . . . : ASOKON                                        *
      * Program . . . . : GL228R                                        *
      * Beskrivelse . . : Historikk over oppdaterte returer, med        *
      *                   mulighet for å legge tilbake batch.           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      *                                                                 *
      *           260600  Nytt program.                            RH   *
      *                                                                 *
      * 30011     300101  Legger sonekontonummer ut i feltet       RH   *
      *                   for bankgironummer.                      RH   *
      *                                                                 *
      * 13071     130701  Legger tilbake utbet.forslaget fra file       *
      *                   GKBFPF til GUBFPF.                            *
      * 13023     130203  Sperret GAPGIR/GKPGIR                    AMD  *
7.xx  * 7.xx      120618  Endringer reltart til ny GUI. NB Kan kun gjelde BKV
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KC = F3  = Exit                                           *
      *       KE = F5  = Forny                                          *
      *       KL = F12 = Annuller                                       *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *   SUBFILE-1:                                                    *
      *       10 = Roll                                                 *
      *       12 = Sfldsp                                               *
      *       13 = Sfldspctl                                            *
      *       14 = Sflclr                                               *
      *       15 = Sflend                                               *
      *       16 = Readc 16 on, no more records                         *
      *   SUBFILE-2:                                                    *
      *       20 = Roll                                                 *
      *       22 = Sfldsp                                               *
      *       23 = Sfldspctl                                            *
      *       24 = Sflclr                                               *
      *       25 = Sflend                                               *
      *       26 = Readc 26 on, no more records                         *
      *                                                                 *
      *   SUBFILE-3:                                                    *
      *       30 = Roll                                                 *
      *       32 = Sfldsp                                               *
      *       33 = Sfldspctl                                            *
      *       34 = Sflclr                                               *
      *       35 = Sflend                                               *
      *       36 = Readc 36 on, no more records                         *
      *                                                                 *
      *    40-50 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    44    = Viser periodefelt i skjrmebildet:                    *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fgl228d    cf   e             workstn
     f                                     sfile(b1sfl:srrn01)
     f                                     sfile(c1sfl:srrn02)
     f                                     sfile(d1wsf:srrn03)
     fgatrlu    uf   e           k disk    rename(gatrpfr:gatrpfu)
     fgatrpf    o  a e           k disk    rename(gatrpfr:gatrpff)
     fgavtl2    if   e           k disk
     fgbtrpf    uf   e           k disk    rename(gbtrpfr:gbtrpff)
     fgbtrl1    if   e           k disk    rename(gbtrpfr:gatrpf1)
     fgubfpf    o  a e           k disk
     fgkbfpf    uf   e           k disk
      *
     d giro            s             11  0 dim(999)                             Gironummer
     d lØpn            s              5  0 dim(999)                             Løpenummer
     d type            s              3    dim(999)                             Type oppdrag
     d oppd            s              7  0 dim(999)                             Oppdrag
     d dato            s              8  0 dim(999)                             Dato
     d klok            s              6  0 dim(999)                             Klokkeslett
     d filg            s              3    dim(999)                             Filgruppe
     d firm            s              3  0 dim(999)                             Firma
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av felter                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      *     nn - er 01 for subfile 01,
      *             02 for subfile 02,
      *             03 for subfile 03, o.s.v.
      * SRRNnn - Relativt recordnummer i subfile
      * SPGEnn - Sidestørrelse i subfile (page)
      * SFRNnn - Første recordnummer på siden
      * SSRNnn - Siste  recordnummer på siden
      * SVRNnn - Vis den side som inneholder denne record.
      *
      * Forklaring på spesielle felter
      * som benyttes ved windows.
      * - - - - - - - - - - - - - - - - -
      *
      * w_arow - Actual line, angir startlinje for vindu.
      * w_acol - Actual posision, angir startposisjon for vindu.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record - Mottatte transaksjoner
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1SFL  - C1 Subfile - record
      * C2CTL  - C2 Subfile - control record - Viser RECORD ("rådump").
      * C2CMD  - C2 Eget skjermbilde for CMD-taster
      * D1WSF  - C1 Subfile window - record
      * D2WCT  - C2 Subfile window - control record - Til oppdatering.
      * D2CMD  - C2 Eget skjermbilde for CMD-taster - Window definisjon
      *
      /eject
      *
     d lda            uds
     d l_prtf                800    809
     d l_chgv                844    844
     d l_user                911    920
     d l_flgr                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Datastruktur for dato:
      *
     d                 ds
     d wpdato                         8  0
     d  wpdatÅ                        4  0 overlay(wpdato)
     d  wpdatm                        2  0 overlay(wpdato:5)
     d  wpdatd                        2  0 overlay(wpdato:7)
      *
      * Datastruktur for dato:
      *
     d                 ds
     d dsdato                         8  0
     d  dsdatd                        2  0 overlay(dsdato)
     d  dsdatm                        2  0 overlay(dsdato:3)
     d  dsdatÅ                        4  0 overlay(dsdato:5)
      *
      * Datastruktur for å "pakke" record fra 80x4 til 320x1:
      *
     d                 ds
     d dstota                       160
     d  dsa                          80    overlay(dstota)
     d  dsb                          80    overlay(dstota:81)
     d dstotb                       160
     d  dsc                          80    overlay(dstotb)
     d  dsd                          80    overlay(dstotb:81)
      *
      * Generell datastruktur for GTRTPF-record:
      *
     d                 ds
     d dsa00a                       160
     d dsa00b                       160
      *
      * Datastruktur som GTRTPF legges over i dersom BETFOR01:
      *
     d                 ds
     d dsb00a                       160
     d  dsbfgr                        3    overlay(dsb00a:95)
     d  dsbfir                        3  0 overlay(dsb00a:108)
     d dsb00b                       160
      *
      * Datastruktur som GTRTPF legges over i dersom BETFOR21:
      *
     d                 ds
     d dsc00a                       160
     d  dscfgr                        3    overlay(dsc00a:95)
     d  dscfir                        3  0 overlay(dsc00a:108)
     d dsc00b                       160
      *
      * Datastruktur som GTRTPF legges over i dersom BETFOR04:
      *
     d                 ds
     d dsd00a                       160
     d  dsdfgr                        3    overlay(dsd00a:131)
     d  dsdfir                        3  0 overlay(dsd00a:134)
     d dsd00b                       160
      *
      * Datastruktur som GBTRPF legges over i:
      *
13071d                 ds
     d dsrec1                        80
     d  dsbetf                        8    overlay(dsrec1:41)
      *
      * Datastruktur som GBTRPF legges over i:
      *
13071d                 ds
     d dsrec2                        80
     d  dsubfn                        3  0 overlay(dsrec2:9)
     d  dsubfl                        5  0 overlay(dsrec2:12)
      *
      * Datastruktur som GTRTPF legges over i dersom BETFOR23:
      *
     d                 ds
     d dse00a                       160
     d dse00b                       160
     d  dsefgr                        3    overlay(dse00b:83)
     d  dsefir                        3  0 overlay(dse00b:86)
      *
      * Datastruktur for test på periode:
      *
     d                 ds
     d dsperi                         6  0
     d  dspemÅ                        2  0 overlay(dsperi)
     d  dspeÅr                        4  0 overlay(dsperi:3)
      *
     d d2peri          s              4  0
     d i               s              3  0
     d sfrn01          s                   like(srrn01)
     d sfrn02          s                   like(srrn02)
     d sfrn03          s                   like(srrn03)
     d spge01          s              3  0
     d spge02          s              3  0
     d spge03          s              3  0
     d srrn01          s              4  0
     d srrn02          s              4  0
     d srrn03          s              4  0
     d ssrn01          s                   like(srrn01)
     d ssrn02          s                   like(srrn02)
     d ssrn03          s                   like(srrn03)
     d w_arow          s              2  0
     d w_acol          s              3  0
     d wjanei          s              3
     d wpgiro          s                   like(gfgiro)
     d wpink           s              2
     d wpin03          s              1  0
     d wpin09          s              2
     d wpin12          s              1  0
     d wplØpn          s                   like(gflØpn)
     d wpmber          s              7
     d wpocr           s              2
     d wpoppd          s              2
     d wppeÅr          s              4  0
     d wppemÅ          s              2  0
     d wpperi          s              6
     d wpregn          s              2
     d wptele          s              2
     d wptell          s                   like(gftell)
     d wptype          s                   like(gftype)
     d wtelle          s              1  0
     d xxgiro          s                   like(gfgiro)
     d xxlØpn          s                   like(gflØpn)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * B2CTL Behandle åpningsbildet med subfile                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Send Alt
      *
     c     b2taga        tag
     c     wpin12        cabeq     1             xslutt
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      xdsp01
      *
      * F3=Avslutt program i fra subfile program.
      * *IN40=*ON -> Record på avtaleregister finnes ikke
      *              for pålogget bruker-id:
      *
     c     *inkc         cabeq     *on           xslutt
     c     *in40         cabeq     *on           b2tagb
      *
      * Forny
      *
     c                   if        *inke = *on                                  .....
      *                                                        .
      * Henter gironummer ut i fra filgruppe/firmanummer:      .
      *                                                        .
     c     key03         setll     gavtl2                                           .
     c     key03         reade     gavtl2                                 60        .
      *                                                        .
     c                   eval      wpgiro = 0                                       .
      *                                                        .
     c                   if        gcbgir <> 0                                  ... .
     c                   eval      wpgiro = gcbgir                                . .
     c                   else                                                     . .
     c                   eval      wpgiro = gcpgir                                . .
     c                   endif                                                  ... .
      *                                                        .
      * Bruker SONEKONTONUMMER (GCSKTO) i fra faste data,      .
      * i steden for BANKKONTONUMMER (GCBGIR).                 .
      *                                                        .
30011c                   eval      wpgiro = gcskto                                  .
      *                                                        .
     c                   exsr      xinke                                            .
     c                   if        sfrn01 <> *zero                              ... .
     c                   goto      b2tagb                                         . .
     c                   else                                                     . .
     c                   goto      b2taga                                         . .
     c                   endif                                                  ... .
     c                   endif                                                  .....
      *
      * Roll
      *
     c                   if        *in10 = *on                                           .....
     c                   exsr      xcrt01                                       CRT SFL      .
     c                   goto      b2tagb                                                    .
     c                   endif                                                           .....
      *
      * Posisjoner startverdi:
      *
      * B2VALG OG B2LØPN ER IKKE I BRUK!
      *
     c                   if        b2valg = *blank                                       .....
     c                   if        b2lØpn <> *zero                                           .
     c                   eval      wplØpn = b2lØpn - 1                                       .
     c                   exsr      xclr01                                       TØM SFL      .
     c                   exsr      xcrt01                                       CRT SFL      .
     c                   eval      b2lØpn = 0                                                .
     c                   goto      b2taga                                                    .
     c                   endif                                                               .
     c                   endif                                                           .....
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Behandling av subfile                                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Sjekk om valg
      * er foretatt i
      * subfile
      *
     c                   do        ssrn01                                                .....
     c                   readc     b1sfl                                  16                 .
      *                                                                 .
     c                   if        *in16 = *on                                  .....        .
     c                   leave                                                      .        .
     c                   endif                                                  .....        .
      *                                                                 .
      * Velge for oppdatering:                                          .
      *                                                                 .
     c                   if        b1valg = '1'                                 .....        .
      *                                                        .        .
      * Tester at post i subfile ikke allerede er valgt via    .        .
      * feltene B2VALG/B2LØPN.  I såfall vil første element    .        .
      * i tabellen inneholde denne verdien:                    .        .
      *                                                        .        .
     c                   if        b1lØpn = lØpn(1)                             ... .        .
     c                   eval      b1valg = *blank                                . .        .
     c                   update    b1sfl                                          . .        .
     c                   iter                                                     . .        .
     c                   endif                                                  ... .        .
      *                                                        .        .
      * Må lagre innhold av WPGIRO og WPLØPN, slik at          .        .
      * posisjonering i subfile-1 opprettholdes:               .        .
      *                                                        .        .
     c                   eval      xxlØpn = wplØpn                                  .        .
     c                   eval      xxgiro = wpgiro                                  .        .
      *                                                        .        .
      * Fyller opp tabell med informasjon om batch som skal    .        .
      * oppdateres.  NB! Det vil ikke være tillatt å kjøre     .        .
      * dobbel oppdatering på OCR-returen, d.v.s. tellerfelt   .        .
      * GBTELL må være ulik null:                              .        .
      *                                                        .        .
     c                   if        b1tell = 0                                   ... .        .
     c                   eval      b1valg = *blank                                . .        .
     c                   update    b1sfl                                          . .        .
     c                   iter                                                     . .        .
     c                   endif                                                  ... .        .
      *                                                        .        .
      * Overfører data fra GBTRPF til GATRPF:                  .        .
      *                                                        .        .
     c                   eval      wpin12 = 0                                       .        .
      *                                                        .        .
      * Legger over verdier til tabellelementer:               .        .
      *                                                        .        .
      *                                                        .        .
     c                   eval      wpgiro = b1bgir                                  .        .
     c                   eval      wplØpn = b1lØpn                                  .        .
      *                                                        .        .
     c                   exsr      xcopy                                            .        .
      *                                                        .        .
     c                   move      srrn01        svrn01                             .        .
     c                   eval      b1valg = *blank                                  .        .
     c                   update    b1sfl                                            .        .
      *                                                        .        .
     c                   iter                                                       .        .
     c                   endif                                                  .....        .
      *                                                                 .
      * Vise post (går over i subfile-2):                               .
      *                                                                 .
     c                   if        b1valg = '5'                                 .....        .
      *                                                        .        .
      * Må lagre innhold av WPGIRO og WPLØPN for å opprettholde.        .
      * posisjonering av Subfile-1:                            .        .
      *                                                        .        .
     c                   eval      xxgiro = wpgiro                                  .        .
     c                   eval      xxlØpn = wplØpn                                  .        .
      *                                                        .        .
      * Må lagre innhold av WPLØPN, slik at posisjonering i    .        .
      * subfile 1 opprettholdes:                               .        .
      *                                                        .        .
     c                   eval      wpin12 = 0                                       .        .
      *                                                        .        .
      * Bygger nøkkel:                                         .        .
      *                                                        .        .
     c                   eval      wplØpn = b1lØpn                                  .        .
     c                   if        b1bgir <> 0                                  ... .        .
     c                   eval      wpgiro = b1bgir                                . .        .
     c                   else                                                     . .        .
     c                   eval      wpgiro = b1pgir                                . .        .
     c                   endif                                                  ... .        .
      *                                                        .        .
     c                   eval      c2lØpn = b1lØpn                                  .        .
     c                   eval      c2dato = b1dato                                  .        .
     c                   eval      c2klok = b1klok                                  .        .
     c     key01         setll     gbtrl1                                           .        .
      * Fyll opp subfile:                                      .        .
     c                   exsr      xclr02                                           .        .
     c                   exsr      xcrt02                                           .        .
     c                   exsr      xc5bld                                           .        .
      *                                                        .        .
      * Legger tilbake innhold av Giro og Løpenummer for       .        .
      * Subfile-1, for å komme "tilbake" til riktig posisjon:  .        .
      *                                                        .        .
     c                   eval      wpgiro = xxgiro                                  .        .
     c                   eval      wplØpn = xxlØpn                                  .        .
      *                                                        .        .
     c                   iter                                                       .        .
     c                   endif                                                  .....        .
      *                                                                 .
     c                   enddo                                                           .....
      *
     c                   goto      b2taga
      *
      /space 3
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * C2BLD Behandle bilde for å vise batch:                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xc5bld        begsr
      *
     c     c2taga        tag
      *
     c                   write     c2cmd
      *
      * Bare data
      *
     c     c2tagb        tag
      *
     c                   exsr      xdsp02
      *
      * F3 =Avslutt program i fra subfile program.
      *
     c     *inkc         cabeq     *on           xutc2b
      *
      * Roll
      *
     c                   if        *in20 = *on                                  ...
     c                   exsr      xcrt02                                         .
     c                   goto      c2tagb                                         .
     c                   endif                                                  ...
      *
     c     xutc2b        tag
      *
     c                   if        b1valg = '5'                                 .....
     c                   move      srrn01        svrn01                             .
     c                   eval      b1valg = *blank                                  .
     c                   update    b1sfl                                            .
     c                   endif                                                  .....
      *
     c                   eval      b2valg = *blank
     c                   eval      b2lØpn = 0
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * C3BLD Behandle bilde for å oppdatere batcher:                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xc1bld        begsr
      *
      * Legger inn dagens periode som forslag til regnskapsperiode:
      *
     c                   eval      d2peri = uyear
     c                   movel     umonth        d2peri
      *
      * Legger inn verdi "K" for Kontrolliste:
      *
     c                   movel     'K'           d2joko
      *
      * Send Alt
      *
     c     d2taga        tag
      *
      * Posisjonerer vindu:
      *
     c                   eval      w_arow = 4
     c                   eval      w_acol = 29
      *
     c                   write     d2cmd
      *
      * Bare data
      *
     c     d2tagb        tag
      *
     c                   exsr      xdsp03
      * F3=Avslutt vindu:
     c     *inkc         cabeq     *on           xutc1
      *
      * Dersom det er valgt ut poster som også skal videre til
      * regnskapsoppdatering, så må det testes på at perioden
      * er riktig:
      *
     c                   eval      wpperi = *blanks
     c                   eval      dsperi = 0
      *
      * Roll
      *
     c                   if        *in30 = *on                                  .....
     c                   exsr      xcrt03                                           .
     c                   goto      d2tagb                                           .
     c                   endif                                                  .....
      *
     c                   goto      d2taga
      *
     c     xutc1         tag
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Tøm subfile - 1                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xclr01        begsr
     c                   move      *on           *in14
     c                   write     b2ctl
     c                   move      *off          *in14
      *
     c                   move      *off          *in15
     c                   eval      srrn01 = 0
     c                   eval      sfrn01 = 0
     c                   eval      ssrn01 = 0
     c                   eval      spge01 = 0
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Fyll opp subfile - 1                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xcrt01        begsr
     c                   eval      spge01 = spge01 + 12                         SFL END
     c                   eval      srrn01 = ssrn01
      *
     c                   dou       srrn01 = spge01                                       .....
      *                                                                 .
     c     xnyles        tag                                                                 .
      *                                                                 .
      * Leser på giro-/løpenummer:                                      .
      *                                                                 .
     c                   eval      wplØpn = wplØpn + 1                                       .
     c     key01         setll     gbtrl1                                                    .
     c     key01         reade     gbtrl1                                 15                 .
      *                                                                 .
      * *IN15 indikerer at det ikke finnes flere poster pr.             .
      * GIRONUMMER/LØPENUMMER.  Det må likevel testes på om             .
      * det er huller i LØPENUMMER-serien på filen.  Ny les på          .
      * kun GIRONUMMER må derfor foretas:                               .
      *                                                                 .
     c                   if        *in15 = *on                                  .......      .
     c     key01         setll     gbtrl1                                             .      .
     c     wpgiro        reade     gbtrl1                                 15          .      .
      *                                                          .      .
      * Dersom det ikke finnes flere poster for firmaet på       .      .
      * gjeldende kontonummer, så må det testes på om det        .      .
      * finnes poster for firmaet på et annet kontonummer:       .      .
      *                                                          .      .
     c                   if        *in15 = *on                                  ..... .      .
      *                                                        . .      .
     c     key03         reade     gavtl2                                 60        . .      .
     c     *in60         cabeq     *on           xend01                             . .      .
      * Initierer les av nye poster for nytt kontonummer:      . .      .
     c                   if        gcbgir <> 0                                  ... . .      .
     c                   eval      wpgiro = gcbgir                                . . .      .
     c                   else                                                     . . .      .
     c                   eval      wpgiro = gcpgir                                . . .      .
     c                   endif                                                  ... . .      .
      *                                                        . .
      * Bruker SONEKONTONUMMER (GCSKTO) i fra faste data,      . .
      * i steden for BANKKONTONUMMER (GCBGIR).                 . .
      *                                                        . .
30011c                   eval      wpgiro = gcskto                                  . .
      *                                                        . .
     c                   exsr      xfØrst                                           . .      .
     c                   goto      xnyles                                           . .      .
     c                   endif                                                  ..... .      .
      *                                                          .      .
      * Det er huller i løpenummer serien...                     .      .
      *                                                          .      .
     c                   eval      wplØpn = gflØpn                                    .      .
     c     key01         setll     gbtrl1                                             .      .
     c     key01         reade     gbtrl1                                 15          .      .
     c     *in15         cabeq     *on           xend01                         EOF   .      .
     c                   endif                                                  .......      .
      *                                                                 .
      * Dersom det er valgt F9 for kun å vise poster som ikke er        .
      * oppdatert, så skal kun returer som ikke har teller lik          .
      * "0" vises:                                                      .
      *                                                                 .
     c                   if        wpin09 = *blanks                             ...          .
     c                             and gftell = 0                                 .          .
     c                   goto      xnyles                                         .          .
     c                   endif                                                  ...          .
      * Snur dato:                                                      .
     c                   eval      wpdato = gfdato                                           .
     c                   eval      dsdatÅ = wpdatÅ                                           .
     c                   eval      dsdatm = wpdatm                                           .
     c                   eval      dsdatd = wpdatd                                           .
      * Display felter:                                                 .
     c                   eval      srrn01 = srrn01 + 1                                       .
04011c                   movel     gcskod        b1skod                                      .
     c                   movel     gffgrp        b1filg                                      .
     c                   eval      b1firm = gffirm                                           .
     c                   eval      b1dato = dsdato                                           .
     c                   eval      b1klok = gfklok                                           .
     c                   eval      b1oppd = gfoppd                                           .
     c                   eval      b1lØpn = gflØpn                                           .
     c                   movel     gftype        b1type                                      .
      *Skjulte-felter                                                   .
     c                   eval      b1bgir = gfgiro                                           .
     c                   eval      b1tell = gftell                                           .
      *                                                                 .
      * GBTELL er null dersom batch allerede er oppdatert:              .
      *                                                                 .
     c                   eval      b1kode = *blank                                           .
      *                                                                 .
     c                   if        b1tell = 0                                   ...          .
     c                   eval      b1kode = *blanks                               .          .
     c                   movel     '*'           b1kode                           .          .
     c                   endif                                                  ...          .
      *                                                                 .
     c                   write     b1sfl                                                     .
     c                   enddo                                                           .....
      *
     c     xend01        tag
      *
     c                   if        srrn01 > ssrn01                                       .....
     c                   eval      sfrn01 = ssrn01 + 1                                       .
     c                   endif                                                           .....
      *
     c                   eval      ssrn01 = srrn01
     c                   eval      svrn01 = sfrn01
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Display subfile - 1                                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xdsp01        begsr
      *
     c     xdstag        tag
      *
     c                   if        srrn01 <> 0                                           .....
     c                   move      *on           *in12                                       .
     c                   endif                                                           .....
      *
     c                   move      *on           *in13
     c                   exfmt     b2ctl
      *
     c                   move      *off          *in12
     c                   move      *off          *in13
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Tøm subfile - 2                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xclr02        begsr
     c                   move      *on           *in24
     c                   write     c2ctl
     c                   move      *off          *in24
      *
     c                   move      *off          *in25
     c                   eval      srrn02 = 0
     c                   eval      sfrn02 = 0
     c                   eval      ssrn02 = 0
     c                   eval      spge02 = 0
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Fyll opp subfile - 2                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xcrt02        begsr
     c                   eval      spge02 = spge02 + 15                         SFL END
     c                   eval      srrn02 = ssrn02
      *
     c                   dou       srrn02 = spge02                                       .....
      * Leser på giro-/løpenummer:                                      .
     c     key01         reade     gbtrl1                                 25                 .
     c     *in25         cabeq     *on           xend02                         EOF          .
      *
     c                   movel     gfreco        c2reco
     c                   eval      srrn02 = srrn02 + 1                                       .
      *                                                                 .
     c                   write     c1sfl                                                     .
     c                   enddo                                                           .....
      *
     c     xend02        tag
      *
     c                   if        srrn02 > ssrn02                                       .....
     c                   eval      sfrn02 = ssrn02 + 1                                       .
     c                   endif                                                           .....
      *
     c                   eval      ssrn02 = srrn02
     c                   eval      svrn02 = sfrn02
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Display subfile - 2                                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xdsp02        begsr
      *
     c                   if        srrn02 <> 0                                           .....
     c                   move      *on           *in22                                       .
     c                   endif                                                           .....
      *
     c                   move      *on           *in23
     c                   exfmt     c2ctl
     c                   move      *off          *in22
     c                   move      *off          *in23
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Tøm subfile - 3                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xclr03        begsr
     c                   move      *on           *in34
     c                   write     d2wct
     c                   move      *off          *in34
      *
     c                   move      *off          *in35
     c                   eval      srrn03 = 0
     c                   eval      sfrn03 = 0
     c                   eval      ssrn03 = 0
     c                   eval      spge03 = 0
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Fyll opp subfile - 3                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xcrt03        begsr
     c                   eval      spge03 = spge03 + 6                          SFL END
     c                   eval      srrn03 = ssrn03
      *
     c                   dou       srrn03 = spge03                                       .....
      * Leser tabell:                                                   .
     c                   eval      i = i + 1                                                 .
      * Tom tabell eller max. teller:                                   .
     c                   if        giro(i) = 0                                  .....        .
     c                             or i = 999                                       .        .
     c                   eval      i = i - 1                                        .        .
     c                   goto      xend03                                       EOF .        .
     c                   endif                                                  .....        .
      *                                                                 .
     c                   eval      d1lØpn = lØpn(i)                                          .
     c                   movel     type(i)       d1type                                      .
     c                   eval      d1dato = dato(i)                                          .
     c                   eval      d1klok = klok(i)                                          .
     c                   eval      d1oppd = oppd(i)                                          .
     c                   eval      d1firm = firm(i)                                          .
     c                   movel     filg(i)       d1filg                                      .
      *                                                                 .
      * Setter "Flagg" for å indikere at det finnes transaksjoner       .
      * som skal videre til regnskap, d.v.s. ved 2.gangsretur og        .
      * OCR-retur.  *IN44 åpner feltet for regnskapsperiode i           .
      * skjermbildet:                                                   .
      *                                                                 .
     c                   if        type(i) = 'TF2'                                           .
     c                             or type(i) = 'OCR'                                        .
     c                   move      'JA'          wpregn                                      .
     c                   endif                                                               .
      *                                                                 .
     c                   eval      srrn03 = srrn03 + 1                                       .
      *Skjulte-felter                                                   .
     c                   eval      d1bgir = giro(i)                                          .
      *                                                                 .
     c                   write     d1wsf                                                     .
     c                   enddo                                                           .....
      *
     c     xend03        tag
      *
     c                   if        srrn03 > ssrn03                                       .....
     c                   eval      sfrn03 = ssrn03 + 1                                       .
     c                   endif                                                           .....
      *
     c                   eval      ssrn03 = srrn03
     c                   eval      svrn03 = sfrn03
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Display subfile - 3                                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xdsp03        begsr
      *
     c                   if        srrn03 <> 0                                           .....
     c                   move      *on           *in32                                       .
     c                   endif                                                           .....
      *
     c                   move      *on           *in33
     c                   exfmt     d2wct
     c                   move      *off          *in32
     c                   move      *off          *in33
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering første løpenummer det skal leses på.   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xfØrst        begsr
     c     key00         setll     gbtrl1
     c     key00         reade     gbtrl1                                 60
     c                   if        *in60 = *off                                 ...
     c                   eval      wplØpn = gflØpn                                .
     c                   else                                                     .
     c                   eval      wplØpn = 0                                     .
     c                   endif                                                  ...
     c                   if        wplØpn >= 1                                  ...
     c                   eval      wplØpn = wplØpn - 1                            .
     c                   endif                                                  ...
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for "forny" -> F5:                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     xinke         begsr
      * Nullstiller tabell:
     c                   eval      i = 0
     c                   eval      wpoppd = *blanks                                          .
     c                   movea     *zeros        giro                                        .
     c                   movea     *zeros        lØpn                                        .
     c                   movea     *blanks       type                                        .
     c                   movea     *zeros        oppd                                        .
     c                   movea     *zeros        dato                                        .
     c                   movea     *zeros        klok                                        .
     c                   movea     *zeros        firm                                        .
     c                   movea     *blanks       filg                                        .
      * Henter løpenummer til første post i filen:
     c                   exsr      xfØrst
      * Nullstiller felt:
     c                   eval      b1valg = *blanks
     c                   eval      b2valg = *blanks
     c                   eval      b2lØpn = 0
      *
     c                   if        sfrn01 <> *zero
     c     sfrn01        chain     b1sfl                              60
     c                   endif
      *
      * Nøkkel på file er primært Bankgironummer dersom dette er
      * utfylt:
      *
     c                   if        gcbgir <> 0                                  ...
     c                   eval      wpgiro = gcbgir                                .
     c                   else                                                     .
     c                   eval      wpgiro = gcpgir                                .
     c                   endif                                                  ...
      *
      * Bruker SONEKONTONUMMER (GCSKTO) i fra faste data,
      * i steden for BANKKONTONUMMER (GCBGIR).
      *
30011c                   eval      wpgiro = gcskto
      *
     c                   eval      wplØpn = 0
      *
     c                   exsr      xclr01                                       TØM SFL
     c                   exsr      xcrt01                                       CRT SFL
     c                   endsr
      *                                                              .
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Vise poster som er valgt til oppdatering:                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xsbf3         begsr                                                               .
      * Fyll opp subfile:                                               .
     c                   eval      i = 0                                                     .
     c                   exsr      xclr03                                                    .
     c                   exsr      xcrt03                                                    .
     c                   exsr      xc1bld                                                    .
     c                   eval      wpoppd = *blanks                                          .
     c                   movea     *zeros        giro                                        .
     c                   movea     *zeros        lØpn                                        .
     c                   movea     *blanks       type                                        .
     c                   movea     *zeros        oppd                                        .
     c                   movea     *zeros        dato                                        .
     c                   movea     *zeros        klok                                        .
     c                   movea     *zeros        firm                                        .
     c                   movea     *blanks       filg                                        .
     c                   eval      wplØpn = xxlØpn                                           .
     c                   eval      i = 0                                                     .
     c                   endsr                                                               .
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for å ta kopi av batcher som er valgt til oppdatering *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
09060c     xcopy         begsr
      *
      * Sletter batch i fra GATRPF for å unngå kopier:
      *
     c     key01         setll     gatrlu
     c     key01         reade     gatrlu                                 63
      *
     c                   dow       *in63 = *off                                 ........
     c                   delete    gatrpfu                                             .
     c     key01         reade     gatrlu                                 63           .
     c                   enddo                                                  ........
      *
      * Legger over batch fra GBTRPF til GATRPF:
      *
     c     key01         setll     gbtrpf
     c     key01         reade     gbtrpf                                 63
      *
     c                   dow       *in63 = *off                                 ........
     c                   movel     gfreco        gbreco                                .
     c                   eval      gbgiro = gfgiro                                     .
     c                   eval      gblØpn = gflØpn                                     .
     c                   movel     gftype        gbtype                                .
     c                   eval      gbtell = gftell                                     .
     c                   eval      gboppd = gfoppd                                     .
     c                   eval      gbdato = gfdato                                     .
     c                   eval      gbklok = gfklok                                     .
     c                   movel     gffgrp        gbfgrp                                .
     c                   eval      gbfirm = gffirm                                     .
     c                   write     gatrpff                                             .
      *                                                           .
13071c                   add       1             wtelle                                .
      *                                                           .
      * Tester på om batchen er en TF2.  I såfall må også         .
      * forslaget legges tilbake fra GKBFPF til GUBFPF:           .
      *                                                           .
13071c                   movel     gfreco        dsrec1                                .
      *                                                           .
13071c                   if        dsbetf = 'BETFOR23'                          ...    .
13071c                             and gftype = 'TF2'                             .    .
13071c                   eval      wtelle = 1                                     .    .
13071c                   movel     'JA '         wjanei                           .    .
13071c                   endif                                                  ...    .
      *                                                           .
      * Dersom det har vært lest en BETFOR23-post, OG det er      .
      * TF2, så må det i 4.posten hentes ut forslagsinfo:         .
      *                                                           .
13071c                   if        wtelle = 4                                   .....  .
13071c                             and wjanei = 'JA '                               .  .
13071c                   eval      wtelle = 0                                       .  .
13071c                   movel     'NEI'         wjanei                             .  .
13071c                   movel     gfreco        dsrec2                             .  .
      *                                                        .  .
      *  Overfører forslag fra backupfil til forslagfilen:     .  .
      *                                                        .  .
13071c     key04         chain     gkbfpf                             64            .  .
13071c                   if        *in64 = *off                                 ... .  .
13071c                   exsr      xcopy2                                         . .  .
13071c                   delete    gkbfpfr                                        . .  .
13071c                   endif                                                  ... .  .
      *                                                        .  .
13071c                   endif                                                  .....  .
      *                                                           .
      *                                                           .
     c     key01         reade     gbtrpf                                 63           .
     c                   enddo                                                  ........
      *
      * Sletter batch i fra GBTRPF for å unngå kopier:
      *
     c     key01         setll     gbtrpf
     c     key01         reade     gbtrpf                                 63
      *
     c                   dow       *in63 = *off                                 ........
     c                   delete    gbtrpff                                             .
     c     key01         reade     gbtrpf                                 63           .
     c                   enddo                                                  ........
      *
      * Sender informasjon om at kopiering er FERDIG!
      *
     c                   eval      w_arow = 8
     c                   eval      w_acol = 18
     c                   exfmt     a1win
      *
     c                   goto      xslutt
      *
09060c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for overføring av forslaget til hovedfilen GUBFPF:    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
13071c     xcopy2        begsr
      *
13071c                   movel     gkstat        gastat
13071c                   eval      gafirm = gkfirm
13071c                   eval      gabfnr = gkbfnr
13071c                   eval      gabfli = gkbfli
13071c                   eval      galevr = gklevr
     c                   eval      gabdat = gkbdat
     c                   eval      gatist = gktist
13071c                   movel     gknavn        ganavn
13071c                   movel     gkvalk        gavalk
13071c                   movel     gkfval        gafval
13071c                   movel     gkatts        gaatts
13071c                   movel     gkbilk        gabilk
13071c                   eval      gabelØ = gkbelØ
13071c                   eval      garest = gkrest
13071c                   eval      gabiln = gkbiln
13071c                   eval      gavalb = gkvalb
13071c                   eval      gavalr = gkvalr
13071c                   movel     gkbetk        gabetk
13071c                   eval      garaar = gkraar
13071c                   eval      garper = gkrper
13071c                   eval      gajour = gkjour
13071c                   eval      gasamb = gksamb
13071c                   eval      garefn = gkrefn
13071c                   movel     gktext        gatext
13071c                   movel     gkvirk        gavirk
     c                   eval      gafdat = gkfdat
13071c                   eval      garrf1 = gkrrf1
13071c                   eval      garrf2 = gkrrf2
13071c                   movel     gkdeck        gadeck
13071c                   movel     gkkidi        gakidi
13071c                   movel     gkkids        gakids
13071c                   eval      gaavde = gkavde
13071c                   movel     gkrekl        garekl
13071c                   eval      gadelb = gkdelb
13071c                   movel     gkbetb        gabetb
13071c                   movel     gkremk        garemk
13071c                   eval      garbn1 = gkrbn1
13071c                   eval      garbn2 = gkrbn2
13071c                   eval      garbn3 = gkrbn3
13071c                   eval      garbv1 = gkrbv1
13071c                   eval      garbv2 = gkrbv2
13071c                   eval      garbv3 = gkrbv3
     c                   eval      garda1 = gkrda1
     c                   eval      garda2 = gkrda2
     c                   eval      garda3 = gkrda3
13071c                   movel     gkrabb        garabb
13071c                   eval      gaopbn = gkopbn
13071c                   eval      gaopbv = gkopbv
E5.00c                   eval      gaopfd = gkopfd
13071c                   eval      gabgir = gkbgir
13023c***                eval      gapgir = gkpgir
13071c                   movel     gktenr        gatenr
13071c                   eval      gateku = gkteku
13071c                   eval      gaavtk = gkavtk
13071c                   movel     gkavtm        gaavtm
13071c                   movel     gkbeva        gabeva
13071c                   movel     gkreko        gareko
     c                   eval      gaopda = gkopda
13071c                   movel     gkopav        gaopav
      *
13071c                   write     gabfpfr
      *
13071c                   endsr
      *
     c     *inzsr        begsr
      *
      * Hjelpefeltet WPMBER inneholder navn på member regnskaps-
      * post skal overføres til:
      *
06108c                   eval      wpmber = *blanks                                          .
      *
     c     *dtaara       define    *lda          lda
      *
      * D2PERI = Regnskapsperiode:'
      *
     c                   eval      d2peri = 0                                                .
      *
      * D2PERI="K" ved kontrolliste og "J" ved journalliste:
      *
     c                   movel     'K'           d2joko                                      .
      *
      * Hjelpefeltet WPREGN er "JA" dersom det finnes poster
      * som skal til regnskap:
      *
     c                   eval      wpregn = *blanks                                          .
      *
      * Hjelpefelt WPIN09 er "JA" dersom det kun skal vises
      * returer som er oppdatert:
      *
     c                   eval      wpin09 = *blanks                                          .
      *
      * Legger ut tekst i F9-feltet + Linjevalg:
      *
     c                   eval      a1valg = '1=Legg tilbake  5=Vise data'                    .
      *
      * Initierer hjelpefelt:
      *
     c                   eval      spge01 = 0
     c                   eval      srrn01 = 0
     c                   eval      spge02 = 0
     c                   eval      srrn02 = 0
     c                   eval      spge03 = 0
     c                   eval      srrn03 = 0
     c                   eval      w_arow = 4
     c                   eval      w_acol = 29
     c                   eval      wpin12 = 0
     c                   eval      wpin03 = 0
     c                   eval      wpperi = *blanks
     c                   eval      wppeÅr = 0
     c                   eval      wppemÅ = 0
      *
      * Hjelpefelt for å indikerer at det er mottatt returposter
      * for telegiro og eller OCR:
      *
     c                   eval      wptele = *blanks
     c                   eval      wpocr = *blanks
11108c                   eval      wpink = *blanks
      *
13071c                   eval      wjanei = *blanks
13071c                   eval      wtelle = 0
      *
      * Dersom det er valgt oppdatering av transaksjoner, så
      * vil WPOPPD inneholde 'JA'.  "I" er elementtellr i tabellen.
      *
     c                   eval      wpoppd = *blanks
     c                   eval      i = 0
      *
     c                   movel     l_flgr        a1fgrp
     c                   eval      a1firm = l_firm
     c                   movel     l_fnav        a1fnav
      *
      * Initirer felten for filgruppe og firma, som skal
      * legges ut på file GTRTPF:
      *
      *
      * Key til bruk på returfiler - GBTRL1, lese på gironummer:
      *
     c     key00         klist
     c                   kfld                    wpgiro
      *
      * Key til bruk på returfiler - GBTRL1, leses på giro-/løpenummer:
      *
     c     key01         klist
     c                   kfld                    wpgiro
     c                   kfld                    wplØpn
      *
      * Key til bruk på returfiler - GBTRLU, behandler hver linje:
      *
     c     key02         klist
     c                   kfld                    wpgiro
     c                   kfld                    wplØpn
     c                   kfld                    wptype
     c                   kfld                    wptell
      *
      * Key til bruk på avtaleregister - GAVTL2, hente gironummer:
      *
     c     key03         klist
     c                   kfld                    l_flgr
     c                   kfld                    l_firm
      *
      * Key til bruk på forslagfilen:
      *
13071c     key04         klist
13071c                   kfld                    l_firm
13071c                   kfld                    dsubfn
13071c                   kfld                    dsubfl
      *
      * Henter gironummer ut i fra pålogget filgruppe/firmanummer.
      * Ved IKKE TILSLAG settes *IN40 *ON for å indikere at pgm.
      * må avsluttes.  Dersom bankgironummer IKKE finnes brukes
      * postgironummer.  Den samme logikken brukes i program
      * GL109R, som legger ut data på file GBTRPF, d.v.s. post-
      * gironummer brukes i nøkkel feltet KUN dersom bankgironr.
      * IKKE er utfylt:
      *
     c     key03         setll     gavtl2
     c     key03         reade     gavtl2                                 60
      *
     c                   if        *in60 = *on                                  ...
     c                   move      *on           *in40                            .
     c                   eval      wpgiro = 0                                     .
     c                   goto      xutinz                                         .
     c                   endif                                                  ...
      *
     c                   if        gcbgir <> 0                                  ...
     c                   eval      wpgiro = gcbgir                                .
     c                   else                                                     .
     c                   eval      wpgiro = gcpgir                                .
     c                   endif                                                  ...
      *
19011c                   eval      wpgiro = gcskto
      *
      * Starter les av transaksjonsfilen, på nivå filgruppe/firma,
      * d.v.s. alle poster må ha likt gironummer.  Først posisjoneres
      * det på gironummer for å hente inn 1. post, og deretter leses
      * det på løpenummer innenfor dette gironummeret:
      *
     c     xutinz        tag
      *
      * Henter løpenummer til første post i filen:
      *
     c                   exsr      xfØrst
      *
      * Fyll opp subfile:
      *
     c                   exsr      xcrt01
      *
     c                   endsr
