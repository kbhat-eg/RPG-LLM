     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RK110R
      * kundrivelse . . : Kundeposteringer, vedlikehold bilag/kunde
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       100802 KOB  Nytt program
      *       250603 KOB  Lagt inn dato for purring/rente i bilde
      * V5.40 210305 KOB  Lagt inn ny logisk file rktrlb (raar/biln)
6.10  * V6.10 220909 BSA  Lagt inn sporingsfelter
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frktrlb    if   e           k disk    rename(rktrpfr:rktrlbr)
     frktrl2    if   e           k disk    rename(rktrpfr:rktrl2r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frktrlu    uf   e           k disk    rename(rktrpfr:rktrlur)
      *
     frk110d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d rktrlb_raar                         like(rnraar)
     d rktrlb_biln                         like(rnbiln)
     d rktrl2_kund     s                   like(rnkund)
     d rkunl1_kund     s                   like(rnkund)
     d rktrlu_kund     s                   like(rnkund)
     d rktrlu_bdat     s                   like(rnbdat)
     d rktrlu_tist     s                   like(rntist)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_valg_ok       s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(rnfirm)
     d w_biln          s                   like(rnbiln)                                      .
     d w_kund          s                   like(rnkund)                                      .
     d w_raar          s                   like(rnraar)                                      .
      *
     d p_stat          s              1
     d p_akod          s                   like(rnbilk)
     d p_gkod          s                   like(rkavde)
     d p_ikod          s                   like(rkslgr)
     d p_atxt          s             20
     d p_gtxt          s             20
     d p_itxt          s             20
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * d_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2biln <> 0 or
     c                             b2kund <> 0 or
     c                             b2raar <> w_raar
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        d_fcrn <> 0
     c     d_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      rktrl2_kund = b1kund
     c     rktrl2_key    setll     rktrl2
     c                   other
     c                   eval      rktrlb_raar = b2raar
     c                   eval      rktrlb_biln = b1biln
     c     rktrlb_key    setll     rktrlb
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      *
      * Posisjoner startverdi, regnskapsår endret
      *
     c                   if        b2raar <> w_raar                                          .
     c                   eval      b2biln = w_biln                                           .
     c                   eval      b2kund = w_kund                                           .
     c                   eval      w_raar = b2raar                                           .
     c                   eval      rktrlb_raar = b2raar                                      .
      *
     c                   if        b2biln = 0 and
     c                             b2kund = 0
     c                   eval      b2biln = 1
     c                   endif
      *                                                                 .
     c                   endif                                                           .....
      *
      * Bilagsnummer
      *
     c                   if        b2biln <> 0
     c                   eval      w_seqe = *blank
     c                   eval      w_biln = b2biln
     c                   eval      w_kund = 0
     c                   eval      rktrlb_raar = b2raar                                      .
     c                   eval      rktrlb_biln = b2biln
     c     rktrlb_key    setll     rktrlb
     c                   goto      end_pos
     c                   endif
      *
      * Kunde
      *
     c                   if        b2kund <> 0
     c                   eval      w_seqe = 'B'
     c                   eval      w_kund = b2kund
     c                   eval      w_biln = 0
     c                   eval      rktrl2_kund = b2kund
     c     rktrl2_key    setll     rktrl2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2biln = 0
     c                   eval      b2kund = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = d_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
      * Filen er tom
      *
     c                   if        b1biln = 0
     c                   goto      avslutt
     c                   endif
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Oppdater
      *
     c                   if        b1valg = 2
     c                   eval      *in30  = *off
     c                   exsr      xc2win                                                  . .
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      *in30  = *on
     c                   exsr      xc2win                                                  . .
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      rktrl2
     c                   other
     c                   read      rktrlb
     c                   endsl
      *
     c                   if        %eof or
     c                             rnfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *                                                                 .
     c                   if        b2raar <> *zero and                                 ..... .
     c                             b2raar <> rnraar
     c                   iter                                                              . .
     c                   endif                                                         ..... .
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      rktrl2_kund = rnbelØ
     c                   other
     c                   eval      rktrlb_biln = rnbiln
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1bdat = rnbdat
     c                   eval      b1tist = rntist
     c                   eval      b1biln = rnbiln
     c     *dmy          move      rnbdat        b1bida
     c                   eval      b1bilk = rnbilk
     c                   eval      b1text = rntext
     c                   eval      b1belØ = rnbelØ
     c                   eval      b1kund = rnkund
      *                                                                 .
      * Hent kunde                                                      .
      *                                                                 .
     c                   eval      rkunl1_kund = rnkund                                      .
     c     rkunl1_key    chain     rkunl1                             61                     .
     c                   if        *in61 = *off                                              .
     c                   eval      b1navn = rknavn                                           .
     c                   endif                                                               .
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     rktrl2_key    setgt     rktrl2
     c                   readp     rktrl2
     c                   other
     c     rktrlb_key    setgt     rktrlb
     c                   readp     rktrlb
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     rktrl2
     c                   other
     c                   readp     rktrlb
     c                   endsl
      *
     c                   if        %eof or
     c                             rnfirm <> w_firm
     c                   leave
     c                   endif
      *                                                                 .
     c                   if        b2raar <> *zero and                                 ..... .
     c                             b2raar <> rnraar
     c                   iter                                                              . .
     c                   endif                                                         ..... .
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      rktrl2_kund = rnbelØ
     c                   other
     c                   eval      rktrlb_biln = rnbiln
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     rktrl2_key    setll     rktrl2
     c                   other
     c     rktrlb_key    setll     rktrlb
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * C2WIN Behandle bilde for oppdatering                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xc2win        begsr
      *
     c                   eval      rktrlu_kund = b1kund
     c                   eval      rktrlu_bdat = b1bdat
     c                   eval      rktrlu_tist = b1tist
     c     rktrlu_key    chain     rktrlu                             60
      *
     c                   move      rnbiln        c2biln                                      .
     c                   move      rnbilk        c2bilk                                      .
     c                   move      rntext        c2text                                      .
     c     *dmy          move      rnbdat        c2bdat                                      .
     c     *dmy          move      rnfdat        c2fdat                                      .
     c                   move      rnrper        c2rper                                      .
     c                   move      rnraar        c2raar                                      .
     c                   move      rnrekl        c2rekl                                      .
     c                   move      rnjour        c2jour                                      .
     c                   move      rnrefn        c2refn                                      .
     c                   move      rnsamf        c2samf                                      .
     c                   move      rnbiln        c2biln                                      .
     c                   move      rnbelØ        c2belØ                                      .
     c                   move      rnrest        c2rest                                      .
     c                   move      rnrenk        c2renk                                      .
     c                   move      rnvalk        c2valk                                      .
     c                   move      rnavde        c2avde                                      .
     c                   move      rnvalb        c2valb                                      .
     c                   move      rnvalr        c2valr                                      .
     c                   move      rnslgr        c2slgr                                      .
      *
     c                   move      rnantp        c2antp                                      .
      *
     c                   if        rnpdat = *loval
     c                   move      0             c2pdat                                      .
     c                   else
     c     *dmy          move      rnpdat        c2pdat                                      .
     c                   endif
      *
     c                   if        rnrdat = *loval
     c                   move      0             c2rdat                                      .
     c                   else
     c     *dmy          move      rnrdat        c2rdat                                      .
     c                   endif
      *
     c                   move      rninko        c2inko                                      .
      *
     c                   if        rnidat = *loval
     c                   move      0             c2idat                                      .
     c                   else
     c     *dmy          move      rnidat        c2idat                                      .
     c                   endif
      *
      * Hent avdelingstekst
      *
     c                   if        rnavde <> *zero                                       .....
     c                   move      rnavde        p_gkod                                      .
     c                   call      'RS707R'                                      avdelings-
     c                   parm                    p_stat                           kode
     c                   parm                    p_gkod                           kode
     c                   parm                    p_gtxt                           kode
      *
     c                   movel     p_gtxt        c2gtxt                                      .
     c                   endif                                                           .....
      *
      * Hent selgertekst
      *
     c                   if        rnslgr <> *zero                                       .....
     c                   move      rnslgr        p_ikod                                      .
     c                   call      'RS709R'
     c                   parm                    p_stat
     c                   parm                    p_ikod
     c                   parm                    p_itxt
      *
     c                   movel     p_itxt        c2itxt                                      .
     c                   endif                                                           .....
      *
     c     c2taga        tag
      *
     c                   exfmt     c2win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_c2bld
     c                   endif
      *
      * Call program for bilagskode
      *
     c                   if        *inkd = *on and                                       .....
     c                             w_afld = 'C2BILK'                                         .
     c                   call      'RA501R'                                                  .
     c                   parm                    w_firm                                      .
     c                   parm                    p_akod                                      .
     c                   parm                    p_atxt                                      .
      *                                                                 .
     c                   if        p_akod <> *zero                                           .
     c                   eval      c2bilk = p_akod                                           .
     c                   eval      c2text = p_atxt                                           .
     c                   endif                                                               .
      *                                                                 .
     c                   move      *on           *in33                                       .
     c                   goto      c2taga                                                    .
     c                   endif                                                           .....
      *
      * Call program for avdeling
      *
     c                   if        *inkd = *on and                                       .....
     c                             w_afld = 'C2AVDE'                                         .
     c                   call      'RA507R'                                                  .
     c                   parm                    w_firm                                      .
     c                   parm                    p_gkod                                      .
     c                   parm                    p_gtxt                                      .
      *                                                                 .
     c                   if        p_gkod <> *zero                                           .
     c                   move      p_gkod        c2avde                                      .
     c                   movel     p_gtxt        c2gtxt                                      .
     c                   endif                                                               .
      *                                                                 .
     c                   move      *on           *in36                                       .
     c                   goto      c2taga                                                    .
     c                   endif                                                               .
      *
      * Call program for
      * selger
      *
     c                   if        *inkd = *on and                                       .....
     c                             w_afld = 'C2SLGR'                                         .
     c                   call      'RA509R'                                                  .
     c                   parm                    w_firm                                      .
     c                   parm                    p_ikod                                      .
     c                   parm                    p_itxt                                      .
      *
     c                   if        p_ikod <> *zero                                           .
     c                   move      p_ikod        c2slgr                                      .
     c                   movel     p_itxt        c2itxt                                      .
     c                   endif                                                               .
      *
     c                   move      *on           *in38                                       .
     c                   goto      c2taga                                                    .
     c                   endif                                                               .
      *
      * Kontroll av skjermbildet
      *
     c                   exsr      sjekk_input
      *
     c                   if        b_feil = *on
     c                   goto      c2taga
     c                   endif
      *
      * Oppdater posteringsregister
      *
     c                   eval      rktrlu_bdat = b1bdat                                      .
     c                   eval      rktrlu_tist = b1tist                                      .
     c     rktrlu_key    chain     rktrlur                            60
      *
     c                   move      c2bilk        rnbilk
     c                   move      c2text        rntext
     c     *dmy          move      c2fdat        rnfdat
     c                   move      c2rekl        rnrekl
     c                   move      c2refn        rnrefn
     c                   move      c2avde        rnavde
     c                   move      c2slgr        rnslgr
     c                   move      c2rest        rnrest                                      .
     c                   move      c2valr        rnvalr                                      .
      *
     c                   move      c2antp        rnantp
      *
     c                   if        c2pdat = 0
     c                   eval      rnpdat = *loval                                           .
     c                   else
     c     *dmy          move      c2pdat        rnpdat
     c                   endif
      *
     c                   move      c2renk        rnrenk
      *
     c                   if        c2rdat = 0
     c                   eval      rnrdat = *loval                                           .
     c                   else
     c     *dmy          move      c2rdat        rnrdat
     c                   endif
      *
     c                   move      c2inko        rninko
      *
     c                   if        c2idat = 0
     c                   eval      rnidat = *loval                                           .
     c                   else
     c     *dmy          move      c2idat        rnidat
     c                   endif
      *
     c                   if        *in60 = *off                                          .....
6.10 c                   time                    rnedat
6.10 c                   time                    rnetim
6.10 c                   eval      rneusr = l_user
     c                   update    rktrlur                                                   .
     c                   endif                                                           .....
      *
     c                   if        b1valg = 2
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Kontroll  C2 bilde (endringer)                                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
     c                   eval      *in32  = *off
     c                   eval      *in33  = *off
     c                   eval      *in34  = *off
     c                   eval      *in35  = *off
      *
      * Sjekke om kjøredato er gyldig
      *
     c     *dmy          test(d)                 c2fdat                 32
     c                   if        *in32 = *on                                           .....
     c                   goto      end_sjekk
     c                   endif                                                           .....
      *
      * Sjekk om bilagskode er gyldig
      *
     c                   move      c2bilk        p_akod
     c                   call      'RS701R'
     c                   parm                    p_stat
     c                   parm                    p_akod
     c                   parm                    p_atxt
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in33
     c                   move      *on           *in81
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif                                                           .....
      *
      * Sjekk om avdeling er gyldig
      *
     c                   if        c2avde <> *zero                                       .....
     c                   move      c2avde        p_gkod                                      .
     c                   call      'RS707R'
     c                   parm                    p_stat
     c                   parm                    p_gkod
     c                   parm                    p_gtxt
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in34
     c                   move      *on           *in81
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif                                                               .
     c                   endif                                                           .....
      *
      * Sjekke om selger er gyldig
      *
     c                   if        c2slgr <> *zero                                       .....
     c                   move      c2slgr        p_ikod                                      .
     c                   call      'RS709R'
     c                   parm                    p_stat
     c                   parm                    p_ikod
     c                   parm                    p_itxt
      *
     c                   if        p_stat = 'N'
     c                   move      *on           *in35
     c                   move      *on           *in81
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif                                                               .
     c                   endif
      *
     c     end_sjekk     endsr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på bilagsnummer
      *
     c     rktrlb_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrlb_raar
     c                   kfld                    rktrlb_biln
      *
      * Key for posisjonering på kunde
      *
     c     rktrl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrl2_kund
      *
      * Key for kunderegister via kundenummer
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppdatering av postering
      *
     c     rktrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrlu_kund
     c                   kfld                    rktrlu_bdat
     c                   kfld                    rktrlu_tist
      *
      * Henter parametre, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
     c                   eval      b2raar = *year
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rktrlb_raar = *year
     c                   eval      rktrlb_biln = 0
     c     rktrlb_key    setll     rktrlb
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
