     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK630R                                              *
      * Beskrivelse . . : Utskrift av saldoliste kunder - parameter           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * V5.00     100902  Åpnet for å kunne sortere alfabetisk i tillegg  KOB *
T5.00 * V5.30     040403  Ny param. Kun konti m/poster i kol. 1-4.        HFL *
T5.30 * V5.30     080904  Lagt inn mulighet for regnskapsår > *year       HFL *
T5.40 * V5.40     080306  Parameter periode/år ble feil ved F4.           HFL *
T5.40 * V5.41     230406  Lagt inn valg på kundenummer fra/til.           KOB *
 6.10 * V6.10     070711  Lagt inn F4 på kundenr fra/til                  NHO *
 6.20 * R6.20     080911  Rekompilert pga endring i RPARRKDS            *NHO
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fausrl1    if   e           k disk
     frk630d    cf   e             workstn
      *
     d s               s              1  0
     d waar            s              4  0
     d wmnd            s              2  0
     d wpakod          s              2  0
     d wpdat1          s              6  0
      *
6.10 d p_kund          s              6  0
     d p_in03          s              1
     d p_stat          s              1
     d p_kode          s              4  0
     d p_tx30          s             30
      *
     d w_firm          s              3  0
     d w_user          s                   like(l_user)
     d w_peri          s              6  0
     d w_pkda          s              6  0
T5.30d var             s             40    dim(2) ctdata perrcd(1)               Varsel m/svarkrav
T5.30d wsavli          s              2  0
T5.30d wsavpo          s              3  0
      *
     d/COPY QCPYLESRC,RPARRKDS
T5.00d pkoll                 501    501
      *
      /eject
      *
     c     dummy         tag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av felter                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * A2BLD  - A2 Bilde for valg av parametre
      *
      /space 3
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A2BLD Behandle bilde for valg av utskriftskriteria              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     a2taga        tag
      *
      * Bare data
      *
     c     a2tagb        tag
      *
     c                   exfmt     a2bld
      *
     c                   eval      *in31 = *off
     c                   eval      *in32 = *off
     c                   eval      *in33 = *off
     c                   eval      *in34 = *off
     c                   eval      *in35 = *off
     c                   eval      *in36 = *off
     c                   eval      *in40 = *off
      *
      * Gå ut
      *
     c                   if        *inkc = *on                                  .....
     c                   move      '1'           p_in03                             .
     c                   goto      xslutt                                           .
     c                   endif                                                  .....
      *
      * Spørring
      *
     c                   if        *inkd = *on
     c                   exsr      spr_rut
     c                   goto      a2taga
     c                   endif
      *
      * Sjekke om kjøredato er gyldig
      *
     c                   if        a2pkda = *zero                                        .....
     c                   move      udate         a2pkda                                      .
     c                   endif                                                           .....
      *
     c     *dmy          test(d)                 a2pkda                 31
     c                   if        *in31 = *on                                           .....
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      * Kontroll av regnskapsår
T5.30 * Varsel ved regnskapsår større enn *year
      *
     c                   if        a2prÅr > *year                                        .....
T5.30c                   movel     var(1)        m1mld1
T5.30c                   movel     var(2)        m1mld2
T5.30c                   move      'N'           m1svar
T5.30c                   exsr      xm1win
T5.30c                   if        m1svar = 'N'
     c                   move      *on           *in32                          Pos.cur.     .
     c                   goto      a2taga                                                    .
T5.30c                   endif
     c                   endif                                                           .....
      *
      *  Kontroll avdeling
      *
     c                   if        a2pavf > a2pavt                                       .....
     c                   move      *on           *in33                          Pos.cur.     .
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      *  Kontroll kundekategori
      *
     c                   if        a2pkaf > a2pkat                                       .....
     c                   move      *on           *in34                          Pos.cur.     .
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      *  Kontroll selger
      *
     c                   if        a2psef > a2pset                                       .....
     c                   move      *on           *in35                          Pos.cur.     .
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      *  Kontroll av kundenr. fra - til
      *
     c                   if        a2pknf > a2pknt                                       .....
     c                   eval      *in40 = *on
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      * Kontroll av sum-nivåer
      * kun et kan være utfylt
      *
     c                   eval      s = 0
      *
     c                   if        a2pktg = 'J'
     c                   eval      s = s + 1
     c                   endif
      *
     c                   if        a2pavd = 'J'
     c                   eval      s = s + 1
     c                   endif
      *
     c                   if        a2pslg = 'J'
     c                   eval      s = s + 1
     c                   endif
      *
S500 c*                  if        a2palf = 'J'
S500 c*                  eval      s = s + 1
S500 c*                  endif
      *
     c                   if        a2pssa = 'J'
     c                   eval      s = s + 1
     c                   endif
      *
     c                   if        s > 1                                                 .....
     c                   move      *on           *in36                          Pos.cur.     .
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      * Skriv parameter
      *
     c                   move      a2pkda        pdato
     c                   move      a2prÅr        paarf
     c                   move      a2prÅr        paart
     c                   move      a2pper        ppert
     c                   move      a2pavf        pavdf
     c                   move      a2pavt        pavdt
     c                   move      a2pkaf        pkatf
     c                   move      a2pkat        pkatt
     c                   move      a2psef        pself
     c                   move      a2pset        pselt
     c                   move      a2pknf        pknrf
     c                   move      a2pknt        pknrt
     c                   move      a2pkpe        pkper
     c                   move      a2ptlf        ptlf
     c                   move      a2psal        psald
     c                   move      a2pkjo        pkjop
     c                   move      a2pktg        pkats
     c                   move      a2pavd        pavds
     c                   move      a2pslg        psels
     c                   move      a2palf        psort
     c                   move      a2pssa        pssal
T5.00c                   move      a2pkol        pkoll
      *
      /space 3
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      /eject
      *
      * Subrutine for forespørsel
      *
     c     spr_rut       begsr
      *
      * Call program for avdeling fra
      *
     c                   if        w_afld = 'A2PAVF'
     c                   eval      p_kode = a2pavf
     c                   call      'RA507R'
     c                   parm                    w_firm
     c                   parm                    p_kode
     c                   parm                    p_tx30
      *
     c                   eval      a2pavf = p_kode
     c                   eval      *in51 = *on
     c                   goto      end_spr_rut
     c                   endif
      *
      * Call program for avdeling til
      *
     c                   if        w_afld = 'A2PAVT'
     c                   eval      p_kode = a2pavt
     c                   call      'RA507R'
     c                   parm                    w_firm
     c                   parm                    p_kode
     c                   parm                    p_tx30
      *
     c                   eval      a2pavt = p_kode
     c                   eval      *in52 = *on
     c                   goto      end_spr_rut
     c                   endif
      *
      * Call program for kundekategori fra
      *
     c                   if        w_afld = 'A2PKAF'
     c                   eval      p_kode = a2pkaf
     c                   call      'RA511R'
     c                   parm                    w_firm
     c                   parm                    p_kode
     c                   parm                    p_tx30
      *
     c                   eval      a2pkaf = p_kode
     c                   eval      *in53 = *on
     c                   goto      end_spr_rut
     c                   endif
      *
      * Call program for kundekategori til
      *
     c                   if        w_afld = 'A2PKAT'
     c                   eval      p_kode = a2pkat
     c                   call      'RA511R'
     c                   parm                    w_firm
     c                   parm                    p_kode
     c                   parm                    p_tx30
      *
     c                   eval      a2pkat = p_kode
     c                   eval      *in54 = *on
     c                   goto      end_spr_rut
     c                   endif
      *
      * Call program for selger fra
      *
     c                   if        w_afld = 'A2PSEF'
     c                   eval      p_kode = a2psef
     c                   call      'RA509R'
     c                   parm                    w_firm
     c                   parm                    p_kode
     c                   parm                    p_tx30
      *
     c                   eval      a2psef = p_kode
     c                   eval      *in55 = *on
     c                   goto      end_spr_rut
     c                   endif
6.10  *
6.10  * Call kunder fra
6.10  *
6.10 c                   if        w_afld = 'A2PKNF'
6.10 c                   eval      p_kund = a2pknf
6.10 c                   call      'RK500R'
6.10 c                   parm                    w_firm
6.10 c                   parm                    p_kund
6.10 c                   parm                    p_tx30
6.10  *
6.10 c                   eval      a2pknf = p_kund
6.10 c                   eval      *in57 = *on
6.10 c                   goto      end_spr_rut
6.10 c                   endif
6.10  *
6.10  * Call kunder til
6.10  *
6.10 c                   if        w_afld = 'A2PKNT'
6.10 c                   eval      p_kund = a2pknt
6.10 c                   call      'RK500R'
6.10 c                   parm                    w_firm
6.10 c                   parm                    p_kund
6.10 c                   parm                    p_tx30
6.10  *
6.10 c                   eval      a2pknt = p_kund
6.10 c                   eval      *in58 = *on
6.10 c                   goto      end_spr_rut
6.10 c                   endif
      *
      *
      * Call program for selger til
      *
     c                   if        w_afld = 'A2PSET'
     c                   eval      p_kode = a2pset
     c                   call      'RA509R'
     c                   parm                    w_firm
     c                   parm                    p_kode
     c                   parm                    p_tx30
      *
     c                   eval      a2pset = p_kode
     c                   eval      *in56 = *on
     c                   goto      end_spr_rut
     c                   endif
      *
     c     end_spr_rut   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
T5.30 * M1WIN Behandle bilde for melding med svar                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
T5.30c     xm1win        begsr
      *
T5.30c                   eval      wsavli = 6
T5.30c                   eval      wsavpo = 22
T5.30c                   eval      w_arow = wsavli
T5.30c                   eval      w_acol = wsavpo
      *
T5.30c                   exfmt     m1win
      *
T5.30c                   if        *inkc = *on
T5.30c                   move      'N'           m1svar
T5.30c                   endif
      *
T5.30c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   eval      wpakod = *zero
     c                   eval      wpdat1 = 0
      *
      * Legge inn firmanummer og navn fra parameter
      *
     c                   move      l_user        w_user
     c                   move      l_firm        w_firm
     c                   eval      a2pavf = *zeros
     c                   eval      a2pavt = 9999
     c                   eval      a2pkaf = *zeros
     c                   eval      a2pkat = 9999
     c                   eval      a2psef = *zeros
     c                   eval      a2pset = 9999
      *
     c                   move      udate         a2pkda
      *
      * Beregner periode
      *
     c                   move      *year         waar
      *
     c                   eval      wmnd = umonth
     c                   if        wmnd = *zero                                          .....
     c                   movel     12            wmnd                                        .
     c                   eval      waar = waar - 1                                           .
     c                   move      waar          a2prÅr                                      .
     c                   else                                                                .
     c                   move      waar          a2prÅr                                      .
     c                   endif                                                           .....
      *
     c                   move      wmnd          a2pper
      *
     c                   move      'J'           a2palf
     c                   eval      pperf = *zeros
     c                   eval      a2pknf = *zeros
     c                   eval      a2pknt = 999999
      *
      * Hent bruker-record
      *
     c     key03         chain     ausrl1                             63
      *
      *  Sjekk om begrenset til avdeling
      *
     c                   if        abbavd = 'J'                                          .....
     c                   move      *on           *in30                          Protect.     .
     c                   move      absavd        a2pavf                                      .
     c                   move      absavd        a2pavt                                      .
     c                   endif                                                           .....
      *
     c                   eval      p_in03 = ' '
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_in03
      *
      * Key for bruker-file
      *
     c     key03         klist
     c                   kfld                    w_user
      *
     c                   endsr
**
Valgt periode er neste år.
Bekreft dersom dette er riktig.
