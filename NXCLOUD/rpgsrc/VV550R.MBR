     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VV550R
      * Beskrivelse . . : Vare, spørring på vare/pris
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       280499 HKO  Nytt program
      *       271099 HKO  Butikkpris vises dersom denne er utfylt på varen,
      *                   og prisen er lavere enn eventuell tilbudspris
      *       200100 HKO  Endret lengden på w_seqe slik at frisøk virker
      *                   sammen med varegruppe
      *       220900 HKO  Lagt inn overgang til LVR
      *       251103 HKO  Ekskluderer/inkluderer skaffe-varer
      *                   Viser ** UTGÅTT ** i vareteksten på utgåtte varer
      *       041205 HKO  Endret logikk ved valg av leveringstype
5.70  * prgr  111108 bof  Utvidet med prigruppe /vartype fra lager
5.71  * prgr  160509 bof  Utvidet med med lager i bildet
6.11  * 6.10  290609 bof  Utivdet eannr til 14 og brukt std. oppslag
6.12  * 6.10  040909 bof  Viser kjedesortment hvis dette er valgt i status
6.12  * 6.11  270510 bof  Rettelse for å hindre ekstra enter
6.20  * 6.20  021110 bof  Utvidet med utg.dato pr. lager
6.21  * 6.20  091110 bof  Utvidet med status for C-sentrallagerv. P=periodev.
6.22  * 6.20  051210 bof  Hvis sentral-lagervare, settes farge lik rød
6.23  * 6.20  150211 bof  Sentral-lagervare kode for styring av farge
6.24  * 6.20  190511 bø   Utvidet vl710r med div. info
6.25  * 6.20  250511 bof  Hvis nobb utg.dato på varen,så vises *UTG.NOBB*
6.26  * 6.20  060312 bof  Justert utlegging av koder
6.30  * 6.30  060114 bof  Vise tilgjengelig beholdning i spørring.
6.31  * 6.30  140414 bof  Justering på hvordan beholdning vises i bildet
6.32  * 6.30  180515 bkv  Endret LVAR pga økt fra 15 til 20 tegn
6.3q  *       260515 bof  Datoformat sql
6.33  * 6.30  081216 bof  Justering på beregning av behold.nytt VL716R t
6.34  * 6.30  170918 bkv  Utvidet med trelast sortiment
7.xx  * 7.00  220116 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  180418 bkv  Tatt vekk pageUp. Bruker self expanding.
7.02  * 7.00  160119 bkv  Alltid posisjonere på frisøkfeltet
7.03  * 7.00  060119 bsa  Overgang til å vise varekort.
7.04  * 7.00  120919 BKV  Endret filter på varetype pga hastighet
7.05  * 7.00  290819 ask  Overgang til å vise lagerlokasjoner, starter med å vise lokasjoner
7.05  *                   på lager som bruker er koblet til.
7.06  * 7.00  131119 BKV  Viser tom liste første gang
7.07  * 7.00  161219 bsa  Viser prisfokusvarer med egen farge
7.08  * 7.00  120320 bkv  132 bilde
7.09  * 7.09  190320 ask  Lagt inn fargestyring basert på kode i sortimentfeltet på VVARPF
7.09  *                   Dette er switch-styrt med koden kode "FARGE_SKAFF_SORTIMENT"
7.09  *                   Denne koden overstyrer fargestyring i 6.22 og 7.07
7.10  * K000  020720 bsa  Utvidet funksjonalitet rundt frisøk og "lagring" av
7.10  * K000              verdier.
7.11  * K000  211020 bsa  Går videre hvis ikke noe er endret.
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
8.02  * K000  040324 bsa  Innført nytt progranm for beregning av disponibel beholdning.
8.03  * K000  200125 bsa  Vise laveste pris, uavhengig av type.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvsopf    if   e           k disk
     fvvarlr    if   e           k disk    rename(vvarpfr:vvarlrr)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarl2    if   e           k disk    rename(vvarpfr:vvarl2r)
     fvvarl3    if   e           k disk    rename(vvarpfr:vvarl3r)
     fvvarl4    if   e           k disk    rename(vvarpfr:vvarl4r)
     fvvarl6    if   e           k disk    rename(vvarpfr:vvarl6r)
     fvvarl7    if   e           k disk    rename(vvarpfr:vvarl7r)
     fvvarl8    if   e           k disk    rename(vvarpfr:vvarl8r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
6.12 flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
6.21 fvsdtl7    if   e           k disk    rename(vsdtpfr:vsdtl7r)
6.21 fvshel1    if   e           k disk    rename(vshepfr:vshel1r)
6.34 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
7.10 fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
      *
7.08 fvv550d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_fnav                951    980
7.09 d l_filg                931    933
5.70 d l_lagr               1001   1002  0
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variable i nøkler
      *
     d vvarlr_vare     s                   like(vvvare)
     d vvarl1_vare     s                   like(vvvare)
     d vvarl2_tek1     s                   like(vvtek1)
     d vvarl3_nobb     s                   like(vvnobb)
     d vvarl4_lvar     s                   like(vvlvar)
     d vvarl6_ogrp     s                   like(vvogrp)
     d vvarl7_ogrp     s                   like(vvogrp)
     d vvarl7_hgrp     s                   like(vvhgrp)
     d vvarl8_ogrp     s                   like(vvogrp)
     d vvarl8_hgrp     s                   like(vvhgrp)
     d vvarl8_ugrp     s                   like(vvugrp)
     d rlevl1_levr     s                   like(rllevr)
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d vltpl1_lety     s                   like(vylety)
6.21 d vsdtl7_vare     s                   like(vdvare)
6.21 d vshel1_vsor     s                   like(vcvsor)
6.21 d vshel1_lage     s                   like(vclage)
7.10 d vlagl1_vare     s                   like(vlvare)
7.10 d vlagl1_lage     s                   like(vllage)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              2
     d b_feil          s              1    inz(*off)
     d b_lager         s              1    inz(*off)
6.12 d b_ksor          s              1    inz(*off)
     d b_open_sok      s              1    inz(*off)
6.21 d b_sess          s              1    inz(*off)
6.21 d b_sent          s              1    inz(*off)
7.04 d b_inlr          s              1    inz(*on)
7.01 d b_pfok          s              1    inz(*off)
7.10 d b_reset         s              1    inz(*off)
7.10 d b_levr          s              1    inz(*off)
7.11 d b_regl          s              1    inz(*off)
      *
6.20 d w_ddat          s               d   datfmt(*iso)
6.20 d w_udat          s               d   datfmt(*iso)
     d w_utim          s               t   timfmt(*iso)
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
5.50 d w_mv2f          s             10  9
5.50 d w_mv2p          s              6  2
5.50 d w_mv2t          s              5  4
     d w_kode          s              1  0
     d w_ste1          s             35
     d w_ste2          s             35
     d w_ste3          s             35
     d w_ste4          s             35
     d w_ste5          s             35
     d w_posi          s              3  0
     d w_otxt          s             35
     d w_htxt          s             35
     d w_utxt          s             35
     d w_bupr          s              9  2
     d w_tipr          s              9  2
5.71 d w_stat          s              1
5.71 d w_jkod          s              2  0
5.71 d w_jtxt          s             30  0
     d w_leng          s              2  0
     d s_stek          s             35
      *
     d w_firm          s                   like(vvfirm)
     d w_ltxt          s                   like(vybesk)
8.01 d w_prgr          s              2
5.70 d w_lage          s              2  0
5.71 d w_avde          s              4  0
5.71 d w_vtyp          s                   like(vvvtyp)
6.11 d w_eann          s             14  0
6.11 d w_eanx          s             14
6.11 d w_eava          s                   like(vvvare)
6.11 d w_eaen          s                   like(vvenhe)
6.11 d w_east          s              1
6.24 d w_ldor          s                   like(vvldor)
6.34 d w_lv_nobb       s                   like(vvlnob)
6.34 d w_lv_modn       s                   like(vvlmod)
6.34 d w_lv_lldo       s                   like(vvlnle)
6.34 d w_lv_llva       s                   like(vvllva)
6.30 d w_behl          s             11  3
6.30 d w_behl0         s              5  0
6.33 d w_undv          s              8  0
6.33 d w_logl          s             15
6.33 d w_jvqvnr        s                   like(vvvare)
6.33 d w_jvquno        s              8  0
7.09 d w_fso1          s             10
7.09 d w_fso2          s             10
      *
7.09 d u_fsor          s              1    inz(*off)                            Fargekode fra sortim
8.02 d u_disp          s              1    inz(*off)                            Disponibel beholdnin
      *
     d s_kund          s                   like(flkund)
     d s_kpro          s                   like(flkpro)
     d s_lety          s                   like(vylety)
     d s_ogrp          s                   like(vvogrp)
     d s_hgrp          s                   like(vvhgrp)
     d s_ugrp          s                   like(vvugrp)
5.71 d s_lage          s              2  0
7.10 d s_levr          s                   like(rllevr)
7.09  *
7.09  * Definering av eksterne prg
7.09  * BKV
7.09   dcl-s co402_verdi1  char(1);
7.09   dcl-s co402_verdi2  char(2048);
7.09   DCL-PR CO402R EXTPGM('CO402R');
7.09     p_filgrp            char(3)     const;
7.09     p_firm              packed(3:0) const;
7.09     p_lager             packed(2:0) const;
7.09     p_nokkel            char(50)    const;
7.09     p_verdi1            char(1);
7.09     p_verdi2            char(2048);
7.09   END-PR;
      *
      * Definering av konstanter
      *
7.08 d c_sfil          s              2  0 inz(15)
     d c_null          c                   '0000000000000'
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
6.3q C/End-Exec
      *
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
7.06 c                   if        *in53 = *on
7.06 c                   eval      *in53 = *off
7.06 c                   write     b2cmd
7.06 c                   endif
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
7.01 c*                   when      *in11
7.01 c*                   exsr      bck_subfile
7.01 c*                   goto      b2tagb
7.xx c*                  when      *in21
7.xx c*                  eval      *in22 = *on
7.xx c*                  goto      b2taga
     c                   endsl
      *
      * Behandling av kunde/kundeprosjekt
      *
     c                   if        b2kund = 0
     c                   eval      b2knav = *blank
     c                   eval      b2kpro = 0
     c                   endif
      *
5 71 c                   if        b2lage = 0
5.71 c                   eval      b2lnav = *blank
5.71 c                   endif
      *
     c                   if        b2kpro = 0
     c                   eval      b2pnav = *blank
     c                   endif
      *
     c                   if        b2kund <> s_kund or
     c                             b2kpro <> s_kpro
      *
     c                   if        b2kund <> 0
     c                   eval      rkunl1_kund = b2kund
     c     rkunl1_key    chain     rkunl1                             91
     c                   if        *in91 = *on
     c                   eval      *in31 = *on
     c                   goto      b2tagb
     c                   endif
     c                   eval      b2knav = rknavn
     c                   endif
      *
     c                   if        b2kpro <> 0
     c                   eval      fkprl1_kund = b2kund
     c                   eval      fkprl1_kpro = b2kpro
     c     fkprl1_key    chain     fkprl1                             91
     c                   if        *in91 = *on
     c                   eval      *in32 = *on
     c                   goto      b2tagb
     c                   endif
     c                   eval      b2pnav = flnavn
     c                   endif
      *
     c                   eval      s_kund = b2kund
     c                   eval      s_kpro = b2kpro
     c                   endif
      *
7.10 c                   if        b2null = 1
7.10 c                   eval      b2ogrp = 0
7.10 c                   eval      s_ogrp = 0
7.10 c                   eval      b2hgrp = 0
7.10 c                   eval      s_hgrp = 0
7.10 c                   eval      b2ugrp = 0
7.10 c                   eval      s_ugrp = 0
7.10 c                   eval      b2levr = 0
7.10 c                   eval      s_levr = 0
7.10 c                   eval      b2lnvn = *blanks
7.10 c                   eval      b2stek = *blank
7.10 c                   eval      s_stek = *blank
7.10 c                   eval      b2lety = 0
7.10 c                   eval      s_lety = 0
7.10 c                   eval      b_reset = *on
7.10 c                   eval      b2null = 0
7.10 c                   eval      b1tek1 = *blanks
7.10 c                   eval      w_seqe = *blank
7.10 c                   exsr      forny
7.10 c                   goto      b2tagb
7.10 c                   endif
      *
      * Behandling av leveringstype
      *
     c                   if        b2lety <> s_lety
      *
     c                   eval      vltpl1_lety = b2lety
     c     vltpl1_key    chain     vltpl1                             91
     c                   if        *in91 = *on
     c                   eval      *in33 = *on
     c                   goto      b2tagb
     c                   endif
      *
     c                   eval      s_lety = b2lety
      *
     c                   if        b2ogrp = 0 and
     c                             b2hgrp = 0 and
     c                             b2ugrp = 0 and
     c                             b2stek = *blank and
     c                             b2tek1 = *blank and
     c                             b2vare = 0 and
     c                             b2lvar = *blank and
7.10 c                             b2nobb = 0 and
7.10 c                             b2levr = 0
     c                   exsr      forny
     c                   goto      b2tagb
     c                   endif
      *
     c                   endif
5.71  *
5.71  * Behandling av lager
5.71  *
5.71 c                   if        b2lage <> s_lage
5.71  *
5.71 c                   call      'RS710R  '
5.71 c                   parm                    w_stat
5.71 c                   parm                    b2lage
5.71 c                   parm                    b2lnav
5.71 c                   if        w_stat <> *Blank
5.71 c                   eval      *in38 = *on
5.71 c                   goto      b2tagb
5.71 c                   else
5.71 c                   eval      s_lage = b2lage
5.71 c                   eval      w_lage = b2lage
5.71 c                   call      'VL712R  '
5.71 c                   parm                    w_firm
5.71 c                   parm                    w_lage
5.71 c                   parm                    w_avde
5.71 c                   parm                    w_prgr
5.71 c                   exsr      forny
5.71 c                   goto      b2taga
5.71 c                   endif
5.71  *
5.71 c                   endif
7.10  *
7.10  * Behandling av leverandør
7.10  *
7.10 c                   if        b2levr <> s_levr
7.10  *
7.10 c                   eval      b2lnvn = *blanks
7.10 c                   if        b2levr <> 0
7.10 c                   eval      rlevl1_levr = b2levr
7.10 c     rlevl1_key    chain     rlevl1
7.10 c                   if        not %found
7.10 c                   eval      b2lnvn = 'Koden er ugyldig'
7.10 c                   goto      b2tagb
7.10 c                   else
7.10 c                   eval      s_levr = b2levr
7.10 c                   eval      b2lnvn = rlnavn
7.10 c                   exsr      forny
7.10 c                   goto      b2taga
7.10 c                   endif
7.10 c                   endif
7.10  *
7.10 c                   endif
      *
      * F8=Skafferegister
      *
     c                   if        *inkh = *on
     c                   call      'FO703C'
     c                   parm                    w_firm
     c                   parm                    b2kund
     c                   parm                    b2kpro
     c                   parm                    b2lety
     c                   goto      b2tagb
     c                   endif
      *
      * F9=Gjenta søk
      *
     c                   if        *inki = *on
     c                   eval      *in22 = *on
     c                   eval      b2ogrp = s_ogrp
     c                   eval      b2hgrp = s_hgrp
     c                   eval      b2ugrp = s_ugrp
     c                   eval      b2stek = s_stek
7.10 c                   eval      b2levr = s_levr
     c                   goto      b2tagb
     c                   endif
      *
      * F13=Scanning
      *
     c                   if        *inkm = *on
     c                   exsr      scanning
     c                   goto      b2taga
     c                   endif
      *
      * F21=Bytt mellom å utelate og ta med skaffevarer
      *
     c                   if        *inkv = *on
     c                   if        *in83 = *off
     c                   eval      *in83 = *on
     c                   else
     c                   eval      *in83 = *off
     c                   endif
     c                   exsr      forny
     c                   goto      b2taga
     c                   endif
      *
      * F23=Bytt informasjon (Varenr/nobbnr/lev.varenr)
      *
     c                   if        *inkx = *on
      *
     c                   if        *in80 = *on
     c                   eval      *in80 = *off
     c                   eval      *in81 = *on
     c                   exsr      forny
     c                   goto      b2taga
     c                   endif
      *
     c                   if        *in81 = *on
     c                   eval      *in81 = *off
7.08 c*                   eval      *in82 = *on
7.08 c                   eval      *in80 = *on
     c                   exsr      forny
     c                   goto      b2taga
     c                   endif
      *
     c                   if        *in82 = *on
     c                   eval      *in82 = *off
     c                   eval      *in80 = *on
     c                   exsr      forny
     c                   goto      b2taga
     c                   endif
      *
     c                   endif
      *
      * Søking
      *
7.11 c                   eval      b_regl = *off
     c                   if        b2ogrp <> 0 or
     c                             b2hgrp <> 0 or
     c                             b2ugrp <> 0 or
7.10 c                             b2levr <> 0 or
     c                             b2stek <> *blank
     c                   exsr      sØk
7.11 c                   if        b_regl = *on
7.11 c                   goto      xs_subfile
7.11 c                   endif
     c                   goto      b2tagb
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2tek1 <> *blank or
     c                             b2vare <> 0 or
     c                             b2lvar <> *blank or
     c                             b2nobb <> 0
     c                   exsr      posisjoner
     c                   goto      b2taga
     c                   endif
      *
      * Behandling av subfile
      *
7.11 c     xs_subfile    tag
     c                   exsr      subfile
7.09 c                   if        u_fsor= *on
7.09 c                   exsr      forny
7.09 c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
7.04 c                   eval      b_inlr = *off
7.04 c                   call      'VL710R'
7.04 c                   parm                    w_firm
7.04 c                   parm                    vvvare
7.04 c                   parm                    w_lage
7.04 c                   parm                    w_vtyp
7.04 c                   parm                    w_udat
7.04 c                   parm                    w_ldor
7.04 c                   parm                    b_inlr
7.04 c                   parm                    w_lv_nobb
7.04 c                   parm                    w_lv_modn
7.04 c                   parm                    w_lv_lldo
7.04 c                   parm                    w_lv_llva
7.04 c                   call      'VL716R'
7.04 c                   parm                    w_firm
7.04 c                   parm                    vvvare
7.04 c                   parm                    w_lage
7.04 c                   parm                    b1enhe
7.04 c                   parm                    w_vtyp
7.04 c                   parm                    w_udat
7.04 c                   parm                    w_ldor
7.04 c                   parm                    w_behl
7.04 c                   parm                    b_inlr
7.04 c
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
7.10  *
7.10  * Det er valgt nullstilling av filter. Må starte på nytt
7.10  *
7.10 c                   if        b_reset = *on
7.10 c                   eval      b1tek1 = *blanks
7.10 c                   eval      b_reset = *off
7.10 c                   endif
      *
     c                   select
     c                   when      w_seqe = 'O'
     c     vvarl6_key    setll     vvarl6
     c                   when      w_seqe = 'H'
     c     vvarl7_key    setll     vvarl7
     c                   when      w_seqe = 'U'
     c     vvarl8_key    setll     vvarl8
     c                   when      w_seqe = 'SO' or
     c                             w_seqe = 'SH' or
     c                             w_seqe = 'SU' or
     c                             w_seqe = 'S '
     c                   eval      b_open_sok = *on
     c                   when      w_seqe = 'V'
     c                   eval      vvarl1_vare = b1vare
     c     vvarl1_key    setll     vvarl1
     c                   when      w_seqe = 'N'
     c                   eval      vvarl3_nobb = b1nobb
     c     vvarl3_key    setll     vvarl3
     c                   when      w_seqe = 'L'
     c                   eval      vvarl4_lvar = b1lvar
     c     vvarl4_key    setll     vvarl4
     c                   other
7.01 c                   if        *inke
7.01 c                   eval      vvarl2_tek1 = ''
7.01 c                   else
7.01 c                   eval      vvarl2_tek1 = b1tek1
7.01 c                   endif
     c     vvarl2_key    setll     vvarl2
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for søking i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sØk           begsr
7.11  *
7.11  * Hvis samme søk, og ikke nullstilt, sjekk om det er registrert valg
7.11  *
7.11 c                   if        b2ogrp = s_ogrp and
7.11 c                             b2hgrp = s_hgrp and
7.11 c                             b2ugrp = s_ugrp and
7.11 c                             b2stek = s_stek and
7.11 c                             b2levr = s_levr
7.11 c                   eval      b_regl = *on
7.11 c                   goto      end_sØk2
7.11 c                   endif
      *
      * Validering av varegruppe
      *
     c                   if        b2ogrp = 0 and
     c                             b2hgrp <> 0
     c                   eval      *in34 = *on
     c                   goto      end_sØk2
     c                   endif
      *
     c                   if        b2hgrp = 0 and
     c                             b2ugrp <> 0
     c                   eval      *in35 = *on
     c                   goto      end_sØk2
     c                   endif
      *
      * Tar vare på søk
      *
     c                   eval      s_ogrp = b2ogrp
     c                   eval      s_hgrp = b2hgrp
     c                   eval      s_ugrp = b2ugrp
     c                   eval      s_stek = b2stek
7.10 c                   eval      s_levr = b2levr
      *
      * Søk via søketekst
      *
     c                   if        b2stek <> *blank
      *
     c                   eval      b_open_sok = *on
      *
     c                   select
     c                   when      b2ogrp = 0 and
     c                             b2hgrp = 0 and
     c                             b2ugrp = 0
     c                   eval      w_seqe = 'S '
     c                   when      b2ogrp <> 0 and
     c                             b2hgrp = 0 and
     c                             b2ugrp = 0
     c                   eval      w_seqe = 'SO'
     c                   when      b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp = 0
     c                   eval      w_seqe = 'SH'
     c                   when      b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp <> 0
     c                   eval      w_seqe = 'SU'
     c                   endsl
      *
      * Utleder søketekster ut av søketekst
      *
     c                   call      'AS700R'
     c                   parm                    b2stek
     c                   parm                    w_ste1
     c                   parm                    w_ste2
     c                   parm                    w_ste3
     c                   parm                    w_ste4
     c                   parm                    w_ste5
      *
     c                   goto      end_sØk
      *
     c                   endif
      *
      * Søk på overgruppe
      *
     c                   if        b2ogrp <> 0 and
     c                             b2hgrp =  0 and
     c                             b2ugrp =  0
     c                   eval      w_seqe = 'O'
     c                   eval      vvarl6_ogrp = b2ogrp
     c     vvarl6_key    setll     vvarl6
     c                   goto      end_sØk
     c                   endif
      *
      * Søk på hovedgruppe
      *
     c                   if        b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp =  0
     c                   eval      w_seqe = 'H'
     c                   eval      vvarl7_ogrp = b2ogrp
     c                   eval      vvarl7_hgrp = b2hgrp
     c     vvarl7_key    setll     vvarl7
     c                   goto      end_sØk
     c                   endif
      *
      * Søk på undergruppe
      *
     c                   if        b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp <> 0
     c                   eval      w_seqe = 'U'
     c                   eval      vvarl8_ogrp = b2ogrp
     c                   eval      vvarl8_hgrp = b2hgrp
     c                   eval      vvarl8_ugrp = b2ugrp
     c     vvarl8_key    setll     vvarl8
     c                   goto      end_sØk
     c                   endif
7.10  *
7.10  * "Søk på leverandør"
7.10  *
7.10 c                   if        b2levr <> 0
7.10 c                   eval      w_seqe = *blank
7.10 c                   eval      vvarl2_tek1 = *blanks
7.10 c     vvarl2_key    setll     vvarl2
7.10 c                   goto      end_sØk
7.10 c                   endif
      *
     c     end_sØk       tag
      *
      * Blanker subfile, og fyller opp i.h.t. søk
      *
7.02 c*                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker søkefelt
      *
7.10 c                   if        b2null = 1
     c                   eval      b2ogrp = 0
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
7.10 c                   eval      b2levr = 0
7.10 c                   eval      b2lnvn = *blanks
     c                   eval      b2stek = *blank
7.10 c                   endif
      *
     c     end_sØk2      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Varetekst
      *
     c                   if        b2tek1 <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      vvarl2_tek1 = b2tek1
     c     vvarl2_key    setll     vvarl2
     c                   goto      end_pos
     c                   endif
      *
      * Varenummer
      *
     c                   if        b2vare <> 0
     c                   eval      w_seqe = 'V'
     c                   eval      *in80 = *on
     c                   eval      *in81 = *off
     c                   eval      *in82 = *off
     c                   movel     b2vare        vvarl1_vare
     c     vvarl1_key    setll     vvarl1
     c                   goto      end_pos
     c                   endif
      *
      * Nobbnummer
      *
     c                   if        b2nobb <> 0
     c                   eval      w_seqe = 'N'
     c                   eval      *in80 = *off
     c                   eval      *in81 = *on
     c                   eval      *in82 = *off
     c                   eval      vvarl3_nobb = b2nobb
     c     vvarl3_key    setll     vvarl3
     c                   goto      end_pos
     c                   endif
      *
      * Lev. varenummer
      *
     c                   if        b2lvar <> *blank
     c                   eval      w_seqe = 'L'
7.08 c*                   eval      *in80 = *off
7.08 c*                   eval      *in81 = *off
7.08 c*                   eval      *in82 = *on
     c                   eval      vvarl4_lvar = b2lvar
     c     vvarl4_key    setll     vvarl4
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
7.02 c*                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2tek1 = *blank
     c                   eval      b2vare = 0
     c                   eval      b2nobb = 0
     c                   eval      b2lvar = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
7.02 c*                   if        *in22 = *off
7.02 c*                   eval      *in22 = *on
7.02 c*                   else
7.02 c*                   eval      *in22 = *off
7.02 c*                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
7.02 c*                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vise/kalkuler
      *
     c                   if        b1valg = 5
     c                   call      'VV551R'
     c                   parm                    w_firm
     c                   parm                    b1vare
     c                   parm                    b2kund
     c                   parm                    b2kpro
     c                   parm                    b2lety
5.71 c                   parm                    b2lage
     c                   eval      s_kund = b2kund
     c                   eval      s_kpro = b2kpro
     c                   eval      s_lety = b2lety
5.71 c                   eval      s_lage = b2lage
     c                   exsr      hent_info
     c                   eval      b1valg = 0
6.33  * sjekk farge
6.33 c                   eval      w_jvqvnr = b1vare
6.33 c                   exsr      finn_logivar
     c                   update    b1sfl
     c                   iter
     c                   endif
7.05  *
7.05  * Viser lagerlokasjoner
7.05  *
7.05 c                   if        b1valg = 6
7.05 c                   exsr      vis_lokasjon
7.05 c                   eval      b1valg = 0
7.05 c                   update    b1sfl
7.05 c                   iter
7.05 c                   endif
7.03  *
7.03  * Viser varekort
7.03  *
7.03 c                   if        b1valg = 7
7.03 c                   call      'VV510R'
7.03 c                   parm                    w_firm
7.03 c                   parm                    b1vare
7.03 c                   eval      b1valg = 0
7.03 c                   update    b1sfl
7.03 c                   iter
7.03 c                   endif
      *
      * Viser produktinformasjon
      *
     c                   if        b1valg = 8
     c                   call      'FO704C'
     c                   parm                    w_firm
     c                   parm                    b1vare
     c                   eval      b1valg = 0
6.33  * sjekk farge
6.33 c                   eval      w_jvqvnr = b1vare
6.33 c                   exsr      finn_logivar
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Viser beholdingstall
      *
     c                   if        b1valg = 9
      *
     c                   if        b_lager = *on
     c                   call      'VL750C'
     c                   parm                    w_firm
     c                   parm                    b1vare
     c                   parm                    b1tek1
     c                   else
     c                   eval      *in40 = *on
     c                   endif
      *
     c                   eval      b1valg = 0
6.33  * sjekk farge
6.33 c                   eval      w_jvqvnr = b1vare
6.33 c                   exsr      finn_logivar
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tøm subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Fyll opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
      * Initierer SQL for søk
      *
     c                   if        b_open_sok = *on
      *
     c                   select
     c                   when      w_seqe = 'SO'
     c/exec sql
     c+                  declare sok_o cursor for
     c+                  select vvsfir,
     c+                         vvsogr,
     c+                         vvstek,
     c+                         vvsvar
     c+                  from   vvsopf
     c+                  where  vvsfir = :w_firm and
     c+                         vvsogr = :b2ogrp and
     c+                         vvstek like :w_ste1
     c+                  order by vvsfir, vvsogr, vvstek
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_o
     c/end-exec
     c/exec sql
     c+                  open sok_o
     c/end-exec
      *
     c                   when      w_seqe = 'SH'
     c/exec sql
     c+                  declare sok_h cursor for
     c+                  select vvsfir,
     c+                         vvsogr,
     c+                         vvshgr,
     c+                         vvstek,
     c+                         vvsvar
     c+                  from   vvsopf
     c+                  where  vvsfir = :w_firm and
     c+                         vvsogr = :b2ogrp and
     c+                         vvshgr = :b2hgrp and
     c+                         vvstek like :w_ste1
     c+                  order by vvsfir, vvsogr, vvshgr, vvstek
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_h
     c/end-exec
     c/exec sql
     c+                  open sok_h
     c/end-exec
      *
     c                   when      w_seqe = 'SU'
     c/exec sql
     c+                  declare sok_u cursor for
     c+                  select vvsfir,
     c+                         vvsogr,
     c+                         vvshgr,
     c+                         vvsugr,
     c+                         vvstek,
     c+                         vvsvar
     c+                  from   vvsopf
     c+                  where  vvsfir = :w_firm and
     c+                         vvsogr = :b2ogrp and
     c+                         vvshgr = :b2hgrp and
     c+                         vvsugr = :b2ugrp and
     c+                         vvstek like :w_ste1
     c+                  order by vvsfir, vvsogr, vvshgr, vvsugr, vvstek
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_u
     c/end-exec
     c/exec sql
     c+                  open sok_u
     c/end-exec
      *
     c                   when      w_seqe = 'S '
     c/exec sql
     c+                  declare sok cursor for
     c+                  select vvsfir,
     c+                         vvstek,
     c+                         vvsvar
     c+                  from   vvsopf
     c+                  where  vvsfir = :w_firm and
     c+                         vvstek like :w_ste1
     c+                  order by vvsfir, vvstek
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok
     c/end-exec
     c/exec sql
     c+                  open sok
     c/end-exec
      *
     c                   endsl
      *
     c                   endif
      *
      * Fyller opp subfile
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'O'
     c     vvarl6_key    reade     vvarl6                                 15
     c                   when      w_seqe = 'H'
     c     vvarl7_key    reade     vvarl7                                 15
     c                   when      w_seqe = 'U'
     c     vvarl8_key    reade     vvarl8                                 15
     c                   when      w_seqe = 'SO'
     c/exec sql
     c+                  fetch next
     c+                  from  sok_o
     c+                  into  :vvsfir,
     c+                        :vvsogr,
     c+                        :vvstek,
     c+                        :vvsvar
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'SH'
     c/exec sql
     c+                  fetch next
     c+                  from  sok_h
     c+                  into  :vvsfir,
     c+                        :vvsogr,
     c+                        :vvshgr,
     c+                        :vvstek,
     c+                        :vvsvar
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'SU'
     c/exec sql
     c+                  fetch next
     c+                  from  sok_u
     c+                  into  :vvsfir,
     c+                        :vvsogr,
     c+                        :vvshgr,
     c+                        :vvsugr,
     c+                        :vvstek,
     c+                        :vvsvar
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'S '
     c/exec sql
     c+                  fetch next
     c+                  from  sok
     c+                  into  :vvsfir,
     c+                        :vvstek,
     c+                        :vvsvar
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'V'
     c                   read      vvarl1                                 15
     c                   when      w_seqe = 'N'
     c                   read      vvarl3                                 15
     c                   when      w_seqe = 'L'
     c                   read      vvarl4                                 15
     c                   other
     c                   read      vvarl2                                 15
     c                   endsl
      *
     c                   if        *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        w_seqe = 'SO' or
     c                             w_seqe = 'SH' or
     c                             w_seqe = 'SU' or
     c                             w_seqe = 'S '
      *
     c                   if        w_ste2 <> *blank
      *
     c                   eval      w_posi = %scan(%trim(w_ste2):%trim(vvstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
      *
     c                   if        w_ste3 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste3):%trim(vvstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste4 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste4):%trim(vvstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste5 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste5):%trim(vvstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c                   eval      vvarlr_vare = vvsvar
     c     vvarlr_key    chain     vvarlr                             91
     c                   if        *in91 = *on
     c                   iter
     c                   endif
      *
     c                   endif
      *
     c                   if        vvfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
7.04  * henter varetype
7.04  *
7.04 c*                   call      'VL710R'
7.04 c*                   parm                    w_firm
7.04 c*                   parm                    vvvare
7.04 c*                   parm                    w_lage
7.04 c*                   parm                    w_vtyp
7.04 c*                   parm                    w_udat
7.04 c*                   parm                    w_ldor
7.04 c*                   parm                    b_inlr
7.04 c*                   parm                    w_lv_nobb
7.04 c*                   parm                    w_lv_modn
7.04 c*                   parm                    w_lv_lldo
7.04 c*                   parm                    w_lv_llva
7.04  *
7.04  * raskere filter på varetype, samenlingnet med å bruke VL710
7.04  *
7.04 c                   if        *in83 = *off
7.04   EXEC SQL
7.04     VALUES ( COALESCE(
7.04    	 (SELECT VLVTYP FROM VLAGPF WHERE VLFIRM = :w_firm AND
7.04        VLLAGE = :w_lage AND VLVARE = :vvvare)
7.04     , (SELECT VVVTYP FROM VVARPF WHERE VVFIRM = :w_firm
7.04         AND VVVARE = :vvvare)
7.04     ,' '
7.04       )
7.04    )  INTO :w_vtyp;
7.04 c                   if        w_vtyp = 'S'
7.04 c                   iter
7.04 c                   endif
7.04 c                   endif
7.04  *
7.04  * henter varetype ++
7.04  *
7.04 c                   call      'VL710R'
7.04 c                   parm                    w_firm
7.04 c                   parm                    vvvare
7.04 c                   parm                    w_lage
7.04 c                   parm                    w_vtyp
7.04 c                   parm                    w_udat
7.04 c                   parm                    w_ldor
7.04 c                   parm                    b_inlr
7.04 c                   parm                    w_lv_nobb
7.04 c                   parm                    w_lv_modn
7.04 c                   parm                    w_lv_lldo
7.04 c                   parm                    w_lv_llva
7.10  *
7.10  * Sjekk mot angitt leverandør. Hvis angitt lager og leverandør, må de
7.10  * finnes på vare/lager, eller være leverandør på varen.
7.10  *
7.10 c                   eval      b_levr = *off
7.10 c                   if        b2levr <> 0
7.10 c                   if        b2lage <> 0
7.10 c                   eval      vlagl1_vare = vvvare
7.10 c                   eval      vlagl1_lage = b2lage
7.10 c     vlagl1_key    chain     vlagl1
7.10 c                   if        %found and
7.10 c                             vlbldo = b2levr
7.10 c                   eval      b_levr = *on
7.10 c                   endif
7.10 c                   endif
7.10 c                   if        vvldor = b2levr
7.10 c                   eval      b_levr = *on
7.10 c                   endif
7.10  *
7.10 c                   if        b_levr = *off
7.10 c                   iter
7.10 c                   endif
7.10 c                   endif
      *
      * Skriver til subfile
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'V'
     c                   eval      vvarl1_vare = vvvare
     c                   when      w_seqe = 'N'
6.34 c                   if        w_lv_nobb > 0
6.34 c                   eval      vvarl3_nobb = w_lv_nobb
6.34 c                   else
     c                   eval      vvarl3_nobb = vvnobb
6.34 c                   endif
     c                   when      w_seqe = 'L'
6.34 c                   if        w_lv_nobb > 0
6.34 c                   eval      vvarl4_lvar = w_lv_llva
6.34 c                   else
     c                   eval      vvarl4_lvar = vvlvar
6.34 c                   endif
     c                   other
     c                   eval      vvarl2_tek1 = vvtek1
     c                   endsl
6.21 c                   eval      b1sess = *blank
6.21 c                   eval      b1sent = *blank
6.21 c                   eval      b1stat = *blank
6.21 c                   exsr      sjekk_vsor
6.21 c                   if        b_sess = *on
6.21 c                   eval      b1sess = 'P'
6.21 c                   endif
7.09 c                   if        u_fsor = *off
7.07 c                   eval      *in54  = *off
7.07 c                   if        b_pfok = *on
7.07 c                   eval      b1sess = 'F'
7.07 c                   eval      *in54 = *on
7.07 c                   endif
6.22 c                   eval      *in50  = *off
6.21 c                   if        b_sent = *on
6.23 c                   if        faafsl = 1
6.22 c                   eval      *in50  = *on
6.23 c                   endif
6.21 c                   eval      b1sent = 'C'
6.21 c                   endif
7.09 c                   else
7.09 c                   eval      *in50  = *off
7.09 c                   eval      *in54  = *off
7.09 c                   if        vvsort = w_fso1
7.09 c                   eval      *in50 = *on
7.09 c                   endif
7.09 c                   if        vvsort = w_fso2
7.09 c                   eval      *in54 = *on
7.09 c                   endif
7.09 c                   endif
      *
     c                   eval      b1valg = 0
     c                   eval      b1tek1 = vvtek1
7.xx c                   eval      b1tek2 = vvtek2
     c                   eval      b1vare = vvvare
6.34 c                   if        w_lv_nobb > 0
6.34 c                   eval      b1nobb = w_lv_nobb
6.34 c                   eval      b1lvar = w_lv_llva
6.34 c                   else
     c                   eval      b1nobb = vvnobb
     c                   eval      b1lvar = vvlvar
6.34 c                   endif
     c                   eval      b1lnav = *blank
     c                   eval      b1kpri = vvkpri
5.70 c                   eval      b1vtyp = w_vtyp
6.12 c                   if        b_ksor = *on
6.12 c                   movel     vvsort        b1sort
6.12 c                   else
6.12 c                   eval      b1sort = *blank
6.12 c                   endif
6.30  *
6.30 c                   if        %abs(w_behl) < 100000
6.30 c                   eval(h)   w_behl0 = w_behl
6.30 c                   else
6.30 c                   eval      w_behl0 = 99999
6.30 c                   endif
      *
6.34 c*                   eval      rlevl1_levr = vvldor
6.34 c                   eval      rlevl1_levr = w_ldor
     c     rlevl1_key    chain     rlevl1                             91
     c                   if        *in91 = *off
     c                   eval      b1lnav = rlnavn
     c                   endif
6.25 c                   if        w_udat <> *loval
6.25 c                   if        w_udat <= w_ddat
     c                   eval      b1tek1 = %subst(vvtek1:1:22) +
     c                                      ' ** UTGÅTT **'
6.25 c                   else
6.25 c                   eval      b1tek1 = %subst(vvtek1:1:22) +
6.25 c                                      ' ** UTGÅR  **'
6.25 c                   endif
6.21 c                   endif
6.25 c                   if        vvnuda <> *loval and
6.25 c                             vvnuda <= w_ddat
6.25 c                   eval      b1tek1 = %subst(vvtek1:1:22) +
6.25 c                                      ' **NOBB.UTG**'
6.25 c                   endif
      *
     c                   time                    w_utim
      *
     c                   call      'VP710R'
     c                   parm                    w_firm
     c                   parm                    vvvare
5.70 c                   parm                    w_prgr
     c                   parm                    b2lety
6.20 c                   parm                    w_ddat
     c                   parm                    w_utim
     c                   parm                    b1enhe
     c                   parm                    b1saim
     c                   parm                    w_bupr
     c                   parm                    w_tipr
8.02  *
8.02  * henter varetype og beholdning
8.02  *
8.02 c                   if        u_disp = *off
8.02 c                   call      'VL719R'
8.02 c                   parm                    w_firm
8.02 c                   parm                    vvvare
8.02 c                   parm                    w_lage
8.02 c                   parm                    b1enhe
8.02 c                   parm                    w_vtyp
8.02 c                   parm                    w_udat
8.02 c                   parm                    w_ldor
8.02 c                   parm                    w_behl
8.02 c                   parm                    b_inlr
8.02  *
8.02 c                   else
6.33  *
6.33  * henter varetype og beholdning
6.33  *
6.33 c                   call      'VL716R'
6.33 c                   parm                    w_firm
6.33 c                   parm                    vvvare
6.33 c                   parm                    w_lage
6.33 c                   parm                    b1enhe
6.33 c                   parm                    w_vtyp
6.33 c                   parm                    w_udat
6.33 c                   parm                    w_ldor
6.33 c                   parm                    w_behl
6.33 c                   parm                    b_inlr
6.33  *
8.02 c                   endif
6.33  *
6.33 c                   if        %abs(w_behl) < 100000
6.33 c                   eval(h)   w_behl0 = w_behl
6.33 c                   else
6.33 c                   eval      w_behl0 = 99999
6.33 c                   endif
      *
8.03 c                   if        w_bupr <> 0 and
8.03 c                             w_bupr <  b1saim
     c                   eval      b1kpri = '*'
     c                   eval      b1saim = w_bupr
     c                   endif
      *
8.03 c                   if        w_tipr <> 0 and
8.03 c                             w_tipr <  b1saim
     c                   if        w_bupr = 0 or
     c                             w_tipr <= w_bupr
     c                   eval      b1kpri = 'T'
     c                   eval      b1saim = w_tipr
     c                   endif
     c                   endif
6.26  * legger alle koder i et felt
6.26 c                   eval      %subst(b1stat:1:1) = b1sent
6.26 c                   eval      %subst(b1stat:2:1) = b1sess
6.26 c                   eval      %subst(b1stat:3:1) = b1kpri
6.26 c                   eval      %subst(b1stat:4:1) = b1vtyp
      *
6.31 c                   eval      b1nypo = *blank
     c                   select
     c                   when      *in80 = *on
6.31 c                   eval      b1nypo = vvvare
     c                   when      *in81 = *on
6.34 c                   if        w_lv_nobb > 0
6.34 c                   eval      b1nypo = %editc(w_lv_nobb : 'Z')
6.34 c                   else
6.31 c                   eval      b1nypo = %editc(vvnobb : 'Z')
6.34 c                   endif
6.31 c                   when      *in82 = *on
6.32 c*                  eval      b1nypo = vvlvar
6.34 c                   if        w_lv_nobb > 0
6.34 c                   movel     w_lv_llva     b1nypo
6.34 c                   else
6.32 c                   movel     vvlvar        b1nypo
6.34 c                   endif
     c                   endsl
7.08 c                   if        w_lv_nobb > 0
7.08 c                   movel     w_lv_llva     b1lvar
7.08 c                   else
7.08 c                   movel     vvlvar        b1lvar
7.08 c                   endif
6.31 c                   eval      b1behl = w_behl0
      *
     c                   eval      w_kode = 4
      *
5.50  * Finn hvilken mva-sats som skal benyttes
5.50  *
5.50 c                   select
5.50 c                   when      vvkmva = 0
5.50 c                   eval      b1saim = b1saim * w_mvat
5.50 c                   when      vvkmva = 2
5.50 c                   eval      b1saim = b1saim * w_mv2t
5.50 c                   endsl
      *
     c                   call      'FA909R'
     c                   parm                    w_firm
     c                   parm                    b1vare
     c                   parm                    w_kode
     c                   parm                    b1saim
      *
6.33  * sjekk farge
6.33 c                   eval      w_jvqvnr = vvvare
6.33 c                   exsr      finn_logivar
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   eval      b_open_sok = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        w_seqe = 'O'  or
     c                             w_seqe = 'H'  or
     c                             w_seqe = 'U'  or
     c                             w_seqe = 'SO' or
     c                             w_seqe = 'SH' or
     c                             w_seqe = 'SU' or
     c                             w_seqe = 'S '
     c                   exsr      forny
     c                   goto      end_bck
     c                   endif
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'V'
     c     vvarl1_key    setgt     vvarl1
     c                   readp     vvarl1
     c                   when      w_seqe = 'N'
     c     vvarl3_key    setgt     vvarl3
     c                   readp     vvarl3
     c                   when      w_seqe = 'L'
     c     vvarl4_key    setgt     vvarl4
     c                   readp     vvarl4
     c                   other
     c     vvarl2_key    setgt     vvarl2
     c                   readp     vvarl2
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'V'
     c                   readp     vvarl1
     c                   when      w_seqe = 'N'
     c                   readp     vvarl3
     c                   when      w_seqe = 'L'
     c                   readp     vvarl4
     c                   other
     c                   readp     vvarl2
     c                   endsl
      *
     c                   if        %eof or
     c                             vvfirm <> w_firm
     c                   leave
     c                   endif
      *
      * henter varetype
      *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    w_lage
5.70 c                   parm                    w_vtyp
6.24 c                   parm                    w_udat
6.24 c                   parm                    w_ldor
6.24 c                   parm                    b_inlr
6.34 c                   parm                    w_lv_nobb
6.34 c                   parm                    w_lv_modn
6.34 c                   parm                    w_lv_lldo
6.34 c                   parm                    w_lv_llva
      *
     c                   if        *in83 = *off and
5.70 c                             w_vtyp = 'S'
     c                   iter
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'V'
     c                   eval      vvarl1_vare = vvvare
     c                   when      w_seqe = 'N'
6.34 c                   if        w_lv_nobb > 0
6.34 c                   eval      vvarl3_nobb = w_lv_nobb
6.34 c                   else
     c                   eval      vvarl3_nobb = vvnobb
6.34 c                   endif
     c                   when      w_seqe = 'L'
6.34 c                   if        w_lv_nobb > 0
6.34 c                   eval      vvarl4_lvar = w_lv_llva
6.34 c                   else
     c                   eval      vvarl4_lvar = vvlvar
6.34 c                   endif
     c                   other
     c                   eval      vvarl2_tek1 = vvtek1
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'V'
     c     vvarl1_key    setll     vvarl1
     c                   when      w_seqe = 'N'
     c     vvarl3_key    setll     vvarl3
     c                   when      w_seqe = 'L'
     c     vvarl4_key    setll     vvarl4
     c                   other
     c     vvarl2_key    setll     vvarl2
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_bck       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Display subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
     c                   if        w_arcd = 'B2CTL'
      *
      * Kunde
      *
     c                   if        w_afld = 'B2KUND'
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    b2kund
     c                   parm                    b2knav
     c                   goto      end_spØrr
     c                   endif
      *
      * Kundeprosjekt
      *
     c                   if        w_afld = 'B2KPRO'
     c                   call      'FL500R'
     c                   parm                    w_firm
     c                   parm                    b2kund
     c                   parm                    b2kpro
     c                   parm                    b2pnav
     c                   parm                    b2knav
     c                   goto      end_spØrr
     c                   endif
      *
      * Leveringstype
      *
     c                   if        w_afld = 'B2LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    b2lety
     c                   parm                    w_ltxt
     c                   goto      end_spØrr
     c                   endif
5.71  *
5.71  * Lager
5.71  *
5.71 c                   if        w_afld = 'B2LAGE'
5.71 c                   call      'RA510R'
5.71 c                   parm                    w_firm
5.71 c                   parm                    b2lage
5.71 c                   parm                    b2lnav
5.71 c                   goto      end_spØrr
5.71 c                   endif
      *
      * Overgruppe
      *
     c                   if        w_afld = 'B2OGRP'
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    w_otxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_afld = 'B2HGRP'
     c                   eval      b2ugrp = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    w_htxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_afld = 'B2UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    b2ugrp
     c                   parm                    w_utxt
     c                   goto      end_spØrr
     c                   endif
7.10  *
7.10  * Leverandør
7.10  *
7.10 c                   if        w_afld = 'B2LEVR'
7.10 c                   call      'RL500R'
7.10 c                   parm                    w_firm
7.10 c                   parm                    b2levr
7.10 c                   parm                    b2lnvn
7.10 c                   goto      end_spØrr
7.10 c                   endif
      *
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter kunde og kundeprosjekt-informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_info     begsr
      *
     c                   eval      b2knav = *blank
     c                   eval      b2pnav = *blank
      *
     c                   eval      rkunl1_kund = b2kund
     c     rkunl1_key    chain     rkunl1                             91
     c                   if        *in91 = *off
     c                   eval      b2knav = rknavn
     c                   endif
      *
     c                   eval      fkprl1_kund = b2kund
     c                   eval      fkprl1_kpro = b2kpro
     c     fkprl1_key    chain     fkprl1                             91
     c                   if        *in91 = *off
     c                   eval      b2pnav = flnavn
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av scanning
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     scanning      begsr
      *
     c                   eval      e1eann = *blank
      *
     c     e1taga        tag
      *
     c                   exfmt     e1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_e1win
     c                   endif
      *
      * EAN-nummer må oppgis
      *
     c                   if        e1eann = *blank
     c                   eval      *in31 = *on
     c                   goto      e1taga
     c                   endif
      *
      * Sjekk om vare finnes for EAN-nummer
      *
     c                   eval      w_leng = %len(%trim(e1eann))
6.11 c                   eval      w_eanx = %subst(c_null:1:14-w_leng) +
     c                                      e1eann
      *
6.11 c                   move      w_eanx        w_eann
6.11 c                   call      'VE710R  '
6.11 c                   parm                    w_firm
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_eava
6.11 c                   parm                    w_eaen
6.11 c                   parm                    w_east
6.11 c                   if        w_east = *blank
     c                   eval      *in32 = *on
     c                   goto      e1taga
     c                   endif
      *
      * Kaller vise-program
      *
     c                   call      'VV551R'
     c                   parm                    w_firm
6.11 c                   parm                    w_eava
     c                   parm                    b2kund
     c                   parm                    b2kpro
     c                   parm                    b2lety
5.71 c                   parm                    b2lage
     c                   exsr      hent_info
      *
     c     end_e1win     endsr
6.21  *
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  * Subrutine for å sjekke om varen er i sortiment for C - sentrallagervarer
6.21  *                           eller i et sortiment for P - periodestyrt varer
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  *
6.21 c     sjekk_vsor    begsr
6.21  *
6.21 c                   eval      b_sent = *off
6.21 c                   eval      b_sess = *off
7.07 c                   eval      b_pfok = *off
6.21 c                   eval      vsdtl7_vare = vvvare
6.21 c     vsdtl7_key    setll     vsdtl7
6.21 c     vsdtl7_key    reade     vsdtl7
6.21 c                   dow       not %eof(vsdtl7)
6.21  *
6.21 c                   eval      vshel1_vsor = vdvsor
6.21 c                   eval      vshel1_lage = vdlage
6.21 c     vshel1_key    chain     vshel1
6.21 c                   if        %found(vshel1)
6.21 c                   if        vctype = 'C' or
7.07 c                             vctype = 'P' or
7.07 c                             vctype = 'F'
7.07  * Prisfokus ?
7.07 c                   if        vctype = 'F'
7.07 c                   eval      b_pfok = *on
7.07 c                   endif
6.21  *
6.21  * hvis lager er 0 -generelt sortiment eller at sortiment
6.21  * finnes på lager for ordre
6.21 c                   if        vclage = w_lage  or
6.21 c                             vclage = 0
6.21 c                   if        vctype = 'C'
6.21 c                   eval      b_sent = *on
6.21 c                   endif
6.21 c                   if        vctype = 'P'
6.21 c                   eval      b_sess = *on
6.21 c                   endif
6.21 c                   endif
6.21  *
6.21 c                   endif
6.21 c                   endif
6.21  *
6.21 c     vsdtl7_key    reade     vsdtl7
6.21 c                   enddo
6.21  *
6.21 c                   endsr
6.37  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.37  * Finn info om logistikkvare for riktig prisgiver
6.37  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.37  *
6.37 c     finn_logivar  begsr
6.37  *
6.37 c                   eval      *in51 = *off
6.37 c                   eval      *in52 = *off
6.37 c
6.37 c                   if        %trim(w_jvqvnr) = ''
6.37 c                   leavesr
6.37 c                   endif
6.37 c
6.37 c                   eval      w_logl = ''
6.37 c                   eval      w_undv = 0
     c                   movel     w_jvqvnr      w_jvquno
6.37 c/exec sql
6.37 c+                  select jvquno, jvqvnr
6.37 c+                  into :w_undv, :w_logl
6.37 c+                  from jvklpf
6.37 c+                  where (jvquno = :w_jvquno or
6.37 c+                  jvqvnr = :w_jvqvnr)
6.37 c+                  and jvqfir = :w_firm
6.37 c+                  fetch first 1 rows only
6.37 c/end-exec
6.37 c                   if        w_logl = w_jvqvnr
6.37 c                   eval      *in52 = *on
6.37 c                   elseif    w_undv > 0
6.37 c                   eval      *in51 = *on
6.37 c                   endif
6.37 c
6.37 c                   endsr
7.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.05  * Vis lagerlokasjoner for varen
7.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.05  *
7.05 c     vis_lokasjon  begsr
7.05  *
7.05 c                   eval      f1lage = l_lagr
7.05 c                   eval      f1behl = b1behl
7.05  *
7.05 c     f1taga        tag
7.05 c                   eval      f1lok1 = *blanks
7.05 c                   eval      f1lok2 = *blanks
7.05 c                   eval      f1lok3 = *blanks
7.05 c                   eval      f1tek1 = *blanks
7.05 c                   eval      f1tek2 = *blanks
7.05 c                   eval      f1tek3 = *blanks
7.05  *
7.05 c/exec sql
7.05 c+                  select rajtxt
7.05 c+                  into :f1ltek
7.05 c+                  from ra10pf
7.05 c+                  where rajfir = :w_firm and rajkod = :f1lage
7.05 c/end-exec
7.05 c/exec sql
7.05 c+                  select vlplas, vlpla1, vlpla2
7.05 c+                  into :f1lok1, :f1lok2, :f1lok3
7.05 c+                  from vlagpf
7.05 c+                  where vlfirm = :w_firm and vlvare = :b1vare
7.05 c+                                  and vllage = :f1lage
7.05 c/end-exec
7.05 c                   if         f1lok1 <> *blanks
7.05 c/exec sql
7.05 c+                  select mdlbes
7.05 c+                  into :f1tek1
7.05 c+                  from mdlopf
7.05 c+                  where mdfirm = :w_firm and mdlage = :f1lage
7.05 c+                        and mdloka = :f1lok1
7.05 c/end-exec
7.05 c                   endif
7.05 c                   if         f1lok2 <> *blanks
7.05 c/exec sql
7.05 c+                  select mdlbes
7.05 c+                  into :f1tek2
7.05 c+                  from mdlopf
7.05 c+                  where mdfirm = :w_firm and mdlage = :f1lage
7.05 c+                        and mdloka = :f1lok2
7.05 c/end-exec
7.05 c                   endif
7.05 c                   if         f1lok3 <> *blanks
7.05 c/exec sql
7.05 c+                  select mdlbes
7.05 c+                  into :f1tek3
7.05 c+                  from mdlopf
7.05 c+                  where mdfirm = :w_firm and mdlage = :f1lage
7.05 c+                        and mdloka = :f1lok3
7.05 c/end-exec
7.05 c                   endif
7.05  *
7.05 c                   exfmt     f1win
7.05  *
7.05 c                   if        *inkc or *inkl
7.05 c                   goto      end_f1win
7.05 c                   endif
7.05  *
7.05  * Spørring på lager
7.05  *
7.05 c                   if        *inkd
7.05 c                   call      'RA510R'
7.05 c                   parm                    w_firm
7.05 c                   parm                    f1lage
7.05 c                   parm                    f1ltek
7.05 c                   endif
7.05  *
7.05 c                   goto      f1taga
7.05  *
7.05 c     end_f1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på vare
      *
     c     vvarlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlr_vare
      *
      * Key for posisjonering på varenummer
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for posisjonering på varetekst
      *
     c     vvarl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl2_tek1
      *
      * Key for posisjonering på nobb-nummer
      *
     c     vvarl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl3_nobb
      *
      * Key for posisjonering på leverandørs varenummer
      *
     c     vvarl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl4_lvar
      *
      * Key for søk på overgruppe
      *
     c     vvarl6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl6_ogrp
      *
      * Key for søk på overgruppe/hovedgruppe
      *
     c     vvarl7_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl7_ogrp
     c                   kfld                    vvarl7_hgrp
      *
      * Key for søk på overgruppe/hovedgruppe/undergruppe
      *
     c     vvarl8_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl8_ogrp
     c                   kfld                    vvarl8_hgrp
     c                   kfld                    vvarl8_ugrp
      *
      * Key for oppslag mot leverandør-register
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag mot kunde-register
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag mot kundeprosjekt-register
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag mot leveringstype-register
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
      *
      * Key for oppslag på OFAK-koderegister
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
6.12  *
6.12  * Key for oppslag på LAGR-koderegister
6.12  *
6.12 c     lstsl1_key    klist
6.12 c                   kfld                    w_firm
6.21  *
6.21  * Key for les av varer fra sortiment
6.21  *
6.21 c     vsdtl7_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    vsdtl7_vare
6.21  *
6.21  * Key for les av soritment hode
6.21  *
6.21 c     vshel1_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    vshel1_vsor
6.21 c                   kfld                    vshel1_lage
7.10  *
7.10  * Key for les av lagerregister
7.10  *
7.10 c     vlagl1_key    klist
7.10 c                   kfld                    w_firm
7.10 c                   kfld                    vlagl1_vare
7.10 c                   kfld                    vlagl1_lage
      *
      * Initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
     c                   eval      *in80 = *on
      *
      * Henter dagens dato
      *
6.20 c                   time                    w_ddat
      *
      * Finner moms-sats
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
6.20 c                   parm                    w_ddat
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
5.50  *
5.50  * Finner hvilken mva-sats
5.50  * som skal benyttes (for halv sats)
5.50  *
5.50 c                   call      'FÅ706R'
5.50 c                   parm                    w_firm
6.20 c                   parm                    w_ddat
5.50 c                   parm                    w_mv2p
5.50 c                   parm                    w_mv2t
5.50 c                   parm                    w_mv2f
6.12  *
6.12  * Henter lager status - kjede-sortiment ?
6.12  *
6.12 c                   eval      b_ksor  = *off
6.12 c     lstsl1_key    chain     lstsl1                             91
6.12 c                   if        %found(lstsl1) and
6.12 c                             laakso <> *blank
6.12 c                   eval      b_ksor  = *on
6.12 c                   endif
      *
      * Setter flagg dersom lagerstyring
      *
     c     fstsl1_key    chain     fstsl1                             91
     c                   if        *in91 = *off and
     c                             faalag = 1
     c                   eval      b_lager = *on
     c                   endif
      *
5.71 c                   eval      b2lage = l_lagr
6.11 c                   eval      s_lage = l_lagr
5.71 c                   eval      w_lage = l_lagr
      *
5.71  * Finner prisgruppe
5.71  *
5.71 c                   call      'VL712R  '
5.71 c                   parm                    w_firm
5.71 c                   parm                    w_lage
5.71 c                   parm                    w_avde
5.71 c                   parm                    w_prgr
5.71  *
5.71 c                   call      'RS710R  '
5.71 c                   parm                    w_stat
5.71 c                   parm                    w_lage
5.71 c                   parm                    b2lnav
7.09  *
7.09  * Konfiguerte tilpasninger
7.09  *
7.09  *
7.09  * Test om fargekode fra sortimentskode
7.09  *
7.09   CallP CO402R(l_filg:w_firm:0:'FARGE_SKAFF_SORTIMENT':
7.09                    co402_verdi1:co402_verdi2);
7.09   if co402_verdi1 = '1';
7.09     u_fsor = *on;
7.09     w_fso1 = %subst(co402_verdi2:3:%scan('B=':co402_verdi2) - 3);
7.09     w_fso2 = %subst(co402_verdi2:%scan('B=':co402_verdi2) + 2:10);
7.09   endif;
8.02  *
8.02  * Test om fargekode fra sortimentskode
8.02  *
8.02   CallP CO402R(l_filg:w_firm:0:'DISPONIBEL_BEREGNING':
8.02                    co402_verdi1:co402_verdi2);
8.02   if co402_verdi1 = '1';
8.02     u_disp = *on;
8.02   endif;
      *
      * Fyller subfile
      *
     c     vvarl2_key    setll     vvarl2
      *
7.06 c*                   exsr      crt_subfile
7.06 c                   eval      *in53 = *on
      *
     c                   endsr
