     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : MO706R
      * Beskrivelse . . : Overvåking - danner grunndata for type 8
      *                   Finner de 100 mest solgte og 100 minst solgte
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       050107 bø   Nytt program
6.10  *       031210 bø   Test hvis verdi er for stor
6.11  *       020813 bø   Justeing av test (6.10)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fmabdl7    if   e           k disk    rename(mabdpfr:mabdl7r)
     fvhisl3    if   e           k disk    rename(vhispfr:vhisl3r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     flwo8lu    uf a e           k disk    rename(lwo8pfr:lwo8lur)
      *
      *
     d vhisl3_vare     s                   like(vhvare)
     d vhisl3_lage     s                   like(vhlage)
     d vhisl3_dato     s                   like(vhdato)
     d vhisl3_time     s                   like(vhtime)
     d vlagl1_vare     s                   like(vlvare)
     d vvarl1_vare     s                   like(vvvare)
     d mabdlx_lagk     s                   like(mblagk)
     d mabdlx_vsor     s                   like(mbvsor)
     d mabdlx_ogrp     s                   like(mbogrp)
     d mabdlx_hgrp     s                   like(mbhgrp)
     d mabdlx_ugrp     s                   like(mbugrp)
     d mabdlx_omse     s                   like(mbomse)
     d lwo8lu_odat     s                   like(mwodat)
     d lwo8lu_ovnr     s                   like(mwovnr)
     d lwo8lx_odat     s                   like(mwodat)
      *
     d w_firm          s                   like(vhfirm)
     d w_vare          s                   like(vhvare)
     d w_dato          s                   like(vhdato)
     d p_dato          s                   like(vhdato)
     d w_verd          s             11  2
     d w_tell          s              4  0
      *
     d b_funn          s              1    inz(*off)
      *
      * Local Data Area
      *
     d                uds
      *
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
      *
      * Definering av variable
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * sletter først for dato som det skal kjøres for.
      *
     c                   eval      lwo8lx_odat = w_dato
readec     lwo8lx_key    setll     lwo8lu
readec     lwo8lx_key    reade     lwo8lu
readec                   dow       not %eof(lwo8lu)
     c                   delete    lwo8lur
readec     lwo8lx_key    reade     lwo8lu
     c                   enddo
      *
     c                   eval      w_tell = 1
      *
     c                   exsr      les_abca
      *
     c                   eval      w_tell = 1
      *
     c                   exsr      les_abcc
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     avslutt       tag
     c                   eval      *inlr = *on
      *
     c                   return
      *
      * --------------------------------------------------------------
      *  Leser abc-stat -finner de 0 mest solgte
      * --------------------------------------------------------------
      *
     c     les_abca      begsr
      *
readec     mabdl7_key    setll     mabdl7
readec     mabdl7_key    reade     mabdl7
readec                   dow       not %eof(mabdl7) and
     c                             w_tell < 101
      *
     c                   exsr      les_lager
      *
     c                   eval      vvarl1_vare = mbvare
     c     vvarl1_key    chain     vvarl1
     c                   eval      lwo8lu_odat = w_dato
     c                   eval      lwo8lu_ovnr = mbvare
     c                   exsr      skriv_logg
      *
     c                   eval      w_tell = w_Tell + 1
      *
readec     mabdl7_key    reade     mabdl7
readec                   enddo
      *
     c                   endsr
      *
      * --------------------------------------------------------------
      *  Leser abc-stat -finner de 0 minst solgte
      * --------------------------------------------------------------
      *
     c     les_abcc      begsr
      *
     c                   eval      mabdlx_lagk = *blank
     c                   eval      mabdlx_vsor = *blank
     c                   eval      mabdlx_ogrp = 0
     c                   eval      mabdlx_hgrp = 0
     c                   eval      mabdlx_ugrp = 0
     c                   eval      mabdlx_omse = 0
readec     mabdlx_key    setll     mabdl7
readec                   readp     mabdl7
readec                   dow       not %eof(mabdl7) and
     c                             w_tell < 101
      *
     c                   exsr      les_lager
      *
     c                   eval      vvarl1_vare = mbvare
     c     vvarl1_key    chain     vvarl1
     c                   eval      lwo8lu_odat = w_dato
     c                   eval      lwo8lu_ovnr = mbvare
     c                   exsr      skriv_logg
      *
     c                   eval      w_tell = w_Tell + 1
      *
readec                   readp     mabdl7
readec                   enddo
      *
     c                   endsr
      *
      *
      * --------------------------------------------------------------
      *  Leser lager for hver vare
      * --------------------------------------------------------------
      *
     c     les_lager     begsr
      *
5.40 c                   eval      w_verd = 0
      *
     c                   eval      vlagl1_vare = mbvare
readec     vlagl1_key    setll     vlagl1
readec     vlagl1_key    reade     vlagl1
readec                   dow       not %eof(vlagl1)
      *
     c                   exsr      finn_lverdi
      *
      *
readec     vlagl1_key    reade     vlagl1
readec                   enddo
      *
     c                   endsr
      *
5.40  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.40  * Subrutine for henting av beholdning pr. en dato (hist.lagerb)
5.40  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.40  *
5.40 c     finn_lverdi   begsr
5.40  *
5.40 c                   eval      vhisl3_vare = vlvare
5.40 c                   eval      vhisl3_lage = vllage
     c                   eval      vhisl3_dato = w_dato
5.40 c                   eval      vhisl3_time = *hival
5.40 c     vhisl3_key    setgt     vhisl3
5.40 c                   readp     vhisl3
5.40 c                   if        not %eof and
5.40 c                             vhvare = vhisl3_vare and
5.40 c                             vhlage = vhisl3_lage
6.11 c                   if        %abs(w_verd + (vhbehl * vhkpri))
6.11 c                              > 999999999,99
6.10 c                   eval      w_verd = 999999999,99
6.10 c                   else
5.40 c                   eval(h)   w_verd = w_verd + vhbehl * vhkpri
6.10 c                   endif
5.40 c                   else
5.40 c                   eval      w_verd = 0
5.40 c                   endif
5.40  *
5.40 c                   endsr
      *
      * --------------------------------------------------------------
      *  Skriver logg ved feil-situasjon type 7
      * --------------------------------------------------------------
      *
     c     skriv_logg    begsr
      *
     c                   clear                   lwo8lur
     c     lwo8lu_key    chain     lwo8lu
     c                   eval      mwoabc = mbkabc
     c                   eval      mwooms = mbomse
     c                   eval      mwovrd = w_verd
     c                   eval      mwovtx = vvtek1
     c                   eval      mwoogr = vvogrp
     c                   eval      mwohgr = vvhgrp
     c                   eval      mwougr = vvugrp
     c                   if        %found(lwo8lu)
     c                   update    lwo8lur
     c                   else
     c                   eval      mwofir = w_firm
     c                   eval      mwodat = lwo8lu_odat
     c                   eval      mwovnr = lwo8lu_ovnr
     c                   write     lwo8lur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av vare
      *
5.40 c     vvarl1_key    klist
5.40 c                   kfld                    w_firm
5.40 c                   kfld                    vvarl1_vare
5.40  *
5.40  * Key for les av lager
5.40  *
5.40 c     vlagl1_key    klist
5.40 c                   kfld                    w_firm
5.40 c                   kfld                    vlagl1_vare
5.40  *
5.40  * Key for les av lager historikk
5.40  *
5.40 c     vhisl3_key    klist
5.40 c                   kfld                    w_firm
5.40 c                   kfld                    vhisl3_vare
5.40 c                   kfld                    vhisl3_lage
5.40 c                   kfld                    vhisl3_dato
5.40 c                   kfld                    vhisl3_time
      *
      *  Key for les av ordre
      *
     c     mabdl7_key    klist
     c                   kfld                    w_firm
      *
      *  Key for les av ordre
      *
     c     mabdlx_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mabdlx_lagk
     c                   kfld                    mabdlx_vsor
     c                   kfld                    mabdlx_ogrp
     c                   kfld                    mabdlx_hgrp
     c                   kfld                    mabdlx_ugrp
     c                   kfld                    mabdlx_omse
      *
      * Key for oppdatering av overvåking  6
      *
     c     lwo8lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwo8lu_odat
     c                   kfld                    lwo8lu_ovnr
      *
      * Key for slett av overvåking på den dato man vil kjøre på.
      *
     c     lwo8lx_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwo8lx_odat
      *
     C     *entry        plist
     c                   parm                    p_dato
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_dato = p_dato
      *
     c                   endsr
