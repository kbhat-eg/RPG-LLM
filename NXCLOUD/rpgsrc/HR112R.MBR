      /title  ASLOGI Bestillingsforslag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : hr112r
      * Beskrivelse . . : Leser detaljene og og sjekker om det er flere lev.
      *                   Danner nye headinger og flytter detaljer.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       170604 bø   Nytt program
      *       010904 bø   Justert parameter mot status best.forslag
      *       290506 bø   Tar med firma over til HR112R
      *       010307 bø   Justert reade
      * 6.10  170609 BSA  Oppdatert med sporingsinformasjon
6.nu  * 5.30  170614 bof  endret ihht. nummerutvidelse
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flfhel1    if   e           k disk    rename(lfhepfr:lfhel1r)
     flfdtl2    uf   e           k disk    rename(lfdtpfr:lfdtl2r)
     flfdtpf    uf a e           k disk    rename(lfdtpfr:lfdtpfr)
     flfhelu    uf   e           k disk    rename(lfhepfr:lfhelur)
     flfhepf    uf a e           k disk    rename(lfhepfr:lfhepfr)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
      *
      * Parameter-inn
     d p_numm          s                   like(lhnumm)
     d p_firm          s                   like(lhfirm)
      *
      * Local Data Area
      *
     d                uds
      *
6.10 d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d lfdtlx_ldor     s                   like(lfldor)
     d lfhelx_numm     s                   like(lhnumm)
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
     d ar              s              6  0 DIM(100)
     d i               s              3  0
      *
     d s_ldor          s                   like(lfldor)
      *
     d w_firm          s                   like(lffirm)
     d w_numm          s                   like(lfnumm)
     d w_ntnr          s                   like(lfnumm)
6.nu d w_nest          s              8  0
     d w_fast          s             10
     d w_fell          s              6
     d w_kode          s              1
     d w_type          s              2
     d w_hnum          s                   like(lfnumm)
     d w_stat          s                   like(lfstat)
     d w_tbel          s                   like(lhtbel)
     d w_tvkt          s             11  3
     d w_ldor          s                   like(lhldor)
     d w_lsor          s                   like(lhlsor)
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram - leser detaljene - finner sum og status
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      s_ldor = 0
     c                   eval      i      = 0
      *
      * sjekker om det er flere leverandører på detaljene
      *
     c     lfdtl2_key    setll     lfdtl2
     c     lfdtl2_key    reade     lfdtl2
     c                   dow       not %eof
     c                   if        lfldor <> s_ldor
     c                   eval      i = i + 1
     c                   eval      ar(i) = lfldor
     c                   endif
     c                   eval      s_ldor  = lfldor
     c     lfdtl2_key    reade     lfdtl2
     c                   enddo
      *
      * Oppdaterer 1. leverandør (oppdaterer eksisterende heading)
      *
     c                   eval      i = 1
     c                   if        ar(i) <> 0
     c     lfhelu_key    chain     lfhelur
     c                   if        %found(lfhelu)
     c                   eval      lhldor = ar(i)
     c                   eval      w_ldor = lhldor
     c                   exsr      les_ldor
     c                   movel     w_lsor        lhlsor
6.10 c                   time                    lhedat
6.10 c                   time                    lhetim
6.10 c                   eval      lheusr = l_user
     c                   update    lfhelur
     c                   endif
     c                   endif
      *
      * Oppdaterer de neste leverandørene
      *
     c                   dou       i = 100
      *
     c                   eval      i = i + 1
     c                   if        ar(i) <> 0
     c                   exsr      hntnum
     c     lfhel1_key    chain     lfhel1r
     c                   if        %found(lfhel1)
     c                   eval      lhnumm = w_ntnr
     c                   eval      lhldor = ar(i)
     c                   eval      w_ldor = lhldor
     c                   exsr      les_ldor
     c                   movel     w_lsor        lhlsor
6.10 c                   time                    lhdate
6.10 c                   time                    lhtime
6.10 c                   eval      lhousr = l_user
6.10 c                   time                    lhedat
6.10 c                   time                    lhetim
6.10 c                   eval      lheusr = l_user
     c                   write     lfhepfr
     c                   endif
      *
      * gir detaljene nytt headingnummer og sletter det gamle.
     c                   eval      lfdtlx_ldor = ar(i)
     c     lfdtlx_key    setll     lfdtl2
     c     lfdtlx_key    reade     lfdtl2
     c                   dow       not %eof
     c                   eval      lfnumm = w_ntnr
6.10 c                   time                    lfodat
6.10 c                   time                    lfotim
6.10 c                   eval      lfousr = l_user
6.10 c                   time                    lfedat
6.10 c                   time                    lfetim
6.10 c                   eval      lfeusr = l_user
     c                   write     lfdtpfr
     c                   delete    lfdtl2r
     c     lfdtlx_key    reade     lfdtl2
     c                   enddo
      *
      * Oppdaterer sum og status på aktuelle heading
      *
     c                   eval      w_hnum = w_ntnr
     c                   eval      w_stat = *blank
     c                   eval      w_tbel = 0
     c                   eval      w_tvkt = 0
     c                   call      'LH714R '
     c                   parm                    w_hnum
     c                   parm                    w_stat
     c                   parm                    w_tbel
     c                   parm                    w_tvkt
      *
     c                   eval      lfhelx_numm = w_ntnr
     c     lfhelx_key    chain     lfhelur
     c                   if        %found(lfhelu)
     c                   eval      lhtbel = w_tbel
     c                   eval      lhvekt = w_tvkt
     c                   eval      lhstat = w_stat
6.10 c                   time                    lhedat
6.10 c                   time                    lhetim
6.10 c                   eval      lheusr = l_user
     c                   update    lfhelur
     c                   endif
      *
     c                   endif
      *
     c                   enddo
      *
      * Oppdaterer sum og status på 1. heading
      *
     c                   eval      w_hnum = w_numm
     c                   eval      w_stat = *blank
     c                   eval      w_tbel = 0
     c                   eval      w_tvkt = 0
     c                   call      'LH714R '
     c                   parm                    w_hnum
     c                   parm                    w_stat
     c                   parm                    w_tbel
     c                   parm                    w_tvkt
      *
     c                   eval      lfhelx_numm = w_numm
     c     lfhelx_key    chain     lfhelur
     c                   if        %found(lfhelu)
     c                   eval      lhtbel = w_tbel
     c                   eval      lhvekt = w_tvkt
     c                   eval      lhstat = w_stat
6.10 c                   time                    lhedat
6.10 c                   time                    lhetim
6.10 c                   eval      lheusr = l_user
     c                   update    lfhelur
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter/Oppdatering av NUMMER-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hntnum        begsr
      *
     c                   eval      w_nest = 0
     c                   eval      w_kode = '0'
     c                   eval      w_fell = laafor
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
      *
      *  Kaller opp subprogram for henting av nummer fra NUMMER-REGISTER
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_nest
      *
     c                   eval      w_ntnr      = w_nest
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for lesing av leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_ldor      begsr
      *
     c                   eval      rlevl1_levr = w_ldor
     c     rlevl1_key    chain     rlevl1                             91
     c                   if        *in91 = *off
     c                   eval      w_lsor = rlalfa
     c                   else
     c                   eval      w_lsor = *blank
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les  forslagslinjer
      *
     c     lfhel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
      *
      *  Key for oppdater forslagslinjer
      *
     c     lfhelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
      *
      *  Key for les /oppdater forslagslinjer
      *
     c     lfhelx_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lfhelx_numm
      *
      *  Key for les /oppdater forslagslinjer
      *
     c     lfdtl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
      *
      *  Key for les /oppdater forslagslinjer
      *
     c     lfdtlx_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    lfdtlx_ldor
      *
      * Key for les av STATUS
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les av leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_numm
      *
      *  Setter startverdier
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_numm = p_numm
      *
     c     lstsl1_key    chain     lstsl1r                            90
      *
     c                   endsr
