     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LI722R
      * Beskrivelse . . : Leser inn godkjente EDI ordrebekreftelser
      *
      * Spesialversjon. : Gipling
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       291018 bsa  Nytt program
      *
8.01  *K0000  230621 BSA  Utvidet meldingsnummer fra 6-8 siffer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fledil1    if   e           k disk    rename(ledipfr:ledil1r)
     fledilr    if   e           k disk    rename(ledipfr:ledilrr)
     fledilu    uf a e           k disk    rename(ledipfr:ledilur)
     felogl3    uf   e           k disk    rename(elogpfr:elogl3r)
      *
      * Definering av Local Data Arae (LDA)
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Parameter-inn
      *
     d p_stat          s              1
     d p_type          s                   like(litype)
     d p_mnum          s                   like(limnum)
     d p_mlin          s                   like(limlin)
      *
     d                 ds
8.01 d d_prec                        32
     d  d_firm                        3  0 overlay(d_prec)
     d  d_type                        4    overlay(d_prec:4)
8.01 d  d_mnum                        8  0 overlay(d_prec:8)
8.01 d  d_mlin                        6  0 overlay(d_prec:16)
8.01 d  d_biln                        8  0 overlay(d_prec:22)
8.01 d  d_kode                        1    overlay(d_prec:30)
8.01 d  d_ekod                        1    overlay(d_prec:31)
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d ledilr_type     s                   like(litype)
     d ledilr_mnum     s                   like(limnum)
     d ledilu_type     s                   like(litype)
     d ledilu_mnum     s                   like(limnum)
     d ledilu_mlin     s                   like(limlin)
     d ledil1_type     s                   like(litype)
     d ledil1_mnum     s                   like(limnum)
     d ledil1_mlin     s                   like(limlin)
     d elogl3_faar     s                   like(xlfaar)
     d elogl3_type     s                   like(xltype)
     d elogl3_mnum     s                   like(xlmnum)
      *
      * Definering av variable i parametre
      *
      *
      * Definering av variable
      *
     d b_forn          s              1    inz(*off)
      *
     d w_mnum          s                   like(limnum)
     d w_mlin          s                   like(limlin)
     d w_firm          s                   like(lifirm)
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_year          s              4  0
     d w_prec          s             45
     d w_besn          s              8  0
     d w_errk          s              2
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(10)
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
     d digit           c                   '0123456789 '
     d null            c                   '0000000000000'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * D1 Behandle åpningsopplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      p_stat = *blanks
      *
      * Dobbeltsjekk at vi har komplett EDI melding
      *
     c                   eval      ledil1_type = 'OBKR'
     c                   eval      ledil1_mnum = p_mnum
     c                   eval      ledil1_mlin = 100001
     c     ledil1_key    chain     ledil1
     c                   if        %found
      *
     c                   exsr      gen_ordre
      *
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for automatisk oppdatering av godkjente EDI meldinger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     gen_ordre     begsr
      *
     c                   eval      d_firm = w_firm
     c                   eval      d_type = p_type
     c                   eval      d_mnum = p_mnum
     c                   eval      d_mlin = p_mlin
     c                   eval      d_kode = *blanks
     c                   eval      w_prec = d_prec
      *
      * Oppdaterer innkjøpssystemet
      *
     c                   select
     c                   when      d_type = 'OBKR'
     c                   call      'LI710R'
     c                   parm                    w_prec
     c                   parm                    w_errk
     c                   endsl
      *
      * Merker oppdatert melding
      *
     c                   eval      ledilr_type = p_type
     c                   eval      ledilr_mnum = p_mnum
     c     ledilr_key    setll     ledilr
     c     ledilr_key    reade     ledilr
      *
     c                   dow       not %eof
      *
     c                   eval      ledilu_type = litype
     c                   eval      ledilu_mnum = limnum
     c                   eval      ledilu_mlin = limlin
     c     ledilu_key    chain     ledilu
     c                   if        %found
     c                   eval      likode = 'G'
     c                   eval      listat = '9'
     c                   time                    liedat
     c                   time                    lietim
     c                   eval      lieusr = l_user
     c                   update    ledilur
     c                   endif
      *
     c     ledilr_key    reade     ledilr
     c                   enddo
      *
      * Logger i fila at den er skrevet og av hvem.
      *
     c                   eval      elogl3_faar = %subdt(limdat:*years)
     c                   eval      elogl3_mnum = limnum
     c     elogl3_key    chain     elogl3
     c                   if        %found
     c                   time                    xlgodd
     c                   time                    xlgodt
     c                   eval      xlgodu = l_user
     c                   update    elogl3r
     c                   endif
      *
     c     end_xb18win   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_type
     c                   parm                    p_mnum
     c                   parm                    p_mlin
     c                   parm                    p_stat
      *
      *  Key for oppdatering av MELDINGSLINJER
      *
     c     ledilu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ledilu_type
     c                   kfld                    ledilu_mnum
     c                   kfld                    ledilu_mlin
      *
      *  Key for oppdatering av MELDINGSLINJER
      *
     c     ledil1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ledil1_type
     c                   kfld                    ledil1_mnum
     c                   kfld                    ledil1_mlin
      *
      *  Key for behandling av MELDINGSLINJER
      *
     c     ledilr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ledilr_type
     c                   kfld                    ledilr_mnum
      *
      *  Key for behandling av loggfile
      *
     c     elogl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    elogl3_faar
     c                   kfld                    elogl3_type
     c                   kfld                    elogl3_mnum
      *
      * Henter verdier fra parmametre
      *
     c                   time                    w_date
     c                   time                    w_time
     c                   eval      w_year = %subdt(w_date:*years)
      *
     c                   endsr
