     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASNAVIOKO
      * Program . . . . : IT018R
      * Beskrivelse . . : Leser kontaktpersoner og skriver til kø
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       080714 ask  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frukpl2    if   e           k disk    rename(rukppfr:rukpl2r)
      *
      * Local Data Area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d rukpl2_kpnr     s                   like(rukpnr)
      *
      *  Definering av datastrukturer for parameter
      *
     d d_parm          ds
     d  d_kpnr                        6
     d  d_enav                             like(ruenav)
     d  d_fnav                             like(rufnav)
     d  d_alfa                             like(rualfa)
     d  d_ptlf                             like(ruktlf)
     d  d_mtlf                             like(rumtlf)
     d  d_mail                             like(rumail)
     d  d_kobl                        6
     d  d_kpko                        1
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_prog          s             40
     d w_comd          s             20
     d w_parm          s           2048
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_rpgp          s             10
     d w_kpnr          s                   like(rukpnr)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram - leser alle poster og kaller opp std skriveprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser alle poster på RUKPL2
      *
     c                   eval      w_kpnr = 0
     c                   eval      rukpl2_kpnr = 0
     c     rukpl2_key    setll     rukpl2
      *
     c                   read      rukpl2
     c                   dow       not %eof and
     c                             rufirm = w_firm
      *
     c                   eval      d_kpnr = %trim(%editw(rukpnr : '    0 '))
     c                   eval      d_enav = ruenav
     c                   eval      d_fnav = rufnav
     c                   eval      d_alfa = rualfa
     c                   eval      d_ptlf = ruktlf
     c                   eval      d_mtlf = rumtlf
     c                   eval      d_mail = rumail
      *
      * Sjekk om kobling til kunde eller leverandør
      *
     c                   if        rukund <> 0
     c                   eval      d_kpko = 'K'
     c                   eval      d_kobl = %triml(%editw(rukund : '    0 '))
     c                   endif
      *
     c                   if        rulevr <> 0
     c                   eval      d_kpko = 'L'
     c                   eval      d_kobl = %triml(%editw(rulevr : '    0 '))
     c                   endif
     c                   eval      w_parm = d_parm
      *
     c                   if        rukpnr <> w_kpnr
     c                   call      'IT001R'
     c                   parm                    w_firm
     c                   parm                    w_prog
     c                   parm                    w_comd
     c                   parm                    w_parm
     c                   parm                    w_rpgp
     c                   eval      w_kpnr = rukpnr
     c                   endif
      *
     c                   read      rukpl2
     c                   enddo
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på valgt kode
      *
     c     rukpl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rukpl2_kpnr
      *
      * Setter inn parameterverdier
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_prog = 'NexstepImportKontaktpersoner'
     c                   eval      w_comd = 'Insert'
     c                   eval      w_rpgp = 'IT018R'
      *
     c                   endsr
