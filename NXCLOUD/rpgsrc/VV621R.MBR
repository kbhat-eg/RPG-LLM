     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VV621R
      * Beskrivelse . . : Vare, utskrift for scanning
      *
      *                   Diversevarer, varer uten ean-nummer, og varer som
      *                   er merket at de skal utelates fra prisbok/prisfil
      *                   blir ikke skrevet ut.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       260898 HKO  Nytt program
      *       230101 HKO  Leser varesortiment via utplukksfil
5.70  * PRGR  241008 bof  Innført prisgruppe /varetupe fra lager
6.11  * 6.10  290609 bof  Utivdet eannr til 14 og brukt std. oppslag
6.20  * 6.20  051110 bof  Utvidet sortiment med lager
6.21  * 6.20  190511 bø   Utvidet vl710r med div. info
6.22  * 6.20  200913 bø   Setter lager før man kaller vd700r
6.30  * 6.30  170918 bkv  Utvidet med trelast sortiment
      *
8.01  *PRG2   140622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
8.01  *PRG2               Endringer består kun i kompilering, da alle
8.01  *PRG2               referanser er eksternt definert.
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
6.30 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
     fvvawpf    if   e           k disk
      *
     fvv621p    o    e             printer infds(d_infds)
      *
      * Array
      *
     d a_index         s             86    dim(5000) ctdata perrcd(1)
      *
      * Definering av datastrukturer
      *
      *  Datastruktur for parameterliste -inn
      *
     d                 ds
     d d_list                        32
     d  d_firm                        3  0 overlay(d_list)
     d  d_vsor                       10    overlay(d_list:4)
     d  d_lage                        2  0 overlay(d_list:14)
      *
      *  Informasjon fra printer-file
      *
     d d_infds         ds
     d  d_pagnbr             369    372B 0
      *
      *  Innhold i index-array
      *
     d                 ds
     d d_index                       86
     d  d_text                       82    overlay(d_index)
     d  d_side                        4  0 overlay(d_index:83)
      *
      * Definering av parametre
      *
     d p_list          s             32
      *
      * Definering av variabler i nøkler
      *
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d b_frst          s              1    inz(*off)
     d b_svgr          s              1
     d b_sldo          s              1
     d b_stek          s              1
6.21 d b_inlr          s              1    inz(*off)
      *
     d w_tmst          s             12  0
     d w_i1            s              4  0
     d w_i2            s              4  0
      *
     d w_ogrp_alf      s              2
     d w_hgrp_alf      s              2
     d w_ugrp_alf      s              3
     d w_ldor_alf      s              6
      *
     d w_firm          s                   like(vvfirm)
     d w_otxt          s                   like(vgotxt)
     d w_htxt          s                   like(vghtxt)
     d w_utxt          s                   like(vgutxt)
     d w_lnav          s                   like(rlnavn)
     d w_ogrp_gml      s                   like(vvogrp)
     d w_hgrp_gml      s                   like(vvhgrp)
     d w_ugrp_gml      s                   like(vvugrp)
     d w_ldor_gml      s                   like(vvldor)
     d w_vare_gml      s                   like(vvvare)
5.70 d w_lage          s              2  0
5.70 d w_vtyp          s                   like(vvvtyp)
6.11 d w_east          s              1
6.11 d w_eann          s             14  0
6.21 d w_ldor          s                   like(vvldor)
6.21 d w_utda          s                   like(vvudat)
6.30 d w_lv_nobb       s                   like(vvlnob)
6.30 d w_lv_modn       s                   like(vvlmod)
6.30 d w_lv_lldo       s                   like(vvlnle)
6.30 d w_lv_llva       s                   like(vvllva)
      *
      * Local data area
      *
     d                uds
5.70 d l_lagr               1001   1002  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initiering og utskrift av hode
      *
     c                   exsr      hode
      *
     c                   eval      b_frst = *on
      *
      * Behandler utskift
      *
     c                   exsr      behanding
      *
      * Skiver innholdsfortegnelse / index
      *
     c                   exsr      index
      *
      * Avslutt
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av hode-opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode          begsr
      *
      * Legger inn dato i faste felter
      *
     c                   time                    w_tmst
     c                   move      w_tmst        a1date
      *
      * Henter firmaopplysninger
      *
     c     afirl1_key    chain     afirl1r                            90
     c                   eval      a1fnav = aafnav
      *
      * Skriver header
      *
     c                   write     a1hdr                                30
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av utskrift - utplukk på varesortiment
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behanding     begsr
      *
      * Kaller utplukksprogram
      *
     c                   eval      b_svgr = *on
     c                   eval      b_sldo = *on
     c                   eval      b_stek = *on
      *
     c                   call      'VD700R'
     c                   parm                    w_firm
     c                   parm                    d_vsor
     c                   parm                    b_svgr
     c                   parm                    b_sldo
     c                   parm                    b_stek
6.22 c                   parm                    w_lage
      *
      * Leser utplukksfil som inneholder alle varer i sortimentet
      *
     c                   eval      w_vare_gml = *blank
      *
     c                   read      vvawpf                                 90
     c                   dow       *in90 = *off
      *
     c                   if        vvvare <> w_vare_gml
     c                   exsr      behandl_vare
     c                   endif
      *
     c                   eval      w_vare_gml = vvvare
      *
     c                   read      vvawpf                                 90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_vare  begsr
      *
      * henter varetype
      *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    w_lage
5.70 c                   parm                    w_vtyp
6.21 c                   parm                    w_utda
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
      *
      * Tester om vare skal være med
      *
5.70 c                   if        w_vtyp = 'D' or
5.70 c                             w_vtyp = 'T'
     c                   goto      end_vare
     c                   endif
      *
     c                   if        vvkpdi = 1
     c                   goto      end_vare
     c                   endif
6.11  *
6.11  * Kaller program for henting av ean-nr
6.11  *
6.11 c                   call      'VE712R'
6.11 c                   parm                    w_firm
6.11 c                   parm                    vvvare
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_east
      *
      * Behandler brudd
      *
     c                   if        vvogrp <> w_ogrp_gml
     c                   exsr      hode_ogrp
     c                   endif
      *
     c                   if        vvogrp <> w_ogrp_gml or
     c                             vvhgrp <> w_hgrp_gml
     c                   exsr      hode_hgrp
     c                   endif
      *
     c                   if        vvogrp <> w_ogrp_gml or
     c                             vvhgrp <> w_hgrp_gml or
     c                             vvugrp <> w_ugrp_gml
     c                   exsr      hode_ugrp
     c                   endif
      *
     c                   if        vvogrp <> w_ogrp_gml or
     c                             vvhgrp <> w_hgrp_gml or
     c                             vvugrp <> w_ugrp_gml or
     c                             vvldor <> w_ldor_gml
     c                   exsr      hode_ldor
     c                   endif
      *
      * Skriver detaljlinje
      *
     c                   exsr      detalj
      *
      * Tar vare på brudd-variable
      *
     c                   eval      w_ogrp_gml = vvogrp
     c                   eval      w_hgrp_gml = vvhgrp
     c                   eval      w_ugrp_gml = vvugrp
     c                   eval      w_ldor_gml = vvldor
      *
     c                   eval      b_frst = *off
      *
     c     end_vare      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift ved brudd på overgruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode_ogrp     begsr
      *
     c                   eval      w_otxt = *blank
      *
     c                   eval      vogrl1_oogr = vvogrp
     c     vogrl1_key    chain     vogrl1                             92
     c                   if        *in92 = *off
     c                   eval      w_otxt = vgotxt
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift ved brudd på hovedgruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode_hgrp     begsr
      *
     c                   eval      w_htxt = *blank
      *
     c                   eval      vhgrl1_hogr = vvogrp
     c                   eval      vhgrl1_hhgr = vvhgrp
     c     vhgrl1_key    chain     vhgrl1                             92
     c                   if        *in92 = *off
     c                   eval      w_htxt = vghtxt
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift ved brudd på undergruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode_ugrp     begsr
      *
     c                   eval      w_utxt = *blank
      *
     c                   eval      vugrl1_uogr = vvogrp
     c                   eval      vugrl1_uhgr = vvhgrp
     c                   eval      vugrl1_uugr = vvugrp
     c     vugrl1_key    chain     vugrl1                             92
     c                   if        *in92 = *off
     c                   eval      w_utxt = vgutxt
     c                   endif
      *
     c                   move      vvogrp        w_ogrp_alf
     c                   move      vvhgrp        w_hgrp_alf
     c                   move      vvugrp        w_ugrp_alf
      *
     c                   eval      a2txt1 = %trim( %trim(w_otxt) + ' - ' +
     c                                      %trim(w_htxt) + ' - ' +
     c                                      %trim(w_utxt) + ' (' +
     c                                      w_ogrp_alf + ' ' +
     c                                      w_hgrp_alf + ' ' +
     c                                      w_ugrp_alf + ')')
      *
     c                   eval      a2txt2 = a2txt1
      *
     c                   write     a2hdr                                30
     c                   exsr      overflow
      *
      * Tar vare på sidenummer i index-array
      *
     c                   eval      d_text = a2txt2
     c                   eval      d_side = d_pagnbr
      *
     c                   eval      w_i1 = w_i1 + 1
     c                   eval      a_index(w_i1) = d_index
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift ved brudd på leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode_ldor     begsr
      *
     c                   eval      w_lnav = *blank
      *
     c                   eval      rlevl1_levr = vvldor
     c     rlevl1_key    chain     rlevl1                             92
     c                   if        *in92 = *off
     c                   eval      w_lnav = rlnavn
     c                   endif
      *
     c                   move      vvldor        w_ldor_alf
      *
     c                   eval      a3txt1 = %trim( %trim(w_lnav) + ' (' +
     c                                      w_ldor_alf + ')')
      *
     c                   eval      a3txt2 = a3txt1
      *
     c                   write     a3hdr                                30
     c                   exsr      overflow
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine utskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalj        begsr
      *
     c                   eval      d1tek1 = vvtek1
     c                   eval      d1tek2 = vvtek2
     c                   eval      d1vare = vvvare
6.11 c                   eval      d1eann = w_eann / 10
      *
      * Skriv detaljlinje
      *
     c                   if        d1eann <> 0
     c                   write     d1dtl                                30
     c                   exsr      overflow
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for håndtering av overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overflow      begsr
      *
     c                   if        *in30 = *on
     c                   write     a1hdr                                30
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift av innholdsfortegnelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     index         begsr
      *
      * Overskrift
      *
     c                   eval      a4fnav = a1fnav
     c                   eval      a4date = a1date
      *
     c                   write     a4hdr
      *
      * Skriver linjer i innholdsfortegnelsen
      *
     c                   dow       w_i2 < w_i1
      *
     c                   eval      w_i2 = w_i2 + 1
      *
     c                   eval      d_index = a_index(w_i2)
      *
     c                   eval      d2text = %trim(d_text) + '   ' +
     c                                     '..............................'+
     c                                     '..............................'+
     c                                     '.......'
     c                   eval      d2side = d_side
      *
     c                   write     d2dtl                                30
     c                   if        *in30 = *on
     c                   write     a4hdr
     c                   endif
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for oppslag på firmaregister
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      *
      * Splitter datastruktur i variable
      *
     c                   eval      d_list = p_list
      *
     c                   eval      w_firm = d_firm
      *
5.70 c                   eval      w_lage = l_lagr
      *
     c                   endsr
