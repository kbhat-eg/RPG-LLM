      /title  ASLAGR Lagertelling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LE711R
      * Beskrivelse . . : Oppdater lager-register, Oppdater lager
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *
      * 6.10  150609 BSA  Oppdatert med sporingsinformasjon
      * 6.10  220909 ASK  Nye parametre til VL001R
6.11  * 6.10  191109 BSA  Feil lengde på parameteroverføring
6.20  *       080410 bø   Utvidet med timestamp for bruk videre
6.21  *       161110 bof  Timestamp skal kun brukes når det syklisk telling
6.22  * 6.20  230211 bsa  Ved syklisk telling skal dato fra tellelinja følge
6.22  *                   ved rekalkulering av lagerbeholdning.
6.nu  * 6.30  230211 bsa  utvidelse mtp. nummerutvidelse
6.31  * 6.30  290814 bof  Utvidet med parm. varetype S-->L (hvis tellet)
6.32  * 6.30  190816 bsa  Hvis sluttt-tid på telling er 0, settes oppdaterings
6.32  *                   oppdateringstidspunkt som klokkeslett. NB Kun hvis
6.32  *                   tellelinja ikke har tidspunkt. Blir riktigere ved
6.32  *                   rekalkulering av lagerbok.
6.33  * 6.30  120117 bof  LE711R skal kun skrives hvis vare har vært S-vare
6.34  * 6.30  150319 nho  Sjekker om modul for gj.kostpris skal kjøres.
6.34  *                   Oppdatering til regnskap.
6.35  * 6.30  040220 ask  Flyttet regnskapsoppdatering utenfor lese loop.
6.35  *                   Skal kjøres bare en gang utenfor batch.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     flbchlu    uf   e           k disk    rename(lbchpfr:lbchlur)
     fltell1    if   e           k disk    rename(ltelpfr:ltell1r)
     fvlaglu    uf   e           k disk    rename(vlagpfr:vlaglur)
6.31 fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
6.31 fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
6.31 fvvarlu    uf   e           k disk    rename(vvarpfr:vvarlur)
6.31 fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
6.34 famodl1    if   e           k disk    rename(amodpfr:amodl1r)
6.34 fvgkkir    if   e           k disk    rename(vgkkst:vgkkirr)
      *
6.31 fle711p    o    e             printer
      *
      *    Definering av LDA
      *
     d                uds
6.10 d l_user                911    920
6.31 d l_wsid                901    910
     d l_firm                944    946  0
      *
      * Definering av parametre -inn
      *
     d                 ds
     d d_prec                        64
     d  d_batc                       10    overlay(d_prec)
     d  d_val1                        1  0 overlay(d_prec:11)
     d  d_val2                        1  0 overlay(d_prec:12)
     d  d_val3                        1  0 overlay(d_prec:13)
6.31 d  d_val4                        1  0 overlay(d_prec:14)
6.31 d  d_val5                        1  0 overlay(d_prec:15)
      *
      * Datastruktur for lageroppdatering
      *
     d                 ds
6.nu d p_rec                        128
     d  p_vare                       15    overlay(p_rec)
     d  p_lage                        2  0 overlay(p_rec:16)
     d  p_dato                         d   overlay(p_rec:18) datfmt(*iso)
     d  p_time                         t   overlay(p_rec:28) timfmt(*iso)
     d  p_otyp                        2    overlay(p_rec:36)
     d  p_ltyp                        2    overlay(p_rec:38)
     d  p_anta                       11  3 overlay(p_rec:40)
     d  p_kopr                        9  2 overlay(p_rec:51)
     d  p_refr                       20    overlay(p_rec:60)
     d  p_syst                        1    overlay(p_rec:80)
     d  p_altk                        2    overlay(p_rec:81)
     d  p_rest                        1    overlay(p_rec:83)
     d  p_type                        1    overlay(p_rec:84)
     d  p_enhe                        3    overlay(p_rec:85)
     d  p_inlr                        1    overlay(p_rec:88)
     d  p_lety                        1    overlay(p_rec:89)
6.nu d  p_onum                        8  0 overlay(p_rec:90)
6.nu d  p_olin                        4  0 overlay(p_rec:98)
6.31  *
6.31  * Datastruktur for lageroppdatering
6.31  *
6.31 d                 ds
6.31 d hlrec                        128
6.31 d  hlvare                       15    overlay(hlrec)
6.31 d  hllage                        2  0 overlay(hlrec:16)
6.31 d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
6.31 d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
6.31 d  hlotyp                        2    overlay(hlrec:36)
6.31 d  hlltyp                        2    overlay(hlrec:38)
6.31 d  hlanta                       11  3 overlay(hlrec:40)
6.31 d  hlkopr                        9  2 overlay(hlrec:51)
6.31 d  hlrefr                       20    overlay(hlrec:60)
6.31 d  hlsyst                        1    overlay(hlrec:80)
6.31 d  hlaltk                        2    overlay(hlrec:81)
6.31 d  hlrest                        1    overlay(hlrec:83)
6.31 d  hltype                        1    overlay(hlrec:84)
6.31 d  hlenhe                        3    overlay(hlrec:85)
6.31 d  hlinlr                        1    overlay(hlrec:88)
6.31 d  hllety                        1    overlay(hlrec:89)
6.31 d  hlonum                        8  0 overlay(hlrec:90)
6.31 d  hlolin                        4  0 overlay(hlrec:98)
      *
      *    Definering av variable i nøkler
      *
     d lbchlu_numm     s                   like(lcbatc)
     d ltell1_batc     s                   like(lebatc)
     d ltell1_line     s                   like(leline)
     d ltell1_numm     s                   like(lenumm)
     d ltell1_suff     s                   like(lesuff)
     d vlaglu_lage     s                   like(vllage)
     d vlaglu_vare     s                   like(vlvare)
6.31 d vvarl1_vare     s                   like(vvvare)
6.31 d vvarlu_vare     s                   like(vvvare)
6.31 d vlagl1_lage     s                   like(vllage)
6.31 d vlagl1_vare     s                   like(vlvare)
6.34 d amodl1_modu     s                   like(axmodu)
      *
      *    Definering av variable
      *
     d w_firm          s                   like(lcfirm)
6.nu d w_lrec          s            128
     d w_parm          s             64
     d w_retu          s              1
6.31 d b_first         s              1    inz(*off)
6.31 d w_date          s               d   datfmt(*iso)
6.31 d w_time          s               t   timfmt(*iso)
6.31 d w_stat          s              1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Merk av at denne tellingen er ferdig
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Oppdater Batch-register med som sier at telle-batch er overført
      *
     c                   eval      lbchlu_numm = d_batc
     c     lbchlu_key    chain     lbchlu                             90
     c                   if        *in90 = *off
     c                   eval      lcsper = 1
6.10 c                   time                    lceeda
6.10 c                   time                    lceeti
6.10 c                   eval      lceusr = l_user
     c                   update    lbchlur
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandle utplukk, Setter inn riktige parameterverdier
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      ltell1_batc = d_batc
     c                   eval      ltell1_numm = lcnumm
     c     ltell1_key    setll     ltell1r
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Leser poster fra register for utplukks-test
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     start         tag
      *
     c                   eval      *in90 = *off
     c                   read      ltell1r                                90
      *
     c                   if        *in90 = *on
     c                   goto      end_pgm
     c                   endif
      *
      *  Sjekk   Er det korrekt batch som leses,    Ellers hopp til Slutt
      *
     c                   if        lebatc <> ltell1_batc
     c                   goto      end_pgm
     c                   endif
      *
      *  Sjekk   Er det korrekt liste som leses,    Ellers hopp til Start
      *
     c                   if        lenumm <> ltell1_numm
     c                   goto      start
     c                   endif
      *
      *  Sjekk   Er det korrekt Subliste som leses, Ellers hopp til Start
      *
     c                   if        lesuff <> *zero
     c                   goto      start
     c                   endif
6.31  *
6.31 c                   if        d_val4 = 1
6.31  *
6.31  *  Hvis vare er skaffevare og den er tellet med beholdning, settes
6.31  *  varetype lik L og utgår dato sette på lageret
6.31  *
6.31 c                   if        leantt >  0
6.31 c                   eval      vlagl1_vare = levare
6.31 c                   eval      vlagl1_lage = lelage
6.31 c     vlagl1_key    chain     vlagl1r                            91
6.31 c                   if        %found(vlagl1) and
6.31 c                             vlvtyp = 'S'
6.31  *
6.31 c                   exsr      logg_lager
6.31  *
6.31 c                   eval      vlaglu_vare = levare
6.31 c                   eval      vlaglu_lage = lelage
6.31 c     vlaglu_key    chain     vlaglur
6.31 c                   if        %found(vlaglu)
6.31 c                   eval      vlvtyp = 'L'
6.31 c                   eval      vludat = lcedat
6.31 c                   time                    vledat
6.31 c                   time                    vletim
6.31 c                   eval      vleusr = l_user
6.31 c                   time                    vlfeda
6.31 c                   time                    vlfeti
6.31 c                   eval      vlfeus = l_user
6.31 c                   update    vlaglur
6.31 c                   endif
6.31  * oppdaterer også  vare til varetype = L
6.31 c                   eval      vvarlu_vare = levare
6.31 c     vvarlu_key    chain     vvarlur
6.31 c                   if        %found(vvarlu) and
6.31 c                             vvvtyp = 'S'
6.31 c                   eval      vvvtyp = 'L'
6.31 c                   time                    vvedat
6.31 c                   time                    vvetim
6.31 c                   eval      vveusr = l_user
6.31 c                   update    vvarlur
6.31 c                   endif
6.31 c                   if        d_val5 = 1
6.31 c                   exsr      utskrift
6.31 c                   endif
6.31 c                   endif
6.31 c                   endif
6.31  *
6.31 c                   endif
      *
      *  Oppdater lager-register utfra Telle-register
      *
     c                   exsr      oppd_lager
      *
      *
      *  Kaller opp subprogram for oppdatering av lager utfra H. Lagerbok
      *  Kan utelates fra parameterbilde.
      *
     c                   if        d_val2 = 1
      *
6.22 c                   if        letims <> *loval and
6.22 c                             lcstel = 1
6.22 c                   move      letims        p_dato
6.22 c                   move      letims        p_time
6.32 c                   if        p_time = *loval
6.32 c                   time                    p_time
6.32 c                   endif
6.22 c                   else
6.22 c                   eval      p_dato = lcedat
6.32  * For riktigere beregning av saldo, bruk kjøretidspunkt hvis
6.32  * slutt-tidspunkt på tellinga ikke er utfylt.
6.32 c                   if        p_time = *loval
6.32 c                   time                    p_time
6.32 c                   else
6.22 c                   eval      p_time = lcetim
6.32 c                   endif
6.22 c                   endif
      *
     c                   call      'LE712R'
6.22 c                   parm                    p_dato
6.22 c                   parm                    p_time
     c                   parm                    levare
     c                   parm                    lelage
     c                   endif
      *
      *  Oppdater årssaldo dersom det er valgt
      *
     c                   if        d_val3 = 1
     c                   exsr      oppd_saldo
     c                   endif
      *
      *  Velg neste
      *
     c                   goto      start
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
6.35  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.35  * Sjekk om modul for gj.kostpris og oppdatering til regnskap skal
6.35  * kjøres
6.35  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.35  * Sjekker om GJKOST Modul
6.35 c                   eval      amodl1_modu = 'GJKOST'
6.35 c     amodl1_key    chain     amodl1
6.35 c                   if        %found(amodl1)
6.35  * Henter kode informasjon
6.35 c     vgkkir_key    chain     vgkkir
6.35 c                   if        %found(vgkkir) and
6.35 c                             vgkrgn = 'J'
6.35 c                   call      'VG806R'
6.35 c                   parm                    w_firm
6.35 c                   parm                    d_batc
6.35 c                   endif
6.35 c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppdatering av Lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_lager    begsr
      *
      *  Legger over verdier i overførings-felter
      *
     c                   eval      p_vare = levare
     c                   eval      p_lage = lelage
     c                   eval      p_altk = 'T '
     c                   eval      p_enhe = leenh1
      *
6.21 c                   if        letims <> *loval and
6.21 c                             lcstel = 1
6.21 c                   move      letims        p_dato
6.21 c                   move      letims        p_time
6.21 c                   else
     c                   eval      p_dato = lcedat
     c                   eval      p_time = lcetim
6.21 c                   endif
     c                   eval      p_otyp = *blank
     c                   eval      p_ltyp = *blank
      *
     c                   eval      p_anta = leantt
     c                   eval      p_kopr = *zero
      *
     c                   eval      p_refr = 'Batc: ' + lcbatc
     c                   eval      p_syst = 'L'
     c                   eval      p_rest = *blank
     c                   eval      p_lety = *blank
     c                   eval      p_inlr = '1'
     c                   eval      p_onum = 0
     c                   eval      p_olin = 0
      *
      *  Kaller opp lageroppdaterings-program
      *
     c                   eval      w_lrec = p_rec
      *
     c                   call      'VL001R'
     c                   parm                    w_retu
     c                   parm                    w_lrec
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Oppdatering av Årssaldo-feltet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_saldo    begsr
      *
     c                   eval      vlaglu_vare = levare
     c                   eval      vlaglu_lage = lelage
     c     vlaglu_key    chain     vlaglur                            91
      *
     c                   if        *in91 = *off
     c                   eval      vlb101 = leantt
6.10 c                   time                    vledat
6.10 c                   time                    vletim
6.10 c                   eval      vleusr = l_user
     c                   update    vlaglur
     c                   endif
      *
     c                   endsr
6.31  *
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *  Utskrift av skaffevarer med tellet beholdning
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *
6.31 c     utskrift      begsr
6.31 c                   if        b_first = *off
6.31 c                   eval      a1user  = l_user
6.31 c                   move      l_wsid        a1wsid
6.31 c                   time                    w_date
6.31 c                   time                    w_time
6.31 c     *dmy          move      w_date        a1date
6.31 c     *hms          move      w_time        a1time
6.31 c                   eval      a1firm  = l_firm
6.31 c                   write     a1hdr                                30
6.31 c                   eval      b_first = *on
6.31 c                   endif
6.31  *
6.31 c                   eval      vvarl1_vare = levare
6.31 c     vvarl1_key    chain     vvarl1                             80
6.31 c                   eval      d1tek1  = vvtek1
6.31 c                   movel     vvvare        d1vare
6.31 c                   eval      d1lage  = lelage
6.31 c                   eval      d1antt  = leantt
6.31  *
6.31 c                   write     d1det                                30
6.31  *
6.31 c                   if        *in30 = *on
6.31 c                   exsr      overflow
6.31 c                   endif
6.31  *
6.31 c                   endsr
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *  OVFLOW    Behandling av overflow
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *
6.31 c     overflow      begsr
6.31  *
6.31  *  Skriver heading
6.31  *
6.31 c                   write     a1hdr                                30
6.31  *
6.31 c                   endsr
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  * Subrutine for å hente å logge endring på lager
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *
6.31 c     logg_lager    begsr
6.31  *
6.31 c                   eval      vvarl1_vare = levare
6.31 c     vvarl1_key    chain     vvarl1
6.31 c                   if        %found(vvarl1)
6.31 c                   move      levare        hlvare
6.31 c                   move      lelage        hllage
6.31 c                   move      vvenhl        hlenhe
6.31 c                   time                    hldato
6.31 c                   time                    hltime
6.31 c                   eval      hlotyp  = *blank
6.31 c                   eval      hlltyp  = *blank
6.31  *
6.31 c                   eval      hlanta = 0
6.31 c                   eval      hlkopr = 0
6.31  *
6.31 c                   eval      hlrefr = 'Vtyp endret S--> L '
6.31 c                   move      'L'           hlsyst
6.31 c                   eval      hlaltk = 'SL'
6.31 c                   eval      hlrest = *blank
6.31 c                   eval      hlonum = 0
6.31 c                   eval      hlolin = 0
6.31  *
6.31 c                   eval      w_stat = *blank
6.31 c                   call      'VL001R'
6.31 c                   parm                    w_stat
6.31 c                   parm                    hlrec
6.31 c                   endif
6.31  *
6.31 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av LAGER-STATUS
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for les av BATCH
      *
     c     lbchlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lbchlu_numm
      *
      *  Key for les av LAGER-TELLE
      *
     c     ltell1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ltell1_batc
     c                   kfld                    ltell1_numm
     c                   kfld                    ltell1_suff
     c                   kfld                    ltell1_line
      *
      *  Key for oppdatering av LAGER
      *
     c     vlaglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglu_vare
     c                   kfld                    vlaglu_lage
6.31  *
6.31  *  Key for les av LAGER
6.31  *
6.31 c     vlagl1_key    klist
6.31 c                   kfld                    w_firm
6.31 c                   kfld                    vlagl1_vare
6.31 c                   kfld                    vlagl1_lage
6.31  *
6.31  *  Key for les av vare
6.31  *
6.31 c     vvarl1_key    klist
6.31 c                   kfld                    w_firm
6.31 c                   kfld                    vvarl1_vare
6.31  *
6.31  *  Key for oppdatering av vare
6.31  *
6.31 c     vvarlu_key    klist
6.31 c                   kfld                    w_firm
6.31 c                   kfld                    vvarlu_vare
6.31  *
6.31  * Key for firmareg
6.31  *
6.31 c     afirl1_key    klist
6.31 c                   kfld                    w_firm
6.34  *
6.34  * Key for les av moduler Nexstep
6.34  *
6.34 c     amodl1_key    klist
6.34 c                   kfld                    amodl1_modu
6.34  * Key for kostpris parameter
6.34  *
6.34 c     vgkkir_key    klist
6.34 c                   kfld                    w_firm
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    w_parm
     c                   eval      d_prec = w_parm
      *
      *  Legge inn firmanummer i nøklene
      *
     c                   eval      w_firm = l_firm
      *
      *  Legger inn type for oppdatering av lager  R=Registrering
      *
     c                   eval      p_type = 'R'
      *
      *  Hent opplysninger fra LAGER-STATUS-KODER
      *
     c     lstsl1_key    chain     lstsl1r                            90
6.31  *
6.31  * opplysninger skal printes ut
6.31  *
6.31 c     afirl1_key    chain     afirl1r                            66
6.31 c                   move      aafnav        a1fnav
      *
     c                   endsr
