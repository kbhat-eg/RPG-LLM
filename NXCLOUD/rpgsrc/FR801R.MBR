     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FR801R
      * Beskrivelse . . : Rabattmatrise, flytter rabatt-1 til rabatt-N/B
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       190204 HKO  Nytt program
      * 6.10  250609 BSA  Oppdatert med sporingsinformasjon
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffrablu    uf   e           k disk    rename(frabpfr:frablur)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
      * Definering av Local data area
      *
     d                uds
6.10 d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_firm          s                   like(frfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   read      frablur
     c                   dow       not %eof
      *
     c                   if        frfirm = w_firm and
     c                             frvare <> *blank and
     c                             frrab1 <> 0 and
     c                             frrab2 = 0
      *
     c                   eval      vvarl1_vare = frvare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
      *
     c                   select
     c                   when      vvkpri = *blank
     c                   eval      frrabn = 0
     c                   eval      frrabb = 0
     c                   when      vvkpri = 'N'
     c                   eval      frrabn = frrab1
     c                   eval      frrabb = 0
     c                   eval      frrab1 = 0
     c                   when      vvkpri = 'B'
     c                   eval      frrabn = 0
     c                   eval      frrabb = frrab1
     c                   eval      frrab1 = 0
     c                   endsl
      *
6.10 c                   time                    fredat
6.10 c                   time                    fretim
6.10 c                   eval      freusr = l_user
     c                   update    frablur
      *
     c                   endif
     c                   endif
      *
     c                   read      frablur
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
