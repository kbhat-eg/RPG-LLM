     H decedit('0.') datedit(*dmy-) option(*nodebugio)
     h DftActgrp(*No) BndDir('CGIDEV2/CGIDEV2')
      /copy hspecsbnd
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : FP272R
      * Beskrivelse . . : Lager xml til kollietiketter
      *
      *
      * Spesialversjon. :
      *
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       090517 bsa  Nytt program
6.30  *       100717 bsa  Henter info om mobil og kontaktperson etter hierarki
6.30  *                   først fra ordrehodet, deretter fra prosjekt, sist fra
6.30  *                   kunde.
6.31  *       061017 bsa  Manglende siffer i antall
7.00  *       050419 bkv  Sjekk om tall
7.01  * K000  050620 bsa  Legger inn mulighet for logging.
7.02  * K000  050620 bsa  Overrfører filgruppe som parameter
      *
8.01  * K000  160123 bsa  Hvis antall ligger i LDA, benyttes
8.01  * K000              det herfra, hvis ikke hardkode med 1.
8.02  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffodtlr    if   e           k disk    rename(fodtpfr:fodtlrr)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fafpslr    if   e           k disk    rename(afpspfr:afpslrr)
6.30 ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
      *
     d p_firm          s              3  0
     d p_numm          s              8  0
     d p_suff          s              2  0
     d p_str_in        s            512
     d p_str_out       s            512
     d p_lr            s              1
     d p_filg          s             10
     d p_xmlf          s            256
7.02 d p_fprg          s              3
      *
      * Definering av variabler i nøkler
      *
     d rkunl1_kund     s                   like(rkkund)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fodtlr_numm     s                   like(fonumm)
     d fodtlr_suff     s                   like(fosuff)
     d fodtlr_line     s                   like(fdline)
6.30 d fkprl1_kund     s                   like(flkund)
6.30 d fkprl1_kpro     s                   like(flkpro)
      *
     d x               s              3  0
     d tmptime         s               T
      *
     d w_anta          s              3  0
     d w_pdf_ds        s            512
     d w_firm          s                   like(rkfirm)
     d w_path          s             50
     d w_xmlp          s             50
     d w_navn          s            100
     d w_gate          s            100
     d w_adr2          s            100
     d w_sted          s            100
     d w_lnav          s            100
     d w_lad1          s            100
     d w_lad2          s            100
     d w_lste          s            100
     d w_date          s               d   datfmt(*iso)
     d w_dato          s              6  0
     d w_time          s               t   timfmt(*iso)
     d w_tidp          s              6  0
     d w_ldat          s              6  0
     d w_fast          s             10
     d w_ruti          s             10
     d w_numm          s              8  0
     d w_pdf           s              1  0
     d w_jnav          s             15
     d w_jkey          s             41
     d w_jtxt          s             35
     d w_jstat         s              2  0
     d w_fell          s              6
     d w_jtext         s            200
     d w_kode          s              1
     d w_type          s              2
     d w_fnav          s            100
     d w_fad1          s            100
     d w_fad2          s            100
     d w_fste          s            100
     d w_knav          s            100
     d w_kgat          s            100
     d w_kad2          s            100
     d w_kste          s            100
     d w_spol          s              5
     d w_draw1         s             10
     d w_draw2         s             10
     d w_land          s              9
     d w_retx          s             50
6.30 d w_kprs          s            100
6.30 d w_tlfn          s             50
8.01 d w_akop          s              5  0
      *
      *  UDS Local Data Area
      *
     d                uds
     d l_poff                584    584
     d l_utqm                585    594
     d l_bach                595    635
     d l_arch                636    636  0
     d l_skje                637    637  0
     d l_land                638    638  0
     d l_exlp                639    639  0
     d l_mail                640    640  0
     d l_kopi                641    641
     d l_ruti                800    809
     d l_outq                810    819
     d l_akop                838    843  0
8.01 d l_akopa               838    843
     d d_alin                856    858  0
     d l_bgir                881    881  0
     d d_kidk                883    883  0
     d d_ocrk                885    885  0
     d l_wsid                901    910
     d l_user                911    920
     d l_filg                931    933
     d l_firm                944    946  0
     d l_avde                947    950  0
      *
     d b_first         s              1    inz(*off)
     d b_pdfp          s              1    inz(*off)
7.01 d b_debug         s              1    inz(*off)
      *
     d XML_Output      s            256a   Varying
     d XML_path        s            256a
     d Out_path        s            256a
      *
     d tmpdate         s               d
      *
     d d_dato          ds            10
     d  d_aar                         4    overlay(d_dato:1)
     d  d_mnd                         2    overlay(d_dato:6)
     d  d_dag                         2    overlay(d_dato:9)
      *
      * Linje med kolliinformasjon
      *
     d d_line          ds
     d  d_ktyp                        8
6.31 d  d_fil0                        1
     d  d_kana                        2
     d  d_kann                        2  0 overlay(d_kana)
6.31 d  d_fil1                        1
     d  d_ktxt                       24
      *
      * PROA info
      *
     d d_pdf_ds        ds
     d  d_xmlm                       75
     d  d_jasm                       75
     d  d_logo                       75
     d  d_path                      100
     d  d_emal                       50
     d  d_fifo                       75
     d  d_fkop                        1
     d  d_dtaq                        1
     d  d_sign                        1
      *
      * Definering av konstanter
      *
7.00 d digits          c                   ' 0123456789'
      *
      *
      /copy prototypeb
      /copy usec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Har vi PROA oppsett som fungerer ??
      *
     c                   if        b_pdfp = *off
     c                   goto      avslutt
     c                   endif
7.01  * Vi logger videre
7.01 c                   if        b_debug = *on
7.01 c                   eval      w_jstat = 10
7.01 c                   eval      w_jtext = 'PROA er ok !'
7.01 c                   call      'AB705R'
7.01 c                   parm                    w_jkey
7.01 c                   parm                    w_jstat
7.01 c                   parm                    w_jtext
7.01 c                   endif
7.01  * Vi logger videre
7.01 c                   if        b_debug = *on
7.01 c                   eval      w_jstat = 10
7.01 c                   eval      w_jtext = 'XML mal ' + %trim(d_xmlm) +
7.01 c                             ' Path ' + %trim(d_path)
7.01 c                   call      'AB705R'
7.01 c                   parm                    w_jkey
7.01 c                   parm                    w_jstat
7.01 c                   parm                    w_jtext
7.01 c                   endif
      *
      * Sjekk at vi har et ordrehode
      *
     c     fohel1_key    chain     fohel1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
7.01  * Vi logger videre
7.01 c                   if        b_debug = *on
7.01 c                   eval      w_jstat = 10
7.01 c                   eval      w_jtext = 'Ordren er funnet !'
7.01 c                   call      'AB705R'
7.01 c                   parm                    w_jkey
7.01 c                   parm                    w_jstat
7.01 c                   parm                    w_jtext
7.01 c                   endif
6.30  *
6.30  * Hent info fra prosjekt, hvis det finnes
6.30  *
6.30 c                   eval      flkprs = *blanks
6.30 c                   eval      fltlfm = *blanks
6.30 c                   if        fokpro > 0
6.30 c                   eval      fkprl1_kund = fokund
6.30 c                   eval      fkprl1_kpro = fokpro
6.30 c     fkprl1_key    chain     fkprl1
6.30 c                   endif
      *
      * Sjekk at vi har et gyldig kundenummer
      *
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
7.01  * Vi logger videre
7.01 c                   if        b_debug = *on
7.01 c                   eval      w_jstat = 10
7.01 c                   eval      w_jtext = 'Kunde er ok !'
7.01 c                   call      'AB705R'
7.01 c                   parm                    w_jkey
7.01 c                   parm                    w_jstat
7.01 c                   parm                    w_jtext
7.01 c                   endif
      *
      * Har vi kolliinfo på linjenivå ??
      *
     c                   eval      w_anta = 0
     c                   eval      fodtlr_line = 9000
     c     fodtlr_key    setll     fodtlr
     c     fodtlr_key2   reade     fodtlr
     c                   dow       not %eof
      *
     c                   if        fdline = 9000
     c                   eval      d_line = fdtek1
7.00 c                   if        %check(digits : d_kana) = 0
6.31 c                   eval      d_kann = %int(%trim(d_kana))
6.31 c**   ' ':'0'       xlate     d_kana        d_kana
     c                   eval      w_anta = w_anta + d_kann
7.00 c                   else
7.00 c                   eval      d_line = *blanks
7.00 c                   endif
     c                   elseif    fdline = 9001
     c                   eval      d_line = fdtek1
7.00 c                   if        %check(digits : d_kana) = 0
6.31 c                   eval      d_kann = %int(%trim(d_kana))
6.31 c**   ' ':'0'       xlate     d_kana        d_kana
     c                   eval      w_anta = w_anta + d_kann
7.00 c                   else
7.00 c                   eval      d_line = *blanks
7.00 c                   endif
     c                   elseif    fdline = 9002
     c                   eval      d_line = fdtek1
7.00 c                   if        %check(digits : d_kana) = 0
6.31 c                   eval      d_kann = %int(%trim(d_kana))
6.31 c**   ' ':'0'       xlate     d_kana        d_kana
     c                   eval      w_anta = w_anta + d_kann
7.00 c                   else
7.00 c                   eval      d_line = *blanks
7.00 c                   endif
     c                   endif
      *
     c     fodtlr_key2   reade     fodtlr
     c                   enddo
      *
      * Begynn å skrive til XML
      *
     c                   if        w_anta > 0
      *
     c                   dou       w_anta = x
      *
     c                   eval      x = x + 1
      *
     c                   if        b_first = *on
     c                   exsr      xml_env
7.01  * Vi logger videre
7.01 c                   if        b_debug = *on
7.01 c                   eval      w_jstat = 10
7.01 c                   eval      w_jtext = 'ENV variabler er skrevet'
7.01 c                   call      'AB705R'
7.01 c                   parm                    w_jkey
7.01 c                   parm                    w_jstat
7.01 c                   parm                    w_jtext
7.01 c                   endif
     c                   endif
      *
      * Legg ut etikettinfo
      *
     c                   exsr      xml_etiket
      *
     c                   enddo
      *
      * Skriv xml til IFS
      *
     c                   exsr      xml_end
      * Hvis forhåndvisning må vi kjøre linken
     c* problemer med LDAif        l_skje = 1
     c                   exsr      pdf_preview
     c*                  endif
      *
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av mal
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xml_env       begsr
      *
     c                   eval      w_xmlp = %trim(XML_path)
      *
     c                   callp     GetHTMLIFS(w_xmlp :'<AS400>')
     c                   callp     ClrHtmlBuffer
8.01  *
8.01 c                   eval      w_akop = 1
8.01 c                   if        %check(digits : l_akopa) = 0
8.01 c                   eval      w_akop = %int(%trim(l_akopa))
8.01 c                   endif
8.01 c                   if        w_akop = 0
8.01 c                   eval      w_akop = 1
8.01 c                   endif
      *
     c                   eval      b_first = *off
     c                   eval      w_spol = 'false'
      * Hent inn skuffinformasjon for skriveren
     c                   eval      w_draw1 = *blank
     c                   eval      w_draw2 = *blank
     c                   call      'AP611R'
7.02 c                   parm                    p_fprg
     c                   parm                    l_outq
     c                   parm                    w_draw1
     c                   parm                    w_draw2
      *
     c                   eval      w_land = 'LANDSCAPE'
      *
      /Free

           // Finne nytt batch nummer
               UpdHTMLVar('BatchNo':             w_jkey);
               UpdHTMLVar('Refbatchno':             ' ');
               UpdHTMLVar('Batchtype':  'PARCELSTICKER');
               UpdHTMLVar('Username':            l_user);

               test(te) *hms w_tidp;
               if %error;
               tmptime = *loval;
               else;
               tmptime = %time(w_tidp : *hms);
               endif;
               UpdHTMLVar('Batchtime': %char(tmptime : *hms));
               WrtSection('Topp');

           // Finn credential variabler
               UpdHTMLVar('IPadr':                  ' ');
               UpdHTMLVar('User':                l_user);
               UpdHTMLVar('PW':                     ' ');
7.02           UpdHTMLVar('Schema':        'ADB'+p_fprg);
               UpdHTMLVar('Companyno':    %char(l_firm));
               UpdHTMLVar('Departmentnoarkiv': %char(l_avde));
               WrtSection('Credential');

           // Finn report command variabler
               UpdHTMLVar('Logo':                   ' ');
               UpdHTMLVar('Keepspool':           w_spol);
               UpdHTMLVar('Jaspertemplate':      d_jasm);
               WrtSection('ReportCommand');

           // Finn print command variabler
               UpdHTMLVar('Printername':         l_outq);
          //   UpdHTMLVar('Copies':       %char(l_akop));
8.01      //   UpdHTMLVar('Copies':                 '1');
8.01           UpdHTMLVar('Copies':       %char(w_akop));
               UpdHTMLVar('Papersource':        w_draw1);
               UpdHTMLVar('Lastpagedrawer':     w_draw2);
               UpdHTMLVar('Duplex':             'false');
               UpdHTMLVar('Quality':                ' ');
               UpdHTMLVar('Sorting':                ' ');
               UpdHTMLVar('Staple':              'true');
               UPDHTMLVar('Alignment':           w_land);
               WrtSection('PrintCommand');

      /End-free
      *
      * Vi må oversette aktuelle tekster til "xml" tegn - Firma
      *
     c                   eval      w_fnav = *blank
     c                   eval      w_fad1 = *blank
     c                   eval      w_fad2 = *blank
     c                   eval      w_fste = *blank
6.30 c                   eval      w_tlfn = *blank
6.30 c                   eval      w_kprs = *blank
      *
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     aafnav        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_fnav
      *
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     aafad1        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_fad1
      *
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     aafad2        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_fad2
      *
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     aafste        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_fste
      *
      * Vi må oversette aktuelle tekster til "xml" tegn - Partner
      *
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     rknavn        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_knav
      *
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     rkgate        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_kgat
      *
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     rkadr2        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_kad2
      *
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     rksted        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_kste
      *
      * Vi må oversette aktuelle tekster til "xml" tegn - Leveringsadresse
      *
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     fonavn        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_lnav
      *
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     foadr1        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_lad1
      *
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     foadr2        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_lad2
      *
     c                   eval      p_str_in = *blank
     c                   eval      p_lr = '0'
     c                   movel     fosted        p_str_in
     c                   call      'AP613R'
     c                   parm                    p_str_in
     c                   parm                    p_str_out
     c                   parm                    p_lr
     c                   movel     p_str_out     w_lste
      *
     c                   if        foldat <> *loval
     c     *dmy          move      foldat        w_ldat
     c                   endif
      *
     c                   eval      w_retx = 'Kollietiketter'
6.30  *
6.30  * Finn ut av kontaktperson
6.30  *
6.30 c                   eval      p_str_in = *blank
6.30 c                   eval      p_lr = '0'
6.30  *
6.30 c                   if        fodref <> *blanks
6.30 c                   movel     fodref        p_str_in
6.30 c                   elseif    flkprs <> *blanks
6.30 c                   movel     flkprs        p_str_in
6.30 c                   elseif    rkpers <> *blanks
6.30 c                   movel     rkpers        p_str_in
6.30 c                   endif
6.30  *
6.30 c                   call      'AP613R'
6.30 c                   parm                    p_str_in
6.30 c                   parm                    p_str_out
6.30 c                   parm                    p_lr
6.30 c                   movel     p_str_out     w_kprs
6.30  *
6.30  * Finn ut av kontaktperson
6.30  *
6.30 c                   eval      p_str_in = *blank
6.30 c                   eval      p_lr = '0'
6.30  *
6.30 c                   if        fomobi <> *blanks
6.30 c                   movel     fomobi        p_str_in
6.30 c                   elseif    fltlfm <> *blanks
6.30 c                   movel     fltlfm        p_str_in
6.30 c                   elseif    rkmobn <> *blanks
6.30 c                   movel     rkmobn        p_str_in
6.30 c                   endif
6.30  *
6.30 c                   call      'AP613R'
6.30 c                   parm                    p_str_in
6.30 c                   parm                    p_str_out
6.30 c                   parm                    p_lr
6.30 c                   movel     p_str_out     w_tlfn
      *
      /Free

           // Skriv firmavariabler
               UpdHTMLVar('Reporttext':     %trim(w_retx));
               UpdHTMLVar('Subheader':                ' ');
               UpdHTMLVar('CompanyName':           w_fnav);
               UpdHTMLVar('Companyadress1':        w_fad1);
               UpdHTMLVar('Companyadress2':        w_fad2);
               UpdHTMLVar('Companypostalcode':        ' ');
               UpdHTMLVar('Companypostoffice':     w_fste);
               UpdHTMLVar('Companyphone':          aaftlf);
               UpdHTMLVar('Companyfax':            aaffax);
               UpdHTMLVar('Companyorgno':   %char(aafftn));
               UpdHTMLVar('CompanyGLN':               '0');
               UpdHTMLVar('Companygiro':    %char(aafbgi));
               UpdHTMLVar('Companyemail':   %trim(aamail));
               UpdHTMLVar('Companywebpage': %trim(aaweba));
               WrtSection('Company');

               UpdHTMLVar('Departmentno':              ' ');
               UpdHTMLVar('Departmentname':            ' ');
               UpdHTMLVar('Departmentadress1':         ' ');
               UpdHTMLVar('Departmentadress2':         ' ');
               UpdHTMLVar('Departmentpostalcode':      ' ');
               UpdHTMLVar('Departmentpostoffice':      ' ');
               UpdHTMLVar('Departmentphone':           ' ');
               UpdHTMLVar('Departmentfax':             ' ');
               WrtSection('Department');

               test(de) *dmy w_dato;
               if %error;
               tmpdate = *loval;
               else;
               tmpdate = %date(w_dato : *dmy);
               endif;
               UpdHTMLVar('Creationdate':   %char(tmpdate : *iso));

               test(te) *hms w_tidp;
               if %error;
               tmptime = *loval;
               else;
               tmptime = %time(w_tidp : *hms);
               endif;
               UpdHTMLVar('Creationtime': %char(tmptime : *hms));
               WrtSection('Creation');

           // Skriv partnerinfo (partner/kunde)
               UpdHTMLVar('Partnername':            w_knav);
               UpdHTMLVar('Partneradress1':         w_kgat);
               UpdHTMLVar('Partneradress2':         w_kad2);
               UpdHTMLVar('Partnerpostalcode':         ' ');
               UpdHTMLVar('Partnerpostoffice':      w_kste);
               UpdHTMLVar('Partnercellphone': %trim(rkmobn));
               WrtSection('Partner');

           // Skriv leveringsadresse
               UpdHTMLVar('Deliveryname':            w_lnav);
               UpdHTMLVar('Deliveryadress1':         w_lad1);
               UpdHTMLVar('Deliveryadress2':         w_lad2);
               UpdHTMLVar('Deliverypostalcode':         ' ');
               UpdHTMLVar('Deliverypostoffice':      w_lste);
6.30           UpdHTMLVar('Deliverycellphone': %trim(w_tlfn));
6.30           UpdHTMLVar('Contactperson':     %trim(w_kprs));
           // Div info

               test(de) *dmy w_ldat;
               if %error;
               tmpdate = *loval;
               else;
               tmpdate = %date(w_ldat : *dmy);
               endif;

               UpdHTMLVar('Deliverydate':   %char(tmpdate : *iso));
               UpdHTMLVar('Salesdocumentno':        %char(fonumm));
               UpdHTMLVar('Salesdocumentnosuffix':  %char(fosuff));
               UpdHTMLVar('Ordertype':                     footyp);
               UpdHTMLVar('Amountofstickers':       %char(w_anta));

               WrtSection('Delivery');
      /End-free
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegg av info
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xml_etiket    begsr
      *
      /Free
           // Skriv etikett info
               UpdHTMLVar('Sticker':               %char(x));
               UpdHTMLVar('Weight':                     '0');
               UpdHTMLVar('Length':                     '0');
               UpdHTMLVar('Height':                     '0');
               UpdHTMLVar('Width':                      '0');
               UpdHTMLVar('Volume':                     '0');
               WrtSection('Sticker');
      /End-free
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for avsluttning av XML
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xml_end       begsr
      *
      /Free
           // Skriv siste tagg
               WrtSection('Footer');

      /End-free
      *
      * Lag navn til xml fila
      *
7.01  * Vi logger videre
7.01 c                   if        b_debug = *on
7.01 c                   eval      w_jstat = 10
7.01 c                   eval      w_jtext = 'Legger den ut på IFS'
7.01 c                   call      'AB705R'
7.01 c                   parm                    w_jkey
7.01 c                   parm                    w_jstat
7.01 c                   parm                    w_jtext
7.01 c                   endif
7.01  * Vi logger videre
7.01 c                   if        b_debug = *on
7.01 c                   eval      w_jstat = 10
7.01 c                   eval      w_jtext = 'Path ' + %trim(xml_path)
7.01 c                   call      'AB705R'
7.01 c                   parm                    w_jkey
7.01 c                   parm                    w_jstat
7.01 c                   parm                    w_jtext
7.01 c                   endif
      *
7.02 c                   eval      XML_Output = %trim(XML_path) + p_fprg +'/'+
     c                             'Koletik_'+ %trim(w_jkey) + '.xml'
      *
7.01  * Vi logger videre
7.01 c                   if        b_debug = *on
7.01 c                   eval      w_jstat = 10
7.01 c                   eval      w_jtext = %trim(XML_Output)
7.01 c                   call      'AB705R'
7.01 c                   parm                    w_jkey
7.01 c                   parm                    w_jstat
7.01 c                   parm                    w_jtext
7.01 c                   endif
      *
     c                   callp     WrtHtmlToStmf(XML_Output: 819)
      * Skriver til datakø for å aktivere sniffer, hvis dataq er aktiv
     c                   if        d_dtaq = '1'
     c                   eval      p_xmlf = xml_output
7.02 c**                 eval      p_filg = 'ADB'+l_filg
7.02 c                   eval      p_filg = 'ADB'+p_fprg
     c                   call      'AP629C'
     c                   parm                    p_filg
     c                   parm                    p_xmlf
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutiner for XML/jasper integrasjon - Skjermvisning           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
     c     pdf_preview   begsr
      *
      * Vi kaller program for launch av PDF i browser
     c**                 call      'AP621R'
     c**                 parm                    w_jkey
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
7.02 c                   parm                    p_fprg
      *
      * Key for oppslag på ordrehodet
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for oppslag på ordrelinjer
      *
     c     fodtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlr_numm
     c                   kfld                    fodtlr_suff
     c                   kfld                    fodtlr_line
      *
      * Key for oppslag på ordrelinjer
      *
     c     fodtlr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlr_numm
     c                   kfld                    fodtlr_suff
      *
      * Key for oppslag på RKUN
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
6.30  *
6.30  * Key for oppslag på kundeprosjekt
6.30  *
6.30 c     fkprl1_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    fkprl1_kund
6.30 c                   kfld                    fkprl1_kpro
      *
      * Key for firmaregister
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
     c                   eval      w_firm = l_firm
     c                   eval      fohel1_numm = p_numm
     c                   eval      fohel1_suff = p_suff
     c                   eval      fodtlr_numm = p_numm
     c                   eval      fodtlr_suff = p_suff
      *
     c                   eval      b_pdfp = *off
     c                   eval      w_pdf = 0
     c                   eval      w_pdf_ds = *blanks
     c                   eval      w_ruti = 'FP270'
      *
7.02 c**                 call      'AP609R'
7.02 c                   call      'AP607R'
7.02 c                   parm                    p_fprg
     c                   parm                    w_ruti
     c                   parm                    w_pdf
     c                   parm                    w_pdf_ds
     c                   eval      d_pdf_ds = w_pdf_ds
      * Vi skal kjøre med proa/iReport
     c                   if        w_pdf = 1 and
     c                             %trim(d_xmlm) <> *blanks
      *
      * Hent inn xml templaten
      *
     c                   callp     GetHTMLIFS(d_xmlm :'<AS400>')
     c                   callp     ClrHtmlBuffer
      *
      * Finn hvor xml'en skal legges
      *
     c                   eval      XML_path = *blanks
     c                   eval      XML_path = %trim(d_path)
      *
      * Finn path til logo
      *
      * Lager nytt batchnummer
      *
     c                   eval      w_kode = '0'
     c                   eval      w_firm = l_firm
     c                   eval      w_fell = 'NXTKLI'
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
     c                   eval      w_numm = *zero
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_numm
      *
     c                   eval      w_jnav = 'PDF' + %char(w_numm)
     c                   eval      w_jtxt = 'Utskrift av kollietiketter'
     c                   call      'AB700R'
     c                   parm                    w_jnav
     c                   parm                    w_jkey
     c                   parm                    w_jtxt
      * Vi logger ar utskriftprogrammet har startet
     c                   eval      w_jstat = 10
7.01 c                   eval      w_jtext = 'Utskrift av kollietiketter-FP272'
     c                   call      'AB705R'
     c                   parm                    w_jkey
     c                   parm                    w_jstat
     c                   parm                    w_jtext
7.01  * Vi logger videre
7.01 c                   if        b_debug = *on
7.01 c                   eval      w_jstat = 10
7.01 c                   eval      w_jtext = 'Firma ' + %char(p_firm) +
7.01 c                             ' Ordrenr: ' + %char(p_numm) +
7.01 c                             ' Suff: ' + %char(p_suff)
7.01 c                   call      'AB705R'
7.01 c                   parm                    w_jkey
7.01 c                   parm                    w_jstat
7.01 c                   parm                    w_jtext
7.01 c                   endif
7.01  * Vi logger videre
7.01 c                   if        b_debug = *on
7.01 c                   eval      w_jstat = 10
7.01 c                   eval      w_jtext = 'Filgruppe: '+ %trim(p_fprg)
7.01 c                   call      'AB705R'
7.01 c                   parm                    w_jkey
7.01 c                   parm                    w_jstat
7.01 c                   parm                    w_jtext
7.01 c                   endif
      *
     c                   eval      b_pdfp = *on
      * Hvis et eller annet er galt med parametrene, logges og kjør uten PROA
     c                   if        %trim(d_xmlm) = *blanks
     c                   eval      b_pdfp = *off
      * Vi logger at noe er galt med parametrene
     c                   eval      w_jstat = 20
     c                   eval      w_jtext = 'Finner ikke path til xml !!! +
     c                             Sjekk rutineregister !'
     c                   call      'AB705R'
     c                   parm                    w_jkey
     c                   parm                    w_jstat
     c                   parm                    w_jtext
     c                   endif
      *
     c                   else
     c                   eval      b_pdfp = *off
     c                   endif
      *
     c                   eval      b_first = *on
      *
     c     afirl1_key    chain     afirl1r                            66
      *
     c                   time                    w_date
     c                   time                    w_time
     c     *dmy          move      w_date        w_dato
     c     *hms          move      w_time        w_tidp
      *
      *
     c                   endsr
