     h option(*nodebugio) datedit(*dmy)
     h dftactgrp(*no)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : VG715R
      * Beskrivelse . . : Oppdaterer salgsstatistikk med gj.sn. kostpris
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       180123 ASK  Nytt program
      *       250723 ask  "Mestergruppen versjon" er lagt inn
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsstal1    uf   e           k disk    rename(sstapfr:sstal1r)
     fvvarlr    if   e           k disk    rename(vvarpfr:vvarlrr)
     fvvenlr    if   e           k disk    rename(vvenpfr:vvenlrr)
     fvg715d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d sstal1_aarr     s                   like(sfpaar)
     d vvarlr_vare     s                   like(vvvare)
     d vvenlr_vare     s                   like(vevare)
     d vvenlr_enhe     s                   like(veenhe)
      *
      * Definering av variable
      *
     d w_firm          s                   like(sffirm)
     d w_gjdat         s              8  0
     d w_gjko          s              9  2
     d w_gjkp          s             13  6
     d w_inlr          s              1
     d b_omre          s              1    inz(*off)
      *
      * HOVEDPROGRAM
      *
      * Behandling av parameterbilde - sjekk gyldige valg
      *
     c/exec sql
     c+ SET OPTION DatFmt=*ISO,
     c+            SRTSEQ=*LANGIDUNQ,
     c+            LANGID=NOR,
     c+            commit=*none
     c/end-exec
      *
      * HOVEDPROGRAM
      *
      * Behandling av parameterbilde - sjekk gyldige valg
      *
     c                   eval      c2erst = 'N'
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl
     c                   goto      avslutt
     c                   endif
      *
      * Sjekk startår
      *
     c                   if        c2saar = 0
     c                   eval      *in41 = *on
     c                   exsr      xc1msg
     c                   goto      c2taga
     c                   endif
      *
      * Sjekk erstatningskode
      *
     c                   if        c2erst <> 'J' and
     c                             c2erst <> 'N'
     c                   eval      *in42 = *on
     c                   exsr      xc1msg
     c                   goto      c2taga
     c                   endif
     c                   eval      w_inlr = ' '
      *
      * Leser i gjennom salgsstatistikk fra startår
      *
     c                   eval      sstal1_aarr = c2saar
     c     sstal1_key    setll     sstal1
      *
     c                   read      sstal1
     c                   dow       not %eof
      *
      * Sjekk om bare et artikkelnummer skal behandles
      *
     c                   if        sfgjkp > 0 and
     c                             c2erst = 'N'
     c                   goto      les_neste
     c                   endif
      *
      * Sjekk om allerede utfylt kostpris skal overstyres
      *
     c                   if        c2artn <> ' ' and
     c                             c2artn <> sfvare
     c                   goto      les_neste
     c                   endif
      *
      * Skal bare noen lager inkluderes?
      *
     c                   if        c2lag0 = 0
     c                   if        c2lagu = 0
     c                   if        sflage <> c2lag1 and
     c                             sflage <> c2lag2 and
     c                             sflage <> c2lag3 and
     c                             sflage <> c2lag4 and
     c                             sflage <> c2lag5 and
     c                             sflage <> c2lag6 and
     c                             sflage <> c2lag7 and
     c                             sflage <> c2lag8 and
     c                             sflage <> c2lag9
     c                   goto      les_neste
     c                   endif
     c                   endif
     c                   endif
      *
      * Skal noen lager ekskluderes?
      *
     c                   if        c2lag0 = 0
     c                   if        c2lagu = 1
     c                   if        sflage = c2lag1 or
     c                             sflage = c2lag2 or
     c                             sflage = c2lag3 or
     c                             sflage = c2lag4 or
     c                             sflage = c2lag5 or
     c                             sflage = c2lag6 or
     c                             sflage = c2lag7 or
     c                             sflage = c2lag8 or
     c                             sflage = c2lag9
     c                   goto      les_neste
     c                   endif
     c                   endif
     c                   endif
      *
      * Skal bare noen ordretyper inkluderes?
      *
     c                   if        c2oty0 = 0
     c                   if        c2otyu = 0
     c                   if        sfotyp <> c2oty1 and
     c                             sfotyp <> c2oty2 and
     c                             sfotyp <> c2oty3 and
     c                             sfotyp <> c2oty4 and
     c                             sfotyp <> c2oty5 and
     c                             sfotyp <> c2oty6 and
     c                             sfotyp <> c2oty7 and
     c                             sfotyp <> c2oty8 and
     c                             sfotyp <> c2oty9
     c                   goto      les_neste
     c                   endif
     c                   endif
     c                   endif
      *
      * Skal noen ordretyper ekskluderes?
      *
     c                   if        c2oty0 = 0

     c                   if        c2otyu = 1
     c                   if        sfotyp = c2oty1 or
     c                             sfotyp = c2oty2 or
     c                             sfotyp = c2oty3 or
     c                             sfotyp = c2oty4 or
     c                             sfotyp = c2oty5 or
     c                             sfotyp = c2oty6 or
     c                             sfotyp = c2oty7 or
     c                             sfotyp = c2oty8 or
     c                             sfotyp = c2oty9
     c                   goto      les_neste
     c                   endif
     c                   endif
     c                   endif
      *
      * Henter vareinformasjon
      *
     c                   eval      vvarlr_vare = sfvare
     c     vvarlr_key    chain     vvarlr
     c                   if        not %found(vvarlr)
     c                   eval      sfgjkp = sfsumk
     c                   goto      oppd_kost
     c                   endif
      *
      * Bruk kalk kost på noen leveringstyper og vare typer
      *
     c                   if        sflety = 1 or
     c                             sflety = 5 or
     c                             sflety = 8
     c                   eval      sfgjkp = sfsumk
     c                   goto      oppd_kost
     c                   endif
      *
     c                   if        sfvtyp = 'S' or
     c                             sfvtyp = 'T' or
     c                             sfvtyp = 'D'
     c                   eval      sfgjkp = sfsumk
     c                   goto      oppd_kost
     c                   endif
      *
      * Henter gj.sn. kostpris på utleveringstidspunkt
      *
     c                   exsr      hent_gjkost
     c                   if        w_gjkp = 0
     c                   eval      sfgjkp = sfsumk
     c                   goto      oppd_kost
     c                   endif
      *
      * Sjekker om statistikk enhet avviker lagerenhet, regn om til statistikk enhet
      *
     c                   if        vvenhs <> vvenhl
     c                   exsr      beregn_pris
     c                   endif
     c                   if        w_gjkp > 0
     c                   eval(h)   sfgjkp = w_gjkp * sfanta
     c                   else
     c                   eval      sfgjkp = sfsumk
     c                   endif
      *
      * Oppdater med glidende gj.sn. kostpris på statistikk
      *
     c     oppd_kost     tag
     c                   update    sstal1r
      *
     c     les_neste     tag
      *
     c                   read      sstal1
     c                   enddo
      *
      * Kjører en siste runde for å lukke tabeller
      *
     c                   eval      w_inlr = '1'
     c                   call      'VG711R'
     c                   parm                    sfvare
     c                   parm                    sflage
     c                   parm                    w_gjdat
     c                   parm                    w_gjko
     c                   parm                    vvenhl
     c                   parm                    w_inlr
      *
     c                   goto      c2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for feilmeldinger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter gj.sn. kostpris på utleverings tidspunkt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
       begsr hent_gjkost;

          if sforda <> *loval;
             w_gjdat = %dec(sforda:*iso);
          endif;

          if sfpdat <> *loval and
             sfpdat >= sforda;
             w_gjdat = %dec(sfpdat:*iso);
          endif;

          if sfldat <> *loval and
             sfldat >= sforda;
             w_gjdat = %dec(sfldat:*iso);
          endif;

          if sfpada <> *loval and
             sfpada >= sforda;
             w_gjdat = %dec(sfpada:*iso);
          endif;

          w_gjko = 0;

          if w_gjdat > 0;
     c                   call      'VG711R'
     c                   parm                    sfvare
     c                   parm                    sflage
     c                   parm                    w_gjdat
     c                   parm                    w_gjko
     c                   parm                    vvenhl
     c                   parm                    w_inlr
      *
           w_gjkp = w_gjko;

          endif;

       endsr;
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for beregning av glidende kostpris i statistikk enhet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beregn_pris   begsr
      *
      * Finn omregningsfaktor på statistikk enhet og beregn priser i forhold til basisenhet
      *
     c                   eval      b_omre = *off
     c                   eval      vvenlr_vare = sfvare
     c                   eval      vvenlr_enhe = vvenhs
     c     vvenlr_key    chain     vvenlr
     c                   if        %found(vvenlr)
     c                   eval(h)   w_gjkp = w_gjkp * veomre
     c                   eval      b_omre = *on
     c                   endif
      *
      * Finn omregningsfaktor på lagerenhet og beregn priser dersom lagerenhet er ulik basisenhet
      *
     c                   if        b_omre = *on
     c                   if        vvenhe <> vvenhl
     c                   eval      vvenlr_enhe = vvenhl
     c     vvenlr_key    chain     vvenlr
     c                   if        %found(vvenlr)
     c                   eval(h)   w_gjkp = w_gjkp / veomre
     c                   else
     c                   eval      b_omre = *off
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for kostpris parameter
      *
     c     vgkkir_key    klist
     c                   kfld                    w_firm
      *
      * Key for les av salgsstatistikk
      *
     c     sstal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sstal1_aarr
      *
      *  Key for oppdslag på vare
      *
     c     vvarlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlr_vare
      *
      *  Key for oppdslag på vare/enhet
      *
     c     vvenlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenlr_vare
     c                   kfld                    vvenlr_enhe
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
