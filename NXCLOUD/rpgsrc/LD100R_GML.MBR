     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LD100R
      * Beskrivelse . . : Bestilling-linje, registrering av best/innkjøp
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       191102 HKO  Skrevet om programmet
      * 6.10  170609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  130709 bof  Utvidet eannr til 14 og brukt std. oppslag
      * 6.10  220909 ASK  Nye parametre til VL001R
6.12  * 6.10  081009 BSA  Legger valutakoden ut i heading
6.13  * 6.10  300610 BSA  Valg for "Send bestilling". Automatisk FTP
6.13  *                   rutiner hvis mulig. Hvis ikke overstyres til
6.13  *                   valg 1 (utskrift)
6.15  * 6.10  071110 bof  Sjekke at varen er lagervare
6.16  * 6.10  090611 bof  Justert på referanse
6.17  * 6.10  030715 bkv  Fiks på 6.15
6.20  * V6.20 280410 bof  Gir melding hvis kostpris er endret på ordre
6.21  * V6.20 280511 bsa  Sjekker om synkronisering mot ordre.
6.22  * 6.20  190511 bø   Utvidet vl710r med div. info
6.23  * 6.20  310511 bsa  Egen funksjon for innhenting av kampanjevarer
6.24  * 6.20  081111 bof  Utvidet med parameter kall til LO740r
6.25  * 6.20  081111 bsa  Kan ikke oppdatere hvis ikke enhetene er like
6.26  * 6.20  030112 bsa  Utvidet kontroll rundt "Hold" av ordre
6.27  * 6.20  090112 bsa  Kan ikke gjøre endringer hvis ordretypen tilsier
6.27  *                   at vi ikke lenger jobber med opprinnelig ordre/best
6.28  * 6.20  100112 bsa  Sjekk at det er endringer før vi sender ut info om
6.28  *                   e-posten.
6.29  * 6.20  100112 bsa  Logg også endringer på kostpris
6.2A  * 6.20  110112 bsa  Utvidet kall til LD105 med kampanje
6.2b  * 6.20  070312 bsa  Kan ikke synkronisere ordre, hvis intern ordretype
6.2b  *                   Dvs at den ligger inne på leverandør.
6.2c  * 6.20  220312 bsa  Dato for når synkronisering kan skje. Gjelder alle
6.2c  *                   endringer på linja. Sjekker mot opprettetdato på
6.2c  *                   bestillingen.
6.2d  * 6.20  270312 bsa  Send over kostpris også til FO701R, hvis ikke kan
6.2d  *                   kostpris bli feil på nytt.
6.2e  * 6.20  190412 bsa  Feil program benyttes for synking av ordrelinjer
6.2f  * 6.20  230113 bsa  Hvis F3 benyttet i FO798, skal det ikke sendes mail
6.2g  * 6.20  131113 bsa  Endrer sjekk på om ordre er ibruk.
6.2h  * 6.20  050814 bof  Hvis linjen slettes, fjernes referanse til evt. ordrelinje
6.2i  * 6.20  080515 nho  Feilmelding dersom vare er utgått og det på lager ligger
6.2j  * 6.20  241115 nho  Endret editering av antallsfelt
      *                   utgått dato frem i tid
6.nu  * 6.30  260514 bof  utvidelse ihht. nummerutvidelse
6.30  * 6.30  300914 bkv  F14 vis leverandør info
6.31  * R6.30 120914 bsa  Utvidet parameterliste til FO798R. Håndtering av
6.31  *                   flere mailfelter.
6.32  * 6.30  040615 bkv  Bestilling økt fra 6 til 8
6.33  * 6.30  170216 bsa  Linjenummer 24 kan ikke tas i bruk til F-taster. Det vil gi
6.33  *                   JIT Gui ved feilmedlinger.
6.34  * R6.30 100616 bsa  Kaller cl for å overstyre printerfile LO581P
6.34  *                   Virker som den kommer ut på skriver.
6.35  * R6.30 100816 bsa  Sjekk om det finnes linjer på bestillingen, før
6.35  *                   retur til LO110R. I spesielle tilfeller kan
6.35  *                   bestillingen være endret fra en annen skjerm.
6.36  * 6.30  170918 bkv  Utvidet med trelast sortiment
6.37  * 6.30  100719 bsa  Disablet F23 i skjermbildet, siden den ikke virker.
      *
7.xx  * 7.00  030316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
Q.01  * 7.00  270319 bsa  Mulighet for å bruke 27x132 størrelse
Q.02  * 7.00  270319 bsa  Quick wins - Lagrer bestilling uten spm, og går ut.
Q.02  *                   Aktiverer F5 funksjon.
7.01  * R7.00 010419 bkv  Varefeltet skal fungere som varesøk, hvis
7.01  *                   eksakt vare ikke finnes.
7.02  * 7.00  010419 bkv  Sjekk om OA
7.03  *       130919 bkv  Korrigerer vising av desimal tall
7.04  *       061119 bsa  Skal ikke kunne plukkes hvis elektronisk pakkseddel
7.04  *                   er mottatt.
7.05  * 7.00  110919 ask  Lagt inn "hurtigknapper" på Utskrift og send bestilling.
7.06  * 7.00  200120 bsa  Kostpris på bestillinga skal ikke påvirke ordren. Skattum
7.07  * 7.00  200120 bkv  Tatt vekk OA sjekk
7.08  * R6.30 240120 bsa  Sjekker om bestillingslinjene er ihht NEB
7.08  *                   standard. Sjekker kun hvis NEB godkjent leverandør  (Eiker)
7.09  * R6.30 240120 bsa  Sjekker kun om det er ordretype tilsvarende bestilling "B"
7.09  *                   Gis mulighet for å overstyre.                       (Eiker)
7.10  * K000  210420 bsa  Tilpasset bredere skjerm. Legger også ut info om vekt og volum
7.11  * K000  270420 bsa  Gjort om til tabellarisk oversikt i subfile.
7.12  * K000  180520 bsa  Default valg ved F3 fra bestilling.
7.13  * K000  240620 bsa  Vasker bort tegn, som kan ødelegge "option" kolonna.
7.14  * K000  250620 bsa  Legger ut sum kost for hele bestillingen.
7.16  * K00   020720 bof  Utvidet felt for vekt
7.17  * K00   210820 bof  NewLook vask for felt i subfile
7.18  * K00   111120 bof  Ekstra test før utregning av rabatt
7.19  * K00   120120 ask  Lagt inn visning av aktuell valutakurs
7.20  * K00   160321 bof  Test på vekt før det legges ut i bildet
7.23  * 7.13  120520 bsa  Ta med kode om nyopprettet eller endring til
7.23  * 7.13              vedlikehold av "faste opplysninger".
      *
8.01  * 800   290421 ask  Lagt inn mer valutainformasjon og indikatorstyring
8.01  * 800               om valutainformasjon skal vises
8.02  * 800   021123 bsa  Jsutert sjekk mot dellevering.
8.03  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarl4    if   e           k disk    rename(vvarpfr:vvarl4r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     flusrl1    if   e           k disk    rename(lusrpfr:lusrl1r)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flohelu    uf   e           k disk    rename(lohepfr:lohelur)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     flodtlr    if   e           k disk    rename(lodtpfr:lodtlrr)
     flodtlu    uf   e           k disk    rename(lodtpfr:lodtlur)
6.13 flldilr    if   e           k disk    rename(lldipfr:lldilrr)
6.21 flsbol1    if   e           k disk    rename(lsbopfr:lsbolrr)
6.21 ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
6.21 ffohelu    uf   e           k disk    rename(fohepfr:fohelur)
6.21 ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
6.21 ffodtlr    if   e           k disk    rename(fodtpfr:fodtlrr)
6.2h ffodtlu    uf   e           k disk    rename(fodtpfr:fodtlur)
6.21 ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
6.21 ffwbolu    uf a e           k disk    rename(fwbopfr:fwbolur)
6.28 ffwbolr    if   e           k disk    rename(fwbopfr:fwbolrr)
6.36 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
6.21 fra09lr    if   e           k disk    rename(ra09pfr:ra09lrr)
7.04 flbhoi4    if   e           k disk    rename(lbhost:lbhoi4r)
7.08 flldil1    if   e           k disk    rename(lldipfr:lldil1r)
7.10 fvvasl1    if   e           k disk    rename(vvaspfr:vvasl1r)
7.10 fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
7.10 fjvpkl2    if   e           k disk    rename(jvpkpfr:jvpkl2r)
      *
     fld100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local Data Arae (LDA)
      *
6.21 dlda             uds
6.21 d l_arch                636    636  0
6.21 d l_skje                637    637  0
6.21 d l_mail                640    640  0
     d l_user                911    920
7.00 d l_filg                931    933
     d l_fnav                951    980
6.20  *
6.20  * Definering av array
6.20  *
6.20 d a_meld          s             50    dim(1) ctdata perrcd(1)
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
7.07 d* ProgStatus     sds
7.07 d* CurPgmNam               1     10
7.07 d* CurPgmLib              81     90
      *
      * Datastrukturer
      *
      * Datastruktur for Ean128 - kode
      *
     d                 ds
     d d_e128                        26
6.11 d  d_fast                        2  0 overlay(d_e128)
6.11 d  d_eann                       14  0 overlay(d_e128:3)
     d  d_kode                        4  0 overlay(d_e128:17)
     d  d_anta                        6  2 overlay(d_e128:21)
      *
      * Datastruktur for lageroppdatering
      *
     d                 ds
6.nu d hlrec                        128
     d  hlvare                       15    overlay(hlrec)
     d  hllage                        2  0 overlay(hlrec:16)
     d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
     d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
     d  hlotyp                        2    overlay(hlrec:36)
     d  hlltyp                        2    overlay(hlrec:38)
     d  hlanta                       11  3 overlay(hlrec:40)
     d  hlkopr                        9  2 overlay(hlrec:51)
     d  hlrefr                       20    overlay(hlrec:60)
     d  hlsyst                        1    overlay(hlrec:80)
     d  hlaltk                        2    overlay(hlrec:81)
     d  hlrest                        1    overlay(hlrec:83)
     d  hltype                        1    overlay(hlrec:84)
     d  hlenhe                        3    overlay(hlrec:85)
     d  hlinlr                        1    overlay(hlrec:88)
     d  hllety                        1    overlay(hlrec:89)
6.nu d  hlonum                        8  0 overlay(hlrec:90)
6.nu d  hlolin                        4  0 overlay(hlrec:98)
      *
      * Parametre
      *
     d p_stat          s              1
     d p_firm          s              3  0
6.nu d p_numm          s                   like(lonumm)
     d p_suff          s              2  0
6.20 d p_orde          s              1
7.01 d p_vare          s             35    inz(*blanks)
6.21 d p_emal          s             50
6.21 d p_ema2          s             50
6.31 d p_ema3          s             50
6.31 d p_kopi          s             50
6.21 d p_emne          s             50
6.21 d p_txt1          s             50
6.21 d p_txt2          s             50
6.21 d p_txt3          s             50
6.21 d p_txt4          s             50
6.21 d p_pers          s             50
6.21 d p_inif          s             40
6.21 d p_in03          s              1
6.21 d p_kule          s              1
6.21 d p_kpro          s              6  0
6.21 d p_kund          s              6  0
6.21 d p_navn          s             30
6.21 d p_selg          s              4  0
6.21 d p_kode          s              1
6.21 d p_acok          s              1
6.21 d p_line          s                   like(ldline)
6.23 d p_kamp          s                   like(lokamp)
6.2i d w_ddat          s               d   datfmt(*iso)
7.08 d w_bsts          s              1
7.23 d p_nybe          s              1
      *
      * Definering av variabler i nøkler
      *
     d rlevl1_levr     s                   like(rllevr)
     d vvarl1_vare     s                   like(vvvare)
7.10 d vvasl1_vare     s                   like(vvvare)
7.10 d vvenl1_vare     s                   like(vevare)
7.10 d vvenl1_enhe     s                   like(veenhe)
     d vvarl4_lvar     s                   like(vvlvar)
     d vltyl1_ltyp     s                   like(valtyp)
     d votyl1_otyp     s                   like(vaotyp)
     d lusrl1_user     s                   like(lbuser)
     d lodtl1_line     s                   like(ldline)
     d lodtlr_line     s                   like(ldline)
     d lodtlu_line     s                   like(ldline)
6.13 d lldilr_ldor     s                   like(lpldor)
6.13 d lldilr_lage     s                   like(lplage)
6.21 d fohel1_numm     s                   like(fonumm)
6.27 d fohell_numm     s                   like(fonumm)
6.21 d fohel1_suff     s                   like(fosuff)
6.21 d fodtl1_numm     s                   like(fdnumm)
6.21 d fodtl1_suff     s                   like(fdsuff)
6.21 d fodtl1_line     s                   like(fdline)
6.21 d fodtlr_numm     s                   like(fdnumm)
6.21 d fodtlr_suff     s                   like(fdsuff)
6.21 d fodtlr_line     s                   like(fdline)
6.2h d fodtlu_numm     s                   like(fdnumm)
6.2h d fodtlu_suff     s                   like(fdsuff)
6.2h d fodtlu_line     s                   like(fdline)
6.21 d fwbolu_numm     s                   like(wfnumm)
6.21 d fwbolu_suff     s                   like(wfsuff)
6.21 d fwbolu_line     s                   like(wfline)
6.21 d fwbolu_type     s                   like(wftype)
6.21 d ra09lr_ikod     s                   like(raikod)
6.28 d fwbolr_numm     s                   like(wfnumm)
6.28 d fwbolr_suff     s                   like(wfsuff)
7.04 d lbhoi4_hnum     s                   like(pbhnum)
7.04 d lbhoi4_hsuf     s                   like(pbhsuf)
7.08 d lldil1_ldor     s                   like(lpldor)
7.08 d lldil1_lage     s                   like(lplage)
7.10 d jvpkl2_vare     s                   like(jwvare)
7.10 d jvpkl2_ldor     s                   like(jwldor)
7.10 d jvpkl2_enhe     s                   like(jwenhe)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d b_oppr          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_kost          s              1    inz(*off)
     d b_text          s              1    inz(*off)
6.20 d b_orde          s              1
6.21 d b_synk          s              1    inz(*off)
6.21 d b_updo          s              1    inz(*off)
6.21 d b_prtl          s              1    inz(*off)
6.21 d b_newl          s              1    inz(*off)
6.27 d b_otyp          s              1    inz(*off)
6.28 d b_logg          s              1    inz(*off)
7.08 d b_blin          s              1    inz(*off)
      *
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
     d w_stat          s              1
     d w_svar          s              1  0
     d w_enor          s              1
     d w_leng          s              2  0
     d w_posi          s              2  0
     d w_totk          s             11  2
     d w_toti          s             11  2
     d w_totr          s             11  2
6.11 d w_eanx          s             14
     d w_type          s              4
6.20 d w_mld1          s             50
7.04 d w_mld2          s             50
7.04 d w_mld3          s             50
6.21 d w_anta          s                   like(ldanta)
6.21 d w_inkp          s                   like(ldipny)
6.29 d w_kopr          s                   like(ldipny)
6.21 d w_csta          s              1
7.09 d w_repl          s              1
7.09 d w_in12          s              1  0
7.10 d w_vekt          s              9  3
7.10 d w_volu          s              9  3
7.10 d w_avvi          s              1
7.23 d w_nybe          s              1
7.12 d w_valg          s              1  0
7.16 d w_vek1          s             10  3
7.14 d w_vol1          s              9  3
7.13 d w_len           s              3  0
8.01 d w_kurs          s             10  6
      *
     d w_firm          s                   like(vvfirm)
     d w_numm          s                   like(lonumm)
     d w_suff          s                   like(losuff)
     d w_line          s                   like(ldline)
     d w_enhe          s                   like(ldenh1)
     d w_vare          s                   like(ldvare)
     d w_lety          s                   like(ldlety)
6.11 d w_eann          s             14  0
6.11 d w_eava          s                   like(vvvare)
6.11 d w_eaen          s                   like(vvenhe)
6.11 d w_east          s              1
6.15 d w_vtyp          s                   like(vvvtyp)
6.32 d w_numa          s              8
6.16 d w_lina          s              4
6.21 d w_tek1          s                   like(wftek1)
6.22 d w_utda          s                   like(vvudat)
6.22 d w_ldor          s                   like(vvldor)
6.22 d b_inlr          s              1    inz(*off)
6.24 d w_retk          s              1
6.36 d w_lv_nobb       s                   like(vvlnob)
6.36 d w_lv_modn       s                   like(vvlmod)
6.36 d w_lv_lldo       s                   like(vvlnle)
6.36 d w_lv_llva       s                   like(vvllva)
6.36 d w_lv_vare       s                   like(vvlvar)
7.17 d w_teller        s              3  0
7.07 d* w_oa_on         s              2  0 inz(0)
7.07 d* w_oa_on1        s              2  0 inz(0)
7.07 d* w_oa_on2        s              2  0 inz(0)
7.07 d* w_oa_on3        s              2  0 inz(0)
7.07 d* w_oa_on4        s              2  0 inz(0)
7.06 d u_706           s              1    inz(*off)
7.00  *
7.00  * Definering av eksterne prg
7.00  *
7.00   dcl-s co402_verdi1  char(1);
7.00   dcl-s co402_verdi2  char(2048);
7.00   DCL-PR CO402R EXTPGM('CO402R');
7.00     p_filgrp            char(3)     const;
7.00     p_firm              packed(3:0) const;
7.00     p_lager             packed(2:0) const;
7.00     p_nokkel            char(50)    const;
7.00     p_verdi1            char(1);
7.00     p_verdi2            char(2048);
7.00   END-PR;
      *
      * Definering av konstanter
      *
7.10 d c_sfil          s              2  0 inz(12)
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
     d digit           c                   '0123456789 '
     d null            c                   '0000000000000'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * D1 Behandle åpningsopplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.36 C/Exec SQL
6.36 C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
6.36 C/End-Exec
     c
7.07   //   *in48 = *on;
7.07   //  // det er forskjellige navn på OA bibliotek
7.07   //  // OA er std 700
7.07   //  // GUIO er for XL
7.07   //  // ASKUNDxxxO skal være std for kundebibl
7.07   //   w_oa_on = 0;
7.07   //   w_oa_on1 = %scan ('OA' : CurPgmLib);
7.07   //   w_oa_on2 = %scan ('GUIO' : CurPgmLib);
7.07   //   if ( %len(%trim(CurPgmLib)) = 10);
7.07   //     if (%subst(CurPgmLib:1:6) = 'ASKUND');
7.07   //       if (%subst(CurPgmLib:10:1) = 'O');
7.07   //         w_oa_on3 =1 ;
7.07   //       endif;
7.07   //     endif;
7.07   //   endif;
7.07   //   if ((w_oa_on1<>0) or (w_oa_on2<>0) or (w_oa_on3<>0)
7.07   //                or (w_oa_on4<>0));
7.07   //     w_oa_on = 1;
7.07   //     *in48 = *on;
7.07   //   endif;
     c
     c     d1tag         tag
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   eval      lodtl1_line = b1line
     c                   else
     c                   eval      lodtl1_line = 0
     c                   endif
      *
     c     lodtl1_key    setll     lodtl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Hent opplysninger fra bestilling
      *
     c                   exsr      hent_best
      *
      * Hent sist brukte linjenummer og legg inn i teller
      *
     c                   exsr      hent_linjenr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * D1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     d1taga        tag
      *
      * Blanker variable som benyttes i bildet
      *
     c                   eval      b2ltyp = laalty
     c                   eval      b2anta = 0
     c                   eval      b2vare = *blank
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   eval      *in22 = *on
      *
7.11 c*                  eval      b2over = *blank
7.11  *
7.11 c*                  if        *in70 = *off
7.11 c*                  if        *in71 = *on
7.11 c*                  eval      b2over = 'Nobbnr   Varetekst' +
7.11 c*                                     '                          ' +
7.11 c*                                     'Antall   Enh Innkj.pris ' +
7.11 c*                                     'Rabatt'
7.11 c*                  else
7.11 c*                  eval      b2over = 'Varenr   Varetekst' +
7.11 c*                                     '                          ' +
7.11 c*                                     'Antall   Enh Innkj.pris ' +
7.11 c*                                     'Rabatt'
7.11 c*                  endif
7.11 c*                  else
7.11 c*                  eval      b2over = 'Varetekst' +
7.11 c*                                     '                    ' +
7.11 c*                                     '               ' +
7.11 c*                                     'Sum netto      Sum kost'
7.11 c*                  endif
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
     c                   eval      *in22 = *off
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * F1=Vis totaler
      *
     c                   if        *inka = *on
     c                   if        *in70 = *off
     c                   eval      *in70 = *on
     c                   else
     c                   eval      *in70 = *off
     c                   endif
     c                   exsr      forny
     c                   goto      b2taga
     c                   endif
      *
      * F2=Sett inn tekstlinje
      *
     c                   if        *inkb = *on
     c                   exsr      hent_linjenr
     c                   eval      w_line = w_line + 10
     c                   exsr      tekstlinje
     c                   exsr      oppdat_side
     c                   goto      d1taga
     c                   endif
      *
      * F3=Starter subrutine for oppdatering/utskrift/overføring
      *
     c                   if        *inkc = *on
7.09  * Sjekk om vi skal teste om linjene er ihht NEB standard.
7.09  * NB Kun hvis akkumulatortype 1
7.09 c                   if        vaoakk = 1
7.08 c                   eval      b_blin = *off
7.08 c                   eval      lldil1_ldor = loldor
7.08 c                   move      lolage        lldil1_lage
7.08 c     lldil1_key    chain     lldil1
7.08 c                   if        %found
7.08 c                   if        lpnebg = '1'
7.08 c                   exsr      sjk_linjer
7.08 c                   if        b_blin = *on
7.09  *
7.09  * Varselbilde for å bekrefte at det er greit
7.09  *
7.09 c                   eval      w_mld1 = 'Akseptere ugyldige varer ?      '
7.09 c                   eval      w_mld2 = 'Bekreft at dette ønskes . .     '
7.09 c                   eval      w_repl = 'N'
7.09  *
7.09 c                   call      'AA007R'
7.09 c                   parm                    w_mld1
7.09 c                   parm                    w_mld2
7.09 c                   parm                    w_repl
7.09 c                   parm                    w_in12
7.09  *
7.09  * F12 er tatt
7.09  *
7.09 c                   if        w_in12 = 1
7.09 c                   goto      b2taga
7.09 c                   endif
7.09  *
7.09 c                   if        w_repl = 'N'
7.09 c                   goto      b2taga
7.09 c                   endif
7.09  *
7.09  * Ugyldige varer er akseptert
7.09  *
7.09 c**                 goto      b2taga
7.08 c                   endif
7.08 c                   endif
7.08 c                   else
7.08 c                   eval      lldil1_lage = *blanks
7.08 c     lldil1_key    chain     lldil1
7.08 c                   if        %found and
7.08 c                             lpnebg = '1'
7.08 c                   exsr      sjk_linjer
7.08 c                   if        b_blin = *on
7.09  *
7.09  * Varselbilde for å bekrefte at det er greit
7.09  *
7.09 c                   eval      w_mld1 = 'Akseptere ugyldige varer ?      '
7.09 c                   eval      w_mld2 = 'Bekreft at dette ønskes . .     '
7.09 c                   eval      w_repl = 'N'
7.09  *
7.09 c                   call      'AA007R'
7.09 c                   parm                    w_mld1
7.09 c                   parm                    w_mld2
7.09 c                   parm                    w_repl
7.099c                   parm                    w_in12
7.09  *
7.09  * F12 er tatt
7.09  *
7.09 c                   if        w_in12 = 1
7.09 c                   goto      b2taga
7.09 c                   endif
7.09  *
7.09 c                   if        w_repl = 'N'
7.09 c                   goto      b2taga
7.09 c                   endif
7.09  *
7.09  * Ugyldige varer er akseptert
7.09  *
7.09 c**                 goto      b2taga
7.08 c                   endif
7.08 c                   endif
7.08 c                   endif
7.09 c                   endif
      *
     c                   eval      *inkc  = *off
      *
     c                   exsr      oppdat_best
      *
     c                   exsr      f1subr
     c                   if        *inkc = *on
     c                   goto      d1tag
     c                   endif
      *
     c                   goto      avslutt
      *
     c                   endif
      *
      * F4=Spørring i vare-register
      *
     c                   if        *inkd = *on
     c                   exsr      oppdat_best
     c                   exsr      hent_linjenr
6.20 c                   eval      p_orde = '0'
7.01 c                   eval      p_vare = *blanks
     c                   call      'LD102R'
     c                   parm                    p_stat
     c                   parm                    w_firm
     c                   parm                    w_numm
     c                   parm                    w_suff
     c                   parm                    w_line
     c                   parm                    b_kost
6.20 c                   parm                    p_orde
7.01 c                   parm                    p_vare
     c                   eval      *in49 = b_kost
     c                   exsr      oppdat_side
     c                   goto      d1taga
     c                   endif
6.23  * Tastet F6=Kampanje (henter inn kun kampanjevarer)
6.23 c                   if        *inkf = *on
6.23 c                   exsr      oppdat_best
6.23 c                   exsr      hent_linjenr
6.23 c                   eval      p_orde = '0'
6.23 c                   eval      p_kamp = b2kamp
6.23 c                   call      'LD103R'
6.23 c                   parm                    p_stat
6.23 c                   parm                    w_firm
6.23 c                   parm                    w_numm
6.23 c                   parm                    w_suff
6.23 c                   parm                    w_line
6.23 c                   parm                    b_kost
6.23 c                   parm                    p_orde
6.23 c                   parm                    p_kamp
6.23 c                   eval      *in49 = b_kost
6.23 c                   exsr      oppdat_side
6.23 c                   goto      d1taga
6.23 c                   endif
Q.02  *
Q.02  * B2VALG = 1,Avslutt ordren uten spm som normalt dukker opp. Ref
Q.02  * samme funksjon som F5. Slår på F5 tast. henger sammen med bruk
Q.02  * av systemet fra cloud.
Q.02  *
Q.02 c                   if        b2valg = 1
Q.02 c                   eval      *inke = *on
Q.02 c                   endif
7.05  *
7.05 c                   if        b2valg = 2
7.05 c                   exsr      xskriv_best
7.05 c                   eval      b2valg = 0
7.05 c                   endif
7.05  *
7.05 c                   if        b2valg = 3
7.05 c                   exsr      xsend_best
7.05 c                   eval      b2valg = 0
7.05 c                   endif
      *
      * F5=Avslutter med oppdatering
      *
     c                   if        *inke = *on
      *
     c                   call      'LO730R'
     c                   parm                    w_enor
     c                   parm                    w_firm
     c                   parm                    w_numm
     c                   parm                    w_suff
      *
     c     lohelu_key    chain     lohelur
     c                   if        lokode = 1
     c                   eval      lokode = 0
     c                   eval      lokoda = *loval
     c                   eval      lokoti = *loval
     c                   eval      lokous = *blank
     c                   eval      lokows = *blank
     c                   endif
6.10 c                   time                    loedat
6.10 c                   time                    loetim
6.10 c                   eval      loeusr = l_user
     c                   update    lohelur
      *
     c                   exsr      oppdat_best
      *
     c                   goto      avslutt
      *
     c                   endif
      *
      * F9=Spørring på vare/lager
      *
     c                   if        *inki = *on
      *
     c                   exsr      oppdat_best
      *
     c                   if        b2vare <> *blank
     c                   eval      w_vare = b2vare
     c                   call      'LL502R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   else
     c                   call      'LL500R'
     c                   endif
      *
     c                   goto      d1tag
     c                   endif
      *
      * F10=Vedlikehold av faste opplysninger
      *
     c                   if        *inkj = *on
     c                   exsr      oppdat_best
     c                   eval      w_stat = *blank
     c                   call      'LO101R'
     c                   parm                    w_stat
     c                   parm                    w_firm
     c                   parm                    w_numm
     c                   parm                    w_suff
7.23 c                   parm                    w_nybe
     c                   goto      d1tag
     c                   endif
      *
      * F11=Vedlikehold av faste tekster
      *
     c                   if        *inkk = *on
     c                   exsr      oppdat_best
     c                   eval      w_stat = *blank
     c                   call      'LO104R'
     c                   parm                    w_stat
     c                   parm                    w_firm
     c                   parm                    w_numm
     c                   parm                    w_suff
     c                   goto      d1tag
     c                   endif
6.30  *
6.30  * F14=Vis leverandør-informasjon
6.30  *
6.30 c                   if        *inkn = *on
6.30 c                   call      'LO575R'
6.30 c                   parm                    w_firm
6.30 c                   parm                    loldor
6.30 c                   parm                    lolage
6.30 c                   goto      d1tag
6.30 c                   endif
6.36  *
6.36  * F20=Skift mellom varenummer og nobbnummer
6.36  *
6.36 c                   if        *inku = *on
6.36 c                   if        *in71 = *off
6.36 C                   eval      *in71 = *on
6.36 c                   else
6.36 C                   eval      *in71 = *off
6.36 c                   endif
6.36 c                   exsr      forny
6.36 c                   goto      b2taga
6.36 c                   endif
      *
      * F21=Spørring på vare/pris
      *
     c                   if        *inkv = *on
     c                   call      'VV550R'
     c                   goto      d1tag
     c                   endif
      *
      * F23=Skifte mellom skjul/vis kostpris (dersom tillatt på bruker)
      *
     c                   if        *inkx = *on and
     c                             lbko02 = 1
     c                   if        *in49 = *off
     C                   eval      *in49 = *on
     c                   else
     C                   eval      *in49 = *off
     c                   endif
     c                   eval      b_kost = *in49
     c                   goto      b2taga
     c                   endif
      *
      * Behandling av opplysninger i bildet
      *
     c                   if        b2ltyp <> *blank or
     c                             b2vare <> *blank
     c                   exsr      hent_linjenr
     c                   eval      w_line = w_line + 10
     c                   exsr      registrer
     c                   select
     c                   when      b_feil = *on
     c                   goto      b2tagb
     c                   when      b_oppr = *on
     c                   exsr      oppdat_side
     c                   goto      d1taga
     c                   endsl
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      d1taga
      *
      * Avslutt program
      *
     c     avslutt       tag
6.35  * Sjekk om det ligger linjer på bestillingen
6.35 c                   eval      lodtlr_line = 0
6.35 c     lodtlr_key    setll     lodtlr
6.35 c     lodtlr_key2   reade     lodtlr
6.35 c                   dow       not %eof
6.35 c                   eval      p_stat = *on
6.35 c     lodtlr_key2   reade     lodtlr
6.35 c                   enddo
      *
6.20 c                   if        b_orde = '1'
6.20 c                   eval      w_mld1 = a_meld(1)
6.20 c                   call      'AA005R'
6.20 c                   parm                    w_mld1
6.20 c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
7.08  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.08  * Sjekk kvaliteten på bestillingslinjene ihht NEB standard
7.08  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.08  *
7.08 c     sjk_linjer    begsr
7.08  *
7.08 c                   eval      w_numm = lonumm
7.08 c                   eval      w_suff = losuff
7.08 c                   eval      w_bsts = *blanks
7.08  *
7.08 c                   call      'LI653R'
7.08 c                   parm                    w_numm
7.08 c                   parm                    w_suff
7.08 c                   parm                    w_bsts
7.08  *
7.08  * Hvis feilkode kommer i retur, send feilmelding
7.08  *
7.08 c                   if        w_bsts = '1'
7.08 c                   call      'LO741R'
7.08 c                   parm                    w_numm
7.08 c                   parm                    w_suff
7.08  *
7.08 c                   eval      b_blin = *on
7.08  *
7.08 c                   endif
7.08  *
7.08 c                   endsr
7.08  *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      lodtl1_line = b1line
     c     lodtl1_key    setll     lodtl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Henter bestillings-total
      *
     c                   exsr      hent_total
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å vise sist registrerte linje nederst på siden
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdat_side   begsr
      *
     c                   eval      lodtl1_line = 9999
      *
     c                   exsr      clr_subfile
      *
     c                   eval      *in15 = *on
     c                   exsr      bck_subfile
      *
      * Henter bestillings-total
      *
     c                   exsr      hent_total
      *
     c     end_side      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for registrering av ny bestilling-linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     registrer     begsr
      *
     c                   eval      b_oppr = *off
     c                   eval      b_feil = *off
      *
     c                   eval      b2vare = %trim(b2vare)
      *
      * Validering
      *
      * Linjetype må finnes
      *
     c                   eval      vltyl1_ltyp = b2ltyp
     c     vltyl1_key    chain     vltyl1                             90
     c                   if        *in90 = *on
     c                   eval      b_feil = *on
     c                   eval      *in31 = *on
     c                   goto      end_reg
     c                   endif
      *
      * Tekst-linjetype og vare kan ikke oppgis samtidig
      *
     c                   if        valktx = 1 and
     c                             b2vare <> *blank
     c                   eval      b_feil = *on
     c                   eval      *in32 = *on
     c                   goto      end_reg
     c                   endif
      *
      * Vare må oppgis sammen med vare-linjetype
      *
     c                   if        valktx = 0 and
     c                             b2vare = *blank
     c                   eval      b_feil = *on
     c                   eval      *in33 = *on
     c                   goto      end_reg
     c                   endif
      *
      *
      * Identifisering/validering av vare
      *
     c                   if        b2vare <> *blank
     c                   exsr      ident_vare
7.01 c                   if        b2vare =  *blank                             varesøk
7.01 c                   goto      end_reg
7.01 c                   endif
6.15  *
6.15  * sjekker om vare er lagervare
6.15  *
6.17 c                   eval      w_vare = b2vare
6.15 c                   call      'VL710R'
6.15 c                   parm                    w_firm
6.15 c                   parm                    w_vare
6.15 c                   parm                    lolage
6.15 c                   parm                    w_vtyp
6.22 c                   parm                    w_utda
6.22 c                   parm                    w_ldor
6.22 c                   parm                    b_inlr
6.36 c                   parm                    w_lv_nobb
6.36 c                   parm                    w_lv_modn
6.36 c                   parm                    w_lv_lldo
6.36 c                   parm                    w_lv_llva
6.15 c                   if        w_vtyp <> 'L' and
6.15 c                             w_vtyp <> 'T' and
6.15 c                             w_vtyp <> 'D'
     c                   eval      *in36  = *on
     c                   eval      b_feil = *on
6.15 c                   endif
     c                   if        b_feil = *on
     c                   goto      end_reg
     c                   endif
     c                   endif
6.2i  *
6.2i c                   time                    w_ddat
6.2i c
6.2i c                   if        vvudat <> *loval
6.2i c                   if        w_utda >= w_ddat and
6.2i c                             vvudat <= w_ddat
6.2i c                   eval      *in40 = '1'
6.2i c                   eval      b_feil = *on
6.2i c                   if        b_feil = *on
6.2i c                   goto      end_reg
6.2i c                   endif
6.2i c                   endif
6.2i c                   endif
6.2i  *
      *
      * Behandling avhengig av linjetype
      *
     c                   if        valktx = 0
     c                   exsr      reg_vare
     c                   else
     c                   exsr      tekstlinje
     c                   endif
      *
     c     end_reg       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for registrering av varelinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     reg_vare      begsr
      *
      * Kaller registreringsrutine
      *
     c                   eval      w_lety = 0
     c                   eval      w_vare = b2vare
     c                   eval      w_enhe = *blank
6.2A c                   eval      p_kamp = lokamp
      *
     c                   call      'LD105R'
     c                   parm                    b_oppr
     c                   parm                    w_firm
     c                   parm                    w_numm
     c                   parm                    w_suff
     c                   parm                    w_line
     c                   parm                    b2ltyp
     c                   parm                    w_lety
     c                   parm                    w_vare
     c                   parm                    b2anta
     c                   parm                    w_enhe
     c                   parm                    b_kost
6.20 c                   parm                    b_orde
6.2A c                   parm                    p_kamp
      *
     c                   if        b_oppr = *on
     c                   eval      p_stat = *on
     c                   endif
      *
     c                   eval      *in49 = b_kost
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for identifisering/validering av vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ident_vare    begsr
      *
      * Identifisering av vare foregår etter følgende rekkefølge, hvor
      * lengden på varenummeret, og om varenummer er alfa eller numerisk
      * er avgjørende:
      *
      * 1) alfa:
      *      - Lev. varenummer (første 15 pos.)
      * 2) num + lengde = 26:
      *      - EAN-128
      * 3) num + lengde <= 8:
      *      - Varenr
6.11  * 4) num + lengde <= 14:
      *      - EAN-nummer
      * 5) num + lengde < 26:
      *      - Lev. varenummer (første 15 pos.)
      *
      * Finner lengde og sjekker om varenummer inneholder alfa-verdi
      *
     c                   eval      w_leng = %len(%trim(b2vare))
      *
     c     digit         check     b2vare        w_posi
      *
      * Behandling dersom alfa
      *
     c                   if        w_posi > 0
      *
6.36  * sjekker om logistikk-vare
6.36 c                   eval      w_lv_vare = *blank
6.36 c/exec sql
6.36 c+ select vvlvnr
6.36 c+ into   :w_lv_vare
6.36 c+ from   vvlepf
6.36 c+ where  vvllva = :b2vare
6.36 c+ fetch first 1 rows only
6.36 c/end-exec
6.36 c                   if        w_lv_vare <> *blank
6.36 c                   movel     w_lv_vare     b2vare
6.36 c                   goto      end_ident
6.36 c                   endif
      *
     c                   eval      vvarl4_lvar = b2vare
     c     vvarl4_key    chain     vvarl4
     c                   if        %found
     c                   eval      b2vare = vvvare
     c                   goto      end_ident
     c                   endif
      *
7.01  *
7.01  * Dette skal ses på som varesøk
7.01  *
7.01 c                   eval      p_vare = %trim(b2vare)
7.01 c                   eval      p_orde = '0'
1.01 c                   call      'LD102R'
7.01 c                   parm                    p_stat
7.01 c                   parm                    w_firm
7.01 c                   parm                    w_numm
7.01 c                   parm                    w_suff
7.01 c                   parm                    w_line
7.01 c                   parm                    b_kost
7.01 c                   parm                    p_orde
7.01 c                   parm                    p_vare
7.01 c                   eval      p_vare = *blanks
7.01 c                   eval      b2vare = *blanks
7.01 c                   eval      *in49 = b_kost
7.01 c                   eval      b_oppr = *on
7.01 c                   leavesr
      *
     c                   eval      b_feil = *on
     c                   eval      *in35 = *on
     c                   goto      end_ident
      *
     c                   endif
      *
      * Behandling dersom lengde = 26
      *
     c                   if        w_leng = 26
      *
     c                   eval      d_e128 = b2vare
6.11 c                   eval      w_eann = d_eann
      *
6.11 c                   call      'VE710R  '
6.11 c                   parm                    w_firm
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_eava
6.11 c                   parm                    w_eaen
6.11 c                   parm                    w_east
6.11 c                   if        w_east = '1'
     c                   eval      vvarl1_vare = w_eava
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      b2vare = vvvare
     c                   eval      b2anta = d_anta
     c                   goto      end_ident
     c                   endif
     c                   endif
      *
     c                   eval      b_feil = *on
     c                   eval      *in35 = *on
     c                   goto      end_ident
      *
     c                   endif
      *
      * Behandling dersom lengde <= 8
      *
     c                   if        w_leng <= 8
      *
     c                   eval      w_vare = %subst(null:1:8-w_leng) + b2vare
      *
     c                   eval      vvarl1_vare = w_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      b2vare = vvvare
     c                   goto      end_ident
     c                   endif
      *
     c                   endif
      *
6.11  * Behandling dersom lengde <= 14
      *
6.11 c                   if        w_leng <= 14
      *
     c                   eval      w_eanx = %subst(null:1:14-w_leng) +
     c                                      B2vare
      *
6.11 c                   movel     w_eanx        w_eann
6.11 c                   call      'VE710R  '
6.11 c                   parm                    w_firm
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_eava
6.11 c                   parm                    w_eaen
6.11 c                   parm                    w_east
     c                   if        w_east = '1'
6.11 c                   eval      vvarl1_vare = w_eava
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      b2vare = vvvare
     c                   goto      end_ident
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Behandling dersom lengde < 26 (samlepost)
      *
6.36  * sjekker om logistikk-vare
6.36 c                   eval      w_lv_vare = *blank
6.36 c/exec sql
6.36 c+ select vvlvnr
6.36 c+ into   :w_lv_vare
6.36 c+ from   vvlepf
6.36 c+ where  vvllva = :b2vare
6.36 c+ fetch first 1 rows only
6.36 c/end-exec
6.36 c                   if        w_lv_vare <> *blank
6.36 c                   movel     w_lv_vare     b2vare
6.36 c                   goto      end_ident
6.36 c                   endif
      *
     c                   eval      vvarl4_lvar = b2vare
     c     vvarl4_key    chain     vvarl4
     c                   if        %found
     c                   eval      b2vare = vvvare
     c                   goto      end_ident
     c                   endif
      *
      * Setter feil-indikator dersom vare ikke ble funnet
      *
     c                   eval      b_feil = *on
     c                   eval      *in35 = *on
      *
     c     end_ident     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for registrering av tekstlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     tekstlinje    begsr
      *
     c                   call      'LD110R'
     c                   parm                    b_oppr
     c                   parm                    w_firm
     c                   parm                    w_numm
     c                   parm                    w_suff
     c                   parm                    w_line
     c                   parm                    b2ltyp
      *
     c                   if        b_oppr = *on
     c                   eval      p_stat = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      w_virn = w_srrn
      *
      * Endre linje
      *
     c                   if        b1valg = 2
     c                   exsr      endre_linje
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett linje
      *
     c                   if        b1valg = 4
     c                   exsr      slett_linje
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis linje
      *
     c                   if        b1valg = 5
     c                   exsr      vis_linje
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
7.xx  *
7.xx  * Legg til linje
7.xx  *
7.xx c                   if        b1valg = 9
7.xx c                   exsr      legg_til
7.xx c                   eval      b1valg = 0
7.xx c                   update    b1sfl
7.xx c                   iter
7.xx c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekk oppdatering/utskrift/utplukk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     f1subr        begsr
      *
      * Oppdater bestilling-hode med verdier fra varelinjer
      *
     c                   call      'LO730R'
     c                   parm                    w_enor
     c                   parm                    w_firm
     c                   parm                    w_numm
     c                   parm                    w_suff
      *
     c                   eval      w_stat = *blank
7.12 c**                 eval      f1valg = 1
7.12 c                   eval      f1valg = w_valg
6.21  * Hvis synkronisering, skal dette utføres før mulighet for utskrift osv.
6.21 c                   if        b_synk = *on and
6.21 c                             loornr <> 0
6.27  * Sjekk status på ordretypen før vi starter synkronisering
6.27  * Ordrenummer/suffix må finnes
6.27 c                   eval      b_otyp = *on
6.27 c                   eval      *in37 = *off
6.27 c                   eval      *in38 = *off
6.27 c                   eval      fohel1_numm = loornr
6.27 c                   eval      fohel1_suff = loorsu
6.27 c     fohel1_key    chain     fohel1
6.27 c                   if        not %found
6.27 c                   eval      b_otyp = *off
6.27 c                   eval      *in37 = *on
6.27 c                   endif
6.27  * Sjekk om det er suffix på ordren. Vi skal ikke oppdatere
6.27 c                   eval      fohell_numm = loornr
8.02 c     fohell_key    setll     fohel1
6.27 c     fohell_key    reade     fohel1
6.27 c                   dow       not %eof
8.02 c*                  if        fosuff > 0
8.02 c                   if        fosuff > loorsu
6.27 c                   eval      b_otyp = *off
6.27 c                   eval      *in38 = *on
6.27 c                   endif
6.27 c     fohell_key    reade     fohel1
6.27 c                   enddo
6.2b  * Sjekk om ordretypen er intern, dvs at den ligger inne på leverandøren
6.2b c                   eval      lldilr_ldor = loldor
6.2b c                   move      lolage        lldilr_lage
6.2b c     lldilr_key    chain     lldilr
6.2b c                   if        %found
6.2b c                   if        lpotyp <> *blank and
6.2b c                             lpotyp = lootyp
6.2b c                   eval      b_otyp = *off
6.2b c                   eval      *in39 = *on
6.2b c                   endif
6.2b c                   else
6.2b c                   eval      lldilr_lage = *blank
6.2b c     lldilr_key    chain     lldilr
6.2b c                   if        %found and
6.2b c                             lpotyp <> *blank and
6.2b c                             lpotyp = lootyp
6.2b c                   eval      b_otyp = *off
6.2b c                   eval      *in39 = *on
6.2b c                   endif
6.2b c                   endif
6.27  *
6.27 c                   if        b_otyp = *off
6.27 c                   exfmt     x1win
6.27 c                   endif
6.27  *
6.27 c                   if        b_otyp = *on
6.21 c                   exsr      synk_subr
6.27 c                   endif
6.21 c                   endif
      *
      * Send bilde f1win
      *
     c     f1tagb        tag
      *
     c                   exfmt     f1win
      *
      * F3=Avslutt
      *
     c                   if        *inkc or *inkl
      *
     c                   eval      *inkc = *on
      *
     c                   if        lokode = 0
     c                   call      'LO709R'
     c                   parm                    w_numm
     c                   parm                    w_suff
     c                   endif
      *
     c     lohelu_key    chain     lohelur
     c                   if        lokode = 0
     c                   eval      lokode = 1
     c                   endif
      *
6.10 c                   time                    loedat
6.10 c                   time                    loetim
6.10 c                   eval      loeusr = l_user
     c                   update    lohelur
      *
     c                   goto      f1tag9
     c                   endif
      *
     c                   if        f1valg <> 0 and
     c                             f1valg <> 1 and
6.13 c                             f1valg <> 2 and
6.13 c                             f1valg <> 3
     c                   eval      *in31 = *on
     c                   goto      f1tagb
     c                   endif
      *
      * Tilbud og faktura kan ikke plukkes
      *
     c                   if        f1valg = 2
     c                   if        vaoakk = 0 or
     c                             vaoakk = 3
     c                   eval      *in32 = *on
     c                   goto      f1tagb
     c                   endif
     c                   endif
7.04  *
7.04  * Hvis elektronisk pakkseddel er mottatt, skal den ikke kunne plukkes.
7.04  *
7.04 c                   if        f1valg = 2
7.04 c                   if        vaoakk = 1
7.04 c                   eval      lbhoi4_hnum = lonumm
7.04 c                   eval      lbhoi4_hsuf = losuff
7.04 c     lbhoi4_key    chain     lbhoi4
7.04 c                   if        %found
7.04 c                   eval      w_mld1 = *blanks
7.04 c                   eval      w_mld2 = *blanks
7.04 c                   eval      w_mld3 = *blanks
7.04 c                   eval      w_mld1 = 'Elektronisk pakkseddel er mottatt-
7.04 c                              på bestilling'
7.04 c                   eval      w_mld2 = 'Kan ikke varemottas manuelt'
7.04 c                   call      'AA031R'
7.04 c                   parm                    w_mld1
7.04 c                   parm                    w_mld2
7.04 c                   parm                    w_mld3
7.04  *
7.04 c                   goto      f1tagb
7.04 c                   endif
7.04 c                   endif
7.04 c                   endif
      *
      * Tilbakestill kode som sier at ordre er opptatt dersom f1valg=0
      *
     c                   if        f1valg = 0
     c     lohelu_key    chain     lohelur
     c                   if        lokode = 1
     c                   eval      lokode = 0
     c                   move      *loval        lokoda
     c                   move      *loval        lokoti
     c                   eval      lokous = *blank
     c                   eval      lokows = *blank
     c                   endif
6.10 c                   time                    loedat
6.10 c                   time                    loetim
6.10 c                   eval      loeusr = l_user
     c                   update    lohelur
     c                   endif
6.13  *
6.13  * Vi ønsker å sende bestilling, da kjører vi som om vi hadde
6.13  * valgt 18. (Sende bestilling) Hvis det ikke går, endrer vi valg
6.13  * til 1
6.13  *
6.13 c                   if        f1valg = 3
6.13  * Finn forsendelsesinfo
6.13 c                   eval      lldilr_ldor = loldor
6.13 c                   move      lolage        lldilr_lage
6.13 c     lldilr_key    chain     lldilr
6.13 c                   if        not %found
6.13 c                   eval      lldilr_lage = *blank
6.13 c     lldilr_key    chain     lldilr
6.13  *
6.13 c                   if        not %found
6.13 c                   eval      f1valg = 1
6.13 c                   endif
6.13 c                   endif
6.13  *
6.13 c                   if        f1valg = 3
6.13 c                   call      'LO740R'
6.13 c                   parm                    w_numm
6.13 c                   parm                    w_suff
6.24 c                   parm                    w_retk
6.13 c                   endif
6.13  *
6.13 c                   endif
      *
      * Sjekk, valg=1, dvs. utskrift
      *
     c                   if        f1valg = 1
     c                   call      'LO600R'
     c                   parm                    w_stat
     c                   parm                    w_firm
     c                   parm                    w_numm
     c                   parm                    w_suff
     c                   parm                    w_svar
     c                   endif
      *
      * Sjekk, valg=2, dvs. utplukk
      *
     c                   if        f1valg = 2
     c                   call      'LO736R'
     c                   parm                    w_stat
     c                   parm                    w_firm
     c                   parm                    w_numm
     c                   parm                    w_suff
     c                   endif
      *
      * Sjekk om retur fra annet program
      *
     c                   if        w_stat <> *blank
     c                   eval      *inkc = *on
     c                   endif
      *
     c     f1tag9        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent opplysninger om bestillingen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_best     begsr
      *
      * Hent opplysninger fra bestilling-hode
      *
     c     lohel1_key    chain     lohel1r
      *
     c                   eval      b2onum = lonumm
6.23 c                   eval      b2kamp = lokamp
     c                   eval      b2otyp = lootyp
     c                   eval      b3navn = lonavn
7.13  * Må vaske navn for tegn
7.13    /FREE
7.13        w_len = %len(%trim(b3navn));
7.13        if (w_len > 0);
7.13         b3navn = %ScanRpl('-':' ':b3navn:1);
7.13         endif;
7.13    /END-FREE
     c                   eval      b3adr1 = loadr1
     c                   eval      b3sted = losted
6.12 c                   eval      b2valu = lovalk
7.19  *
8.01  * Henter både løpende og låst valutakurs
8.01  * Indikator styrer om valutainfo skal vises
7.19  *
8.01 c                   eval      *in24 = *on
8.01 c                   eval      w_kurs = 1
7.19 c                   if        lovalk <> '   ' and
7.19 c                             lovalk <> 'NOK'
8.01 c                   eval      *in24 = *off
7.19 c/exec sql
7.19 c+ select rapmku
7.19 c+ into   :b2kurs
7.19 c+ from   ra16pf
7.19 c+ where  rapfir = :w_firm and rapkod = :b2valu
7.19 c/end-exec
8.01 c                   if        sqlcod = 100
8.01 c                   eval      b2kurs = 0
8.01 c                   else
8.01 c                   eval      w_kurs = b2kurs
8.01 c                   endif
7.19 c/exec sql
7.19 c+ select rlvkur
8.01 c+ into   :b2kurl
7.19 c+ from   ra60st
7.19 c+ where  :lobdat >= rlvfda and :lobdat <= rlvtda and :loldor = rlvlev
7.19 c/end-exec
8.01 c                   if        sqlcod = 100
8.01 c                   eval      b2kurl = 0
8.01 c                   else
8.01 c                   eval      w_kurs = b2kurl
8.01 c                   endif
7.19 c                   endif
      *
     c                   eval      *in23 = *on
     c                   if        loornr > 0
     c                   eval      *in23 = *off
     c                   eval      b2ornr = loornr
     c                   eval      b2orsu = loorsu
     c                   endif
      *
     c                   eval      b2ldor = loldor
      *
      * Hent beskrivelse av ordretype
      *
     c                   eval      b2otxt = *blank
      *
     c                   eval      votyl1_otyp = lootyp
     c     votyl1_key    chain     votyl1
     c                   if        %found
     c                   eval      b2otxt = vaotxt
     c                   endif
      *
      * Henter beskrivelse av leveringstype
      *
      * Hent opplysninger fra leverandør
      *
     c                   eval      b2navn = *blank
     c                   eval      b2adr1 = *blank
     c                   eval      b2sted = *blank
     c                   eval      b2tlfn = *blank
     c                   eval      b2mobn = *blank
      *
     c                   eval      rlevl1_levr = loldor
     c     rlevl1_key    chain     rlevl1
     c                   if        %found
6.nu c                   movel     rlnavn        b2navn
6.nu c                   movel     rlgate        b2adr1
6.nu c                   movel     rlsted        b2sted
     c                   eval      b2tlfn = rltlfn
     c                   eval      b2mobn = rlmobn
     c                   endif
      *
      * Finner hvilken mva-sats som skal benyttes
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    lobdat
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
      *
      * Hent bestillings-total
      *
     c                   exsr      hent_total
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdat_best   begsr
      *
      * Henter ny bestillings-total
      *
     c                   exsr      hent_total
      *
      * Oppdaterer bestilling-hode med ny saldo
      *
     c     lohelu_key    chain     lohelur
      *
     c                   eval      lototi = w_toti
     c                   eval      lototr = w_totr
     c                   eval      lototk = w_totk
      *
6.10 c                   time                    loedat
6.10 c                   time                    loetim
6.10 c                   eval      loeusr = l_user
     c                   update    lohelur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatering av Lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdat_lager  begsr
      *
      * Legger over verdier i overførings-felter
      *
     c                   move      ldvare        hlvare
     c                   move      lolage        hllage
     c                   move      ldenh1        hlenhe
     c                   move      *loval        hldato
     c                   move      *loval        hltime
     c                   move      lootyp        hlotyp
     c                   move      ldltyp        hlltyp
      *
     c                   eval      hlanta = ldanta
     c                   eval      hlkopr = ldkpny
      *
     c                   eval      hlrefr = *blank
     c                   move      'L'           hlsyst
     c                   eval      hlaltk = *blank
     c                   eval      hlrest = *blank
6.16 c                   move      ldnumm        w_numa
6.16 c                   move      ldline        w_lina
6.16 c                   eval      hlrefr = 'B:' + w_numa +
6.16 c                                      'L:' + w_lina
     c                   eval      hlonum = ldnumm
     c                   eval      hlolin = ldline
     c                   move      ldlety        hllety
      *
      * Kaller opp lageroppdaterings-program
      * dersom dette ikke er en vare hentet inn fra LVR
      *
     c                   if        ldlvre = 0
      *
     c                   eval      w_stat = *blank
      *
     c                   call      'VL001R'
     c                   parm                    w_stat
     c                   parm                    hlrec
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
7.10  *
7.10  * Finn totalen for bestillingen
7.10  *
7.10 c                   eval      w_vekt = 0
7.10 c                   eval      w_volu = 0
7.10 c                   eval      w_avvi = *off
7.10  *
7.10 c                   call      'LO786R'
7.10 c                   parm                    w_firm
7.10 c                   parm                    w_numm
7.10 c                   parm                    w_suff
7.10 c                   parm                    w_vekt
7.10 c                   parm                    w_volu
7.10 c                   parm                    w_avvi
7.10  *
7.10 c                   eval      b2vekt = w_vekt
7.10 c                   eval      b2volu = w_volu
7.10  *
     c                   dou       w_srrn = w_spge
      *
     c                   read      lodtl1
      *
     c                   if        %eof or
     c                             ldfirm <> w_firm or
     c                             ldnumm <> w_numm or
     c                             ldsuff <> w_suff
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      lodtl1_line = ldline
      *
     c                   eval      b1valg = 0
     c                   eval      b1line = ldline
     c                   eval      b1lety = ldlety
     c                   eval      b1vare = ldvare
6.36 c                   eval      b1nobb = ldnobb
     c                   eval      b1tek1 = ldtek1
7.11 c*                  eval      b1tek2 = ldtek2
     c                   eval      b1enhe = ldenh1
     c                   eval      b1raba = 0
     c                   eval      b1sumn = ldsumi - ldsumr
     c                   eval      b1sumk = ldsumk
      *
     c                   move      ldanta        b1anta
     c                   move      ldipny        b1inpr
7.10  *
7.10  * Kalkuler vekt og volum for varelinje
7.10  *
7.10 c                   exsr      cal_line
7.10  *
     c                   if        ldrab2 = 0
     c                   eval      b1raba = ldrab1
     c                   else
7.18 c                   if        ldsumr <> 0 and ldsumi <> 0
     c                   eval(h)   b1raba = ldsumr * 100 / ldsumi
7.18 c                   endif
     c                   endif
      *
      * Vises negativt antall dersom kredit linjetype
      *
     c                   if        ldanta <> 0 and
     c                             ldltyp <> *blank
     c                   eval      vltyl1_ltyp = ldltyp
     c     vltyl1_key    chain     vltyl1
     c                   if        valkre = '-'
     c                   eval      b1anta = b1anta * -1
     c                   endif
     c                   endif
      *
      * Dersom bruker ikke skal ha tilgang til å se kostpris blir
      * informasjon om denne fjernet.
      *
     c                   if        *in49  = *off
     c                   eval      b1sumk = 0
     c                   endif
      *
      * Konkatinerer linje
      *
7.11 c                   eval      b1iden = *blanks
7.11 c                   if        *in71 = *on
7.11 c                   if        b1nobb <> 0
7.11 c                   eval      b1iden = %char(b1nobb)
7.11 c                   endif
7.11 c                   else
7.11 c                   eval      b1iden = %char(b1vare)
7.11 c                   endif
7.11 c*                  select
7.11 c*                  when      b1vare = *blank
7.11 c*                  eval      b1text = ldtek1 + ldtek2
7.11 c*                  when      *in70 = *off
7.11 c*                  if        *in71 = *on
7.11 c*                  eval      b1text = %char(b1nobb) + ' ' +
7.11 c*                                     %subst(b1tek1:1:31) + ' ' +
7.11 c*                                     %editw(b1anta:'    0 ,   -') + ' ' +
7.11 c*                                     b1enhe + ' ' +
7.11 c*                                     %editw(b1inpr:'    0 ,  -') + ' ' +
7.11 c*                                     %editw(b1raba:'   ,  -')
7.11 c*                  else
7.11 c*                  eval      b1text = b1vare + ' ' +
7.11 c*                                     %subst(b1tek1:1:31) + ' ' +
7.11 c*                                     %editw(b1anta:'    0 ,   -') + ' ' +
7.11 c*                                     b1enhe + ' ' +
7.11 c*                                     %editw(b1inpr:'    0 ,  -') + ' ' +
7.11 c*                                     %editw(b1raba:'   ,  -')
7.11 c*                  endif
7.11 c*                  when      *in70 = *on
7.11 c*                  eval      b1text = b1tek1 + '     ' +
7.11 c*                                     %editw(b1sumn:'         ,  -') +
7.11 c*                                     ' ' +
7.11 c*                                     %editw(b1sumk:'         ,  -')
7.11 c*                  endsl
      *
7.17 c                   exsr      newlo_vask
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     lodtl1_key    setgt     lodtl1
     c                   readp     lodtl1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     lodtl1
      *
     c                   if        %eof or
     c                             ldfirm <> w_firm or
     c                             ldnumm <> w_numm or
     c                             ldsuff <> w_suff
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      lodtl1_line = ldline
      *
     c                   enddo
      *
     c                   if        %eof
     c     lodtl1_key    setll     lodtl1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for innlegging av ny linje, tast inn kode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     legg_til      begsr
      *
     c                   eval      t1valg = 1
     c                   eval      t1ltyp = laalty
      *
      * Vis bilde
      *
     c     t1tagb        tag
      *
     c                   exfmt     t1win
      *
      * F3=Avslutt
      *
     c                   if        *inkc or *inkl
     c                   goto      end_legg_til
     c                   endif
      *
      * Finner linjenummer
      *
     c                   if        t1valg = 1
     c                   eval      w_line = b1line + 1
     c                   else
     c                   eval      w_line = b1line - 1
     c                   endif
      *
      * Dersom linje ikke allerede finnes, kalles registreringsrutine
      *
     c                   eval      lodtlr_line = w_line
     c     lodtlr_key    chain     lodtlrr
     c                   if        not %found
      *
     c                   eval      b2ltyp = t1ltyp
     c                   eval      b2vare = t1vare
      *
     c                   if        b2ltyp <> *blank or
     c                             b2vare <> *blank
     c                   exsr      registrer
     c                   select
     c                   when      b_feil = *on
     c                   goto      t1tagb
     c                   when      b_oppr = *on
     c                   eval      b_forn = *on
     c                   goto      end_legg_til
     c                   endsl
     c                   endif
      *
     c                   endif
      *
     c     end_legg_til  endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å endre linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     endre_linje   begsr
      *
     c                   eval      lodtlr_line = b1line
     c     lodtlr_key    chain     lodtlrr
     c                   if        not %found
     c                   goto      end_endre
     c                   endif
      *
     c                   if        ldvare <> *blank
6.2A c                   eval      p_kamp = lokamp
     c                   call      'LD105R'
     c                   parm                    b_oppr
     c                   parm                    w_firm
     c                   parm                    w_numm
     c                   parm                    w_suff
     c                   parm                    ldline
     c                   parm                    ldltyp
     c                   parm                    ldlety
     c                   parm                    ldvare
     c                   parm                    ldanta
     c                   parm                    ldenh1
     c                   parm                    b_kost
6.20 c                   parm                    b_orde
6.2A c                   parm                    p_kamp
     c                   eval      *in49 = b_kost
     c                   else
     c                   call      'LD110R'
     c                   parm                    p_stat
     c                   parm                    w_firm
     c                   parm                    w_numm
     c                   parm                    w_suff
     c                   parm                    ldline
     c                   parm                    ldltyp
     c                   endif
      *
     c                   eval      b_forn = *on
      *
     c     end_endre     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sletting av linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slett_linje   begsr
      *
     c                   eval      lodtlu_line = b1line
     c     lodtlu_key    chain     lodtlur
     c                   if        not %found
     c                   goto      end_slett
     c                   endif
      *
     c                   eval      b_forn = *on
      *
     c                   delete    lodtlur
      *
6.2h  *
6.2h  * sletter best.referanse på ordre.
6.2h  *
6.2h c                   eval      fodtlu_numm = ldornu
6.2h c                   eval      fodtlu_suff = ldorsu
6.2h c                   eval      fodtlu_line = ldorli
6.2h c     fodtlu_key    chain     fodtlur
6.2h c                   if        %found
6.2h c                   eval      fdbenr = 0
6.2h c                   eval      fdbsuf = 0
6.2h c                   eval      fdblin = 0
6.2h c                   eval      fdantb = 0
6.2h c                   time                    fdedat
6.2h c                   time                    fdetim
6.2h c                   eval      fdeusr = l_user
6.2h  *
6.2h c                   update    fodtlur
6.2h c                   endif
      * Oppdaterer lager
      *
     c                   if        laalag <> 0
     c                   eval      ldanta = ldanta * -1
     c                   exsr      oppdat_lager
     c                   endif
      *
     c     end_slett     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for å vise en vare/tekst-linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vis_linje     begsr
      *
     c                   eval      lodtlr_line = b1line
     c     lodtlr_key    chain     lodtlr
     c                   if        not %found
     c                   goto      end_vis
     c                   endif
      *
     c                   if        ldvare = *blank
     c                   goto      end_vis
     c                   endif
      *
     c                   call      'LD505R'
     c                   parm                    w_firm
     c                   parm                    w_numm
     c                   parm                    w_suff
     c                   parm                    b1line
      *
     c     end_vis       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å hente høyeste linjenummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_linjenr  begsr
      *
     c                   eval      lodtlr_line = 9999
     c     lodtlr_key    setll     lodtlr
     c                   readp     lodtlr
     c                   if        not %eof and
     c                             ldfirm = w_firm and
     c                             ldnumm = w_numm and
     c                             ldsuff = w_suff
     c                   eval      w_line = ldline
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av bestillingstotal
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_total    begsr
      *
     c                   eval      w_totk = 0
     c                   eval      w_toti = 0
     c                   eval      w_totr = 0
      *
     c     lodtlr_key2   setll     lodtlr
     c     lodtlr_key2   reade     lodtlr
     c                   dow       not %eof
     c                   eval      w_totk = w_totk + ldsumk
     c                   eval      w_toti = w_toti + ldsumi
     c                   eval      w_totr = w_totr + ldsumr
     c     lodtlr_key2   reade     lodtlr
     c                   enddo
      *
     c                   eval      b2nett = w_toti - w_totr
8.01 c                   eval      b2netv = w_toti * w_kurs
7.14 c                   eval      b2kost = w_totk
      *
      * Beregner faktura-diff
      *
     c                   eval      b2totf = lototf
      *
     c                   if        lomkod = 0
     c                   eval(h)   b2mvab = (b2nett * w_mvap) / 100
     c                   else
     c                   eval      b2mvab = 0
     c                   endif
      *
     c                   eval(h)   b2diff = (b2nett + b2mvab) - b2totf
      *
     c                   endsr
      *
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  * Subrutine for behandling av synkronisering mellom bestilling og ordre
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  *
6.21 c     synk_subr     begsr
6.21  * Sjekk synronisering mellom bestilling og ordre
6.21 c                   eval      b_updo = *off
6.21  * Sjekk om du har tilgang til ordre
6.21 c                   eval      p_acok = '1'
6.21 c**6.26             call      'FO711R'
6.21 c**6.26             parm                    w_firm
6.21 c**6.26             parm                    loornr
6.21 c**6.26             parm                    loorsu
6.21 c**6.26             parm                    p_acok
6.26  * Hvis ordren er holdt av deg selv, skal vi tillate endring
6.26 c**                 if        p_acok = '1'
6.26 c                   eval      fohel1_numm = loornr
6.26 c                   eval      fohel1_suff = loorsu
6.26 c     fohel1_key    chain     fohel1
6.26 c                   if        %found
6.26 c                   if        fokous <> *blanks and
6.2G c                             fokous <> l_user  and
6.2G c                             fokode = 1
6.26 c                   eval      p_acok = *blanks
6.26 c                   endif
6.26 c                   endif
6.21  * Kall oppdatering av ordre hvis ikke sperret
6.21 c                   if        p_acok = '1'
6.21  * Manuell endringe av linjer
6.21 c                   if        laftyp = 'M'
6.21 c                   eval      w_numm = lonumm
6.21 c                   eval      w_suff = losuff
6.21 c                   call      'LO580R'
6.21 c                   parm                    w_firm
6.21 c                   parm                    w_numm
6.21 c                   parm                    w_suff
6.21  * Vi forusetter at det er gjort endringer. Skrur på endringsindikator
6.21 c                   eval      b_updo = *on
6.21  *
6.21 c                   else
6.21  *
6.21  * Automatisk endring av linjer
6.21  * Les alle bestillingslinjene, sjekk hvilke endringer og gjør disse
6.21  *
6.21 c     lodtlr_key2   setll     lodtlr
6.21 c     lodtlr_key2   reade     lodtlr
6.21 c                   dow       not %eof
6.21  *
6.21 c                   eval      w_anta = 0
6.21 c                   eval      w_inkp = 0
6.29 c                   eval      w_kopr = 0
6.2c  * Bestillingslinje må være opprettet senere enn dato satt på LSBO
6.2c c                   if        ldodat  < lafgfr
6.2c c                   goto      les_next
6.2c c                   endif
6.21  * Må vare varelinje
6.21 c                   if        ldvare <> *blank
6.21  * Ordrenummer/suffix må finnes
6.21 c                   eval      fohel1_numm = loornr
6.21 c                   eval      fohel1_suff = loorsu
6.21 c     fohel1_key    chain     fohel1
6.21 c                   if        %found
6.21 c                   eval      fodtl1_numm = ldornu
6.21 c                   eval      fodtl1_suff = ldorsu
6.21 c                   eval      fodtl1_line = ldorli
6.21 c     fodtl1_key    chain     fodtl1
6.21 c                   if        %found
6.25  * Hvis avvik enhet, kan ikke synkronisering kjøres
6.25 c                   if        fdenh1 <> ldenh1
6.25 c                   goto      les_next
6.25 c                   endif
6.21  * Hvis avvik på antall, og dette skal oppdateres, utfør dette
6.21 c                   if        lafcha = 'J' and
6.21 c                             ldanta <> fdanta
6.21 c                   eval      w_anta = ldanta
6.21 c                   endif
6.21  * Hvis avvik på innkjøpspris, rekalkuler kost
6.21 c                   if        lafchk = 'J' and
6.21 c                             ldipny <> fdinpr
6.21 c                   eval      w_inkp = ldipny
6.21 c                   endif
6.29  * Hvis avvik på kostpris, logg
7.06 c                   if        u_706 = *off
6.29 c                   if        lafchk = 'J' and
6.29 c                             ldkpny <> fdkopr
6.29 c                   eval      w_kopr = ldkpny
6.29 c                   endif
7.06 c                   endif
6.21  *
6.21 c                   if        w_anta <> 0 or
6.29 c                             w_inkp <> 0 or
6.29 c                             w_kopr <> 0
6.21 c                   exsr      opd_ordrel
6.21 c                   endif
6.21  *
6.21 c                   else
6.21  * Skal vi legge til nye linjer
6.21 c                   if        lafchl = 'J'
6.21 c                   exsr      add_nylinje
6.21 c                   endif
6.21 c                   endif
6.21  *
6.21 c                   endif
6.21 c                   endif
6.21  *
6.25 c     les_next      tag
6.21  *
6.21 c     lodtlr_key2   reade     lodtlr
6.21 c                   enddo
6.21  *
6.21  * Oppdater ordrehode med verdier fra varelinjer
6.21  *
6.21 c                   if        b_updo = *on
6.21 c                   call      'FO730R'
6.21 c                   parm                    w_enor
6.21 c                   parm                    w_firm
6.21 c                   parm                    loornr
6.21 c                   parm                    loorsu
6.21  * Hvis ordren har kommet lenger enn behandlingskode 1, settes infokode i heading
6.21  *
6.21 c                   eval      votyl1_otyp = footyp
6.21 c     votyl1_key    chain     votyl1r                            90
6.21 c                   if        vaoakk > 1
6.21 c     fohel1_key    chain     fohelu
6.21 c                   if        %found
6.21 c                   eval      foityp = lafity
6.21 c                   update    fohelur
6.21  * Skriv til kvitteringsfile for info til selger
6.21 c                   eval      fwbolu_type = 'INF'
6.21 c                   eval      w_tek1 = 'Ordreinfokode ' + lafity +
6.21 c                             ' er satt på ordren.'
6.21  *
6.21 c                   exsr      logg_txt
6.21 c                   endif
6.21 c                   endif
6.21 c                   endif
6.21  *
6.21  * Skal vi varsle mottaker om at det er gjort endringer
6.21 c                   if        lafvar = 'J'
6.21 c                   exsr      inf_selger
6.21 c                   endif
6.21  *
6.21 c                   endif
6.21 c                   else
6.21  * Informer selger om eventuelle endringer. Skriv melding i
6.21 c                   exsr      log_bestil
6.21 c                   if        lafvar = 'J'
6.21 c                   eval      p_txt1 = 'Endringer er gjort i bestilling '+
6.21 c                             %char(lonumm)
6.21 c                   eval      p_txt2 = 'som kan påvirke ordrenummer '+
6.21 c                             %char(loornr)
6.21 c                   eval      p_txt3 = 'Ordren er ikke oppdatert. Trolig å+
6.21 c                                      rsak er '
6.21 c                   eval      p_txt4 = 'manglende tilgang.'
6.21 c                   exsr      inf_selger
6.21 c                   endif
6.21 c                   endif
6.21  *
6.21 c                   endsr
6.21  *
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  * Subrutine for oppdatering av ordrelinje
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  *
6.21 c     opd_ordrel    begsr
6.21  * Hvis det skal varsles, skal vi logge endringen før
6.21 c                   if        lafvar = 'J'
6.21 c                   eval      fwbolu_type = 'FRA'
6.21 c                   exsr      oppd_logg
6.21 c                   endif
6.21  * Kall program for endring av antall
6.21 c                   call      'FO701R'
6.21 c                   parm                    w_firm
6.21 c                   parm                    ldornu
6.21 c                   parm                    ldorsu
6.21 c                   parm                    ldorli
6.21 c                   parm                    w_anta
6.21 c                   parm                    w_inkp
6.2d c                   parm                    w_kopr
6.21 c                   parm                    w_csta
6.21 c
6.21  * Hvis det skal varsles, skal vi logge endringen etter
6.21 c                   if        lafvar = 'J'
6.21 c                   eval      fwbolu_type = 'TIL'
6.21 c                   exsr      oppd_logg
6.21 c                   endif
6.21  * Vi forusetter at det er gjort endringer. Skrur på endringsindikator
6.21 c                   eval      b_updo = *on
6.21  *
6.21 c                   endsr
6.21  *
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  * Subrutine for skrive hele bestilling til loggfile
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  *
6.21 c     log_bestil    begsr
6.21  * Gjøres hvis vi ikke har tilgang til ordre/sperret
6.21  *
6.21 c     lodtlr_key2   setll     lodtlr
6.21 c     lodtlr_key2   reade     lodtlr
6.21 c                   dow       not %eof
6.21  * Må vare varelinje
6.21 c                   if        ldvare <> *blank
6.21  *
6.21 c                   exsr      logg_bline
6.21  *
6.21 c                   endif
6.21  *
6.21 c     lodtlr_key2   reade     lodtlr
6.21 c                   enddo
6.21  * Legg ut tekstlinje i bunn
6.21 c                   eval      fwbolu_type = 'INF'
6.21 c                   eval      w_tek1 = 'Ordren var sperret. Sjekk manuelt'
6.21  *
6.21 c                   exsr      logg_txt
6.21  *
6.21 c                   endsr
6.21  *
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  * Subrutine for sende info til ansvarlig selger
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  *
6.21 c     inf_selger    begsr
6.21  * Sjekk først om det er noe å sende informasjon om.
6.28 c                   eval      b_logg = *off
6.28 c                   eval      fwbolr_numm = loornr
6.28 c                   eval      fwbolr_suff = loorsu
6.28 c     fwbolr_key    setll     fwbolrr
6.28 c     fwbolr_key    reade     fwbolr                                 91
6.28 c                   dow       *in91 = *off
6.28 c                   eval      b_logg = *on
6.28 c     fwbolr_key    reade     fwbolr                                 91
6.28 c                   enddo
6.28  * Hvis ikke finnes, sjekk også bestillingen
6.28 c                   if        b_logg = *off
6.28 c                   eval      fwbolr_numm = lonumm
6.28 c                   eval      fwbolr_suff = losuff
6.28 c     fwbolr_key    setll     fwbolrr
6.28 c     fwbolr_key    reade     fwbolr                                 91
6.28 c                   dow       *in91 = *off
6.28 c                   eval      b_logg = *on
6.28 c     fwbolr_key    reade     fwbolr                                 91
6.28 c                   enddo
6.28 c                   endif
6.28  *
6.28 c                   if        b_logg = *on
6.20  * Finn emailadresse fra selger, som skal varsles
6.20 c                   if        foselg <> 0
6.20 c                   eval      ra09lr_ikod = foselg
6.20 c     ra09lr_key    chain     ra09lr
6.20 c                   if        %found and
6.20 c                             raiema <> *blank
6.20 c                   eval      p_emal = raiema
6.20 c                   endif
6.20 c                   endif
6.21  *
6.21  * Legg inn beskrivelser, og mottaker av mailadresse
6.21  *
6.2f c                   eval      p_in03 = '0'
6.21 c                   eval      p_kund = fokund
6.21 c                   eval      p_selg = foselg
6.21 c                   eval      p_navn = raitxt
6.21  * Setter p_kule til ' '. Da vil FO798 bruke email adressen i parameterlista
6.21 c                   eval      p_kule = ' '
6.21 c                   eval      p_emne = 'Endringer i best: '+ %char(lonumm)+
6.21 c                             '. Ref. ordre: ' + %char(loornr)
6.21  *
6.21 c                   call      'FO798R'
6.21 c                   parm                    p_kule
6.21 c                   parm                    p_kund
6.21 c                   parm                    p_kpro
6.21 c                   parm                    p_numm
6.31 c                   parm                    p_suff
6.21 c                   parm                    p_selg
6.21 c                   parm                    p_navn
6.21 c                   parm                    p_emal
6.30 c                   parm                    p_ema2
6.30 c                   parm                    p_ema3
6.21 c                   parm                    p_emne
6.21 c                   parm                    p_txt1
6.21 c                   parm                    p_txt2
6.21 c                   parm                    p_txt3
6.21 c                   parm                    p_txt4
6.21 c                   parm                    p_pers
6.31 c                   parm                    p_kopi
6.21 c                   parm                    p_inif
6.21 c                   parm                    p_in03
6.21  * Merk LDA, slik at vi kan bruke PROA funksjonalitet
6.21 c                   eval      l_arch = 0
6.21 c                   eval      l_skje = 0
6.21 c                   eval      l_mail = 1
6.2f  * Er F3 eller F12 benyttet fra mailbildet ?
6.2f c                   if        p_in03 = '1'
6.2f c                   eval      l_mail = 0
6.2f c                   endif
6.21  *
6.21 c                   out       lda
6.21  *
6.34 c**                 call      'LO581R'
6.34 c                   call      'LO581C'
6.21 c                   parm                    loornr
6.21 c                   parm                    loorsu
6.21 c                   parm                    p_emal
6.21 c                   parm                    p_emne
6.21 c                   parm                    p_txt1
6.21 c                   parm                    p_txt2
6.21 c                   parm                    p_txt3
6.21 c                   parm                    p_txt4
6.21 c                   parm                    p_pers
6.31 c                   parm                    p_kopi
6.21  *
6.28 c                   endif
6.21  *
6.21 c                   endsr
6.21  *
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  * Subrutine for utlegg av ny linje
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  *
6.21 c     add_nylinje   begsr
6.21  * Sjekk om hvilket linjenummer denne skal ha på FODT
6.21 c                   eval      fodtlr_numm = loornr
6.21 c                   eval      fodtlr_suff = loorsu
6.21 c                   eval      fodtlr_line = *hival
6.21 c     fodtlr_key    setgt     fodtlr
6.21 c                   readp     fodtlr
6.21 c                   if        %found
6.21 c                   eval      p_line = fdline + 10
6.21 c                   endif
6.21  * Oppdater bestillingslinje med info om ordren
6.21 c                   eval      lodtlu_line = ldline
6.21 c     lodtlu_key    chain     lodtlur
6.21 c                   if        %found
6.21 c                   eval      ldornu = loornr
6.21 c                   eval      ldorsu = loorsu
6.21 c                   eval      ldorli = p_line
6.21 c                   update    lodtlur
6.21 c                   endif
6.21  * Hvis det skal varsles, skal vi logge endringen før
6.21 c                   if        lafvar = 'J'
6.21 c                   eval      fwbolu_type = 'FRA'
6.21 c                   exsr      oppd_logg
6.21 c                   endif
6.21  * Skaffelinje
6.2e c*                  if        ldlvre = 0
6.2e c*                  call      'FD120R'
6.2e c*                  parm                    b_oppr
6.2e c*                  parm                    w_firm
6.2e c*                  parm                    loornr
6.2e c*                  parm                    loorsu
6.2e c*                  parm                    p_line
6.2e c*                  parm                    faalty
6.2e c*                  parm                    folety
6.2e c*                  parm                    ldvare
6.2e c*                  parm                    ldanta
6.2e  *
6.2e c*                  else
6.21  * Vanlig linje
6.21 c                   call      'FD108R'
6.21 c                   parm                    p_kode
6.21 c                   parm                    w_firm
6.21 c                   parm                    loornr
6.21 c                   parm                    loorsu
6.21 c                   parm                    p_line
6.21 c                   parm                    faalty
6.21 c                   parm                    folety
6.21 c                   parm                    ldvare
6.21 c                   parm                    ldanta
6.21 c                   parm                    ldenh1
6.21 c                   parm                    foldat
6.21 c                   parm                    lonumm
6.21 c                   parm                    losuff
6.21 c                   parm                    ldline
6.21  *
6.2e c*                  endif
6.21  * Hvis det skal varsles, skal vi logge endringen etter
6.21 c                   if        lafvar = 'J'
6.21 c                   eval      fwbolu_type = 'TIL'
6.21 c                   exsr      oppd_logg
6.21 c                   endif
6.21  * Vi forusetter at det er gjort endringer. Skrur på endringsindikator
6.21 c                   eval      b_updo = *on
6.21  *
6.21 c                   endsr
6.21  *
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  * Subrutine for logg av endringer
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  *
6.21 c     oppd_logg     begsr
6.21  * Sjekk om varelinjen finnes fra før
6.21 c                   eval      b_newl = *off
6.21  *
6.21 c                   eval      fodtl1_numm = loornr
6.21 c                   eval      fodtl1_suff = loorsu
6.21 c                   eval      fodtl1_line = ldorli
6.21 c     fodtl1_key    chain     fodtl1r
6.21 c                   if        not %found
6.21 c                   eval      b_newl = *on
6.21 c                   endif
6.21  *
6.21 c                   eval      fwbolu_numm = loornr
6.21 c                   eval      fwbolu_suff = loorsu
6.21 c                   eval      fwbolu_line = ldorli
6.21 c     fwbolu_key    chain     fwbolur
6.21 c                   if        %found
6.21 c                   if        fwbolu_type = 'TIL'
6.21 c                   eval      wfvare = fdvare
6.21 c                   eval      wftek1 = fdtek1
6.21 c                   eval      wfanta = fdanta
6.21  * Logger kost
6.21 c                   eval      wfsapr = fdkopr
6.21 c                   eval      wfsums = fdsumk
6.21 c                   eval      wfbnum = ldnumm
6.21 c                   eval      wfenhe = fdenh1
6.21  *
6.21 c                   time                    wfedat
6.21 c                   time                    wfetim
6.21 c                   eval      wfeusr = l_user
6.21 c                   update    fwbolur
6.21 c                   endif
6.21 c                   else
6.21  *
6.21 c                   clear                   fwbolur
6.21  *
6.21 c                   eval      wffirm = w_firm
6.21 c                   eval      wfnumm = fwbolu_numm
6.21 c                   eval      wfsuff = fwbolu_suff
6.21 c                   eval      wfline = fwbolu_line
6.21 c                   eval      wftype = fwbolu_type
6.21 c                   eval      wfbnum = ldnumm
6.21 c                   if        b_newl = *on
6.21 c                   eval      wftek1 = 'Linje finnes ikke !!!'
6.21 c                   else
6.21 c                   eval      wfvare = fdvare
6.21 c                   eval      wftek1 = fdtek1
6.21 c                   eval      wfanta = fdanta
6.21  * Logger kost
6.21 c                   eval      wfsapr = fdkopr
6.21 c                   eval      wfsums = fdsumk
6.21 c                   eval      wfenhe = fdenh1
6.21 c                   endif
6.21  *
6.21 c                   time                    wfedat
6.21 c                   time                    wfetim
6.21 c                   eval      wfeusr = l_user
6.21 c                   time                    wfodat
6.21 c                   time                    wfotim
6.21 c                   eval      wfousr = l_user
6.21  *
6.21 c                   write     fwbolur
6.21  *
6.21 c                   endif
6.21  *
6.21 c                   eval      b_prtl = *on
6.21  *
6.21 c                   endsr
6.21  *
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  * Subrutine for logg av bestillingen
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  *
6.21 c     logg_bline    begsr
6.21  * Sjekk om varelinjen finnes fra før
6.21 c                   eval      b_newl = *off
6.21  *
6.21 c                   eval      fodtl1_numm = loornr
6.21 c                   eval      fodtl1_suff = loorsu
6.21 c                   eval      fodtl1_line = ldorli
6.21 c     fodtl1_key    chain     fodtl1r
6.21 c                   if        not %found
6.21 c                   eval      b_newl = *on
6.21 c                   endif
6.21  * Legg ut bestillingsinformasjon
6.21 c                   eval      fwbolu_numm = loornr
6.21 c                   eval      fwbolu_suff = loorsu
6.21 c                   eval      fwbolu_line = ldline
6.21 c                   eval      fwbolu_type = 'BES'
6.21 c     fwbolu_key    chain     fwbolur
6.21 c                   if        not %found
6.21 c                   clear                   fwbolur
6.21  *
6.21 c                   eval      wffirm = w_firm
6.21 c                   eval      wfnumm = fwbolu_numm
6.21 c                   eval      wfsuff = fwbolu_suff
6.21 c                   eval      wfline = fwbolu_line
6.21 c                   eval      wftype = fwbolu_type
6.21 c                   eval      wfbnum = ldnumm
6.21  *
6.21 c                   eval      wfvare = ldvare
6.21 c                   eval      wftek1 = ldtek1
6.21 c                   eval      wfanta = ldanta
6.21 c                   eval      wfsapr = ldipny
6.21 c                   eval      wfsums = ldsumi
6.21 c                   eval      wfenhe = ldenh1
6.21  *
6.21 c                   time                    wfedat
6.21 c                   time                    wfetim
6.21 c                   eval      wfeusr = l_user
6.21 c                   time                    wfodat
6.21 c                   time                    wfotim
6.21 c                   eval      wfousr = l_user
6.21  *
6.21 c                   write     fwbolur
6.21 c                   endif
6.21  *
6.21  * Legg ut ordrelinje
6.21 c                   eval      fwbolu_numm = loornr
6.21 c                   eval      fwbolu_suff = loorsu
6.21 c                   eval      fwbolu_line = ldline
6.21 c                   eval      fwbolu_type = 'ONR'
6.21 c     fwbolu_key    chain     fwbolur
6.21 c                   if        not %found
6.21 c                   clear                   fwbolur
6.21  *
6.21 c                   eval      wffirm = w_firm
6.21 c                   eval      wfnumm = fwbolu_numm
6.21 c                   eval      wfsuff = fwbolu_suff
6.21 c                   eval      wfline = fwbolu_line
6.21 c                   eval      wftype = fwbolu_type
6.21 c                   eval      wfbnum = ldnumm
6.21 c                   if        b_newl = *on
6.21 c                   eval      wftek1 = 'Linje finnes ikke !!!'
6.21 c                   else
6.21 c                   eval      wfvare = fdvare
6.21 c                   eval      wftek1 = fdtek1
6.21 c                   eval      wfanta = fdanta
6.21  * Logger kost
6.21 c                   eval      wfsapr = fdkopr
6.21 c                   eval      wfsums = fdsumk
6.21 c                   eval      wfenhe = fdenh1
6.21 c                   endif
6.21  *
6.21 c                   time                    wfedat
6.21 c                   time                    wfetim
6.21 c                   eval      wfeusr = l_user
6.21 c                   time                    wfodat
6.21 c                   time                    wfotim
6.21 c                   eval      wfousr = l_user
6.21  *
6.21 c                   write     fwbolur
6.21 c                   endif
6.21  *
6.21 c                   eval      b_prtl = *on
6.21  *
6.21 c                   endsr
6.21  *
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  * Subrutine for logg av tekstinfomasjon
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  *
6.21 c     logg_txt      begsr
6.21  * Sjekk om varelinjen finnes fra før
6.21  *
6.21 c                   eval      fwbolu_numm = loornr
6.21 c                   eval      fwbolu_suff = loorsu
6.21 c                   eval      fwbolu_line = 9999
6.21 c     fwbolu_key    chain     fwbolur
6.21 c                   if        %found
6.21  * Tekstinformasjon er allerede initiert
6.21 c                   time                    wfedat
6.21 c                   time                    wfetim
6.21 c                   eval      wfeusr = l_user
6.21 c                   update    fwbolur
6.21 c                   else
6.21  *
6.21 c                   clear                   fwbolur
6.21  *
6.21 c                   eval      wffirm = w_firm
6.21 c                   eval      wfnumm = fwbolu_numm
6.21 c                   eval      wfsuff = fwbolu_suff
6.21 c                   eval      wfline = fwbolu_line
6.21 c                   eval      wftype = fwbolu_type
6.21 c                   eval      wftek1 = w_tek1
6.21 c                   time                    wfedat
6.21 c                   time                    wfetim
6.21 c                   eval      wfeusr = l_user
6.21 c                   time                    wfodat
6.21 c                   time                    wfotim
6.21 c                   eval      wfousr = l_user
6.21  *
6.21 c                   write     fwbolur
6.21  *
6.21 c                   endif
6.21  *
6.21 c                   eval      b_prtl = *on
6.21  *
6.21 c                   endsr
6.21  *
7.05  *
7.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.05  * Subrutine for hurtigutskrift
7.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.05  *
7.05 c     xskriv_best   begsr
7.05  *
7.05 c                   exsr      oppdat_best
7.05 c                   call      'LO600R'
7.05 c                   parm                    w_stat
7.05 c                   parm                    w_firm
7.05 c                   parm                    w_numm
7.05 c                   parm                    w_suff
7.05 c                   parm                    w_svar
7.05  *
7.05 c                   endsr
7.05  *
7.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.05  * Subrutine for hurtig sending av bestilling
7.05  *  - finnes ikke distribusjonsinfo, prøv utskrift
7.05  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.05  *
7.05 c     xsend_best    begsr
7.05  *
7.05 c                   exsr      oppdat_best
7.05  *
7.05 c                   eval      lldilr_ldor = loldor
7.05 c                   move      lolage        lldilr_lage
7.05 c     lldilr_key    chain     lldilr
7.05 c                   if        not %found
7.05 c                   eval      lldilr_lage = *blank
7.05 c     lldilr_key    chain     lldilr
7.05  *
7.05 c                   if        not %found
7.05 c                   exsr      xskriv_best
7.05 c                   goto      end_send
7.05 c                   endif
7.05 c                   endif
7.05  *
7.05 c                   call      'LO740R'
7.05 c                   parm                    w_numm
7.05 c                   parm                    w_suff
7.05 c                   parm                    w_retk
7.05  *
7.05 c     end_send      endsr
7.10  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.10  * Subrutine for å behandle kalkulering av vekt og volum
7.10  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.10  *
7.10 c     cal_line      begsr
7.10  *
7.10 c                   eval      b1vekt = 0
7.10 c                   eval      b1volu = 0
7.10  *
7.10 c                   if        ldvare = *blank
7.10 c                   goto      end_cal
7.10 c                   endif
7.10  *
7.10  * Behandler ikke tjenester og diversevarer
7.10  *
7.10 c                   eval      vvarl1_vare = ldvare
7.10 c     vvarl1_key    chain     vvarl1
7.10 c                   if        not %found
7.10 c     vvarl1_key    chain     vvasl1
7.10 c                   endif
7.10  *
7.10  * Kaller program for henting av varetype
7.10  *
7.10 c                   call      'VL710R'
7.10 c                   parm                    w_firm
7.10 c                   parm                    vvvare
7.10 c                   parm                    ldlage
7.10 c                   parm                    w_vtyp
7.10 c                   parm                    w_utda
7.10 c                   parm                    loldor
7.10 c                   parm                    b_inlr
7.10 c                   parm                    w_lv_nobb
7.10 c                   parm                    w_lv_modn
7.10 c                   parm                    w_lv_lldo
7.10 c                   parm                    w_lv_llva
7.10  *
7.10 c                   if        w_vtyp = 'T' or
7.10 c                             w_vtyp = 'D'
7.10 c                   goto      end_cal
7.10 c                   endif
7.10  *
7.10  * Finner vekt og volum
7.10  *
7.10 c                   eval      vvenl1_vare = ldvare
7.10 c                   eval      vvenl1_enhe = ldenh1
7.10 c     vvenl1_key    chain     vvenl1
7.10 c                   if        not %found or
7.10 c                             vevekt = 0 or
7.10 c                             vevolu = 0
7.10 c                   eval      jvpkl2_vare = ldvare
7.10 c                   eval      jvpkl2_ldor = loldor
7.10 c                   eval      jvpkl2_enhe = ldenh1
7.10 c     jvpkl2_key    chain     jvpkl2
7.10 c                   if        not %found
7.10 c                   eval      jvpkl2_vare = ldvare
7.10 c                   eval      jvpkl2_ldor = vvnlev
7.10 c                   eval      jvpkl2_enhe = ldenh1
7.10 c     jvpkl2_key    chain     jvpkl2
7.10 c                   endif
7.10 c                   endif
7.10  *
7.10 c                   if        vevekt <> 0
7.10 c                   eval      w_vekt = vevekt
7.10 c                   else
7.10 c                   eval      w_vekt = jwvekt / 1000
7.10 c                   endif
7.10  *
7.10 c                   if        vevolu <> 0
7.10 c                   eval      w_volu = vevolu
7.10 c                   else
7.10 c                   eval      w_volu = jwvolu
7.10 c                   endif
7.10  *
7.10  * Summerer vekt og volum
7.10  *
7.20 c                   if        (w_vekt * ldanta) > 99999
7.20 c                   eval      b1vekt = 99999
7.20 c                   else
7.10 c                   eval      b1vekt = (w_vekt * ldanta)
7.20 c                   endif
7.20 c                   if        (w_volu * ldanta) > 999,999
7.20 c                   eval      b1volu = 999,999
7.20 c                   else
7.10 c                   eval      b1volu = (w_volu * ldanta)
7.20 c                   endif
7.10  *
7.10  *
7.10 c     end_cal       endsr
7.17  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.17  * Newlook vask av data
7.17  *  Fjerner tegn som gjør at Newlook feiltolker
7.17  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.17  *
7.17  /FREE
7.17
7.17   begsr newlo_vask;
7.17     for w_teller = 1 to 5 ;
7.17       w_len = %len(%trim(b1tek1));
7.17       if (w_len > 0);
7.17       b1tek1 = %ScanRpl('.':'':b1tek1:w_len); // newlook tåler ikke . på slutten
7.17       endif;
7.17       w_len = %len(%trim(b1tek1));
7.17       if (w_len > 0);
7.17       b1tek1 = %ScanRpl(':':'':b1tek1:w_len); // newlook tåler ikke : på slutten
7.17       endif;
7.17     endfor ;
7.17   endsr;
7.17  /END-FREE
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *dtaara       define    *lda          lda
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_stat
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
7.23 c                   parm                    p_nybe
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på koderegister
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på ordretype
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for oppslag på linjetype
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på vare m/lev.varenr
      *
     c     vvarl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl4_lvar
      *
      * Key for oppslag på odre-hode
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for oppdatering av odre-hode
      *
     c     lohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for les på bestilling-linje
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
     c                   kfld                    lodtl1_line
      *
      * Key for les/oppslag på bestilling-linje
      *
     c     lodtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
     c                   kfld                    lodtlr_line
      *
     c     lodtlr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for oppdatering av bestilling-linje
      *
     c     lodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
     c                   kfld                    lodtlu_line
      *
      * Key for oppslag på bruker-registrer
      *
     c     lusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lusrl1_user
6.13  *
6.13  * Key for oppslag på leverandør distribusjon
6.13  *
6.13 c     lldilr_key    klist
6.13 c                   kfld                    w_firm
6.13 c                   kfld                    lldilr_ldor
6.13 c                   kfld                    lldilr_lage
6.21  *
6.21  * Key for oppslag på synkeregister
6.21  *
6.21 c     lsbol1_key    klist
6.21 c                   kfld                    w_firm
6.27  *
6.27  * Key for oppslag på odre-hode
6.27  *
6.27 c     fohell_key    klist
6.27 c                   kfld                    w_firm
6.27 c                   kfld                    fohell_numm
6.21  *
6.21  * Key for oppslag på odre-hode
6.21  *
6.21 c     fohel1_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    fohel1_numm
6.21 c                   kfld                    fohel1_suff
6.21  *
6.21  * Key for oppslag på odre-linjer
6.21  *
6.21 c     fodtl1_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    fodtl1_numm
6.21 c                   kfld                    fodtl1_suff
6.21 c                   kfld                    fodtl1_line
6.21  *
6.21  * Key for oppslag på odre-linjer
6.21  *
6.21 c     fodtlr_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    fodtlr_numm
6.21 c                   kfld                    fodtlr_suff
6.21 c                   kfld                    fodtlr_line
6.2h  *
6.2h  * Key for oppdatering av Ordre-linje-register
6.2h  *
6.2h c     fodtlu_key    klist
6.2h c                   kfld                    w_firm
6.2h c                   kfld                    fodtlu_numm
6.2h c                   kfld                    fodtlu_suff
6.2h c                   kfld                    fodtlu_line
6.21  *
6.21  * Key for arbeidsregister for endringer
6.21  *
6.21 c     fwbolu_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    fwbolu_numm
6.21 c                   kfld                    fwbolu_suff
6.21 c                   kfld                    fwbolu_line
6.21 c                   kfld                    fwbolu_type
6.28  *
6.28  * Key for les av leverandør distribusjon
6.28  *
6.28 c     fwbolr_key    klist
6.28 c                   kfld                    w_firm
6.28 c                   kfld                    fwbolr_numm
6.28 c                   kfld                    fwbolr_suff
6.21  *
6.21  * Key for oppslag på selger-register
6.21  *
6.21 c     ra09lr_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    ra09lr_ikod
6.21  *
6.21  * Key for oppslag på koderegister
6.21  *
6.21 c     fstsl1_key    klist
6.21 c                   kfld                    w_firm
7.04  *
7.04  * Key for oppslag på elektroniske pakksedler
7.04  *
7.04 c     lbhoi4_key    klist
7.04 c                   kfld                    w_firm
7.04 c                   kfld                    lbhoi4_hnum
7.04 c                   kfld                    lbhoi4_hsuf
7.08  *
7.08  * Key for les av leverandør distribusjon
7.08  *
7.08 c     lldil1_key    klist
7.08 c                   kfld                    w_firm
7.08 c                   kfld                    lldil1_ldor
7.08 c                   kfld                    lldil1_lage
7.10  *
7.10  * Key for les av enhet
7.10  *
7.10 c     vvenl1_key    klist
7.10 c                   kfld                    w_firm
7.10 c                   kfld                    vvenl1_vare
7.10 c                   kfld                    vvenl1_enhe
7.10  *
7.10  * Key for oppslag på vare-pakning (VA)
7.10  *
7.10 c     jvpkl2_key    klist
7.10 c                   kfld                    jvpkl2_vare
7.10 c                   kfld                    jvpkl2_ldor
7.10 c                   kfld                    jvpkl2_enhe
      *
      * Legger inn type for oppdatering av lager  R=Registrering
      *
     c                   move      'R'           hltype
      *
      * Henter verdier fra parmametre
      *
     c                   move      p_firm        w_firm
     c                   move      p_numm        w_numm
     c                   move      p_suff        w_suff
7.12 c                   eval      w_valg = 1
7.06  *
7.06  * Endring 7.06
7.06  *
7.06   CallP CO402R(l_filg:w_firm:0:'LD100R_706':
7.06                    co402_verdi1:co402_verdi2);
7.06   if co402_verdi1 = '1';
7.06     u_706 = *on;
7.06   endif;
7.12  *
7.12  * Endring 7.12
7.12  *
7.12   CallP CO402R(l_filg:w_firm:0:'LD100R_712':
7.12                    co402_verdi1:co402_verdi2);
7.12   if co402_verdi1 = '1';
7.12    if %trim(co402_verdi2) = '0';
7.12     w_valg = 0;
7.12    endif;
7.12    if %trim(co402_verdi2) = '1';
7.12     w_valg = 1;
7.12    endif;
7.12    if %trim(co402_verdi2) = '2';
7.12     w_valg = 2;
7.12    endif;
7.12    if %trim(co402_verdi2) = '3';
7.12     w_valg = 3;
7.12    endif;
7.12   endif;
      *
      * Henter statusopplysninger fra status-register
      *
     c     lstsl1_key    chain     lstsl1
6.21  *
6.21  * Henter inn informasjon om synkronisering
6.21  *
6.21 c                   eval      b_synk = *off
6.21 c     lsbol1_key    chain     lsbol1
6.21 c                   if        %found
6.21 c                   if        laftyp = 'M' or
6.21 c                             laftyp = 'A'
6.21 c                   eval      b_synk = *on
6.21 c                   endif
6.21 c                   endif
      *
      * Henter opplysninger fra bruker
      *
     c                   eval      lusrl1_user = l_user
     c     lusrl1_key    chain     lusrl1
      *
      * Sett indikator som styrer kostpris i henhold til kode på bruker
      *
     c                   if        lbko01 = 1
     c                   eval      *in49 = *on
     c                   else
     c                   eval      *in49 = *off
     c                   endif
      *
     c                   eval      b_kost = *in49
      *
      * Sett indikator som styrer varenummer/ean-nummer
      *
     c                   eval      *in87 = *on
6.21  *
6.21  * Hent opplysninger fra ofak koderegister
6.21  *
6.21 c     fstsl1_key    chain     fstsl1r                            90
      *
     c                   endsr
**     M e l d i n g e r
Kostpris er endret på salgsordren                 <---
