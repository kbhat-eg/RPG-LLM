      /TITLE  Utskrift av kontoutdrag  - leverandører
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RL642R                                              *
      * Beskrivelse . . : Utskrift av kontoutdrag  - leverandører             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
T5.30 * V5.30 220704 HFL  Skrev innstillingslinje ubetinget.
T5.30 *                   Skrev også kunder uten poster og saldo.
T5.30 * V5.30 130804 HFL  Skriver leverandørens fakturanr.
T5.30 * V5.30 030904 KOB  Kan skrive kontoutdrag på enkeltleverandør.
T5.40 * V5.40 091106 HFL  Skriver foretaksnummer.
T5.30 * V5.40 301208 KOB  Endret til å skrive 132 pos pr. side
      *                   og antall pos. bkjøp til 13.2                       *
T5.50 * V5.50 280907 HFL  Tilpasser bilagstekst når lev.faktura er 15 pos.
      *                                                                       *
T6.10 * V6.10 090210 NHO  Tester ikke på restbeløp dersom 'J' til val. os.
pdf   * R6.10 291110 BSA  Innført PDF utskrift. Innebærer primært å finne     *
pdf   *                   generert spool, og kall av AP620R med dette som     *
pdf   *                   parametre.                                          *
6.20  * R6.20     080911  Rekompilert pga endring i RPARRKDS            *NHO
6.21  * 6.20  061212 bsa  Tillegg for å berike metataggger
6.30  * R6.30 180816 bsa  Rokkert på rekkefølge av close printerfile
7.01  * 7.01  031219 ask  Felt i printerfil er forlenget
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frlw1l1    if   e           k disk    rename(rlw1pfr:rlw1l1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frltrl1    if   e           k disk    rename(rltrpfr:rltrl1r)
     faforl1    if   e           k disk    rename(aforpfr:aforl1r)
     fra01l1    if   e           k disk    rename(ra01pfr:ra01l1r)
     frl642p    o    e             printer oflind(*in30)
pdf  f                                     usropn
      *
     d txt             s             20    dim(4) ctdata perrcd(1)              Tekst ukjent
     d kna             s             30    dim(10)                              Lever. navn
      *
      /eject
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      * Data struktur
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      *
     d                 ds
     d wkwrec                  2    192
     d  wkfirm                        3  0 overlay(wkwrec)
     d  wklevr                        6  0 overlay(wkwrec:4)
     d  wknavn                       30    overlay(wkwrec:10)
      *
     d w_sald          s                   like(robelØ)
     d w_salv          s                   like(rovalb)
     d w_gsal          s                   like(robelØ)
     d w_gval          s                   like(rovalb)
     d w_kjØp          s                   like(rovalb)
     d w_kjØv          s                   like(rovalb)
     d*bkjØp           s                   like(robelØ)
     d btink           s                   like(robelØ)
     d btsal           s                   like(robelØ)
     d tkjØp           s                   like(robelØ)
     d w_pdat          s              6  0
      *
     d brsum           s              1
     d brtxt           s             20
     d n               s              2  0
     d wkun            s              7  0
      *
     d pavdsx          s              3
     d pkatsx          s              3
     d pselsx          s              3
     d psvalx          s              3
     d psortx          s             10
      *
     d w_anta          s              6  0
     d w_usal          s              6  0
     d w_post          s              6  0
      *
     d w_prfi          s              1  0
     d w_ffir          s              3  0
     d w_fnav          s             30
     d w_fad1          s             30
     d w_fad2          s             30
     d w_fste          s             30
     d w_fftx          s             30
     d w_tfax          s              8
     d w_ftlf          s             11
     d w_ffax          s             11
     d w_tbnk          s             10
     d w_fbgi          s             13
     d w_aprt          s              1  0
     d w_avde          s              4  0
T5.30d w_lengde        s              2  0
T5.30d w_text          s             20
      *
     d w_firm          s                   like(rlfirm)
      *
     d rlevl1_levr     s                   like(rllevr)
     d rltrl1_levr     s                   like(rolevr)
     d ra01l1_akod     s                   like(raakod)
     d aforl1_ruti     s                   like(afruti)
      *
pdf  d p_btyp          s            100
      *
pdf  d splfname2       s             10a
pdf  d jobname2        s             10a
pdf  d username2       s             10a
pdf  d jobnbr2         s              6a
pdf  d splfnbr2        s             10i 0
pdf  d splfname3       s             10a
pdf  d jobname3        s             10a
pdf  d username3       s             10a
pdf  d jobnbr3         s              6a
pdf  d splfnbr3        s             10i 0
6.21 d p_tagg0         s             20
6.21 d p_tagg0val      s             50
6.21 d p_tagg1         s             20
6.21 d p_tagg1val      s             50
6.21 d p_tagg2         s             20
6.21 d p_tagg2val      s             50
6.21 d p_tagg3         s             20
6.21 d p_tagg3val      s             50
6.21 d p_tagg4         s             20
6.21 d p_tagg4val      s             50
6.21 d p_tagg5         s             20
6.21 d p_tagg5val      s             50
6.21 d p_tagg6         s             20
6.21 d p_tagg6val      s             50
6.21 d p_tagg7         s             20
6.21 d p_tagg7val      s             50
6.21 d p_tagg8         s             20
6.21 d p_tagg8val      s             50
6.21 d p_tagg9         s             20
6.21 d p_tagg9val      s             50
6.21 d p_refn          s             50
6.21 d p_dspl          s            100
pdf   *
pdf  d qsprilsp        pr                  extpgm('QSPRILSP')
pdf  d rcvvar                     32767a   options(*varsize)
pdf  d rcvvarlen                     10i 0 const
pdf  d format                         8a   const
pdf  d errorcode                  32767a   options(*varsize)
pdf   *
pdf  d sprl0100        ds
pdf  d  bytesrtn                     10i 0
pdf  d  bytesavl                     10i 0
pdf  d  splfname                     10a
pdf  d  jobname                      10a
pdf  d  username                     10a
pdf  d  jobnbr                        6a
pdf  d  splfnbr                      10i 0
pdf  d  systemname                    8a
pdf  d  createdate                    7a
pdf  d                                1a
pdf  d  createtime                    6a
pdf   *
pdf  d errorcode       ds
pdf  d  bytesprov                    10i 0 inz(0)
pdf  d  byteavail                    10i 0 inz(0)
pdf   *
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      * LOCAL DATA
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     d/COPY QCPYLESRC,RPARRKDS
      *
pdf  d l_poff                584    584
pdf  d l_bach                595    635
pdf  d l_skje                637    637  0
     d l_ruti                800    809
     d l_oflw                860    862  0
     d l_kidk                883    883  0
     d l_ocrk                885    885  0
      *
      /eject
      *
      * Leser transfile
      *
     c     nyles         tag
      *
     c                   read      rlw1l1                                 60
      *
     c                   if        *in60 = *on
     c                   eval      *inlr = *on
     c                   goto      slutt
     c                   endif
      *
     c                   movel     rwkrec        wkwrec
      *
     c                   eval      page = 0
     c                   eval      w_gsal = 0                                   Saldo
     c                   eval      w_sald = 0                                   Saldo
     c                   eval      w_kjØp = 0                                   Saldo
     c                   eval      h1gsad = 0                                   Saldo
     c                   eval      h1gsak = 0                                   Saldo
     c                   eval      t1kjØp = 0
     c                   eval      t1sald = 0                                   Saldo
     c                   eval      t1salk = 0                                   Saldo
      *
      * Leser leverandør
      *
     c                   move      wklevr        rlevl1_levr
     c     rlevl1_key    chain     rlevl1                             61
      *
      * Leser leverandørposteringer
      *
     c                   exsr      les_kuntr
      *
      * Telle leverandører
      *
     c                   eval      w_anta = w_anta + 1
      *
     c                   if        w_sald <> *zeros
     c                   eval      w_msal = w_msal + 1
     c                   else
     c                   eval      w_usal = w_usal + 1
     c                   endif
      *
      * Skriv ut totaler pr leverandør
      *
T5.30c                   if        w_post = 0 and
T5.30c                             w_sald <> 0
T5.30c                   write     h101
T5.30c                   endif
      *
E5.30c                   if        w_post > 0 or
T5.30c                             w_sald <> 0
      *
     c                   if        *in15 = *off
      *
     c                   if        w_sald > *zeros
     c                   eval      t1sald = w_sald
     c                   eval      t1salk = 0                                   Saldo
     c                   eval      *in33 = *on                                  Saldo
     c                   else
     c                   eval      t1salk = w_sald
     c                   eval      t1sald = 0                                   Saldo
     c                   eval      *in33 = *off                                 Saldo
     c                   endif
      *
     c                   eval      t1kjØp = w_kjØp
     c                   eval      btsal = btsal + w_sald
     c                   eval      bkjØp = bkjØp + w_kjØp
      *
     c                   else
      *
     c                   if        w_salv > *zeros
     c                   eval      t1sald = w_salv
     c                   eval      t1salk = 0                                   Saldo
     c                   eval      *in33 = *on                                  Saldo
     c                   else
     c                   eval      t1salk = w_salv
     c                   eval      t1sald = 0                                   Saldo
     c                   eval      *in33 = *off                                 Saldo
     c                   endif
      *
     c                   eval      t1kjØp = w_kjØv
     c                   eval      btsal = btsal + w_salv
     c                   eval      bkjØp = bkjØp + w_kjØv
      *
      *
     c                   endif
      *
     c                   write     t10s
      *
     c                   endif
      *
     c                   goto      nyles
      *
     c     slutt         tag
      *
      * Skriver header LR hvis flere enn 1 leverandør
      *
     clr                 if        w_anta > 1
     clr                 write     t101
     clr                 write     t102
      *
      * Skriver  parameter - Utvalg enkeltleverandører
      *
     clr                 eval      n = n + 1
     clr                 dou       n > 10                                       ..............
     clr                 if        kun(n) <> *zeros                                          .
     clr                 write     t102k                                                     .
     clr                 endif                                                               .
     clr                 eval      n = n + 1                                                 .
     clr                 enddo                                                  ..............
     clr                 endif                                                  ..............
      *
pdf   * Generer xml for videre utskrift
pdf  clr                 if        l_poff = '1'
pdf  clr                 exsr      spoolnbr
6.30 clr                 close     rl642p
6.30 clr                 exsr      xml_create
pdf  clr                 exsr      pdf_preview
pdf   * Avslutt jobbiden
pdf  clr                 call      'AB710R'
pdf  c                   parm                    l_bach
pdf  clr                 endif
      /eject
      *
      * Leser leverandørtransaksjoner
      *
     c     les_kuntr     begsr
      *
     c                   eval      w_post    = *zeros
     c                   eval      t1sdat = *zeros
     c                   eval      w_pdat = *zeros
     c                   eval      *in41 = *off
      *
     c                   eval      rltrl1_levr = rllevr
     c     rltrl1_key    setll     rltrl1
     c     rltrl1_key    reade     rltrl1                                 62
      *
     c                   dow       *in62 = *off
      *
      * År/periode
      *
     c                   if        roraar > paart
     c                   goto      nesttr
     c                   endif
      *
     c                   if        roraar = paart and
     c                             rorper > ppert
     c                   goto      nesttr
     c                   endif
      *
      * Finn saldo pr. dato
      *
     c                   eval      *in40 = *off
      *
     c                   if        roraar < paarf
     c                   eval      w_gsal = w_gsal + robelØ
     c                   eval      w_gval = w_gval + rovalb
     c     *dmy          move      robdat        w_pdat
     c                   eval      *in40 = *on
     c                   endif
      *
     c                   if        roraar = paarf and
     c                             rorper < pperf
     c                   eval      w_gsal = w_gsal + robelØ
     c                   eval      w_gval = w_gval + rovalb
     c     *dmy          move      robdat        w_pdat
     c                   eval      *in40 = *on
     c                   endif
      *
      * Ta vare på siste transaksjonsdato
      *
     c     *dmy          move      robdat        t1sdat
      *
      * Beregn saldo
      *
     c                   eval      w_sald = w_sald + robelØ                     Saldo
     c                   eval      w_salv = w_salv + rovalb                     Saldo
      *
     c                   if        *in40 = *on
     c                   goto      nesttr
     c                   endif
      *
      * Skriver heading ny side
      *
     c                   if        *in41 = *off
      *
     c                   write     h101
      *
      * Skriver pr. dato (inng. saldo)
      *
     c                   if        *in15 = *off
      *
     c                   if        w_gsal > *zeros
     c                   eval      h1gsad = w_gsal
     c                   eval      h1gsak = 0                                   Saldo
     c                   eval      *in31 = *on                                  Saldo
     c                   else
     c                   eval      h1gsak = w_gsal
     c                   eval      h1gsad = 0                                   Saldo
     c                   eval      *in31 = *off                                 Saldo
     c                   endif
      *
     c                   if        w_gsal <> 0
     c                   write     h10s
     c                   endif
      *
     c                   else
      *
     c                   if        w_gval > *zeros
     c                   eval      h1gsad = w_gval
     c                   eval      h1gsak = 0                                   Saldo
     c                   eval      *in31 = *on                                  Saldo
     c                   else
     c                   eval      h1gsak = w_gval
     c                   eval      h1gsad = 0                                   Saldo
     c                   eval      *in31 = *off                                 Saldo
     c                   endif
      *
     c                   if        w_gval <> 0
     c                   write     h10s
     c                   endif
      *
     c                   endif
      *
     c                   eval      *in41 = *on
     c                   endif
      *
      * Overflow ?
      *
     c                   if        *in30 = *on
     c                   write     h101
     c                   eval      *in30 = *off
     c                   endif
      *
      * Sjekker bilagskode for kjøp i år
      *
     c                   eval      ra01l1_akod = robilk
     c     ra01l1_key    chain     ra01l1                               63       bilags-
      *
     c                   if        raakjØ = '1'
      *
     c                   if        *in15 = *off
     c                   eval      w_kjØp = w_kjØp + robelØ
     c                   else
     c                   eval      w_kjØv = w_kjØv + rovalb
     c                   endif
      *
     c                   endif
      *
      * Skriver detalj-linje
      *
     c                   if        *in15 = *off
      *
     c                   if        robelØ > 0
     c                   eval      d1debe = robelØ
     c                   eval      d1kred = 0
     c                   eval      *in32 = *on                                  Saldo
     c                   else
     c                   eval      d1kred = robelØ
     c                   eval      d1debe = 0
     c                   eval      *in32 = *off                                 Saldo
     c                   endif
      *
     c                   else
      *
6.10 c                   if        rovalb > 0
     c                   eval      d1debe = rovalb
     c                   eval      d1kred = 0
     c                   eval      *in32 = *on                                  Saldo
     c                   else
     c                   eval      d1kred = rovalb
     c                   eval      d1debe = 0
     c                   eval      *in32 = *off                                 Saldo
     c                   endif
      *
     c                   endif
      *
     c     *dmy          move      robdat        d1bdat
     c     *dmy          move      rofdat        d1fdat
      *
     c                   eval      w_post = w_post + 1
      *
T5.30 * Hvis leverandørens fakturanr. finnes, legges det i tekst.
      *
T5.30c                   if        rolfak <> *blanks
T5.30c                   eval      w_lengde = %len(%trim(rolfak))
T5.30c                   select
T5.30c                   when      w_lengde = 1
T5.30c                   eval      w_text = %subst(rotext:1:18) + ' ' +
T5.30c                             %trim(rolfak)
T5.30c                   when      w_lengde = 2
T5.30c                   eval      w_text = %subst(rotext:1:17) + ' ' +
T5.30c                             %trim(rolfak)
T5.30c                   when      w_lengde = 3
T5.30c                   eval      w_text = %subst(rotext:1:16) + ' ' +
T5.30c                             %trim(rolfak)
T5.30c                   when      w_lengde = 4
T5.30c                   eval      w_text = %subst(rotext:1:15) + ' ' +
T5.30c                             %trim(rolfak)
T5.30c                   when      w_lengde = 5
T5.30c                   eval      w_text = %subst(rotext:1:14) + ' ' +
T5.30c                             %trim(rolfak)
T5.30c                   when      w_lengde = 6
T5.30c                   eval      w_text = %subst(rotext:1:13) + ' ' +
T5.30c                             %trim(rolfak)
T5.30c                   when      w_lengde = 7
T5.30c                   eval      w_text = %subst(rotext:1:12) + ' ' +
T5.30c                             %trim(rolfak)
T5.30c                   when      w_lengde = 8
T5.30c                   eval      w_text = %subst(rotext:1:11) + ' ' +
T5.30c                             %trim(rolfak)
T5.30c                   when      w_lengde = 9
T5.30c                   eval      w_text = %subst(rotext:1:10) + ' ' +
T5.30c                             %trim(rolfak)
T5.50c                   when      w_lengde = 10
T5.50c                   eval      w_text = %subst(rotext:1:9) + ' ' +
T5.50c                             %trim(rolfak)
T5.50c                   when      w_lengde = 11
T5.50c                   eval      w_text = %subst(rotext:1:8) + ' ' +
T5.50c                             %trim(rolfak)
T5.50c                   when      w_lengde = 12
T5.50c                   eval      w_text = %subst(rotext:1:7) + ' ' +
T5.50c                             %trim(rolfak)
T5.50c                   when      w_lengde = 13
T5.50c                   eval      w_text = %subst(rotext:1:6) + ' ' +
T5.50c                             %trim(rolfak)
T5.50c                   when      w_lengde = 14
T5.50c                   eval      w_text = %subst(rotext:1:5) + ' ' +
T5.50c                             %trim(rolfak)
T5.50c                   when      w_lengde = 15
T5.50c                   eval      w_text = %subst(rotext:1:4) + ' ' +
T5.50c                             %trim(rolfak)
T5.30c                   endsl
T5.30c                   eval      rotext = w_text
T5.30c                   endif
     c                   write     d101
      *
     c     nesttr        tag
      *
     c     rltrl1_key    reade     rltrl1                                 62
     c                   enddo                                                     fordeling
      *
     c                   endsr
      *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Finner spool som er generert av jobben
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     spoolnbr      begsr
pdf   /Free
pdf
pdf            QSPRILSP( SPRL0100
pdf                    : %size(SPRL0100)
pdf                    : 'SPRL0100'
pdf                    : errorcode );
pdf   /End-free
pdf  c*
pdf   *
pdf  c                   endsr
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Kaller eget program for generering av xml fil. Eget pgm         *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     xml_create    begsr
pdf   *
pdf  c                   eval      splfname2 = *blanks
pdf  c                   eval      splfname3 = *blanks
pdf  c                   eval      jobname2 = *blanks
pdf  c                   eval      jobname3 = *blanks
pdf  c                   eval      username2 = *blanks
pdf  c                   eval      username3 = *blanks
pdf  c                   eval      jobnbr2 = *blanks
pdf  c                   eval      jobnbr3 = *blanks
pdf  c                   eval      splfnbr2 = *zeros
pdf  c                   eval      splfnbr3 = *zeros
pdf  c                   eval      p_btyp = 'KONTOUTDRAG'
6.21 c                   eval      p_dspl = 'Kontoutdrag leverandører, '+
6.21 c                                      ' kjørt dato: '+
6.21 c                                      %char(udate)
6.21 c                   eval      p_refn = 'LKU-' + %char(udate)
6.21 c                   eval      p_tagg0 = *blanks
6.21 c                   eval      p_tagg0val = *blanks
6.21 c                   eval      p_tagg1 = *blanks
6.21 c                   eval      p_tagg1val = *blanks
6.21 c                   eval      p_tagg2 = *blanks
6.21 c                   eval      p_tagg2val = *blanks
6.21 c                   eval      p_tagg3 = *blanks
6.21 c                   eval      p_tagg3val = *blanks
6.21 c                   eval      p_tagg4 = *blanks
6.21 c                   eval      p_tagg4val = *blanks
6.21 c                   eval      p_tagg5 = *blanks
6.21 c                   eval      p_tagg5val = *blanks
6.21 c                   eval      p_tagg6 = *blanks
6.21 c                   eval      p_tagg6val = *blanks
6.21 c                   eval      p_tagg7 = *blanks
6.21 c                   eval      p_tagg7val = *blanks
6.21 c                   eval      p_tagg8 = *blanks
6.21 c                   eval      p_tagg8val = *blanks
6.21 c                   eval      p_tagg9 = *blanks
6.21 c                   eval      p_tagg9val = *blanks
pdf   *
6.21 c**                 call      'AP620R'
6.21 c                   call      'AP627R'
pdf  c                   parm                    splfname
pdf  c                   parm                    jobname
pdf  c                   parm                    username
pdf  c                   parm                    jobnbr
pdf  c                   parm                    splfnbr
pdf  c                   parm                    splfname2
pdf  c                   parm                    jobname2
pdf  c                   parm                    username2
pdf  c                   parm                    jobnbr2
pdf  c                   parm                    splfnbr2
pdf  c                   parm                    splfname3
pdf  c                   parm                    jobname3
pdf  c                   parm                    username3
pdf  c                   parm                    jobnbr3
pdf  c                   parm                    splfnbr3
6.21 c                   parm                    p_tagg0
6.21 c                   parm                    p_tagg0val
6.21 c                   parm                    p_tagg1
6.21 c                   parm                    p_tagg1val
6.21 c                   parm                    p_tagg2
6.21 c                   parm                    p_tagg2val
6.21 c                   parm                    p_tagg3
6.21 c                   parm                    p_tagg3val
6.21 c                   parm                    p_tagg4
6.21 c                   parm                    p_tagg4val
6.21 c                   parm                    p_tagg5
6.21 c                   parm                    p_tagg5val
6.21 c                   parm                    p_tagg6
6.21 c                   parm                    p_tagg6val
6.21 c                   parm                    p_tagg7
6.21 c                   parm                    p_tagg7val
6.21 c                   parm                    p_tagg8
6.21 c                   parm                    p_tagg8val
6.21 c                   parm                    p_tagg9
6.21 c                   parm                    p_tagg9val
pdf  c                   parm                    l_bach
pdf  c                   PARM                    p_btyp
6.21 c                   PARM                    p_refn
6.21 c                   PARM                    p_dspl
pdf   *
pdf  c                   endsr
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf   * Subrutiner for XML/jasper integrasjon - Skjermvisning           *
pdf   * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
pdf  c     pdf_preview   begsr
pdf  c                   if        l_skje = 1
pdf   * Vi kaller program for launch av PDF i browser
pdf  c                   call      'AP621R'
pdf  c                   parm                    l_bach
pdf   *
pdf  c                   endif
pdf   *
pdf  c                   endsr
pdf   *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   eval      brtxt = *blank
      *
      * Key for rutine-register
      *
     c     aforl1_key    klist
     c                   kfld                    aforl1_ruti
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
     c     rltrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rltrl1_levr
      *
     c     ra01l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra01l1_akod
      *
      * Henter kodeopplysninger ######################### START ######
      *
pdf  c                   open      rl642p
     c                   eval      *in05 = *off
     c                   eval      *in06 = *off
     c                   eval      *in07 = *off
     c                   eval      *in08 = *off
     c                   eval      *in09 = *off
     c                   eval      *in10 = *off
      *
     c                   move      l_firm        w_firm
      *
      * Hente inn formular-register
      *
     c                   eval      aforl1_ruti = l_ruti
     c     aforl1_key    chain     aforl1r
      *
     c                   if        not%found
     c                   eval      afinst = 1
     c                   eval      afprfi = 1
     c                   endif
      *
      * Instillinger av blankett
      *
     c                   if        afinst = 1
     c                   write     h100
     c                   endif
      *
      *  Hent inn firma/avd. opplysninger dersom
      *  kode for det er satt i rutineregister
      *
     c                   if        afprfi = 2
     c                   eval      w_avde = rlavde
     c                   endif
      *
     c                   eval      w_prfi = afprfi
     c                   eval      w_ffir = w_firm
      *
     c                   call      'RÅ707R'
     c                   parm                    w_prfi
     c                   parm                    w_ffir
     c                   parm                    w_avde
     c                   parm                    w_fnav
     c                   parm                    w_fad1
     c                   parm                    w_fad2
     c                   parm                    w_fste
     c                   parm                    w_fftx
     c                   parm                    w_tfax
     c                   parm                    w_ftlf
     c                   parm                    w_ffax
     c                   parm                    w_tbnk
     c                   parm                    w_fbgi
      *
     c                   eval      a1fnav = w_fnav
     c                   eval      a1fad1 = w_fad1
     c                   eval      a1fad2 = w_fad2
     c                   eval      a1fste = w_fste
     c                   eval      a1fftx = w_fftx
     c                   eval      a1tfax = w_tfax
     c                   eval      a1ftlf = w_ftlf
     c                   eval      a1ffax = w_ffax
     c                   eval      a1tbnk = w_tbnk
     c                   eval      a1fbgi = w_fbgi
      *
      * Skal firmanavn droppes på utskrift
      *
     c                   if        afprfi = 0
     c                   eval      a1fnav = *blank
     c                   eval      a1fad1 = *blank
     c                   eval      a1fad2 = *blank
     c                   eval      a1fste = *blank
     c                   eval      a1fftx = *blank
     c                   eval      a1tfax = *blank
     c                   eval      a1ftlf = *blank
     c                   eval      a1ffax = *blank
     c                   eval      a1tbnk = *blank
     c                   eval      a1fbgi = *blank
     c                   endif
      *
      * Henter kodeopplysninger ######################### SLUTT ######
      *
      * Sjekker om det ønskes bruddsum ################## START ######
      *
     c                   if        pavds <> *blank
     c                   movel     'X'           brsum
     c                   endif
      *
     c                   if        pkats <> *blank
     c                   movel     'X'           brsum
     c                   endif
      *
     c                   if        psels <> *blank
     c                   movel     'X'           brsum
     c                   endif
      *
      * Behandle parameter      ############################ START ######
S5.30 * Innstillingslinje
      *
S5.30c*                  write     h100
      *
      * Valutabeløp skrives
      *
     c                   if        pvalu = *blank                                        .....
     c                   movel     'Nei'         psvalx                                      .
     c                   eval      *in15 = *off                                              .
     c                   else                                                                .
     c                   eval      *in15 = *on                                               .
     c                   movel     'Ja '         psvalx                                      .
     c                   endif                                                           .....
      *
      * Enkeltleverandører
      *
     c                   xfoot     kun           wkun
     c                   eval      n = 1
      *
     c                   if        wkun <> 0                                             .....
      *
     c                   dou       n > 10                                                    .
      *
     c                   if        kun(n) <> *zeros                                          .
     c                   move      kun(n)        rlevl1_levr                                 .
     c     rlevl1_key    chain     rlevl1                             61                     .
      *
     c                   if        *in61 = *off
     c                   move      rlnavn        kna(n)                                      .
     c                   else
     c                   move      txt(4)        kna(n)                                      .
     c                   endif                                                               .
      *                                                                 .
     c                   endif                                                               .
     c                   eval      n = n + 1                                                 .
     c                   enddo                                                               .
     c                   endif                                                           .....
      *                                                                 .
     c                   eval      n = 1
      *
      * Sum pr. avdeling
      *                                                                 .
     c                   if        pavds = *blank                                        .....
     c                   movel     'Nei'         pavdsx                                      .
     c                   else                                                                .
     c                   movel     'Ja '         pavdsx                                      .
     c                   endif                                                           .....
      *                                                                 .
      * Sum pr. kategori
      *                                                                 .
     c                   if        pkats = *blank                                        .....
     c                   movel     'Nei'         pkatsx                                      .
     c                   else                                                                .
     c                   movel     'Ja '         pkatsx                                      .
     c                   endif                                                           .....
      *                                                                 .
      * Sum pr. selger
      *                                                                 .
     c                   if        psels = *blank                                        .....
     c                   movel     'Nei'         pselsx                                      .
     c                   else                                                                .
     c                   movel     'Ja '         pselsx                                      .
     c                   endif                                                           .....
      *                                                                 .
      * Sortering
      *                                                                 .
     c                   if        psort = ' '                                           .....
     c                   movel     'Nummeris'    psortx                                      .
     c                   move      'k '          psortx                                      .
     c                   else                                                                .
     c                   movel     'Alfabeti'    psortx                                      .
     c                   move      'sk'          psortx                                      .
     c                   endif                                                           .....
      *
     c                   endsr
      **
**   MELDINGER
Avdeling
Kategori
Selger
*** U K J E N T ***
