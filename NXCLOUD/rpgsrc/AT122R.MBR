     h option(*nodebugio : *srcstmt) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASPGPLU
      * Program . . . . : AT122R
      * Beskrivelse . . : NX meny
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       200220 BKV  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     fat122d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering tabell DS
      *
      *
      * Definering av variabler i nøkler
      *
      *
      * Definering av variable
      *
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d b_forn          s              1    inz(*off)
     d b_oppr          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
      *
     d w_amhuid        s             18  0
     d w_amhmnu        s             10
     d w_amhseq        s              3  0
     d w_amhtxt        s             50
     d w_amhsel        s              3  0
     d w_amhtyp        s             10
     d w_amhopt        s             20
      *
     d sqlquery        s           4096
     d w_frisok        s             30
     d w_teller        s              3  0
     d b_open_sok      s              1    inz(*off)
      *
     d w_firm          s              3  0
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(18)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      *
     C/Exec SQL
     C+  Set Option DatFmt=*ISO, commit=*none, DlyPrp=*YES
     C+  , SRTSEQ=*LANGIDSHR, LANGID=NOR
     C/End-Exec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inka
     c                   exsr      xf1msg
     c                   goto      b2taga
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   eval      *in30 = *on
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *inkh
     c                   call      'AT123R'
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2fris <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      b_open_sok = *on
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
     c                   eval      b_open_sok = *on
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2fris = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      *in30 = *on
     c                   exsr      xc2bld
     c                   eval      *in30 = *off
     c                   eval      b1valg = 0
     c                   eval      b_forn = *on
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopi
      *
     c                   if        b1valg = 3
     c                   eval      *in30 = *on
     c                   exsr      xc1bld
     c                   eval      *in30 = *off
     c                   eval      b_forn = *on
     c                   eval      b1valg = 0
     c*                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1huid = b1huid
     c                   exsr      xd1win
     c                   eval      b_forn = *on
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c*                   eval      c1gave = b1gave
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
     c     c1taga        tag
      *
     c                   if        b1valg = 3
     c                   eval      c1hmnu = b1hmnu
     c                   eval      c1hseq = b1hseq
     c                   eval      c1htyp = b1htyp
     c                   else
     c                   eval      c1hmnu = *blank
     c                   eval      c1hseq = 0
     c                   eval      c1htyp = *blank
     c                   endif
      *
     c     c1tagb        tag
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c/exec sql
     c+ select count(*)
     c+ into  :w_teller
     c+ from amnhst
     c+ where amhmnu = :c1hmnu and
     c+  amhseq = :c1hseq and
     c+  amhtyp = :c1htyp
     c/end-exec
     c                   if        w_teller > 0
     c                   exsr      xc1msg
     c                   goto      c1tagb
     c                   endif
     c                   if        c1hmnu = *blank or c1htyp = *blank
     c                   goto      c1tagb
     c                   endif
     c                   eval      b_forn = *on
      *
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      c1hmnu = *blank
     c                   eval      c1hseq = 0
     c                   eval      c1htyp = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c/exec sql
     c+ select menynavn,sekvensnr,menytype
     c+ into  :w_amhmnu,:w_amhseq,:w_amhtyp
     c+ from amnhst
     c+ where amhuid = :d1huid
     c/end-exec
     c                   eval      d1hmnu = w_amhmnu
     c                   eval      d1hseq = w_amhseq
     c                   eval      d1htyp = w_amhtyp
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter nokklel
      *
     c/exec sql
     c+ delete
     c+ from amnhst
     c+ where amhuid = :d1huid
     c/end-exec
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   if        b_oppr = *on  and b1valg <> 3
     c                   eval      c2hmnu = c1hmnu
     c                   eval      c2hseq = c1hseq
     c                   eval      c2htyp = c1htyp
     c                   eval      c2htxt = *blank
     c                   eval      c2hsel = 0
     c                   eval      c2htxt = *blank
     c                   eval      c2hopt = *blank
     c                   else
     c/exec sql
     c+ select menynavn,sekvensnr,menytekst,sikkerhetsniva
     c+ ,menytype,menyvalg,opprettet,opprettetbruker
     c+ ,endrettispunkt,endretbruker
     c+ into  :w_amhmnu,:w_amhseq,:w_amhtxt,:w_amhsel
     c+ ,:w_amhtyp,:w_amhopt
     c+ from amnhst
     c+ where amhuid = :b1huid
     c/end-exec
      *
     c                   if        b1valg = 3
     c                   eval      c2hmnu = c1hmnu
     c                   eval      c2hseq = c1hseq
     c                   eval      c2htyp = c1htyp
     c                   else
     c                   eval      c2hmnu = w_amhmnu
     c                   eval      c2hseq = w_amhseq
     c                   eval      c2htyp = w_amhtyp
     c                   endif
     c                   eval      c2htxt = w_amhtxt
     c                   eval      c2hsel = w_amhsel
     c                   eval      c2hopt = w_amhopt
     c                   endif
      *
htxt
hsel
hopt
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   eval      b_forn = *off
     c                   goto      end_c2bld
     c                   endsl
      *
      * Oppdatering er valgt
      *
     c                   if        *in30 = *on
     c                   time                    w_time
     c                   if        b1valg = 2
     c/exec sql
     c+ update amnhst set MENYNAVN=:c2hmnu,SEKVENSNR=:c2hseq
     c+ ,MENYTEKST=:c2htxt,SIKKERHETSNIVA=:c2hsel
     c+ ,MENYTYPE=:c2htyp,MENYVALG=:c2hopt
     c+ where amhuid = :b1huid
     c/end-exec
     c                   elseif    b1valg = 3 or b_oppr = *on
     c/exec sql
     C+ INSERT INTO amnhst
     c+ (menynavn,sekvensnr,menytekst,sikkerhetsniva
     c+ ,menytype,menyvalg)
     c+ values (
     c+ :c2hmnu,:c2hseq,:c2htxt,:c2hsel
     c+ ,:c2htyp,:c2hopt
     c+ )
     c/end-exec
     c                   endif
     c                   if        sqlcod < 0
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
      *
     c     end_c2bld     tag
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
     c
     c
      *
      * Initierer SQL for søk
      *
     c                   if        b_open_sok = *on
     c/exec sql
     c+ close STMT
     c/end-exec
     c                   eval      sqlquery  = 'select +
     c                                   uid         +
     c                                  ,menynavn  +
     c                                  ,sekvensnr      +
     c                                  ,menytekst      +
     c                                  ,sikkerhetsniva     +
     c                                  ,menytype     +
     c                                  ,menyvalg +
     c                                   from dynamiskcloudmeny +
     c                               '
     c                   if        b2fris <> *blank
     c                   eval      w_frisok = '%' + %trim(b2fris) + '%'
     c                   eval      sqlquery  = %trim(sqlquery) +
     c                                 '  where      +
     c                                   ( +
     c                                    (UPPER(menytekst) like ?) +
     c                                 or (menynavn like ?) +
     c                                 or (menytype like ?) +
     c                                 or (menyvalg like ?)   +
     c                                   )'
     c                   endif
     c
     c                   eval      sqlquery  = %trim(sqlquery) +
     c                                 '  order by      +
     c                                   menynavn     +
     c                                   ,menytype        +
     c                                   ,sekvensnr    +
     c                                   for read only +
     c                                   '
     c
     c/exec sql
     c+ PREPARE SQL_STMT FROM :sqlquery
     c/end-exec
     c
     c/exec sql
     c+ DECLARE STMT CURSOR FOR SQL_STMT
     c/end-exec
     c
     c                   if        b2fris <> *blank
     c/exec sql
     c+ OPEN STMT using  :w_frisok, :w_frisok, :w_frisok
     c/end-exec
     c                   else
     c/exec sql
     c+ OPEN STMT
     c/end-exec
     c                   endif
     c
     c                   endif
     c
     c                   if        sqlcod = 100 or sqlcod < 0
     c                   eval      *in15 = *on
     c                   leavesr
     c                   endif
      *
     c                   dou       w_srrn = w_spge
      *
     c/exec sql
     c+ fetch STMT
     c+ into  :w_amhuid,:w_amhmnu,:w_amhseq,:w_amhtxt,:w_amhsel,:w_amhtyp
     c+       ,:w_amhopt
     c/end-exec
     c                   if        sqlcod = 100 or sqlcod < 0
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1hmnu = w_amhmnu
     c                   eval      b1hseq = w_amhseq
     c                   eval      b1htxt = w_amhtxt
     c*                   eval      b1hsel = w_amhsel
     c                   eval      b1htyp = w_amhtyp
     c                   eval      b1hopt = w_amhopt
     c                   eval      b1huid = w_amhuid
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   eval      b_open_sok = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av Infobilde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xf1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     f1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_f1msg
     c                   endif
      *
     c     end_f1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjermbildet
      *
     c                   eval      *in22 = *on
     c                   time                    w_date
     c                   time                    w_time
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      b_open_sok = *on
     c                   exsr      crt_subfile
      *
     c                   endsr
