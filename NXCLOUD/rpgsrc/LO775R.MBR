     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LO775R
      *  Beskrivelse . . : Leverandørbestilling i EFO/NELFO - format
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      *  --------- ------  -------------------------------------------------
6.11  * 6.10  130709 bof  Utvidet eannr til 14 og brukt std. oppslag
6.20  * 6.20  121110 nho  Proj er nå alfa
6.nu  * 6.30  120614 bof  Gejennomgått mtp. nummerutvidelse
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     flldilr    if   e           k disk    rename(lldipfr:lldilrr)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     frlevlr    if   e           k disk    rename(rlevpfr:rlevlrr)
     flwnepf    o    e           k disk
      *
      * Definering av outputrecord for EFO/NELFO
      * - Poster bygges i programmet (dynamisk recordlengde)
      *
      * - Bestillingshode     (posttype BH, max. recordlengde 961 ch.)
      * - Bestillingslinje    (posttype BL, max. recordlengde 167 ch.)
      * - Bestilling fritekst (posttype BT, max. recordlengde  32 ch.)
      *
     d d_hrec          s            961
     d d_lrec          s            167
     d d_trec          s             32
      *
      * Definering av LDA
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d lodtl1_line     s                   like(ldline)
     d lldilr_ldor     s                   like(lpldor)
     d lldilr_lage     s                   like(lplage)
     d vvarl1_vare     s                   like(vvvare)
     d rllevr_levr     s                   like(rllevr)
      *
      *  Definering av variable i parameter
      *
     d p_numm          s                   like(lonumm)
     d p_suff          s                   like(losuff)
     d p_filn          s             20
      *
      * Definering av øvrige variable
      *
     d w_firm          s                   like(lofirm)
     d w_line          s             80
     d w_ant_alf       s              6
     d w_numm_alf      s              6
     d b_feil          s              1    inz(*off)
     d b_seqn          s              1    inz(*off)
     d b_hode          s              1    inz(*off)
     d w_dato          s             10
     d w_lcnt          s              4  0
     d w_ftnr          s              9
     d w_vlin          s              4  0
     d w_vmrk          s              1  0
     d w_v2dc          s             11  2
     d w_vant          s             13  0
     d w_pren          s              1  0
     d w_numm          s              6
     d w_suff          s              2
     d w_seqx          s              3
     d w_seqn          s              3  0
     d w_seix          s              1  0
     d w_filn          s             12
6.11 d w_east          s              1
6.11 d w_eann          s             14  0
      *
      * Definering av arbeidsfelt ihht meldingsdefinisjon
      * - Bestillingshode
     d w_bhcd          s              2               inz('BH')
     d w_form          s              8               inz('EFONELFO')
     d w_vers          s              3               inz('3.0')
     d w_seid          s             14
     d w_kjid          s             14
     d w_lonu          s             10
     d w_lknr          s             10
     d w_ornm          s             10
     d w_kavd          s             10
     d w_proj          s             10
     d w_klag          s             10
     d w_slag          s             10
     d w_eref          s             10
     d w_kref          s             25
     d w_mrkt          s             25
     d w_tran          s             25
     d w_meld          s             25
     d w_ldat          s              8
     d w_best          s              2
     d w_lfir          s             35
     d w_lad1          s             35
     d w_lad2          s             35
     d w_lpon          s              9
     d w_lpos          s             35
     d w_llan          s              2
     d w_kfir          s             35
     d w_kad1          s             35
     d w_kad2          s             35
     d w_kpon          s              9
     d w_kpos          s             35
     d w_klan          s              2
     d w_ktlf          s             15
     d w_kfax          s             15
     d w_kepo          s             40
     d w_kweb          s             40
     d w_ffir          s             35
     d w_fad1          s             35
     d w_fad2          s             35
     d w_fpon          s              9
     d w_fpos          s             35
     d w_flan          s              2
     d w_sfir          s             35
     d w_sad1          s             35
     d w_sad2          s             35
     d w_spon          s              9
     d w_spos          s             35
     d w_slan          s              2
      *
      * - Bestillingslinje
      *
     d w_blcd          s              2               inz('BL')
     d w_vlix          s              4
     d w_vbes          s              7
     d w_vmrx          s              1
     d w_vnum          s             14
     d w_vtxt          s             30
     d w_vtek          s             25
     d w_vanx          s             11
     d w_prex          s              1
     d w_kvar          s             25
     d w_vlda          s              8
     d w_vref          s             25
     d w_vkto          s             16
      *
      * - Bestillingstekstlinje
      *
     d w_btcd          s              2               inz('BT')
     d w_txtl          s             30
      *
      * Meldingskonstanter
      *
     d c_fsep          c                   ';'
     d c_ftpr          c                   'NO'
     d c_ftsx          c                   'MVA'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Les av hoderecord
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lohel1_key    chain     lohel1r
      *
     c                   if        %found
     c*                  exsr      bygg_hrec
      *
     c                   if        b_feil = *off
     c     lodtl1_key    setll     lodtl1r
     c     lodtl1_key    reade     lodtl1r
      *
     c                   dow       not %eof
      *
     c                   if        ldltyp = laatty
     c                   exsr      bygg_trec
     c                   else
     c                   exsr      bygg_lrec
     c                   endif
      *
     c                   if        b_feil = *off
     c                   if        b_hode = *off
     c                   exsr      bygg_hrec
     c                   eval      nefrec = d_hrec
     c                   write     lwnepfr
     c                   eval      b_hode = *on
     c                   endif
     c                   endif
      *
     c                   if        ldltyp = laatty
     c                   eval      nefrec = d_trec
     c                   write     lwnepfr
     c                   else
     c                   eval      nefrec = d_lrec
     c                   write     lwnepfr
     c                   endif
      *
     c     lodtl1_key    reade     lodtl1r
     c                   enddo
      *
     c                   endif
     c                   endif
      *
     c                   eval      w_filn = 'bo' + %subst(lplknr:1:6) + '.'
     c                   eval      p_filn = w_filn
     c
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å bygge bestillingshode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bygg_hrec     begsr
      *
      * Finn lev./distribusjonsrecord
      *
     c                   eval      lldilr_ldor = loldor
     c                   move      lolage        lldilr_lage
     c     lldilr_key    chain     lldilr
     c                   if        not %found
     c                   eval      lldilr_lage = *blank
     c     lldilr_key    chain     lldilr
     c                   if        not %found
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   eval      rllevr_levr = loldor
     c     rlevlr_key    chain     rlevlrr
     c                   if        not %found or rlftnr = 0
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Foretaksnummer for selger/kjøper m/ pre- og suffix (NO/MVA)
     c                   movel     rlftnr        w_ftnr
     c                   eval      w_seid = c_ftpr + w_ftnr
     c                   if        rlmkod = 0
     c                   eval      w_seid = %trim(w_seid) + c_ftsx
     c                   endif
     c                   movel     aafftn        w_ftnr
     c                   eval      w_kjid = c_ftpr + w_ftnr
     c                   if        aamvap = 0
     c                   eval      w_kjid = %trim(w_kjid) + c_ftsx
     c                   endif
      *
      * Flytt inn i arbeidsvariable ihht meldingsformat
      * - ukjent felt ihht egen database kommentert ut
     c                   eval      w_lonu = %editc(lonumm : 'Z')
     c                   movel     lplknr        w_lknr
     c*                  movel     ?             w_ornm
     c                   eval      w_kavd = %editc(loavde : 'Z')
6.20 c**                 eval      w_proj = %editc(loproj : 'Z')
6.20 c                   eval      w_proj = loproj
     c                   eval      w_klag = %editc(lolage : 'Z')
     c*                  movel     ?             w_slag
     c                   movel     lodref        w_eref
     c                   movel     lovref        w_kref
      *
     c*                  movel     ?             w_mrkt
     c*                  movel     ?             w_tran
     c*                  movel     ?             w_meld
      *
     c                   if        lovida <> *loval
     c                   move      lovida        w_dato
     c                   endif
     c                   if        lomoda <> *loval
     c                   move      lomoda        w_dato
     c                   endif
     c                   if        w_dato <> *blank
     c                   eval      w_ldat = %subst(w_dato:1:4) +
     c                                      %subst(w_dato:6:2) +
     c                                      %subst(w_dato:9:2)
     c                   endif
      *
     c                   movel     lovref        w_kref
     c                   movel     '1'           w_best
      *
     c                   if        ldlety = 1
     c                   movel     lonavn        w_lfir
     c                   movel     loadr1        w_lad1
     c                   movel     loadr2        w_lad2
     c                   eval      w_lpon = %subst(losted:1:4)
     c                   eval      w_lpos = %subst(losted:5)
     c*                  movel     ?             w_llan
     c                   endif
      *
     c                   movel     aafnav        w_kfir
     c                   movel     aafad1        w_kad1
     c                   movel     aafad2        w_kad2
     c                   eval      w_kpon = %subst(aafste:1:4)
     c                   eval      w_kpos = %subst(aafste:5)
     c*                  movel     ?             w_klan
     c*                  movel     ?             w_ktlf
     c*                  movel     ?             w_kfax
     c*                  movel     ?             w_kepo
     c*                  movel     ?             w_kweb
      *
     c*                  movel     ?             w_ffir
     c*                  movel     ?             w_fad1
     c*                  movel     ?             w_fad2
     c*                  movel     ?             w_fpon
     c*                  movel     ?             w_fpos
     c*                  movel     ?             w_flan
      *
     c                   movel     rlnavn        w_sfir
     c                   movel     rlgate        w_sad1
     c                   movel     rladr2        w_sad2
     c                   movel     rlponr        w_spon
     c                   eval      w_spos = %subst(rlsted:5)
     c                   movel     rlland        w_slan
      *
     c                   eval      d_hrec = w_bhcd + c_fsep +
     c                                      w_form + c_fsep +
     c                                      w_vers + c_fsep +
     c                                      %trim(w_seid) + c_fsep +
     c                                      %trim(w_kjid) + c_fsep +
     c                                      %trim(w_lonu) + c_fsep +
     c                                      %trim(w_lknr) + c_fsep +
     c                                      %trim(w_ornm) + c_fsep +
     c                                      %trim(w_kavd) + c_fsep +
     c                                      %trim(w_proj) + c_fsep +
     c                                      %trim(w_klag) + c_fsep +
     c                                      %trim(w_slag) + c_fsep +
     c                                      %trim(w_eref) + c_fsep +
     c                                      %trim(w_kref) + c_fsep +
     c                                      %trim(w_mrkt) + c_fsep +
     c                                      %trim(w_tran) + c_fsep +
     c                                      %trim(w_meld) + c_fsep +
     c                                      %trim(w_ldat) + c_fsep +
     c                                      %trim(w_best) + c_fsep +
     c                                      %trim(w_lfir) + c_fsep +
     c                                      %trim(w_lad1) + c_fsep +
     c                                      %trim(w_lad2) + c_fsep +
     c                                      %trim(w_lpon) + c_fsep +
     c                                      %trim(w_lpos) + c_fsep +
     c                                      %trim(w_llan) + c_fsep +
     c                                      %trim(w_kfir) + c_fsep +
     c                                      %trim(w_kad1) + c_fsep +
     c                                      %trim(w_kad2) + c_fsep +
     c                                      %trim(w_kpon) + c_fsep +
     c                                      %trim(w_kpos) + c_fsep +
     c                                      %trim(w_klan) + c_fsep +
     c                                      %trim(w_ktlf) + c_fsep +
     c                                      %trim(w_kfax) + c_fsep +
     c                                      %trim(w_kepo) + c_fsep +
     c                                      %trim(w_kweb) + c_fsep +
     c                                      %trim(w_ffir) + c_fsep +
     c                                      %trim(w_fad1) + c_fsep +
     c                                      %trim(w_fad2) + c_fsep +
     c                                      %trim(w_fpon) + c_fsep +
     c                                      %trim(w_fpos) + c_fsep +
     c                                      %trim(w_flan) + c_fsep +
     c                                      %trim(w_sfir) + c_fsep +
     c                                      %trim(w_sad1) + c_fsep +
     c                                      %trim(w_sad2) + c_fsep +
     c                                      %trim(w_spon) + c_fsep +
     c                                      %trim(w_spos) + c_fsep +
     c                                      %trim(w_slan)
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å bygge bestillingslinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bygg_lrec     begsr
      *
     c                   exsr      finn_vnr
     c                   if        b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      w_lcnt = w_lcnt + 1
     c                   eval      w_vlin = w_lcnt
      *
     c                   eval      w_vbes = %editc(ldnumm : 'Z')
     c                   movel     ldtek1        w_vtxt
     c*                  movel     ?             w_vtek
     c                   eval(h)   w_v2dc = ldanta
     c                   movel     w_v2dc        w_vant
      *
     c                   select
     c                   when      ldenh1 = 'M'
     c                   eval      w_pren = 2
     c                   when      ldenh1 = 'LTR'
     c                   eval      w_pren = 3
     c                   when      ldenh1 = 'KG'
     c                   eval      w_pren = 4
     c                   other
     c                   eval      w_pren = 1
     c                   endsl
      *
     c*                  movel     ?             w_kvar
     c*                  movel     ldldat        w_vlda
     c*                  movel     ?             w_vref
     c*                  movel     ldkont        w_vkto
      *
     c                   eval      w_vlix = %editc(w_vlin : 'Z')
     c                   movel     w_vmrk        w_vmrx
     c                   if        w_vant > 0
     c                   eval      w_vanx = %editc(w_vant : 'Z')
     c                   else
     c                   eval      w_vanx = '000'
     c                   endif
     c                   movel     w_pren        w_prex
      *
     c                   eval      d_lrec = w_blcd + c_fsep +
     c                                      %trim(w_vlix) + c_fsep +
     c                                      %trim(w_vbes) + c_fsep +
     c                                      w_vmrx + c_fsep +
     c                                      %trim(w_vnum) + c_fsep +
     c                                      %trim(w_vtxt) + c_fsep +
     c                                      %trim(w_vtek) + c_fsep +
     c                                      %trim(w_vanx) + c_fsep +
     c                                      w_prex + c_fsep +
     c                                      %trim(w_kvar) + c_fsep +
     c                                      %trim(w_vlda) + c_fsep +
     c                                      %trim(w_vref) + c_fsep +
     c                                      %trim(w_vkto)
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å bygge bestillingstekstlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bygg_trec     begsr
      *
     c                   movel     ldtek1        w_txtl
      *
     c                   eval      d_trec = w_btcd + c_fsep +
     c                                      %trim(w_txtl)
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne varenummer (EAN, Lev.vnr. eller NOBB)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_vnr      begsr
      *
     c                   eval      w_vnum = *blank
     c                   eval      w_vmrk = 0
      *
     c                   eval      vvarl1_vare = ldvare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        vvlvar <> *blank
     c                   movel     vvlvar        w_vnum
     c                   eval      w_vmrk = 3
     c                   leavesr
     c                   endif
      *
6.11  * Kaller program for henting av ean-nr
6.11  *
6.11 c                   call      'VE711R'
6.11 c                   parm                    w_firm
6.11 c                   parm                    ldvare
6.11 c                   parm                    ldenh1
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_east
     c                   if        w_east = '1' and
     c                             w_eann <> 0
     c                   eval      w_vnum = %editc(w_eann : 'Z')
     c                   eval      w_vmrk = 2
     c                   leavesr
     c                   endif
      *
     c                   if        vvnobb <> 0
     c                   eval      w_vnum = %editc(vvnobb : 'Z')
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for file les FIRMA-OPPLYSNINGER
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for file les LAGER-KODE
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for les av BESTILLING-HODE
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      * Key for oppslag på leverandør distribusjon
      *
     c     lldilr_key    klist
     C                   kfld                    w_firm
     c                   kfld                    lldilr_ldor
     c                   kfld                    lldilr_lage
      *
      * Key for oppdatering av leverandør distribusjon
      *
     c*    lldilu_key    klist
     c*                  kfld                    w_firm
     c*                  kfld                    lldilu_ldor
     c*                  kfld                    lldilu_lage
      *
      *  Key for file les BESTILLING-LINJE
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for leverandører
      *
     c     rlevlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rllevr_levr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *entry        plist
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_filn
     c*                  eval      p_numm = 867
     c*                  eval      p_suff = 0
      *
      *  Legger inn verdier i nøklene
      *
     c                   eval      w_firm = l_firm
     c                   eval      lohel1_numm = p_numm
     c                   eval      lohel1_suff = p_suff
     c                   eval      lodtl1_numm = p_numm
     c                   eval      lodtl1_suff = p_suff
      *
      *  Hent opplysninger fra FIRMA og LAGER-KODE
      *
     c     lstsl1_key    chain     lstsl1r                            90
      *
     c     afirl1_key    chain     afirl1r                            90
      *
     c                   endsr
