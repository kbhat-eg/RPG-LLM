     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JV845R
      * Beskrivelse . . : Oppdater fra NIB, modul
      *
      *                   Oppdaterer/oppretter moduler
      *                   Norgros/NIB (XML-format)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       141105 GRo  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fnxmopf    uf   e           k disk
     fjmodlu    uf a e           k disk    rename(jmodpfr:jmodlur)
     fjmstlu    uf a e           k disk    rename(jmstpfr:jmstlur)
     fjmsdlu    uf a e           k disk    rename(jmsdpfr:jmsdlur)
     fjmtxlu    uf a e           k disk    rename(jmtxpfr:jmtxlur)
     fjvarlg    uf   e           k disk    rename(jvarpfr:jvarlgr)
     fjstsl1    if   e           k disk    rename(jstspfr:jstsl1r)
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
      *
      * Definering av Local Data Area
      *
     d                uds
     d l_user                911    920
      *
      *  Dataelementer modul
      *
     d                 ds
     d d_mrec                       200
     d  d_modn                        8    overlay(d_mrec:1)
     d   d_modn_num                   8  0 overlay(d_modn:1)
     d  d_stat                        1    overlay(d_mrec:9)
     d   d_stat_num                   1  0 overlay(d_stat:1)
     d  d_mte1                       35    overlay(d_mrec:10)
     d  d_mte2                       35    overlay(d_mrec:45)
     d  d_merk                       35    overlay(d_mrec:80)
     d  d_ldor                        6    overlay(d_mrec:115)
     d   d_ldor_num                   6  0 overlay(d_ldor:1)
     d  d_prod                        6    overlay(d_mrec:121)
     d   d_prod_num                   6  0 overlay(d_prod:1)
     d  d_vgrp                        7    overlay(d_mrec:127)
     d   d_ogrp                       2    overlay(d_vgrp:1)
     d   d_ogrp_num                   2  0 overlay(d_ogrp:1)
     d   d_hgrp                       2    overlay(d_vgrp:3)
     d   d_hgrp_num                   2  0 overlay(d_hgrp:1)
     d   d_ugrp                       3    overlay(d_vgrp:5)
     d   d_ugrp_num                   3  0 overlay(d_ugrp:1)
     d  d_noda                       10    overlay(d_mrec:134)
     d   d_noda_iso                    d   overlay(d_noda:1) datfmt(*iso)
     d  d_neda                       10    overlay(d_mrec:144)
     d  d_odat                       10    overlay(d_mrec:154)
     d  d_edat                       10    overlay(d_mrec:164)
     d  d_etim                       10    overlay(d_mrec:174)
      *
      *  Dataelementer stikkord
      *
     d                 ds
     d d_skrc                        50
     d  d_sord                       35    overlay(d_skrc:1)
      *
      *  Dataelementer standard
      *
     d                 ds
     d d_strc                        50
     d  d_sstd                       35    overlay(d_strc:1)
      *
      *  Dataelementer fritekst
      *
     d                 ds
     d d_ftrc                        80
     d  d_ftxt                       78    overlay(d_ftrc:1)
      *
      * Definering av variable i nøkler
      *
     d jmodlu_modn     s                   like(jumodn)
     d jmstlu_smod     s                   like(jusmod)
     d jmsdlu_umod     s                   like(juumod)
     d jmtxlu_tmod     s                   like(jutmod)
     d jvarlg_modn     s                   like(jvmodn)
      *
      * Definering av variable
      *
     d b_inlr          s              1
     d b_kild          s              1    inz(*off)
     d b_modu          s              1    inz(*off)
     d b_mfel          s              1    inz(*off)
     d b_blnk          s              1    inz(*off)
      *
     d w_stda          s              3  0
     d w_slda          s              3  0
     d w_welm          s            400
     d w_wlen          s              3  0
     d w_decp          s              3  0
     d w_line          s              3  0
      *
     d w_str_i         s            400
     d w_str_u         s            400
     d w_posi          s              4  0
     d w_posu          s              4  0
     d w_str_x         s              1
      *
     d w_firm          s              3  0
      *
      *  Konstanter
      *
     d c_apos          c                   x'7D'
     d c_digits        c                   ' 0123456789'
     d c_nsep          c                   ','
      *
     d c_NIBk          c                   '0'
     d c_NVk           c                   '3'
      *
      * XML string konstanter
      *
     d c_xsle          c                   '</'
     d c_xend          c                   '>'
      *
      * XML feltnavn - meldingsspesifikke
      *
      * Hovedstruktur - Avsender
      *
     d c_xstr          c                   '<Avsender>'
     d c_kild          c                   '<Kilde>'
      *
      * Substruktur #1 - Modul
      *
     d c_modu_s        c                   '<Modul>'
     d c_modu_e        c                   '</Modul>'
     d c_modn          c                   '<Modulnr>'
     d c_stat          c                   '<Status>'
     d c_mtx1          c                   '<Modultekst1>'
     d c_mtx2          c                   '<Modultekst2>'
     d c_mnvn          c                   '<Merkenavn>'
     d c_levr          c                   '<Lev>'
     d c_prod          c                   '<Prod>'
     d c_ogrp          c                   '<Ogrp>'
     d c_hgrp          c                   '<Hgrp>'
     d c_ugrp          c                   '<Ugrp>'
     d c_nbdo          c                   '<Nobb_oppr_dato>'
     d c_nbde          c                   '<Nobb_endr_dato>'
     d c_opda          c                   '<Oppr_dato>'
     d c_enda          c                   '<Endr_dato>'
     d c_entd          c                   '<Endr_tidspunkt>'
      *
      * Substrukturer under modul - stikkord/standard /fritekst
      *
     d c_ssti_s        c                   '<Stikkord>'
     d c_ssti_e        c                   '</Stikkord>'
     d c_sord          c                   '<Ord>'
     d c_sstd_s        c                   '<Standard>'
     d c_sstd_e        c                   '</Standard>'
     d c_stan          c                   '<Std>'
     d c_ssft_s        c                   '<Fritekst>'
     d c_ssft_e        c                   '</Fritekst>'
     d c_teks          c                   '<Txt>'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      b_inlr = *on
      *
      * Leser XML-fil modul
      *
     c                   read      nxmopfr
     c                   dow       not %eof(nxmopf)
      *
     c                   if        b_kild = *on
     c                   if        %subst(nxmodu:1:7) = c_modu_s
     c                   eval      b_modu = *on
     c                   exsr      beh_modu
     c                   endif
     c                   endif
      *
      *  Finn gyldig avsender/kilde
      *
     c                   eval      w_stda = %scan(c_xend:nxmodu:1)
     c                   eval      w_slda = %scan(c_xsle:nxmodu:w_stda)
     c                   eval      w_stda = w_stda + 1
      *
     c                   eval      w_wlen = *zero
     c                   eval      w_welm = *blank
     c                   if        w_slda > w_stda
     c                   eval      w_wlen = w_slda - w_stda
     c                   eval      w_welm = %subst(nxmodu:w_stda:w_wlen)
     c                   endif
      *
     c                   select
     c                   when      %subst(nxmodu:1:7) = c_kild
     c                   exsr      uttr_kild
     c                   endsl
      *
     c                   delete    nxmopfr
      *
     c                   read      nxmopfr
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av avsenderelement : Kilde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_kild     begsr
      *
     c                   eval      b_kild = *off
      *
     c                   if        %trim(w_welm) = c_NIBk
     c                   eval      b_kild = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av modulinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beh_modu      begsr
      *
     c                   eval      b_mfel = *off
     c                   exsr      modu_init
      *
     c                   dow       not %eof(nxmopf)
      *
      *  Start på stikkord element
      *
     c                   if        %subst(nxmodu:1:10) = c_ssti_s
      *
     c                   if        b_mfel = *off
     c                   exsr      beh_stikkord
     c                   endif
      *
     c                   endif
      *
      *  Start på standard element
      *
     c                   if        %subst(nxmodu:1:10) = c_sstd_s
      *
     c                   if        b_mfel = *off
     c                   exsr      beh_standard
     c                   endif
      *
     c                   endif
      *
      *  Start på fritekst element
      *
     c                   if        %subst(nxmodu:1:10) = c_ssft_s
      *
     c                   if        b_mfel = *off
     c                   exsr      beh_fritekst
     c                   endif
      *
     c                   endif
      *
      *  Slutt på modulelement
      *
     c                   if        %subst(nxmodu:1:8) = c_modu_e
     c                   exsr      skr_modu
     c                   leave
     c                   endif
      *
      *  Enkeltelement modulnivå herfra
      *
     c                   eval      w_stda = %scan(c_xend:nxmodu:1)
     c                   eval      w_slda = %scan(c_xsle:nxmodu:w_stda)
     c                   eval      w_stda = w_stda + 1
      *
     c                   eval      w_wlen = *zero
     c                   eval      w_welm = *blank
     c                   if        w_slda > w_stda
     c                   eval      w_wlen = w_slda - w_stda
     c                   eval      w_welm = %subst(nxmodu:w_stda:w_wlen)
     c                   endif
      *
     c                   select
     c                   when      %subst(nxmodu:1:9) = c_modn
     c                   exsr      uttr_modn
     c                   when      %subst(nxmodu:1:8) = c_stat
     c                   exsr      uttr_stat
     c                   when      %subst(nxmodu:1:13) = c_mtx1
     c                   exsr      uttr_mtx1
     c                   when      %subst(nxmodu:1:13) = c_mtx2
     c                   exsr      uttr_mtx2
     c                   when      %subst(nxmodu:1:11) = c_mnvn
     c                   exsr      uttr_mnvn
     c                   when      %subst(nxmodu:1:5) = c_levr
     c                   exsr      uttr_levr
     c                   when      %subst(nxmodu:1:6) = c_prod
     c                   exsr      uttr_prod
     c                   when      %subst(nxmodu:1:6) = c_ogrp
     c                   exsr      uttr_ogrp
     c                   when      %subst(nxmodu:1:6) = c_hgrp
     c                   exsr      uttr_hgrp
     c                   when      %subst(nxmodu:1:6) = c_ugrp
     c                   exsr      uttr_ugrp
     c                   when      %subst(nxmodu:1:16) = c_nbdo
     c                   exsr      uttr_nbdo
     c                   when      %subst(nxmodu:1:16) = c_nbde
     c                   exsr      uttr_nbde
     c                   when      %subst(nxmodu:1:11) = c_opda
     c                   exsr      uttr_opda
     c                   when      %subst(nxmodu:1:11) = c_enda
     c                   exsr      uttr_enda
     c                   when      %subst(nxmodu:1:16) = c_entd
     c                   exsr      uttr_entd
     c                   endsl
      *
     c                   delete    nxmopfr
      *
     c                   read      nxmopfr
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av modulinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     modu_init     begsr
      *
     c                   eval      d_mrec = *blank
     c                   eval      d_modn_num = *zero
     c                   eval      d_stat_num = *zero
     c                   eval      d_ldor_num = *zero
     c                   eval      d_prod_num = *zero
     c                   eval      d_ogrp_num = *zero
     c                   eval      d_hgrp_num = *zero
     c                   eval      d_ugrp_num = *zero
     c                   eval      d_noda_iso = *loval
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av modulelement : Modulnr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_modn     begsr
      *
     c                   if        %check(c_digits : w_welm) = 0
     c                   evalr     d_modn = %trim(w_welm)
     c                   eval      d_modn = %xlate(' ':'0':d_modn)
     c                   else
     c                   eval      b_mfel = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av modulelement : Status
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_stat     begsr
      *
     c                   eval      d_stat_num = *zero
      *
     c                   if        w_wlen = 1
     c                   if        %check(c_digits : w_welm) = 0
     c                   eval      d_stat = %trim(w_welm)
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av modulelement : Modultekst-1
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_mtx1     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_mte1 = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av modulelement : Modultekst-2
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_mtx2     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_mte2 = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av modulelement : Merkenavn
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_mnvn     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_merk = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av modulelement : Lev.nr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_levr     begsr
      *
     c                   eval      d_ldor_num = *zero
      *
     c                   if        %check(c_digits : w_welm) = 0
     c                   evalr     d_ldor = %trim(w_welm)
     c                   eval      d_ldor = %xlate(' ':'0':d_ldor)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av modulelement : Prod.nr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_prod     begsr
      *
     c                   eval      d_prod_num = *zero
      *
     c                   if        %check(c_digits : w_welm) = 0
     c                   evalr     d_prod = %trim(w_welm)
     c                   eval      d_prod = %xlate(' ':'0':d_prod)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av modulelement : Overgruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_ogrp     begsr
      *
     c                   eval      d_ogrp_num = *zero
      *
     c                   if        %check(c_digits : w_welm) = 0
     c                   evalr     d_ogrp = %trim(w_welm)
     c                   eval      d_ogrp = %xlate(' ':'0':d_ogrp)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av modulelement : Hovedgruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_hgrp     begsr
      *
     c                   eval      d_hgrp_num = *zero
      *
     c                   if        %check(c_digits : w_welm) = 0
     c                   evalr     d_hgrp = %trim(w_welm)
     c                   eval      d_hgrp = %xlate(' ':'0':d_hgrp)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av modulelement : Undergruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_ugrp     begsr
      *
     c                   eval      d_ugrp_num = *zero
      *
     c                   if        %check(c_digits : w_welm) = 0
     c                   evalr     d_ugrp = %trim(w_welm)
     c                   eval      d_ugrp = %xlate(' ':'0':d_ugrp)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av modulelement : NOBB oppr.dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_nbdo     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_noda = %trim(w_welm)
     c                   else
     c                   eval      d_noda_iso = *loval
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av modulelement : NOBB endr.dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_nbde     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_neda = %trim(w_welm)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av modulelement : Opprettet dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_opda     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_odat = %trim(w_welm)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av modulelement : Endret dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_enda     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_edat = %trim(w_welm)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av modulelement : Endringstidspunkt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_entd     begsr
      *
     c                   if        w_wlen = 8
     c                   eval      d_etim = %trim(w_welm)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av stikkord-informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beh_stikkord  begsr
      *
     c                   eval      d_skrc = *blank
      *
     c                   eval      jmstlu_smod = d_modn_num
     c     jmstlu_key    setll     jmstlu
     c     jmstlu_key    reade     jmstlur
     c                   dow       not %eof
     c                   delete    jmstlur
     c     jmstlu_key    reade     jmstlur
     c                   enddo
      *
     c                   dow       not %eof(nxmopf)
      *
      *  Slutt på stikkordelement
      *
     c                   if        %subst(nxmodu:1:11) = c_ssti_e
     c                   leave
     c                   endif
      *
      *  Enkeltelement stikkord herfra
      *
     c                   eval      w_stda = %scan(c_xend:nxmodu:1)
     c                   eval      w_slda = %scan(c_xsle:nxmodu:w_stda)
     c                   eval      w_stda = w_stda + 1
      *
     c                   eval      w_wlen = *zero
     c                   eval      w_welm = *blank
     c                   if        w_slda > w_stda
     c                   eval      w_wlen = w_slda - w_stda
     c                   eval      w_welm = %subst(nxmodu:w_stda:w_wlen)
     c                   endif
      *
     c                   select
     c                   when      %subst(nxmodu:1:5) = c_sord
     c                   exsr      uttr_sord
     c                   exsr      oppd_stikkord
     c                   endsl
      *
     c                   delete    nxmopfr
      *
     c                   read      nxmopfr
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av stikkordelement : Ord
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_sord     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_sord = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer modulstikkord
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_stikkord begsr
      *
     c                   clear                   jmstlur
      *
     c                   eval      jusmod = d_modn_num
     c                   eval      jusord = d_sord
     c                   eval      juseus = l_user
      *
     c                   time                    jusoda
     c                   time                    juseda
     c                   time                    juseti
      *
     c                   write     jmstlur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av standard-informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beh_standard  begsr
      *
     c                   eval      d_strc = *blank
      *
     c                   eval      jmsdlu_umod = d_modn_num
     c     jmsdlu_key    setll     jmsdlu
     c     jmsdlu_key    reade     jmsdlur
     c                   dow       not %eof
     c                   delete    jmsdlur
     c     jmsdlu_key    reade     jmsdlur
     c                   enddo
      *
     c                   dow       not %eof(nxmopf)
      *
      *  Slutt på standardelement
      *
     c                   if        %subst(nxmodu:1:11) = c_sstd_e
     c                   leave
     c                   endif
      *
      *  Enkeltelement standard herfra
      *
     c                   eval      w_stda = %scan(c_xend:nxmodu:1)
     c                   eval      w_slda = %scan(c_xsle:nxmodu:w_stda)
     c                   eval      w_stda = w_stda + 1
      *
     c                   eval      w_wlen = *zero
     c                   eval      w_welm = *blank
     c                   if        w_slda > w_stda
     c                   eval      w_wlen = w_slda - w_stda
     c                   eval      w_welm = %subst(nxmodu:w_stda:w_wlen)
     c                   endif
      *
     c                   select
     c                   when      %subst(nxmodu:1:5) = c_stan
     c                   exsr      uttr_stan
     c                   exsr      oppd_standard
     c                   endsl
      *
     c                   delete    nxmopfr
      *
     c                   read      nxmopfr
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av standardelement : Std
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_stan     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_sstd = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer modulstandard
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_standard begsr
      *
     c                   clear                   jmsdlur
      *
     c                   eval      juumod = d_modn_num
     c                   eval      juustd = d_sstd
     c                   eval      juueus = l_user
      *
     c                   time                    juuoda
     c                   time                    juueda
     c                   time                    juueti
      *
     c                   write     jmsdlur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av fritekst-informasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beh_fritekst  begsr
      *
     c                   eval      d_ftrc = *blank
     c                   eval      w_line = *zero
      *
     c                   eval      jmtxlu_tmod = d_modn_num
     c     jmtxlu_key    setll     jmtxlu
     c     jmtxlu_key    reade     jmtxlur
     c                   dow       not %eof
     c                   delete    jmtxlur
     c     jmtxlu_key    reade     jmtxlur
     c                   enddo
      *
     c                   dow       not %eof(nxmopf)
      *
      *  Slutt på fritekstelement
      *
     c                   if        %subst(nxmodu:1:11) = c_ssft_e
     c                   leave
     c                   endif
      *
      *  Enkeltelement fritekst herfra
      *
     c                   eval      w_stda = %scan(c_xend:nxmodu:1)
     c                   eval      w_slda = %scan(c_xsle:nxmodu:w_stda)
     c                   eval      w_stda = w_stda + 1
      *
     c                   eval      w_wlen = *zero
     c                   eval      w_welm = *blank
     c                   if        w_slda > w_stda
     c                   eval      w_wlen = w_slda - w_stda
     c                   eval      w_welm = %subst(nxmodu:w_stda:w_wlen)
     c                   endif
      *
     c                   select
     c                   when      %subst(nxmodu:1:5) = c_teks
     c                   exsr      uttr_teks
     c                   exsr      oppd_fritekst
     c                   endsl
      *
     c                   delete    nxmopfr
      *
     c                   read      nxmopfr
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av fritekstelement : Txt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_teks     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_ftxt = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer modulfritekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_fritekst begsr
      *
     c                   clear                   jmtxlur
      *
     c                   eval      w_line = w_line + 1
      *
     c                   eval      jutmod = d_modn_num
     c                   eval      jutlin = w_line
     c                   eval      juttxt = d_ftxt
     c                   eval      juteus = l_user
      *
     c                   time                    jutoda
     c                   time                    juteda
     c                   time                    juteti
      *
     c                   write     jmtxlur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer modul
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_modu      begsr
      *
     c                   if        b_mfel = *on
     c                   leavesr
     c                   endif
      *
     c                   clear                   jmodlur
      *
     c                   eval      jmodlu_modn = d_modn_num
     c     jmodlu_key    chain     jmodlur
      *
     c*                  if        d_stat = '4'
     c*                  if        %found
     c*                  delete    jmodlur
     c*                  endif
     c*                  leavesr
     c*                  endif
      *
     c                   eval      jumte1 = d_mte1
     c                   eval      jumte2 = d_mte2
     c                   eval      jumerk = d_merk
     c                   eval      juldor = d_ldor_num
     c                   eval      juprod = d_prod_num
     c                   eval      juogrp = d_ogrp_num
     c                   eval      juhgrp = d_hgrp_num
     c                   eval      juugrp = d_ugrp_num
     c                   eval      jueusr = l_user
      *
     c                   time                    juedat
     c                   time                    juetim
      *
     c                   if        %found(jmodlu)
     c                   update    jmodlur
     c                   else
     c                   eval      jumodn = d_modn_num
     c                   time                    juodat
     c                   write     jmodlur
     c                   endif
      *
      * Oppdaterer alle varer med endret modulinformasjon
      *
     c                   eval      jvarlg_modn = d_modn_num
     c     jvarlg_key    setll     jvarlg
     c     jvarlg_key    reade     jvarlgr
     c                   dow       not %eof
      *
     c                   if        jvldor <> juldor or
     c                             jvprod <> juprod or
     c                             jvogrp <> juogrp or
     c                             jvhgrp <> juhgrp or
     c                             jvugrp <> juugrp
     c                   eval      jvldor = juldor
     c                   eval      jvprod = juprod
     c                   eval      jvogrp = juogrp
     c                   eval      jvhgrp = juhgrp
     c                   eval      jvugrp = juugrp
     c                   eval      jveusr = l_user
     c                   time                    jvedat
     c                   time                    jvetim
     c                   update    jvarlgr
     c                   else
     c                   unlock    jvarlg
     c                   endif
      *
     c     jvarlg_key    reade     jvarlgr
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tilbakestilling av XML-escape tegn i tekstfelt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     konv_XML_esc  begsr
      *
     c                   eval      w_posi = 0
     c                   eval      w_posu = 0
     c                   eval      w_str_u = *blank
     c                   eval      b_blnk = *off
      *
     c                   dow       w_posi < 395 and
     c                             b_blnk = *off
     c                   eval      w_posi = w_posi + 1
     c                   if        %subst(w_str_i:w_posi) = *blank
     c                   eval      b_blnk = *on
     c                   else
     c                   eval      w_str_x = %subst(w_str_i:w_posi:1)
     c                   if        w_posu < 395
     c                   select
     c                   when      %subst(w_str_i:w_posi:5) = '&amp;'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = '&'
     c                   eval      w_posi = w_posi + 4
      *
     c                   when      %subst(w_str_i:w_posi:4) = '&lt;'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = '<'
     c                   eval      w_posi = w_posi + 3
      *
     c                   when      %subst(w_str_i:w_posi:4) = '&gt;'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = '>'
     c                   eval      w_posi = w_posi + 3
      *
     c                   when      %subst(w_str_i:w_posi:6) = '&apos;'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = c_apos
     c                   eval      w_posi = w_posi + 5
      *
     c                   when      %subst(w_str_i:w_posi:6) = '&quot;'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = '"'
     c                   eval      w_posi = w_posi + 5
      *
     c                   other
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = w_str_x
      *
     c                   endsl
     c                   endif
     c                   endif
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
      *
      * Key for oppdatering av modul
      *
     c     jmodlu_key    klist
     c                   kfld                    jmodlu_modn
      *
      * Key for oppdatering av modul-stikkord
      *
     c     jmstlu_key    klist
     c                   kfld                    jmstlu_smod
      *
      * Key for oppdatering av modul-standard
      *
     c     jmsdlu_key    klist
     c                   kfld                    jmsdlu_umod
      *
      * Key for oppdatering av modul-tekst
      *
     c     jmtxlu_key    klist
     c                   kfld                    jmtxlu_tmod
      *
      * Key for oppdatering av vare
      *
     c     jvarlg_key    klist
     c                   kfld                    jvarlg_modn
      *
      * Key for oppslag på status
      *
     c     jstsl1_key    klist
     c                   kfld                    w_firm
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c     jstsl1_key    chain     jstsl1
      *
     c                   endsr
