     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : RY770R
      * Beskrivelse . . : FIX: Leser Nobb Kontrakt og oppretter påslag
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       061105 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fpcbetpf   if   e           k disk
      *
     fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
     fjvprl1    if   e           k disk    rename(jvprpfr:jvprl1r)
     fjpfalu    uf a e           k disk    rename(jpfapfr:jpfalur)
      *
      * Definering av Local data area *LDA
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Betingelse-fil
      *
     d                 ds
     d d_bet                        240
     d  d_beti                       35    overlay(d_bet:1)
     d  d_ldor                        6  0 overlay(d_bet:46)
     d  d_ogrp                        2    overlay(d_bet:52)
     d  d_hgrp                        2    overlay(d_bet:54)
     d  d_ugrp                        3    overlay(d_bet:56)
     d  d_modn                        8    overlay(d_bet:59)
     d  d_nobb                        8    overlay(d_bet:67)
     d  d_kofa                        9    overlay(d_bet:85)
     d   d_kofa_hel                   3  0 overlay(d_kofa:1)
     d   d_kofa_des                   5  5 overlay(d_kofa:5)
     d  d_safa                        9    overlay(d_bet:94)
     d   d_safa_hel                   3  0 overlay(d_safa:1)
     d   d_safa_des                   5  5 overlay(d_safa:5)
     d  d_kobe                        9    overlay(d_bet:123)
     d   d_kobe_kr                    6  0 overlay(d_kobe:1)
     d   d_kobe_Øre                   2  2 overlay(d_kobe:8)
     d  d_sabe                        9    overlay(d_bet:132)
     d   d_sabe_kr                    6  0 overlay(d_sabe:1)
     d   d_sabe_Øre                   2  2 overlay(d_sabe:8)
     d  d_akti                        1    overlay(d_bet:103)
     d  d_line                        8  0 overlay(d_bet:141)
      *
      * Definering av variable i nøkler
      *
     d jvarl3_nobb     s                   like(jvnobb)
     d jvprl1_vare     s                   like(jxvare)
     d jvprl1_ldor     s                   like(jxldor)
      *
      * Definering av variable
      *
     d w_firm          s                   like(jcfirm)
     d w_ogrp          s                   like(jcogrp)
     d w_hgrp          s                   like(jchgrp)
     d w_ugrp          s                   like(jcugrp)
     d w_modn          s                   like(jcmodn)
     d w_vare          s                   like(jcvare)
      *
     d w_sabe          s              9  2
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser betingelser, og oppretter påslag
      *
     c                   read      pcbetpf
     c                   dow       not %eof
     c                   exsr      opprett
     c                   read      pcbetpf
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av påslag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opprett       begsr
      *
     c                   eval      d_bet = pcbet
      *
     c                   eval      w_ogrp = 0
     c                   eval      w_hgrp = 0
     c                   eval      w_ugrp = 0
     c                   eval      w_modn = 0
     c                   eval      w_vare = *blank
      *
      * Avbryter dersom leverandør ikke er utfylt
      *
     c                   if        d_ldor = 0
     c                   leavesr
     c                   endif
      *
     c                   if        d_akti <> 'A'
     c                   leavesr
     c                   endif
      *
      * Henter betingelse. Dersom betingelse på varenummernivå, hentes
      * varegruppe og modul fra vare.
      *
     c                   if        d_ogrp <> '--'
     c                   move      d_ogrp        w_ogrp
     c                   endif
      *
     c                   if        d_hgrp <> '--'
     c                   move      d_hgrp        w_hgrp
     c                   endif
      *
     c                   if        d_ugrp <> '---'
     c                   move      d_ugrp        w_ugrp
     c                   endif
      *
     c                   if        d_modn <> '--------'
     c                   move      d_modn        w_modn
     c                   endif
      *
     c                   if        d_nobb <> '--------'
     c                   move      d_nobb        jvarl3_nobb
     c     jvarl3_key    chain     jvarl3
     c                   if        not %found
     c                   leavesr
     c                   endif
     c                   eval      w_ogrp = jvogrp
     c                   eval      w_hgrp = jvhgrp
     c                   eval      w_ugrp = jvugrp
     c                   eval      w_modn = jvmodn
     c                   eval      w_vare = jvvare
     c                   endif
      *
     c     ' ':'0'       xlate     d_kofa        d_kofa
     c     ' ':'0'       xlate     d_safa        d_safa
     c     ' ':'0'       xlate     d_kobe        d_kobe
     c     ' ':'0'       xlate     d_sabe        d_sabe
      *
      * Oppdaterer/oppretter betingelse
      *
     c                   clear                   jpfalur
      *
     c                   eval      jcfirm = w_firm
     c                   eval      jcogrp = w_ogrp
     c                   eval      jchgrp = w_hgrp
     c                   eval      jcugrp = w_ugrp
     c                   eval      jcldor = d_ldor
     c                   eval      jcmodn = w_modn
     c                   eval      jcvare = w_vare
      *
     c                   eval      jckofa = d_kofa_hel + d_kofa_des
     c                   eval      jckobe = d_kobe_kr  + d_kobe_Øre
     c                   eval      jcsafa = d_safa_hel + d_safa_des
      *
     c**                 eval      w_sabe = d_sabe_kr + d_sabe_Øre
     c**                 if        w_sabe <> 0 and
     c**                           w_vare <> *blank
     c**                 eval      jvprl1_vare = w_vare
     c**                 eval      jvprl1_ldor = d_ldor
     c**   jvprl1_key    chain     jvprl1
     c**                 if        %found and
     c**                           jxpris <> 0
     c**                 eval      jcsafa = w_sabe / jxpris
     c**                 endif
     c**                 endif
      *
     c                   eval      jceusr = l_user
      *
     c                   time                    jcodat
     c                   time                    jcedat
     c                   time                    jcetim
      *
     c                   if        jckofa <> 1 or
     c                             jckobe <> 0 or
     c                             jcsafa <> 1
     c                   write     jpfalur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på vare
      *
     c     jvarl3_key    klist
     c                   kfld                    jvarl3_nobb
      *
      * Key for oppslag på vare-pris
      *
     c     jvprl1_key    klist
     c                   kfld                    jvprl1_vare
     c                   kfld                    jvprl1_ldor
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
