     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VP680R
      * Beskrivelse . . : Vare-pris, prisfil til kunder - parameter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       300599 HKO  Nytt program
      *       260400 HKO  Lagt inn valg av mappenavn
      *       260606 HKO  Lagt inn valg av leveringstype
5.70  * PRGR  110509 BOF  Lagt inn valg av lager - hvis lagerspes.priser
6.20  *       111110 bof  Utvidet med lager på varesortiment
7.xx  * 7.00  290916 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
6.32  * 6.32  151019 hko  Utvidet med valg av EDI varekatalog
6.32  *                   EDI varekatalog forutsetter oppsett av EDI
6.32  *                   utveksling mot Logiq.
7.01  *k00  170620 BOF NXS-1195 Utvidet med parameter fra kundedistr.
7.02  *     220617 bof  Paramter for å kun ta ut nobbnr.
7.03  *     231018 sst  Fjernet opppretting av mappe i qdls
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra06pf    if   e           k disk    rename(ra06pfr:ra06pfr)
5.70 fra10l1    if   e           k disk    rename(ra10pfr:ra10l1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     ffkdil1    if   e           k disk    rename(fkdipfr:fkdil1r)
     fvshel1    if   e           k disk    rename(vshepfr:vshel1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     ffprcl1    if   e           k disk    rename(fprcpfr:fprcl1r)
      *
     fvp680d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Datastruktur for parameterliste -ut
      *
     d                 ds
7.02 d d_list                        82
     d  d_firm                        3  0 overlay(d_list)
     d  d_rkat                        3  0 overlay(d_list:4)
     d  d_kund                        6  0 overlay(d_list:7)
     d  d_kpro                        6  0 overlay(d_list:13)
     d  d_rfmt                       10    overlay(d_list:19)
     d  d_vsor                       10    overlay(d_list:29)
     d  d_ogrp                        2  0 overlay(d_list:39)
     d  d_hgrp                        2  0 overlay(d_list:41)
     d  d_ugrp                        3  0 overlay(d_list:43)
     d  d_ldor                        6  0 overlay(d_list:46)
     d  d_rfm2                       10    overlay(d_list:52)
     d  d_sval                        1  0 overlay(d_list:62)
     d  d_tval                        1  0 overlay(d_list:63)
     d  d_2val                        1  0 overlay(d_list:64)
     d  d_bval                        1  0 overlay(d_list:65)
     d  d_lety                        1  0 overlay(d_list:66)
     d  d_dato                         d   overlay(d_list:67) datfmt(*iso)
5.70 d  d_lage                        2  0 overlay(d_list:79)
7.02 d  d_nval                        1  0 overlay(d_list:81)
      *
      * Definering av variabler i nøkler
      *
     d ra06pf_fkod     s                   like(rafkod)
5.70 d ra10l1_jkod     s                   like(rajkod)
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d fkdil1_kund     s                   like(fhkund)
     d fkdil1_kpro     s                   like(fhkpro)
     d vltpl1_lety     s                   like(vylety)
     d vshel1_vsor     s                   like(vcvsor)
6.20 d vshel1_lage     s                   like(vclage)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d rlevl1_levr     s                   like(rllevr)
     d fprcl1_rfmt     s                   like(fcrfmt)
      *
      * Definering av variable
      *
     d w_udat          s               d   datfmt(*iso)
6.32 d w_ediv          s              1
      *
     d w_firm          s                   like(flfirm)
      *
     d b_feil          s              1    inz(*off)
     d b_frst          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandler første bildet
      *
     c     b1taga        tag
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
      * Behandler andre bildet
      *
     c     b2taga        tag
     c                   exfmt     b2bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   goto      b1taga
     c                   endif
      *
      * Sjekker input-felter
      *
     c                   if        b2fldr = *blank
     c                   eval      b2fldr = 'PRISFIL'
     c                   endif
      *
     c                   eval      b2fldr = %trim(b2fldr)
      *
      * Sletter mappe og oppretter den på nytt
      *
     c                   exsr      mappe
     c                   if        b_feil = *on
     c                   eval      b_feil = *off
     c                   goto      b2taga
     c                   endif
      *
6.32  * Valg om EDI varekatalog dersom kunde er satt opp med EDI
6.32  * og det er valgt Logiq varekatalog
6.32  *
6.32 c                   eval      b2ediv = 0
6.32 c                   if        b1kund <> 0 and
6.32 c                             rkordb = 1 and
6.32 c                             rkeank <> 0
6.32 c                   if        b1rfm2 = 'LOGIQ' or
6.32 c                             b1rfm2 = *blank and
6.32 c                             %found(fkdil1) and
6.32 c                             fhrfmt = 'LOGIQ'
6.32 c                   exsr      xb2win
6.32 c                   endif
6.32 c                   endif
      *
      * Kaller program for generering av prisfil
      *
     c                   eval      d_firm = w_firm
     c                   eval      d_rkat = b1rkat
     c                   eval      d_kund = b1kund
     c                   eval      d_kpro = b1kpro
     c                   eval      d_rfmt = b1rfmt
     c                   eval      d_lety = b1lety
5.70 c                   eval      d_lage = b1lage
     c                   eval      d_vsor = b1vsor
     c                   eval      d_ogrp = b1ogrp
     c                   eval      d_hgrp = b1hgrp
     c                   eval      d_ugrp = b1ugrp
     c                   eval      d_ldor = b1ldor
     c                   eval      d_rfm2 = b1rfm2
     c                   eval      d_sval = b1sval
     c                   eval      d_tval = b1tval
     c                   eval      d_2val = b12val
     c                   eval      d_bval = b1bval
7.02 c                   eval      d_nval = b1nval
     c     *dmy          move      b1dato        d_dato
      *
6.32 c                   if        b2ediv = 1
6.32 c                   eval      w_ediv = *on
6.32 c                   else
6.32 c                   eval      w_ediv = *off
6.32 c                   endif
      *
     c                   call      'VP680C'
     c                   parm                    d_list
     c                   parm                    b2fldr
6.32 c                   parm                    w_ediv
      *
     c                   exfmt     b2sbm
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av spørringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Rabattkategori
      *
     c                   if        w_afld = 'B1RKAT'
     c                   call      'RA506R'
     c                   parm                    w_firm
     c                   parm                    b1rkat
     c                   parm                    b1rtxt
     c                   leavesr
     c                   endif
      *
      * Kunde
      *
     c                   if        w_afld = 'B1KUND'
     c                   eval      b1kpro = 0
     c                   eval      b1pnav = *blank
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    b1kund
     c                   parm                    b1knav
     c                   leavesr
     c                   endif
      *
      * Kundeprosjekt
      *
     c                   if        w_afld = 'B1KPRO'
     c                   call      'FL500R'
     c                   parm                    w_firm
     c                   parm                    b1kund
     c                   parm                    b1kpro
     c                   parm                    b1knav
     c                   parm                    b1pnav
     c                   leavesr
     c                   endif
      *
      * Varesortiment
      *
     c                   if        w_afld = 'B1VSOR'
     c                   call      'VC500R'
     c                   parm                    w_firm
     c                   parm                    b1vsor
6.20 c                   parm                    b1lage
     c                   parm                    b1sbes
     c                   leavesr
     c                   endif
      *
      * Overgruppe
      *
     c                   if        w_afld = 'B1OGRP'
     c                   eval      b1hgrp = 0
     c                   eval      b1ugrp = 0
     c                   eval      b1htxt = *blank
     c                   eval      b1utxt = *blank
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1otxt
     c                   leavesr
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_afld = 'B1HGRP'
     c                   eval      b1ugrp = 0
     c                   eval      b1utxt = *blank
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    b1htxt
     c                   leavesr
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_afld = 'B1UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    b1ugrp
     c                   parm                    b1utxt
     c                   leavesr
     c                   endif
      *
      * Leverandør
      *
     c                   if        w_afld = 'B1LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    b1ldor
     c                   parm                    b1lnav
     c                   leavesr
     c                   endif
      *
      * Pris-record
      *
     c                   if        w_afld = 'B1RFMT'
     c                   call      'FC500R'
     c                   parm                    w_firm
     c                   parm                    b1rfmt
     c                   parm                    b1fbes
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'B1RFM2'
     c                   call      'FC500R'
     c                   parm                    w_firm
     c                   parm                    b1rfm2
     c                   parm                    b1fbe2
     c                   leavesr
     c                   endif
      *
      * Leveringstype
      *
     c                   if        w_afld = 'B1LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    b1lety
     c                   parm                    b1lbes
     c                   leavesr
     c                   endif
5.70  *
5.70  * Lager
5.70  *
5.70 c                   if        w_afld = 'B1LAGE'
5.70 c                   call      'RA510R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    b1lage
5.70 c                   parm                    b1ltxt
5.70 c                   leavesr
5.70 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
     c                   eval      b1rtxt = *blank
     c                   eval      b1knav = *blank
     c                   eval      b1pnav = *blank
     c                   eval      b1fbes = *blank
     c                   eval      b1lbes = *blank
     c                   eval      b1sbes = *blank
     c                   eval      b1htxt = *blank
     c                   eval      b1utxt = *blank
     c                   eval      b1lnav = *blank
     c                   eval      b1fbe2 = *blank
      *
      * Rabattkategori
      *
     c                   if        b1rkat <> 0
     c                   eval      ra06pf_fkod = b1rkat
     c     ra06pf_key    chain     ra06pf                             31
     c                   if        *in31 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1rtxt = raftxt
     c                   endif
      *
     c                   if        b1rkat <> 0 and
     c                             b1kund <> 0
     c                   eval      *in32  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Kunde
      *
     c                   if        b1kund <> 0
     c                   eval      rkunl1_kund = b1kund
     c     rkunl1_key    chain     rkunl1                             33
     c                   if        *in33 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1knav = rknavn
     c                   endif
      *
     c                   if        b1kund = 0 and
     c                             b1kpro <> 0
     c                   eval      *in34  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b1kund <> 0 and
     c                             b1rfmt <> *blank
     c                   eval      *in35  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Kundeprosjekt
      *
     c                   if        b1kpro <> 0
     c                   eval      fkprl1_kund = b1kund
     c                   eval      fkprl1_kpro = b1kpro
     c     fkprl1_key    chain     fkprl1                             36
     c                   if        *in36 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1pnav = flnavn
     c                   endif
      *
      * Pris-recordformat
      *
     c                   if        b1rfmt <> *blank
     c                   eval      fprcl1_rfmt = b1rfmt
     c     fprcl1_key    chain     fprcl1                             37
     c                   if        *in37 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1fbes = fcbesk
     c                   endif
      *
      * Leveringstype
      *
     c                   eval      vltpl1_lety = b1lety
     c     vltpl1_key    chain     vltpl1                             48
     c                   if        *in48 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      b1lbes = vybesk
5.70  *
5.70  * Lager
5.70  *
5.70 c                   if        b1lage <> 0
5.70 c                   eval      ra10l1_jkod = b1lage
5.70 c     ra10l1_key    chain     ra10l1                             49
5.70 c                   if        *in49 = *on
5.70 c                   eval      b_feil = *on
5.70 c                   leavesr
5.70 c                   endif
5.70  *
5.70 c                   eval      b1ltxt = rajtxt
5.70 c                   endif
      *
      * Dato
      *
     c                   if        b1dato = 0
     c     *dmy          move      w_udat        b1dato
     c                   endif
      *
     c                   if        b1dato > 0
     c     *dmy          test(d)                 b1dato                 38
     c                   if        *in38
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Overstyring av vareutplukk og pris-recordformat må oppgis når
      * kunde ikke finnes i distribusjonslisten. Pris-recordformat må også
      * oppgis dersom pris-recordformt ikke er oppgitt i
      * distribusjonslisten.
      *
     c                   if        b1kund <> 0
      *
     c                   eval      fkdil1_kund = b1kund
     c                   eval      fkdil1_kpro = b1kpro
      *
     c                   if        b1kpro <> 0
     c     fkdil1_key1   chain     fkdil1                             90
     c                   else
     c     fkdil1_key2   chain     fkdil1                             90
     c                   endif
      *
     c                   if        *in90 = *on and
     c                             b1vsor = *blank and
     c                             b1ogrp = 0 and
     c                             b1hgrp = 0 and
     c                             b1ugrp = 0 and
     c                             b1ldor = 0
     c                   eval      *in39  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        *in90 = *on and
     c                             b1rfm2 = *blank
     c                   eval      *in40  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
7.01 c                   if        %found(fkdil1) and
7 01 c                             b_frst = *off
7.01 c                   eval      b1sval = fhsval
7.01 c                   eval      b1tval = fhtval
7.01 c                   eval      b_frst = *on
7.01 c                   eval      b_feil = *on
7.01 c                   endif
      *
     c                   endif
      *
      * Varesortiment
      *
     c                   if        b1vsor <> *blank
     c                   if        b1ogrp <> 0 or
     c                             b1hgrp <> 0 or
     c                             b1ugrp <> 0 or
     c                             b1ldor <> 0
     c                   eval      *in41  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b1vsor <> *blank
     c                   eval      vshel1_vsor = b1vsor
6.20 c                   eval      vshel1_lage = b1lage
6.20 c     vshel1_key    chain     vshel1
6.20 c                   if        not %found(vshel1)
6.20 c                   eval      vshel1_lage = 0
6.20 c     vshel1_key    chain     vshel1
6.20 c                   if        not %found(vshel1)
6.20 c                   eval      *in42 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
6.20 c                   endif
     c                   eval      b1sbes = vcbesk
     c                   endif
      *
      * Overgruppe
      *
     c                   if        b1ogrp <> 0
     c                   eval      vogrl1_oogr = b1ogrp
     c     vogrl1_key    chain     vogrl1                             43
     c                   if        *in43 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1otxt = vgotxt
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        b1hgrp <> 0
     c                   eval      vhgrl1_hogr = b1ogrp
     c                   eval      vhgrl1_hhgr = b1hgrp
     c     vhgrl1_key    chain     vhgrl1                             44
     c                   if        *in44 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1htxt = vghtxt
     c                   endif
      *
      * Undergruppe
      *
     c                   if        b1ugrp <> 0
     c                   eval      vugrl1_uogr = b1ogrp
     c                   eval      vugrl1_uhgr = b1hgrp
     c                   eval      vugrl1_uugr = b1ugrp
     c     vugrl1_key    chain     vugrl1                             45
     c                   if        *in45 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1utxt = vgutxt
     c                   endif
      *
      * Leverandør
      *
     c                   if        b1ldor <> 0
     c                   eval      rlevl1_levr = b1ldor
     c     rlevl1_key    chain     rlevl1                             46
     c                   if        *in46 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1lnav = rlnavn
     c                   endif
      *
      * Pris-recordformat (overstyringer)
      *
     c                   if        b1rfm2 <> *blank
     c                   eval      fprcl1_rfmt = b1rfm2
     c     fprcl1_key    chain     fprcl1                             47
     c                   if        *in47 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1fbe2 = fcbesk
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Kaller program som sletter eksisterende mappe og oppretter denne
      * på nytt. Dersom feil oppsto under behandling av mappe, returneres
      * feil-flagg, og feilmeldingsbilde sendes opp
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     mappe         begsr
      *
     c                   eval      b_feil = *off
      *
7.03 c*                  call      'VP683C'
7.03 c*                  parm                    b2fldr
7.03 c*                  parm                    b_feil
      *
7.03 c*                  if        b_feil = *on
7.03 c*                  exfmt     b2msg
7.03 c*                  endif
      *
     c                   endsr
      *
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * Subrutine for behandling av varekatalog-bildet (B2WIN)
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     xb2win        begsr
6.32  *
6.32 c                   exfmt     b2win
6.32  *
6.32 c                   if        *inkc or *inkl
6.32 c                   eval      b2ediv = 0
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på rabattkategori
      *
     c     ra06pf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra06pf_fkod
5.70  *
5.70  * Key for oppslag på lager
5.70  *
5.70 c     ra10l1_key    klist
5.70 c                   kfld                    w_firm
5.70 c                   kfld                    ra10l1_jkod
      *
      * Key for oppslag på kunde-register
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kundeprosjekt-register
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag på distribusjonsliste-register
      *
     c     fkdil1_key1   klist
     c                   kfld                    w_firm
     c                   kfld                    fkdil1_kund
     c                   kfld                    fkdil1_kpro
      *
     c     fkdil1_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    fkdil1_kund
      *
      * Key for oppslag på leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
      *
      * Key for oppslag på varesortiment-hode
      *
     c     vshel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vshel1_vsor
6.20 c                   kfld                    vshel1_lage
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på pris recordformat
      *
     c     fprcl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fprcl1_rfmt
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjerm-felter
      *
     c                   time                    w_udat
     c     *dmy          move      w_udat        b1dato
      *
     c                   eval      b2fldr = 'PRISFIL'
      *
     c                   endsr
