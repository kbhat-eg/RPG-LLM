     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FX805R
      * Beskrivelse . . : Oppdaterer lagerbeholdning fra pakkseddel
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       181202 ASK  Nytt program
      * 6.10  240609 BSA  Oppdatert med sporingsinformasjon
      * 6.10  220909 ASK  Nye parametre til VL001R
6.11  * 6.10  110610 bof  Justert i utvidelse av parm VL001R
6.nu  * 6.30  050614 bof  Utvidet mtp. nummerutvidelse
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffohelu    uf a e           k disk    rename(fohepfr:fohelur)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
      *
      * Definering av local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Data-struktur for lageroppdatering
      *
     d                 ds
6.nu d hlrec                        128
     d  hlvare                       15    overlay(hlrec)
     d  hllage                        2  0 overlay(hlrec:16)
     d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
     d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
     d  hlotyp                        2    overlay(hlrec:36)
     d  hlltyp                        2    overlay(hlrec:38)
     d  hlanta                       11  3 overlay(hlrec:40)
     d  hlkopr                        9  2 overlay(hlrec:51)
     d  hlrefr                       20    overlay(hlrec:60)
     d  hlsyst                        1    overlay(hlrec:80)
     d  hlaltk                        2    overlay(hlrec:81)
     d  hlrest                        1    overlay(hlrec:83)
     d  hltype                        1    overlay(hlrec:84)
     d  hlenhe                        3    overlay(hlrec:85)
     d  hlinlr                        1    overlay(hlrec:88)
     d  hllety                        1    overlay(hlrec:89)
6.nu d  hlonum                        8  0 overlay(hlrec:90)
6.nu d  hlolin                        4  0 overlay(hlrec:98)
      *
      * Definering av variable i nøkler
      *
     d w_firm          s                   like(fofirm)
     d fohelu_numm     s                   like(fonumm)
     d fohelu_suff     s                   like(fosuff)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d votyl1_otyp     s                   like(vaotyp)
      *
      * Definering av variable
      *
6.nu d w_lrec          s            128
     d w_date          s                   like(foldat)
     d w_dato          s                   like(foldat)
     d w_stat          s              1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Leser alle ordrehoder på firma
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     fohel1_key    setll     fohel1
     c     fohel1_key    reade     fohel1
      *
     c                   dow       not %eof
      *
      * Kjøres det lagerstyring ?
      *
     c                   if        faalag = 0
     c                   goto      les_hode
     c                   endif
      *
      * Lager allerede oppdatert?
      *
     c                   if        foolag = 1
     c                   goto      les_hode
     c                   endif
      *
      * Hent opplysninger fra ORDRETYPE-REGISTER
      *
     c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1r
     c                   if        not %found or
     c                             vaoakk <> 2
     c                   goto      les_hode
     c                   endif
      *
     c                   exsr      les_linjer
      *
      * Oppdaterer ordrehode
      *
     c                   eval      fohelu_numm = fonumm
     c                   eval      fohelu_suff = fosuff
      *
     c     fohelu_key    chain     fohelu
     c                   if        %found
     c                   eval      foolag = 1
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   update    fohelur
     c                   endif
      *
     c     les_hode      tag
     c     fohel1_key    reade     fohel1
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Leser ordrelinjer og oppdaterer lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_linjer    begsr
      *
     c                   eval      fodtl1_numm = fonumm
     c                   eval      fodtl1_suff = fosuff
      *
     c     fodtl1_key    setll     fodtl1
     c     fodtl1_key    reade     fodtl1
      *
     c                   dow       not %eof
      *
      * Redusere til utlevering
      *
      *
      * Redusere lagerbeholdning
      *
     c                   select
     c                   when      fdldat <> *loval
     c                   eval      w_dato = fdldat
     c                   when      foldat <> *loval
     c                   eval      w_dato = foldat
     c                   when      fobdat <> *loval
     c                   eval      w_dato = fobdat
     c                   other
     c                   eval      w_dato = w_date
     c                   endsl
      *
     c                   exsr      opp_lag
      *
     c     fodtl1_key    reade     fodtl1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatering av Lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opp_lag       begsr
      *
      * Legger over verdier i overførings-felter
      *
     c                   eval      hlvare = fdvare
     c                   eval      hllage = folage
     c                   eval      hlenhe = fdenh1
     c                   eval      hldato = w_dato
     c                   eval      hltime = *loval
     c                   eval      hlotyp = footyp
     c                   eval      hlltyp = fdltyp
      *
     c                   eval      hlanta = fdanta
     c                   eval      hlkopr = fdkopr
     c                   eval      hlrefr = 'O:' + %char(fdnumm) +
     c                                      'L:' + %char(fdline)
     c                   eval      hlonum = fdnumm
     c                   eval      hlolin = fdline
     c                   eval      hlsyst = 'F'
     c                   eval      hlaltk = *blank
     c                   eval      hlrest = *blank
     c                   move      fdlety        hllety
      *
      * Kaller opp lageroppdaterings-program
      *
     c                   eval      w_lrec = hlrec
      *
     c                   call      'VL001R'
     c                   parm                    w_stat
     c                   parm                    w_lrec
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les av OFAK-KODE-REGISTER
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les av ORDRE-HODE
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppdatering av ORDRE-HODE
      *
     c     fohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelu_numm
     c                   kfld                    fohelu_suff
      *
      * Key for les av ORDRE-LINJE
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
      *
      * Key for les av ORDRETYPE
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Legger inn startverdier
      *
     c                   eval      hltype = *blank
     c                   eval      w_firm      = l_firm
      *
      * Legger inn dagens dato
      *
     c                   time                    w_date
      *
      * Leser OFAK Kode register
      *
     c     fstsl1_key    chain     fstsl1r
      *
     c                   endsr
