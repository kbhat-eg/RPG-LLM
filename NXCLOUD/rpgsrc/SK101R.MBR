     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASSTAT
      *  Program . . . . : SK101R
      *  Beskrivelse . . : Rekalkulerer referansepriser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato  Endringsbeskrivelse                     Sign
      * --------- ------  --------------------------------------------------
      *           250219  Nytt program
9.01  *           110319  Fortegn på beløp, må styres av fortegn på antall
9.02  *           110319  SAREFK skal følge verdien i SFPKON
9.03  *   bsa     190525  Håndterer for store beløp
      *
8.01  *PRG2   160622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsstal1    if   e           k disk    rename(sstapfr:sstal1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fsrepl1    if   e           k disk    rename(sreppfr:srepl1r)
     fsreplu    uf   e           k disk    rename(sreppfr:sreplur)
     fsanslu    uf a e           k disk    rename(sanspfr:sanslur)
      *
      * Definering av array
      *
      *
      *  Local Data Area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d votyl1_otyp     s                   like(vaotyp)
     d rkunl1_kund     s                   like(rkkund)
     d vvarl1_vare     s                   like(vvvare)
     d sanslu_paar     s                   like(sfpaar)
     d sanslu_binr     s                   like(sfbinr)
     d sanslu_ornr     s                   like(sfornr)
     d sanslu_orsu     s                   like(sforsu)
     d sanslu_orli     s                   like(sforli)
     d sanslu_bong     s                   like(sfbong)
     d sanslu_vare     s                   like(sfvare)
     d sanslu_enhe     s                   like(sfenhe)
     d sanslu_anta     s                   like(sfanta)
     d sanslu_date     s                   like(sfdate)
     d sanslu_time     s                   like(sftime)

      *
      * Definering av variable
      *
     d w_firm          s                   like(sffirm)
      *
     d w_raba          s                   like(sfsumb)
     d w_sumr          s                   like(sfsumb)
     d w_pkon          s              3
     d w_sumn          s             11  2
     d w_suno          s             11  2
     d w_ntpr          s             11  2
     d w_suok          s             11  2
     d w_diff          s             11  2
     d w_pkns          s              3
     d w_pkko          s              3
     d w_pkok          s              3
     d w_pkoi          s              3
     d w_kild          s              3
     d w_rkro_stk      s             11  2
9.03 d w_xsum          s             15  2
      *
     d p_paar          s                   like(sfpaar)
     d b_les_stat      s              1    inz(*off)
      *
      * Datastruktur parameterliste -inn
      *
     d                 ds
     d hirec                        120
     d  hifirm                        3  0 overlay(hirec)
     d  hirkat                        3  0 overlay(hirec:4)
     d  hikund                        6  0 overlay(hirec:7)
     d  hikpro                        6  0 overlay(hirec:13)
     d  hivare                       15    overlay(hirec:19)
     d  hilety                        1  0 overlay(hirec:34)
     d  hienhe                        3    overlay(hirec:35)
     d  hidato                         d   overlay(hirec:38) datfmt(*iso)
     d  hitime                         t   overlay(hirec:48) timfmt(*iso)
     d  hinumm                        8  0 overlay(hirec:56)
     d  hikode                        1    overlay(hirec:64)
     d  hidekn                        5  2 overlay(hirec:65)
     d  hiavgf                        1  0 overlay(hirec:70)
     d  hipriv                        1  0 overlay(hirec:71)
     d  hitilb                        1    overlay(hirec:72)
     d  hiskaf                        1    overlay(hirec:73)
     d  hildor                        6  0 overlay(hirec:74)
     d  hisapr                        9  2 overlay(hirec:80)
     d  hikopr                        9  2 overlay(hirec:89)
8.01 d  hiprgr                        2    overlay(hirec:98)
8.01 d  hiinpr                        9  2 overlay(hirec:100)
     d  hiinlr                        1    overlay(hirec:120)
      *
      * Datastruktur parameterliste -ut
      *
     d                 ds
     d horec                        100
     d  holety                        1  0 overlay(horec)
     d  hokpri                        1    overlay(horec:2)
     d  hooppr                        9  2 overlay(horec:3)
     d  hosapr                        9  2 overlay(horec:12)
     d  hokopr                        9  2 overlay(horec:21)
     d  horab1                        5  2 overlay(horec:30)
     d  horab2                        5  2 overlay(horec:35)
     d  hodekn                        5  2 overlay(horec:40)
     d  hopros                        5  2 overlay(horec:45)
     d  hokamp                        5    overlay(horec:50)
     d  hoalme                        4  0 overlay(horec:55)
     d  hobrty                        1  0 overlay(horec:59)
     d  hobrr1                        5  2 overlay(horec:60)
     d  hobrr2                        5  2 overlay(horec:65)
     d  hoomva                        1    overlay(horec:70)
8.01 d  hoprgr                        2    overlay(horec:71)
8.01 d  hoinpr                        9  2 overlay(horec:73)
8.01 d  hopkon                        3    overlay(horec:82)
8.01 d  hopkns                        3    overlay(horec:85)
8.01 d  hopkko                        3    overlay(horec:88)
8.01 d  hopkok                        3    overlay(horec:91)
8.01 d  hopkoi                        3    overlay(horec:94)
      *
      *
     C/Exec SQL
     C+  Set Option DatFmt=*ISO
     C/End-Exec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandle utplukk, Setter inn riktige parameterverdier
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Hent referansekunde og dato
      *
     c     w_firm        chain     srepl1
     c                   if        not %found
     c                   goto      end_pgm
     c                   endif
      *
      * Sjekk at det er gyldig kunde
      *
     c                   eval      rkunl1_kund = srkund
     c     rkunl1_key    chain     rkunl1
     c                   if        not %found
     c                   goto      end_pgm
     c                   endif
      * Hent inn statistikkposter fra valgt dato
     c/exec sql
      *
     c+                  declare sok cursor for
     c+                  select sfpaar,
     c+                         sfbinr,
     c+                         sfornr,
     c+                         sforsu,
     c+                         sforli,
     c+                         sfbong,
     c+                         sfvare,
     c+                         sfenhe,
     c+                         sfanta,
     c+                         sfdate,
     c+                         sftime,
     c+                         sfenho,
     c+                         sforda,
     c+                         sfotyp,
     c+                         sflety,
     c+                         sfkmva,
     c+                         sfldor,
     c+                         sflvre,
     c+                         sfsumb,
     c+                         sffsys,
     c+                         sfrabo,
     c+                         sfrab1,
     c+                         sfrab2,
     c+                         sfanto,
9.02 c+                         sfprgr,
9.02 c+                         sfpkon
     c+                  from   sstapf
     c+                  where  sffirm = :w_firm and
     c+                         sfdate >= :srkdat and
     c+                         sftime >= :srktid
     c+                  order by sfdate, sftime
     c+                  for read only
     c/end-exec
      *
     c/exec sql
     c+                  close sok
     c/end-exec
      *
     c/exec sql
     c+                  open sok
     c/end-exec
      *
     c                   eval      b_les_stat = *on
     c                   dow       b_les_stat = *on
      *
     c/exec sql
      *
     c+                  fetch next
     c+                  from sok
     c+                  into  :sfpaar,
     c+                        :sfbinr,
     c+                        :sfornr,
     c+                        :sforsu,
     c+                        :sforli,
     c+                        :sfbong,
     c+                        :sfvare,
     c+                        :sfenhe,
     c+                        :sfanta,
     c+                        :sfdate,
     c+                        :sftime,
     c+                        :sfenho,
     c+                        :sforda,
     c+                        :sfotyp,
     c+                        :sflety,
     c+                        :sfkmva,
     c+                        :sfldor,
     c+                        :sflvre,
     c+                        :sfsumb,
     c+                        :sffsys,
     c+                        :sfrabo,
     c+                        :sfrab1,
     c+                        :sfrab2,
     c+                        :sfanto,
9.02 c+                        :sfprgr,
9.02 c+                        :sfpkon
     c/end-exec
      *
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      b_les_stat = *off
     c                   endif
      *
      * Hent info fra vare
      *
     c                   eval      vvarl1_vare = sfvare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   goto      les_neste
     c                   endif
      *
      * Hent info fra ordretype
      *
     c                   eval      votyl1_otyp = sfotyp
     c     votyl1_key    chain     votyl1
      *
      * TEST
      *
9.xx c                   if        sfbinr = 112033
9.xx c                   goto      videre
9.xx c                   endif
9.xx c     videre        tag
      *
      * Behandler poster, og oppdater referanseregister
      *
     c                   exsr      kalk_prisref
      *
     c     les_neste     tag
      *
     c                   enddo
      *
      * Oppdater med kjøretidspunkt
      *
     c     w_firm        chain     sreplu
     c                   if        %found
      *
     c                   time                    srkdat
     c                   time                    srktid
      *
     c                   time                    sredat
     c                   time                    sretim
     c                   eval      sreusr = l_user
      *
     c                   update    sreplur
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å å lese all stat for ett år
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kalk_prisref  begsr
      *
     c                   eval      hifirm = w_firm
     c                   eval      hirkat = 0
      *
     c                   eval      hikund = srkund
     c                   eval      hikpro = 0
     c                   eval      hivare = sfvare
     c                   eval      hienhe = sfenho
     c                   eval      hilety = sflety
     c                   eval      hidekn = 0
     c                   eval      hidekn = 0
     c                   eval      hiavgf = sfkmva
     c                   eval      hipriv = 0
     c                   eval      hidato = sforda
     c                   eval      hitime = *loval
     c                   eval      hinumm = 0
     c                   eval      hikode = *blank
     c                   eval      hiskaf = *off
     c                   eval      hildor = sfldor
     c                   eval      hisapr = 0
     c                   eval      hikopr = 0
     c                   eval      hiinpr = 0
     c                   eval      hiinpr = 0
     c                   eval      hiprgr = sfprgr
     c                   if        sflvre = 1
     c                   eval      hiskaf = *on
     c                   else
     c                   eval      hiskaf = *off
     c                   endif
      *
     c                   call      'VP905R'
     c                   parm                    hirec
     c                   parm                    horec
      *
      * Koder fra VP905
      *
     c                   eval      w_pkon = hopkon
     c                   eval      w_pkns = hopkns
     c                   eval      w_pkko = hopkko
     c                   eval      w_pkok = hopkok
     c                   eval      w_pkoi = hopkoi
      *
      * Skaffevare får vi ikke beregnet ettersom vi ikke vet hvilken
      * betingelse som ble valgt ved ordreregistrering
     c*                  if        sflvre  = 1
      * her må det testes mer for å sette PK-koder
     c*                  if        w_bels <> 0
     c*                  if        w_pkon = *blank
     c*                  eval      w_pkon = 'VEI'
     c*                  endif
     c*                  if        w_pkns = *blank
     c*                  eval      w_pkns = 'VEI'
     c*                  endif
     c*                  endif
      *
     c*                  if        w_belk <> 0
     c*                  if        w_pkko = *blank
     c*                  eval      w_pkko = 'VEI'
     c*                  endif
     c*                  if        w_pkok = *blank
     c*                  eval      w_pkok = 'VEI'
     c*                  endif
     c*                  endif
      *
     c*                  endif
      *
      * Sum opprinnelig netto salgspris (SFSUMO) ---------------------------
      *
     c*                  if        sflvre = 1
     c*                  eval      w_ntpr = w_bels
     c*                  eval      hokopr = w_belk
     c*                  else
     c*                  endif
      *
     c                   eval      w_ntpr = hosapr
      *
     c                   if        horab1 > 0
     c                   eval(h)   w_ntpr = w_ntpr - (w_ntpr * horab1 / 100)
     c                   endif
     c                   if        horab2 > 0
     c                   eval(h)   w_ntpr = w_ntpr - (w_ntpr * horab2 / 100)
     c                   endif
      *
      * Varelinjens andel av ordrerabatt (pr. stk)
      *
     c                   if        vvkord = 0   and
     c                             rkorab <> 0
     c                   eval(h)   w_rkro_stk = (w_ntpr * rkorab) / 100
     c                   endif
      * trekkker fra ordrerabatt
     c                   eval      w_ntpr = w_ntpr - w_rkro_stk
      *
     c*                  if        sflvre = 0
9.03 c                   eval(h)   w_xsum = w_ntpr * sfanto
9.03 c                   if        w_xsum < 999999999
     c                   eval(h)   w_suno = w_ntpr * sfanto
9.03 c                   else
9.03 c                   eval(h)   w_suno = 999999999
9.03 c                   endif
     c*                  else
     c*                  eval      w_suno = 0
     c*                  endif
      *
      * sjekk negativ fortegn på ordretype -----------------------------
      *
     c                   if        vaokre = '-'
     c                   eval      w_suno = w_suno * -1
     c                   endif
      *
      * Sum netto salg salgspris (SFSUMN) --------------------------------
      *
     c                   exsr      beregn_rabatt
      *
     c                   eval(h)   w_sumn = sfsumb - w_sumr
      *
      * Priskode netto salgspris  (SFPKNS)
      *
     c                   if        w_suno <> 0 and
     c                             w_sumn <> 0
     c                   eval      w_diff  = %abs(w_suno - w_sumn)
     c                   if        w_diff > 1
     c                   eval      w_pkns  = 'MAN'
     c                   endif
     c                   endif
      *
      * Kilde  (SFKILD)
      *
9.02 c*                  if        sffsys <> *blank
9.02 c*                  eval      w_kild = sffsys
9.02 c*                  else
9.02 c*                  if        sfotyp = 'CD' or
9.02 c*                            sfotyp = 'CK'
9.02 c*                  eval      w_kild = 'KAS'
9.02 c*                  else
9.02 c*                  eval      w_kild = 'ORD'
9.02 c*                  endif
9.02 c*                  endif
      *
      * Kontakt/fakturert (SFKOFA)
      *
     c*                  if        sfotyp = 'CD' or
     c*                            sfotyp = 'CK'
     c*                  eval      w_kofa = 'K'
     c*                  else
     c*                  eval      w_kofa = 'F'
     c*                  endif
      *
      * Behandle diverse vare
      *
     c                   if        %found(vvarl1) and
     c                             vvvtyp = 'D'
     c                   eval      w_pkon = *blank
     c                   eval      w_pkns = 'DIV'
     c                   eval      w_suok = 0
     c                   eval      w_pkok = *blank
     c                   eval      w_pkko = 'DIV'
     c                   endif
      *
      * hvis skaffevare så settes alle priskilder = blank
      *
     c                   if        sflvre = 1
     c                   eval      w_pkon = *blank
     c                   eval      w_pkns = *blank
     c                   eval      w_pkko = *blank
     c                   eval      w_pkok = *blank
     c                   eval      w_pkoi = *blank
     c                   endif
      *
      * Legges ikke ut hvis pris er 0
      *
     c                   if        w_suno <> 0
      *
      * oppdaterer statistikk
      *
     c                   eval      sanslu_paar  = sfpaar
     c                   eval      sanslu_binr  = sfbinr
     c                   eval      sanslu_ornr  = sfornr
     c                   eval      sanslu_orsu  = sforsu
     c                   eval      sanslu_orli  = sforli
     c                   eval      sanslu_bong  = sfbong
     c                   eval      sanslu_vare  = sfvare
     c                   eval      sanslu_enhe  = sfenhe
     c                   eval      sanslu_anta  = sfanta
     c                   eval      sanslu_date  = sfdate
     c                   eval      sanslu_time  = sftime
     c     sanslu_key    chain     sanslu
      *
     c                   eval      sabelb = w_suno
9.01 c                   if        sfanta < 0 and
9.01 c                             sabelb > 0 or
9.01 c                             sfanta > 0 and
9.01 c                             sabelb < 0
9.01 c                   eval      sabelb = sabelb * -1
9.01 c                   endif
9.02 c*                  eval      sarefk = w_kild
9.02 c                   eval      sarefk = w_pkon
     c                   time                    saedat
     c                   time                    saetim
     c                   eval      saeusr = l_user
      *
     c                   if        not %found
     c                   eval      safirm = w_firm
     c                   eval      sapaar = sfpaar
     c                   eval      sabinr = sfbinr
     c                   eval      saornr = sfornr
     c                   eval      saorsu = sforsu
     c                   eval      saorli = sforli
     c                   eval      sabong = sfbong
     c                   eval      savare = sfvare
     c                   eval      saenhe = sfenhe
     c                   eval      saanta = sfanta
     c                   eval      sadate = sfdate
     c                   eval      satime = sftime
      *
     c                   time                    saodat
     c                   time                    saotim
     c                   eval      saousr = l_user
     c                   write     sanslur
     c                   else
     c                   update    sanslur
     c                   endif
      *
     c                   endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å beregne rabatt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beregn_rabatt begsr
      *
     c                   eval      w_sumr = 0
      *
      * hente forabp fra sohepf (slå opp).
      *
      * Rabatt
      *
     c                   if        vvkord = 0
     c                   eval(h)   w_raba = sfrabo
     c                   eval      w_sumr = w_raba
     c                   endif
      *
     c                   if        sfrab1 <> 0
     c                   eval      w_sumr = w_sumr + sfrab1
     c                   endif
      *
     c                   if        sfrab2 <> 0
     c                   eval      w_sumr = w_sumr + sfrab2
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c*    *entry        plist
     c*                  parm                    p_paar
      *
      *  Key for les av VARE
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      *  Key for les av ordretype
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for file : KUNDE-REGISTER
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for file : oppdatering av statistikk
      *
     c     sanslu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sanslu_paar
     c                   kfld                    sanslu_binr
     c                   kfld                    sanslu_ornr
     c                   kfld                    sanslu_orsu
     c                   kfld                    sanslu_orli
     c                   kfld                    sanslu_bong
     c                   kfld                    sanslu_vare
     c                   kfld                    sanslu_enhe
     c                   kfld                    sanslu_anta
     c                   kfld                    sanslu_date
     c                   kfld                    sanslu_time
      *
      *  Legge inn firmanummer i nøklene
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
