      /TITLE  ASLAGR Sletting av relasjoner til en lagertellingsbatch
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LC400R
      * Beskrivelse . . : Batch, slette relasjoner (LTEL,LBHE,LBDT)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       191199 ASK  Nytt program
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fltell1    if   e           k disk    rename(ltelpfr:ltell1r)
     fltellu    uf   e           k disk    rename(ltelpfr:ltellur)
     flbhel1    if   e           k disk    rename(lbhepfr:lbhel1r)
     flbhelu    uf   e           k disk    rename(lbhepfr:lbhelur)
     flbdtl1    if   e           k disk    rename(lbdtpfr:lbdtl1r)
     flbdtlu    uf a e           k disk    rename(lbdtpfr:lbdtlur)
      *
      * Definering av variabler i n√∏kler
      *
     d ltell1_batc     s                   like(lebatc)
     d ltellu_batc     s                   like(lebatc)
     d ltellu_numm     s                   like(lenumm)
     d ltellu_suff     s                   like(lesuff)
     d ltellu_line     s                   like(leline)
     d lbhel1_batc     s                   like(lubatc)
     d lbhelu_batc     s                   like(lubatc)
     d lbhelu_numm     s                   like(lunumm)
     d lbdtl1_batc     s                   like(lvbatc)
     d lbdtlu_batc     s                   like(lvbatc)
     d lbdtlu_numm     s                   like(lvnumm)
     d lbdtlu_line     s                   like(lvline)
      *
      * Definering av variable
      *
     d p_firm          s                   like(lefirm)
     d p_batc          s                   like(lebatc)
     d w_firm          s                   like(lefirm)
     d w_batc          s                   like(lebatc)
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  HOVEDPROGRAM
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Sletter rader i TELLING for denne BATCH
      *
     c     ltell1_key    setll     ltell1
     c     ltell1_key    reade     ltell1                                 90
      *
     c                   dow       *in90 = *off
      *
     c                   eval      ltellu_batc = lebatc
     c                   eval      ltellu_numm = lenumm
     c                   eval      ltellu_suff = lesuff
     c                   eval      ltellu_line = leline
     c     ltellu_key    chain     ltellur                            91
     c                   if        *in91 = *off
     c                   delete    ltellur
     c                   endif
      *
     c     ltell1_key    reade     ltell1                                 90
     c                   enddo
      *
      *  Sletter rader i BUNT-HODE for denne BATCH
      *
     c                   eval      *in90 = *off
     c     lbhel1_key    setll     lbhel1
     c     lbhel1_key    reade     lbhel1                                 90
      *
     c                   dow       *in90 = *off
      *
     c                   eval      lbhelu_batc = lubatc
     c                   eval      lbhelu_numm = lunumm
     c     lbhelu_key    chain     lbhelur                            91
     c                   if        *in91 = *off
     c                   delete    lbhelur
     c                   endif
      *
     c     lbhel1_key    reade     lbhel1                                 90
     c                   enddo
      *
      *  Sletter rader i BUNT-LINJE for denne BATCH
      *
     c                   eval      *in90 = *off
     c     lbdtl1_key    setll     lbdtl1
     c     lbdtl1_key    reade     lbdtl1                                 90
      *
     c                   dow       *in90 = *off
      *
     c                   eval      lbdtlu_batc = lvbatc
     c                   eval      lbdtlu_numm = lvnumm
     c                   eval      lbdtlu_line = lvline
     c     lbdtlu_key    chain     lbdtlur                            91
     c                   if        *in91 = *off
     c                   delete    lbdtlur
     c                   endif
      *
     c     lbdtl1_key    reade     lbdtl1                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval       *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les av TELLING
      *
     c     ltell1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_batc
      *
      * Key for oppdatering av TELLING
      *
     c     ltellu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_batc
     c                   kfld                    ltellu_numm
     c                   kfld                    ltellu_suff
     c                   kfld                    ltellu_line
      *
      *  Key for les av BUNT-HODE
      *
     c     lbhel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_batc
      *
      *  Key for oppdatering av BUNT-HODE
      *
     c     lbhelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_batc
     c                   kfld                    lbhelu_numm
      *
      *  Key for les av BUNT-LINJE
      *
     c     lbdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_batc
      *
      *  Key for oppdatering av BUNT-LINJE
      *
     c     lbdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_batc
     c                   kfld                    lbdtlu_numm
     c                   kfld                    lbdtlu_line
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_batc
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_batc = p_batc
      *
     c                   endsr
