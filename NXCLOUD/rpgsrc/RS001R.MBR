      /TITLE  ASOKON: Kontroll av sist brukte kundenummer
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  System. . . . . : ASOKON                                       *
      *  Program . . . . : RS001R                                       *
      *  Beskrivelse . . : Henter sist brukte  kundenummer              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Spesialversjon. :                                              *
      *  Tilpasset for . :                                              *
      *                                                                 *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.*
      *  --------- ------  ---------------------------------------------*
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     fraa4l1    if   e           k disk    rename(raa4pfr:raa4l1r)
     fraa4lu    uf   e           k disk    rename(raa4pfr:raa4lur)
     fraa1l1    if   e           k disk    rename(raa1pfr:raa1l1r)
     frkunlr    if   e           k disk    rename(rkunpfr:rkunlrr)
      *
     d p_retk          s              1
     d p_4knr          s                   like(ra4knr)
     d w_firm          s                   like(ra4fir)
     d w_test          s                   like(p_retk)
     d w_4knr          s                   like(ra4knr)
     d w_kund          s                   like(rkkund)
      *
     d rkunlr_kund     s                   like(rkkund)
     d raa4l1_4aar     s                   like(ra4aar)
     d raa4lu_4aar     s                   like(ra4aar)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Hent sist brukte kundenummer fra koderegister                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c                   move      p_retk        w_test
     c                   eval      p_retk = *blank
      *
     c                   eval      raa4l1_4aar = 0                                           .
     c     raa4l1_key    chain     raa4l1r                            90
      *
      * Nei: Legger inn kode for ikke funnet
      *
     c                   if        *in90 = *on                                           .....
     c                   move      'N'           p_retk                                      .
     c                   goto      xslutt                                                    .
     c                   endif                                                           .....
      *
      * Ja:  Oppdaterer sist brukte nummer
      *
     c                   if        w_test = *blank                                       .....
     c                   eval      w_4knr = ra4knr + 1                                       .
     c                   endif                                                           .....
      *
     c                   if        w_test = 'S'                                          .....
     c                             and p_4knr = ra4knr                                       .
     c                   eval      w_4knr = ra4knr - 1                                       .
     c                   endif                                                           .....
      *
     c                   if        w_test = 'K'                                          .....
     c                   eval      w_4knr = p_4knr                                           .
     c                   endif                                                           .....
      *
      * Sjekk mot grenser
      *
     c                   if        w_4knr <= ra1hkt or                                   .....
     c                             w_4knr >= ra1hrs                                          .
     c                   eval      p_retk = 'N'                                              .
     c                   goto      xslutt                                                    .
     c                   endif                                                           .....
      *
      * Sjekker om sist brukte kundenummer finnes
      * i kunderegisteret - hvis ja, finn neste ledige                 finn neste ledig.
      *
     c                   eval      w_kund = w_4knr
      *
     c     finnes        tag
      *
     c                   eval      rkunlr_kund = w_kund
     c     rkunlr_key    chain     rkunlrr                            90
     c                   if        *in90 = *off                                          .....
     c                   eval      w_kund = w_kund + 1                                       .
     c                   goto      finnes                                                    .
     c                   endif                                                           .....
      *
      * Oppdater sist bruket kundenummer
      *
     c                   eval      raa4lu_4aar = 0                                           .
     c     raa4lu_key    chain     raa4lur                            90
      *
     c                   if        *in90 = *off                                          .....
      *
     c                   if        w_kund > ra4knr                                       .....
     c                   eval      ra4knr = w_kund                                           .
     c                   endif                                                           .....
      *
     c                   if        w_test = 'S'                                          .....
     c                             and p_4knr = ra4knr                                       .
     c                   eval      ra4knr = w_4knr                                           .
     c                   endif                                                           .....
      *
     c                   update    raa4lur                                                   .
     c                   endif                                                           .....
      *
     c                   eval      p_4knr = ra4knr                                           .
     c                   eval      p_retk = ' '
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      *  Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    p_retk
     c                   parm                    w_firm
     c                   parm                    p_4knr
      *
      *
      * Key for statuskode 1
      *
     c     raa1l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for statuskode 4
      *
     c     raa4l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    raa4l1_4aar
      *
      * Key for statuskode 4
      *
     c     raa4lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    raa4lu_4aar
      *
      * Key for kunderegister
      *
     c     rkunlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunlr_kund
      *
      *  Hent opplysninger grenseverdier
      *
     c     raa1l1_key    chain     raa1l1r                            90
      *
     c                   endsr
