     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JV841R
      * Beskrivelse . . : Oppdater fra NIB, produsent
      *
      *                   Oppdaterer/oppretter produsenter
      *                   Norgros/NIB (XML-format)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       151105 GRo  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fnxprpf    uf   e           k disk
     fjprolu    uf a e           k disk    rename(jpropfr:jprolur)
     fjstsl1    if   e           k disk    rename(jstspfr:jstsl1r)
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
      *
      * Definering av Local Data Area
      *
     d                uds
     d l_user                911    920
      *
      *  Dataelementer produsent
      *
     d                 ds
     d d_prec                       100
     d  d_stat                        1    overlay(d_prec:1)
     d   d_stat_num                   1  0 overlay(d_stat:1)
     d  d_prod                        6    overlay(d_prec:2)
     d   d_prod_num                   6  0 overlay(d_prod:1)
     d  d_navn                       35    overlay(d_prec:8)
     d  d_edat                       10    overlay(d_prec:43)
     d  d_etim                       10    overlay(d_prec:53)
      *
      * Definering av variable i nøkler
      *
     d jprolu_prod     s                   like(jqprod)
      *
      * Definering av variable
      *
     d b_inlr          s              1
     d b_kild          s              1    inz(*off)
     d b_prod          s              1    inz(*off)
     d b_pfel          s              1    inz(*off)
     d b_blnk          s              1    inz(*off)
      *
     d w_stda          s              3  0
     d w_slda          s              3  0
     d w_welm          s            400
     d w_wlen          s              3  0
     d w_decp          s              3  0
      *
     d w_str_i         s            400
     d w_str_u         s            400
     d w_posi          s              4  0
     d w_posu          s              4  0
     d w_str_x         s              1
      *
     d w_firm          s              3  0
      *
      *  Konstanter
      *
     d c_apos          c                   x'7D'
     d c_digits        c                   ' 0123456789'
     d c_nsep          c                   ','
      *
     d c_NIBk          c                   '0'
     d c_NVk           c                   '3'
      *
      * XML string konstanter
      *
     d c_xsle          c                   '</'
     d c_xend          c                   '>'
      *
      * XML feltnavn - meldingsspesifikke
      *
      * Hovedstruktur - Avsender
      *
     d c_xstr          c                   '<Avsender>'
     d c_kild          c                   '<Kilde>'
      *
      * Substruktur - Produsent
      *
     d c_prod_s        c                   '<Produsent>'
     d c_prod_e        c                   '</Produsent>'
     d c_prnr          c                   '<Produsentnr>'
     d c_stat          c                   '<Status>'
     d c_navn          c                   '<Navn>'
     d c_enda          c                   '<Endr_dato>'
     d c_entd          c                   '<Endr_tidspunkt>'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      b_inlr = *on
      *
      * Leser XML-fil produsent
      *
     c                   read      nxprpfr
     c                   dow       not %eof(nxprpf)
      *
     c                   if        b_kild = *on
     c                   if        %subst(nxprod:1:11) = c_prod_s
     c                   eval      b_prod = *on
     c                   exsr      beh_prod
     c                   endif
     c                   endif
      *
      *  Finn gyldig avsender/kilde
      *
     c                   eval      w_stda = %scan(c_xend:nxprod:1)
     c                   eval      w_slda = %scan(c_xsle:nxprod:w_stda)
     c                   eval      w_stda = w_stda + 1
      *
     c                   eval      w_wlen = *zero
     c                   eval      w_welm = *blank
     c                   if        w_slda > w_stda
     c                   eval      w_wlen = w_slda - w_stda
     c                   eval      w_welm = %subst(nxprod:w_stda:w_wlen)
     c                   endif
      *
     c                   select
     c                   when      %subst(nxprod:1:7) = c_kild
     c                   exsr      uttr_kild
     c                   endsl
      *
     c                   delete    nxprpfr
      *
     c                   read      nxprpfr
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av avsenderelement : Kilde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_kild     begsr
      *
     c                   eval      b_kild = *off
      *
     c                   if        %trim(w_welm) = c_NIBk
     c                   eval      b_kild = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av produsentinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     beh_prod      begsr
      *
     c                   eval      b_pfel = *off
     c                   exsr      prod_init
      *
     c                   dow       not %eof(nxprpf)
      *
      *  Slutt på produsentelement
      *
     c                   if        %subst(nxprod:1:12) = c_prod_e
     c                   exsr      skr_prod
     c                   leave
     c                   endif
      *
      *  Enkeltelement produsentnivå herfra
      *
     c                   eval      w_stda = %scan(c_xend:nxprod:1)
     c                   eval      w_slda = %scan(c_xsle:nxprod:w_stda)
     c                   eval      w_stda = w_stda + 1
      *
     c                   eval      w_wlen = *zero
     c                   eval      w_welm = *blank
     c                   if        w_slda > w_stda
     c                   eval      w_wlen = w_slda - w_stda
     c                   eval      w_welm = %subst(nxprod:w_stda:w_wlen)
     c                   endif
      *
     c                   select
     c                   when      %subst(nxprod:1:13) = c_prnr
     c                   exsr      uttr_prnr
     c                   when      %subst(nxprod:1:8) = c_stat
     c                   exsr      uttr_stat
     c                   when      %subst(nxprod:1:6) = c_navn
     c                   exsr      uttr_navn
     c                   when      %subst(nxprod:1:11) = c_enda
     c                   exsr      uttr_enda
     c                   when      %subst(nxprod:1:16) = c_entd
     c                   exsr      uttr_entd
     c                   endsl
      *
     c                   delete    nxprpfr
      *
     c                   read      nxprpfr
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av produsentinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     prod_init     begsr
      *
     c                   eval      d_prec = *blank
     c                   eval      d_prod_num = *zero
     c                   eval      d_stat_num = *zero
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av produsentelement : Prod.nr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_prnr     begsr
      *
     c                   if        %check(c_digits : w_welm) = 0
     c                   evalr     d_prod = %trim(w_welm)
     c                   eval      d_prod = %xlate(' ':'0':d_prod)
     c                   else
     c                   eval      b_pfel = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av produsentelement : Status
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_stat     begsr
      *
     c                   eval      d_stat_num = *zero
      *
     c                   if        w_wlen = 1
     c                   if        %check(c_digits : w_welm) = 0
     c                   eval      d_stat = %trim(w_welm)
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av produsentelement : Navn
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_navn     begsr
      *
     c                   eval      w_str_i = *blank
     c                   eval      w_str_i = %trim(w_welm)
     c                   exsr      konv_XML_esc
     c                   eval      d_navn = %trim(w_str_u)
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av delt.element : Endret dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_enda     begsr
      *
     c                   if        w_wlen = 10
     c                   eval      d_edat = %trim(w_welm)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av delt.element : Endringstidspunkt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     uttr_entd     begsr
      *
     c                   if        w_wlen = 8
     c                   eval      d_etim = %trim(w_welm)
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer produsent
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_prod      begsr
      *
     c                   if        b_pfel = *on
     c                   leavesr
     c                   endif
      *
     c                   clear                   jprolur
      *
     c                   eval      jprolu_prod = d_prod_num
     c     jprolu_key    chain     jprolur
      *
     c*                  if        d_stat = '4'
     c*                  if        %found
     c*                  delete    jprolur
     c*                  endif
     c*                  leavesr
     c*                  endif
      *
     c                   eval      jqnavn = d_navn
     c                   eval      jqeusr = l_user
      *
     c                   time                    jqedat
     c                   time                    jqetim
      *
     c                   if        %found
     c                   update    jprolur
     c                   else
     c                   eval      jqprod = d_prod_num
     c                   time                    jqodat
     c                   write     jprolur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tilbakestilling av XML-escape tegn i tekstfelt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     konv_XML_esc  begsr
      *
     c                   eval      w_posi = 0
     c                   eval      w_posu = 0
     c                   eval      w_str_u = *blank
     c                   eval      b_blnk = *off
      *
     c                   dow       w_posi < 395 and
     c                             b_blnk = *off
     c                   eval      w_posi = w_posi + 1
     c                   if        %subst(w_str_i:w_posi) = *blank
     c                   eval      b_blnk = *on
     c                   else
     c                   eval      w_str_x = %subst(w_str_i:w_posi:1)
     c                   if        w_posu < 395
     c                   select
     c                   when      %subst(w_str_i:w_posi:5) = '&amp;'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = '&'
     c                   eval      w_posi = w_posi + 4
      *
     c                   when      %subst(w_str_i:w_posi:4) = '&lt;'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = '<'
     c                   eval      w_posi = w_posi + 3
      *
     c                   when      %subst(w_str_i:w_posi:4) = '&gt;'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = '>'
     c                   eval      w_posi = w_posi + 3
      *
     c                   when      %subst(w_str_i:w_posi:6) = '&apos;'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = c_apos
     c                   eval      w_posi = w_posi + 5
      *
     c                   when      %subst(w_str_i:w_posi:6) = '&quot;'
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = '"'
     c                   eval      w_posi = w_posi + 5
      *
     c                   other
     c                   eval      w_posu = w_posu + 1
     c                   eval      %subst(w_str_u:w_posu:1) = w_str_x
      *
     c                   endsl
     c                   endif
     c                   endif
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
      *
      * Key for oppdatering av produsent
      *
     c     jprolu_key    klist
     c                   kfld                    jprolu_prod
      *
      * Key for oppslag på status
      *
     c     jstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c     jstsl1_key    chain     jstsl1
      *
     c                   endsr
