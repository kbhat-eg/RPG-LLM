      /title  ASOFAK Fjerner slettede varer fra lager-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FX801R
      * Beskrivelse . . : Fjerner slettede varer fra lager-register
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       010699 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvlaglu    uf   e           k disk    rename(vlagpfr:vlaglur)
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vlaglu_vare     s                   like(vlvare)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vvfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser lager-register
      *
     c     vlagl1_key    setll     vlagl1
     c     vlagl1_key    reade     vlagl1                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      vvarl1_vare = vlvare
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *on
      *
     c                   eval      vlaglu_vare = vlvare
     c     vlaglu_key    setll     vlaglu
     c     vlaglu_key    reade     vlaglur                                92
     c                   dow       *in92 = *off
     c                   delete    vlaglur
     c     vlaglu_key    reade     vlaglur                                92
     c                   enddo
      *
     c                   endif
      *
     c     vlagl1_key    reade     vlagl1                                 90
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les på lager
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppdatering av lager
      *
     c     vlaglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglu_vare
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
