     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FX907R
      * Beskrivelse . . : Legger T og D varer inn på lager og legger inn pris 0
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       090914 bkv  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvprlu    uf a e           k disk    rename(vvprpfr:vvprlur)
     fvvprl3    if   e           k disk    rename(vvprpfr:vvprl3r)
     fvvenlr    if   e           k disk    rename(vvenpfr:vvenlrr)
     fvlaglu    uf a e           k disk    rename(vlagpfr:vlaglur)
     fra10l1    if   e           k disk    rename(ra10pfr:ra10l1r)
      *
      * Definering av pamametre
      *
      *
      * Definering av variable i nøkler
      *
     d vvprl3_ldor     s                   like(vpldor)
     d vvprl3_vare     s                   like(vpvare)
     d vvenlr_vare     s                   like(vevare)
     d vvarl7_ogrp     s                   like(vvogrp)
     d vlaglu_vare     s                   like(vlvare)
     d vlaglu_lage     s                   like(vllage)
      *
      * Definering av variable
      *
      *
     d w_firm          s              3  0
     d w_enh1          s              3
     d w_enh2          s              3
     d w_enh3          s              3
     d d_antw          s              5  0
     d d_antu          s              5  0
      *
     d                uds
     d l_firm                944    946  0
     d l_user                911    920
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser alle varer, og behandler D og T
      *
     c     vvarl1_key    setll     vvarl1
     c     vvarl1_key    reade     vvarl1
     c                   dow       not %eof(vvarl1)
      *
     c                   if        vvvtyp = 'D' or
     c                             vvvtyp = 'T'
      * finn enheter
     c                   eval      w_enh1 = ''
     c                   eval      w_enh2 = ''
     c                   eval      w_enh3 = ''
     c                   eval      vvenlr_vare = vvvare
     c     vvenlr_key    setll     vvenlr
     c     vvenlr_key    reade     vvenlr                                 91
     c                   dow       *in91 = *off
     c                   if        w_enh1 = ''
     c                   eval      w_enh1 = veenhe
     c                   elseif    w_enh2 = ''
     c                   eval      w_enh2 = veenhe
     c                   elseif    w_enh3 = ''
     c                   eval      w_enh3 = veenhe
     c                   endif
     c     vvenlr_key    reade     vvenlr                                 91
     c                   enddo
      *
     c                   exsr      oppr_pris
     c                   exsr      oppr_lagr
      *
     c                   endif
      *
     c     vvarl1_key    reade     vvarl1
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av vare på lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppr_lagr     begsr

      * Oppretter vare på lager
      *
      *
     c     ra10l1_key    setll     ra10l1
     c     ra10l1_key    reade     ra10l1
     c                   dow       not %eof(ra10l1)
      *
     c                   clear                   vlaglur
     c                   eval      vlaglu_vare = vvvare
     c                   eval      vlaglu_lage = rajkod
     c     vlaglu_key    chain     vlaglu
     c                   if        not %found(vlaglu)
     c                   eval      vlfirm = w_firm
     c                   eval      vlvare = vvvare
     c                   eval      vllage = rajkod
     c                   eval      vlenhl = vvenhl
     c*                  eval      vltrda = ''
     c                   eval      vlvtyp = 'T'
     c                   time                    vlodat
     c                   time                    vlotim
     c                   time                    vledat
     c                   time                    vletim
     c                   eval      vlousr = l_user
     c                   eval      vleusr = l_user
     c                   write     vlaglur
     c                   endif
      *
     c     ra10l1_key    reade     ra10l1
     c                   enddo

     c                   endsr

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av vare-pris på leveringstype
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppr_pris     begsr
      *
      * sjekker om det finnes pris på denne datoen allerede
      *
     c                   eval      vvprl3_vare = vvvare
     c                   eval      vvprl3_ldor = 0
     c     vvprl3_key    chain     vvprl3
     c                   if        %found(vvprl3)
     c                   leavesr
     c                   endif
      *
     c                   clear                   vvprlur
      *
      * Salgsenheter
      *
     c                   eval      vpfirm = w_firm
     c                   eval      vpvare = vvvare
     c                   eval      vpldor = 0
     c                   eval      vpprgr = ''
     c                   eval      vplety = 0
     c                   eval      vpenhe = w_enh1
     c                   eval      vpsapr = 0
     c                   eval      vpkopr = 0
     c                   eval      vpinpr = 0
     c                   eval      vpinp2 = 0
     c                   eval      vpbupr = 0
     c                   eval      vpeusr = l_user
     c                   eval      vpousr = l_user
      *
     c                   time                    vppdat
     c                   time                    vpedat
     c                   time                    vpetim
     c                   time                    vpodat
     c                   time                    vpotim
      *
      *
     c                   write     vvprlur
      *
     c                   if        w_enh2 <> ''
     c                   eval      vpenhe = w_enh2
     c                   eval      vpsapr = 0
     c                   eval(h)   vpkopr = 0
     c                   eval(h)   vpinpr = 0
     c                   eval(h)   vpinp2 = 0
     c                   write     vvprlur
     c                   endif
      *
     c                   if        w_enh3 <> ''
     c                   eval      vpenhe = w_enh3
     c                   eval      vpsapr = 0
     c                   eval(h)   vpkopr = 0
     c                   eval(h)   vpinpr = 0
     c                   eval(h)   vpinp2 = 0
     c                   write     vvprlur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *
      * Key for oppslag på vare-register
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les av vare-pris pr. leverandør  vare
      *
     c     vvprl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprl3_ldor
     c                   kfld                    vvprl3_vare
      *
      *  Key for les av VARE/ENHET
      *
     c     vvenlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenlr_vare
      *
      * Key for les på lager
      *
     c     ra10l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på lager
      *
     c     vlaglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglu_vare
     c                   kfld                    vlaglu_lage
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = l_firm
      *
      *
     c                   endsr
