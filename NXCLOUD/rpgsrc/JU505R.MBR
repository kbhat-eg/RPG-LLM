     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JU505R
      * Beskrivelse . . : Modul, liste/detaljer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       170905 HKO  Nytt program
os71  * os71  161210 bsa  Lagt inn korreksjoner på SQL pga. OS/400 feil
os71  *                   Det er også lagt inn endring på SQL,
os71  *                   som er nødvendig ved overgang til v6.1
6.3q  *       260515 bof  Datoformat sql
7.xx  * 7.00  010816 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjmodlr    if   e           k disk    rename(jmodpfr:jmodlrr)
     fjmodl1    if   e           k disk    rename(jmodpfr:jmodl1r)
     fjmodl2    if   e           k disk    rename(jmodpfr:jmodl2r)
     fjmodl3    if   e           k disk    rename(jmodpfr:jmodl3r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
      *
     fju505d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d jmodlr_modn     s                   like(jumodn)
     d jmodl1_modn     s                   like(jumodn)
     d jmodl2_mte1     s                   like(jumte1)
     d jmodl3_ldor     s                   like(juldor)
     d jlevl1_ldor     s                   like(jlldor)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_open_sok      s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_ste1          s             35
     d w_ste2          s             35
     d w_ste3          s             35
     d w_ste4          s             35
     d w_ste5          s             35
     d w_ogrp_alf      s              2
     d w_hgrp_alf      s              2
     d w_ugrp_alf      s              3
      *
     d s_stek          s             35
      *
     d w_lnav          s                   like(jlnavn)
      *
      * Definering av konstanter
      *
7.xx d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
6.3q C/End-Exec
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * F9=Gjenta søk
      *
     c                   if        *inki = *on
     c                   eval      *in22 = *on
     c                   eval      b2stek = s_stek
     c                   goto      b2tagb
     c                   endif
      *
      * Søking
      *
     c                   if        b2stek <> *blank
     c                   exsr      sØk
     c                   goto      b2tagb
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2mte1 <> *blank or
     c                             b2modn <> 0 or
     c                             b2ldor <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'S'
     c                   eval      b_open_sok = *on
     c                   when      w_seqe = 'M'
     c                   eval      jmodl1_modn = b1modn
     c     jmodl1_key    setll     jmodl1
     c                   when      w_seqe = 'L'
     c                   eval      jmodl3_ldor = b1ldor
     c     jmodl3_key    setll     jmodl3
     c                   other
     c                   eval      jmodl2_mte1 = b1mte1
     c     jmodl2_key    setll     jmodl2
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for søking i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sØk           begsr
      *
     c                   eval      w_seqe = 'S'
     c                   eval      b_open_sok = *on
      *
     c                   eval      s_stek = b2stek
      *
      * Utleder søketekster ut av søketekst
      *
     c                   call      'AS701R'
     c                   parm                    b2stek
     c                   parm                    w_ste1
     c                   parm                    w_ste2
     c                   parm                    w_ste3
     c                   parm                    w_ste4
     c                   parm                    w_ste5
      *
      * Blanker subfile, og fyller opp i.h.t. søk
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker søkefelt
      *
     c                   eval      b2stek = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på modultekst-1
      *
     c                   if        b2mte1 <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      jmodl2_mte1 = b2mte1
     c     jmodl2_key    setll     jmodl2
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på modul
      *
     c                   if        b2modn <> 0
     c                   eval      w_seqe = 'M'
     c                   eval      jmodl1_modn = b2modn
     c     jmodl1_key    setll     jmodl1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på leverandør
      *
     c                   if        b2ldor <> 0
     c                   eval      w_seqe = 'L'
     c                   eval      jmodl3_ldor = b2ldor
     c     jmodl3_key    setll     jmodl3
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2mte1 = *blank
     c                   eval      b2modn = 0
     c                   eval      b2ldor = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      w_virn = w_srrn
      *
      * Detaljer
      *
     c                   if        b1valg = 5
     c                   call      'JU501R'
     c                   parm                    w_firm
     c                   parm                    b1modn
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Varer
      *
     c                   if        b1valg = 6
     c                   call      'JV530R'
     c                   parm                    w_firm
     c                   parm                    b1ldor
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    b1ugrp
     c                   parm                    b1modn
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Stikkord
      *
     c                   if        b1valg = 7
     c                   call      'JU520R'
     c                   parm                    b1modn
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Fritekst
      *
     c                   if        b1valg = 8
     c                   call      'JU511R'
     c                   parm                    w_firm
     c                   parm                    b1modn
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Standarder
      *
     c                   if        b1valg = 9
     c                   call      'JU530R'
     c                   parm                    b1modn
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
      * Initierer SQL for søk
      *
     c                   if        w_seqe = 'S' and
     c                             b_open_sok = *on
     c/exec sql
     c+                  declare sok cursor for
os71 c+                  select m.jumte1,
os71 c+                         m.jumodn,
os71 c+                         m.juldor,
os71 c+                         m.juogrp,
os71 c+                         m.juhgrp,
os71 c+                         m.juugrp
os71 c+                  from   jmodpf as m inner join
os71 c+                         jlevpf as l on
os71 c+                         m.juldor = l.jlldor
os71 c+                  where  char(m.jumodn) concat(m.jumte1) concat(m.jumte2)
os71 c+                         concat(m.jumerk) concat(upper(l.jlnavn))
os71 c+                         like :w_ste1 and
os71 c+                         char(m.jumodn) concat(m.jumte1) concat(m.jumte2)
os71 c+                         concat(m.jumerk) concat(upper(l.jlnavn))
os71 c+                         like :w_ste2 and
os71 c+                         char(m.jumodn) concat(m.jumte1) concat(m.jumte2)
os71 c+                         concat(m.jumerk) concat(upper(l.jlnavn))
os71 c+                         like :w_ste3 and
os71 c+                         char(m.jumodn) concat(m.jumte1) concat(m.jumte2)
os71 c+                         concat(m.jumerk) concat(upper(l.jlnavn))
os71 c+                         like :w_ste4 and
os71 c+                         char(m.jumodn) concat(m.jumte1) concat(m.jumte2)
os71 c+                         concat(m.jumerk) concat(upper(l.jlnavn))
os71 c+                         like :w_ste5
os71 c+                  order by m.jumte1, m.jumodn
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok
     c/end-exec
     c/exec sql
     c+                  open sok
     c/end-exec
     c                   endif
      *
      * Fyller opp subfile
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'S'
     c/exec sql
     c+                  fetch next
     c+                  from  sok
     c+                  into  :jumte1,
     c+                        :jumodn,
     c+                        :juldor,
     c+                        :juogrp,
     c+                        :juhgrp,
     c+                        :juugrp
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'M'
     c                   read      jmodl1                                 15
     c                   when      w_seqe = 'L'
     c                   read      jmodl3                                 15
     c                   other
     c                   read      jmodl2                                 15
     c                   endsl
      *
     c                   if        *in15
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = *blank
     c                   eval      jmodl2_mte1 = jumte1
     c                   when      w_seqe = 'M'
     c                   eval      jmodl1_modn = jumodn
     c                   when      w_seqe = 'L'
     c                   eval      jmodl3_ldor = juldor
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1mte1 = jumte1
     c                   eval      b1modn = jumodn
     c                   eval      b1ldor = juldor
     c                   eval      b1ogrp = juogrp
     c                   eval      b1hgrp = juhgrp
     c                   eval      b1ugrp = juugrp
     c                   eval      b1lnav = *blank
     c                   eval      b1vgrp = *blank
      *
     c                   if        juldor <> 0
     c                   eval      jlevl1_ldor = juldor
     c     jlevl1_key    chain     jlevl1
     c                   if        %found
     c                   eval      b1lnav = jlnavn
     c                   endif
     c                   endif
      *
     c                   move      juogrp        w_ogrp_alf
     c                   move      juhgrp        w_hgrp_alf
     c                   move      juugrp        w_ugrp_alf
     c                   eval      b1vgrp = w_ogrp_alf + w_hgrp_alf +
     c                                      w_ugrp_alf
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   eval      b_open_sok = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        w_seqe = 'S'
     c                   exsr      forny
     c                   leavesr
     c                   endif
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'M'
     c     jmodl1_key    setgt     jmodl1
     c                   readp     jmodl1
     c                   when      w_seqe = 'L'
     c     jmodl3_key    setgt     jmodl3
     c                   readp     jmodl3
     c                   other
     c     jmodl2_key    setgt     jmodl2
     c                   readp     jmodl2
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'M'
     c                   readp     jmodl1
     c                   when      w_seqe = 'L'
     c                   readp     jmodl3
     c                   other
     c                   readp     jmodl2
     c                   endsl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'M'
     c                   eval      jmodl1_modn = jumodn
     c                   when      w_seqe = 'L'
     c                   eval      jmodl3_ldor = juldor
     c                   other
     c                   eval      jmodl2_mte1 = jumte1
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'M'
     c     jmodl1_key    setll     jmodl1
     c                   when      w_seqe = 'L'
     c     jmodl3_key    setll     jmodl3
     c                   other
     c     jmodl2_key    setll     jmodl2
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Leverandør
      *
     c                   if        w_afld = 'B2LDOR'
     c                   call      'JL500R'
     c                   parm                    b2ldor
     c                   parm                    w_lnav
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les på modul
      *
     c     jmodlr_key    klist
     c                   kfld                    jmodlr_modn
      *
      * Key for posisjonering på modul
      *
     c     jmodl1_key    klist
     c                   kfld                    jmodl1_modn
      *
      * Key for posisjonering på modultekst-1
      *
     c     jmodl2_key    klist
     c                   kfld                    jmodl2_mte1
      *
      * Key for posisjonering på leverandør
      *
     c     jmodl3_key    klist
     c                   kfld                    jmodl3_ldor
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      jmodl2_mte1 = *blank
     c     jmodl2_key    setll     jmodl2
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
