     H/TITLE  ASSTAT: Vedlikehold av referansekunde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSTAT
      * Program . . . . : SK100R
      * Beskrivelse . . : Vedlikehold av referansekunde priser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       220219 bsa  Nytt program
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fsrepl1    if   e           k disk    rename(sreppfr:srepl1r)
     fsreplu    uf a e           k disk    rename(sreppfr:sreplur)
      *
     fsk100D    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                UDS
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable
      *
     d w_firm          s                   like(srfirm)
      *
      * Definering av variable i nøkler
      *
     d rkunl1_kund     s                   like(rkkund)
      *
      * Parametre
      *
     d p_stat          s              1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * A1BLD Behandle bildet for valg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     a1taga        tag
      *
      * Hent opplysninger fra referanseregister
      *
     c     srepl1_key    chain     srepl1
     c                   if        %found
     c                   eval      a1kund = srkund
     c                   eval      a1ktxt = *blanks
     c                   eval      a1kdat = srkdat
     c                   eval      a1ktid = srktid
     c                   eval      rkunl1_kund = srkund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   eval      a1ktxt = rknavn
     c                   endif
     c                   else
     c                   eval      a1kund = 0
     c                   eval      a1ktxt = *blanks
     c                   eval      a1kdat = *loval
     c                   eval      a1ktid = *loval
     c                   endif
      *
      * Vis bilde
      *
     c     a1tag1        tag
      *
     c                   exfmt     a1bld
      *
      * F3=Avslutt
      *
     c                   if        *inkc = *on or
     c                             *inkl = *on
     c                   goto      xslutt
     c                   endif
      *
      * F4=Spørring
      *
     c                   if        *inkd  = *on and
     c                             w_afld = 'A1KUND'
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    a1kund
     c                   parm                    a1ktxt
     c                   goto      a1tag1
     c                   endif
      *
      * Oppdaterer preferanseregisteret
      *
     c     sreplu_key    chain     sreplur
      *
     c                   eval      srkund = a1kund
     c                   eval      srkdat = a1kdat
     c                   eval      srktid = a1ktid
      *
     c                   if        %found
     c                   time                    sredat
     c                   time                    sretim
     c                   eval      sreusr = l_user
     c                   update    sreplur
     c                   else
     c                   eval      srfirm = w_firm
     c                   time                    srodat
     c                   time                    srotim
     c                   eval      srousr = l_user
     c                   time                    sredat
     c                   time                    sretim
     c                   eval      sreusr = l_user
     c                   write     sreplur
     c                   endif
      *
      * F10=Kjør oppdatering direkte
      *
     c                   if        *inkj  = *on
     c                   call      'SK101R'
     c**                 goto      a1tag1
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på referanseregister
      *
     c     srepl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på referanseregister
      *
     c     sreplu_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på kunde-registeret
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Legger inn firmanummer i nøkklene
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
