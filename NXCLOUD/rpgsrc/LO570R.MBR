     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LO570R
      * Beskrivelse . . : Søking på bestilling via leverandør
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       080905 GRo  Nytt program
6.nu  *       250514 bof  Utvidet mtp. nummerutvidelse
7.xx  * 7.xx  180516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.xx  270819 bsa  Takler bredere skjermbilder
7.02  * 7.00  310119 bkv  Vasker vekk probl Newlook tegn
7.03  *       310817 bsa  Legger lager fra EDI melding som default verdi
7.03  *                   i filter.
7.04  * 7.00  220416 ask  Filtrer bestillinger ihht. filter i LI100r
      *
8.01  * 8.00  130821 bsa  Avstemmer bruken av lager
8.02  * 8.00  061021 bsa  Avstemmer bruken av lager 2
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flohelk    if   e           k disk    rename(lohepfr:lohelkr)
     flodtlv    if   e           k disk    rename(lodtpfr:lodtlvr)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
      *
     flo570d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(lofirm)
     d p_ldor          s                   like(loldor)
     d p_numm          s                   like(lonumm)
     d p_suff          s                   like(losuff)
7.03 d p_lage          s                   like(lolage)
7.04 d p_lag1          s                   like(lolage)
7.04 d p_lag2          s                   like(lolage)
7.04 d p_lag3          s                   like(lolage)
7.04 d p_lag4          s                   like(lolage)
7.04 d p_lag5          s                   like(lolage)
      *
      * Definering av variabler i nøkler
      *
     d lohelk_ldor     s                   like(loldor)
     d lodtlv_vare     s                   like(ldvare)
     d lodtlv_numm     s                   like(ldnumm)
     d vvarl1_vare     s                   like(vvvare)
     d rlevl1_levr     s                   like(rllevr)

      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_eof           s              1    inz(*off)
     d b_valg_ok       s              1    inz(*off)
     d b_disp          s              1    inz(*off)
     d b_vare          s              1    inz(*off)
     d w_bdat          s                   like(lobdat)
     d w_best          s             20
7.02 d w_len           s              3  0
7.02 d w_teller        s              3  0
      *
     d w_firm          s                   like(lofirm)
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(10)
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Vis den side som inneholder denne record.
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * H1WIN  - H1 Vindu for angivelse av søkefilter
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inka
     c                   exsr      xh1win
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   if        b_valg_ok = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   if        b_valg_ok = *off
     c                   eval      p_numm = *zero
     c                   eval      p_suff = *zero
     c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av filter-bildet (H1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xh1win        begsr
      *
     c                   if        h1vare = *blank
     c                   eval      h1vtxt = *blank
     c                   endif
     c                   if        h1innk = *zero
     c                   eval      h1inav = *blank
     c                   endif
      *
     c     h1taga        tag
     c                   exfmt     h1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_h1win
     c                   endif
      *
     c                   if        *inkd
     c                   if        w_afld = 'H1OTYP'
     c                   call      'VA550R'
     c                   parm                    w_firm
     c                   parm                    h1otyp
     c                   endif
      *
     c                   if        w_afld = 'H1VARE'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    h1vare
     c                   parm                    h1vtxt
     c                   endif
      *
     c                   if        w_afld = 'H1INNK'
     c                   call      'RA509R'
     c                   parm                    w_firm
     c                   parm                    h1innk
     c                   parm                    h1inav
     c                   endif
      *
     c                   goto      h1taga
     c                   endif
      *
     c                   if        h1bdat <> 0
     c     *dmy          test(d)                 h1bdat                 31
     c                   if        *in31  = *on
     c                   goto      h1taga
     c                   endif
     c                   endif
      *
     c                   if        h1best <> *blank
     c                   eval      h1best = %trim(%xlate(lo:up:h1best))
     c                   endif
      *
     c                   if        h1vare <> *blank and
     c                             h1vtxt = *blank
     c                   eval      vvarl1_vare = h1vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      h1vtxt = vvtek1
     c                   endif
     c                   endif
      *
8.02 c                   eval      p_lag1 = h1lage
      *
     c     end_h1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   eval      lohelk_ldor = p_ldor
     c     lohelk_key    setll     lohelk
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Velg
      *
     c                   if        b1valg = 1
     c                   eval      p_numm = b1numm
     c                   eval      p_suff = b1suff
     c                   eval      b_valg_ok = *on
     c                   leave
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      p_numm = b1numm
     c                   eval      p_suff = b1suff
     c                   call      'LO505R'
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   eval      b1valg = *zero
     c                   leave
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c     lohelk_key    reade     lohelk
      *
     c                   if        %eof
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   exsr      sjk_filter
     c                   if        b_disp = *off
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
      * Finner moms-sats
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    lobdat
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
      *
     c                   eval      b1valg = 0
     c                   eval      b1numm = lonumm
     c                   eval      b1suff = losuff
     c     *dmy          move      lobdat        b1dato
     c                   movel     lonavn        b1navn
     c                   eval      b1otyp = lootyp
     c                   eval      b1ornr = loornr
     c                   eval      b1toti = lototi - lototr
     c                   eval      b1toti = (b1toti * w_mvat)
      *
7.02 c                   exsr      newlo_vask
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kontroll mot filterverdier
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_filter    begsr
      *
     c                   eval      b_disp = *on
      *
      * Bestillingsdato
      *
     c                   if        h1bdat <> *zero
     c     *dmy          move      h1bdat        w_bdat
     c                   if        lobdat <> w_bdat
     c                   eval      b_disp = *off
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Bestillingsnr.
      *
     c                   if        h1best <> *blank
     c                   eval      w_best = *blank
     c                   movel     lonumm        w_best
      *
     c                   if        %scan(%trim(h1best) : w_best) = 0
     c                   eval      b_disp = *off
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Ordretype
      *
     c                   if        h1otyp <> *blank
     c                   if        lootyp <> h1otyp
     c                   eval      b_disp = *off
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Fra/til best.total
      *
     c                   if        h1fbel <> *zero and
     c                             h1tbel = *zero
     c                   if        lototi < h1fbel
     c                   eval      b_disp = *off
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        h1fbel = *zero and
     c                             h1tbel <> *zero
     c                   if        lototi > h1tbel
     c                   eval      b_disp = *off
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        h1fbel <> *zero and
     c                             h1tbel <> *zero
     c                   if        lototi < h1fbel or
     c                             lototi > h1tbel
     c                   eval      b_disp = *off
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Selger/innkjøper
      *
     c                   if        h1innk <> *zero
     c                   if        loinnk <> h1innk
     c                   eval      b_disp = *off
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Varenummer i bestilling
      *
     c                   if        h1vare <> *blank
     c                   exsr      sjk_vare
     c                   if        b_vare = *off
     c                   eval      b_disp = *off
     c                   leavesr
     c                   endif
     c                   endif
7.04  *
7.04  * Lagernummer fra filter
7.04  *
7.04 c                   if        p_lag1 = 0 and
7.04 c                             p_lag2 = 0 and
7.04 c                             p_lag3 = 0 and
7.04 c                             p_lag4 = 0 and
7.04 c                             p_lag5 = 0
7.04 c                   leavesr
7.04 c                   end
7.04  *
7.04 c                   if        p_lag1 <> 0 and
7.04 c                             p_lag1 = lolage
7.04 c                   leavesr
7.04 c                   endif
7.04 c                   if        p_lag2 <> 0 and
7.04 c                             p_lag2 = lolage
7.04 c                   leavesr
7.04 c                   endif
7.04 c                   if        p_lag3 <> 0 and
7.04 c                             p_lag3 = lolage
7.04 c                   leavesr
7.04 c                   endif
7.04 c                   if        p_lag4 <> 0 and
7.04 c                             p_lag4 = lolage
7.04 c                   leavesr
7.04 c                   endif
7.04 c                   if        p_lag5 <> 0 and
7.04 c                             p_lag5 = lolage
7.04 c                   leavesr
7.04 c                   endif
7.04  *
7.04 c                   eval      b_disp = *off
      *
      * Selger/innkjøper
      *
     c                   if        h1innk <> *zero
     c                   if        loinnk <> h1innk
     c                   eval      b_disp = *off
     c                   leavesr
     c                   endif
     c                   endif
7.03  *
7.03  * Lager
7.03  *
7.03 c                   if        h1lage <> 0   and
7.03 c                             h1lage <> lolage
7.03 c                   eval      b_disp = *off
7.03 c                   leavesr
7.03 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kontroll om varenr. finnes i best.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_vare      begsr
      *
     c                   eval      b_vare = *on
     c                   eval      lodtlv_vare = h1vare
     c                   eval      lodtlv_numm = lonumm
      *
     c     lodtlv_key    setll     lodtlv
     c     lodtlv_key    reade     lodtlv
     c                   if        %eof(lodtlv)
     c                   eval      b_vare = *off
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  * Newlook vask av data
7.02  *  Fjerner tegn som gjør at Newlook feiltolker
7.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.02  *
7.02   begsr newlo_vask;
7.02     for w_teller = 1 to 5 ;
7.02       w_len = %len(%trim(b1navn));
7.02       if (w_len > 0);
7.02       b1navn = %ScanRpl('.':'':b1navn:w_len); // newlook tåler ikke . på slutten
7.02       endif;
7.02       w_len = %len(%trim(b1navn));
7.02       if (w_len > 0);
7.02       b1navn = %ScanRpl(':':'':b1navn:w_len); // newlook tåler ikke : på slutten
7.02       endif;
7.02     endfor ;
7.02   endsr;
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
     c                   parm                    p_numm
     c                   parm                    p_suff
8.01 c**                 parm                    p_lage
7.04 c                   parm                    p_lag1
7.04 c                   parm                    p_lag2
7.04 c                   parm                    p_lag3
7.04 c                   parm                    p_lag4
7.04 c                   parm                    p_lag5
      *
      * Key for posisjonering leverandør
      *
     c     lohelk_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelk_ldor
      *
      *  Key for sjekk av bestillingslinje på varenr.
      *
     c     lodtlv_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlv_vare
     c                   kfld                    lodtlv_numm
      *
      * Key for oppslag på vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Henter parametre, og initierer skjermbildet
      *
     c                   eval      w_firm = p_firm
8.01 c**                 eval      h1lage = p_lage
8.01 c                   eval      h1lage = p_lag1
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      lohelk_ldor = p_ldor
     c     lohelk_key    setll     lohelk
      *
     c                   exsr      crt_subfile
      *
     c                   eval      b2ldor = p_ldor
     c                   eval      rlevl1_levr = p_ldor
     c     rlevl1_key    chain     rlevl1
     c                   if        %found
     c                   eval      b2lnav = rlnavn
     c                   endif
      *
     c                   endsr
