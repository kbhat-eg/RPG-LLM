      /TITLE  ASSTAT Utvalg til parameterfil, fra/til periode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASSTAT
      * Program . . . . : SR141R
      * Beskrivelse . . : Utvalg til parameterfil, fra/til periode
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fSR141d    cf   e             workstn
      *
     fslpml1    if   e           k disk    rename(slpmpfr:slpml1r)
     fslpmlu    uf a e           k disk    rename(slpmpfr:slpmlur)
      *
      *---------------------------------------------------------------------
      *
     d wptype          s                   like(srtype)
     d wptext          s             30
      *
     d wwfirm          s                   like(srfirm)
     d wwtype          s                   like(srtype)
     d wwkode          s                   like(srkode)
      *
     dwwrest           ds           330
     d wwtext                  1     30
     d wwfper                 31     36  0
     d wwtper                 37     42  0
      *
      *
      *
     d wwvalg          ds
     d  wwfyll                 1     15
      *
     d                uds
     d dsuser                911    920
     d dsfirm                944    946  0
     d l_fnav                951    980
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * B2BLD Behandle åpningsbildet                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     b2tagb        tag
      *
      * slå opp
      * i filen
      *
     c     key01         chain     slpml1                             60
      *
      * post finnes
      * fra før
      *
     c                   if        *in60 = *off                                          .....
     c                   movel     srrest        wwrest                                      .
     c                   movel     wwfper        b2fper                                      .
     c                   movel     wwtper        b2tper                                      .
      *                                                                                      .
      *                                                                                      .
      *                                                                                      .
     c                   endif                                                           .....
      *
      * post finnes
      * ikke
      *
     c                   if        *in60 = *on                                           .....
     c                   eval      b2fper = *zero                                            .
     c                   eval      b2tper = *zero                                            .
      *
      *
     c                   endif                                                           .....
      *
     c     b2taga        tag
      *
     c                   exfmt     b2bld
      *
      * Avslutt program
      *
     c     *inkc         cabeq     *on           xslutt
     c     *inkl         cabeq     *on           xslutt
      *
      * Sjekk om fra er større enn til
      *
     c                   if        b2fper > b2tper
     c                   goto      b2taga
     c                   endif
      *
     c                   if        b2fper <> 0
     c                   if        b2fper < 195000
     c                   goto      b2taga
     c                   endif
     c                   endif
      *
     c     key01         chain     slpmlu                             60
      *
     c                   eval      wwfper = b2fper
     c                   eval      wwtper = b2tper
     c                   eval      wwtext = 'Periode fra/til'
     c                   eval      srrest = wwrest
     c                   eval      srsyst = 'L'
      *
      * posten finnes
      * fra før
      *
     c                   if        *in60 = *off
     c                   if        b2fper = 0 and b2tper = 0                             .....
     c                   delete    slpmlur                                                   .
     c                   else                                                                .
     c                   update    slpmlur                                                   .
     c                   endif
     c                   endif
      *
      * posten finnes
      * ikke fra før
      *
     c                   if        *in60 = *on                                           .....
     c                   eval      srfirm = wwfirm
     c                   eval      srtype = wwtype
     c                   eval      srkode = wwkode
     c                   eval      srvalg = wwvalg
     c                   write     slpmlur                                                   .
     c                   endif
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *inzsr        begsr
      *
      * Key for file
      *
     c     key01         klist
     c                   kfld                    wwfirm
     c                   kfld                    wwtype
     c                   kfld                    wwkode
     c                   kfld                    wwvalg
      *
      * Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    wptype
     c                   parm                    wptext
      *
      * Legger inn verdier i feltene
      *
     c                   eval      wwfirm = dsfirm
     c                   eval      wwtype = wptype
     c                   eval      wwkode = 41
     c                   eval      wwvalg = *blank
      *
     c                   eval      srfirm = wwfirm
      *
     c                   eval      b2type = wptype
     c                   eval      b2text = wptext
      *
     c                   endsr
