      /TITLE  Tilrettelegger trans fra scanning
     h decedit(',') datedit(*dmy.)
     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * System. . . . . : ASOKON
      * Program . . . . : RFP15R
      * Beskrivelse . . : Tilrettelegger trans fra scanning Nexstep Arkiv
      * Div. Dokumentasjon:
      *      Bilagskode, Settes i properties-file på server
      *                  (Kan bli borte ved innl. av ny versjon)(Espen/Arild)
      *                  RFP15R transporterer denne ukritisk videre.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * V5.41 161005 KOB  Nytt program basert på RFA15R
      * V5.50 240607 KOB  Lagt ut reksontronummer og lev.fakturanr
      * V5.50 240607 KOB  Leser logisk fil i perioderekkefølge
      * V5.50 227087 KOB  Endret logikk knyttet til beløp/val.beløp
1.00  * V5.50 100210 Sst  Ev. flytte postering til 1. periode ikke sperret.
6.10  * 6.10  121010 NHO  Leser kun for inneværende firma
6.11  *       011110 Sst  Rettet logikk vedr firmanr/periode i bunt
6.12  *       270611 sst  Konv. av bilagskoder i.h.h.t compregler
6.13  *       130711 sst  Erstattet REMS med RCMP + filgr. i record
6.14  *       071211 sst  Ikke feilmelding dersom kjøring fra klienten
6.14  *       130212 sst  Nullstille trapfr for hver ny transaksjon
6.15  *       050312 sst  Ny logikk for test om innlest før
6.16  *       250512 sst  Hente Prosj, UnderP og Aktivitet fra ArbOrdre
6.17  *       151012 sst  Ta med tekst i sjekk om innlest før
6.30  *       200117 sst  Legge ut transer til NAV
6.31  *       130217 sst  Endret iinkst til useropn
6.32  *       230517 sst  Endret iinkst til egen fil Rnavpf
6.33  *       140617 sst  Endret rnavpf til icomst(blir nok bra en gang
6.34  *       150617 sst  Ny logikk vedr hente levnr alle tr. til NAV
6.35  *       190617 sst  Satt bokf.dato fra Periode fra compello igjen
6.36  *       240817 sst  Satt bokf.dato til 01. i perioden  TAB/NAV
7.xx  * K000  220620 bsa  Takler brede skjermbilder
7.00  *       161020 sst  Ikke legge ut INKST når regns.syst = 5 og
7.00  *                   NX 'IKKE_INKST_VED_REGN_SYST=5' er satt
8.00  *       080322 sst  Tilpasset ny versjon av RV015R
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.13 frcmpl1    uf   e           k disk    rename(rcmppfr:rcmpl1r)
     f                                     prefix(x:1)
6.15 frcmtl1    if   e           k disk    rename(rcmtpfr:rcmtl1r)
6.15 f                                     prefix(B:1)
6.15 frcmtpf    uf a e           k disk    rename(rcmtpfr:rcmtpfr)
6.15 f                                     prefix(x:1)
6.15 frcmdpf    uf a e           k disk    rename(rcmdpfr:rcmdpfr)
6.15 f                                     prefix(x:1)
6.34 frcmrl1    if   e           k disk    rename(rcmrpfr:rcmrl1r)
6.15 f                                     prefix(rr:2)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fraa3pf    if   e           k disk
1.0  fraa1pf    if   e           k disk
6.16 frp4ol1    if   e           k disk    rename(rp4opfr:rp4ol1r)
     frbunlu    uf a e           k disk    rename(rbunpfr:rbunlur)
     frfp16d    cf   e             workstn
     frtrapf    o    e           k disk
6.33 ficomst    o    e           k disk    rename(icomst:icomstr)
6.31 f                                     usropn
      *
     d                uds
1.00 d l_perfradat           100    100
1.00 d l_perettspr           101    101
1.00 d l_v_f_per             110    110
6.12 d l_blk                 111    126
     d l_user                911    920
6.13 d l_fgrp                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av array's
      *
     d per             s              2  0 dim(13)
      *
      * Definering av variable i nøkler
      *
     d rbunlu_bunt     s                   like(rsbunt)
      *
      * Definering av variable
      *
610  d rbunlu_nivo     s                   like(rsnivo)
610  d rbunlu_memb     s                   like(rsmemb)
6.15 d rcmtl1_fgrp     s                   like(xtfgrp)
6.15 d rcmtl1_firm     s                   like(xtfirm)
6.15 d rcmtl1_bdat     s                   like(xtbdat)
6.15 d rcmtl1_belo     s                   like(xtbelo)
6.15 d rcmtl1_raar     s                   like(xtraar)
6.15 d rcmtl1_rper     s                   like(xtrper)
6.15 d rcmtl1_biln     s                   like(xtbiln)
6.15 d rcmtl1_davd     s                   like(xtdavd)
6.15 d rcmtl1_dkto     s                   like(xtdkto)
6.15 d rcmtl1_dsps     s                   like(xtdsps)
6.15 d rcmtl1_dmva     s                   like(xtdmva)
6.15 d rcmtl1_cavd     s                   like(xtcavd)
6.15 d rcmtl1_ckto     s                   like(xtckto)
6.15 d rcmtl1_csps     s                   like(xtcsps)
6.15 d rcmtl1_cmva     s                   like(xtcmva)
6.15 d rcmtl1_bilk     s                   like(xtbilk)
6.15 d rcmtl1_lfak     s                   like(xtlfak)
6.16 d rcmtl1_pros     s                   like(xtpros)
6.17 d rcmtl1_text     s                   like(xttext)
6.34 d rcmrl1_firm     s                   like(rrfirm)
6.34 d rcmrl1_biln     s                   like(rrbiln)
6.16 d rp4ol1_firm     s                   like(rp4frm)
6.16 d rp4ol1_arbo     s                   like(rp4ord)
610  d w_memb          s                   like(rsmemb)
6.13 d w_fgrp          s                   like(xtfgrp)
     d w_firm          s                   like(rtfirm)
     d w_bunt          s                   like(rtbunt)
     d w_xper          s              8  0
     d w_linj          s              8  0
     d w_arec          s              9  0
     d w_Årm1          s              2  0
     d wpe             s              2  0
     d w_rper          s                   like(rsrper)
     d w_raar          s                   like(rsraar)
1.00 d w_sp_aar        s                   like(rsraar)
1.00 d w_sp_mnd        s                   like(rsrper)
1.00 d w_sp_per        s              6  0
1.00 d w_p_f_comp      s              1
1.00 d w_kv_bilnr      s                   like(xtbiln)
1.00 d w_m1            s             50    inz('Bilagsnr 123456 er postert på -
1.00 d                                     en avsluttet periode')
1.00 d w_m2            s             50    inz('Skal periode 2010-01 posteres -
1.00 d                                     på?                 ')
1.00 d w_sv            s              1
1.00 d w_in12          s              1  0
1.00 d w_bnr_x         s              6
1.00 d w_aar_x         s              4
1.00 d w_per_x         s              2
1.00 d w_ddx           s              2
1.00 d w_mmx           s              2
1.00 d w_aax           s              2
1.00 d w_perx          s              6
1.00 d w_pern          s              6  0
      *
     d w_bsum          s                   like(rsbsum)
     d w_psum          s                   like(rspsum)
     d w_pdeb          s                   like(rspdeb)
     d w_pkre          s                   like(rspkre)
     d w_belp          s                   like(rtbelØ)
      *
     d w_peri          s              6  0
1.00 d w_amd           s              6  0
1.00 d w_20            s              2  0
1.00 d w_40            s              4  0
1.00 d w_rtra          s              1
      *
6.12 d w_bilk          s              2
6.14 d w_jkey          s             41
6.14 d w_jsta          s              2  0
6.14 d w_jmld          s            200
6.14 d w_stat          s              1  0
6.14 d wc_lev          s                   like(rtresn)
6.14 d w_ktox          s              8
6.14 d wcdkto          s              8  0
6.14 d wcckto          s              8  0
      *
8.00 d w_valg          s              2  0 inz(0)
6.30 d w_nxdr          s              1  0 inz(0)
6.30 d w_dato          s              6  0
6.30 d dat             s             10
     d w_isodat        s               D   datfmt(*iso)
     d w_isodd         s               D   datfmt(*iso)
      *
      * Definering av save-variable
      *
6.13 d s_fgrp          s                   like(xtfgrp)
     d s_firm          s                   like(rtfirm)
     d s_peri          s              6  0
7.00  *
7.00 d u_inkst         s              1    inz(*off)
7.00  *
7.00  * Definering av eksterne prg
7.00  *
7.00   dcl-s co402_verdi1  char(1);
7.00   dcl-s co402_verdi2  char(2048);
7.00   DCL-PR CO402R EXTPGM('CO402R');
7.00     p_filgrp            char(3)     const;
7.00     p_firm              packed(3:0) const;
7.00     p_lager             packed(2:0) const;
7.00     p_nokkel            char(50)    const;
7.00     p_verdi1            char(1);
7.00     p_verdi2            char(2048);
7.00   END-PR;
      *
      *  Statusopplysninger - avvikende perioder
      *
     iraa3pfr
     i              ra3p01                      per(01)
     i              ra3p02                      per(02)
     i              ra3p03                      per(03)
     i              ra3p04                      per(04)
     i              ra3p05                      per(05)
     i              ra3p06                      per(06)
     i              ra3p07                      per(07)
     i              ra3p08                      per(08)
     i              ra3p09                      per(09)
     i              ra3p10                      per(10)
     i              ra3p11                      per(11)
     i              ra3p12                      per(12)
     i              ra3p13                      per(13)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedrutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      * Les transaksjonsfile til e.o.f.
      *
6.13 c                   read      rcmpl1                                 90
      *
     c                   dow       *in90 = *off
6.10  *
6.10 c                   if        xtfirm <> l_firm or
6.10 c                             xtfgrp <> l_fgrp
6.10 c                   goto      nyles
6.10 c                   endif
6.16  *
6.16  *   Ny løsn Didrik 06.03.2012.  Ikke beh poster som ikke er merket
6.16  *
6.16 c                   if        xtautx <> 'X'
6.16 c                   goto      nyles
6.16 c                   endif
6.10  *
6.15  *  Sjekk om trans er innlest før
6.15  *
6.15 c                   eval      rcmtl1_FGRP = xTFGRP
6.15 c                   eval      rcmtl1_FIRM = xTFIRM
6.15 c                   eval      rcmtl1_BDAT = xTBDAT
6.15 c                   eval      rcmtl1_BELO = xTBELO
6.15 c                   eval      rcmtl1_RAAR = xTRAAR
6.15 c                   eval      rcmtl1_RPER = xTRPER
6.15 c                   eval      rcmtl1_BILN = xTBILN
6.15 c                   eval      rcmtl1_DAVD = xTDAVD
6.15 c                   eval      rcmtl1_DKTO = xTDKTO
6.15 c                   eval      rcmtl1_DSPS = xTDSPS
6.15 c                   eval      rcmtl1_DMVA = xTDMVA
6.15 c                   eval      rcmtl1_CAVD = xTCAVD
6.15 c                   eval      rcmtl1_CKTO = xTCKTO
6.15 c                   eval      rcmtl1_CSPS = xTCSPS
6.15 c                   eval      rcmtl1_CMVA = xTCMVA
6.15 c                   eval      rcmtl1_BILK = xTBILK
6.15 c                   eval      rcmtl1_LFAK = xTLFAK
6.15 c                   eval      rcmtl1_pros = xTpros
6.17 c                   eval      rcmtl1_text = xTtext
6.15 c     rcmtl1_key    chain     rcmtl1
6.15 c                   if        %found(rcmtl1)
6.15 c                   write     rcmdpfr
6.15 c*                  eval      w_dupl = 'J'
6.15 c                   goto      upd_rcmp
6.15 c                   else
6.15 c*                  eval      w_dupl = 'N'
6.15 c                   write     rcmtpfr
6.15 c                   endif
6.15  *
6.13 c                   eval      w_fgrp = xtfgrp
     c                   eval      w_firm = xtfirm
1.00 c                   eval      w_p_f_comp = 'J'
1.00  *   Dette må gjøres uavhengig av l_perfradat hvis periode = 0
1.00 c                   if        xtrper = *zero
1.00 c                   eval      w_p_f_comp = 'N'
1.00 c     *ymd          move      xtbdat        w_amd
1.00 c                   eval      w_40   = w_amd/100
1.00 c                   eval      w_20   = w_amd/10000
1.00 c                   eval      xtrper = w_40 - w_20 * 100
1.00 c                   eval      xtraar = w_20 + 2000
1.00 c                   endif
      *
     c                   eval      w_raar = xtraar
     c                   eval      w_rper = xtrper
      *
     c                   movel     xtraar        w_peri
     c                   move      xtrper        w_peri
1.00  *  Ev. Sett periode til første periode etter sperret periode.
1.00 c                   if        l_perettspr = 'J'   and
1.00 c                             xtrper <> 13        and
1.00 c                             w_peri <= w_sp_per  and
1.00 c                             w_p_f_comp = 'N'
1.00 c                   eval      xtrper = w_sp_mnd + 1
1.00 c                   if        xtrper > 12
1.00 c                   eval      xtrper = 1
1.00 c                   eval      xtraar = xtraar + 1
1.00 c                   endif
1.00 c                   eval      w_raar = xtraar
1.00 c                   eval      w_rper = xtrper
1.00 c                   movel     xtraar        w_peri
1.00 c                   move      xtrper        w_peri
1.00 c                   endif
1.00  *  Gi melding dersom det fra Compello er satt inn en sperret periode
6.14 c                   eval      w_sv = *blank
1.00 c                   if        w_p_f_comp  = 'J'   and
1.00 c                             w_peri <= w_sp_per
1.00  *    Ikke dersom samme bilagsnr er kvittert før
1.00 c                   if        xtbiln <> w_kv_bilnr
1.00 c                   eval      w_kv_bilnr =  xtbiln
1.00 c                   move      xtbiln        w_bnr_x
1.00 c                   move      xtraar        w_aar_x
1.00 c                   move      xtrper        w_per_x
1.00 c                   eval      %subst(w_m1:10:6) = w_bnr_x
1.00 c                   eval      %subst(w_m2:14:4) = w_aar_x
1.00 c                   eval      %subst(w_m2:19:2) = w_per_x
1.00 c                   eval      w_sv = *blank
6.14 c                   if        w_jkey = *blank
1.00 c                   call      'AA007R'
1.00 c                   parm                    w_m1
1.00 c                   parm                    w_m2
1.00 c                   parm                    w_sv
1.00 c                   parm                    w_in12
6.14 c                   else
6.14 c                   eval      w_jsta = 10
6.14 c                   eval      w_jmld = %trim(w_m1) + ' ' + %trim(w_m2)
6.14 c                   call      'AB705R'
6.14 c                   parm                    w_jkey
6.14 c                   parm                    w_jsta
6.14 c                   parm                    w_jmld
6.14 c                   endif
1.00 c                   endif
1.00  *  Dersom sperret periode ikke godkjennes settes neste gyldige periode
1.00 c                   if        w_sv <> 'J'
1.00 c                   eval      xtrper = w_sp_mnd + 1
1.00 c                   if        xtrper > 12
1.00 c                   eval      xtrper = 1
1.00 c                   eval      xtraar = xtraar + 1
1.00 c                   endif
1.00 c                   eval      w_raar = xtraar
1.00 c                   eval      w_rper = xtrper
1.00 c                   movel     xtraar        w_peri
1.00 c                   move      xtrper        w_peri
1.00 c                   else
1.00 c                   eval      l_v_f_per = 'J'
1.00 c                   endif
1.00 c                   endif
s.ss  *
      *
      * Dersom brudd,
      * oppdater RBUNPF
      *
6.30 c                   if        w_valg <> 5
6.13 c                   if        w_fgrp <> s_fgrp or
6.13 c                             w_firm <> s_firm or
1.00 c                             w_peri <> s_peri or
1.0  c                             w_peri = 0
     c                   if        w_arec <> 0
     c                   exsr      opdat_rbun
     c                   else
     c                   if        %found(rbunlu)
     c                   delete    rbunlur
     c                   endif
     c                   endif
     c                   endif
6.30 c                   endif
      *
      * Nytt firma ?
      *
6.13 c                   if        w_fgrp <> s_fgrp  or
6.13 c                             w_firm <> s_firm  or
     c                             w_arec =  0
     c                   exsr      nytt_firma
     c                   endif
      *
      * Ny periode ?
      *
6.13 c                   if        w_fgrp <> s_fgrp or
6.13 c                             w_firm <> s_firm or
     c                             w_peri <> s_peri
     c                   exsr      ny_periode
     c                   endif
      *
      * Ta vare på gamle verdier
      *
6.13 c                   eval      s_fgrp = w_fgrp
     c                   eval      s_firm = w_firm
     c                   eval      s_peri = w_peri
      *
      * Redigerer regnskapsperiode og år
      *
     c                   exsr      opdat_rtra
     c                   eval      w_arec = w_arec + 1
6.10  *
6.15 c     upd_rcmp      tag
6.10 c                   eval      xthent = 'J'
6.13 c                   update    rcmpl1r
      *
6.10  *
6.10 c     nyles         tag
6.10  *
6.13 c                   read      rcmpl1                                 90
      *
     c                   enddo
      *
      * Siste buntsum,
      * oppdater RBUNPF
      *
6.30 c                   if        w_valg <> 5
     c                   if        w_arec <> 0
     c                   exsr      opdat_rbun
     c                   endif
6.30 c                   endif
      *
      * Avslutt program
      *
     c                   eval      *inlr = *on
6.31  *  Ev close rnavpf
6.31 c                   if        w_valg = 5
7.00 c                             and u_inkst = *on
6.33 c                   close     icomst
6.31 c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for brudd ved nytt firmanr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     nytt_firma    begsr
      *
      * Hent inn firmaregister
      *
     c     afirl1_key    chain     afirl1                             91
      *
      * Hent inn statuskoder (avvikende perioder)
      *
     c     raa3pf_key    chain     raa3pf                             91
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for brudd periode / år
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ny_periode    begsr
      *
      * Brudd periode/år
      * Finn neste ledige buntnr
      *
6.30 c                   if        w_valg <> 5
610  c                   eval      rbunlu_nivo = *blank
610  c                   eval      rbunlu_memb = w_memb
     c                   eval      rbunlu_bunt = 99999
     c     rbunlu_key    setgt     rbunlu
     c                   readp     rbunlu                                 91
      *
     c                   if        *in91 = *on
     c                   eval      w_bunt = 1
     c                   else
     c                   eval      w_bunt = rsbunt + 1
     c                   endif
6.30 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av RTRAPF
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opdat_rtra    begsr
V5.50 *
6.13 c                   if        xtbelo < 0
6.13 c                   eval      xtbelo = xtbelo * -1
     c                   endif
V5.50 *
     c                   if        xtvalb < 0
     c                   eval      xtvalb = xtvalb * -1
     c                   endif
V5.50 *
V5.50 * Dersom beløp = 0 og valutakode er blank, settes
V5.50 * beløp = valutabeløp og valutabeløp = 0
V5.50 *
6.13 c                   if        xtbelo = 0 and
     c                             xtvalk = ' '
6.13 c                   eval      xtbelo = xtvalb
     c                   eval      xtvalb = 0
     c                   endif
      *
      * Beregn buntsummer
      *
     c                   if        xtdkto <> 0
6.13 c                   eval      w_psum = w_psum + xtbelo
6.13 c                   eval      w_bsum = w_bsum + xtbelo
6.13 c                   eval      w_pdeb = w_pdeb + xtbelo
     c                   endif
      *
     c                   if        xtckto <> 0
6.13 c                   eval      w_psum = w_psum + xtbelo
6.13 c                   eval      w_pkre = w_pkre + xtbelo
     c                   endif
      *
     c                   eval      w_linj = w_linj + 1
      *
     c                   clear                   rtrapfr
     c                   eval      rtreid = 'T'
6.11 c                   eval      rtfirm = s_firm
     c                   eval      rtbunt = w_bunt
     c                   eval      rtlinj = w_linj
     c                   eval      rtstat = *blank
     c                   eval      rtraar = xtraar
     c                   eval      rtrper = xtrper
     c                   eval      rtbiln = xtbiln
     c                   eval      rtbill = xtbill
      *
     c                   eval      rtdavd = xtdavd
     c                   eval      rtdkto = xtdkto
     c                   eval      rtdsps = xtdsps
     c                   eval      rtdmva = xtdmva
      *
     c                   eval      rtcavd = xtcavd
     c                   eval      rtckto = xtckto
     c                   eval      rtcsps = xtcsps
     c                   eval      rtcmva = xtcmva
      *
6.13 c                   eval      rtbelØ = xtbelo
V5.50c                   eval      rtvalb = xtvalb
V5.50c                   eval      rtvalk = xtvalk
V5.50 *
     c                   eval      rtgmva = 0
6.16 c                   movel     xtfirm        rp4ol1_firm
6.16 c                   movel     xtarbo        rp4ol1_arbo
6.16 c     rp4ol1_key    chain     rp4ol1
6.16 c                   if        %found(rp4ol1)
6.16 c                   eval      rtpros = rp4pro
6.16 c                   eval      rtupro = rp4upr
6.16 c                   eval      rtakti = rp4akt
6.16 c                   eval      rtarbo = rp4ord
6.16 c                   else
6.16 c                   movel     xtpros        rtpros
6.16 c                   movel     xtakti        rtakti
6.16 c                   endif
     c                   eval      rtrefn = 0
     c                   eval      rtkrab = 0
     c                   eval      rtmngd = 0
     c                   eval      rtmngk = 0
     c                   eval      rtresn = xtresn
     c                   eval      rtlfak = xtlfak
     c                   eval      rtvirk = ' '
     c                   eval      rtautx = ' '
     c                   eval      rtatte = ' '
      *
     c                   eval      rtbdat = xtbdat
     c                   eval      rtfdat = xtfdat
     c                   eval      rtkidi = xtkidi
6.12  *
6.12  *  Ev. konv av bilagskoder
6.12  *
6.12 c                   move      xtbilk        w_bilk
6.12 c                   select
6.12 c                   when      %subst(l_blk:01:4) <> *blank and
6.12 c                             %subst(l_blk:01:2) = w_bilk
6.12 c                   eval      w_bilk = %subst(l_blk:03:2)
6.12 c                   move      w_bilk        xtbilk
6.12  *
6.12 c                   when      %subst(l_blk:05:4) <> *blank and
6.12 c                             %subst(l_blk:05:2) = w_bilk
6.12 c                   eval      w_bilk = %subst(l_blk:07:2)
6.12 c                   move      w_bilk        xtbilk
6.12  *
6.12 c                   when      %subst(l_blk:09:4) <> *blank and
6.12 c                             %subst(l_blk:09:2) = w_bilk
6.12 c                   eval      w_bilk = %subst(l_blk:11:2)
6.12 c                   move      w_bilk        xtbilk
6.12  *
6.12 c                   when      %subst(l_blk:13:4) <> *blank and
6.12 c                             %subst(l_blk:13:2) = w_bilk
6.12 c                   eval      w_bilk = %subst(l_blk:15:2)
6.12 c                   move      w_bilk        xtbilk
6.12 c                   other
6.12 c                   endsl
     c                   eval      rtbilk = xtbilk
     c                   eval      rttext = xttext
     c                   eval      rtbetb = 0
     c                   eval      rtbref = ' '
610  c                   eval      rtnivo = *blank
610  c                   eval      rtmemb = w_memb
610  c                   time                    rtedat
610  c                   time                    rtetim
610  c                   eval      rteusr = l_user
      *
6.30 c                   if        w_valg <> 5
     c                   write     rtrapfr
     c                   eval      W_rtra = 'J'
6.30 c                   else
6.30  *   Byge posteringer til NAV
6.30  *
7.00 c                   if        u_inkst = *on
6.30 c*    NB!           eval      uid    = rt
6.33 c                   eval      icFIRM = rtfirm
6.33 c                   eval      icBILN = rtbiln
6.33 c                   eval      icBDAT = rtbdat
     c                   move      rtrper        w_mmx
     c                   move      rtraar        w_aax
     c                   move      '31'          w_ddx
     c                   select
     c                   when      rtrper = 02
     c                   eval      w_ddx = '28'
     c                   when      rtrper = 04
     c                             or rtrper = 06
     c                             or rtrper = 09
     c                             or rtrper = 11
     c                   eval      w_ddx = '30'
     c                   endsl
     c                   eval      %subst(w_perx:1:2) = w_ddx
     c                   eval      %subst(w_perx:3:2) = w_mmx
     c                   eval      %subst(w_perx:5:2) = w_aax
     c                   move      w_perx        w_pern
6.35 c     *dmy          move      w_pern        icboda
6.33 c                   eval      icFDAT = rtfdat
6.33 c                   eval      icBILK = rtbilk
6.33 c                   eval      icTEXT = rttext
6.33 c                   eval      icDAVD = rtdavd
6.33 c                   eval      icDKTO = rtdkto
6.33 c                   eval      icDSPS = rtdsps
6.33 c                   eval      icDMOM = rtdmva
6.33 c                   eval      icCAVD = rtcavd
6.33 c                   eval      icCKTO = rtckto
6.33 c                   eval      icCSPS = rtcsps
6.33 c                   eval      icCMOM = rtcmva
     c                   if        rtckto > 0
6.33 c                   eval      icbelp = rtbelØ * -1
     c                   else
6.33 c                   eval      icBELP = rtbelØ
     c                   endif
6.33 c                   eval      icKUND = rtresn
6.33 c                   if        ickund = 0
     c                   if        rtckto > ra1hrs
6.33 c                   eval      ickund = rtckto
     c                   else
     c                   if        rtdkto > ra1hrs
6.33 c                   eval      ickund = rtdkto
     c                   endif
     c                   endif
     c                   endif
6.34 c                   eval      rcmrl1_firm = rtfirm
6.34 c                   eval      rcmrl1_biln = rtbiln
6.34 c     rcmrl1_key    chain     rcmrl1
6.34 c                   if        %found(rcmrl1)
6.35 c                   if        ickund = 0
6.35 c                   eval      ickund = rrresn
6.35 c                   endif
6.35 c                   if        rrrper > 0
6.35 c                   move      rrrper        w_mmx
6.35 c                   move      rrraar        w_aax
6.36 c*                  move      '31'          w_ddx
6.36 c                   move      '01'          w_ddx
6.35 c                   select
6.35 c                   when      rrrper = 02
6.36 c*                  eval      w_ddx = '28'
6.36 c                   eval      w_ddx = '01'
6.35 c                   when      rrrper = 04
6.35 c                             or rrrper = 06
6.35 c                             or rrrper = 09
6.35 c                             or rrrper = 11
6.36 c*                  eval      w_ddx = '30'
6.36 c                   eval      w_ddx = '01'
6.35 c                   endsl
6.35 c                   eval      %subst(w_perx:1:2) = w_ddx
6.35 c                   eval      %subst(w_perx:3:2) = w_mmx
6.35 c                   eval      %subst(w_perx:5:2) = w_aax
6.35 c                   move      w_perx        w_pern
6.35 c     *dmy          move      w_pern        icboda
6.35 c                   endif
6.35 c                   endif
      *
6.33 c                   eval      icKREF = rtrefn
6.33 c                   eval      icKIDI = rtkidi
6.33 c                   eval      icVALK = rtvalk
6.33 c                   eval      icVALB = rtvalb
6.33 c                   eval      icPROJ = rtpros
6.33 c                   eval      icAKTI = rtakti
6.33 c                   eval      icSFAK = rtlfak
6.33  *
6.33  *  NAV-Løsning,  Her settes alltid Overstyrt bokføringsdato
6.33  *                Settes tidligere utfra periode fra Compello
6.35 c*                  eval      icboda = w_isodat
6.35 c                   eval      icboda = icboda
6.33  * debug
6.33 c                   eval      icODAT = rtedat
6.33 c                   eval      icOTIM = rtetim
6.33 c                   eval      icOUSR = rteusr
6.33 c                   write     icomstr
7.00 c                   endif
6.30 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av RBUNPF
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opdat_rbun    begsr
      *
     c                   xfoot     per           w_xper
     c                   movel     rtrper        wpe
      *
     c                   if        ra3stp > 01 and
     c                             w_xper > *zero
     c                   movel     per(wpe)      rtrper
     c                   if        wpe    < ra3stp
     c                   move      w_Årm1        rtrper
     c                   endif
     c                   endif
      *
6.11 c                   eval      rsfirm = s_firm
     c                   eval      rsbunt = w_bunt
     c                   eval      rsstat = *blank
     c                   eval      rsraar = rtraar
     c                   eval      rsrper = rtrper
     c                   eval      rsfØrs = 1
     c                   eval      rssist = w_linj
     c                   eval      rsbsum = w_bsum
     c                   eval      rspsum = w_psum
     c                   eval      rspdeb = w_pdeb
     c                   eval      rspkre = w_pkre
     c                   time                    rspdat
      *
610  c                   eval      rsnivo = *blank
610  c                   eval      rsmemb = w_memb
610  c                   time                    rsedat
610  c                   time                    rsetim
610  c                   eval      rseusr = l_user
     c                   write     rbunlur
      *
      * Nullstill buntsum
      *
     c                   eval      w_bsum = 0
     c                   eval      w_psum = 0
     c                   eval      w_pdeb = 0
     c                   eval      w_pkre = 0
     c                   eval      w_arec = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
610  c     *entry        plist
610  c                   parm                    w_memb
610  c                   parm                    w_jkey
610  c                   parm                    w_stat
      *
     c                   eval      wpe = 0
     c                   eval      w_bsum = 0
     c                   eval      w_Årm1 = uyear - 1
     c                   eval      w_rper = *month
      *
6.13 c                   eval      w_fgrp = l_fgrp
     c                   eval      w_firm = l_firm
6.13 c                   eval      s_fgrp = l_fgrp
     c                   eval      s_firm = l_firm
      *
      * Key for les av firmaregister
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les av statuskoder 3 (avvikende perioder)
      *
     c     raa3pf_key    klist
     c                   kfld                    w_firm
1.00  *
1.00  * Key for les av statuskoder 1 (Sperret periode)
1.00  *
1.00 c     raa1pf_key    klist
1.00 c                   kfld                    w_firm
      *
      * Key for oppdatering av buntfile
      *
     c     rbunlu_key    klist
610  c                   kfld                    rbunlu_nivo
610  c                   kfld                    rbunlu_memb
     c                   kfld                    w_firm
     c                   kfld                    rbunlu_bunt
6.15  *
6.15  * Key for sjekk om innlest før
6.15  *
6.15 c     rcmtl1_key    klist
6.15 c                   kfld                    rcmtl1_fgrp
6.15 c                   kfld                    rcmtl1_firm
6.15 c                   kfld                    rcmtl1_bdat
6.15 c                   kfld                    rcmtl1_belo
6.15 c                   kfld                    rcmtl1_raar
6.15 c                   kfld                    rcmtl1_rper
6.15 c                   kfld                    rcmtl1_biln
6.15 c                   kfld                    rcmtl1_davd
6.15 c                   kfld                    rcmtl1_dkto
6.15 c                   kfld                    rcmtl1_dsps
6.15 c                   kfld                    rcmtl1_dmva
6.15 c                   kfld                    rcmtl1_cavd
6.15 c                   kfld                    rcmtl1_ckto
6.15 c                   kfld                    rcmtl1_csps
6.15 c                   kfld                    rcmtl1_cmva
6.15 c                   kfld                    rcmtl1_bilk
6.15 c                   kfld                    rcmtl1_lfak
6.16 c                   kfld                    rcmtl1_pros
6.17 c                   kfld                    rcmtl1_text
6.34  *
6.34  * Key for henting av levnr på posteringer til NAV
6.34  *
6.34 c     rcmrl1_key    klist
6.34 c                   kfld                    rcmrl1_firm
6.34 c                   kfld                    rcmrl1_biln
6.16  *
6.16  * Key for henting av prosjet.... fra rp4ol1
6.16  *
6.16 c     rp4ol1_key    klist
6.16 c                   kfld                    rp4ol1_firm
6.16 c                   kfld                    rp4ol1_arbo
1.00  *
1.00  * Les RAA1PF for p finne sists sperret periode
1.00  *
1.00 c     raa1pf_key    chain     raa1pf
1.00 c                   if        %found(raa1pf)
1.00 c                   eval      W_sp_mnd = ra1per
1.00 c                   eval      w_sp_aar = ra1aar
1.00 c                   eval      w_sp_per = w_sp_aar * 100 + w_sp_mnd
1.00 c                   else
1.00 c                   eval      w_sp_per = *zero
1.00 c                   endif
1.00 c                   eval      l_v_f_per = *blank
7.00  *
7.00  * Test om INKST skal legges ut ved Regnsk.syst = 5
7.00  *
7.00   u_INKST = *on;
7.00   CallP CO402R(l_fgrp:w_firm:0:'IKKE_INKST_VED_REGN_SYST=5':
7.00   co402_verdi1:co402_verdi2);
7.00   if co402_verdi1 = '1';
7.00     u_INKST = *off;
7.00   endif;
6.12  *
6.12  * Hent ev konv bilagskoder fra l_blkegler   pos 28(4) * 4 (fra-til)
6.12  *
6.12  *    Bilagskoder måtte hentes i cl da dta-område varierer m/firma
6.12  *                lagt i l_blk  (lda(111-126))
6.30  *
6.30  *
6.30  *  Sjekk om transer skal legges ut til NAV
6.30  *                                          w_valg = 5 = NAV
6.30 c                   call      'RV015R'
6.30 c                   parm                    w_valg
6.30 c                   parm                    w_nxdr
6.31  *  Ev. åpne fil til NAV
6.31 c                   if        w_valg = 5
6.33 c                   open      icomst
6.33 c                   move      udate         dato
6.33 c     *dmy          move      udate         w_isodd
6.33 c                   eval      *in43 = *on
6.33 c                   eval      *in44 = *off
6.33 c     param         tag
     c                   goto      noparam
6.33 c                   exfmt     k1win
6.33 c                   eval      dato = dato
6.33 c                   movel     dato          w_dato
6.33 c     *dmy          test(d)                 w_dato                 33
6.33 c                   if        not *in33
6.33 c     *dmy          move      w_dato        w_isodat
6.33 c                   if        w_isodat > (w_isodd + %days(30))
6.33 c                   eval      *in33 = *on
6.33 c                   endif
6.33 c                   endif
6.33 c                   if        not *in33
6.33 c     *dmy          move      w_dato        w_isodat
6.33 c                   if        w_isodat < (w_isodd - %days(90))
6.33 c                   eval      *in33 = *on
6.33 c                   endif
6.33 c                   endif
6.33 c                   if        *in33
6.33 c                   eval      meld = 'Feil dato'
6.33 c                   eval      *in43 = *off
6.33 c                   eval      *in44 = *on
6.33 c                   goto      param
6.33 c                   endif
6.33  *
6.33 c     *dmy          move      w_dato        w_isodat
6.31 c                   endif
     c     noparam       tag
6.30  *
     c                   endsr
