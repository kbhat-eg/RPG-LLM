     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASNAVIOKO
      * Program . . . . : IT112R
      * Beskrivelse . . : Oppdaterer leverandørinformasjon fra NAV
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       191114 ask  Nytt program
7.01  * 7.01  250220 ask  Utvidet med nytt felt, kode "Fast forretningssted"
7.02  * 7.02  180321 ask  Lagt inn p_user i paramererliste, gav feil l logging
7.03  * 7.03  070421 ask  Utvidet med nye felter, blokkeringskode og sperrekode
8.01  * 800   070521 ask  Lagt inn oppdatering av betalingsbetingelse
8.02  * 800   050921 sst  Fjernet oppdatering av selger
8.03  * 800   070921 ask  Fjernet oppdatering av mail adresse
8.04  * 800   241121 ask  Fjernet oppdatering av mail telefon
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frlevlu    uf a e           k disk    rename(rlevpfr:rlevlur)
      *
      * Parameter-inn
      *
     d p_firm          s                   like(rlfirm)
     d p_comd          s             20
     d p_parm          s           2048
     d p_user          s             10
     d p_stat          s              1
      *
      * Definering av variabler i nøkler
      *
     d rlevlu_levr     s                   like(rllevr)
      *
      *  Definering av datastrukturer for parameter
      *
     d d_parm          ds          2048
     d  d_levr                        6
     d  d_navn                       30
     d  d_adr1                       30
     d  d_adr2                       30
     d  d_ponr                        4
     d  d_pste                       30
     d  d_bad1                       30
     d  d_bad2                       30
     d  d_bpnr                        4
     d  d_land                        2
     d  d_alfa                       18
     d  d_fort                        9
     d  d_emai                       50
     d  d_weba                       40
     d  d_tlfn                       11
     d  d_tlfx                       11
     d  d_tlfm                       11
     d  d_bgir                       11
     d  d_eann                       13
     d  d_avde                        4
     d  d_lage                        2
     d  d_selg                        4
     d  d_dist                        4
     d  d_bbet                        2
     d  d_valk                        3
     d  d_lbet                        2
     d  d_flev                        6
7.01 d  d_isok                        1
7.03 d  d_blok                        1
7.03 d  d_sper                        1
      *
      * Definering av variable
      *
     d w_firm          s                   like(rlfirm)
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_levr          s                   like(rllevr)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram - oppdaterer informasjon på levr., setter statusflagg
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Bytter ut blanke med nuller i tommer felter som skal konverteres
      *
     c                   eval      d_parm = p_parm
      *
     c                   if        d_ponr = *blanks
     c     ' ':'0'       xlate     d_ponr        d_ponr
     c                   endif
     c                   if        d_bpnr = *blanks
     c     ' ':'0'       xlate     d_bpnr        d_bpnr
     c                   endif
     c                   if        d_fort = *blanks
     c     ' ':'0'       xlate     d_fort        d_fort
     c                   endif
     c                   if        d_bgir = *blanks
     c     ' ':'0'       xlate     d_bgir        d_bgir
     c                   endif
     c                   if        d_eann = *blanks
     c     ' ':'0'       xlate     d_eann        d_eann
     c                   endif
     c                   if        d_avde = *blanks
     c     ' ':'0'       xlate     d_avde        d_avde
     c                   endif
     c                   if        d_lage = *blanks
     c     ' ':'0'       xlate     d_lage        d_lage
     c                   endif
     c                   if        d_selg = *blanks
     c     ' ':'0'       xlate     d_selg        d_selg
     c                   endif
     c                   if        d_dist = *blanks
     c     ' ':'0'       xlate     d_dist        d_dist
     c                   endif
     c                   if        d_lbet = *blanks
     c     ' ':'0'       xlate     d_lbet        d_lbet
     c                   endif
8.01 c                   if        d_bbet = *blanks
8.01 c     ' ':'0'       xlate     d_bbet        d_bbet
8.01 c                   endif
      *
      * Omformer numeriske felter fra parameterfelt til variable
      *
     c                   eval      w_levr = %dec(d_levr:6:0)
      *
      * Velger oppdatering avhengig av kommando
      *
     c                   select
     c                   when      p_comd = 'INSERT'
     c                   exsr      NyEndLev
     c                   when      p_comd = 'UPDATE'
     c                   exsr      NyEndLev
     c                   when      p_comd = 'DELETE'
     c                   exsr      sletlev
     c                   endsl
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slutt         tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Opprette/endre leverandørpost
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     NyEndLev      begsr
      *
     c                   eval      w_firm = p_firm
     c                   eval      rlevlu_levr = w_levr
     c     rlevlu_key    chain     rlevlu
     c                   eval      rlfirm = w_firm
     c                   eval      rllevr = w_levr
     c                   eval      rlnavn = d_navn
     c                   eval      rlgate = d_adr1
     c                   eval      rladr2 = d_adr2
     c                   if        d_land = 'NO'
     c                   eval      rlland = *blank
     c                   else
     c                   eval      rlland = d_land
     c                   endif
      *
     c                   if        rlland = 'NO' or
     c                             rlland = '  '
     c                   eval      rlponr = %dec(d_ponr:4:0)
     c                   eval      rlsted = d_ponr + ' ' + d_pste
     c                   else
     c                   eval      rlponr = 0
     c                   eval      rlsted = d_pste
     c                   endif
      *
     c                   eval      rlftnr = %dec(d_fort:9:0)
     c                   eval      rlalfa = d_alfa
8.03 c**                 eval      rlemal = d_emai
     c                   eval      rlweba = d_weba
8.04 c**                 eval      rltlfn = d_tlfn
     c                   eval      rltlfx = d_tlfx
     c                   eval      rlbgir = %dec(d_bgir:11:0)
     c                   eval      rlavde = %dec(d_avde:4:0)
     c                   eval      rllagr = %dec(d_lage:2:0)
8.02 c**                 eval      rlsaks = %dec(d_selg:4:0)
     c                   eval      rldist = %dec(d_dist:4:0)
     c                   eval      rlvalk = d_valk
     c                   eval      rllbet = %dec(d_lbet:2:0)
8.01 c                   eval      rlbetb = %dec(d_bbet:2:0)
7.01 c                   eval      rlisok = d_isok
7.03 c                   if        d_blok = '1'
7.03 c                   eval      rlsprk = 'J'
7.03 c                   else
7.03 c                   eval      rlsprk = ' '
7.03 c                   endif
7.01 c                   eval      rlisok = d_isok
7.03 c                   if        d_sper = '1'
7.03 c                   eval      rlpass = 'J'
7.03 c                   else
7.03 c                   eval      rlpass = ' '
7.03 c                   endif
     c                   eval      rledat = w_date
     c                   eval      rletim = w_time
     c                   eval      rlesig = p_user
      *
     c                   if        not %found
     c                   eval      rlbpnr = %dec(d_bpnr:4:0)
     c                   eval      rlbgat = d_bad1
     c                   eval      rlbadr = d_bad2
     c                   eval      rlmobn = d_tlfm
     c*                  eval      rleanl = %dec(d_eann:13:0)
     c                   eval      rlndat = w_date
     c                   eval      rlntim = w_time
     c                   eval      rlnsig = p_user
     c                   write     rlevlur
     c                   eval      p_stat = '1'
     c                   else
     c                   update    rlevlur
     c                   eval      p_stat = '1'
     c                   endif
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Slett leverandørpost
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sletlev       begsr
      *
     c                   eval      w_firm = p_firm
     c                   eval      rlevlu_levr = w_levr
     c     rlevlu_key    chain     rlevlu
     c                   if        %found
     c                   delete    rlevlur
     c                   eval      p_stat = '1'
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      p_stat = ' '
     c                   eval      *inlr = *on
     c                   return
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_comd
     c                   parm                    p_parm
7.02 c                   parm                    p_user
     c                   parm                    p_stat
      *
      * Key for posisjonering på valgt kode
      *
     c     rlevlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevlu_levr
      *
      * Setter "timestamp" for postoppdateringer
      *
     c                   time                    w_date
     c                   time                    w_time
      *
     c                   endsr
