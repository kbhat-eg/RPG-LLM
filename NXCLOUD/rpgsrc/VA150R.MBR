     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VA150R
      * Beskrivelse . . : Ordretype, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       070302 HKO  Nytt program
5.01  *       100502 AMD  Lagt på valg av bilde-alternativ
5.02  *       150502 AMD  Lagt på valg av felles/direkte/jobbkø
5.32  *       080904 AMD  Lagt inn ordretype for nummerserie
5.40  *       070608 AMD  Lagt inn ordretype for motsvarende system
      *
6.10  * 6.10  260609 BSA  Oppdatert med sporingsinformasjon
6.20  *       260913 bof  utvidet med kode for intern ordretype
      *
7.xx  * 7.00  110316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  *       231116 bof  utvidet med kode for intern kontering  08.02.2021-sst
      *
8.01  *K0000  261124 bsa  Utvidet tillegstabell for skifte av ordretype hvis
8.01  *K0000              faktura/kredtitnota bytter type. Brukes kun under
8.01  *K0000              fakturering.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvotyl2    if   e           k disk    rename(votypfr:votyl2r)
     fvotylr    if   e           k disk    rename(votypfr:votylrr)
     fvotylu    uf a e           k disk    rename(votypfr:votylur)
6.20 fvot1lr    if   e           k disk    rename(vot1pfr:vot1lrr)
6.20 fvot1lu    uf a e           k disk    rename(vot1pfr:vot1lur)
      *
     fva150d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d votyl1_otyp     s                   like(vaotyp)
     d votyl2_otxt     s                   like(vaotxt)
     d votylr_otyp     s                   like(vaotyp)
     d votylu_otyp     s                   like(vaotyp)
6.20 d vot1lr_1typ     s                   like(va1typ)
6.20 d vot1lu_1typ     s                   like(va1typ)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
5.01 d w_txt1          s             20
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(vaofir)
      *
     d s_otyp          s                   like(vaotyp)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meleingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2otyp <> *blank or
     c                             b2otxt <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      votyl2_otxt = b1otxt
     c     votyl2_key    setll     votyl2
     c                   other
     c                   eval      votyl1_otyp = b1otyp
     c     votyl1_key    setll     votyl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Ordretype
      *
     c                   if        b2otyp <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      votyl1_otyp = b2otyp
     c     votyl1_key    setll     votyl1
     c                   goto      end_pos
     c                   endif
      *
      * Beskrivelse
      *
     c                   if        b2otxt <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      votyl2_otxt = b2otxt
     c     votyl2_key    setll     votyl2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2otyp = *blank
     c                   eval      b2otxt = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1otyp = b1otyp
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1otyp = b1otyp
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1otyp = b1otyp
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1otyp = b1otyp
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1otyp = b2otyp
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
     c                   if        c1otyp = *blank
     c                   goto      end_c1bld
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      votylr_otyp = c1otyp
     c     votylr_key    chain     votylr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1otyp = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2otyp = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      votylr_otyp = c1otyp
     c     votylr_key    chain     votylr
      *
     c                   if        %found
     c                   eval      c2otxt = vaotxt
     c                   eval      c2osys = vaosys                                            .
     c                   eval      c2oakk = vaoakk                                            .
5.01 c                   eval      c2obld = vaobld                                            .
5.02 c                   eval      c2ofdj = vaofdj                                            .
     c                   eval      c2oogr = vaoogr                                            .
     c                   eval      c2oboo = vaoboo                                            .
     c                   eval      c2oska = vaoska                                            .
     c                   eval      c2osam = vaosam                                            .
     c                   eval      c2olag = vaolag                                            .
     c                   eval      c2ooko = vaooko                                            .
     c                   eval      c2osta = vaosta                                            .
     c                   eval      c2opro = vaopro                                            .
     c                   eval      c2obut = vaobut                                            .
     c                   eval      c2okre = vaokre                                            .
     c                   eval      c2obil = vaobil                                            .
5.32 c                   eval      c2onum = vaonum                                            .
5.40 c                   eval      c2omot = vaomot                                            .
     c                   eval      c2onxt = vaonxt                                            .
3.31 c                   eval      c2on01 = vaon01                                            .
3.31 c                   eval      c2on02 = vaon02                                            .
3.31 c                   eval      c2on03 = vaon03                                            .
3.31 c                   eval      c2on04 = vaon04                                            .
3.31 c                   eval      c2on05 = vaon05                                            .
3.31 c                   eval      c2on06 = vaon06                                            .
     c                   eval      c2orut = vaorut                                            .
     c                   eval      c2orub = vaorub                                            .
     c                   else
     c                   eval      c2otxt = *blank
     c                   eval      c2osys = 0                                                 .
     c                   eval      c2oakk = 0                                                 .
5.01 c                   eval      c2obld = 0                                                 .
5.02 c                   eval      c2ofdj = 0                                                 .
     c                   eval      c2oogr = 0                                                 .
     c                   eval      c2oboo = 0                                                 .
     c                   eval      c2oska = 0                                                 .
     c                   eval      c2osam = 0                                                 .
     c                   eval      c2olag = 0                                                 .
     c                   eval      c2ooko = 0                                                 .
     c                   eval      c2osta = 0                                                 .
     c                   eval      c2opro = 0                                                 .
     c                   eval      c2obut = 0                                                 .
     c                   eval      c2okre = *blank                                            .
     c                   eval      c2obil = 0                                                 .
5.32 c                   eval      c2onum = *blank                                            .
5.40 c                   eval      c2omot = *blank                                            .
     c                   eval      c2onxt = *blank                                            .
3.31 c                   eval      c2on01 = *blank                                            .
3.31 c                   eval      c2on02 = *blank                                            .
3.31 c                   eval      c2on03 = *blank                                            .
3.31 c                   eval      c2on04 = *blank                                            .
3.31 c                   eval      c2on05 = *blank                                            .
3.31 c                   eval      c2on06 = *blank                                            .
     c                   eval      c2orut = *blank                                            .
     c                   eval      c2orub = *blank                                            .
     c                   endif
6.20  *
6.20 c                   eval      vot1lr_1typ = c1otyp
6.20 c     vot1lr_key    chain     vot1lr
6.20 c                   if        %found(vot1lr)
6.20 c                   eval      c21int = va1int
7.01 c                   eval      c21ink = va1ink
8.01 c                   eval      c21mot = va1mot
6.20 c                   else
6.20 c                   eval      c21int = 0
7.01 c                   eval      c21ink = 0
8.01 c                   eval      c21mot = *blanks
6.20 c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1otyp = k1otyp
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
5.01  * Spørring ?
5.01 c                   if        *inkd
5.01 c                   exsr      spØrring
5.01 c                   goto      c2taga
5.01 c                   endif
8.01  *
8.01  * Sjekk gyldig OT er lagt inn
8.01  *
8.01 c                   if        c21mot <> *blanks
8.01  *
8.01 c                   if        c2osys <> 0
8.01 c                   eval      *in35 = *on
8.01 c                   goto      c2taga
8.01 c                   endif
8.01  *
8.01 c                   if        c2oakk <> 3
8.01 c                   eval      *in34 = *on
8.01 c                   goto      c2taga
8.01 c                   endif
8.01  *
8.01 c                   eval      votylr_otyp = c21mot
8.01 c     votylr_key    chain     votylr
8.01  *
8.01 c                   if        not %found
8.01 c                   eval      *in33 = *on
8.01 c                   goto      c2taga
8.01 c                   endif
8.01  *
8.01 c                   if        vaoakk <> 3
8.01 c                   eval      *in31 = *on
8.01 c                   goto      c2taga
8.01 c                   endif
8.01  *
8.01 c                   if        c2okre = vaokre
8.01 c                   eval      *in32 = *on
8.01 c                   goto      c2taga
8.01 c                   endif
8.01  * Hent tilbake verdier fra ordretypen vi er på
8.01 c                   eval      votylr_otyp = c1otyp
8.01 c     votylr_key    chain     votylr
8.01 c                   endif
      *
      * Oppdatering
      *
     c                   clear                   votylur
      *
     c                   eval      votylu_otyp = c1otyp
     c     votylu_key    chain     votylur
      *
     c                   eval      vaotxt = c2otxt
     c                   eval      vaosys = c2osys
     c                   eval      vaoakk = c2oakk
5.01 c                   eval      vaobld = c2obld
5.02 c                   eval      vaofdj = c2ofdj
     c                   eval      vaoogr = c2oogr
     c                   eval      vaoboo = c2oboo
     c                   eval      vaoska = c2oska
     c                   eval      vaosam = c2osam
     c                   eval      vaolag = c2olag
     c                   eval      vaooko = c2ooko
     c                   eval      vaosta = c2osta
     c                   eval      vaopro = c2opro
     c                   eval      vaobut = c2obut
     c                   eval      vaokre = c2okre
     c                   eval      vaobil = c2obil
5.32 c                   eval      vaonum = c2onum
5.40 c                   eval      vaomot = c2omot
     c                   eval      vaonxt = c2onxt
3.31 c                   eval      vaon01 = c2on01
3.31 c                   eval      vaon02 = c2on02
3.31 c                   eval      vaon03 = c2on03
3.31 c                   eval      vaon04 = c2on04
3.31 c                   eval      vaon05 = c2on05
3.31 c                   eval      vaon06 = c2on06
     c                   eval      vaorut = c2orut
     c                   eval      vaorub = c2orub
      *
6.10 c                   time                    vaoeda
6.10 c                   time                    vaoeti
6.10 c                   eval      vaoeus = l_user
     c                   if        %found
     c                   update    votylur
     c                   else
     c                   eval      vaofir = w_firm
     c                   eval      vaotyp = c1otyp
6.10 c                   time                    vaooda
6.10 c                   time                    vaooti
6.10 c                   eval      vaoous = l_user
     c                   write     votylur
     c                   endif
6.20  *
6.20 c                   eval      vot1lu_1typ = c1otyp
6.20 c     vot1lu_key    chain     vot1lur
6.20 c                   eval      va1int = c21int
7.01 c                   eval      va1ink = c21ink
8.01 c                   eval      va1mot = c21mot
6.20 c                   time                    va1eda
6.20 c                   time                    va1eti
6.20 c                   eval      va1eus = l_user
6.20 c                   if        %found(vot1lu)
6.20 c                   update    vot1lur
6.20 c                   else
6.20 c                   eval      va1fir = w_firm
6.20 c                   eval      va1typ = c1otyp
6.20 c                   time                    va1oda
6.20 c                   time                    va1oti
6.20 c                   eval      va1ous = l_user
6.20 c                   write     vot1lur
6.20 c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1otxt = vaotxt
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      votylr_otyp = d1otyp
     c     votylr_key    chain     votylr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      votylu_otyp = d1otyp
     c     votylu_key    chain     votylur
      *
     c                   delete    votylur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_otyp = k1otyp
      *
      * Fyller kopierings-bildet
      *
     c                   eval      votylr_otyp = k1otyp
     c     votylr_key    chain     votylr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1otyp = *blank
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      votylr_otyp = k1otyp
     c     votylr_key    chain     votylr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1otyp = s_otyp
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      votyl2
     c                   other
     c                   read      votyl1
     c                   endsl
      *
     c                   if        %eof or
     c                             vaofir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      votyl2_otxt = vaotxt
     c                   other
     c                   eval      votyl1_otyp = vaotyp
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1otyp = vaotyp
     c                   eval      b1otxt = vaotxt
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     votyl2_key    setgt     votyl2
     c                   readp     votyl2
     c                   other
     c     votyl1_key    setgt     votyl1
     c                   readp     votyl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     votyl2
     c                   other
     c                   readp     votyl1
     c                   endsl
      *
     c                   if        %eof or
     c                             vaofir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      votyl2_otxt = vaotxt
     c                   other
     c                   eval      votyl1_otyp = vaotyp
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     votyl2_key    setll     votyl2
     c                   other
     c     votyl1_key    setll     votyl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
5.01  *
5.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.01  * Subrutine for behandling av spørring
5.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.01  *
5.01 c     spØrring      begsr
5.01  *
5.01  * Bilde-alternativer
5.01  *
5.01 c                   if        w_afld = 'C2OBLD'
5.01 c                   eval      c2obld = 0
5.01 c                   eval      w_txt1 = *blank
5.01 c                   call      'VA590R'
5.01 c                   parm                    w_firm
5.01 c                   parm                    c2obld
5.01 c                   parm                    w_txt1
5.01 c                   goto      end_spØrr
5.01 c                   endif
5.01  *
5.01 c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på ordretype
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
6.20  *
6.20  * Key for posisjonering på ordretype sidereg
6.20  *
6.20 c     vot1lr_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    vot1lr_1typ
      *
      * Key for posisjonering på beskrivelse
      *
     c     votyl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl2_otxt
      *
      * Key for les på ordretype
      *
     c     votylr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votylr_otyp
      *
      * Key for oppdatering av ordretype
      *
     c     votylu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votylu_otyp
6.20  *
6.20  * Key for oppdatering av ordretype  sideregister
6.20  *
6.20 c     vot1lu_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    vot1lu_1typ
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjermbildet
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      votyl1_otyp = *blank
     c     votyl1_key    setll     votyl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
