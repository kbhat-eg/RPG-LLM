     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASPGPL
      * Program . . . . : AF110R
      * Beskrivelse . . : Logger for FTP, hode
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       280104 MHA  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fafthl1    if   e           k disk    rename(afthpfr:afthl1r)
     fafthlr    if   e           k disk    rename(afthpfr:afthlrr)
     fafthlu    uf a e           k disk    rename(afthpfr:afthlur)
     faftllu    uf a e           k disk    rename(aftlpfr:aftllur)
      *
     faf110d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d afthl1_navn     s                   like(adhnav)
     d afthlr_navn     s                   like(adhnav)
     d afthlr_uidn     s                   like(adhuid)
     d afthlu_navn     s                   like(adhnav)
     d afthlu_uidn     s                   like(adhuid)
     d aftllu_navn     s                   like(adlnav)
     d aftllu_uidn     s                   like(adluid)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_stat          s              1  0
     d w_retu          s              1
     d w_ltxt          s             20
     d w_ldat          s               d   datfmt(*iso)
     d w_udat          s               d   datfmt(*iso)
      *
     d w_firm          s                   like(adhfir)
      *
      * Parametre
      *
     d p_navn          s                   like(adhnav)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
5.10 c*                  if        b2kode <> *blank
     c*                  exsr      posisjoner
     c*                  goto      b2tagb
     c*                  endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      afthl1_navn = b2navn
     c     afthl1_key    setll     afthl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   eval      b_forn = *off
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Kode
      *
5.10 c*                  if        b2kode <> *blank
     c*                  eval      w_seqe = *blank
     c*                  eval      nkeol1_kode = b2kode
     c*    nkeol1_key    setll     nkeol1
     c*                  goto      end_pos
     c*                  endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
5.10 c*                  eval      b2kode = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1navn = b2navn
     c                   eval      d1uidn = b1uidn
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Se logg
      *
     c                   if        b1valg = 7
     c                   call      'AF111R'
     c                   parm                    b2navn
     c                   parm                    b1uidn
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      afthlr_navn = b2navn
     c                   eval      afthlr_uidn = d1uidn
     c     afthlr_key    chain     afthlr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter logghodet
      *
     c                   eval      afthlu_navn = b2navn
     c                   eval      afthlu_uidn = d1uidn
     c     afthlu_key    chain     afthlur
     c                   delete    afthlur
      *
      * Sletter logglinjene
      *
     c                   eval      aftllu_navn = b2navn
     c                   eval      aftllu_uidn = d1uidn
     c     aftllu_key    setll     aftllu
     c     aftllu_key    reade     aftllu
     c                   dou       %eof
     c                   delete    aftllu
     c     aftllu_key    reade     aftllu
     c                   enddo
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c     afthl1_key    reade     afthl1
      *
     c                   if        %eof or
     c                             adhfir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1user = adhusr
     c                   eval      b1stat = adhsta
     c                   eval      b1kom1 = adhkom
     c                   eval      b1uidn = adhuid
     c                   eval      b1dato = %subdt(adhuid:*DAYS) * 10000 +
     c                             %subdt(adhuid:*MONTHS) * 100 +
     c                             (%subdt(adhuid:*YEARS) - 2000)
     c                   eval      b1time = %subdt(adhuid:*HOURS) * 10000 +
     c                             %subdt(adhuid:*MINUTES) * 100 +
     c                             %subdt(adhuid:*SECONDS)
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     afthl1_key    setgt     afthl1
     c     afthl1_key    readpe    afthl1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c     afthl1_key    readpe    afthl1
      *
     c                   if        %eof or
     c                             adhfir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   enddo
      *
     c                   if        %eof
     c     afthl1_key    setll     afthl1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_navn
      *
      * Key for posisjonering
      *
     c     afthl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    afthl1_navn
      *
      * Key for les
      *
     c     afthlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    afthlr_navn
     c                   kfld                    afthlr_uidn
      *
      * Key for les
      *
     c     afthlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    afthlu_navn
     c                   kfld                    afthlu_uidn
      *
      * Key for les
      *
     c     aftllu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    aftllu_navn
     c                   kfld                    aftllu_uidn
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      *
      *
     c                   eval      b2navn = p_navn
      *
      * Initierer skjermbildet
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      afthl1_navn = b2navn
     c     afthl1_key    setll     afthl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
