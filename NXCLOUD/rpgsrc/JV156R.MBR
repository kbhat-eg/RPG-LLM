     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JV156R
      * Beskrivelse . . : Vare, bytting av varetype
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       151003 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvarl9    if   e           k disk    rename(jvarpfr:jvarl9r)
     fjvarlg    if   e           k disk    rename(jvarpfr:jvarlgr)
     fjvdtlu    uf a e           k disk    rename(jvdtpfr:jvdtlur)
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_ldor          s                   like(jvldor)
     d p_ogrp          s                   like(jvogrp)
     d p_hgrp          s                   like(jvhgrp)
     d p_ugrp          s                   like(jvugrp)
     d p_modn          s                   like(jvmodn)
     d p_skod          s                   like(jvdsko)
      *
      * Definering av variable i nøkler
      *
     d jvarl9_ldor     s                   like(jvldor)
     d jvarl9_ogrp     s                   like(jvogrp)
     d jvarl9_hgrp     s                   like(jvhgrp)
     d jvarl9_ugrp     s                   like(jvugrp)
     d jvarlg_modn     s                   like(jvmodn)
     d jvdtlu_dvar     s                   like(jvdvar)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Kaller subruiner for oppdatering av varetype
      *
     c                   select
     c                   when      p_ldor <> 0
     c                   exsr      behandl_ldor
     c                   other
     c                   exsr      behandl_modn
     c                   endsl
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av varer for leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_ldor  begsr
      *
     c                   eval      jvarl9_ldor = p_ldor
     c                   eval      jvarl9_ogrp = p_ogrp
     c                   eval      jvarl9_hgrp = p_hgrp
     c                   eval      jvarl9_ugrp = p_ugrp
      *
     c                   select
     c                   when      p_ugrp <> 0
     c     jvarl9_key1   setll     jvarl9
     c     jvarl9_key1   reade     jvarl9
     c                   when      p_hgrp <> 0
     c     jvarl9_key2   setll     jvarl9
     c     jvarl9_key2   reade     jvarl9
     c                   when      p_ogrp <> 0
     c     jvarl9_key3   setll     jvarl9
     c     jvarl9_key3   reade     jvarl9
     c                   other
     c     jvarl9_key4   setll     jvarl9
     c     jvarl9_key4   reade     jvarl9
     c                   endsl
      *
     c                   dow       not %eof
      *
     c                   if        p_modn = 0 or
     c                             p_modn <> 0 and
     c                             p_modn = jvmodn
     c                   exsr      oppdater
     c                   endif
      *
     c                   select
     c                   when      p_ugrp <> 0
     c     jvarl9_key1   reade     jvarl9
     c                   when      p_hgrp <> 0
     c     jvarl9_key2   reade     jvarl9
     c                   when      p_ogrp <> 0
     c     jvarl9_key3   reade     jvarl9
     c                   other
     c     jvarl9_key4   reade     jvarl9
     c                   endsl
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av varer for modul
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_modn  begsr
      *
     c                   eval      jvarlg_modn = p_modn
     c     jvarlg_key    setll     jvarlg
     c     jvarlg_key    reade     jvarlg
     c                   dow       not %eof
      *
     c                   if        p_ldor = 0 or
     c                             p_ldor <> 0 and
     c                             p_ldor = jvldor
     c                   exsr      oppdater
     c                   endif
      *
     c     jvarlg_key    reade     jvarlg
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av varetype på vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
     c                   clear                   jvdtlur
      *
     c                   eval      jvdtlu_dvar = jvvare
     c     jvdtlu_key    chain     jvdtlur
      *
     c                   eval      jvdsko = p_skod
      *
     c                   if        %found
     c                   update    jvdtlur
     c                   else
     c                   eval      jvdfir = w_firm
     c                   eval      jvdvar = jvvare
     c                   eval      jvdhko = 1
     c                   write     jvdtlur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
     c                   parm                    p_ogrp
     c                   parm                    p_hgrp
     c                   parm                    p_ugrp
     c                   parm                    p_modn
     c                   parm                    p_skod
      *
      * Key for les på vare m/leverandør
      *
     c     jvarl9_key1   klist
     c                   kfld                    jvarl9_ldor
     c                   kfld                    jvarl9_ogrp
     c                   kfld                    jvarl9_hgrp
     c                   kfld                    jvarl9_ugrp
      *
     c     jvarl9_key2   klist
     c                   kfld                    jvarl9_ldor
     c                   kfld                    jvarl9_ogrp
     c                   kfld                    jvarl9_hgrp
      *
     c     jvarl9_key3   klist
     c                   kfld                    jvarl9_ldor
     c                   kfld                    jvarl9_ogrp
      *
     c     jvarl9_key4   klist
     c                   kfld                    jvarl9_ldor
      *
      * Key for les på vare m/modulnr
      *
     c     jvarlg_key    klist
     c                   kfld                    jvarlg_modn
      *
      * Key for oppdatering av vare-detalj
      *
     c     jvdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jvdtlu_dvar
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
