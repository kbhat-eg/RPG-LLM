     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : RY763R
      * Beskrivelse . . : FIX: Leser Norgoskalkyle og oppretter påslag
      *
      *                   Varer innefor varegruppe med 8% rabatt merkes
      *                   med priskode 'N' mens varer med 5% rabatt merkes
      *                   med priskode 'B'
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       060905 HKO  Nytt program
5.70  * PRGR  211108 BOF  Utvider med prisgruppe
      *
8.01  *PRG2   160622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjrvapf    if   e           k disk
      *
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fjvarlc    if   e           k disk    rename(jvarpfr:jvarlcr)
     fjpfal1    if   e           k disk    rename(jpfapfr:jpfal1r)
     fjpfalu    uf a e           k disk    rename(jpfapfr:jpfalur)
     fjvdtlu    uf   e           k disk    rename(jvdtpfr:jvdtlur)
      *
      * Definering av LDA
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av recordbeskrivelse - Norgros kalkyle
      *
     d                 ds
     d d_inpu                        40
     d  d_vgrp                        7    overlay(d_inpu:1)                    Varegruppe
     d   d_ogrp                       2  0 overlay(d_vgrp:1)                    Varegruppe
     d   d_hgrp                       2  0 overlay(d_vgrp:3)                    Varegruppe
     d   d_ugrp                       3  0 overlay(d_vgrp:5)                    Varegruppe
     d  d_modn_alf                    8    overlay(d_inpu:8)                    EAN-nr
     d   d_modn                       8  0 overlay(d_modn_alf:1)                EAN-nr
     d  d_raba                        2    overlay(d_inpu:17)                   Varegruppetekst
     d  d_safa                       13    overlay(d_inpu:21)                   Varenr
     d   d_safa_hel                   6  0 overlay(d_safa:1)
     d   d_safa_des                   6  6 overlay(d_safa:8)
      *
      * Definering av variable i nøkler
      *
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d jvarlc_ogrp     s                   like(jvogrp)
     d jvarlc_hgrp     s                   like(jvhgrp)
     d jvarlc_ugrp     s                   like(jvugrp)
5.70 d jpfal1_prgr     s                   like(jcprgr)
     d jpfal1_ogrp     s                   like(jcogrp)
     d jpfal1_hgrp     s                   like(jchgrp)
     d jpfal1_ugrp     s                   like(jcugrp)
     d jpfal1_ldor     s                   like(jcldor)
     d jpfal1_modn     s                   like(jcmodn)
     d jpfal1_vare     s                   like(jcvare)
     d jpfal1_kpri     s                   like(jckpri)
     d jvdtlu_dvar     s                   like(jvdvar)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
      *
     d w_kpri          s              1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser Norgros fil
      *
     c                   read      jrvapf
     c                   dow       not %eof
      *
     c                   exsr      oppdater
      *
     c                   read      jrvapf
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av varerecord
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
     c                   eval      d_inpu = jrvrad
      *
     c                   eval      d_vgrp = %xlate(' ':'0':d_vgrp)
     c                   eval      d_safa = %xlate(' ':'0':d_safa)
      *
     c                   eval      d_modn_alf = %xlate(' ':'0':d_modn_alf)
      *
     c                   select
     c                   when      d_raba = ' 5'
     c                   eval      w_kpri = 'B'
     c                   when      d_raba = ' 8'
     c                   eval      w_kpri = 'N'
     c                   other
     c                   eval      w_kpri = *blank
     c                   endsl
      *
     c                   eval      vugrl1_uogr = d_ogrp
     c                   eval      vugrl1_uhgr = d_hgrp
     c                   eval      vugrl1_uugr = d_ugrp
     c     vugrl1_key    chain     vugrl1
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
5.70 c                   eval      jpfal1_prgr = *blank
     c                   eval      jpfal1_ogrp = d_ogrp
     c                   eval      jpfal1_hgrp = d_hgrp
     c                   eval      jpfal1_ugrp = d_ugrp
     c                   eval      jpfal1_ldor = 0
     c                   eval      jpfal1_modn = 0
     c                   eval      jpfal1_vare = *blank
     c                   eval      jpfal1_kpri = w_kpri
     c     jpfal1_key    chain     jpfal1
      *
     c                   if        not %found or
     c                             %found and
     c                             d_modn <> 0 and
     c                             (d_safa_hel + d_safa_des) <> jcsafa
      *
     c                   clear                   jpfalur
      *
     c                   eval      jcfirm = w_firm
5.70 c                   eval      jcprgr = *blank
     c                   eval      jcogrp = d_ogrp
     c                   eval      jchgrp = d_hgrp
     c                   eval      jcugrp = d_ugrp
     c                   eval      jckpri = w_kpri
     c                   eval      jckofa = 1
     c                   eval      jcsafa = d_safa_hel + d_safa_des
      *
     c                   if        %found(jpfal1)
     c                   eval      jcmodn = d_modn
     c                   endif
      *
     c                   time                    jcodat
     c                   time                    jcedat
     c                   time                    jcetim
      *
     c                   write     jpfalur
      *
      * Legger inn priskode på varer
      *
     c                   if        d_raba = ' 5' or
     c                             d_raba = ' 8'
     c                   eval      jvarlc_ogrp = d_ogrp
     c                   eval      jvarlc_hgrp = d_hgrp
     c                   eval      jvarlc_ugrp = d_ugrp
     c     jvarlc_key    setll     jvarlc
     c     jvarlc_key    reade     jvarlc
     c                   dow       not %eof
      *
     c                   if        not %found(jpfal1) or
     c                             %found(jpfal1) and
     c                             jvmodn = d_modn
     c                   eval      jvdtlu_dvar = jvvare
     c     jvdtlu_key    chain     jvdtlur
     c                   eval      jvdpko = w_kpri
     c                   update    jvdtlur
     c                   endif
      *
     c     jvarlc_key    reade     jvarlc
     c                   enddo
     c                   endif
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på varegruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for les på vare
      *
     c     jvarlc_key    klist
     c                   kfld                    jvarlc_ogrp
     c                   kfld                    jvarlc_hgrp
     c                   kfld                    jvarlc_ugrp
      *
      * Key for oppslag på påslag
      *
     c     jpfal1_key    klist
     c                   kfld                    w_firm
5.70 c                   kfld                    jpfal1_prgr
     c                   kfld                    jpfal1_ogrp
     c                   kfld                    jpfal1_hgrp
     c                   kfld                    jpfal1_ugrp
     c                   kfld                    jpfal1_ldor
     c                   kfld                    jpfal1_modn
     c                   kfld                    jpfal1_vare
     c                   kfld                    jpfal1_kpri
      *
      * Key for oppslag på vare-detalj
      *
     c     jvdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jvdtlu_dvar
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
