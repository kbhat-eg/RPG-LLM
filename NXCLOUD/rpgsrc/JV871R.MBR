     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JV871R
      * Beskrivelse . . : Ved scanning av varer i systemene og varen ikke
      *                   finnes EVR, så sjekkes VA - hvis finnes lagres.
      *                   hvis den ikke finnes så lagres med kode 2
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       011107 bof  Nytt program
5.61  *       080109 bof  Justering på utlegging av data
6.10  *       260809 bof  endringer vedr. eann opprydding.
6.11  *       280909 bof  Endret fra jvpkpf til jvpppf på ean-info
6.12  *       160610 bof  Justert noe logikk i forb. av les
6.12  *                   Tatt bort søk i JEANPF, dette er ikke i bruk
6.14  *       010311 bof  Fjernet bug vedr. wsid
6.30  *       250414 bof  Fjernet jvpp, erstattet med jvpkpf
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvsclu    uf a e           k disk    rename(jvscpfr:jvsclur)
     fjvscl2    if   e           k disk    rename(jvscpfr:jvscl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjvarl4    if   e           k disk    rename(jvarpfr:jvarl4r)
6.30 fjvpkl3    if   e           k disk    rename(jvpkpfr:jvpkl3r)
      *
     fjv871d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_scan          s             26
5.61 d p_text          s             30
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
5.61 d l_filg                931    933
      *
      * Datastruktur - EAN128
      *
     d                 ds
     d d_e128                        26
     d  d_fast                        2  0 overlay(d_e128)
6.12 d  d_eann                       14  0 overlay(d_e128:3)
     d  d_kode                        4  0 overlay(d_e128:17)
     d  d_anta                        6  2 overlay(d_e128:21)
      *
      * Definering av variabler i nøkler
      *
     d jvarl1_vare     s                   like(jvvare)
     d jvarl4_lvar     s                   like(jvlvar)
     d jvsclu_cean     s                   like(jscean)
     d jvsclu_csca     s                   like(jscsca)
6.30 d jvpkl3_gtin     s                   like(jwgtin)
     d jvscl2_ctel     s                   like(jsctel)
      *
      * Definering av variable
      *
     d w_scan          s                   like(p_scan)
5.61 d w_text          s                   like(p_text)
     d w_leng          s              2  0
     d w_posi          s              2  0
     d w_vare          s             15
     d w_eann          s             14  0
     d w_eanx          s             14
     d x               s              1  0
     d w_ctel          s                   like(jsctel)
      *
      *
      * Definering av konstanter
      *
     d c_digi          c                   '0123456789 '
     d c_null          c                   '0000000000000'
      *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * wvisrn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1BLD  - D1 Skjermbilde for slette (Delete)
      * E1BLD  - E1 Skjermbilde for innlegging av tilleggsinformasjon
      * F1BLD  - F1 Skjermbilde for avslutting av ekspedisjon
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c                   exsr      scanning
      * Avslutt program
      *
     c     avslutt       tag
      *
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av scanning
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     scanning      begsr
      *
      * Identifisering av vare foregår etter følgende rekkefølge, hvor
      * lengden på varenummeret, og om varenummer er alfa eller numerisk
      * er avgjørende:
      *
      * 1) alfa:
      *      - Lev. varenummer (første 15 pos.)
      * 2) num + lengde = 26:
      *      - EAN-128
      * 3) num + lengde <= 8:
      *      - Varenr
      * 4) num + lengde <= 14:
      *      - EAN-nummer
      * 5) num + lengde < 26:
      *      - Lev. varenummer (første 15 pos.)
      *
     c                   eval      w_scan = %trim(p_scan)
      *
     c                   eval      w_leng = %len(%trim(w_scan))
      *
      * Sjekker om scannefelt inneholder alfa-verdi
      *
     c     c_digi        check     w_scan        w_posi
      *
      * Behandling dersom alfa
      *
     c                   if        w_posi > 0
      *
     c                   eval      jvarl4_lvar = w_scan
     c     jvarl4_key    chain     jvarl4
     c                   if        %found
     c                   movel     jvvare        w_vare
     c                   movel     jvean1        w_eann
     c                   exsr      oppd_scan
     c                   goto      end_scan
     c                   endif
      *
     c                   exsr      ikke_VA
      *
     c                   goto      end_scan
      *
     c                   endif
      *
      * Behandling dersom lengde = 26
      *
     c                   if        w_leng = 26
      *
6.12  * sjekker pakning
6.12 c                   eval      d_e128 = w_scan
6.30 c                   eval      jvpkl3_gtin = d_eann
6.30 c     jvpkl3_key    chain     jvpkl3
6.30 c                   if        %found(jvpkl3)
6.30 c                   eval      jvarl1_vare = jwvare
6.30 c     jvarl1_key    chain     jvarl1
6.30 c                   if        %found(jvarl1)
6.30 c                   movel     jvvare        w_vare
6.30 c                   movel     jvean1        w_eann
6.30 c                   exsr      oppd_scan
6.30 c                   goto      end_scan
6.30 c                   endif
6.30 c                   endif
      *
      *
     c                   exsr      ikke_VA
     c                   goto      end_scan
      *
     c                   endif
      *
      * Behandling dersom lengde <= 8
      *
     c                   if        w_leng <= 8
      *
     c                   eval      w_vare = %subst(c_null:1:8-w_leng) +
     c                                      w_scan
      *
     c                   eval      jvarl1_vare = w_vare
     c     jvarl1_key    chain     jvarl1
     c                   if        %found
     c                   movel     jvvare        w_vare
     c                   movel     jvean1        w_eann
     c                   exsr      oppd_scan
     c                   goto      end_scan
     c                   endif
      *
     c                   endif
      *
      * Behandling dersom lengde <= 14
      *
     c                   if        w_leng <= 14
      *
     c                   eval      w_eanx = %subst(c_null:1:14-w_leng) +
     c                                      w_scan
      *
      * sjekker pakning
6.30 c                   movel     w_eanx        jvpkl3_gtin
6.30 c     jvpkl3_key    chain     jvpkl3
6.30 c                   if        %found(jvpkl3)
6.30 c                   eval      jvarl1_vare = jwvare
     c     jvarl1_key    chain     jvarl1
     c                   if        %found
     c                   movel     jvvare        w_vare
     c                   movel     jvean1        w_eann
     c                   exsr      oppd_scan
     c                   goto      end_scan
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Behandling dersom lengde < 26 (samlepost)
      *
     c                   eval      jvarl4_lvar = w_scan
     c     jvarl4_key    chain     jvarl4
     c                   if        %found
     c                   movel     jvvare        w_vare
     c                   movel     jvean1        w_eann
     c                   exsr      oppd_scan
     c                   goto      end_scan
     c                   endif
      *
     c                   exsr      ikke_VA
      *
     c     end_scan      tag
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å oppdatere scanneregister
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_scan     begsr
      *
      * finner siste teller
      *
     c                   exsr      finn_teller
      *
5.61 c                   clear                   jvsclur
     c                   eval      jvsclu_cean = w_eann
5.61 c                   eval      jvsclu_csca = w_scan
5.61 c**                 eval      jvsclu_csca = *blank
     c     jvsclu_key    chain     jvsclu
     c                   if        not %found(jvsclu)
     c                   eval      jscean = w_eann
5.61 c                   eval      jscsca = w_scan
5.61 c**                 eval      jscsca = *blank
     c                   eval      jscsts = 0
     c                   eval      jsctel = w_ctel
5.61 c                   eval      jsctxt = w_text
5.61 c                   eval      jscfgr = l_filg
     c                   eval      jscwsd = l_wsid
     c                   time                    jscoda
     c                   time                    jsceda
     c                   time                    jsceti
     c                   eval      jsceus = l_user
     c                   write     jvsclur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å vise i bildet at scannet vare ikke finne i VA
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ikke_VA       begsr
      *
     c                   exfmt     g1bld
      *
     c*                  if        *inkc
     c*                  goto      end_g1bld
     c*                  endif
      *
      * finner siste teller
      *
     c                   exsr      finn_teller
      *
     c                   eval      jvsclu_cean = 0
     c                   eval      jvsclu_csca = w_scan
     c     jvsclu_key    chain     jvsclu
     c                   if        not %found(jvsclu)
     c                   eval      jscean = 0
     c                   eval      jscsca = w_scan
     c                   eval      jscsts = 2
     c                   eval      jsctel = w_ctel
     c                   eval      jsctxt = %trim(a1txt1) + %trim(a1txt2)
     c                   eval      jscwsd = l_wsid
     c                   time                    jscoda
     c                   time                    jsceda
     c                   time                    jsceti
     c                   eval      jsceus = l_user
     c                   eval      jscwsd = l_user
     c                   write     jvsclur
     c                   endif
      *
      *
     c     end_g1bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne siste teller
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_teller   begsr
      *
     c                   eval      w_ctel = 0
      *
     c                   eval      jvscl2_ctel = *hival
     c     jvscl2_key    setgt     jvscl2
     c                   readp     jvscl2
     c                   eval      w_ctel = jsctel
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_scan
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på vare m/lev.varenummer
      *
     c     jvarl4_key    klist
     c                   kfld                    jvarl4_lvar
      *
      * Key for oppslag på EAN
      *
6.30 c     jvpkl3_key    klist
6.30 c                   kfld                    jvpkl3_gtin
      *
      * Key for oppslag på scannereg
      *
     c     jvsclu_key    klist
     c                   kfld                    jvsclu_cean
     c                   kfld                    jvsclu_csca
      *
      * Key for les av teller
      *
     c     jvscl2_key    klist
     c                   kfld                    jvscl2_ctel
      * Henter firma, timestamp og kunde parameter
      *
     c                   eval      w_scan = p_scan
      *
     c                   endsr
