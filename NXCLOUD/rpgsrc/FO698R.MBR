      /TITLE  ASOFAK: Utskrift av lev.plan-liste
     h decedit('0,') datedit(*dmy.)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : FO698R
      * Beskrivelse . . : Utskrift av lev.plan-liste
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
3.13  * R3.13 110900 AMD  Dersom vare er hentet fra LVR settes det inn
3.13  *                   2 desimaler som standard (finnes jo ikke i EVR)
6.nu  * R6.30 280514 bsa  Programmet gjennomgått mtp nummerutvidelser.
      *
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffpsol1    ipe  e           k disk    rename(fpsopfr:fpsol1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffotxl1    if   e           k disk    rename(fotxpfr:fotxl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     faforl1    if   e           k disk    rename(aforpfr:aforl1r)
     ffo698p    o    e             printer
      *
      * Definering av parametere
      *
     d p_parm          s             64
      *
      * Definering av Local data area
      *
     d                uds
     d l_ruti                800    809
     d l_alin                856    858  0
     d l_wsid                901    910
     d l_user                        10
     d l_firm                944    946  0
      *
      * Definering av datastruktur for parameter inn
      *
     d                 ds
     d d_prec                        64
     d  d_otyp                        2    overlay(d_prec)
6.nu d  d_fonr                        8  0 overlay(d_prec:5)
6.nu d  d_tonr                        8  0 overlay(d_prec:13)
6.nu d  d_fkun                        6  0 overlay(d_prec:21)
6.nu d  d_tkun                        6  0 overlay(d_prec:27)
     d  d_fdat                       10d   datfmt(*iso)
6.nu d                                     overlay(d_prec:33)
     d  d_tdat                       10d   datfmt(*iso)
6.nu d                                     overlay(d_prec:43)
6.nu d  d_val1                        1  0 overlay(d_prec:53)
6.nu d  d_val2                        1  0 overlay(d_prec:54)
6.nu d  d_lmat                        2  0 overlay(d_prec:55)
      *
      * Definering av variable i nøkler
      *
     d w_firm          s                   like(fofirm)
     d w_kund          s                   like(fokund)
     d w_line          s                   like(fdline)
     d w_ltyp          s                   like(fdltyp)
     d w_numm          s                   like(fonumm)
     d w_otyp          s                   like(vaotyp)
     d w_ruti          s             10
     d w_suff          s                   like(fosuff)
     d w_vare          s                   like(vvvare)
      *
      * Definering av variable
      *
     d d1npri          s                   like(fdsapr)
     d fcycle          s              1  0
     d sanumm          s                   like(fdnumm)
     d utime           s              6  0
     d wltell          s              3  0
     d wltest          s              3  0
     d wwwsid          s                   like(fowsid)
     d w0kund          s                   like(faakun)
     d w1anta          s                   like(fdanta)
     d w1npri          s                   like(fdsapr)
     d w1rab1          s                   like(fdsapr)
     d w_retk          s              1
     d w_gtxt          s             20
      *
     ifpsol1r       01
     i                                          fkssrt        l4
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av formater                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede formater
      * - - - - - - - - - -
      *
      * A1HDR  - Heading A1 på rapport
      * D1DET  - Detail  D1 på rapport, Varelinje
      * E1DET  - Detail  E1 på rapport, Tekstlinje
      *
      *
      * Forklaring på variabler
      * - - - - - - - - - - - -
      *
      * D1ANT0 - Antall med 0 desimaler
      * D1ANT1 - Antall med 1 desimaler
      * D1ANT2 - Antall med 2 desimaler
      * D1ANT3 - Antall med 3 desimaler
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Styre-rutine                                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Initierings-rutine
      *
     c   01fcycle        caseq     *zero         xstrsr
     c                   endcs
      *
      * Ordrehode
      *
     c     fkskod        cabeq     'H'           tag01
      *
      * Varelinje
      *
     c     fkskod        cabeq     'V'           tag02
      *
      * Kommentar/
      * tekstlinje
      *
     c     fkskod        cabeq     'K'           tag03
      *
      * Ferdig
      *
     c                   goto      xslutt
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A1HDR Behandle heading A1                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     tag01         tag
      *
      * Null-stilling av
      * variabler
      *
     c                   eval      a1atxt = *blank
      *
      * Hent ordre-heading
      *
     c                   eval      w_numm = fksonr
     c                   eval      w_suff = fkssuf
     c     fohel1_key    chain     fohel1r                            61
      *
      * Snu dato
      *
     c                   eval      a1ldat = 0
      *
     c                   if        foldat <> *loval
     c     *dmy          move      foldat        a1ldat
     c                   endif
      *
      * Legg inn tilbudsnummer
      *
     c                   move      fonumm        a1numm
     c                   movel     '/'           a1suff
     c                   move      fosuff        a1suff
      *
      * Hente inn kunderegister
      *
     c                   eval      w_kund = fokund
     c     rkunl1_key    chain     rkunl1r                            64
     c                   eval      a1navn = rknavn
      *
      *  Hent tekst for avdeling
      *
     c                   call      'RS707R'
     c                   parm                    w_retk
     c                   parm                    foavde
     c                   parm                    w_gtxt
     c                   eval      a1atxt = w_gtxt
      *
      * Flytte øvrige felter
      *
     c                   move      fonavn        a2navn
     c                   move      foavde        a1avde
      *
      * Skrive heading
      *
     c                   if        fksalt <> 1
     c                   eval      wltell = 0
     c                   write     a1hdr
     c                   endif
      *
     c                   write     a1hdr1                               30
      *
      * Er det overflow ?
      *
     c                   if        fksalt = 1
     c     *in30         caseq     *on           xovrfl
     c                   endcs
     c                   endif
      *
     c                   goto      xslutt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * D1DET Behandle detail D1                                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     tag02         tag
      *
      * Nullstilling av variabler
      *
3.11 c                   eval      d1benr = *zero
     c                   eval      d1line = *zero
     c                   eval      d1vare = *blank
     c                   eval      d1tek1 = *blank
     c                   eval      d1enh1 = *blank
     c                   eval      d1ant0 = *zero
     c                   eval      d1ant1 = *zero
     c                   eval      d1ant2 = *zero
     c                   eval      d1ant3 = *zero
     c                   eval      *in44  = *off
     c                   eval      *in46  = *off
      *
     c                   eval      w_numm = fksonr
     c                   eval      w_suff = fkssuf
     c                   eval      w_line = fkslin
     c     fodtl1_key    chain     fodtl1r                            62
      *
      * Hent linje-type
      *
     c                   eval      w_ltyp = fdltyp
     c     vltyl1_key    chain     vltyl1r                            68
      *
      * Sjekker om tekstlinjer
      *
     c     valktx        cabeq     1             tag02a
      *
      * Oppslag mot vareregister
      *
     c                   eval      w_vare = fdvare
     c     vvarl1_key    chain     vvarl1r                            69
3.13  *
3.13  * Dersom vare er hentet fra LVR,
3.13  * sett inn  2 desimaler som std.
3.13  *
3.13 c                   if        fdlvre = 1
3.13 c                   eval      vvdesi = 2
3.13 c                   endif
      *
      * Test på antall desimaler
      *
     c                   eval      *in40 = vvdesi = 0
     c                   eval      *in41 = vvdesi = 1
     c                   eval      *in42 = vvdesi = 2
     c                   eval      *in43 = vvdesi = 3
     c                   if        *in40 = *on
     c                   eval      d1ant0 = fdanta
     c                   endif
     c                   if        *in41 = *on
     c                   eval      d1ant1 = fdanta
     c                   endif
     c                   if        *in42 = *on
     c                   eval      d1ant2 = fdanta
     c                   endif
     c                   if        *in43 = *on
     c                   eval      d1ant3 = fdanta
     c                   endif
      *
      * Varelinje
      *
3.11 c                   eval      d1benr = fdbenr
     c                   eval      d1line = fdline
     c                   eval      d1vare = fdvare
     c                   eval      d1tek1 = fdtek1
     c                   eval      d1enh1 = fdenh1
      *
      * Skriv varelinjer
      *
     c     tag02a        tag
     c                   if        valktx <> 1
     c                   eval      wltell = wltell + 1
     c                   write     d1det                                30
     c                   else
     c                   eval      d1tek1 = fdtek1
     c                   eval      d1tek2 = fdtek2
     c                   eval      *in44  = *on
     c                   eval      wltell = wltell + 1
     c                   write     d2det                                30
     c                   endif
      *
      * Er det overflow ?
      *
     c     *in30         caseq     *on           xovrfl
     c                   endcs
      *
     c                   goto      xslutt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Behandling av tekstlinjer                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     tag03         tag
      *
     c                   eval      w_numm = fksonr
     c                   eval      w_suff = fkssuf
     c                   eval      w_line = fkslin
     c     fotxl1_key    chain     fotxl1r                            63
      *
      * Gå ut dersom det
      * ikke er oppsamling,
      * og linje-nummer er
      * større enn 5000.
      * Da skal print fore-
      * gå tilslutt på L4.
      *
     c                   if        vaosam = 0
     c                   if        fkslin >= 5000
     c                   goto      xslutt
     c                   endif
     c                   endif
      *
     c                   movel     fttext        e1text
     c                   eval      wltell = wltell + 1
     c                   write     e1det                                30
      *
      * Er det overflow ?
      *
     c     *in30         caseq     *on           xovrfl
     c                   endcs
      *
     c     xslutt        tag
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * T1TOT Behandle total T1                                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      * Finne ut om det er
      * plass til å skrive
      * avslutning uten å
      * skifte til ny side
      * først.
      *
      * Trekker ut heading
      *
     cl4                 eval      wltest = l_alin - 24
      *
      * Trekker ut antall
      * linjer skrevet
      *
     cl4                 eval      wltest = wltest - wltell
      *
      * Trekker ut plassbehov
      * for avslutning
      *
     cl4                 eval      wltest = wltest - 10
      *
      * Skrive ny heading ?
      *
     cl4                 if        wltest < 0
     cl4                 if        fksalt <> 1
     cl4                 write     a1hdr
     cl4                 write     a1hdr1
     cl4                 eval      wltell = 0
     cl4                 endif
     cl4                 endif
      *
      * Skrive total
      *
     cl4                 write     t1tot                                30
      *
      * Skrive tekstlinjer
      * dersom det ikke er
      * oppsamling og linje
      * nummer er større enn
      * 5000.
      *
     cl4                 if        vaosam = 0                                            .....
     cl4                 eval      w_numm = fksonr                                           .
     cl4                 eval      w_suff = fkssuf                                           .
     cl4                 eval      w_line = 5000                                             .
     cl4   fotxl1_key    setll     fotxl1r                                                   .
     cl4                 read      fotxl1r                                63                 .
     cl4   *in63         cabeq     *on           tag99                                       .
      *                                                                 .
     cl4                 dow       w_numm = ftnumm                                       ... .
     cl4                 if        w_suff = ftsuff                                         . .
      *                                                               . .
     cl4                 movel     fttext        e1text                                    . .
     cl4                 eval      wltell = wltell + 1                                     . .
     cl4                 if        fksalt <> 1
     cl4                 write     e1det                                30                 . .
     cl4                 endif                                                             . .
      *                                                               . .
      * Er det overflow ?                                             . .
      *                                                               . .
     cl4                 if        *in30 = *on                                             . .
     cl4                 if        fksalt <> 1
     cl4                 write     a1hdr                                                   . .
     cl4                 write     a1hdr1                                                  . .
     cl4                 eval      wltell = 0                                              . .
     cl4                 endif                                                             . .
     cl4                 endif                                                             . .
      *                                                               . .
     cl4                 read      fotxl1r                                63               . .
     cl4   *in63         cabeq     *on           tag99                                     . .
     cl4                 endif                                                             . .
     cl4                 enddo                                                           ... .
     cl4                 endif                                                           .....
      *
     cl4   tag99         tag
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for overflow                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xovrfl        begsr
      *
     c                   write     a1hdr
      *
     c                   if        fksalt <> 1
     c                   write     a1hdr1
     c                   endif
      *
     c                   eval      wltell = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for oppstart                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xstrsr        begsr
      *
     c                   eval      fcycle = 1
      *
      * Legger inn firma i nøkkel
      *
     c                   eval      w_firm = fksfir
      *
      * Klargjøring dersom firma -
      * opplysninger skal printes ut
      *
     c     afirl1_key    chain     afirl1r                            66
     c                   move      aafnav        a1fnav
      *
      * Hente Kunde-tilbuds-nummer
      *
     c     fstsl1_key    chain     fstsl1r                            69
      *
     c                   if        *in69 = *off
     c                   eval      w0kund = faakun
     c                   endif
      *
      * Slår opp ordretype
      *
     c                   eval      w_otyp = fksoty
     c     votyl1_key    chain     votyl1r                            67
      *
      * Er ordretype negativ ?
      *
     c                   if        vaokre = '-'
     c                   eval      *in36  = *on
     c                   endif
      *
      * Legg over parameter til datastruktur
      *
     c                   eval      d_prec = p_parm
     c                   eval      a1otyp = d_otyp
     c                   eval      a1lmat = d_lmat
      *
      * Skrive heading
      *
     c                   if        fksalt = 1
     c                   eval      wltell = 0
     c                   write     a1hdr
     c                   endif
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Nullstilling av felter som ikke kan redefineres
      *
     c                   eval      w_ruti = *blank
     c                   eval      wltell = *zero
     c                   eval      wltest = *zero
      *
      * Key for Heading-file
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for Detalj-file
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
     c                   kfld                    w_line
      *
      * Key for Tekst-linjer
      *
     c     fotxl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
     c                   kfld                    w_line
      *
      * Key for kunderegister
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_kund
      *
      * Key for firmaregister
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for ordretyper
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_otyp
      *
      * Key for linjetyper
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ltyp
      *
      * Key for statusregister
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for formular-register
      *
     c     aforl1_key    klist
     c                   kfld                    w_ruti
      *
      * Key for vare-register
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
      * Legger inn arb.stasjon og bruker i faste felter
      *
     c                   move      l_user        a1user
     c                   move      l_wsid        a1wsid
     c                   move      udate         a1date
     c                   time                    utime
     c                   move      utime         a1time
      *
      * Hente inn rutinenavn
      * og meldingstekst
      *
     c                   eval      w_ruti = l_ruti
     c     aforl1_key    chain     aforl1r                            69
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_parm
      *
     c                   endsr
