     H/TITLE  ASSRVL Spørrevindu m/vedlikehold
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * System  . . . . : VA                                            *
      * Program . . . . : JA201R                                        *
      * Beskrivelse . . : Oppretting av avrundingsregler                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      *                   Nytt program Jan. - 2014                      *
7.xx  * 7.00  010816 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KC = F3  = Exit                                           *
      *       KE = F5  = Forny                                          *
      *       KL = F12 = Annuller                                       *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       10 = Roll                                                 *
      *       12 = Sfldsp                                               *
      *       13 = Sfldspctl                                            *
      *       14 = Sflclr                                               *
      *       15 = Sflend                                               *
      *       16 = Readc 16 on, no more records                         *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     Fjav2l1    if   e           k disk    rename(jav2pfr:jav2l1r)
     Fjav2l2    if   e           k disk    rename(jav2pfr:jav2l2r)
     Fjav2lu    uf a e           k disk    rename(jav2pfr:jav2lur)
     Fja201d    cf   e             workstn sfile(b1wsf:srrn01)
      *
     D MLD             S             11    DIM(4) CTDATA PERRCD(1)
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av felter                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      *     nn - er 01 for subfile 01,
      *             02 for subfile 02,
      *             03 for subfile 03, o.s.v.
      * SRRNnn - Relativt recordnummer i subfile
      * SPGEnn - Sidestørrelse i subfile (page)
      * SFRNnn - Første recordnummer på siden
      * SSRNnn - Siste  recordnummer på siden
      * SVRNnn - Vis den side som inneholder denne record.
      *
      * Forklaring på spesielle felter
      * som benyttes ved windows.
      * - - - - - - - - - - - - - - - - -
      *
      * WACTLI - Actual line, angir startlinje for vindu.
      * WACTPO - Actual posision, angir startposisjon for vindu.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * b1WFL  - b1 Subfile - vindu for valg av post
      * B2WCT  - B2 Subfile - vindu control record
      * B2CMD  - B2 Vindu for CMD-taster
      * C2WIN  - C2 Vindu for endre vise opprette
      * D1WIN  - D1 Vindu for slette (Delete)
      * ASUME  -    Record for å styre at vindu legges over
      *             eksisterende data.
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * B2WCT Behandle åpningsbildet med subfile                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Send Alt
      *
     C     B2TAGA        TAG
     C                   z-add     4             WACTLI
     C                   z-add     26            WACTPO
     C                   write     B2CMD
      *
      * Bare data
      *
     C     B2TAGB        TAG
     C                   exsr      XDSP01
      *
      * Avslutt program
      *
     C     *INKC         cabeq     *ON           XSLUTT
      *
      * Forny
      *
     C     *INKE         ifeq      *ON
     C     SFRN01        ifne      *zero
     C     SFRN01        chain     b1WSF                              60
     C                   end
     C                   move      b1regl        wwregl
     C                   move      b1rtxt        wwrtxt
      *                                                                 .
     C     WSEQEN        ifeq      'A'
     C     jav2l2_key    setll     jav2l2
     C                   else
     C     jav2l1_key    setll     jav2l1
     C                   end
      *                                                                 .
     C                   exsr      XCLR01
     C                   exsr      XCRT01
     C                   goto      B2TAGB
     C                   endif
      *
      * Ny post, men
      * konto mangler
      *
     C     *INKF         ifeq      *ON
     C     b2regl        ifeq      *zero
     C                   move      *ON           *IN31
     C                   goto      B2TAGB
     C                   end
     C                   endif
      *
      * Ny post, men
      * konto finnes
      * allerede
      *
     C     *INKF         ifeq      *ON
     C                   move      b2regl        wwregl
     C     jav2l1_key    chain     jav2l1                             60
     C     *IN60         ifeq      *OFF
     C                   move      *ON           *IN32
     C                   goto      B2TAGB
     C                   end
     C                   endif
      *
      * Opprette ny
      *
     C     *INKF         ifeq      *ON
     C                   move      b2regl        c1regl
     C                   move      b2regl        wwregl
     C                   exsr      XC2WIN
      *  sven
     C                   move      c1regl        wwregl
     C     jav2l1_key    setll     jav2l1
     C                   exsr      XCLR01
     C                   exsr      XCRT01
     C                   goto      B2TAGA
     C                   endif
      *
      * Roll
      *
     C     *IN10         ifeq      *ON
     C                   exsr      XCRT01
     C                   goto      B2TAGB
     C                   endif
      *
      * Posisjoner
      * startverdi
      * på regel
      *
     C     b2regl        ifne      *zero
     C                   move      ' '           WSEQEN
     C                   move      b2regl        wwregl
     C     jav2l1_key    setll     jav2l1
     C                   exsr      XCLR01
     C                   exsr      XCRT01
     C                   move      *zero         b2regl
     C                   goto      B2TAGB
     C                   endif
      *
      * Posisjoner
      * startverdi
      * på alfafelt
      *
     C     b2rtxt        ifne      *blank
     C                   move      'A'           WSEQEN
     C                   move      *zero         wwregl
     C                   move      b2rtxt        wwrtxt
     C     jav2l2_key    setll     jav2l2
     C                   exsr      XCLR01
     C                   exsr      XCRT01
     C                   move      *zero         b2regl
     C                   move      *blank        b2rtxt
     C                   goto      B2TAGB
     C                   endif
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Behandling av subfile                                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Sjekk om valg
      * er foretatt i
      * subfile
      *
     C                   DO        SSRN01
     C                   READC     b1WSF                                  16
     C     *IN16         ifeq      *ON
     C                   LEAVE
     C                   endif
      *                                                                 .
      * Hente post                                                      .
      *                                                                 .
     C     b1VALG        ifeq      '1'
     C                   move      b1regl        paregel
     C                   move      b1rtxt        patext
     C                   goto      XSLUTT
     C                   endif
      *                                                                 .
      * endre post                                                      .
      *                                                                 .
     C     b1VALG        ifeq      '2'
     C                   move      b1regl        c1regl
     C                   move      b1regl        wwregl
     C                   exsr      XC2WIN
     C                   ITER
     C                   endif
      *                                                                 .
      * Vise post                                                       .
      *                                                                 .
     C     b1VALG        ifeq      '5'
     C                   move      *ON           *IN30
     C                   move      b1regl        c1regl
     C                   move      b1regl        wwregl
     C                   exsr      XC2WIN
     C                   ITER
     C                   endif
      *                                                                 .
     C                   endDO
      *
     C                   goto      B2TAGA
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * C1BLD Behandle åpningsbildet for ny post                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     C1TAGA        TAG
     C                   EXFMT     C2WIN
      *
     C     *INKC         cabeq     *ON           B2TAGA
     C     *INKL         cabeq     *ON           B2TAGA
      *
     C                   move      c1regl        wwregl
      *
     C                   exsr      XC2WIN
     C                   goto      C1TAGA
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XSLUTT        TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * C2WIN Behandle bilde for ny - endring - vise                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XC2WIN        BEGSR
     C     jav2l1_key    chain     jav2l1                             60
      *
     C     *IN60         ifeq      *OFF
     C                   moveL     MLD(2)        C2NYEN
     C                   move      ja2txt        c2rtxt
     C                   else
     C                   moveL     MLD(1)        C2NYEN
     C                   move      *blank        c2rtxt
     C                   endif
      *
     C     *IN30         ifeq      *ON
     C                   moveL     MLD(3)        C2NYEN
     C                   endif
      *
     C                   z-add     7             WACTLI
     C                   z-add     20            WACTPO
     C                   EXFMT     C2WIN
      *
     C     *INKC         cabeq     *ON           C2TAG1
     C     *INKL         cabeq     *ON           C2TAG1
     C     *IN30         cabeq     *ON           C2TAG1
      *
     C     jav2l1_key    chain     jav2lu                             60
      *
     C                   move      c2rtxt        ja2txt
      *
     C     *IN60         ifeq      *OFF
     C                   UPDATE    jav2lur
     C                   else
     C                   move      wwfirm        ja2fir
     C                   move      c1regl        ja2rgl
     C                   write     jav2lur
     C                   endif
      *
      * Oppdater subfile
      *
     C     b1VALG        ifeq      '2'
     C                   move      c1regl        b1regl
     C                   move      c2rtxt        b1rtxt
     C                   endif
      *
     C     C2TAG1        TAG
      *
     C     b1VALG        ifeq      '2'
     C     b1VALG        OREQ      '5'
     C                   move      SRRN01        SVRN01
     C                   move      *blank        b1VALG
     C                   UPDATE    b1WSF
     C                   endif
      *
     C                   move      *blank        b2rtxt
     C                   move      *zero         b2regl
     C                   move      *OFF          *IN30
      *
     C                   endSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Tøm subfile                                                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XCLR01        BEGSR
     C                   move      *ON           *IN14
     C                   write     B2WCT
     C                   move      *OFF          *IN14
      *
     C                   move      *OFF          *IN15
     C                   z-add     0             SRRN01
     C                   z-add     0             SFRN01
     C                   z-add     0             SSRN01
     C                   z-add     0             SPGE01
      *
     C                   endSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Fyll opp subfile                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -  - -*
      *
     C     XCRT01        BEGSR
     C                   ADD       7             SPGE01
     C                   z-add     SSRN01        SRRN01
      *
     C     SRRN01        DOUEQ     SPGE01
      *                                                                 .
     C     WSEQEN        ifeq      'A'
     C                   READ      jav2l2                                 15
     C                   else
     C                   READ      jav2l1                                 15
     C                   end
      *
     C                   if        *in15 = *off and
     C                             ja2fir <> wwfirm
     C                   move      *on           *in15
     C                   endif
      *
     C     *IN15         cabeq     '1'           Xend01
     C                   ADD       1             SRRN01
     C                   move      ja2rgl        b1regl
     C                   move      ja2txt        b1rtxt
     C                   write     b1WSF
     C                   endDO
      *
     C     Xend01        TAG
      *
     C     SRRN01        IFGT      SSRN01
     C     SSRN01        ADD       1             SFRN01
     C                   endif
      *
     C                   z-add     SRRN01        SSRN01
     C                   z-add     SFRN01        SVRN01
      *
     C                   endSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Display subfile                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XDSP01        BEGSR
      *
     C     SRRN01        ifne      0
     C                   move      *ON           *IN12
     C                   endif
      *
     C                   move      *ON           *IN13
     C                   EXFMT     B2WCT
     C                   move      *OFF          *IN12
     C                   move      *OFF          *IN13
      *
     C                   endSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     *INZSR        BEGSR
      *
     C                   z-add     0             SPGE01            3 0
     C                   z-add     0             SRRN01            4 0
     C                   z-add     0             WACTLI            2 0
     C                   z-add     0             WACTPO            3 0
     C                   move      *blank        WSEQEN            1
      *
     C     *LIKE         DEFINE    SRRN01        SFRN01
     C     *LIKE         DEFINE    SRRN01        SSRN01
     C     *LIKE         DEFINE    ja2fir        WWFIRM
     C     *LIKE         DEFINE    ja2fir        pafirm
     C     *LIKE         DEFINE    ja2txt        patext
     C     *LIKE         DEFINE    ja2rgl        wwregl
     C     *LIKE         DEFINE    ja2txt        wwrtxt
     C     *LIKE         DEFINE    ja2rgl        paregel
      *
      * Parameterliste
      *
     C     *ENTRY        PLIST
     C                   parm                    pafirm
     C                   parm                    paregel
     C                   parm                    patext
      *
     C                   move      pafirm        WWFIRM
      *
      * Key for file nummerisk
      *
     C     jav2l1_key    KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    wwregl
      *
      * Key for file alfabetisk
      *
     C     jav2l2_key    KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    wwrtxt
      *
     C                   move      pafirm        WWFIRM
     C     WSEQEN        ifeq      'A'
     C     jav2l2_key    setll     jav2l2
     C                   else
     C     jav2l1_key    setll     jav2l1
     C                   end
      *
     C                   exsr      XCRT01
      *
     C                   endSR
      *
**
  Ny post                  001
  endring                  002
  Vise                     003
* Slettet *                004
