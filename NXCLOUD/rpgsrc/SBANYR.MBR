      /title  Akkumulering av statistikk-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASSTAT
      * Program . . . . : SBANYR
      * Beskrivelse . . : Akkumumlering av statistikk-register
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * V5.30 221104 KOB  Korrigert feltlengder i DS
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner - COMP
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsstal1    if   e           k disk    rename(sstapfr:sstal1r)
      *
      * Definering av parameter (inn)
      *
     d                 ds
     d wsparm                        64
     d  wsfirm                        3  0 overlay(wsparm:01)
     d  wshhaa                        4  0 overlay(wsparm:04)
     d  wsvers                        2    overlay(wsparm:08)
     d  wsakod                       12    overlay(wsparm:10)
     d  wsaabb                        1    overlay(wsparm:22)
     d  wskode                       12    overlay(wsparm:23)
      *
      * Definering av parameter (opp)
      *
     d                 ds
     d wkparm                       128
     d  wkfirm                        3  0 overlay(wkparm:01)
     d  wkhhaa                        4  0 overlay(wkparm:04)
     d  wkkode                       12    overlay(wkparm:08)
     d  wkperi                        2  0 overlay(wkparm:20)
     d  wkavde                             like(sfsavd) overlay(wkparm:31)
     d  wklage                             like(sflage) overlay(wkparm:35)
     d  wkotyp                             like(sfotyp) overlay(wkparm:37)
     d  wkland                        3                 overlay(wkparm:39)
     d  wkdist                             like(sfdist) overlay(wkparm:42)
     d  wkselg                             like(sfselg) overlay(wkparm:44)
     d  wkkkat                             like(sfkkat) overlay(wkparm:48)
     d  wkkund                             like(sffkun) overlay(wkparm:52)
     d  wkogrp                             like(sfogrp) overlay(wkparm:58)
     d  wkhgrp                             like(sfhgrp) overlay(wkparm:60)
     d  wkugrp                             like(sfugrp) overlay(wkparm:62)
     d  wkvare                             like(sfvare) overlay(wkparm:65)
      *
     d  wkanta                       11  3              overlay(wkparm:81)
     d  wksumb                       11  2              overlay(wkparm:92)
     d  wksumk                       11  2              overlay(wkparm:103)
     d  wkrabb                       11  2              overlay(wkparm:114)
      *
      * Definering av variable i n√∏kler
      *
     d w_firm          s                   like(sffirm)
     d w_hhaa          s                   like(sfpaar)
      *
     d sstal1_binr     s                   like(sfbinr)
     d sstal1_line     s                   like(sfline)
     d sstal1_bida     s                   like(sfbida)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * XINIT  Initisiering av utskriften                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c                   move      *blank        wkparm
     c                   move      wskode        wkkode
     c                   move      wsfirm        wkfirm
     c                   move      wshhaa        wkhhaa
      *
      * Kaller opp subprogram for sletting
      *
     c                   call      'SBADER'
     c                   parm                    wkparm
      *
      * Legger inn verdier for start lesing statistikk-register
      *
     c                   eval      sstal1_binr = *zero
     c                   eval      sstal1_line = *zero
     c                   eval      sstal1_bida = *loval
     c     sstal1_key    setll     sstal1
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * XSTART Behandler linjer fra statistikk                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xstart        tag
     c                   read      sstal1                                 10
     c     *in10         cabeq     *on           xslutt
      *
      * Sjekk, Hopp ut dersom ikke riktig
      *
     c     sffirm        cabne     w_firm        xslutt
     c     sfpaar        cabne     w_hhaa        xslutt
      *
      * Legger inn verdier fra statistikk-record
      *
     c                   move      *blank        wkparm
     c                   move      wskode        wkkode
      *
     c                   move      sffirm        wkfirm
     c                   move      sfpaar        wkhhaa
     c                   move      sfpmnd        wkperi
      *
     c                   move      sfsavd        wkavde
     c                   move      sflage        wklage
     c                   move      sfotyp        wkotyp
     c                   move      sfland        wkland
     c                   move      sfdist        wkdist
     c                   move      sfselg        wkselg
     c                   move      sfkkat        wkkkat
     c                   move      sffkun        wkkund
     c                   move      sfogrp        wkogrp
     c                   move      sfhgrp        wkhgrp
     c                   move      sfugrp        wkugrp
     c                   move      sfvare        wkvare
      *
     c                   z-add     sfanta        wkanta
     c                   z-add     sfsumb        wksumb
     c                   z-add     sfsumk        wksumk
     c                   z-add     sfrab1        wkrabb
     c                   add       sfrab2        wkrabb
     c                   add       sfrabo        wkrabb
      *
      * Kaller opp subprogram for oppdatering
      *
     c                   call      'VS001R'
     c                   parm                    wkparm
      *
      * Les neste
      *
     c                   goto      xstart
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
      *
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     csr   *inzsr        begsr
      *
      * Key for lesing av statistikk-register
      *
     c     sstal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_hhaa
     c                   kfld                    sstal1_binr
     c                   kfld                    sstal1_line
     c                   kfld                    sstal1_bida
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     *entry        plist
     c                   parm                    dsparm           64
     c                   move      dsparm        wsparm
      *
     c                   move      wsfirm        w_firm
     c                   eval      w_hhaa      = wshhaa
      *
     csr                 endsr
