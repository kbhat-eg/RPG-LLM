     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . :
      * Program . . . . : JK703R
      * Beskrivelse . . : Leser alle logistikk-varer - kaller program
      *                   som justerer varekort i VA
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       071118 hko  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvkhl1    if   e           k disk    rename(jvkhpfr:jvkhl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fafpslr    if   e           k disk    rename(afpspfr:afpslrr)
      *
      * Local Data Area
      *
     d                uds
     d l_filg                931    933
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
     d jvarl1_vare     s                   like(jvvare)
     d afpslr_ufil     s                   like(l_filg)
     d afpslr_uide     s                   like(afuide)
      *
      * Definering av variable
      *
      *
     d w_firm          s                   like(jvkfir)
     d w_ldor          s                   like(jvldor)
     d w_ldox          s              6
     d w_vare          s                   like(jvvare)
     d w_dumm          s              1    inz('*')

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandler alle undervarer
      *
     c     jvkhl1_key    setll     jvkhl1
     c     jvkhl1_key    reade     jvkhl1
     c                   dow       not %eof(jvkhl1)
     c                   eval      jvarl1_vare = jvkvnr
     c     jvarl1_key    chain     jvarl1
     c                   if        %found(jvarl1)
     c                   call      'JK702R'
     c                   parm                    jvvare
     c                   parm                    w_ldor
     c                   parm                    w_dumm
     c                   endif
      *
     c     jvkhl1_key    reade     jvkhl1
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les på varekonvertering linje
      *
     c     jvkhl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på vare (VA)
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for propertiesfil
      *
     c     afpslr_key    klist
     c                   kfld                    afpslr_ufil
     c                   kfld                    afpslr_uide
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_ldox = *blank
     c                   eval      afpslr_ufil = l_filg
     c                   eval      afpslr_uide = 'LOGILEV   '
      *
      * Sjekk om byggdok eller cobuilder aktivering er på/av
      *
     c     afpslr_key    chain     afpslrr
     c                   if        %found(afpslr)
     c                   eval      w_ldox = afudss
     c                   endif
     c                   if        w_ldox = *blank
     c                   eval      w_ldox = '900099'
     c                   endif
     c                   move      w_ldox        w_ldor
      *
      *
     c                   endsr
