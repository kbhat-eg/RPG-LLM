     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -  *
      *                                                                  *
      *  System. . . . . : ASLAGR                                        *
      *  Program . . . . : LI300R                                        *
      *  Beskrivelse . . : Plukke pakksedler til godkjenning             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -  *
      *                                                                  *
      *  Spesialversjon. :                                               *
      *  Tilpasset for . :                                               *
      *                                                                  *
      *  Ref   Sign Dato    Endringsbeskrivelse                         .*
      *  ----       -----   ---------------------------------------------*
      *  7.00  bsa  300819  Nytt program                                 *
7.01  *  K000  bsa  051119  Hvis avvik mellom mottatt og bestilt settes  *
7.01  *  K000               koder for å håndtere dette videre.           *
7.02  *  K000  bsa  291119  Skal ikke legge ut samme bestillingsnummer   *
7.02  *  K000               flere ganger.                                *
7.03  *  K000  bsa  090120  Må også få med tekstlinjer.                  *
7.04  *  K000  bsa  070220  Endret måten å plukke linjer på. Fjernet     *
7.04  *  K000               bruken av LWVAPF.                            *
7.05  *  K000  bsa  170220  Justert måte å legge ut siste tekstlinjer    *
7.06  *  K000  bsa  170220  Må frigjøre post, hvis funnet                *
7.07  *  K000  bsa  260620  Parameter for å akseptere resting etter      *
7.07  *  K000               oppdatering av pakkseddel.                   *
7.08  *  K000  bsa  051020  Plukker tjenestevarer etter samme logikk     *
7.08  *  K000               som tekstlinjer.                             *
7.09  *  K000  bsa  131020  Mer logikk rundt tjenestevarer, omstrukt. av *
7.09  *  K000               subrutine.                                   *
      *                                                                  *
      * - - - - - - -  - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
      *
7.04 flbhoi2    uf   e           k disk    rename(lbhost:lbhoi2r)
7.04 flbhoi3    if   e           k disk    rename(lbhost:lbhoi3r)
     flpkoi3    uf   e           k disk    rename(lpkost:lpkoi3r)
     flpkliu    uf   e           k disk    rename(lpklst:lpkliur)
     flblii2    uf   e           k disk    rename(lblist:lblii2r)
     flphoi1    if   e           k disk    rename(lphost:lphoi1r)
     flphoiu    uf   e           k disk    rename(lphost:lphoiur)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flohelu    uf   e           k disk    rename(lohepfr:lohelur)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     flodtlr    if   e           k disk    rename(lodtpfr:lodtlrr)
7.07 flodtlu    uf   e           k disk    rename(lodtpfr:lodtlur)
      *
     flplolu    uf a e           k disk    rename(lplopfr:lplolur)
     flplol1    if   e           k disk    rename(lplopfr:lplol1r)
     flplllu    uf a e           k disk    rename(lpllpfr:lplllur)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     flusrl1    if   e           k disk    rename(lusrpfr:lusrl1r)
7.07 frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
7.07 flbreiu    uf a e           k disk    rename(lbrest:lbreiur)
7.08 fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
     fli300d    cf   e             workstn
      *
      * Definering av array's
      *
     d bnr             s              8  0 dim(20)
     d bsu             s              2  0 dim(20)
     d oty             s              2    dim(20)
     d ssc             s             18  0 dim(20)
      *
      *
      * Definering av variable i parametre
      *
     d p_type          s              4
     d p_mnum          s              8  0
     d p_aarr          s              4  0
     d p_paid          s             17
     d p_pver          s              3  0
     d p_orgi          s              5
7.07 d p_levr          s                   like(rllevr)
      *
      * Definering av variabler i nøkler
      *
     d lpkoi3_type     s                   like(potype)
     d lpkoi3_aarr     s                   like(poaarr)
     d lpkoi3_paid     s                   like(popaid)
     d lpkoi3_pver     s                   like(popver)
      *
     d lpkliu_type     s                   like(potype)
     d lpkliu_mnum     s                   like(pomnum)
     d lpkliu_aarr     s                   like(poaarr)
     d lpkliu_paid     s                   like(popaid)
     d lpkliu_pver     s                   like(popver)
     d lpkliu_fsni     s                   like(plfsni)
     d lpkliu_fniv     s                   like(plfniv)
      *
7.04 d lbhoi3_hpid     s                   like(pbhpid)
7.04 d lbhoi2_hpid     s                   like(pbhpid)
7.04 d lbhoi2_hpve     s                   like(pbhpve)
7.04 d lbhoi2_hnum     s                   like(pbhnum)
7.04 d lbhoi2_hsuf     s                   like(pbhsuf)
7.04 d lbhoi2_hssc     s                   like(pbhssc)
      *
     d lohelu_numm     s                   like(lonumm)
     d lohelu_suff     s                   like(losuff)
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
      *
     d lplolu_numm     s                   like(lkponr)
     d lplolu_suff     s                   like(lkpsuf)
     d lusrl1_user     s                   like(lbuser)
      *
     d lphoi1_type     s                   like(phtype)
     d lphoi1_aarr     s                   like(phaarr)
     d lphoi1_mnum     s                   like(phmnum)
     d lphoi1_paid     s                   like(phpaid)
     d lphoi1_pver     s                   like(phpver)
      *
     d lblii2_lpid     s                   like(pblpid)
     d lblii2_lpve     s                   like(pblpve)
     d lblii2_lnum     s                   like(pblnum)
     d lblii2_lsuf     s                   like(pblsuf)
      *
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d lodtl1_line     s                   like(ldline)
      *
7.07 d lodtlu_numm     s                   like(ldnumm)
7.07 d lodtlu_suff     s                   like(ldsuff)
7.07 d lodtlu_line     s                   like(ldline)
      *
     d lodtlr_numm     s                   like(ldnumm)
     d lodtlr_suff     s                   like(ldsuff)
     d lodtlr_line     s                   like(ldline)
      *
     d lplllu_lonr     s                   like(lklonr)
     d lplllu_lsuf     s                   like(lklsuf)
     d lplllu_llin     s                   like(lkllin)
      *
     d lplol1_lonr     s                   like(lkponr)
     d lplol1_lsuf     s                   like(lkpsuf)
     d votyl1_otyp     s                   like(vaotyp)
7.07 d rlevl1_levr     s                   like(rllevr)
7.07 d lbreiu_rnum     s                   like(pbrnum)
7.07 d lbreiu_rsuf     s                   like(pbrsuf)
7.07 d lbreiu_rlin     s                   like(pbrlin)
7.08 d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av øvrige variable
      *
     d w_on01          s              2
     d w_on02          s              2
     d w_on03          s              2
     d w_on04          s              2
     d w_on05          s              2
     d w_on06          s              2
     d w_firm          s                   like(lofirm)
     d w_valg          s              1  0
     d w_ipor          s              1
7.04 d w_line          s              4  0
7.04 d w_slin          s              4  0
7.07 d w_rest          s              1  0
7.07 d w_stat          s              1
7.07 d w_numa          s              8
7.07 d w_lina          s              4
7.07 d w_retu          s              1
7.07 d w_typd          s              4
7.07 d w_kom1          s             30
7.07 d w_kom2          s             30
      *
     d x               s              2  0
      *
     d b_best          s              1    inz(*off)
7.07 d b_slet          s              1    inz(*off)
      *
7.07 d                 ds
7.07 d hlrec                        128
7.07 d  hlvare                       15    overlay(hlrec)
7.07 d  hllage                        2  0 overlay(hlrec:16)
7.07 d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
7.07 d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
7.07 d  hlotyp                        2    overlay(hlrec:36)
7.07 d  hlltyp                        2    overlay(hlrec:38)
7.07 d  hlanta                       11  3 overlay(hlrec:40)
7.07 d  hlkopr                        9  2 overlay(hlrec:51)
7.07 d  hlrefr                       20    overlay(hlrec:60)
7.07 d  hlsyst                        1    overlay(hlrec:80)
7.07 d  hlaltk                        2    overlay(hlrec:81)
7.07 d  hlrest                        1    overlay(hlrec:83)
7.07 d  hltype                        1    overlay(hlrec:84)
7.07 d  hlenhe                        3    overlay(hlrec:85)
7.07 d  hlinlr                        1    overlay(hlrec:88)
7.07 d  hllety                        1    overlay(hlrec:89)
7.07 d  hlonum                        8  0 overlay(hlrec:90)
7.07 d  hlolin                        4  0 overlay(hlrec:98)
      *
     d                UDS
     d  l_wsid               901    910
     d  l_user               911    920
     d  l_firm               944    946  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  A1 Behandler plukking av EDI pakksedler                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c                   eval      x = 0
     c                   eval      bnr = *zeros
     c                   eval      bsu = *zeros
     c                   eval      oty = *blanks
     c                   eval      ssc = *zeros
     c                   eval      a1paid = p_paid
     c                   eval      a1pver = p_pver
      *
     c     a1tag1        tag
      *
      * Finner om det er en bestillingsreferanse - minst en av dem må være gyldig
      *
     c                   eval      b_best = *off
     c                   eval      lbhoi3_hpid = p_paid
7.04 c     lbhoi3_key    setll     lbhoi3
7.04 c     lbhoi3_key    reade     lbhoi3
     c                   dow       not %eof
     c                   if        pbhnum <> 0     and
     c                             pbhaar = p_aarr and
     c                             pbhsta <> '99'  and
     c                             pbhsta <> '90'
      *
      *  Sjekk om bestilling er gyldig
      *
     c                   eval      lohel1_numm = pbhnum
     c                   eval      lohel1_suff = pbhsuf
     c     lohel1_key    chain     lohel1
     c                   if        %found
      *
      *  Hent opplysninger fra ORDRETYPE-REGISTER
      *
     c                   eval      votyl1_otyp = lootyp
     c     votyl1_key    chain     votyl1
     c                   if        %found
     c                   if        vaoakk <> 1
     c                   eval      b_best = *off
     c                   else
7.02 c                   if        %lookup(pbhnum:bnr) = 0
     c                   eval      b_best = *on
     c                   eval      x = x +1
     c                   eval      bnr(x) = pbhnum
     c                   eval      bsu(x) = pbhsuf
     c                   if        pbhssc = 0
     c                   eval      ssc(x) = *hival
     c                   else
     c                   eval      ssc(x) = pbhssc
     c                   endif
     c                   eval      oty(x) = vaonxt
7.02 c                   endif
      *
      * Lagrer informasjon for "neste ordretype" sjekk
      * (Brukes ikke for øyeblikket)
      *
     c                   eval      w_on01 = vaon01
     c                   eval      w_on02 = vaon02
     c                   eval      w_on03 = vaon03
     c                   eval      w_on04 = vaon04
     c                   eval      w_on05 = vaon05
     c                   eval      w_on06 = vaon06
     c                   endif
     c                   endif
      *
     c                   endif
     c                   endif
7.04 c     lbhoi3_key    reade     lbhoi3
     c                   enddo
      *
      *  Legger inn opplysningene i skjermbildefeltene
      *
     c**                 eval      a1otyp = vaonxt
      *
     c                   if        b_best = *off
     c                   eval      *in31 = *on
     c                   endif
      *
      *  Send Alt
      *
     c                   write     a1bld
      *
      *  Bare data
      *
     c     a1tagb        tag
     c                   exfmt     a1bld
      *
      *  F3=Avslutt program
      *
     c                   if        *inkc = *on
     c                   goto      xslutt
     c                   endif
      *
      * Kan ikke gjøre noe hvis ikke gyldig bestilling
      *
     c                   if        b_best = *off
     c                   goto      a1tag1
     c                   endif
      *
      *  F4=Spørring
      *
     c                   if        *inkd = *on
      * Ordretype
     c*                  if        wactre = 'A1BLD' and
     c*                            wactfe = 'A1OTYP'
     c*                  call      'VA550R'
     c*                  parm                    w_firm
     c*                  parm                    a1otyp
     c*                  goto      a1tagb
     c*                  endif
      *
     c                   endif
      *
      * Sjekk om det skal testes på lov til utplukk
      *
     c     lusrl1_key    chain     lusrl1
     c                   if        %found
     c                   if        lbko10 = 0 and
     c                             vaoakk > 2
     c                   eval      *in35 = *on
     c                   goto      a1tagb
     c                   endif
     c                   endif
     c
      *
      * Sett sperre på EDI pakkseddelen, så den ikke kan plukkes flere ganger.
      *
     c     lphoi1_key    chain     lphoiur
     c                   if        %found
     c                   eval      phstat = '50'
     c                   time                    phedat
     c                   time                    phetim
     c                   eval      pheusr = l_user
     c                   update    lphoiur
     c                   endif
      *
      * Sett sperre på alle EDI pakkseddel/kolli, så de ikke kan plukkes flere ganger.
      *
     c     lpkoi3_key    setll     lpkoi3
     c     lpkoi3_key    reade     lpkoi3
     c                   dow       not %eof
     c                   eval      postat = '50'
     c                   time                    poedat
     c                   time                    poetim
     c                   eval      poeusr = l_user
     c                   update    lpkoi3r
      *
      * Sett sperre på alle underliggende kollilinjer, så de ikke kan plukkes fra andre.
      *
     c                   eval      lpkliu_fsni = pofsni
     c                   eval      lpkliu_fniv = pofniv
     c     lpkliu_key    setll     lpkliu
     c     lpkliu_key    reade     lpkliu
     c                   dow       not %eof
     c                   eval      plstat = '50'
     c                   time                    pledat
     c                   time                    pletim
     c                   eval      pleusr = l_user
     c                   update    lpkliur
     c     lpkliu_key    reade     lpkliu
     c                   enddo
      *
     c     lpkoi3_key    reade     lpkoi3
     c                   enddo
      *
      * Så looper vi de bestillingene som er gyldige, og skal plukkes/delplukkes.
7.04  * Dette vil bare være en bestilling, hvis ikke vil all videre behandling av
7.04  * EDI faktura osv gå helt i frø.
      *
     c     1             do        20            x
     c                   if        bnr(x) <> 0
     c                   eval      lohel1_numm = bnr(x)
     c                   eval      lohel1_suff = bsu(x)
     c                   eval      lohelu_numm = bnr(x)
     c                   eval      lohelu_suff = bsu(x)
     c                   eval      lonumm = bnr(x)
     c                   eval      losuff = bsu(x)
      *
      *  Oppdater BESTILLING-HODE-REGISTER, Bestillingen er til behandling
      *
     c                   call      'LO709R'
     c                   parm                    lonumm
     c                   parm                    losuff
      *
      * Sett "sperrekode" på bestilling
      *
     c     lohelu_key    chain     lohelur
     c                   if        %found
     c                   eval      lokode = 3
     c                   time                    loedat
     c                   time                    loetim
     c                   eval      loeusr = l_user
     c                   update    lohelur
     c                   endif
      *
      * Sett "sperrekode" på "kollibestilling"
      *
7.04 c                   eval      lbhoi2_hnum = bnr(x)
7.04 c                   eval      lbhoi2_hsuf = bsu(x)
     c                   eval      lbhoi2_hssc = 0
     c     lbhoi2_key    setll     lbhoi2
     c     lbhoi2_key2   reade     lbhoi2
7.04 c                   dow       not %eof
     c                   eval      pbhsta = '50'
     c                   time                    pbheda
     c                   time                    pbheti
     c                   eval      pbheus = l_user
     c                   update    lbhoi2r
7.04 c     lbhoi2_key2   reade     lbhoi2
7.04 c                   enddo
      *
      * Kjør utplukk avhengig av hvor vi kommer fra
      *
     c                   exsr      scan_li200
      *
      *  Kaller opp utskrift-program for å sette inn alternativ
      *  og om direktevalg skal benyttes
      *
     c                   eval      w_valg = 1
      *
     c                   call      'LI301R'
     c                   parm                    w_ipor
     c                   parm                    w_firm
     c                   parm                    lohelu_numm
     c                   parm                    lohelu_suff
     c                   parm                    w_valg
      *
      * Sette tilbake status
      *
     c     lohelu_key    chain     lohelur
     c                   if        %found
     c                   eval      lokode = 0
     c                   time                    loedat
     c                   time                    loetim
     c                   eval      loeusr = l_user
     c                   update    lohelur
     c                   endif
7.04  *
7.04  *  Oppdater at bestillingshode er varemottatt
7.04  *
7.04 c                   eval      lbhoi2_hnum = lohelu_numm
7.04 c                   eval      lbhoi2_hsuf = lohelu_suff
7.04 c                   eval      lbhoi2_hssc = 0
7.04 c     lbhoi2_key    setll     lbhoi2
7.04 c     lbhoi2_key2   reade     lbhoi2
7.04 c                   dow       not %eof
7.04 c                   eval      pbhsta = '90'
7.04 c                   time                    pbheda
7.04 c                   time                    pbheti
7.04 c                   eval      pbheus = l_user
7.04 c                   update    lbhoi2r
7.04 c     lbhoi2_key2   reade     lbhoi2
7.04 c                   enddo
      *
      *  Oppdater at linjene er varemottatt
      *
     c                   eval      lblii2_lnum = lohel1_numm
     c                   eval      lblii2_lsuf = lohel1_suff
     c     lblii2_key    setll     lblii2
     c     lblii2_key    reade     lblii2
     c                   dow       not %eof
     c                   eval      pblvan = pblpan
     c                   eval      pblven = pblpen
     c                   eval      pblsta = '90'
     c                   time                    pbleda
     c                   time                    pbleti
     c                   eval      pbleus = l_user
     c                   update    lblii2r
      *
     c     lblii2_key    reade     lblii2
     c                   enddo
      *
     c                   endif
     c                   enddo
      *
      * Setter statusen til ferdig varemottatt
      *
     c     lphoi1_key    chain     lphoiur
     c                   if        %found
     c                   eval      phstat = '90'
     c                   time                    phedat
     c                   time                    phetim
     c                   eval      pheusr = l_user
     c                   update    lphoiur
     c                   endif
      *
      * Sett sperre på alle EDI pakkseddel/kolli, så de ikke kan plukkes flere ganger.
      *
     c     lpkoi3_key    setll     lpkoi3
     c     lpkoi3_key    reade     lpkoi3
     c                   dow       not %eof
     c                   eval      postat = '90'
     c                   time                    poedat
     c                   time                    poetim
     c                   eval      poeusr = l_user
     c                   update    lpkoi3r
      *
      * Sett sperre på alle underliggende kollilinjer, så de ikke kan plukkes fra andre.
      *
     c                   eval      lpkliu_fsni = pofsni
     c                   eval      lpkliu_fniv = pofniv
     c     lpkliu_key    setll     lpkliu
     c     lpkliu_key    reade     lpkliu
     c                   dow       not %eof
     c                   eval      plstat = '90'
     c                   time                    pledat
     c                   time                    pletim
     c                   eval      pleusr = l_user
     c                   update    lpkliur
     c     lpkliu_key    reade     lpkliu
     c                   enddo
      *
     c     lpkoi3_key    reade     lpkoi3
     c                   enddo
      *
7.07  *
7.07  * Sjekk om bestilingslinje skal slettes pga restkode. NB Det er kun
7.07  * linjer som ikke skal restes som vil ligge her.
7.07  *
7.07 c                   eval      lbreiu_rnum = lohel1_numm
7.07 c                   eval      lbreiu_rsuf = lohel1_suff
7.07 c                   eval      lbreiu_rlin = 0
7.07 c     lbreiu_key    setll     lbreiu
7.07 c     lbreiu_key2   reade     lbreiu
7.07 c                   dow       not %eof
7.07 c                   eval      lodtlu_numm = lbreiu_rnum
7.07 c                   eval      lodtlu_suff = lbreiu_rsuf
7.07 c                   eval      lodtlu_line = pbrlin
7.07 c                   delete    lbreiur
7.07 c     lodtlu_key    chain     lodtlu
7.07 c                   if        %found
7.07 c                   if        laalag <> 0
7.07 c                   eval      ldanta = ldanta * -1
7.07 c                   exsr      oppdat_lager
7.07 c                   endif
7.07 c                   delete    lodtlur
7.07 c                   endif
7.07 c     lbreiu_key2   reade     lbreiu
7.07 c                   enddo
7.07  *
7.07  * Hvis bestillingen nå er "tom", kan vi slette den via standard
7.07  * sletteprogram.
7.07  *
7.07 c                   eval      b_slet = *on
7.07 c                   eval      lodtlr_numm = lohelu_numm
7.07 c                   eval      lodtlr_suff = lohelu_suff
7.07 c                   eval      lodtlr_line = 0
7.07 c     lodtlr_key    setll     lodtlr
7.07 c     lodtlr_key2   reade     lodtlr
7.07 c                   dow       not %eof
7.07 c                   if        ldvare <> *blanks
7.07 c                   eval      b_slet = *off
7.07 c                   endif
7.07 c     lodtlr_key2   reade     lodtlr
7.07 c                   enddo
7.07  *
7.07 c                   if        b_slet = *on
7.07 c                   eval      w_retu = *blank
7.07 c                   eval      w_typd = '*EDI'
7.07 c                   eval      w_kom1 = 'Slettet fra EDI pakkseddel mot'
7.07 c                   eval      w_kom2 = 'tak. Valg 10'
7.07 c                   call      'LO400R'
7.07 c                   parm                    w_retu
7.07 c                   parm                    w_firm
7.07 c                   parm                    lohelu_numm
7.07 c                   parm                    lohelu_suff
7.07 c                   parm                    w_typd
7.07 c                   parm                    w_kom1
7.07 c                   parm                    w_kom2
7.07 c                   endif
7.07  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for utplukk fra godkjenning av hele pakkseddel       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     scan_li200    begsr
7.04  *
7.04  * Finn siste linjenummer, så skal vi ta med evnt teskstlinjer som
7.04  * kommer etterpå.
7.04  *
7.04 c                   eval      w_line = 0
7.04 c                   eval      lodtlr_numm = lohel1_numm
7.04 c                   eval      lodtlr_suff = lohel1_suff
7.04 c                   eval      lodtlr_line = 9999
7.04 c     lodtlr_key    setll     lodtlr
7.04 c     lodtlr_key2   readpe    lodtlr
7.04  *
7.04 c                   dow       not %eof
7.04  *
7.09 c                   if        ldvare <> *blanks
7.09 c                   eval      vvarl1_vare = ldvare
7.09 c     vvarl1_key    chain     vvarl1
7.09 c                   if        %found
7.09 c                   if        vvvtyp = 'T'
7.09 c                   goto      neste
7.09 c                   endif
7.09 c                   endif
7.09 c                   endif
7.04  *
7.09 c                   if        ldltyp = 'TX'
7.09 c                   goto      neste
7.04 c                   endif
7.04  *
7.04 c                   eval      w_line = ldline
7.04 c                   leave
7.04  *
7.09 c     neste         tag
7.04  *
7.04 c     lodtlr_key2   readpe    lodtlr
7.04 c                   enddo
      *
      *  Legg ut linjene også
      *
     c                   eval      lblii2_lpid = phpaid
     c                   eval      lblii2_lpve = phpver
     c                   eval      lblii2_lnum = lohel1_numm
     c                   eval      lblii2_lsuf = lohel1_suff
     c     lblii2_key    setll     lblii2
     c     lblii2_key    reade     lblii2
     c                   dow       not %eof
      * Frigjør post
     c                   unlock    lblii2
      *
      * Legg inn verdier i UTPLUKK-LINJE-REGISTER
      *
     c                   eval      lodtl1_numm = pblnum
     c                   eval      lodtl1_suff = pblsuf
     c                   eval      lodtl1_line = pbllin
     c     lodtl1_key    chain     lodtl1
7.04 c                   if        %found
7.09  *
7.09  * Legg ut arbeidsregister for pakkseddel, vis resting ikke skal skje
7.09  *
7.09 c                   if        a1rest = 0
7.09 c                   exsr      sub_rest
7.09 c                   endif
      *
7.04 c                   if        ldvare = *blank
7.04 c                   eval      pblpan = 1
7.04 c                   else
7.04 c                   eval      w_slin = ldline
7.04 c                   endif
      *
     c                   eval      lplllu_lonr = ldnumm
     c                   eval      lplllu_lsuf = ldsuff
     c                   eval      lplllu_llin = ldline
     c     lplllu_key    chain     lplllu
     c                   if        not %found
     c                   eval      lklfir = w_firm
     c                   eval      lklonr = ldnumm
     c                   eval      lklsuf = ldsuff
     c                   eval      lkllin = ldline
     c                   eval      lklkod = '1'
      * Antall hentes fra EDI pakkseddel
7.04 c                   eval      lklant = pblpan
7.04 c                   if        ldvare = *blank
7.04 c                   eval      lklant = 0
7.04 c                   endif
7.04 c                   write     lplllur
7.04 c                   else
      * Antall hentes fra EDI pakkseddel
7.04 c                   eval      lklant = lklant + pblpan
7.04 c                   update    lplllur
7.04 c                   endif
      *
7.04 c                   eval      w_slin = ldline
7.03  *
7.03  * Hvis linjen(e) over er tekstlinjer, skal de også plukkes. Vi forutsetter
7.09  * at disse har med linja å gjøre. NB gjelder også tjenestelinjer.
7.03  *
7.09 c                   exsr      sub_prevtxt
7.04  *
7.04  * Hvis dette er siste varelinje, sjekk om neste linjer også
7.09  * skal være med. NB Gjelder også tjenestelinjer.
7.04  *
7.04 c                   if        w_line = w_slin
7.09 c                   exsr      sub_lasttxt
7.04 c                   endif
      *
7.09 c                   endif
      *
     c     lblii2_key    reade     lblii2
     c                   enddo
      *
      *  Oppdater UTPLUKK-PARAM-REGISTER med ny bestilling
      *
7.01  * Sekvens er flyttet
     c                   eval      lplol1_lonr = lohel1_numm
     c                   eval      lplol1_lsuf = lohel1_suff
     c     lplol1_key    chain     lplolur
     c                   if        not %found
     c                   eval      lkpfir = w_firm
     c                   eval      lkponr = lohel1_numm
     c                   eval      lkpsuf = lohel1_suff
     c                   eval      lkpoty = oty(x)
     c                   eval      lkplag = lolage
     c                   eval      lkpwsd = l_wsid
     c                   eval      lkpusr = l_user
     c                   eval      lkpalt = 0
     c                   eval      lkpdir = 1
7.01 c                   eval      lkpres = 1
     c                   eval      lkptxt = 0
     c                   eval      lkpkol = 0
     c                   eval      lkpnet = 0
     c                   eval      lkpbru = 0
     c                   eval      lkplmt = *blanks
     c                   eval      lkpfpr = 0
     c                   eval      lkpfte = *blanks
     c                   eval      lkpepr = 0
     C                   eval      lkpete = *blanks
     c                   write     lplolur
     c                   else
     c                   update    lplolur
     c                   endif
      *
     c                   endsr
      *
7.09  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.09  * Legger ut for resting
7.09  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.09  *
7.09 c     sub_rest      begsr
7.09  *
7.09 c                   eval      lbreiu_rnum = pblnum
7.09 c                   eval      lbreiu_rsuf = pblsuf
7.09 c                   eval      lbreiu_rlin = pbllin
7.09 c     lbreiu_key    chain     lbreiu
7.09 c                   if        not %found
7.09 c                   eval      pbrfir = ldfirm
7.09 c                   eval      pbrnum = ldnumm
7.09 c                   eval      pbrsuf = ldsuff
7.09 c                   eval      pbrlin = ldline
7.09  *
7.09 c                   eval      pbrsta = '50'
7.09  *
7.09 c                   time                    pbroda
7.09 c                   time                    pbroti
7.09 c                   eval      pbrous = l_user
7.09 c                   time                    pbreda
7.09 c                   time                    pbreti
7.09 c                   eval      pbreus = l_user
7.09 c                   write     lbreiur
7.09 c                   endif
7.09  *
7.09 c                   endsr
7.09  *
7.09  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.09  * Legger ut evt tekstlinje
7.09  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.09  *
7.09 c     sub_prevtxt   begsr
7.09  *
7.03 c                   eval      lodtlr_numm = pblnum
7.03 c                   eval      lodtlr_suff = pblsuf
7.03 c                   eval      lodtlr_line = pbllin
7.03 c     lodtlr_key    setll     lodtlr
7.03 c     lodtlr_key2   readpe    lodtlr
7.03 c                   dow       not %eof
7.03  *
7.03 c                   if        ldltyp = 'TX'
7.03 c                   eval      lplllu_lonr = ldnumm
7.03 c                   eval      lplllu_lsuf = ldsuff
7.03 c                   eval      lplllu_llin = ldline
7.03 c     lplllu_key    chain     lplllu
7.03  *
7.03 c                   eval      lklkod = '1'
7.03 c                   eval      lklant = 0
7.03  *
7.03 c                   if        %found
7.03 c                   update    lplllur
7.03 c                   else
7.03 c                   eval      lklfir = w_firm
7.03 c                   eval      lklonr = ldnumm
7.03 c                   eval      lklsuf = ldsuff
7.03 c                   eval      lkllin = ldline
7.03 c                   write     lplllur
7.03 c                   endif
7.03 c                   else
7.09  * Tjenestevare ??
7.09 c                   eval      vvarl1_vare = ldvare
7.09 c     vvarl1_key    chain     vvarl1
7.09 c                   if        %found
7.09 c                   if        vvvtyp = 'T'
7.09 c                   eval      lplllu_lonr = ldnumm
7.09 c                   eval      lplllu_lsuf = ldsuff
7.09 c                   eval      lplllu_llin = ldline
7.09 c     lplllu_key    chain     lplllu
7.09 c                   if        not %found
7.09 c                   eval      lklfir = w_firm
7.09 c                   eval      lklonr = ldnumm
7.09 c                   eval      lklsuf = ldsuff
7.09 c                   eval      lkllin = ldline
7.09 c                   eval      lklkod = '1'
7.09  * Antall hentes fra bestillingen
7.09 c                   eval      lklant = ldanta
7.09 c                   if        ldvare = *blank
7.09 c                   eval      lklant = 0
7.09 c                   endif
7.09 c                   write     lplllur
7.09 c                   else
7.09  * Antall hentes fra bestillingen
7.09 c                   eval      lklant = lklant + ldanta
7.09 c                   update    lplllur
7.09 c                   endif
7.09 c                   endif
7.09 c                   endif
7.09 c                   endif
7.09  *
7.09 c     lodtlr_key2   readpe    lodtlr
7.09 c                   enddo
7.09  *
7.09 c                   endsr
7.09  *
7.09  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.09  * Legger ut evt tekstlinje - hvis siste linje
7.09  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.09  *
7.09 c     sub_lasttxt   begsr
7.09  *
7.09 c                   eval      lodtlr_numm = lohel1_numm
7.09 c                   eval      lodtlr_suff = lohel1_suff
7.09 c                   eval      lodtlr_line = w_slin
7.09 c     lodtlr_key    setll     lodtlr
7.09 c     lodtlr_key2   reade     lodtlr
7.09  *
7.09 c                   dow       not %eof
7.09 c                   if        ldline > w_slin
7.09 c                   if        ldltyp = 'TX'
7.09  * Skriv tekstlinje som skal plukkes
7.09 c                   eval      lplllu_lonr = ldnumm
7.09 c                   eval      lplllu_lsuf = ldsuff
7.09 c                   eval      lplllu_llin = ldline
7.09 c     lplllu_key    chain     lplllu
7.09 c                   if        not %found
7.09 c                   eval      lklfir = w_firm
7.09 c                   eval      lklonr = lohelu_numm
7.09 c                   eval      lklsuf = lohelu_suff
7.09 c                   eval      lkllin = ldline
7.09 c                   eval      lklkod = '1'
7.09 c                   eval      lklant = 0
7.09 c                   write     lplllur
7.09 c                   else
7.09 c                   eval      lklant = lklant + pblpan
7.09 c                   update    lplllur
7.09 c                   endif
7.09 c                   else
7.09  * Tjenestevare ??
7.09 c                   eval      vvarl1_vare = ldvare
7.09 c     vvarl1_key    chain     vvarl1
7.09 c                   if        %found
7.09 c                   if        vvvtyp = 'T'
7.09 c                   eval      lplllu_lonr = ldnumm
7.09 c                   eval      lplllu_lsuf = ldsuff
7.09 c                   eval      lplllu_llin = ldline
7.09 c     lplllu_key    chain     lplllu
7.09 c                   if        not %found
7.09 c                   eval      lklfir = w_firm
7.09 c                   eval      lklonr = ldnumm
7.09 c                   eval      lklsuf = ldsuff
7.09 c                   eval      lkllin = ldline
7.09 c                   eval      lklkod = '1'
7.09  * Antall hentes fra bestillingen
7.09 c                   eval      lklant = ldanta
7.09 c                   if        ldvare = *blank
7.09 c                   eval      lklant = 0
7.09 c                   endif
7.09 c                   write     lplllur
7.09 c                   else
7.09  * Antall hentes fra bestillingen
7.09 c                   eval      lklant = lklant + ldanta
7.09 c                   update    lplllur
7.09 c                   endif
7.09 c                   endif
7.09 c                   endif
7.09  *
7.09 c                   endif
7.09 c                   endif
7.09  *
7.09 c     lodtlr_key2   reade     lodtlr
7.09 c                   enddo
7.09  *
7.09 c                   endsr
7.09  *
7.07  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.07  * Oppdatering av Lager
7.07  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.07  *
7.07 c     oppdat_lager  begsr
7.07  *
7.07  * Legger over verdier i overførings-felter
7.07  *
7.07 c                   move      ldvare        hlvare
7.07 c                   move      lolage        hllage
7.07 c                   move      ldenh1        hlenhe
7.07 c                   move      *loval        hldato
7.07 c                   move      *loval        hltime
7.07 c                   move      lootyp        hlotyp
7.07 c                   move      ldltyp        hlltyp
7.07  *
7.07 c                   eval      hlanta = ldanta
7.07 c                   eval      hlkopr = ldkpny
7.07  *
7.07 c                   eval      hlrefr = *blank
7.07 c                   move      'L'           hlsyst
7.07 c                   eval      hlaltk = *blank
7.07 c                   eval      hlrest = *blank
7.07 c                   move      ldnumm        w_numa
7.07 c                   move      ldline        w_lina
7.07 c                   eval      hlrefr = 'B:' + w_numa +
7.07 c                                      'L:' + w_lina
7.07 c                   eval      hlonum = ldnumm
7.07 c                   eval      hlolin = ldline
7.07 c                   move      ldlety        hllety
7.07  *
7.07  * Kaller opp lageroppdaterings-program
7.07  * dersom dette ikke er en vare hentet inn fra LVR
7.07  *
7.07 c                   if        ldlvre = 0
7.07  *
7.07 c                   eval      w_stat = *blank
7.07  *
7.07 c                   call      'VL001R'
7.07 c                   parm                    w_stat
7.07 c                   parm                    hlrec
7.07 c                   endif
7.07  *
7.07 c                   endsr
7.07  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_type
     c                   parm                    p_mnum
     c                   parm                    p_aarr
     c                   parm                    p_paid
     c                   parm                    p_pver
     c                   parm                    p_orgi
7.07 c                   parm                    p_levr
      *
      *  Key for les av bestillingshode pr. kolli
      *
7.04 c     lbhoi3_key    klist
7.04 c                   kfld                    w_firm
7.04 c                   kfld                    lbhoi3_hpid
7.04  *
7.04  *  Key for les av bestillingshode pr. kolli
7.04  *
7.04 c     lbhoi2_key    klist
7.04 c                   kfld                    w_firm
7.04 c                   kfld                    lbhoi2_hpid
7.04 c                   kfld                    lbhoi2_hpve
7.04 c                   kfld                    lbhoi2_hnum
7.04 c                   kfld                    lbhoi2_hsuf
7.04 c                   kfld                    lbhoi2_hssc
7.04  *
7.04  *  Key for les av bestillingshode pr. kolli
7.04  *
7.04 c     lbhoi2_key2   klist
7.04 c                   kfld                    w_firm
7.04 c                   kfld                    lbhoi2_hpid
7.04 c                   kfld                    lbhoi2_hpve
7.04 c                   kfld                    lbhoi2_hnum
7.04 c                   kfld                    lbhoi2_hsuf
      *
      *  Key for les av bestillingshode pr. kolli linjer
      *
     c     lblii2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lblii2_lpid
     c                   kfld                    lblii2_lpve
     c                   kfld                    lblii2_lnum
     c                   kfld                    lblii2_lsuf
      *
      *  Key for les av pakksedddel linjer
      *
     c     lusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lusrl1_user
      *
      *  Key for file : ORDRETYPE-REGISTER
      *
     c     votyl1_key    KLIST
     c                   KFLD                    w_firm
     c                   KFLD                    votyl1_otyp
      *
      *  Key for file : BESTILLING-HODE-REGISTER
      *
     c     lohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelu_numm
     c                   kfld                    lohelu_suff
      *
      *  Key for file : Kollihode
      *
     c     lpkoi3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lpkoi3_type
     c                   kfld                    lpkoi3_aarr
     c                   kfld                    lpkoi3_paid
     c                   kfld                    lpkoi3_pver
      *
      *  Key for file : Pakkseddelhode
      *
     c     lphoi1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lphoi1_type
     c                   kfld                    lphoi1_mnum
     c                   kfld                    lphoi1_aarr
     c                   kfld                    lphoi1_paid
     c                   kfld                    lphoi1_pver
      *
      *  Key for file : Kollilinjer
      *
     c     lpkliu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lpkliu_type
     c                   kfld                    lpkliu_mnum
     c                   kfld                    lpkliu_aarr
     c                   kfld                    lpkliu_paid
     c                   kfld                    lpkliu_pver
     c                   kfld                    lpkliu_fsni
     c                   kfld                    lpkliu_fniv
      *
      *  Key for file : UTPLUKK-PARAM-REGISTER
      *
     c     lplol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lplol1_lonr
     c                   kfld                    lplol1_lsuf
      *
      * Key for les STATUS
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les av bestillingshode
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      * Key for les av bestillingslinjer
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
     c                   kfld                    lodtl1_line
      *
      * Key for les av bestillingslinjer
      *
     c     lodtlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlr_numm
     c                   kfld                    lodtlr_suff
     c                   kfld                    lodtlr_line
7.07  *
7.07  * Key for les av bestillingslinjer
7.07  *
7.07 c     lodtlu_key    klist
7.07 c                   kfld                    w_firm
7.07 c                   kfld                    lodtlu_numm
7.07 c                   kfld                    lodtlu_suff
7.07 c                   kfld                    lodtlu_line
      *
      * Key for les av bestillingslinjer
      *
     c     lodtlr_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlr_numm
     c                   kfld                    lodtlr_suff
      *
      * Key for les/oppd. av utplukksfil/hode
      *
     c     lplolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lplolu_numm
     c                   kfld                    lplolu_suff
      *
      * Key for les av plukkede linjer
      *
     c     lplllu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lplllu_lonr
     c                   kfld                    lplllu_lsuf
     c                   kfld                    lplllu_llin
7.07  *
7.07  * Key for les av leverandører
7.07  *
7.07 c     rlevl1_key    klist
7.07 c                   kfld                    w_firm
7.07 c                   kfld                    rlevl1_levr
7.07  *
7.07  *  Key for file : Pakkseddel arbeidsregister
7.07  *
7.07 c     lbreiu_key    klist
7.07 c                   kfld                    w_firm
7.07 c                   kfld                    lbreiu_rnum
7.07 c                   kfld                    lbreiu_rsuf
7.07 c                   kfld                    lbreiu_rlin
7.07  *
7.07  *  Key for file : Pakkseddel arbeidsregister
7.07  *
7.07 c     lbreiu_key2   klist
7.07 c                   kfld                    w_firm
7.07 c                   kfld                    lbreiu_rnum
7.07 c                   kfld                    lbreiu_rsuf
7.08  *
7.08  * Key for les av vare
7.08  *
7.08 c     vvarl1_key    klist
7.08 c                   kfld                    w_firm
7.08 c                   kfld                    vvarl1_vare
      *
     c                   eval      lusrl1_user = l_user
     c                   eval      w_firm = l_firm
7.07 c                   eval      rlevl1_levr = p_levr
      *
7.04 c                   eval      lbhoi2_hpid = p_paid
7.04 c                   eval      lbhoi2_hpve = p_pver
7.04 c                   eval      lblii2_lpid = p_paid
7.04 c                   eval      lblii2_lpve = p_pver
      *
     c                   eval      lpkoi3_type = p_type
     c                   eval      lpkoi3_aarr = p_aarr
     c                   eval      lpkoi3_paid = p_paid
     c                   eval      lpkoi3_pver = p_pver
      *
     c                   eval      lpkliu_type = p_type
     c                   eval      lpkliu_mnum = p_mnum
     c                   eval      lpkliu_aarr = p_aarr
     c                   eval      lpkliu_paid = p_paid
     c                   eval      lpkliu_pver = p_pver
      *
     c                   eval      lphoi1_type = p_type
     c                   eval      lphoi1_mnum = p_mnum
     c                   eval      lphoi1_aarr = p_aarr
     c                   eval      lphoi1_paid = p_paid
     c                   eval      lphoi1_pver = p_pver
     c     lphoi1_key    chain     lphoi1
      *
      *  Nullstiller felter som alltid skal være blanke/null
      *
7.07 c     rlevl1_key    chain     rlevl1
7.07 c                   if        rlrest = 1
7.07 c                   eval      a1rest = 1
7.07 c                   endif
      *
      * Hent opplysninger fra LAGER-STATUS-KODER
      *
     c     lstsl1_key    chain     lstsl1r                            90
      *
     c                   endsr
