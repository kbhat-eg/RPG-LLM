     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JX430R
      * Beskrivelse . . : Vare, sletting av utgåtte priser - parameter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       210810 bof  Nytt program
6.11  *       190713 bof  lagt inn valg på om man vil slette på fra dato også
7.xx  * 7.00  020816 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
      *
     fJX430d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Datastruktur for parameterliste -ut
      *
     d                 ds
     d d_list                        20
     d  d_firm                        3  0 overlay(d_list)
     d  d_ldor                        6  0 overlay(d_list:4)
     d  d_dato                        6  0 overlay(d_list:10)
     d  d_valg                        1  0 overlay(d_list:16)
6.11 d  d_vfra                        1  0 overlay(d_list:17)
6.11 d  d_vtil                        1  0 overlay(d_list:18)
      *
      * Definering av variable i nøkler
      *
     d jlevl1_ldor     s                   like(jlldor)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s              3  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     b1tag         tag
      *
      * Behandler bildet
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
      * Kaller slette-program
      *
     c                   eval      d_firm = w_firm
     c                   eval      d_ldor = b1ldor
     c                   eval      d_dato = b1dato
     c                   eval      d_valg = b1valg
6.11 c                   eval      d_vfra = b1vfra
6.11 c                   eval      d_vtil = b1vtil
      *
     c                   call      'JX430C'
     c                   parm                    d_list
      *
      * Sender melding om batch-jobb
      *
     c                   exfmt     b1sbm
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Leverandør
      *
     c                   if        w_afld = 'B1LDOR'
     c                   call      'JL500R'
     c                   parm                    b1ldor
     c                   parm                    b1navn
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
     c                   eval      b1navn = *blank
      *
      * Leverandør
      *
     c                   if        b1ldor <> 0
     c                   eval      jlevl1_ldor = b1ldor
     c     jlevl1_key    chain     jlevl1                             32
     c                   if        *in32 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1navn = jlnavn
     c                   endif
      * Dato
     c                   if        b1dato = 0
     c                   eval      *in33 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     *dmy          test(d)                 b1dato                 33
     c                   if        *in33 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      *
      * Henter firma fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      b1valg = 1
      *
     c                   endsr
