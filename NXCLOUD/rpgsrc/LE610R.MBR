      /TITLE  ASLAGR: Lagertelling
     h datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LE610R
      *  Beskrivelse . . : Utskrift av avvik-liste
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign
      * --------- ------  --------------------------------------------------
6.10  *  6.10     100512  Siden til lager ikke vises settes,       bsa
6.10  *                   fra og til lager likt.
6.30  *  6.30     130814  Endret i skjermfila                      bsa
7.xx  * 7.00 190516  bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
8.01  * 800  161024  ask  Lagt inn mulighet for å kalle opp pgm med batchid
8.02  * 800  251024  ask  Henter lager fra batch om det finnes
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fle610d    cf   e             workstn
      *
      * Datastruktur for file-member
      *
     d                 ds
     d d_lmbr                        10
     d  d_lbok                        1    overlay(d_lmbr)
     d  d_lfir                        3  0 overlay(d_lmbr:2)
     d  d_llst                        6  0 overlay(d_lmbr:5)
8.01  *
8.01  * Definering av variable i parameter
8.01  *
8.01 d p_batc          s             10
      *
      * Datastruktur for parameterliste
      *
     d                 ds
     d d_prec                        64
     d  d_batc                       10    overlay(d_prec)
     d  d_fogr                        2  0 overlay(d_prec:11)
     d  d_togr                        2  0 overlay(d_prec:13)
     d  d_fhgr                        2  0 overlay(d_prec:15)
     d  d_thgr                        2  0 overlay(d_prec:17)
     d  d_fugr                        3  0 overlay(d_prec:19)
     d  d_tugr                        3  0 overlay(d_prec:22)
     d  d_fvar                       15    overlay(d_prec:25)
     d  d_tvar                       15    overlay(d_prec:40)
     d  d_flag                        2  0 overlay(d_prec:55)
     d  d_tlag                        2  0 overlay(d_prec:57)
     d  d_val1                        1  0 overlay(d_prec:59)
     d  d_val2                        1  0 overlay(d_prec:60)
      *
      * Local Data Area
      *
8.01 d lda            uds
     d l_firm                944    946  0
     d l_fnav                951    980
8.01 d l_batc               1006   1015
      *
      * Definering av variable
      *
     d w_batc          s             10
     d w_numm          s              6  0
     d w_qnnn          s              1  0
     d w_retu          s              1
     d w_valg          s              1  0
     d w_firm          s              3  0
     d w_vare          s             15
     d w_vtxt          s             35
     d w_latx          s             30
     d w_otxt          s             35
     d w_htxt          s             35
     d w_utxt          s             35
8.02 d w_lage          s              2  0
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  A1 Kaller opp subprogram for inntasting av batchnummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     a1tag         tag
      *
8.01 c                   if        l_batc <> ' '
8.01 c                   eval      w_batc = l_batc
8.01 c                   else
     c                   eval      w_retu = 'L'
     c                   call      'LC500R'
     c                   parm                    w_retu
     c                   parm                    w_batc
8.01 c                   endif
      *
     c                   if        w_retu <> *blank
     c                   goto      end_pgm
     c                   endif
      *
     c                   eval      d_batc = w_batc
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandle bilde for registrering av parametre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     b1tag         tag
      *
      *  Nullstiller variable som skal vises i skjermbildet
      *
     c                   eval      b1fogr = 0
     c                   eval      b1togr = 0
     c                   eval      b1fhgr = 0
     c                   eval      b1thgr = 0
     c                   eval      b1fugr = 0
     c                   eval      b1tugr = 0
     c                   eval      b1fvar = *blank
     c                   eval      b1tvar = *blank
     c                   eval      b1flag = 0
     c                   eval      b1tlag = 99
8.02  *
8.02  *  Sjekk om filtreringslager på batch
8.02  *
8.02       exec sql
8.02         select l1lage into :w_lage
8.02            from lbc1pf
8.02          where l1batc = :l_batc;
8.02       if sqlcod < 0 or
8.02          sqlcod = 100;
8.02         else;
8.02          b1flag = w_lage;
8.02          b1tlag = w_lage;
8.02       endif;
      *
     c     b1taga        tag
     c                   exfmt     b1bld
      *
      *  Behandle funksjonstaster
      *
     c                   select
     c                   when      *inkc = *on
     c                   goto      end_pgm
     c                   when      *inkd = *on
     c                   exsr      sp_input
     c                   goto      b1taga
     c                   endsl
      *
      *
      *  Legger inntastede opplysninger i Hjelpe-felter for Parameter
      *
     c                   eval      d_fogr = b1fogr
     c                   eval      d_togr = b1togr
     c                   eval      d_fhgr = b1fhgr
     c                   eval      d_thgr = b1thgr
     c                   eval      d_fugr = b1fugr
     c                   eval      d_tugr = b1tugr
     c                   eval      d_fvar = b1fvar
     c                   eval      d_tvar = b1tvar
     c                   eval      d_flag = b1flag
6.10 c**                 eval      d_tlag = b1tlag
6.10 c                   eval      d_tlag = b1flag
      *
      *  Dersom overgruppenr til=0 skal t.o.m. alle hovedgrupper velges
      *
     c                   if        d_togr = 0
     c                   eval      d_togr = 99
     c                   endif
      *
      *  Dersom hovedgruppenr til=0 skal t.o.m. alle hovedgrupper velges
      *
     c                   if        d_thgr = 0
     c                   eval      d_thgr = 99
     c                   endif
      *
      *  Dersom undergruppenr til=0 skal t.o.m. alle undergrupper velges
      *
     c                   if        d_tugr = 0
     c                   eval      d_tugr = 999
     c                   endif
      *
      *  Dersom varenummer til=0 skal t.o.m. alle varenummer velges
      *
     c                   if        b1tvar = *blank
     c                   eval      d_tvar = '99999999'
     c                   eval      d_tvar = '99999999'
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandle bilde for valg av sortering/uttrekk osv.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     c1tag         tag
      *
      *  Nullstiller variable som skal vises i skjermbildet
      *
     c                   eval      c1val1 = 1
     c                   eval      c1val2 = 0
      *
     c     c1tagb        tag
     c                   exfmt     c1bld
      *
      *  F3=Gå tilbake
      *
     c                   if        *inkc = *on
     c                   goto      b1taga
     c                   endif
      *
      *  Legger inntastede opplysninger i Hjelpe-felter for Parameter
      *
     c                   eval      d_val1 = c1val1
     c                   eval      d_val2 = c1val2
      *
      *  Henter siste nummer for File-member
      *
     c                   call      'LÅ900R'
     c                   parm                    w_qnnn
     c                   parm                    w_numm
      *
     c                   eval      d_lbok = 'K'
     c                   eval      d_llst = w_numm
      *
      *  Kaller opp program for videre behandling
      *
     c                   call      'LE610C'
     c                   parm                    d_prec
     c                   parm                    d_lmbr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for spørring på inputfelter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sp_input      begsr
      *
     c                   if        w_actr = 'B1BLD'
      *
      *  Spørring i overgrupperegister
      *
     c                   if        w_actf = 'B1FOGR'
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b1fogr
     c                   parm                    w_otxt
     c                   goto      end_sp
     c                   endif
      *
     c                   if        w_actf = 'B1TOGR'
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b1togr
     c                   parm                    w_otxt
     c                   goto      end_sp
     c                   endif
      *
      *  Spørring i hovedgrupperegister
      *
     c                   if        w_actf = 'B1FHGR'
 001 C                   call      'VG511R'
 001 C                   parm                    w_firm
 001 C                   parm                    b1fogr
 001 C                   parm                    b1fhgr
 001 C                   parm                    w_htxt
     c                   goto      end_sp
     c                   endif
      *
     c                   if        w_actf = 'B1THGR'
 001 C                   call      'VG511R'
 001 C                   parm                    w_firm
 001 C                   parm                    b1togr
 001 C                   parm                    b1thgr
 001 C                   parm                    w_htxt
     c                   goto      end_sp
     c                   endif
      *
      *  Spørring i undergrupperegister
      *
     c                   if        w_actf = 'B1FUGR'
 001 C                   call      'VG512R'
 001 C                   parm                    w_firm
 001 C                   parm                    b1fogr
 001 C                   parm                    b1fhgr
 001 C                   parm                    b1fugr
 001 C                   parm                    w_utxt
     c                   goto      end_sp
     c                   endif
      *
     c                   if        w_actf = 'B1TUGR'
 001 C                   call      'VG512R'
 001 C                   parm                    w_firm
 001 C                   parm                    b1togr
 001 C                   parm                    b1thgr
 001 C                   parm                    b1tugr
 001 C                   parm                    w_utxt
     c                   goto      end_sp
     c                   endif
      *
      *  Spørring i vareregister
      *
     c                   if        w_actf = 'B1FVAR'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   movel     w_vare        b1fvar
     c                   goto      end_sp
     c                   endif
      *
     c                   if        w_actf = 'B1TVAR'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   movel     w_vare        b1tvar
     c                   goto      end_sp
     c                   endif
      *
      *  Spørring lager
      *
     c                   if        w_actf = 'B1FLAG'
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    b1flag
     c                   parm                    w_latx
     c                   goto      end_sp
     c                   endif
      *
     c                   if        w_actf = 'B1TLAG'
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    b1tlag
     c                   parm                    w_latx
     c                   goto      end_sp
     c                   endif
      *
     c                   endif
      *
     c     end_sp        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
8.01  *
8.01  * Definisjon av local data area for output
8.01  *
8.01 c     *dtaara       define    *lda          lda
      *
      *  Setter startverdier
      *
     c                   eval      w_batc = *blank
     c                   eval      w_retu = *blank
     c                   eval      w_valg = 0
     c                   eval      w_qnnn = 0
     c                   eval      w_numm = 0
      *
      *  Legge inn firmanummer i nøklene
      *
     c                   eval      d_lfir = l_firm
     c                   eval      w_firm = l_firm
      *
     c                   endsr
