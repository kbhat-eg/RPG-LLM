     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASNAVIOKO
      * Program . . . . : IT013R
      * Beskrivelse . . : Leser distrikter og skriver til kø
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       230614 ask  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra08l1    if   e           k disk    rename(ra08pfr:ra08l1r)
      *
      * Local Data Area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d ra08l1_hkod     s                   like(rahkod)
      *
      *  Definering av datastrukturer for parameter
      *
     d d_parm          ds
     d  d_kode                        4
     d  d_besk                       40
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_prog          s             40
     d w_comd          s             20
     d w_parm          s           2048
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_rpgp          s             10
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram - leser alle poster og kaller opp std skriveprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser alle poster på ra08PF
      *
     c                   eval      ra08l1_hkod = 0
     c     ra08l1_key    setll     ra08l1
      *
     c                   read      ra08l1
     c                   dow       not %eof and
     c                             rahfir = w_firm
      *
     c                   eval      d_kode = %triml(%editw(rahkod : '   0'))
     c                   eval      d_besk = rahtxt
     c                   eval      w_parm = d_parm
      *
     c                   call      'IT001R'
     c                   parm                    w_firm
     c                   parm                    w_prog
     c                   parm                    w_comd
     c                   parm                    w_parm
     c                   parm                    w_rpgp
      *
     c                   read      ra08l1
     c                   enddo
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på valgt kode
      *
     c     ra08l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra08l1_hkod
      *
      * Setter inn parameterverdier
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_prog = 'NexstepImportDistrikter'
     c                   eval      w_comd = 'Insert'
     c                   eval      w_rpgp = 'IT013R'
      *
     c                   endsr
