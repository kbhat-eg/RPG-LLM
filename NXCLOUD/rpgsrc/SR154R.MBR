      /TITLE  ASSTAT Utvalg Produktansvarlig
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASSTAT
      * Program . . . . : SR154R
      * Beskrivelse . . : Utvalg Produktansvarlig
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fSR154d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
     fslpml1    if   e           k disk    rename(slpmpfr:slpml1r)
     fslpmlr    if   e           k disk    rename(slpmpfr:slpmlrr)
     fslpmlu    uf a e           k disk    rename(slpmpfr:slpmlur)
      *
      *
      * Definering av variabler i nøkler
      *
     d slpml1_type     s                   like(srtype)
     d slpml1_kode     s                   like(srkode)
     d slpml1_valg     s                   like(srvalg)
     d slpmlr_type     s                   like(srtype)
     d slpmlr_kode     s                   like(srkode)
     d slpmlr_valg     s                   like(srvalg)
     d slpmlu_type     s                   like(srtype)
     d slpmlu_kode     s                   like(srkode)
     d slpmlu_valg     s                   like(srvalg)
      *
      *
      * Definering av variable
      *
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(srfirm)
     d wptype          s                   like(srtype)
     d wpkode          s                   like(srkode)
     d wptext          s             30
     d wprtxt          s                   like(wptext)
     d wppran          s                   like(b1pran)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(12)
      *
      * Definering av Local data area
      *
     d                uds
     d dsuser                911    920
     d dsfirm                944    946  0
     d l_fnav                951    980
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2valg = *blank
     c                   if        b2pran <> *blanks
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
     c                   endif
      *
      * Behandling av subfile control
      *
     c                   if        b2valg <> *blank
     c                   exsr      control
     c                   if        b_feil = *on
     c                   goto      b2tagb
     c                   else
     c                   goto      b2taga
     c                   endif
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program                                                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     forny         begsr
      *
     c     slpml1_key    setll     slpml1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi
      *
     c                   movel     b2pran        slpml1_valg
     c     slpml1_key    setll     slpml1
     c                   goto      end_pos
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2pran = *blanks
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behanding av subfile-control
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     control       begsr
      *
     c                   eval      b_forn = *off
     c                   eval      b_feil = *off
      *
      * Opprette
      *
     c                   if        b2valg = '1'
     c                   eval      c1pran = b2pran
     c                   exsr      xc1bld
     c                   goto      end_ctl
     c                   endif
      *
      * Validering av nøkkel
      *
     c                   if        b2pran = *blanks
     c                   eval      b_feil = *on
     c                   eval      *in31  = *on
     c                   goto      end_ctl
     c                   endif
      *
      * Slette
      *
     c                   if        b2valg = '4'
     c                   eval      d1pran = b2pran
     c                   exsr      xd1win
     c                   goto      end_ctl
     c                   endif
      *
     c     end_ctl       tag
      *
     c                   eval      b2valg = *blank
     c                   eval      b2pran = *blanks
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        wcurrn > 0
     c                   eval      wvisrn = wcurrn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      wvisrn = w_srrn
      *
      * Slette post
      *
     c                   if        b1valg = '4'
     c                   eval      d1pran = b1pran
     c                   eval      d1text = b1text
     c                   exsr      xd1win
     c                   eval      b1valg = *blank
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xc1bld        begsr
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
     c                   endsl
      *
      * Sender opp informasjonsbilde dersom post finnes fra før
      *
     c                   movel     c1pran        slpmlr_valg
     c     slpmlr_key    chain     slpmlr                             90
     c                   if        *in90 = *off
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      * Kaller registreringsrutine
      *
     c                   move      c1pran        c2pran
     c                   exsr      xc2bld
      *
     c                   goto      c1taga
      *
     c     end_c1bld     endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xc2bld        begsr
      *
     c                   movel     c1pran        slpmlr_valg
     c     slpmlr_key    chain     slpmlr                             90
      *
     c                   if        *in90 = *off
     c                   movel     srrest        c2text
     c                   else
      *                  eval      c2text = *blank
     c                   endif
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   slpmlur
      *
     c                   movel     c1pran        slpmlu_valg
     c     slpmlu_key    chain     slpmlur                            90
      *
     c                   movel     c2text        srrest
     c                   move      wptype        srtype
     c                   movel     wpkode        srkode
     c                   movel     c1pran        srvalg
     c                   eval      srfirm = w_firm
     c                   eval      srsyst = 'L'
      *
     c                   if        *in90 = *off
     c                   update    slpmlur
     c                   else
     c                   write     slpmlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = '1' or b2valg = '1'
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xd1win        begsr
      *
     c                   movel     d1pran        slpmlr_valg
     c     slpmlr_key    chain     slpmlr                             90
      *
     c                   movel     srrest        d1text
      *
     c                   write     s1win
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter
      *
     c                   movel     d1pran        slpmlu_valg
     c     slpmlu_key    chain     slpmlu                             90
     c                   delete    slpmlur
      *
     c     end_d1win     endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      slpml1                                 15
      *
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
     c                   if        srfirm <> w_firm
     c                             or srkode <> wpkode
     c                             or srtype <> wptype
     c                   eval      *in15 = *on
     c                   goto      end_crt
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = *blank
     c                   movel     srvalg        b1pran
     c                   movel     srrest        b1text
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      wvisrn = w_sfrn
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *inzsr        begsr
      *
      * KEY for file
      *
     c     slpml1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    slpml1_type
     c                   kfld                    slpml1_kode
     c                   kfld                    slpml1_valg
      *
      * KEY for file
      *
     c     slpmlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    slpmlr_type
     c                   kfld                    slpmlr_kode
     c                   kfld                    slpmlr_valg
      *
      * KEY for file
      *
     c     slpmlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    slpmlu_type
     c                   kfld                    slpmlu_kode
     c                   kfld                    slpmlu_valg
      *
      *
      *
      *
      *
      *
      *
      *  Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    wptype
     c                   parm                    wptext
      *
     c                   eval      wpkode = 54
     c                   eval      *in22 = *on
      *
     c                   eval      b2type = wptype
     c                   eval      b2text = wptext
      *
     c                   eval      slpml1_type = wptype
     c                   eval      slpml1_kode = wpkode
     c                   eval      slpml1_valg = *blank
      *
     c                   eval      slpmlu_type = wptype
     c                   eval      slpmlu_kode = wpkode
     c                   eval      slpmlu_valg = *blank
      *
     c                   eval      slpmlr_type = wptype
     c                   eval      slpmlr_kode = wpkode
     c                   eval      slpmlr_valg = *blank
      *
      * Henter firmanummer fra LDA
      *
     c                   eval      w_firm = dsfirm
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     slpml1_key    setll     slpml1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
