     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LX899R
      * Beskrivelse . . : Danne lager-rec.hvis dette mangler for varen
      *                   Setter alle til varetype = L
      *                   Fjernet at man må kjøre lagersturing.
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       221013 bof  Nytt program
6.20  *       071113 bof  Man må ikke kjøre lagerstyring
6.21  *       041213 bof  Utivdet med leverandør på lager
6.22  *       060214 bof  Fjernet oppdat av lev.nr  v/MBE og RST
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fra10l1    if   e           k disk    rename(ra10pfr:ra10l1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     fvlaglu    uf a e           k disk    rename(vlagpfr:vlaglur)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d vlaglu_firm     s                   like(vlfirm)
     d vlaglu_vare     s                   like(vlvare)
     d vlaglu_lage     s                   like(vllage)
     d ra10l1_firm     s                   like(vvfirm)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vvfirm)
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.20 c*                  if        faalag = 1
      * kjører lager
      * da leses alle varer,
      * hvis lager ikke  finnes fra før, dannes lager med
      * varetype=L
     c                   read      vvarl1
     c                   dow       not %eof(vvarl1)
      *
     c                   if        vvvtyp = 'L' or
     c                             vvvtyp = 'S'
      *
     c                   eval      ra10l1_firm = vvfirm
     c     ra10l1_key    setll     ra10l1
     c     ra10l1_key    reade     ra10l1
     c                   dow       not %eof(ra10l1)
      *
     c                   eval      vlaglu_firm = vvfirm
     c                   eval      vlaglu_vare = vvvare
     c                   eval      vlaglu_lage = rajkod
     c     vlaglu_key    chain     vlaglu
     c                   if        not %found(vlaglu)
     c                   exsr      dann_lager
     c                   endif
      *
     c     ra10l1_key    reade     ra10l1
     c                   enddo
      *
     c                   endif
      *
     c     les_neste     tag
     c                   read      vvarl1
     c                   enddo
      *
6.20 c*                  endif
      *
      *
      * Avslutt program
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å danne lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dann_lager    begsr
      *
     c                   clear                   vlaglur
     c                   eval      vlfirm = vvfirm
     c                   eval      vlvare = vvvare
     c                   eval      vllage = rajkod
     c                   eval      vlvtyp = 'L'
6.22 c*                  eval      vlbldo = vvldor
     c                   time                    vlodat
     c                   time                    vlotim
     c                   time                    vledat
     c                   time                    vletim
     c                   time                    vlfeda
     c                   time                    vlfeti
     c                   eval      vlousr = l_user
     c                   eval      vleusr = l_user
     c                   eval      vlfeus = l_user
     c                   write     vlaglur
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for oppdatering av LAGER
      *
     c     vlaglu_key    klist
     c                   kfld                    vlaglu_firm
     c                   kfld                    vlaglu_vare
     c                   kfld                    vlaglu_lage
      *
      *  Key for les av lager
      *
     c     ra10l1_key    klist
     c                   kfld                    ra10l1_firm
      *
      *  Key for les av statusreg
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Henter firma
      *
     c                   eval      w_firm = l_firm
      *
     c     fstsl1_key    chain     fstsl1
      *
     c                   endsr
