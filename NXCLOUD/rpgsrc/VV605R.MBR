     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VV605R
      * Beskrivelse . . : Vare, utskrift av konto-opplysninger
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       210998 HKO  Nytt program
5.20  * R5.20 141003 AMD  Utvidet med mulighet for å kunne kontere på
5.20  *                   leveringstype
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvawpf    if   e           k disk
      *
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     fvkhvl1    if   e           k disk    rename(vkhvpfr:vkhvl1r)
      *
     fvv605p    o    e             printer
      *
      * Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
      *
      * Definering av datastruktur
      *
      * Datastruktur for parameterliste -inn
      *
     d                 ds
     d d_list                        80
     d  d_firm                        3  0 overlay(d_list:1)
     d  d_fogr                        2  0 overlay(d_list:4)
     d  d_fhgr                        2  0 overlay(d_list:6)
     d  d_fugr                        3  0 overlay(d_list:8)
     d  d_togr                        2  0 overlay(d_list:11)
     d  d_thgr                        2  0 overlay(d_list:13)
     d  d_tugr                        3  0 overlay(d_list:15)
     d  d_flev                        6  0 overlay(d_list:18)
     d  d_tlev                        6  0 overlay(d_list:24)
     d  d_fvar                       15    overlay(d_list:30)
     d  d_tvar                       15    overlay(d_list:45)
     d  d_dekn                        4  2 overlay(d_list:60)
     d  d_utsk                        1  0 overlay(d_list:64)
     d  d_srt1                        1  0 overlay(d_list:65)
     d  d_srt2                        1  0 overlay(d_list:66)
     d  d_srt3                        1  0 overlay(d_list:67)
     d  d_srt4                        1  0 overlay(d_list:68)
      *
      * Definering av parametre
      *
     d p_list          s             80
      *
      * Definering av variabler i nøkler
      *
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vogrl1_oogr     s                   like(vgoogr)
     d vkhvl1_kkod     s                   like(vakkod)
5.20 d vkhvl1_klty     s                   like(vaklty)
     d vkhvl1_koty     s                   like(vakoty)
      *
      * Definering av variable
      *
     d w_timestamp     s             12  0
      *
     d w_firm          s                   like(vvfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Utskrift av header
      *
     c                   exsr      hode
      *
      * Utskrift av vareregister
      *
     c                   exsr      behandling
      *
      * Avslutt
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering og utskrift av hode-opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode          begsr
      *
      * Legger inn arb.stasjon og bruker i faste felter
      *
     c                   eval      a1firm = w_firm
     c                   eval      a1user = l_user
     c                   eval      a1wsid = l_wsid
     c                   time                    w_timestamp
     c                   move      w_timestamp   a1date
     c                   movel     w_timestamp   a1time
      *
      * Henter firmaopplysninger
      *
     c     afirl1_key    chain     afirl1r                            90
     c                   eval      a1fnav = aafnav
      *
      * Henter parametre
      *
     c                   eval      a2fogr = d_fogr
     c                   eval      a2fhgr = d_fhgr
     c                   eval      a2fugr = d_fugr
     c                   eval      a2togr = d_togr
     c                   eval      a2thgr = d_thgr
     c                   eval      a2tugr = d_tugr
     c                   eval      a2flev = d_flev
     c                   eval      a2tlev = d_tlev
     c                   eval      a2fvar = d_fvar
     c                   eval      a2tvar = d_tvar
      *
     c                   select
     c                   when      d_srt1 = 1
     c                   eval      a2sort = 'Varegruppe'
     c                   when      d_srt2 = 1
     c                   eval      a2sort = 'Leverandør'
     c                   when      d_srt3 = 1
     c                   eval      a2sort = 'Varetekst'
     c                   when      d_srt4 = 1
     c                   eval      a2sort = 'Varenummer'
     c                   endsl
      *
     c                   select
     c                   when      d_srt1 = 2
     c                   eval      a2sort = %trim(a2sort) + ' - Varegruppe'
     c                   when      d_srt2 = 2
     c                   eval      a2sort = %trim(a2sort) + ' - Leverandør'
     c                   when      d_srt3 = 2
     c                   eval      a2sort = %trim(a2sort) + ' - Varetekst'
     c                   when      d_srt4 = 2
     c                   eval      a2sort = %trim(a2sort) + ' - Varenummer'
     c                   endsl
      *
     c                   select
     c                   when      d_srt1 = 3
     c                   eval      a2sort = %trim(a2sort) + ' - Varegruppe'
     c                   when      d_srt2 = 3
     c                   eval      a2sort = %trim(a2sort) + ' - Leverandør'
     c                   when      d_srt3 = 3
     c                   eval      a2sort = %trim(a2sort) + ' - Varetekst'
     c                   when      d_srt4 = 3
     c                   eval      a2sort = %trim(a2sort) + ' - Varenummer'
     c                   endsl
      *
      * Skriver heading
      *
     c                   write     a1hdr
     c                   write     a2hdr
     c                   write     a3hdr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Leser arbeidsregsiter, og behandler utskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   read      vvawpf                                 90
     c                   dow       *in90 = *off
      *
      * Skriver detaljlinje
      *
     c                   exsr      detalj
      *
      * Leser neste rad
      *
     c                   read      vvawpf                                 90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine utskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalj        begsr
      *
     c                   eval      d1ogrp = vvogrp
     c                   eval      d1hgrp = vvhgrp
     c                   eval      d1ugrp = vvugrp
     c                   eval      d1ldor = vvldor
     c                   eval      d1tek1 = vvtek1
     c                   eval      d1vare = vvvare
      *
     c                   eval      d1khev = *blank
     c                   eval      d1kild = *blank
      *
     c                   eval      d1kko1 = 0
     c                   eval      d1ksp1 = 0
     c                   eval      d1kav1 = 0
     c                   eval      d1kko2 = 0
     c                   eval      d1ksp2 = 0
     c                   eval      d1kav2 = 0
     c                   eval      d1kko3 = 0
     c                   eval      d1ksp3 = 0
     c                   eval      d1kav3 = 0
     c                   eval      d1kko4 = 0
     c                   eval      d1ksp4 = 0
     c                   eval      d1kav4 = 0
     c                   eval      d1kko5 = 0
     c                   eval      d1ksp5 = 0
     c                   eval      d1kav5 = 0
     c                   eval      d1kko6 = 0
     c                   eval      d1ksp6 = 0
     c                   eval      d1kav6 = 0
     c                   eval      d1kko7 = 0
     c                   eval      d1ksp7 = 0
     c                   eval      d1kav7 = 0
      *
      * Henter kontohenvisning. Kontohenvisning kan ligge på vare,
      * undergruppe, hovedgruppe, overgruppe eller status
      *
     c                   if        vvkhev <> *blank
     c                   eval      d1khev = vvkhev
     c                   eval      d1kild = 'Vare'
     c                   endif
      *
     c                   if        d1khev = *blank
     c                   eval      vugrl1_uogr = vvogrp
     c                   eval      vugrl1_uhgr = vvhgrp
     c                   eval      vugrl1_uugr = vvugrp
     c     vugrl1_key    chain     vugrl1                             91
     c                   if        *in91 = *off
     c                   eval      d1khev = vgukhv
     c                   eval      d1kild = 'Ugrp'
     c                   endif
     c                   endif
      *
     c                   if        d1khev = *blank
     c                   eval      vhgrl1_hogr = vvogrp
     c                   eval      vhgrl1_hhgr = vvhgrp
     c     vhgrl1_key    chain     vhgrl1                             91
     c                   if        *in91 = *off
     c                   eval      d1khev = vghkhv
     c                   eval      d1kild = 'Hgrp'
     c                   endif
     c                   endif
      *
     c                   if        d1khev = *blank
     c                   eval      vogrl1_oogr = vvogrp
     c     vogrl1_key    chain     vogrl1                             91
     c                   if        *in91 = *off
     c                   eval      d1khev = vgokhv
     c                   eval      d1kild = 'Ogrp'
     c                   endif
     c                   endif
      *
     c                   if        d1khev = *blank
     c     fstsl1_key    chain     fstsl1                             91
     c                   if        *in91 = *off
     c                   eval      d1khev = faakhv
     c                   eval      d1kild = 'Status'
     c                   endif
     c                   endif
      *
      * Henter konti knyttet mot kontohenvisning
      *
     c                   if        d1khev <> *blank
     c                   eval      vkhvl1_kkod = d1khev
5.20 c                   eval      vkhvl1_klty = *zero
     c                   eval      vkhvl1_koty = *blank
     c     vkhvl1_key    chain     vkhvl1                             91
     c                   if        *in91 = *off
     c                   eval      d1kko1 = vakko1
     c                   eval      d1ksp1 = vaksp1
     c                   eval      d1kav1 = vakav1
     c                   eval      d1kko2 = vakko2
     c                   eval      d1ksp2 = vaksp2
     c                   eval      d1kav2 = vakav2
     c                   eval      d1kko3 = vakko3
     c                   eval      d1ksp3 = vaksp3
     c                   eval      d1kav3 = vakav3
     c                   eval      d1kko4 = vakko4
     c                   eval      d1ksp4 = vaksp4
     c                   eval      d1kav4 = vakav4
     c                   eval      d1kko5 = vakko5
     c                   eval      d1ksp5 = vaksp5
     c                   eval      d1kav5 = vakav5
     c                   eval      d1kko6 = vakko6
     c                   eval      d1ksp6 = vaksp6
     c                   eval      d1kav6 = vakav6
     c                   eval      d1kko7 = vakko7
     c                   eval      d1ksp7 = vaksp7
     c                   eval      d1kav7 = vakav7
     c                   endif
     c                   endif
      *
      * Skriv detaljlinje
      *
     c                   write     d1dtl                                30
     c                   exsr      overflow
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overflow      begsr
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   write     a3hdr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for oppslag på firma
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på koderegister / statusregister
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på kontohenvisning-register
      *
     c     vkhvl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vkhvl1_kkod
5.20 c                   kfld                    vkhvl1_klty
     c                   kfld                    vkhvl1_koty
      *
      *
      * Kopierer parameterliste til datastruktur
      *
     c                   eval      d_list = p_list
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = d_firm
      *
     c                   endsr
