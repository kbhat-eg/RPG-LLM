     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO791R
      * Beskrivelse . . : Bytting av leverandørnummer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       080500 HKO  Nytt program
      *       130803 HKO  Endret vare-sortiment-register
      *       090908 bof  Utvidet med en del filer
      *       110908 bof  Endret logikk på les av vare og rab.matr
5.70  * PRGR  211008 bof  Utvidet med prisgruppe
      * 6.10  230609 BSA  Oppdatert med sporingsinformasjon
6.20  * 6.20  041110 bof  utvidet nøkkel vsdtpf/vshepf
6.21  * 6.20  111213 bof  Utvidet med VVEPPF, VVPRPF
6.22  * 6.20  140114 bof  Byttet rekkefølge på VVARPF og VVPRPF
      *
8.01  *PRG2   090622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohelg    uf   e           k disk    rename(fohepfr:fohelgr)
     ffodtlg    uf   e           k disk    rename(fodtpfr:fodtlgr)
     fvvarl5    uf   e           k disk    rename(vvarpfr:vvarl5r)
     ffrabl8    uf   e           k disk    rename(frabpfr:frabl8r)
     fflvalu    uf   e           k disk    rename(flvapfr:flvalur)
     fvsdtl1    if   e           k disk    rename(vsdtpfr:vsdtl1r)
     fvsdtlu    uf   e           k disk    rename(vsdtpfr:vsdtlur)
6.21 fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
6.21 fvvprlu    uf   e           k disk    rename(vvprpfr:vvprlur)
6.21 fvvepl1    if   e           k disk    rename(vveppfr:vvepl1r)
6.21 fvveplu    uf   e           k disk    rename(vveppfr:vveplur)
6.21 fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
6.21 fvlaglu    uf   e           k disk    rename(vlagpfr:vlaglur)
     ffrawl1    if   e           k disk    rename(frawpfr:frawl1r)
     ffrawlu    uf   e           k disk    rename(frawpfr:frawlur)
     fvpdtl1    if   e           k disk    rename(vpdtpfr:vpdtl1r)
     fvpdtlu    uf   e           k disk    rename(vpdtpfr:vpdtlur)
     fvvasl1    if   e           k disk    rename(vvaspfr:vvasl1r)
     fvvaslu    uf   e           k disk    rename(vvaspfr:vvaslur)
     fvvgkl4    uf   e           k disk    rename(vvgkpfr:vvgkl4r)
      *
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av parametere
      *
     d p_firm          s                   like(fofirm)
     d p_gldo          s                   like(foldor)
     d p_ldor          s                   like(foldor)
      *
      * Definering av variable i nøkler
      *
     d vvarl5_ldor     s                   like(vvldor)
     d frabl8_ldor     s                   like(frldor)
     d flvalu_ldor     s                   like(fwldor)
     d vvgkl4_ldor     s                   like(vkldor)
     d fohelg_ldor     s                   like(foldor)
     d fodtlg_ldor     s                   like(fdldor)
     d vsdtlu_vsor     s                   like(vdvsor)
6.20 d vsdtlu_lage     s                   like(vdlage)
     d vsdtlu_ldor     s                   like(vdldor)
     d vsdtlu_ogrp     s                   like(vdogrp)
     d vsdtlu_hgrp     s                   like(vdhgrp)
     d vsdtlu_ugrp     s                   like(vdugrp)
     d vsdtlu_modn     s                   like(vdmodn)
     d vsdtlu_vare     s                   like(vdvare)
     d frawlu_rkat     s                   like(frrkat)
     d frawlu_kkat     s                   like(frkkat)
     d frawlu_kund     s                   like(frkund)
     d frawlu_kpro     s                   like(frkpro)
     d frawlu_ogrp     s                   like(frogrp)
     d frawlu_hgrp     s                   like(frhgrp)
     d frawlu_ugrp     s                   like(frugrp)
     d frawlu_ldor     s                   like(frldor)
     d frawlu_modn     s                   like(frmodn)
     d frawlu_vare     s                   like(frvare)
     d frawlu_enhe     s                   like(frenhe)
     d frawlu_lety     s                   like(frlety)
     d vpdtlu_pasl     s                   like(vjpasl)
     d vpdtlu_ldor     s                   like(vjldor)
     d vpdtlu_ogrp     s                   like(vjogrp)
     d vpdtlu_hgrp     s                   like(vjhgrp)
     d vpdtlu_ugrp     s                   like(vjugrp)
     d vpdtlu_kpri     s                   like(vjkpri)
     d vpdtlu_vare     s                   like(vjvare)
     d vpdtlu_lety     s                   like(vjlety)
     d vvaslu_vare     s                   like(vvvare)
6.21 d vvprlu_vare     s                   like(vpvare)
6.21 d vvprlu_ldor     s                   like(vPldor)
6.21 d vvprlu_prgr     s                   like(vpprgr)
6.21 d vvprlu_enhe     s                   like(vpenhe)
6.21 d vvprlu_lety     s                   like(vplety)
6.21 d vvprlu_pdat     s                   like(vppdat)
6.21 d vveplu_pvar     s                   like(vepvar)
6.21 d vveplu_penh     s                   like(vepenh)
6.21 d vveplu_pldo     s                   like(vepldo)
6.21 d vlaglu_vare     s                   like(vlvare)
6.21 d vlaglu_lage     s                   like(vllage)
      *
      * Definering av variable
      *
     d w_firm          s                   like(fofirm)
     d w_gldo          s                   like(foldor)
8.01 d w_prgr          s              2
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Ordre-hode
      *
     c                   eval      fohelg_ldor = p_gldo
     c     fohelg_key    setll     fohelg
     c     fohelg_key    reade     fohelg                                 90
     c                   dow       *in90 = *off
     c                   eval      foldor = p_ldor
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   update    fohelgr
     c     fohelg_key    reade     fohelg                                 90
     c                   enddo
      *
      * Ordre-linje
      *
     c                   eval      fodtlg_ldor = p_gldo
     c     fodtlg_key    setll     fodtlg
     c     fodtlg_key    reade     fodtlg                                 90
     c                   dow       *in90 = *off
     c                   eval      fdldor = p_ldor
6.10 c                   time                    fdedat
6.10 c                   time                    fdetim
6.10 c                   eval      fdeusr = l_user
     c                   update    fodtlgr
     c     fohelg_key    reade     fodtlg                                 90
     c                   enddo
6.22  *
6.22  * Vare-pris  (må ligge foran VVARPF pga. 2dp)
6.22  *
6.22 c     key01         setll     vvprl1
6.22 c     key01         reade     vvprl1                                 90
6.22 c                   dow       *in90 = *off
6.22 c                   if        vpldor = p_gldo
6.22 c                   eval      vvprlu_vare = vpvare
6.22 c                   eval      vvprlu_ldor = vpldor
6.22 c                   eval      vvprlu_prgr = vpprgr
6.22 c                   eval      vvprlu_enhe = vpenhe
6.22 c                   eval      vvprlu_lety = vplety
6.22 c                   eval      vvprlu_pdat = vppdat
6.22 c     vvprlu_key    chain     vvprlur
6.22 c                   if        %found
6.22 c                   eval      vpldor = p_ldor
6.22 c                   time                    vpedat
6.22 c                   time                    vpetim
6.22 c                   eval      vpeusr = l_user
6.22 c                   update    vvprlur
6.22 c                   endif
6.22 c                   endif
6.22 c     key01         reade     vvprl1                                 90
6.22 c                   enddo
      *
      * Vare
      *
     c                   eval      vvarl5_ldor = p_gldo
     c     vvarl5_key    setll     vvarl5
     c     vvarl5_key    reade     vvarl5
     c                   dow       not %eof(vvarl5)
     c                   eval      vvldor = p_ldor
6.10 c                   time                    vvedat
6.10 c                   time                    vvetim
6.10 c                   eval      vveusr = l_user
     c                   update    vvarl5r
     c     vvarl5_key    reade     vvarl5
     c                   enddo
      *
      * Rabattmatrise
      *
     c                   eval      frabl8_ldor = p_gldo
     c     frabl8_key    setll     frabl8
     c     frabl8_key    reade     frabl8
     c                   dow       not %eof(frabl8)
     c                   eval      frldor = p_ldor
6.10 c                   time                    fredat
6.10 c                   time                    fretim
6.10 c                   eval      freusr = l_user
     c                   update    frabl8r
     c     frabl8_key    reade     frabl8
     c                   enddo
      *
      * Leverandør vareregister
      *
     c                   eval      flvalu_ldor = p_gldo
     c     flvalu_key    setll     flvalu
     c     flvalu_key    reade     flvalu
     c                   dow       not %eof(flvalu)
     c                   eval      fwldor = p_ldor
6.10 c                   time                    fwedat
6.10 c                   time                    fwetim
6.10 c                   eval      fweusr = l_user
     c                   update    flvalur
     c     flvalu_key    reade     flvalu
     c                   enddo
      *
      * Varegruppeknytning
      *
     c                   eval      vvgkl4_ldor = p_gldo
     c     vvgkl4_key    setll     vvgkl4
     c     vvgkl4_key    reade     vvgkl4
     c                   dow       not %eof(vvgkl4)
     c                   eval      vkldor = p_ldor
6.10 c                   time                    vkedat
6.10 c                   time                    vketim
6.10 c                   eval      vkeusr = l_user
     c                   update    vvgkl4r
     c     vvgkl4_key    reade     vvgkl4
     c                   enddo
      *
      * Varesortiment-linje
      *
     c     key01         setll     vsdtl1
     c     key01         reade     vsdtl1                                 90
     c                   dow       *in90 = *off
     c                   if        vdldor = p_gldo
     c                   eval      vsdtlu_vsor = vdvsor
6.20 c                   eval      vsdtlu_lage = vdlage
     c                   eval      vsdtlu_ldor = vdldor
     c                   eval      vsdtlu_ogrp = vdogrp
     c                   eval      vsdtlu_hgrp = vdhgrp
     c                   eval      vsdtlu_ugrp = vdugrp
     c                   eval      vsdtlu_modn = vdmodn
     c                   eval      vsdtlu_vare = vdvare
     c     vsdtlu_key    chain     vsdtlur
     c                   if        %found
     c                   eval      vdldor = p_ldor
6.10 c                   time                    vdedat
6.10 c                   time                    vdetim
6.10 c                   eval      vdeusr = l_user
     c                   update    vsdtlur
     c                   endif
     c                   endif
     c     key01         reade     vsdtl1                                 90
     c                   enddo
6.21  *
6.21  * Vare-enhet-prisgiver
6.21  *
6.21 c     key01         setll     vvepl1
6.21 c     key01         reade     vvepl1                                 90
6.21 c                   dow       *in90 = *off
6.21 c                   if        vepldo = p_gldo
6.21 c                   eval      vveplu_pvar = vepvar
6.21 c                   eval      vveplu_penh = vepenh
6.21 c                   eval      vveplu_pldo = vepldo
6.21 c     vveplu_key    chain     vveplur
6.21 c                   if        %found
6.21 c                   eval      vepldo = p_ldor
6.21 c                   time                    vepeda
6.21 c                   time                    vepeti
6.21 c                   eval      vepeus = l_user
6.21 c                   update    vveplur
6.21 c                   endif
6.21 c                   endif
6.21 c     key01         reade     vvepl1                                 90
6.21 c                   enddo
6.21  *
6.21  * Vare-LAGER
6.21  *
6.21 c     key01         setll     vlagl1
6.21 c     key01         reade     vlagl1                                 90
6.21 c                   dow       *in90 = *off
6.21 c                   if        vlbldo = p_gldo
6.21 c                   eval      vlaglu_vare = vlvare
6.21 c                   eval      vlaglu_lage = vllage
6.21 c     vlaglu_key    chain     vlaglur
6.21 c                   if        %found
6.21 c                   eval      vlbldo = p_ldor
6.21 c                   time                    vledat
6.21 c                   time                    vletim
6.21 c                   eval      vleusr = l_user
6.21 c                   update    vlaglur
6.21 c                   endif
6.21 c                   endif
6.21 c     key01         reade     vvepl1                                 90
6.21 c                   enddo
      *
      * Rabattmatrise work
      *
     c     key01         setll     frawl1
     c     key01         reade     frawl1                                 90
     c                   dow       *in90 = *off
     c                   if        frldor = p_gldo
     c                   eval      frawlu_rkat = frrkat
     c                   eval      frawlu_kkat = frkkat
     c                   eval      frawlu_kund = frkund
     c                   eval      frawlu_kpro = frkpro
     c                   eval      frawlu_ogrp = frogrp
     c                   eval      frawlu_hgrp = frhgrp
     c                   eval      frawlu_ugrp = frugrp
     c                   eval      frawlu_ldor = frldor
     c                   eval      frawlu_modn = frmodn
     c                   eval      frawlu_vare = frvare
     c                   eval      frawlu_enhe = frenhe
     c                   eval      frawlu_lety = frlety
     c     frawlu_key    chain     frawlu
     c                   if        %found
     c                   eval      frldor = p_ldor
6.10 c                   time                    fredat
6.10 c                   time                    fretim
6.10 c                   eval      freusr = l_user
     c                   update    frawlur
     c                   endif
     c                   endif
     c     key01         reade     frawl1                                 90
     c                   enddo
      *
      *
      * Påslag linje
      *
     c     key01         setll     vpdtl1
     c     key01         reade     vpdtl1                                 90
     c                   dow       *in90 = *off
     c                   if        vjldor = p_gldo
     c                   eval      vpdtlu_pasl = vjpasl
     c                   eval      vpdtlu_ldor = vjldor
     c                   eval      vpdtlu_ogrp = vjogrp
     c                   eval      vpdtlu_hgrp = vjhgrp
     c                   eval      vpdtlu_ugrp = vjugrp
     c                   eval      vpdtlu_kpri = vjkpri
     c                   eval      vpdtlu_vare = vjvare
     c                   eval      vpdtlu_lety = vjlety
     c     vpdtlu_key    chain     vpdtlu
     c                   if        %found
     c                   eval      vjldor = p_ldor
6.10 c                   time                    vjedat
6.10 c                   time                    vjetim
6.10 c                   eval      vjeusr = l_user
     c                   update    vpdtlur
     c                   endif
     c                   endif
     c     key01         reade     vpdtl1                                 90
     c                   enddo
      *
      * Slettede varer
      *
     c     key01         setll     vvasl1
     c     key01         reade     vvasl1                                 90
     c                   dow       *in90 = *off
     c                   if        vvldor = p_gldo
     c                   eval      vvaslu_vare = vvvare
     c     vvaslu_key    chain     vvaslur
     c                   if        %found
     c                   eval      vvldor = p_ldor
6.10 c                   time                    vvedat
6.10 c                   time                    vvetim
6.10 c                   eval      vveusr = l_user
     c                   update    vvaslur
     c                   endif
     c                   endif
     c     key01         reade     vvasl1                                 90
     c                   enddo
      *
      * Avslutt program
      *
     c***                eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_gldo
     c                   parm                    p_ldor
      *
      * Key for les på firma
      *
     c     key01         klist
     c                   kfld                    w_firm
      *
      * Key for les på firma og leverandør
      *
     c     key02         klist
     c                   kfld                    w_firm
     c                   kfld                    w_gldo
      *
      * Key for oppdatering av vare
      *
     c     vvarl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl5_ldor
      *
      * Key for oppdatering av rab.matrise
      *
     c     frabl8_key    klist
     c                   kfld                    w_firm
     c                   kfld                    frabl8_ldor
      *
      * Key for oppdatering av ordre-hode
      *
     c     fohelg_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelg_ldor
      *
      * Key for oppdatering av ordre-linje
      *
     c     fodtlg_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtlg_ldor
      *
      * Key for oppdatering av lev.vare
      *
     c     flvalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    flvalu_ldor
      *
      * Key for oppdatering av varegruppe-knytninger
      *
     c     vvgkl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvgkl4_ldor
      *
      * Key for oppdatering av varesortiment-linje
      *
     c     vsdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vsdtlu_vsor
6.20 c                   kfld                    vsdtlu_lage
     c                   kfld                    vsdtlu_ldor
     c                   kfld                    vsdtlu_ogrp
     c                   kfld                    vsdtlu_hgrp
     c                   kfld                    vsdtlu_ugrp
     c                   kfld                    vsdtlu_modn
     c                   kfld                    vsdtlu_vare
6.21  *
6.21  * Key for oppdatering av varepris
6.21  *
6.21 c     vvprlu_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    vvprlu_vare
6.21 c                   kfld                    vvprlu_ldor
6.21 c                   kfld                    vvprlu_prgr
6.21 c                   kfld                    vvprlu_enhe
6.21 c                   kfld                    vvprlu_lety
6.21 c                   kfld                    vvprlu_pdat
6.21  *
6.21  * Key for oppdatering av vare-enhet-prisgiver
6.21  *
6.21 c     vveplu_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    vveplu_pvar
6.21 c                   kfld                    vveplu_penh
6.21 c                   kfld                    vveplu_pldo
6.21  *
6.21  * Key for oppdatering av lager
6.21  *
6.21 c     vlaglu_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    vlaglu_vare
6.21 c                   kfld                    vlaglu_lage
      *
      * Key for oppdatering av rabattmatrise work
      *
     c     frawlu_key    klist
     c                   kfld                    w_firm
5.70 c                   kfld                    w_prgr
     c                   kfld                    frawlu_rkat
     c                   kfld                    frawlu_kkat
     c                   kfld                    frawlu_kund
     c                   kfld                    frawlu_kpro
     c                   kfld                    frawlu_ogrp
     c                   kfld                    frawlu_hgrp
     c                   kfld                    frawlu_ugrp
     c                   kfld                    frawlu_ldor
     c                   kfld                    frawlu_modn
     c                   kfld                    frawlu_vare
     c                   kfld                    frawlu_enhe
     c                   kfld                    frawlu_lety
      *
      * Key for oppdatering av påslag
      *
     c     vpdtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vpdtlu_pasl
     c                   kfld                    vpdtlu_ldor
     c                   kfld                    vpdtlu_ogrp
     c                   kfld                    vpdtlu_hgrp
     c                   kfld                    vpdtlu_ugrp
     c                   kfld                    vpdtlu_kpri
     c                   kfld                    vpdtlu_vare
     c                   kfld                    vpdtlu_lety
      *
      * Key for oppdatering av slettede varer
      *
     c     vvaslu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvaslu_vare
      *
      *
      * Henter firma og gammelt leverandørnummer fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_gldo = p_gldo
5.70  *
5.70  * henter prisgruppe
5.70  *
5.70 c                   call      'VL711R   '
5.70 c                   parm                    w_prgr
      *
     c                   endsr
