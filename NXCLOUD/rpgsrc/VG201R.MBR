     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : VG201R
      * Beskrivelse . . : Spørring på gj.sn. kostpris historikk
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       211118 ASK  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvgkpi3    if a e           k disk    rename(vgkpst:vgkpi3r)
     fvgkpir    if a e           k disk    rename(vgkpst:vgkpirr)
     fvvarl1    if   e           K disk    rename(vvarpfr:vvarl1r)
      *
     fvg201d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d vgkpi3_lage     s              2  0
     d vgkpi3_vare     s                   like(vgvare)
     d vgkpir_lage     s              2  0
     d vgkpir_hkod     s                   like(vghkod)
     d vgkpir_vare     s                   like(vgvare)
     d vgkpir_gdat     s                   like(vggdat)
     d vgkpir_gtim     s                   like(vggtim)
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d p_vare          s                   like(vgvare)
     d p_lage          s                   like(vglage)
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_ttim          s              6  0
     d b_forn          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(vgfirm)
     d w_var1          s                   like(vvvare)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Siden som skal vises
      * w_cfld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * C1MSG  - C1 Meldingsbilde
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      * Vis
     c                   if        b1valg = 5
     c                   movel     b1vare        c2vare
     c                   eval      *in31 = *on
     c                   eval      *in30 = *on
     c                   exsr      xc2bld
     c                   eval      *in31 = *off
     c                   eval      *in30 = *off
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      vgkpir_hkod = b1hkod
     c                   movel     c2vare        vgkpir_vare
     c                   eval      vgkpir_lage = p_lage
     c     *dmy          move      b1gdat        vgkpir_gdat
     c     *hms          move      b1gtim        vgkpir_gtim

     c     vgkpir_key    chain     vgkpir
      *
     c                   if        %found
     c                   movel     vgvare        c2vare
     c                   eval      c2busr = vgbusr
     c                   eval      c2enhe = vgenhe
     c                   eval      c2type = vgtype
     c                   eval      c2cost = vgcost
     c                   eval      c2type = vgtype
     c                   eval      c2best = vgbest
     c                   eval      c2suff = vgsuff
     c                   eval      c2line = vgline
     c                   eval      c2gmkp = vggmkp
     c                   eval      c2inkp = vginkp
     c                   eval      c2pkod = '(' + vgpkod + ')'
     c                   eval      c2behl = vgbehl
     c                   eval      c2anta = vganta
     c     *hms          move      vgttim        w_ttim
     c                   movel     w_ttim        c2ttim
     c                   eval      c2type = vgtype
      *
      * Hent varetekst
      *
     c                   movel     c2vare        w_var1
     c                   exsr      les_vare
     c                   eval      c2vtek = vvtek1
      *
      * Dato gjeldene fra
      *
     c                   if        vggdat = *loval
     c                   eval      c2gdat = *zeros
     c                   else
     c     *dmy          move      vggdat        c2gdat
     c                   endif
      *
      * Dato beregnet
     c                   if        vgbdat = *loval
     c                   eval      c2bdat = *zeros
     c                   else
     c     *dmy          move      vgbdat        c2bdat
     c                   endif
      *
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in31 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      vgkpi3
      *
     c                   if        %eof or
     c                             vgfirm <> w_firm or
     c                             vgvare <> p_vare
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      vgkpi3_vare = vgvare
      *
     c                   eval      b1valg = 0
     c                   eval      b1hkod = vghkod
     c                   eval      b1vare = vgvare
     c                   movel     b1vare        w_var1
     c                   exsr      les_vare
     c                   movel     vvtek1        b1vtek
      * Dato gjeldene fra
     c                   if        vggdat <> *loval
     c     *dmy          move      vggdat        b1gdat
     c                   endif
     c                   if        vggtim <> *loval
     c     *hms          move      vggtim        b1gtim
     c                   endif
      *
     c                   eval      b1enhe = vgenhe
     c                   eval      b1type = vgtype
     c                   eval      b1cost = vgcost
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     vgkpi3_key    setgt     vgkpi3
     c                   readp     vgkpi3
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     vgkpi3
      *
     c                   if        %eof or
     c                             vgfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      vgkpi3_vare = vgvare
      *
     c                   enddo
      *
     c                   if        %eof
     c     vgkpi3_key    setll     vgkpi3
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppslag på vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_vare      begsr
      *
     c                   eval      vvarl1_vare = w_var1
     c     vvarl1_key    chain     vvarl1r
     c                   if        %found
     c                   eval      b_feil = *off
     c                   else
     c                   eval      b_feil = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_vare
     c                   parm                    p_lage
      *
      * Key for oppslag av kostpris
      *
     c     vgkpi3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vgkpi3_lage
     c                   kfld                    vgkpi3_vare
      *
      * Key for les av kostpris
      *
     c     vgkpir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vgkpir_lage
     c                   kfld                    vgkpir_hkod
     c                   kfld                    vgkpir_vare
     c                   kfld                    vgkpir_gdat
     c                   kfld                    vgkpir_gtim
      *
      *  Key for file les av vareregister
      *
     c     vvarl1_key    KLIST
     c                   KFLD                    w_firm
     c                   KFLD                    vvarl1_vare
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      vgkpi3_vare = p_vare
     c                   eval      vgkpi3_lage = p_lage
     c     vgkpi3_key    setll     vgkpi3
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
