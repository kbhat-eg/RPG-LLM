     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VP670R
      * Beskrivelse . . : Vare-pris, prisfil til SmartKalk - styreprogram
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       120422 bsa  Nytt program, basert på VP686R
      *
8.01  *K000  080422 bsa NXS-10275 Utvidet med GLN nummer for lager og filnavn
8.02  *K000  080422 bsa Utvidet med parameter kun nobbnummer
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffkdil1    if   e           k disk    rename(fkdipfr:fkdil1r)
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_rkat          s              3  0
     d p_kund          s              6  0
     d p_kpro          s              6  0
     d p_rfmt          s             10
     d p_vsor          s             10
     d p_ogrp          s              2  0
     d p_hgrp          s              2  0
     d p_ugrp          s              3  0
     d p_ldor          s              6  0
     d p_rfm2          s             10
     d p_sval          s              1  0
     d p_tval          s              1  0
     d p_2val          s              1  0
     d p_bval          s              1  0
     d p_lety          s              1  0
     d p_dato          s               d   datfmt(*iso)
     d p_lage          s              2  0
8.02 d p_nval          s              1  0
8.01 d p_lgln          s             13  0
      *
      * Definering av variabler i nøkler
      *
     d fkdil1_kund     s                   like(fhkund)
     d fkdil1_kpro     s                   like(fhkpro)
      *
      * Definering av variable
      *
     d w_vsor          s             10
     d w_ogrp          s              2  0
     d w_hgrp          s              2  0
     d w_ugrp          s              3  0
     d w_ldor          s              6  0
     d w_rfmt          s             10
     d w_sval          s              1  0
     d w_tval          s              1  0
     d w_2val          s              1  0
     d w_bval          s              1  0
     d w_dato          s               d   datfmt(*iso)
     d w_tmst          s               z
     d w_tmst_20       s             20
     d w_memb          s             10
      *
     d w_firm          s                   like(fhfirm)
     d w_kund          s                   like(fhkund)
     d w_kpro          s                   like(fhkpro)
     d w_lety          s                   like(fhlety)
     d w_ftis          s                   like(fhftis)
     d w_endr          s                   like(fhendr)
     d w_lage          s              2  0
8.01 d w_nval          s              1  0
8.01 d w_lgln          s             13  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandling avhengig av valgte parametre
      *
     c                   select
     c                   when      p_kpro <> 0
     c                   exsr      behandl_kpro
     c                   when      p_kund <> 0
     c                   exsr      behandl_kund
     c                   endsl
      *
      * Avslutt
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av utplukk på kundeprosjekt
      *  - sjekker distribusjonsliste på kundeprosjekt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_kpro  begsr
      *
     c                   eval      fkdil1_kund = p_kund
     c                   eval      fkdil1_kpro = p_kpro
     c     fkdil1_key1   chain     fkdil1
      *
     c                   eval      w_vsor = p_vsor
     c                   eval      w_lety = p_lety
     c                   eval      w_rfmt = p_rfm2
7.03 c                   eval      w_sval = p_sval
7.03 c                   eval      w_tval = p_tval
     c                   eval      w_lage = p_lage
      *
      * Bruker paramtre fra distribusjonsliste om disse er utfylt
      *
     c                   if        %found
     c                   if        fhvsor <> *blank
     c                   eval      w_vsor = fhvsor
     c                   endif
      *
     c                   eval      w_lety = fhlety
7.03 c                   eval      w_sval = fhsval
7.03 c                   eval      w_tval = fhtval
      *
     c                   if        fhrfmt <> *blank
     c                   eval      w_rfmt = fhrfmt
     c                   endif
     c                   endif
      *
     c                   exsr      prisfil
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av utplukk på kunde
      *  - sjekker distribusjonsliste på kunde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_kund  begsr
      *
     c                   eval      fkdil1_kund = p_kund
     c     fkdil1_key2   chain     fkdil1
      *
     c                   eval      w_vsor = p_vsor
     c                   eval      w_lety = p_lety
     c                   eval      w_rfmt = p_rfm2
     c                   eval      w_lage = p_lage
7.03 c                   eval      w_sval = p_sval
7.03 c                   eval      w_tval = p_tval
      *
      * Bruker paramtre fra distribusjonsliste om disse er utfylt
      *
     c                   if        %found
     c                   if        fhvsor <> *blank
     c                   eval      w_vsor = fhvsor
     c                   endif
      *
     c                   eval      w_lety = fhlety
7.03 c                   eval      w_sval = fhsval
7.03 c                   eval      w_tval = fhtval
      *
     c                   if        fhrfmt <> *blank
     c                   eval      w_rfmt = fhrfmt
     c                   endif
     c                   endif
      *
     c                   exsr      prisfil
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift til prisfil
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     prisfil       begsr
      *
     c                   if        w_rfmt = *blank
     c                   leavesr
     c                   endif
      *
     c                   if        p_ogrp <> 0
     c                   eval      w_vsor = *blank
     c                   endif
      *
     c                   eval      w_kund = p_kund
     c                   eval      w_kpro = p_kpro
     c                   eval      w_ogrp = p_ogrp
     c                   eval      w_hgrp = p_hgrp
     c                   eval      w_ugrp = p_ugrp
     c                   eval      w_ldor = p_ldor
7.03 c*                   eval      w_sval = p_sval
7.03 c*                   eval      w_tval = p_tval
     c                   eval      w_2val = p_2val
     c                   eval      w_bval = p_bval
     c                   eval      w_dato = p_dato
     c                   eval      w_endr = 0
      *
     c                   time                    w_tmst
     c     *iso0         move      w_tmst        w_tmst_20
      *
     c                   eval      w_memb = 'P' + %subst(w_tmst_20:9:9)
      *
     c                   call      'VP671C'
     c                   parm                    w_firm
     c                   parm                    w_kund
     c                   parm                    w_kpro
     c                   parm                    w_vsor
     c                   parm                    w_ogrp
     c                   parm                    w_hgrp
     c                   parm                    w_ugrp
     c                   parm                    w_ldor
     c                   parm                    w_rfmt
     c                   parm                    w_endr
     c                   parm                    fhdato
     c                   parm                    w_sval
     c                   parm                    w_tval
     c                   parm                    w_2val
     c                   parm                    w_bval
     c                   parm                    w_lety
     c                   parm                    w_dato
     c                   parm                    w_lage
     c                   parm                    w_ftis
     c                   parm                    w_memb
8.02 c                   parm                    w_nval
8.01 c                   parm                    w_lgln
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_rkat
     c                   parm                    p_kund
     c                   parm                    p_kpro
     c                   parm                    p_rfmt
     c                   parm                    p_vsor
     c                   parm                    p_ogrp
     c                   parm                    p_hgrp
     c                   parm                    p_ugrp
     c                   parm                    p_ldor
     c                   parm                    p_rfm2
     c                   parm                    p_sval
     c                   parm                    p_tval
     c                   parm                    p_2val
     c                   parm                    p_bval
     c                   parm                    p_lety
     c                   parm                    p_dato
     c                   parm                    p_lage
8.02 c                   parm                    p_nval
8.01 c                   parm                    p_lgln
      *
      * Key for les på kunde distribusjonsregister
      *
     c     fkdil1_key1   klist
     c                   kfld                    w_firm
     c                   kfld                    fkdil1_kund
     c                   kfld                    fkdil1_kpro
      *
     c     fkdil1_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    fkdil1_kund
      *
     c                   eval      w_firm = p_firm
8.01 c                   eval      w_lgln = p_lgln
8.02 c                   eval      w_nval = p_nval
      *
      * Henter timestamp for å benytte denne som unik nøkkel på
      * forsendelsen.
      *
     c                   time                    w_ftis
      *
     c                   endsr
