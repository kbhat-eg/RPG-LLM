     h option(*nodebugio) datedit(*dmy)
     h dftactgrp(*no)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VV770R
      * Beskrivelse . . : Vare, oppdater varetype etter telleresultatr
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       080223 ASK  Nytt program
8.01  *       210525 bof  Legge hinder for at denne skal kunne kjøres NXS-20613
8.01  *                   (testet, men den satt altfor mange til S)
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fltell2    if   e           k disk    rename(ltelpfr:ltell2r)
     flusrl1    if   e           k disk    rename(lusrpfr:lusrl1r)
      *
     fvv770d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d ltell2_batc     s                   like(lebatc)
     d ltell2_numm     s                   like(lenumm)
     d lusrl1_user     s                   like(lbuser)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_stel          s              1  0
     d w_batc          s                   like(lebatc)
     d w_antl          s              2  0
8.01 d w_meld          s             50
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
8.01 c                   eval      w_meld = 'Denne rutinen kan ikke  ' +
8.01 c                                      'kjøres '
8.01 c                   call      'AA005R'
8.01 c                   parm                    w_meld
8.01 c                   goto      avslutt
      * Sjekker om bruker har tilgang til funksjon
      *
     c                   eval      lusrl1_user = l_user
     c     lusrl1_key    chain     lusrl1
     c                   if        not %found or
     c                             lbtans < 2
     c                   exsr      b12msg0
     c                   goto      avslutt
     c                   endif
      *
      * Behandler bildet
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
     c                   if        *inkd = *on
     c                   call      'LC501R'
     c                   parm                    w_firm
     c                   parm                    b1batc
     c                   endif
      *
      * Hent batch info hvis ny batch
      *
     c                   if        b1batc <> w_batc
     c                   exsr      les_batch
     c                   eval      w_batc = b1batc
     c                   goto      b1taga
     c                   endif
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
      * Sjekker antall lager
      *
     c                   exsr      sjekk_lager
      *
      * Kaller oppdateringsprogram
      *
     c                   call      'VV771C'
     c                   parm                    w_firm
     c                   parm                    b1batc
     c                   parm                    b1numm
     c                   parm                    b1lage
     c                   parm                    b1oppd
     c                   parm                    w_antl
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
     c                   if        w_stel = 1
     c                   exsr      b12msg1
     c                   eval      b_feil = *on
     c                   eval      *in31  = *on
     c                   endif
      *
     c                   if        b1batc = *blank or
     c                             b1numm = 0
     c                   eval      b_feil = *on
     c                   eval      *in31  = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   eval      ltell2_batc = b1batc
     c                   eval      ltell2_numm = b1numm
     c     ltell2_key    chain     ltell2                             32
     c                   if        *in32 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent info om valgt batvh
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_batch     begsr
      *
     c/Exec SQL
     c+  select lcnumm, lcstel
     c+    into :b1numm, :w_stel
     c+     from lbchpf
     c+     where lcfirm = :w_firm and lcbatc = :b1batc
     c/End-Exec
      *
     c/Exec SQL
     c+  select distinct lelage
     c+    into :b1lage
     c+     from ltelpf
     c+     where lefirm = :w_firm and lebatc = :b1batc
     c/End-Exec
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * sjekker antall lager på filgruppa
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_lager   begsr
      *
     c/Exec SQL
     c+  select count(*)
     c+    into :w_antl
     c+     from ra10pf
     c+     where rajfir = :w_firm
     c/End-Exec
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Melding om at bruker ikke har tilgang til funksjonen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     b12msg0       begsr
     c                   exfmt     b1msg0
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Melding om at batch med syklisk telling er valgt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     b12msg1       begsr
     c                   exfmt     b1msg1
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på telling
      *
     c     ltell2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ltell2_batc
     c                   kfld                    ltell2_numm
      *
      *  Key for les av BRUKER
      *
     c     lusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lusrl1_user
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
