     h option(*nodebugio) datedit(*dmy)
     h dftactgrp(*no)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : VG701R
      * Beskrivelse . . : Henter ut gjeldende glidende gennomsnittlige kospris pr. dato
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       231118 ASK  Nytt program
8.01  * 8.01  210921 ask  Lagt inn oppslag mot prisgruppe
8.02  * 8.02  080223 ask  Lagt inn utvidet nøkkel indeks
8.03  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvgkpi3    if a e           k disk    rename(vgkpst:vgkpi2r)
     fvvarlr    if a e           k disk    rename(vvarpfr:vvarlrr)
     fvgkkir    if   e           k disk    rename(vgkkst:vgkkirr)
      *
      * Definering av parametre
      *
     d p_vare          s                   like(vgvare)
     d p_lage          s                   like(vglage)
     d p_dato          s              8  0
     d p_cost          s                   like(vgcost)
8.03 d p_prgr          s              2
     d p_lety          s              1  0
     d p_sapr          s              9  2
     d p_gmpr          s              9  2
     d p_kopr          s              9  2
     d p_inpr          s              9  2
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d vgkpi3_lage     s              2  0
     d vgkpi3_vare     s                   like(vgvare)
     d vgkpi3_gdat     s                   like(vggdat)
8.02 d vgkpi3_gtim     s                   like(vggtim)
8.02 d vgkpi3_hkod     s                   like(vghkod)
     d vvarlr_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vgfirm)
     d w_date          s                   like(vggdat)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedrutine - finner aktuell gjennomsnittlig kostpris ut fra dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      vgkpi3_vare = p_vare
     c                   eval      vgkpi3_lage = p_lage
     c                   eval      vgkpi3_gdat = %date(p_dato:*iso)
8.02 c                   eval      vgkpi3_gtim = *hival
8.02 c                   eval      vgkpi3_hkod = ' '
     c     vgkpi3_key    setll     vgkpi3
      *
     c                   read      vgkpi3
      *
     c                   if        %eof or
     c                             vgfirm <> w_firm or
     c                             vgvare <> p_vare or
     c                             vglage <> p_lage
     c                   eval      p_cost = 0
     c                   else
     c                   eval      p_cost = vgcost
     c                   endif
      *
      * Dersom gj.sn. kostpris ikke funnet - hent kalk. kostpris
      *
     c                   if        p_cost = 0
     c                   eval      p_prgr = *blank
     c                   eval      p_lety = 0
8.01 c/Exec SQL
8.01 c+  select vpgprg into :p_prgr
8.01 c+     from vpgrpf
8.01 c+     where vpglag = :p_lage
8.01 c/End-Exec
8.01  *
      *
     c                   eval      w_date = %date(p_dato:*iso)
     c                   eval      vvarlr_vare = p_vare
     c     vvarlr_key    chain     vvarlr
      *
     c                   call      'VP730R'
     c                   parm                    w_firm
     c                   parm                    p_vare
     c                   parm                    p_prgr
     c                   parm                    p_lety
     c                   parm                    w_date
     c                   parm                    vvenhl
     c                   parm                    p_sapr
     c                   parm                    p_gmpr
     c                   parm                    p_kopr
     c                   parm                    p_inpr
     c                   parm                    p_lage
      *
     c                   eval      p_cost = p_kopr
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_vare
     c                   parm                    p_lage
     c                   parm                    p_dato
     c                   parm                    p_cost
      *
      * Key for oppslag av kostpris
      *
     c     vgkpi3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vgkpi3_lage
     c                   kfld                    vgkpi3_vare
     c                   kfld                    vgkpi3_gdat
8.02 c                   kfld                    vgkpi3_gtim
8.02 c                   kfld                    vgkpi3_hkod
      *
      * Key for oppslag på vare
      *
     c     vvarlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlr_vare
      *
      * Key for posisjonering på kodetabell
      *
     c     vgkkir_key    klist
     c                   kfld                    w_firm
      *
     c                   eval      w_firm = l_firm
      *
      *
      * Sjekk om kostpris pr. lager.
      *
     c     vgkkir_key    chain     vgkkir
     c                   if        vgprlg = 'N'
     c                   eval      p_lage = 0
     c                   endif
      *
     c                   endsr
