      /TITLE  ASOFAK: Skriver reklameetiketter
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FP226R
      * Beskrivelse . . : Skriver reklameetiketter
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       020799 KOB  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvparpf    if   f  512    64aidisk
      *
     fEtikett   o    f  132        Printer oflind(*inof)
     *
     *+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     *               :  LOCAL DATA AREA                                 +
     *+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     *
     D                uds
     *
     d lcskty                110    110  0
     d lcuser                911    920
     d lcfirm                944    946  0
     d l_fnav                951    980
     *
      * Definering av variable
      *
     d w_skty          s                   like(lcskty)
      *
     d w_anta_1        s              1  0
     d w_anta_2        s              2  0
     d w_anta_3        s              3  0
      *
     ivparpf    ns  01
      *
     i                                  1    4 0vaanta
     i                                  5   19  vatek1
     i                                 20   34  vatek2
     i                                 35   49  vatek3
     i                                 50   64  vatek4
      *
     i                                256  256  vadumy
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   read      vparpf                                 61
      *
     c                   dow       *in61 = *off
      *
     c                   if        *in60 = *off
      *
      * Hent skriverinformasjon
      *
     c                   eval      w_skty = lcskty
      *
     c     w_skty        ifeq      0
     c                   except    Æclr0
     c                   endif
      *
     C     w_skty        ifeq      1
     c                   except    Æclr1
     c                   endif
      *
     c     w_skty        ifeq      2
     c                   except    Æpgm2
     c                   endif
      *
     c                   eval      *in60 = *on
      *
     c                   endif
      *
     c     w_skty        ifeq      0
     c                   except    Æpgm0
     c                   endif
      *
     c     w_skty        ifeq      1
     c                   except    Æpgm1
     c                   endif
      *
      * Skriv avhengig av skrivertype
      *
     c     w_skty        ifeq      0
     c                   except    Ædata0
     c                   endif
      *
     c     w_skty        ifeq      1
     c                   except    Ædata1
     c                   endif
      *
     c     w_skty        ifeq      2
      *
     c                   eval      *in81 = *off
     c                   eval      *in82 = *off
     c                   eval      *in83 = *off
      *
     c                   select
     c                   when      vaanta <= 9
     c                   eval      w_anta_1 = vaanta
     c                   eval      *in81 = *on
     c                   when      vaanta <= 99
     c                   eval      w_anta_2 = vaanta
     c                   eval      *in82 = *on
     c                   when      vaanta <= 500
     c                   eval      w_anta_3 = vaanta
     c                   eval      *in83 = *on
     c                   other
     c                   eval      w_anta_3 = 500
     c                   eval      *in83 = *on
     c                   endsl
      *
     c                   except    Ædata2
     c                   endif
      *
     c                   read      vparpf                                 61
     c                   enddo
      *
     c                   goto      avslutt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c     w_skty        ifeq      2
     c                   except    Æend2
     c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c                   endsr
      *
      * Twinax-skriver
      *                                                                ***
      *
      * Halo printer here am I, are you ready?
      *
     oEtikett   e            Æclr0
     O                                              '&02&20&03'
      *
     *Clear LLU 000002
      *
     O          e            Æclr0
     O                                              '&02&53000002&03'
      *
     *Clear PGM-buffer 02
      *
     O          e            Æclr0
     O                                              '&02&5402&03'
      *
     * Skriv program
      *
     O          e            Æpgm0
     O                                              '&02&4402032'
     O                                              '&04'
      *
      * Tekst 1
      *
     O          e            Æpgm0
     O                                              '&02'
     O                                              '&0D005020015012322'
     O                                              '&04'
      *
      * Tekst 2
      *
     O          e            Æpgm0
     O                                              '&02'
     O                                              '&0D005015015012322'
     O                                              '&04'
      *
      * Tekst 3
      *
     O          e            Æpgm0
     O                                              '&02'
     O                                              '&0D005010015012322'
     O                                              '&04'
      *
      * Tekst 4
      *
     O          e            Æpgm0
     O                                              '&02'
     O                                              '&0D005005015012322'
     O                                              '&04'
      *
      * Avslutt
      *
     O          e            Æpgm0
     O                                              '&02'
     O                                              '&0D'
     O                                              '&0D'
     O                                              '&0D'
     O                                              '&0D'
     O                                              '&03'
      *
     * Skriv data
      *
     O          e            Ædata0
     O                                              '&02&4300000202&0D'
     O                                              '&04'
      *
      * Tekst 1
      *
     O          e            Ædata0
     O                                              '&02'
     O                       vatek1
     O                                              '&0D'
     O                                              '&04'
      *
      * Tekst 2
      *
     O          e            Ædata0
     O                                              '&02'
     O                       vatek2
     O                                              '&0D'
     O                                              '&04'
      *
      * Tekst 3
      *
     O          e            Ædata0
     O                                              '&02'
     O                       vatek3
     O                                              '&0D'
     O                                              '&04'
      *
      * Teskt 4
      *
     O          e            Ædata0
     O                                              '&02'
     O                       vatek4
     O                                              '&0D'
     O                                              '&04'
      *
      * Avslutt
      *
     O          e            Ædata0
     O                                              '&02'
     O                                              '&0D'
     O                                              '&03'
      *
     * Antall
      *
     O          e            Ædata0
     O                                              '&02&66'
     O                       vaanta
     O                                              '&03'
      *
      * Behandle standard-skriver
      *
      *
     * Halo printer here am I, are you ready?
      *
     OEtikett   e            Æclr1
     O                                              '{ & } '
      *
     *Clear LLU 000002
      *
     O          e            Æclr1
     O                                              '{&S000002} '
      *
     *Clear PGM-buffer 02
      *
     O          e            Æclr1
     O                                              '{&T02} '
      *
     * Write program
      *
     O          e            Æpgm1
     O                                              '{&D02032'
     O                                              '! '
      *
      * Tekst 1
      *
     O          e            Æpgm1
     O                                              '{'
     O                                              '\005020015012322'
     O                                              '! '
      *
      * Tekst2
      *
     O          e            Æpgm1
     O                                              '{'
     O                                              '\005015015012322'
     O                                              '! '
      *
      * Tekst 3
      *
     O          e            Æpgm1
     O                                              '{'
     O                                              '\005005015012322'
     O                                              '! '
      *
      * Tekst 4
      *
     O          e            Æpgm1
     O                                              '{'
     O                                              '\005010015012322'
     O                                              '! '
      *
      *
      *
     O          e            Æpgm1
     O                                              '{'
     O                                              '\'
     O                                              '\'
     O                                              '\'
     O                                              '\'
     O                                              '\'
     O                                              '} '
      *
     * Write data to  LLU-000002
      *
     O          e            Ædata1
     O                                              '{&C00000202\'
     O                                              '! '
      *
      * Tekst1
      *
     O          e            Ædata1
     O                                              '{'
     O                       vatek1
     O                                              '\'
     O                                              '! '
      *
      * Tekst 2
      *
     O          e            Ædata1
     O                                              '{'
     O                       vatek2
     O                                              '\'
     O                                              '! '
      *
      * Tekst 3
      *
     O          e            Ædata1
     O                                              '{'
     O                       vatek3
     O                                              '\'
     O                                              '! '
      *
      * Tekst 4
      *
     O          e            Ædata1
     O                                              '{'
     O                       vatek4
     O                                              '\'
     O                                              '! '
      *
      * Avslutt
      *
     O          e            Ædata1
     O                                              '{'
     O                                              '\'
     O                                              '} '
      *
     * Antall
      *
     O          e            Ædata1
     O                                              '{&f'
     O                       vaanta
     O                                              '} '
      *
      * Behandle Bandit-skriver
      *                                                                ***
     oEtikett   e            Æpgm2
     O                                              '{'
      *
     O          e            Æpgm2
     O                                              '[Ct "0"]'
      *
     O          e            Æpgm2
     O                                              '[N m "m"]'
      *
     O          e            Æpgm2
     O                                              '[M n "swpaper"]'
      *
     O          e            Æpgm2
     O                                              '[M w "49.0:0:0:1"]'
      *
     O          e            Æpgm2
     O                                              '[M l "68.0:0:0:1:direct '
     O                                              'table 100"]'
      *
     O          e            Æpgm2
     O                                              '[M c "0"]'
      *
     O          e            Æpgm2
     O                                              '[M m "0:00:1"]'
      *
     O          e            Æpgm2
     O                                              '[M a "swpaper"]'
      *
     O          e            Æpgm2
     O                                              '[D W "1" "8" "3" '
     O                                              '"swpaper" "0,0:0,0" "\N"'
      *
      * Avslutt innhold
      *
     O          e            Æpgm2
     O                                              '"\N"]'
      *
      * Etiketter
      *
     O          e            Ædata2
     O                                              '[D W "1" "8" "3" '
     O                                              '"swpaper" "0,0:0,0" "\N"'
      *
      * Tekst 1
      *
     O          e            Ædata2
     O                                              '"C" "07.0:12.0:1" "90" '
     O                                              '"15531807:13" "15:0" '
     O                                              '"" "" "1:1:2:1:1" "\N"'
      *
      * Tekst 2
      *
     O          e            Ædata2
     O                                              '"C" "07.0:12.0:1" "90" '
     O                                              '"15531807:13" "15:0" '
     O                                              '"" "" "1:1:2:1:1" "\N"'
      *
      * Tekst 3
      *
     O          e            Ædata2
     O                                              '"C" "07.0:12.0:1" "90" '
     O                                              '"15531807:13" "15:0" '
     O                                              '"" "" "1:1:2:1:1" "\N"'
      *
      * Tekst 4
      *
     O          e            Ædata2
     O                                              '"C" "07.0:12.0:1" "90" '
     O                                              '"15531807:13" "15:0" '
     O                                              '"" "" "1:1:2:1:1" "\N"'
      *
      * Avslutt program
      *
     O          e            Ædata2
     O                                              '"\N"]'
      *
      * Start innhold
      *
     O          e            Ædata2
     O                                              '[ D w "LLU" "1" "\N"'
      *
      * Tekst 1
      *
     O          e            Ædata2
     O                                              '"'
     O                       vatek1
     O                                              '"'
      *
      * Tekst 2
      *
     O          e            Ædata2
     O                                              '"'
     O                       vatek2
     O                                              '"'
      *
      * Tekst 3
      *
     O          e            Ædata2
     O                                              '"'
     O                       vatek3
     O                                              '"'
      *
      * Tekst 4
      *
     O          e            Ædata2
     O                                              '"'
     O                       vatek4
     O                                              '"'
      *
      * Avslutt innhold
      *
     O          e            Ædata2
     O                                              '"\N"]'
      *
      * Antall etiketter
      *
     O          e            Ædata2
     O                                            6 '[P b "'
     O               81      w_anta_1             7
     O               82      w_anta_2             8
     O               83      w_anta_3             9
     O               81                          19 ':1:swpaper"]'
     O               82                          20 ':1:swpaper"]'
     O               83                          21 ':1:swpaper"]'
      *
     O          e            Æend2
     O                                              '}'
