     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : EM112R
      * Beskrivelse . . : Bonus, oppdaterer kundekategori med bonusmal
      *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       080911 bsa  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fekboiu    uf a e           k disk    rename(ekbost:ekboiur)
     fekboic    uf a e           k disk    rename(ekbost:ekboicr)
     fekbeiu    uf a e           k disk    rename(ekbest:ekbeiur)
     fekbtiu    uf a e           k disk    rename(ekbtst:ekbtiur)
     fembeir    if   e           k disk    rename(embest:embeirr)
     fembdir    if   e           k disk    rename(embdst:embdirr)
     fembhi1    if   e           k disk    rename(embhst:embhi1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
      *
      * Definering av Local data area
      *
     d                uds
     d  l_dato               101    108
     d  l_tek1               201    270
     d  l_tek2               301    370
     d  l_tek3               401    470
     d  l_user               911    920
     d  l_firm               944    946  0
     d  l_fnav               951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d embeir_ebot     s                   like(emebot)
     d embeir_emnr     s                   like(ememnr)
      *
     d embdir_dbot     s                   like(emdbot)
     d embdir_dmnr     s                   like(emdmnr)
      *
     d embdi2_dbot     s                   like(emdbot)
     d embdi2_dmnr     s                   like(emdmnr)
     d embdi2_dogr     s                   like(emdogr)
     d embdi2_dhgr     s                   like(emdhgr)
     d embdi2_dugr     s                   like(emdugr)
     d embdi2_dldo     s                   like(emdldo)
     d embdi2_dmod     s                   like(emdmod)
     d embdi2_dvar     s                   like(emdvar)
     d embdi2_denh     s                   like(emdenh)
     d embdi2_dlet     s                   like(emdlet)
     d embdi2_dfda     s                   like(emdfda)
     d embdi2_dtda     s                   like(emdtda)
      *
     d ekboiu_bkun     s                   like(ekbkun)
     d ekboiu_rkat     s                   like(ekrkat)
     d ekboiu_kund     s                   like(ekkund)
     d ekboiu_kpro     s                   like(ekkpro)
     d ekboiu_ogrp     s                   like(ekogrp)
     d ekboiu_hgrp     s                   like(ekhgrp)
     d ekboiu_ugrp     s                   like(ekugrp)
     d ekboiu_ldor     s                   like(ekldor)
     d ekboiu_modn     s                   like(ekmodn)
     d ekboiu_vare     s                   like(ekvare)
     d ekboiu_enhe     s                   like(ekenhe)
     d ekboiu_lety     s                   like(eklety)
     d ekboiu_boty     s                   like(ekboty)
     d ekboiu_fdat     s                   like(ekfdat)
     d ekboiu_tdat     s                   like(ektdat)
      *
     d ekboic_bkun     s                   like(ekbkun)
     d ekboic_boty     s                   like(ekboty)
      *
     d ekbeiu_ebku     s                   like(ekebku)
     d ekbeiu_erka     s                   like(ekerka)
     d ekbeiu_ekun     s                   like(ekekun)
     d ekbeiu_ekpr     s                   like(ekekpr)
     d ekbeiu_eogr     s                   like(ekeogr)
     d ekbeiu_ehgr     s                   like(ekehgr)
     d ekbeiu_eugr     s                   like(ekeugr)
     d ekbeiu_eldo     s                   like(ekeldo)
     d ekbeiu_emod     s                   like(ekemod)
     d ekbeiu_evar     s                   like(ekevar)
     d ekbeiu_eenh     s                   like(ekeenh)
     d ekbeiu_elet     s                   like(ekelet)
     d ekbeiu_ebot     s                   like(ekebot)
     d ekbeiu_efda     s                   like(ekefda)
     d ekbeiu_etda     s                   like(eketda)
      *
     d ekbtiu_tbku     s                   like(ektbku)
     d ekbtiu_tbot     s                   like(ektbot)
      *
     d rkunl1_kund     s                   like(rkkund)
      *
     d embhi1_hbot     s                   like(emhbot)
     d embhi1_hmnr     s                   like(emhmnr)
      *
      * Definering av variable
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_oppr          s              1    inz(*off)
     d b_bonus         s              1    inz(*off)
      *
     d w_udat          s               d   datfmt(*iso)
     d w_udat_8        s              8
     d w_firm_alf      s              3
     d w_kund_alf      s              6
     d w_paar_alf      s              4
     d w_m1ar          s              4  0
     d w_naar          s              4  0
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_type          s              2
     d w_fast          s             10
     d w_numm          s              6  0
     d w_kode          s              1
     d w_fell          s              6
     d w_hfra          s                   like(emefda)
     d w_htil          s                   like(emefda)
      *
     d w_firm          s                   like(emefir)
      *
     d p_kund          s                   like(ekbkun)
     d p_hmnr          s                   like(ememnr)
     d p_tbot          s                   like(emebot)
     d p_hbka          s                   like(emhbka)
     d p_hfra          s              6  0
     d p_htil          s              6  0
      *
      * Definering av konstanter
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å kopiere av bonusmal
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      b_bonus = *off
      * Sjekk om bonusmal inneholder bonuskategorier som er angitt
     c                   eval      embhi1_hbot = p_tbot
     c                   eval      embhi1_hmnr = p_hmnr
     c     embhi1_key    chain     embhi1
     c                   if        emhbka = p_hbka
     c                   if        p_hfra <> 0
     c     *dmy          move      p_hfra        w_hfra
     c                   else
     c                   eval      w_hfra = *loval
     c                   endif
     c                   if        p_htil <> 0
     c     *dmy          move      p_htil        w_htil
     c                   else
     c                   eval      w_htil = *loval
     c                   endif
     c                   exsr      del_bonus
     c                   exsr      add_bonus
     c                   endif
      *
     c                   eval      *inlr = *on
      *--------------------------------------------------------------------
      * Fjerner gamle poster fra kunde på bonustype som ikke er sperret   -
      *--------------------------------------------------------------------
     c     del_bonus     Begsr
      *
      * Fjern alle gamle poster på kunden med valgt bonustype
      *
     c                   eval      ekboic_bkun = p_kund
     c                   eval      ekboic_boty = p_tbot
     c     ekboic_key    setll     ekboic
     c     ekboic_key    reade     ekboic
     c                   dow       not %eof(ekboic)
      * Kan ikke fjernes hvis sperrekode er satt på elementet
     c                   if        eksper <> 1
      *
      * Fjerner bonusdetaljer på kunde
      *
     c                   eval      ekbeiu_ebku = p_kund
     c                   eval      ekbeiu_erka = ekrkat
     c                   eval      ekbeiu_ekun = ekkund
     c                   eval      ekbeiu_ekpr = ekkpro
     c                   eval      ekbeiu_eogr = ekogrp
     c                   eval      ekbeiu_ehgr = ekhgrp
     c                   eval      ekbeiu_eugr = ekugrp
     c                   eval      ekbeiu_eldo = ekldor
     c                   eval      ekbeiu_emod = ekmodn
     c                   eval      ekbeiu_evar = ekvare
     c                   eval      ekbeiu_eenh = ekenhe
     c                   eval      ekbeiu_elet = eklety
     c                   eval      ekbeiu_ebot = ekboty
     c                   eval      ekbeiu_efda = ekfdat
     c                   eval      ekbeiu_etda = ektdat
     c     ekbeiu_key    setll     ekbeiu
     c     ekbeiu_key    reade     ekbeiu
     c                   dow       not %eof(ekbeiu)
     c                   delete    ekbeiur
     c     ekbeiu_key    reade     ekbeiu
     c                   enddo
      *
     c                   delete    ekboicr
     c                   endif
      *
     c     ekboic_key    reade     ekboic
     c                   enddo
      *
     c                   endsr
      *--------------------------------------------------------------------
      * Leser informasjon fra malen                                       *
      *--------------------------------------------------------------------
     c     add_bonus     Begsr
      *
     c                   eval      embeir_ebot = p_tbot
     c                   eval      embeir_emnr = p_hmnr
      *
     c     embeir_key    setll     embeir
     c     embeir_key    reade     embeir
     c                   dow       not %eof(embeir)
      *
      * Så legger vi ut nye poster/oppdaterer
      *
     c                   eval      ekboiu_bkun = p_kund
     c                   eval      ekboiu_boty = p_tbot
     c                   eval      ekboiu_rkat = 0
     c                   eval      ekboiu_kund = 0
     c                   eval      ekboiu_kpro = 0
     c                   eval      ekboiu_ogrp = emeogr
     c                   eval      ekboiu_hgrp = emehgr
     c                   eval      ekboiu_ugrp = emeugr
     c                   eval      ekboiu_ldor = emeldo
     c                   eval      ekboiu_modn = ememod
     c                   eval      ekboiu_vare = emevar
     c                   eval      ekboiu_enhe = emeenh
     c                   eval      ekboiu_lety = emelet
     c**                 eval      ekboiu_fdat = emefda
     c**                 eval      ekboiu_tdat = emetda
      * Hvis valg dato fra oppdatering, benyttes denne
     c                   if        p_hfra <> 0
     c                   eval      ekboiu_fdat = w_hfra
     c                   else
     c                   eval      ekboiu_fdat = emefda
     c                   endif
     c                   if        p_htil <> 0
     c                   eval      ekboiu_tdat = w_htil
     c                   else
     c                   eval      ekboiu_tdat = emetda
     c                   endif
      *
     c     ekboiu_key2   chain     ekboiu
      * Hvis sperret skal det ikke oppdateres
     c                   if        %found(ekboiu) and
     c                             eksper = 1
     c                   goto      neste
     c                   endif
     c                   eval      ekmerk = ememrk
     c                   eval      ekebyg = emebyg
     c                   eval      ekeusr = l_user
      *
     c                   time                    eketim
     c                   time                    ekedat
      *
     c                   if        %found(ekboiu)
     c                   update    ekboiur
     c                   else
     c                   eval      ekfirm = w_firm
     c                   eval      ekboty = p_tbot
     c                   eval      ekbkun = p_kund
     c                   eval      ekrkat = 0
     c                   eval      ekkund = 0
     c                   eval      ekkpro = 0
     c                   eval      eksper = 0
     c                   eval      ekogrp = emeogr
     c                   eval      ekhgrp = emehgr
     c                   eval      ekugrp = emeugr
     c                   eval      ekldor = emeldo
     c                   eval      ekmodn = ememod
     c                   eval      ekvare = emevar
     c                   eval      ekenhe = emeenh
     c                   eval      eklety = emelet
      * Hvis valg dato fra oppdatering, benyttes denne
     c                   if        p_hfra <> 0
     c                   eval      ekfdat = w_hfra
     c                   else
     c                   eval      ekfdat = emefda
     c                   endif
     c                   if        p_htil <> 0
     c                   eval      ektdat = w_htil
     c                   else
     c                   eval      ektdat = emetda
     c                   endif
      *
     c                   eval      ekousr = l_user
     c                   time                    ekotim
     c                   time                    ekodat
     c                   write     ekboiur
      * Ny bonus er lagt ut
     c                   eval      b_bonus = *on
     c                   endif
      *
      * Legger ut nye bonusdetaljer (hent info fra malen)
      *
     c                   eval      embdi2_dbot = emebot
     c                   eval      embdi2_dmnr = p_hmnr
     c                   eval      embdi2_dogr = emeogr
     c                   eval      embdi2_dhgr = emehgr
     c                   eval      embdi2_dugr = emeugr
     c                   eval      embdi2_dldo = emeldo
     c                   eval      embdi2_dmod = ememod
     c                   eval      embdi2_dvar = emevar
     c                   eval      embdi2_denh = emeenh
     c                   eval      embdi2_dlet = emelet
     c**                 eval      embdi2_dfda = emefda
     c**                 eval      embdi2_dtda = emetda
      * Hvis valg dato fra oppdatering, benyttes denne
     c                   if        p_hfra <> 0
     c                   eval      embdi2_dfda = w_hfra
     c                   else
     c                   eval      embdi2_dfda = emefda
     c                   endif
     c                   if        p_htil <> 0
     c                   eval      embdi2_dtda = w_htil
     c                   else
     c                   eval      embdi2_dtda = emetda
     c                   endif
     c     embdir_key2   setll     embdir
     c     embdir_key2   reade     embdir
      * Les gjennom alle detaljer, og legge de ut
     c                   dow       not %eof(embdir)
     c                   eval      ekefir = w_firm
     c                   eval      ekebku = p_kund
     c                   eval      ekerka = 0
     c                   eval      ekekun = 0
     c                   eval      ekekpr = 0
     c                   eval      ekeogr = emdogr
     c                   eval      ekehgr = emdhgr
     c                   eval      ekeugr = emdugr
     c                   eval      ekeldo = emdldo
     c                   eval      ekemod = emdmod
     c                   eval      ekevar = emdvar
     c                   eval      ekeenh = emdenh
     c                   eval      ekelet = emdlet
     c                   eval      ekebot = emdbot
     c**                 eval      ekefda = emdfda
     c**                 eval      eketda = emdtda
      * Hvis valg dato fra oppdatering, benyttes denne
     c                   if        p_hfra <> 0
     c                   eval      ekefda = w_hfra
     c                   else
     c                   eval      ekefda = emdfda
     c                   endif
     c                   if        p_htil <> 0
     c                   eval      eketda = w_htil
     c                   else
     c                   eval      eketda = emdtda
     c                   endif
     c                   eval      ekebgf = emdbgf
     c                   eval      ekebgt = emdbgt
      *
     c                   eval      ekemrk = emdmrk
     c                   eval      ekektb = emdktb
     c                   eval      ekebpr = emdbpr
     c                   eval      ekesbo = 0
     c                   eval      ekebda = *loval
     c                   eval      ekebti = *loval
     c                   eval      ekeous = l_user
      *
     c                   time                    ekeoda
     c                   time                    ekeoti
     c                   eval      ekeeus = l_user
     c                   time                    ekeeda
     c                   time                    ekeeti
     c                   write     ekbeiur
      *
     c     embdir_key2   reade     embdir
     c                   enddo
      *
     c     neste         tag
      *
     c     embeir_key    reade     embeir
     c                   enddo
      * Sjekke om bonustypen som er lagt ut finnes på kunde
     c                   if        b_bonus = *on
      * Hent informasjon fra kunderegister
     c                   eval      rkunl1_kund = p_kund
     c     rkunl1_key    chain     rkunl1
      *
     c     ekbtiu_key    chain     ekbtiu
     c                   eval      ekbtiu_tbku = p_kund
     c                   eval      ekbtiu_tbot = p_tbot
     c     ekbtiu_key    chain     ekbtiu
     c                   if        not %found
     c                   clear                   ekbtiur
     c                   eval      ektfir = w_firm
     c                   eval      ektbku = p_kund
     c                   eval      ektbot = p_tbot
     c                   eval      ektkka = rkkatg
      *
     c                   time                    ekeoda
     c                   time                    ekeoti
     c                   eval      ekeeus = l_user
     c                   time                    ekeeda
     c                   time                    ekeeti
     c                   write     ekbtiur
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    p_kund
     c                   parm                    p_tbot
     c                   parm                    p_hmnr
     c                   parm                    p_hbka
     c                   parm                    p_hfra
     c                   parm                    p_htil
      *
      * Key for oppslag på bonusregister - elementer
      *
     c     embeir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    embeir_ebot
     c                   kfld                    embeir_emnr
      *
      * Key for oppslag på bonusregister - elementdetaljer
      *
     c     embdir_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    embdi2_dbot
     c                   kfld                    embdi2_dmnr
     c                   kfld                    embdi2_dogr
     c                   kfld                    embdi2_dhgr
     c                   kfld                    embdi2_dugr
     c                   kfld                    embdi2_dldo
     c                   kfld                    embdi2_dmod
     c                   kfld                    embdi2_dvar
     c                   kfld                    embdi2_denh
     c                   kfld                    embdi2_dlet
     c                   kfld                    embdi2_dfda
     c                   kfld                    embdi2_dtda
      *
      * Key for oppslag på kundebonus
      *
     c     ekboiu_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    ekboiu_bkun
     c                   kfld                    ekboiu_rkat
     c                   kfld                    ekboiu_kund
     c                   kfld                    ekboiu_kpro
     c                   kfld                    ekboiu_ogrp
     c                   kfld                    ekboiu_hgrp
     c                   kfld                    ekboiu_ugrp
     c                   kfld                    ekboiu_ldor
     c                   kfld                    ekboiu_modn
     c                   kfld                    ekboiu_vare
     c                   kfld                    ekboiu_enhe
     c                   kfld                    ekboiu_lety
     c                   kfld                    ekboiu_boty
     c                   kfld                    ekboiu_fdat
     c                   kfld                    ekboiu_tdat
      *
      * Key for oppslag på kundebonus
      *
     c     ekboic_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ekboic_bkun
     c                   kfld                    ekboic_boty
      *
      * Key for skriv på kundebonus-elment
      *
     c     ekbeiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ekbeiu_ebku
     c                   kfld                    ekbeiu_erka
     c                   kfld                    ekbeiu_ekun
     c                   kfld                    ekbeiu_ekpr
     c                   kfld                    ekbeiu_eogr
     c                   kfld                    ekbeiu_ehgr
     c                   kfld                    ekbeiu_eugr
     c                   kfld                    ekbeiu_eldo
     c                   kfld                    ekbeiu_emod
     c                   kfld                    ekbeiu_evar
     c                   kfld                    ekbeiu_eenh
     c                   kfld                    ekbeiu_elet
     c                   kfld                    ekbeiu_ebot
     c                   kfld                    ekbeiu_efda
     c                   kfld                    ekbeiu_etda
      *
      * Key for oppslag på kunde typeregister
      *
     c     ekbtiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ekbtiu_tbku
     c                   kfld                    ekbtiu_tbot
      *
      * Key for oppslag på kunderegister
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på bonusregister - elementer
      *
     c     embhi1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    embhi1_hbot
     c                   kfld                    embhi1_hmnr
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
     c                   time                    w_date
     c                   time                    w_time
      *
     c                   endsr
