     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RX001R                                              *
      * Beskrivelse . . : Oppdaterer saldo på kunde                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      *        130219 ASK  Nytt program
7.00  *K00 sst 061120 sst NXAS-1234 Ta hensyn til ikke lesbare tall  x.xxxxE-12
7.01  *K00 sst 250221 sst           Endret sperring fra RKSPRK til RKKRES v/Roar
7.02  *K00    060421 bof  Utvidet med endret 2dp dato                         *
      *
8.01  *K0000  171121 bsa  Ekstra test for å unngå dump ved "rusk" i data.     *
      *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     fveinpf    if   e             disk
     frkunlu    uf   e           k disk    rename(rkunpfr:rkunlur)
      *
      * Definering av LDA
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Datastruktur for innlesning fra fil
      *
     d d_innf          ds
     d  d_felt                      100
      *
      * Definering av variable i nøkler
      *
     d  rkunlu_kund    s                         like(rkkund)
      *
      * Definering av variable
      *
     d w_firm          s                   like(rkfirm)
     d w_sald          s                   like(rksald)
     d w_salf          s             20  6
7.00 d w_salx          s             30
7.00 d w_sprk          s              1
     d w_kund          s                   like(rkkund)
     d w_start         s              6
     d w_pb            s              2  0
     d w_ps            s              2  0
     d w_lg            s              2  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Leser saldofil fra Visma Business
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   read      veinpf
     c                   dow       not %eof
     c                   eval      d_felt = veinrec
     c                   exsr      oppdat
     c                   read      veinpf
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatering av poster
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdat        begsr
      *
      * Sjekk om startrecord
      *
     c                   eval      w_start = %subst(d_felt:1:1)
     c                   if        w_start < '0'
     c                             or w_start > '9'
     c                   goto      end_oppd
     c                   endif
7.00 c                   eval      w_ps = %scan('       ':d_felt:1)
7.00 c                   eval      %subst(d_felt:w_ps:4) = ';;;;'
      *
      * Finn kundenummer
      *
     c                   eval      w_pb = 1
     c                   eval      w_ps = %scan(';':d_felt:w_pb)
     c                   eval      w_lg = w_ps - w_pb
8.01  *
8.01  * Ugyldig kundenummer
8.01  *
8.01 c                   if        w_lg > 6
8.01 c                   goto      end_oppd
8.01 c                   endif
     c                   eval      w_kund = %dec(%subst(d_felt:w_pb:w_lg):6:0)
      *
      * Finn saldo
      *
     c                   eval      w_pb = w_ps + 1
7.00 c                   eval      w_ps = %scan(';':d_felt:w_pb)
7.00 c                   if        w_ps = 0
     c                   eval      w_ps = %scan(' ':d_felt:w_pb)
7.00 c                   endif
     c                   eval      w_lg = w_ps - w_pb
7.00  *   Sjekk om saldo inneholder E-nn,  da kan vi ikke behandle det
7.00 c                   eval      w_salx = %subst(d_felt:w_pb:w_lg)
7.00 c                   if        %scan('E-':d_felt:w_pb) > 0
7.00 c                   goto      end_oppd
7.00 c                   endif
      *
     c                   if        %subst(d_felt:w_pb:w_lg) = ' '
     c                   eval      w_sald = 0
     c                   else
     c                   eval      w_salf = %dec(%subst(d_felt:w_pb:w_lg):14:6)
     c                   eval(h)   w_sald = w_salf
7.00 c                   endif
7.00  *
7.00  * Finn ev sperrekode
7.00  *
7.00 c                   eval      w_pb = w_ps + 1
7.00 c                   eval      w_ps = %scan(';':d_felt:w_pb)
7.00 c                   if        w_ps = 0
7.00 c                   eval      w_ps = %scan(' ':d_felt:w_pb)
7.00 c                   endif
7.00 c                   eval      w_lg = w_ps - w_pb
7.00 c                   eval      w_sprk = %subst(d_felt:w_pb:w_lg)
      *
      * Oppdaterer kunden med ny saldo
      *
     c                   eval      rkunlu_kund = w_kund
     c     rkunlu_key    chain     rkunlu
     c                   if        %found
     c                   eval      rksald = w_sald
7.00 c                   if        w_sprk = '1'
7.01 c                             and rkkres <> 2
7.01 c                   eval      rkkres = 2
7.00 c                   eval      rkesig = l_user
7.00 c                   time                    rkedat
7.00 c                   time                    rketim
7.02 c                   time                    rk2dpt
7.00 c                   endif
     c                   update    rkunlur
     c                   endif
      *
     c     end_oppd      endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på kunde
      *
     c     rkunlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunlu_kund
      *
     c                   eval      w_firm = l_firm
      *
      *
     c                   endsr
