     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * System  . . . . : ASVADM
      * Program . . . . : JV190R
      * Beskrivelse . . : Vare Egenskap , spørring
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringbeskrrivelse
      * ----- ------ ---- -------------------------------------------------
      *       160624 bof  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
     fjvegi1    if   e           k disk    rename(jvegst:jvegi1r)
      *
     fJV190d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d jvarl3_nobb     s                   like(jvnobb)
     d jvegi1_wgui     s                   like(jvwgui)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_wnob          s                   like(jvwnob)
     d w_wnav          s            256
     d len             s              4  0
      *
     d p_wnob          s                   like(jvwnob)
      *
      *
      * Definering av konstanter
      *
7.xx d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2wgui <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      jvegi1_wgui = b2wgui
     c     jvegi1_key    setll     jvegi1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Type
      *
     c                   if        b2wgui <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      jvegi1_wgui = b2wgui
     c     jvegi1_key    setll     jvegi1
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2wgui = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c2wgui = b1wguih
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      jvegi1_wgui = c2wgui
     c     jvegi1_key    chain     jvegi1
      *
     c                   if        %found
     c                   eval      w_wnav = jvwnav
     c                   eval      c2wgui = jvwgui
     c                   eval      len = %len(jvwnav)
     c                   eval      c2wnav1 = %subst(w_wnav:1:74)
     c                   eval      c2wnav2 = %subst(w_wnav:75:74)
     c                   eval      c2wnav3 = %subst(w_wnav:150:74)
     c                   eval      c2wnav4 = %subst(w_wnav:225:32)
     c*                   if        len < 74
     c*                   eval      c2wnav1 = %subst(w_wnav:1:75 - len)
     c*                   eval      c2wnav2 = *Blank
     c*                   eval      c2wnav3 = *Blank
     c*                   eval      c2wnav4 = *Blank
     c*                   endif
     c*                   if        len > 74 and len < 150
     c*                   eval      c2wnav1 = %subst(w_wnav:1:74)
     c*                   eval      c2wnav2 = %subst(jvwnav:75:150 - len)
     c*                   eval      c2wnav3 = *Blank
     c*                   eval      c2wnav4 = *Blank
     c*                   endif
     c*                   if        len > 150 and len < 225
     c*                   eval      c2wnav1 = %subst(jvwnav:1:74)
     c*                   eval      c2wnav2 = %subst(jvwnav:75:74)
     c*                   eval      c2wnav3 = %subst(jvwnav:150 :225 - len)
     c*                   eval      c2wnav4 = *Blank
     c*                   endif
     c*                   if        len > 225
     c*                   eval      c2wnav1 = %subst(jvwnav:1:74)
     c*                   eval      c2wnav2 = %subst(jvwnav:75:74)
     c*                   eval      c2wnav3 = %subst(jvwnav:150 :74)
     c*                   eval      c2wnav3 = %subst(jvwnav:225 :256 - len)
     c*                   eval      c2wnav4 = *Blank
     c*                   endif
     c                   eval      c2wvrd = jvwvrd
     c                   eval      c2wenh = jvwenh
     c                   if        jvwoda <> *loval
     c     *dmy          move      jvwoda        c2woda
     c                   else
     c                   eval      c2woda = 0
     c                   endif
     c                   if        jvweda <> *loval
     c     *dmy          move      jvweda        c2weda
     c                   else
     c                   eval      c2weda = 0
     c                   endif
     c                   else
     c                   eval      c2wgui = *blank
     c                   eval      c2wnav1 = *blank
     c                   eval      c2wnav2 = *blank
     c                   eval      c2wnav3 = *blank
     c                   eval      c2wnav4 = *blank
     c                   eval      c2wvrd = *blank
     c                   eval      c2wenh = *blank

     c                   eval      c2wusr = *blank
     c                   eval      c2woda = 0
     c                   eval      c2weda = 0
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      jvegi1
      *
     c                   if        %eof or
     c                             jvwnob <> w_wnob
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
      * les vartekst
      *
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      jvegi1_wgui = jvwgui
      *
     c                   eval      b1valg = 0
     c                   eval      b1wgui = jvwgui
     c                   eval      b1wguih = jvwgui
     c                   eval      b1wnav = jvwnav
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     jvegi1_key    setgt     jvegi1
     c                   readp     jvegi1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     jvegi1
      *
     c                   if        %eof or
     c                             jvwnob <> w_wnob
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      jvegi1_wgui = jvwgui
      *
     c                   enddo
      *
     c                   if        %eof
     c     jvegi1_key    setll     jvegi1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_wnob
      *
      * Parametre
      *
      * Key for posisjonering på avgifstkode
      *
     c     jvegi1_key    klist
     c                   kfld                    w_wnob
     c                   kfld                    jvegi1_wgui
      *
      * Key for les av vare
      *
     c     jvarl3_key    klist
     c                   kfld                    jvarl3_nobb
      *
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   movel     p_wnob        jvarl3_nobb
     c     jvarl3_key    chain     jvarl3
     c                   eval      b2wnob = p_wnob
     c                   eval      b2vte2 = jvtek1
     c                   eval      c1wnob = p_wnob
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
7.01 c                   eval      w_wnob = p_wnob
     c     jvegi1_key    setll     jvegi1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
