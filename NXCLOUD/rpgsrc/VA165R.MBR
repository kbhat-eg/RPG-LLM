     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VA165R
      * Beskrivelse . . : Bonustype, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       191005 HKO  Nytt program
6.20  * 6.20  150611 bsa  Utvidet funksjonalitet ifbm v6.20
6.21  * 6.20  220911 bsa  Lagt inn ny funksjonalitet for sumberegning
6.21  *                   av bonussats.
6.22  * 6.20  260911 bsa  Eget vedlikehold for bonussatser pr bonustype
7.xx  * 7.00  281216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 6.20  270214 bsa  Tar med seg alle underliggende nivåer ved
7.01  *                   kopiering av avtaler.
7.02  * 6.30  120517 ask  Lagt inn oppdatering av bonustype undertekster
7.02  *                   for visning i portalen.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvbotl1    if   e           k disk    rename(vbotpfr:vbotl1r)
     fvbotl2    if   e           k disk    rename(vbotpfr:vbotl2r)
     fvbotlr    if   e           k disk    rename(vbotpfr:vbotlrr)
     fvbotlu    uf a e           k disk    rename(vbotpfr:vbotlur)
7.01 fembeiu    uf a e           k disk    rename(embest:embeiur)
7.01 fembeir    if   e           k disk    rename(embest:embeirr)
7.01 f                                     prefix(xy:2)
7.01 fembdiu    uf a e           k disk    rename(embdst:embdiur)
7.01 fembdir    if   e           k disk    rename(embdst:embdirr)
7.01 f                                     prefix(xz:2)
7.01 fembhiu    uf a e           k disk    rename(embhst:embhiur)
7.01 fembhir    if a e           k disk    rename(embhst:embhirr)
7.01 f                                     prefix(xx:2)
7.02 fcbutiu    uf a e           k disk    rename(cbutst:cbutiur)
      *
     fva165d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d vbotl1_tbot     s                   like(vatbot)
     d vbotl2_tbes     s                   like(vatbes)
     d vbotlr_tbot     s                   like(vatbot)
     d vbotlu_tbot     s                   like(vatbot)
      *
7.01 d embhiu_hbot     s                   like(emhbot)
7.01 d embhiu_hmnr     s                   like(emhmnr)
7.01  *
7.01 d embhir_hbot     s                   like(emhbot)
7.01 d embhir_hmnr     s                   like(emhmnr)
7.01  *
7.01 d embeir_ebot     s                   like(emebot)
7.01 d embeir_emnr     s                   like(ememnr)
7.01  *
7.01 d embeiu_ebot     s                   like(emebot)
7.01 d embeiu_emnr     s                   like(ememnr)
7.01 d embeiu_eogr     s                   like(emeogr)
7.01 d embeiu_ehgr     s                   like(emehgr)
7.01 d embeiu_eugr     s                   like(emeugr)
7.01 d embeiu_eldo     s                   like(emeldo)
7.01 d embeiu_emod     s                   like(ememod)
7.01 d embeiu_evar     s                   like(emevar)
7.01 d embeiu_eenh     s                   like(emeenh)
7.01 d embeiu_elet     s                   like(emelet)
7.01 d embeiu_efda     s                   like(emefda)
7.01 d embeiu_etda     s                   like(emetda)
7.01  *
7.01 d embdir_dbot     s                   like(emdbot)
7.01 d embdir_dmnr     s                   like(emdmnr)
7.01  *
7.01 d embdiu_dbot     s                   like(emdbot)
7.01 d embdiu_dmnr     s                   like(emdmnr)
7.01 d embdiu_dogr     s                   like(emdogr)
7.01 d embdiu_dhgr     s                   like(emdhgr)
7.01 d embdiu_dugr     s                   like(emdugr)
7.01 d embdiu_dldo     s                   like(emdldo)
7.01 d embdiu_dmod     s                   like(emdmod)
7.01 d embdiu_dvar     s                   like(emdvar)
7.01 d embdiu_denh     s                   like(emdenh)
7.01 d embdiu_dlet     s                   like(emdlet)
7.01 d embdiu_dfda     s                   like(emdfda)
7.01 d embdiu_dtda     s                   like(emdtda)
7.01 d embdiu_dbgf     s                   like(emdbgf)
7.01 d embdiu_dbgt     s                   like(emdbgt)
7.02 d cbutiu_ttyp     s                   like(cbttyp)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(vatfir)
7.01 d w_kode          s              1
7.01 d w_numm          s              6  0
7.01 d w_type          s              2
7.01 d w_fast          s             10
7.01 d w_fell          s              6
      *
     d s_tbot          s                   like(vatbot)
7.01 d s_tbes          s                   like(vatbes)
      *
6.20 d p_tbot          s                   like(vatbot)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meleingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
6.20 c                   when      *inki
6.20 c                   call      'VA166R'
6.20 c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2tbot <> *blank or
     c                             b2tbes <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      vbotl2_tbes = b1tbes
     c     vbotl2_key    setll     vbotl2
     c                   other
     c                   eval      vbotl1_tbot = b1tbot
     c     vbotl1_key    setll     vbotl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Enhet
      *
     c                   if        b2tbot <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      vbotl1_tbot = b2tbot
     c     vbotl1_key    setll     vbotl1
     c                   goto      end_pos
     c                   endif
      *
      * Beskrivelse
      *
     c                   if        b2tbes <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      vbotl2_tbes = b2tbes
     c     vbotl2_key    setll     vbotl2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2tbot = *blank
     c                   eval      b2tbes = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1tbot = b1tbot
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1tbot = b1tbot
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1tbot = b1tbot
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1tbot = b1tbot
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
6.20  *
6.20  * Malvedlikehold
6.20  *
6.20 c                   if        b1valg = 7
6.20 c                   eval      c1tbot = b1tbot
6.20 c                   exsr      xc7bld
6.20 c                   eval      b1valg = 0
6.20 c                   update    b1sfl
6.20 c                   iter
6.20 c                   endif
6.22  *
6.22  * Bonusstaser vedlikehold
6.22  *
6.22 c                   if        b1valg = 8
6.22 c                   eval      c1tbot = b1tbot
6.22 c                   exsr      xc8bld
6.22 c                   eval      b1valg = 0
6.22 c                   update    b1sfl
6.22 c                   iter
6.22 c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1tbot = b2tbot
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
     c                   if        c1tbot = *blank
     c                   goto      end_c1bld
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      vbotlr_tbot = c1tbot
     c     vbotlr_key    chain     vbotlr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1tbot = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2tbot = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      vbotlr_tbot = c1tbot
     c     vbotlr_key    chain     vbotlr
      *
     c                   if        %found
     c                   eval      c2tbes = vatbes
     c                   eval      c2teus = vateus
6.21 c                   eval      c2bsum = vatsum
6.21 c                   eval      c2bred = vatred
     c     *dmy          move      vateda        c2teda
     c     *dmy          move      vatoda        c2toda
     c                   else
     c                   eval      c2tbes = *blank
     c                   eval      c2teus = *blank
6.21 c                   eval      c2bsum = 0
6.21 c                   eval      c2bred = 0
     c                   eval      c2teda = 0
     c                   eval      c2toda = 0
     c                   endif
7.02  *
7.02  * Henter bonus undertekst fra ekstra register
7.02  *
7.02 c                   eval      cbutiu_ttyp = c1tbot
7.02 c     cbutiu_key    chain     cbutiur
7.02 c                   if        %found
7.02 c                   eval      c2utx1 = %subst(cbttek:1:78)
7.02 c                   eval      c2utx2 = %subst(cbttek:79:78)
7.02 c                   else
7.02 c                   eval      c2utx1 = *blank
7.02 c                   eval      c2utx2 = *blank
7.02 c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1tbot = k1tbot
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2tbes = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   vbotlur
      *
     c                   eval      vbotlu_tbot = c1tbot
     c     vbotlu_key    chain     vbotlur
      *
     c                   eval      vatbes = c2tbes
6.21 c                   eval      vatsum = c2bsum
6.21 c                   eval      vatred = c2bred
      *
     c                   time                    vateda
     c                   time                    vateti
     c                   eval      vateus = l_user
      *
     c                   if        %found
     c                   update    vbotlur
     c                   else
     c                   eval      vatfir = w_firm
     c                   eval      vatbot = c1tbot
     c                   time                    vatoda
     c                   write     vbotlur
     c                   endif
7.02  *
7.02  * Oppdaterer bonus undertekst til ekstra register
7.02  *
7.02 c                   eval      cbutiu_ttyp = c1tbot
7.02 c     cbutiu_key    chain     cbutiur
7.02 c                   eval      cbttek = c2utx1 + c2utx2
7.02  *
     c                   if        %found
     c                   update    cbutiur
     c                   else
     c                   eval      cbttyp = c1tbot
     c                   write     cbutiur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1tbes = vatbes
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      vbotlr_tbot = d1tbot
     c     vbotlr_key    chain     vbotlr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      vbotlu_tbot = d1tbot
     c     vbotlu_key    chain     vbotlur
      *
     c                   delete    vbotlur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_tbot = k1tbot
7.01 c                   eval      s_tbes = *blanks
      *
      * Fyller kopierings-bildet
      *
     c                   eval      vbotlr_tbot = k1tbot
     c     vbotlr_key    chain     vbotlr
7.01 c                   eval      s_tbes = vatbes
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1tbot = *blank
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      vbotlr_tbot = k1tbot
     c     vbotlr_key    chain     vbotlr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
7.01  * Kopier med alle undernivåer
7.01 c                   exsr      sub_kopi
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1tbot = s_tbot
     c                   exsr      xc2bld
      *
     c                   endsr
      *
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  * Subrutine for vedlikehold av maler
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     xc7bld        begsr
6.20  *
6.20 c                   eval      p_tbot = b1tbot
6.20  *
6.20 c                   call      'EM101R'
6.20 c                   parm                    p_tbot
6.20  *
6.20 c                   endsr
6.20  *
6.22  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.22  * Subrutine for vedlikehold av bonussatser
6.22  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.22  *
6.22 c     xc8bld        begsr
6.22  *
6.22 c                   eval      p_tbot = b1tbot
6.22  *
6.22 c                   call      'VA167R'
6.22 c                   parm                    p_tbot
6.22  *
6.22 c                   endsr
6.22  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Subrutine for kopiering av underliggende nivåer
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01 c     sub_kopi      begsr
7.01  *
7.01  * Les først fra bonuselementer
7.01  *
7.01 c                   eval      embhir_hbot = s_tbot
7.01 c                   eval      embhir_hmnr = 0
7.01 c     embhir_key    setll     embhir
7.01 c     embhir_key2   reade     embhir
7.01 c                   dow       not %eof
7.01  * Hent malnummer
7.01 c                   exsr      hent_malnr
7.01  *
7.01 c                   exsr      kopi_belem
7.01  *
7.01 c     embhir_key2   reade     embhir
7.01 c                   enddo
7.01  *
7.01 c                   endsr
7.01  *
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Kopiere inn underliggende elementer fra avtale
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01 c     hent_malnr    begsr
7.01  *
7.01  * Hent inn neste ledige nummer for mal
7.01  *
7.01 c                   eval      w_kode = '0'
7.01 c                   eval      w_firm = l_firm
7.01 c                   eval      w_fell = 'BONUSM'
7.01 c                   eval      w_type = *blank
7.01 c                   eval      w_fast = *blank
7.01 c                   eval      w_numm = *zero
7.01  *
7.01 c                   call      'AS100R'
7.01 c                   parm                    w_kode
7.01 c                   parm                    w_firm
7.01 c                   parm                    w_fell
7.01 c                   parm                    w_type
7.01 c                   parm                    w_fast
7.01 c                   parm                    w_numm
7.01  *
7.01 c                   endsr
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Henter inn neste ledige nummer for mal
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01 c     kopi_belem    begsr
7.01  *
7.01  * Opprett først ny post
7.01  *
7.01 c                   clear                   embhiur
7.01  *
7.01 c                   eval      embhiu_hbot = k1tbot
7.01 c                   eval      embhiu_hmnr = w_numm
7.01 c     embhiu_key    chain     embhiur
7.01  *
7.01 c                   eval      emhbes = s_tbes
7.01  *
7.01 c                   time                    emheda
7.01 c                   time                    emheti
7.01 c                   eval      emheus = l_user
7.01  *
7.01 c                   if        %found
7.01 c                   update    embhiur
7.01 c                   else
7.01 c                   eval      emhfir = w_firm
7.01 c                   eval      emhbot = k1tbot
7.01 c                   eval      emhmnr = w_numm
7.01 c                   time                    emhoda
7.01 c                   time                    emhoti
7.01 c                   eval      emhous = l_user
7.01 c                   write     embhiur
7.01 c                   endif
7.01  *
7.01  * Finn avtalen som skal kopieres, og legg den over
7.01  *
7.01  * Les først fra bonuselementer
7.01  *
7.01 c                   eval      embeir_ebot = s_tbot
7.01 c                   eval      embeir_emnr = xxhmnr
7.01 c     embeir_key    setll     embeir
7.01 c     embeir_key    reade     embeir
7.01 c                   dow       not %eof
7.01 c                   eval      embeiu_ebot = k1tbot
7.01 c                   eval      embeiu_emnr = w_numm
7.01 c                   eval      embeiu_eogr = xyeogr
7.01 c                   eval      embeiu_ehgr = xyehgr
7.01 c                   eval      embeiu_eugr = xyeugr
7.01 c                   eval      embeiu_eldo = xyeldo
7.01 c                   eval      embeiu_emod = xyemod
7.01 c                   eval      embeiu_evar = xyevar
7.01 c                   eval      embeiu_eenh = xyeenh
7.01 c                   eval      embeiu_elet = xyelet
7.01 c                   eval      embeiu_efda = xyefda
7.01 c                   eval      embeiu_etda = xyetda
7.01 c     embeiu_key    chain     embeiur
7.01  *
7.01 c                   if        not %found
7.01 c                   clear                   embeiur
7.01 c                   eval      emefir = w_firm
7.01 c                   eval      emebot = k1tbot
7.01 c                   eval      ememnr = w_numm
7.01 c                   eval      emeogr = xyeogr
7.01 c                   eval      emehgr = xyehgr
7.01 c                   eval      emeugr = xyeugr
7.01 c                   eval      emeldo = xyeldo
7.01 c                   eval      ememod = xyemod
7.01 c                   eval      emevar = xyevar
7.01 c                   eval      emeenh = xyeenh
7.01 c                   eval      emelet = xyelet
7.01 c                   eval      emefda = xyefda
7.01 c                   eval      emetda = xyetda
7.01  *
7.01 c                   time                    emeoda
7.01 c                   time                    emeoti
7.01 c                   eval      emeous = l_user
7.01 c                   endif
7.01  *
7.01 c                   eval      ememrk = xyemrk
7.01 c                   time                    emdeda
7.01 c                   time                    emdeti
7.01 c                   eval      emdeus = l_user
7.01  *
7.01 c                   if        %found
7.01 c                   update    embeiur
7.01 c                   else
7.01 c                   write     embeiur
7.01 c                   endif
7.01  *
7.01 c     embeir_key    reade     embeir
7.01 c                   enddo
7.01  *
7.01  * Les deretter fra elementdetaljer
7.01  *
7.01 c                   eval      embdir_dbot = s_tbot
7.01 c                   eval      embdir_dmnr = xxhmnr
7.01 c     embdir_key    setll     embdir
7.01 c     embdir_key    reade     embdir
7.01 c                   dow       not %eof
7.01 c                   eval      embdiu_dbot = k1tbot
7.01 c                   eval      embdiu_dmnr = w_numm
7.01 c                   eval      embdiu_dogr = xzdogr
7.01 c                   eval      embdiu_dhgr = xzdhgr
7.01 c                   eval      embdiu_dugr = xzdugr
7.01 c                   eval      embdiu_dldo = xzdldo
7.01 c                   eval      embdiu_dmod = xzdmod
7.01 c                   eval      embdiu_dvar = xzdvar
7.01 c                   eval      embdiu_denh = xzdenh
7.01 c                   eval      embdiu_dlet = xzdlet
7.01 c                   eval      embdiu_dfda = xzdfda
7.01 c                   eval      embdiu_dtda = xzdtda
7.01 c                   eval      embdiu_dbgf = xzdbgf
7.01 c                   eval      embdiu_dbgt = xzdbgt
7.01 c     embdiu_key    chain     embdiur
7.01  *
7.01 c                   if        not %found
7.01 c                   clear                   embdiur
7.01 c                   eval      emdfir = w_firm
7.01 c                   eval      emdbot = k1tbot
7.01 c                   eval      emdmnr = w_numm
7.01 c                   eval      emdogr = xzdogr
7.01 c                   eval      emdhgr = xzdhgr
7.01 c                   eval      emdugr = xzdugr
7.01 c                   eval      emdldo = xzdldo
7.01 c                   eval      emdmod = xzdmod
7.01 c                   eval      emdvar = xzdvar
7.01 c                   eval      emdenh = xzdenh
7.01 c                   eval      emdlet = xzdlet
7.01 c                   eval      emdfda = xzdfda
7.01 c                   eval      emdtda = xzdtda
7.01 c                   eval      emdbgf = xzdbgf
7.01 c                   eval      emdbgt = xzdbgt
7.01  *
7.01 c                   time                    emdoda
7.01 c                   time                    emdoti
7.01 c                   eval      emdous = l_user
7.01 c                   endif
7.01  *
7.01 c                   eval      emdmrk = xzdmrk
7.01 c                   eval      emdktb = xzdktb
7.01 c                   eval      emdbpr = xzdbpr
7.01 c                   time                    emdeda
7.01 c                   time                    emdeti
7.01 c                   eval      emdeus = l_user
7.01  *
7.01 c                   if        %found
7.01 c                   update    embdiur
7.01 c                   else
7.01 c                   write     embdiur
7.01 c                   endif
7.01  *
7.01 c     embdir_key    reade     embdir
7.01 c                   enddo
7.01  *
7.01 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      vbotl2
     c                   other
     c                   read      vbotl1
     c                   endsl
      *
     c                   if        %eof or
     c                             vatfir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      vbotl2_tbes = vatbes
     c                   other
     c                   eval      vbotl1_tbot = vatbot
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1tbot = vatbot
     c                   eval      b1tbes = vatbes
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     vbotl2_key    setgt     vbotl2
     c                   readp     vbotl2
     c                   other
     c     vbotl1_key    setgt     vbotl1
     c                   readp     vbotl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     vbotl2
     c                   other
     c                   readp     vbotl1
     c                   endsl
      *
     c                   if        %eof or
     c                             vatfir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      vbotl2_tbes = vatbes
     c                   other
     c                   eval      vbotl1_tbot = vatbot
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     vbotl2_key    setll     vbotl2
     c                   other
     c     vbotl1_key    setll     vbotl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på enhet
      *
     c     vbotl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vbotl1_tbot
      *
      * Key for posisjonering på beskrivelse
      *
     c     vbotl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vbotl2_tbes
      *
      * Key for les på enhet
      *
     c     vbotlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vbotlr_tbot
      *
      * Key for oppdatering av enhet
      *
     c     vbotlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vbotlu_tbot
7.01  *
7.01  * Key for oppslag på bonusregister
7.01  *
7.01 c     embhir_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    embhir_hbot
7.01 c                   kfld                    embhir_hmnr
7.01  *
7.01  * Key for oppslag på bonusregister
7.01  *
7.01 c     embhir_key2   klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    embhir_hbot
7.01  *
7.01  * Key for oppslag på bonusregister
7.01  *
7.01 c     embhiu_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    embhiu_hbot
7.01 c                   kfld                    embhiu_hmnr
7.01  *
7.01  * Key for oppslag på bonusregister - elementer
7.01  *
7.01 c     embeiu_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    embeiu_ebot
7.01 c                   kfld                    embeiu_emnr
7.01 c                   kfld                    embeiu_eogr
7.01 c                   kfld                    embeiu_ehgr
7.01 c                   kfld                    embeiu_eugr
7.01 c                   kfld                    embeiu_eldo
7.01 c                   kfld                    embeiu_emod
7.01 c                   kfld                    embeiu_evar
7.01 c                   kfld                    embeiu_eenh
7.01 c                   kfld                    embeiu_elet
7.01 c                   kfld                    embeiu_efda
7.01 c                   kfld                    embeiu_etda
7.01  *
7.01  * Key for oppslag på bonusregister - elementer
7.01  *
7.01 c     embeir_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    embeir_ebot
7.01 c                   kfld                    embeir_emnr
7.01  *
7.01  * Key for oppslag på bonusregister - elementdetaljer
7.01  *
7.01 c     embdiu_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    embdiu_dbot
7.01 c                   kfld                    embdiu_dmnr
7.01 c                   kfld                    embdiu_dogr
7.01 c                   kfld                    embdiu_dhgr
7.01 c                   kfld                    embdiu_dugr
7.01 c                   kfld                    embdiu_dldo
7.01 c                   kfld                    embdiu_dmod
7.01 c                   kfld                    embdiu_dvar
7.01 c                   kfld                    embdiu_denh
7.01 c                   kfld                    embdiu_dlet
7.01 c                   kfld                    embdiu_dfda
7.01 c                   kfld                    embdiu_dtda
7.01 c                   kfld                    embdiu_dbgf
7.01 c                   kfld                    embdiu_dbgt
7.01  *
7.01  * Key for oppslag på bonusregister - elementdetaljer
7.01  *
7.01 c     embdir_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    embdir_dbot
7.01 c                   kfld                    embdir_dmnr
7.02  *
7.02  * Key for oppdatering av bonustyper undertekst
7.02  *
7.02 c     cbutiu_key    klist
7.02 c                   kfld                    cbutiu_ttyp
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Initierer skjermbildet
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      vbotl1_tbot = *blank
     c     vbotl1_key    setll     vbotl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
