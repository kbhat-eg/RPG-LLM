     h option(*nodebugio) datedit(*dmy)
     h decedit('0.')
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : NXCLOUD
      * Program . . . . : GETKIDD
      * Beskrivelse . . : Henter kidd fra div. steder til eksterne øk.systemer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * ----- ------ ---- -------------------------------------------------
      *       050122 sst  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     fsohel1    if   e           k disk    rename(sohepfr:sohel1r)
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_user                911    920
      *
      * Definering av variabler i nøkler
      *
     d sohel1_aarr     s                   like(soaarr)
     d sohel1_binr     s                   like(sobinr)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_aarr          s              4  0
     d w_kidr          s                   like(sokidr)
      *
     d*w_kidr          s              9  0
     d p_biln          s                   like(sobinr)
     d p_bdat          s               d   datfmt(*iso)
     d p_kref          s                   like(sobinr)
     d p_kidd          s                   like(sokidd)
     d p_kida          s              9
      *
     c/Exec SQL
     c+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR,
     c+             commit=*none
     c/End-Exec
      *
      *
     c                   eval      p_kida = *blank
     c                   eval      p_kidd = *blank
     c                   eval      w_kidr = 0
      *
      * Fra BREFPF
      *
     c/Exec SQL
     c+  select bxkidr
     c+    into :p_kidd
     c+  from brefpf
     c+    where bxfirm = :w_firm and bxbiln = :p_biln
     c+
     c/End-Exec
     c                   if        p_kidd <> *blank
     c                   goto      end_kid
     c                   endif
      *
      * Fra SOHEPF med bilagsnummer
      *
     c                   eval      w_aarr = %subdt(p_bdat:*years)
     c                   eval      sohel1_aarr = w_aarr
     c                   eval      sohel1_binr = p_biln
      *
     c     sohel1_key    setll     sohel1
     c     sohel1_key    reade     sohel1
     c                   dow       not %eof
     c                   if        sokidd <> *blanks or
     c                             sokidr <> 0
     c                   eval      p_kidd = sokidd
     c                   eval      w_kidr = sokidr
     c                   goto      end_kid
     c                   endif
     c     sohel1_key    reade     sohel1
     c                   enddo
      *
     c                   if        p_kidd <> *blank
     c                   goto      end_kid
     c                   endif
      *
      * Fra SOHEPF med kreditreferanse
      *
     c                   eval      w_aarr = %subdt(p_bdat:*years)
     c                   eval      sohel1_aarr = w_aarr
     c                   eval      sohel1_binr = p_kref
      *
     c     sohel1_key    setll     sohel1
     c     sohel1_key    reade     sohel1
     c                   dow       not %eof
     c                   if        sokidd <> *blanks or
     c                             sokidr <> 0
     c                   eval      p_kidd = sokidd
     c                   eval      w_kidr = sokidr
     c                   goto      end_kid
     c                   endif
     c     sohel1_key    reade     sohel1
     c                   enddo
      *
     c     end_kid       tag
     c                   eval      p_kida = %EditC(w_kidr :'X')
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_biln
     c                   parm                    p_bdat
     c                   parm                    p_kref
     c                   parm                    p_kidd
     c                   parm                    p_kida
      *
      * Key for oppslag på fakturanummer
      *
     c     sohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sohel1_aarr
     c                   kfld                    sohel1_binr
      *
      * Klargjør for øreavrundings konvertering
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
