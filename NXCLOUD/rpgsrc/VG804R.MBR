     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : VG804R
      * Beskrivelse . . : Oppdatering kosttransaksjoner / LOVFPF
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       020119 ask  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     fvgkkir    if   e           k disk    rename(vgkkst:vgkkirr)
     fvkhvl1    if   e           k disk    rename(vkhvpfr:vkhvl1r)
     flovfpf    o  a e             disk
      *
      * Definering av lda
      *
     d                uds
     d l_firm                944    946  0
     d l_peri                563    566  0
     d l_fkda                567    576d   datfmt(*iso)
      *
      * Datastruktur for kontoinformasjon
      *
     d                 ds
     d d_hele                       128
     d  d_kav1                        4  0 overlay(d_hele)
     d  d_kko1                        6  0 overlay(d_hele:5)
     d  d_ksp1                        4  0 overlay(d_hele:11)
     d  d_kav2                        4  0 overlay(d_hele:15)
     d  d_kko2                        6  0 overlay(d_hele:19)
     d  d_ksp2                        4  0 overlay(d_hele:25)
     d  d_kav3                        4  0 overlay(d_hele:29)
     d  d_kko3                        6  0 overlay(d_hele:33)
     d  d_ksp3                        4  0 overlay(d_hele:39)
     d  d_kav4                        4  0 overlay(d_hele:43)
     d  d_kko4                        6  0 overlay(d_hele:47)
     d  d_ksp4                        4  0 overlay(d_hele:53)
     d  d_kav5                        4  0 overlay(d_hele:57)
     d  d_kko5                        6  0 overlay(d_hele:61)
     d  d_ksp5                        4  0 overlay(d_hele:67)
     d  d_kav6                        4  0 overlay(d_hele:71)
     d  d_kko6                        6  0 overlay(d_hele:75)
     d  d_ksp6                        4  0 overlay(d_hele:81)
     d  d_kav7                        4  0 overlay(d_hele:85)
     d  d_kko7                        6  0 overlay(d_hele:89)
     d  d_ksp7                        4  0 overlay(d_hele:95)
     d  d_khnv                        4    overlay(d_hele:99)
      *
      * Datastruktur for kostprisfaktorer
      *
     d d_frec          ds
     d  d_fty1                       30
     d  d_fsi1                        1
     d  d_fak1                        8  6
     d  d_kto1                        6  0
     d  d_mot1                        6  0
     d  d_fty2                       30
     d  d_fsi2                        1
     d  d_fak2                        8  6
     d  d_kto2                        6  0
     d  d_mot2                        6  0
     d  d_fty3                       30
     d  d_fsi3                        1
     d  d_fak3                        8  6
     d  d_kto3                        6  0
     d  d_mot3                        6  0
     d  d_fty4                       30
     d  d_fsi4                        1
     d  d_fak4                        8  6
     d  d_kto4                        6  0
     d  d_mot4                        6  0
     d  d_fty5                       30
     d  d_fsi5                        1
     d  d_fak5                        8  6
     d  d_kto5                        6  0
     d  d_mot5                        6  0
      *
      * Definering av parametre
      *
     d p_numm          s                   like(lonumm)
     d p_suff          s                   like(losuff)
     d p_line          s                   like(loline)
     d p_biln          s              8  0
     d p_vare          s                   like(ldvare)
     d p_ldor          s                   like(loldor)
     d p_lage          s                   like(ldlage)
     d p_frec          s            300
      *
      * Definering av variable i nøkler
      *
     d vkhvl1_kkod     s                   like(vakkod)
     d vkhvl1_klty     s                   like(vaklty)
     d vkhvl1_koty     s                   like(vakoty)
     d votyl1_otyp     s                   like(vaotyp)
     d vltyl1_ltyp     s                   like(valtyp)
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d lodtl1_line     s                   like(ldline)
      *
      * Definering av variable
      *
     d w_firm          s                   like(ldfirm)
     d w_peri          s                   like(lgperi)
     d w_dato          s               d   datfmt(*iso)
     d w_hele          s            128
     d w_stat          s              1
     d w_belp          s             11  2
     d w_belh          s             11  2
     d w_fak1          s             11  2
     d w_fak2          s             11  2
     d w_fak3          s             11  2
     d w_fak4          s             11  2
     d w_fak5          s             11  2
     d w_avde          s              4  0
     d w_spes          s              4  0
     d w_kont          s              6  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram - behandling av bestillingslinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Hent kode opplysninger
      *
     c     lstsl1_key    chain     lstsl1r
      *
     c     vgkkir_key    chain     vgkkir
      *
      *  Henter inn standard kontohenvisning
      *
     c                   eval      vkhvl1_kkod = laakhv
     c     vkhvl1_key    chain     vkhvl1
      *
      *  Finner bestillingsinformasjon
      *
     c                   eval      lohel1_numm = p_numm
     c                   eval      lohel1_suff = p_suff
     c     lohel1_key    chain     lohel1r
     c                   if        not %found(lohel1)
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      votyl1_otyp = lootyp
     c     votyl1_key    chain     votyl1r
      *
     c                   eval      lodtl1_numm   = p_numm
     c                   eval      lodtl1_suff   = p_suff
     c                   eval      lodtl1_line   = p_line
      *
     c     lodtl1_key    chain     lodtl1
     c                   if        not %found(lodtl1)
     c                   goto      avslutt
     c                   endif
      *
     c                   if        vgkprk = 'I'
     c                   eval      w_belp = ldsumi
     c                   else
     c                   eval      w_belp = ldsumk
     c                   endif
      *
     c                   eval      vltyl1_ltyp = ldltyp
     c     vltyl1_key    chain     vltyl1r
      *
      *  Finner fakturadato og periode basert på føringstidspunkt
      *
     c                   if        vgoppd = 'F'
     c                   if        lofkda <> *loval
     c                   eval      w_dato = lofkda
     c                   eval      w_peri = (%subdt(w_dato:*months) * 100)
     c                                     + (%subdt(w_dato:*years) - 2000)
     c                   else
     c                   eval      w_dato = l_fkda
     c                   eval      w_peri = l_peri
     c                   endif
     c                   endif
      *
     c                   if        vgoppd = 'M'
     c                   time                    w_dato
     c                   eval      w_peri = (%subdt(w_dato:*months) * 100)
     c                                     + (%subdt(w_dato:*years) - 2000)
     c                   endif
      *
      * Sjekk om kostpris tillegg/fradrag skal føres
      *
     c                   eval      p_vare = ldvare
     c                   eval      p_ldor = loldor
     c                   eval      p_frec = *blank
     c                   call      'VG703R'
     c                   parm                    p_vare
     c                   parm                    p_lage
     c                   parm                    p_ldor
     c                   parm                    p_frec
      *
     c                   eval      d_frec = p_frec
     c                   exsr      post_fakt
      *
      * Hent kontohenvisning basert på varelinje - legger inn std hvis ikke funnet
      *
     c                   exsr      hntkon
     c                   if        w_stat = '9'
     c                   eval      d_kav1 = vakav1
     c                   eval      d_kko1 = vakko1
     c                   eval      d_ksp1 = vaksp1
     c                   eval      d_kav2 = vakav2
     c                   eval      d_kko2 = vakko2
     c                   eval      d_ksp2 = vaksp2
     c                   eval      d_kav3 = vakav3
     c                   eval      d_kko3 = vakko3
     c                   eval      d_ksp3 = vaksp3
     c                   eval      d_kav4 = vakav4
     c                   eval      d_kko4 = vakko4
     c                   eval      d_ksp4 = vaksp4
     c                   eval      d_kav5 = vakav5
     c                   eval      d_kko5 = vakko5
     c                   eval      d_ksp5 = vaksp5
     c                   eval      d_kav6 = vakav6
     c                   eval      d_kko6 = vakko6
     c                   eval      d_ksp6 = vaksp6
     c                   eval      d_kav7 = vakav7
     c                   eval      d_kko7 = vakko7
     c                   eval      d_ksp7 = vaksp7
     c                   endif
      *
      * Før kostpostering
      *
     c                   eval      w_belh = w_belp
     c                   eval      w_avde = d_kav3
     c                   eval      w_kont = d_kko3
     c                   eval      w_spes = d_ksp3
      *
     c                   exsr      skriv_post
      *
      * Før motpost
      *
     c                   eval      w_belh = w_belp * -1
     c                   eval      w_avde = d_kav5
     c                   eval      w_kont = d_kko5
     c                   eval      w_spes = d_ksp5
      *
     c                   exsr      skriv_post
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Beregner og posterer kostprisfaktorer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     post_fakt     begsr
      *
     c                   eval      w_fak1 = 0
     c                   eval      w_fak2 = 0
     c                   eval      w_fak3 = 0
     c                   eval      w_fak4 = 0
     c                   eval      w_fak5 = 0
      *
     c                   if        d_fty1 <> *blank
     c                   eval      w_fak1 = (w_belp * d_fak1) / 100
     c                   if        d_fsi1 = '-'
     c                   eval      w_fak1 = w_fak1 * -1
     c                   endif
      *
     c                   if        d_kto1 > 0 and
     c                             d_mot1 > 0
     c                   eval      w_kont = d_kto1
     c                   eval      w_avde = 0
     c                   eval      w_spes = 0
     c                   eval      w_belh = w_fak1
     c                   exsr      skriv_post
      *
     c                   eval      w_kont = d_mot1
     c                   eval      w_belh = w_fak1 * -1
     c                   exsr      skriv_post
     c                   endif
     c                   endif
      *
     c                   if        d_fty2 <> *blank
     c                   eval      w_fak2 = (w_belp * d_fak2) / 100
     c                   if        d_fsi2 = '-'
     c                   eval      w_fak2 = w_fak2 * -1
     c                   endif
      *
     c                   if        d_kto2 > 0 and
     c                             d_mot2 > 0
     c                   eval      w_kont = d_kto2
     c                   eval      w_avde = 0
     c                   eval      w_spes = 0
     c                   eval      w_belh = w_fak2
     c                   exsr      skriv_post
      *
     c                   eval      w_kont = d_mot2
     c                   eval      w_belh = w_fak2 * -1
     c                   exsr      skriv_post
     c                   endif
     c                   endif
      *
     c                   if        d_fty3 <> *blank
     c                   eval      w_fak3 = (w_belp * d_fak3) / 100
     c                   if        d_fsi3 = '-'
     c                   eval      w_fak3 = w_fak3 * -1
     c                   endif
      *
     c                   if        d_kto3 > 0 and
     c                             d_mot3 > 0
     c                   eval      w_kont = d_kto3
     c                   eval      w_avde = 0
     c                   eval      w_spes = 0
     c                   eval      w_belh = w_fak3
     c                   exsr      skriv_post
      *
     c                   eval      w_kont = d_mot3
     c                   eval      w_belh = w_fak3 * -1
     c                   exsr      skriv_post
     c                   endif
     c                   endif
      *
     c                   if        d_fty4 <> *blank
     c                   eval      w_fak4 = (w_belp * d_fak4) / 100
     c                   if        d_fsi4 = '-'
     c                   eval      w_fak4 = w_fak4 * -1
     c                   endif
      *
     c                   if        d_kto4 > 0 and
     c                             d_mot4 > 0
     c                   eval      w_kont = d_kto4
     c                   eval      w_avde = 0
     c                   eval      w_spes = 0
     c                   eval      w_belh = w_fak4
     c                   exsr      skriv_post
      *
     c                   eval      w_kont = d_mot4
     c                   eval      w_belh = w_fak4 * -1
     c                   exsr      skriv_post
     c                   endif
     c                   endif
      *
     c                   if        d_fty5 <> *blank
     c                   eval      w_fak5 = (w_belp * d_fak5) / 100
     c                   if        d_fsi5 = '-'
     c                   eval      w_fak5 = w_fak5 * -1
     c                   endif
      *
     c                   if        d_kto5 > 0 and
     c                             d_mot5 > 0
     c                   eval      w_kont = d_kto5
     c                   eval      w_avde = 0
     c                   eval      w_spes = 0
     c                   eval      w_belh = w_fak5
     c                   exsr      skriv_post
      *
     c                   eval      w_kont = d_mot5
     c                   eval      w_belh = w_fak5 * -1
     c                   exsr      skriv_post
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Skriver hovedboks postering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_post    begsr
      *
     c                   eval      lgfirm = w_firm
     c                   eval      lgperi = w_peri
     c                   eval      lgbunt = 0
     c                   eval      lgbiln = p_biln
     c                   eval      lgbill = ldline
     c                   eval      lgbdat = w_dato
     c                   eval      lgfdat = *loval
     c                   eval      lgbilk = vgkbik
     c                   eval      lgtext = 'Bestnr ' + %char(lonumm) +
     c                                      '/' + %char(ldline)
     c                   eval      lgdavd = 0
     c                   eval      lgdkto = 0
     c                   eval      lgdsps = 0
     c                   eval      lgdmom = ' '
     c                   eval      lgbelp = w_belh
     c                   eval      lgcavd = 0
     c                   eval      lgckto = 0
     c                   eval      lgcsps = 0
     c                   eval      lgcmom = ' '
     c                   eval      lgproj = *blank
     c                   eval      lgarbo = *blank
     c                   eval      lgakti = *blank
     c                   eval      lgkref = 0
     c                   eval      lgkund = loldor
     c                   eval      lgvalk = *blank
     c                   eval      lgvalb = 0
     c                   eval      lgmngd = 0
     c                   eval      lgautx = *blank
      *
      * Legger inn riktig
      * kontobegrep
      *
     c                   if        lgbelp < 0
     c                   eval      lgcavd = w_avde
     c                   eval      lgckto = w_kont
     c                   eval      lgcsps = w_spes
     c                   eval      lgbelp = lgbelp * -1
     c                   else
     c                   eval      lgdavd = w_avde
     c                   eval      lgdkto = w_kont
     c                   eval      lgdsps = w_spes
     c                   endif
      *
     c                   eval      lgmvag = 0
      *
     c                   write     lovfpfr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter avdeling/konto/spesifikasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hntkon        begsr
      *
      *  Legger ut parametere for konto-henvisnings-program
      *
     c                   eval      w_stat = *blank
     c                   eval      w_hele = *zero
      *
      *  Kaller program for å finne konto
      *
     c                   call      'VA720R'
     c                   parm                    w_stat
     c                   parm                    ldlety
     c                   parm                    lootyp
     c                   parm                    ldogrp
     c                   parm                    ldhgrp
     c                   parm                    ldugrp
     c                   parm                    ldvare
     c                   parm                    w_hele
     c                   eval      d_hele = w_hele
      *
      *  Overstyrer avdeling dersom denne
      *  finnes på ordrehode
      *
     c                   if        loavde > 0
     c                   eval      d_kav1 = loavde
     c                   eval      d_kav2 = loavde
     c                   eval      d_kav3 = loavde
     c                   eval      d_kav4 = loavde
     c                   eval      d_kav5 = loavde
     c                   eval      d_kav6 = loavde
     c                   eval      d_kav7 = loavde
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_line
     c                   parm                    p_biln
     c                   parm                    p_lage
      *
      *  Key for statuskoder
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for kostpris parameter
      *
     c     vgkkir_key    klist
     c                   kfld                    w_firm
      *
      * Key for kontohenvisninger
      *
     c     vkhvl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vkhvl1_kkod
     c                   kfld                    vkhvl1_klty
     c                   kfld                    vkhvl1_koty
      *
      * Key for ordretyper
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for linjetype
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
      *  Key for bestilling hode
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      *  Key for bestilling linje
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
     c                   kfld                    lodtl1_line
      *
      * Henter inn firmakode fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
      *
