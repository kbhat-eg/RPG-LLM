      /TITLE  ASOFAK Lev.vare, utskrift av varer - parameter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FW600R
      * Beskrivelse . . : Utskrift av varer fra leverandør, parameterprog.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       060699 HKO  Nytt program
      *
6.30  * 6.30  290514 BKV  LVAR økt fra 15 til 20
7.xx  * 7.00  290916 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
      *
     ffw600d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Datastruktur for parameterliste -ut
      *
     d                 ds
6.30 d d_list                        74
     d  d_firm                        3  0 overlay(d_list)
     d  d_rtyp                        1  0 overlay(d_list:4)
     d  d_ldor                        6  0 overlay(d_list:5)
6.30 d  d_lvaf                       20    overlay(d_list:11)
6.30 d  d_lvat                       20    overlay(d_list:31)
6.30 d  d_dato                         d   overlay(d_list:51) datfmt(*iso)
6.30 d  d_oper                        2    overlay(d_list:61)
6.30 d  d_endr                        5  2 overlay(d_list:63)
6.30 d  d_ekod                        1    overlay(d_list:68)
      *
      * Definering av variable i nøkler
      *
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
     d w_dato          s               d   datfmt(*iso)
      *
     d w_firm          s                   like(rlfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer skjermbildet
      *
     c                   eval      b1rtyp = 1
      *
      * Behandler bildet
      *
     c     b1taga        tag
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_inp
     c                   if        b_feil = *on
     c                   eval      b_feil = *off
     c                   goto      b1taga
     c                   endif
      *
      * Flytter variable til parameterliste
      *
     c                   eval      d_firm = w_firm
     c                   eval      d_rtyp = b1rtyp
     c                   eval      d_ldor = b1ldor
     c                   eval      d_lvaf = b1lvaf
     c                   eval      d_lvat = b1lvat
     c                   eval      d_oper = b1oper
     c                   eval      d_endr = b1endr
     c                   eval      d_ekod = b1ekod
      *
     c                   if        b1dato <> 0
     c     *dmy          move      b1dato        d_dato
     c                   else
     c                   eval      d_dato = *loval
     c                   endif
      *
      * Kaller utskriftsprogram
      *
     c                   call      'FW600C'
     c                   parm                    d_list
      *
     c                   goto      b1taga
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørringer (FD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
     c                   if        w_afld = 'B1LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    b1ldor
     c                   parm                    b1navn
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_inp     begsr
      *
     c                   eval      b_feil = *off
      *
      * Initierer variable
      *
     c                   eval      b1navn = *blank
      *
     c                   if        b1lvaf <> *blank and
     c                             b1lvat = *blank
     c                   eval      b1lvat = b1lvaf
     c                   endif
      *
      * Rapporttype
      *
     c                   if        b1rtyp <> 1 and
     c                             b1rtyp <> 2 and
     c                             b1rtyp <> 3
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Leverandør
      *
     c                   if        b1ldor = 0
     c                   eval      *in32  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   eval      rlevl1_levr = b1ldor
     c     rlevl1_key    chain     rlevl1                             91
     c                   if        *in91  = *on
     c                   eval      *in33  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   eval      b1navn = rlnavn
      *
      * Leverandørs varenummer
      *
     c                   if        b1lvaf > b1lvat                                       .....
     c                   eval      *in34  = *on                                              .
     c                   eval      b_feil = *on
     c                   goto      end_sjekk                                                 .
     c                   endif                                                           .....
      *
      * Prisdato + operand
      *
     c                   if        b1dato <> 0
     c     *dmy          test(d)                 b1dato                 35
     c                   if        *in35
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
     c                   if        b1dato <> 0 and
     c                             b1oper = *blank
     c                   eval      b1oper = 'EQ'
     c                   endif
      *
     c                   if        b1oper <> *blank and
     c                             b1oper <> 'EQ' and
     c                             b1oper <> 'GT' and
     c                             b1oper <> 'LT'
     c                   eval      *in36  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Endringsprosent
      *
     c                   if        b1endr <> 0 and
     c                             b1rtyp <> 1
     c                   eval      *in37  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Endringskode
      *
     c                   if        b1ekod <> ' ' and
     c                             b1ekod <> 'X' and
     c                             b1ekod <> 'N' and
     c                             b1ekod <> 'E' and
     c                             b1ekod <> 'S' and
     c                             b1ekod <> 'P'
     c                   eval      *in38  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      *
      * Henter firmanummer fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
