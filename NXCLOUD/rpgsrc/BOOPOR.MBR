      /TITLE  ASSHOP: Oppdaterer Ordre-register etter behandling i butikk
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSHOP
      * Program . . . . : BOOPOR
      * Beskrivelse . . : Oppdaterer ordre-register etter behandling
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       110101 AMD  Programmet er lagt om til standard ILE
5.01  * R5.01 241002 AMD  Lagt om rutiner for oppdatering av lager
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohelu    uf   e           k disk    rename(fohepfr:fohelur)
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
      *
      * Definering av parameter
      *
     d p_aarr          s              4  0
     d p_firm          s                   like(fofirm)
     d p_behd          s                   like(w_alf1)
     d p_bong          s                   like(fonumm)
     d p_numm          s                   like(fonumm)
     d p_suff          s                   like(fosuff)
     d p_wsid          s                   like(fowsid)
      *
      * Definering av variabler i nøkler
      *
     d w_firm          s                   like(fofirm)
     d fohelu_numm     s                   like(fonumm)
     d fohelu_suff     s                   like(fosuff)
      *
      * Definering av variable
      *
     d w_alf1          s              1
     d w_kom1          s             30
     d w_kom2          s             30
     d w_type          s              4
5.01 d w_opdt          s              1
      *
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandler ordre som er ferdigbehandlet i butikk
      *
      * Kode=0  :  Ingen behandling pågår nå
      *     =1  :  Differansen slettes
      *     =2  :  Differansen beholdes, dvs restordre
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Merker Ordre i ORDRE-HODE-REGISTER, Ferdigbehandlet i butikk
      *
      * Kode=0  :  Ingen behandling pågår nå
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c                   if        p_behd = *zero
     c     fohelu_key    chain     fohelur                            90
     c                   if        *in90 = *off
     c                   eval      fokode = *zero
     c                   update    fohelur
     c                   endif
     c                   endif
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Merker Ordre i ORDRE-HODE-REGISTER, Ferdigbehandlet i butikk
      *
      * Valg=1  :  Differansen slettes
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c                   if        p_behd = '1'
      *
      * Kaller opp subprogrammet for sletting av ordre
      *
     c                   eval      w_type = 'SHOP'
     c                   call      'FO410R'
     c                   parm                    w_firm
     c                   parm                    fohelu_numm
     c                   parm                    fohelu_suff
     c                   parm                    w_type
     c                   parm                    w_kom1
     c                   parm                    w_kom2
5.01 c                   parm                    w_opdt
      *
     c                   endif
      *
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *inzsr        begsr
      *
      * Nullstilling av felter som ikke kan redefineres
      *
     c                   eval      w_type = *blank
     c                   eval      w_kom1 = *blank
     c                   eval      w_kom2 = *blank
     c                   eval      w_alf1 = *blank
      *
      * Key for file : ORDRE-HODE-REGISTER
      *
     c     fohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelu_numm
     c                   kfld                    fohelu_suff
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     *entry        plist
     c                   parm                    p_behd
     c                   parm                    p_firm
     c                   parm                    p_aarr
     c                   parm                    p_wsid
     c                   parm                    p_bong
     c                   parm                    p_numm
     c                   parm                    p_suff
      *
      * Legger inn firmanummer, ordrenummer og suffix inn i nøkklene
      *
     c                   eval      w_firm = p_firm
      *
     c                   eval      fohelu_numm = p_numm
     c                   eval      fohelu_suff = p_suff
      *
     c                   endsr
