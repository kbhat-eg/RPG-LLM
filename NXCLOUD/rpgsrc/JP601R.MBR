     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : JP601R
      * Beskrivelse . . : Utskrift av betingelser
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       090696 HKO  Nytt program
      *       210498 HKO  Fjernet rapportør fra varegruppereferanse
      *                   registeret
      *       130898 HKO  Tilpasset utskriften til ny datamodell
      *       280605 HKO  Utvidet nivå med varetype og leveringstype
6.3x  *       100615 bof  Utvidet med prisgruppe
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjbetl1    if   e           k disk    rename(jbetpfr:jbetl1r)
     fjrael1    if   e           k disk    rename(jraepfr:jrael1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fjmodl1    if   e           k disk    rename(jmodpfr:jmodl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
      *
     fjp601p    o    e             printer
      *
      * Definering av parametre
      *
     d p_parm          s             64
      *
      * Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
      *
      * Datastruktur for parameterliste -inn
      *
     d                 ds
     d d_list                        64
     d  d_firm                        3  0 overlay(d_list)
     d  d_ldor                        6  0 overlay(d_list:4)
     d  d_dato                         d   overlay(d_list:10)
     d  d_valr                        1  0 overlay(d_list:20)
     d  d_vala                        1  0 overlay(d_list:21)
     d  d_valu                        1  0 overlay(d_list:22)
      *
      * Definering av variable i nøkler
      *
     d jbetl1_ldor     s                   like(jpldor)
     d jrael1_eldo     s                   like(jpeldo)
6.3x d jrael1_eprg     s                   like(jpeprg)
     d jrael1_eogr     s                   like(jpeogr)
     d jrael1_ehgr     s                   like(jpehgr)
     d jrael1_eugr     s                   like(jpeugr)
     d jrael1_emod     s                   like(jpemod)
     d jrael1_evar     s                   like(jpevar)
     d jrael1_evty     s                   like(jpevty)
     d jrael1_elet     s                   like(jpelet)
     d jrael1_ebrs     s                   like(jpebrs)
     d jrael1_ebli     s                   like(jpebli)
     d jlevl1_ldor     s                   like(jlldor)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d jmodl1_modn     s                   like(jumodn)
     d jvarl1_vare     s                   like(jvvare)
      *
      * Definering av variable
      *
     d b_fØrste        s              1    inz(*on)
     d b_lev_skrevet   s              1    inz(*off)
     d b_utgÅtt        s              1    inz(*off)
      *
     d w_time          s              6  0
      *
     d w_firm          s                   like(jpfirm)
      *
     d s_ogrp          s                   like(jpogrp)
     d s_hgrp          s                   like(jphgrp)
     d s_ugrp          s                   like(jpugrp)
     d s_modn          s                   like(jpmodn)
     d s_vare          s                   like(jpvare)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Header
      *
     c                   exsr      hode
      *
      * Behandling
      *
     c                   exsr      behandling
      *
      * Avslutter prorgram
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering og utskrift av hode-opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode          begsr
      *
      * Legger inn arb.stasjon og bruker i faste felter
      *
     c                   eval      a1firm = w_firm
     c                   eval      a1user = l_user
     c                   eval      a1wsid = l_wsid
     c                   eval      a1date = udate
     c                   time                    w_time
     c                   eval      a1time = w_time
      *
      * Henter firmaopplysninger
      *
     c     afirl1_key    chain     afirl1
     c                   eval      a1fnav = aafnav
      *
      * Henter parametre med beskrivelse
      *
     c                   eval      a2ldor = d_ldor
     c                   eval      a2valr = d_valr
     c                   eval      a2vala = d_vala
     c                   eval      a2valu = d_valu
      *
     c     *dmy          move      d_dato        a2dato
      *
     c                   if        d_ldor <> *zero
     c                   eval      jlevl1_ldor = d_ldor
     c     jlevl1_key    chain     jlevl1
     c                   if        %found
     c                   eval      a2lnav = jlnavn
     c                   endif
     c                   endif
      *
      * Skriver heading
      *
     c                   write     a1hdr                                30
     c                   write     a2hdr                                30
     c                   write     a3hdr                                30
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utplukk til - og utskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   if        d_ldor <> 0
     c                   eval      b_lev_skrevet = *on
     c                   endif
      *
      * Leser betingelser
      *
     c                   eval      jbetl1_ldor = d_ldor
     c     jbetl1_key    setll     jbetl1
     c                   read      jbetl1
     c                   dow       not %eof
      *
      * Test på leverandør
      *
     c                   if        d_ldor <> 0 and
     c                             jpldor <> d_ldor
     c                   leave
     c                   endif
      *
      * Behandling ved brudd på leverandør
      *
     c                   if        d_ldor = 0 and
     c                             jpldor <> a4ldor
      *
     c                   eval      a4ldor = jpldor
     c                   eval      b_lev_skrevet = *off
      *
     c                   eval      s_ogrp = jpogrp
     c                   eval      s_hgrp = jphgrp
     c                   eval      s_ugrp = jpugrp
     c                   eval      s_modn = jpmodn
     c                   eval      s_vare = jpvare
      *
     c                   endif
      *
      * Test på aktive betingelser
      *
     c                   if        d_vala = 1 and
     c                             jpakti = 0
     c                   goto      neste_bet
     c                   endif
      *
      * Test på utgåtte betingelser/rabattelement
      *
     c                   if        d_valu = 1
     c                   exsr      sjekk_utgÅtt
     c                   if        b_utgÅtt = *off
     c                   goto      neste_bet
     c                   endif
     c                   endif
      *
      * Skriver leverandør-linje dersom dette ikke allere er skrevet
      *
     c                   if        b_lev_skrevet = *off
     c                   exsr      skriv_lev
     c                   eval      b_lev_skrevet = *on
     c                   endif
      *
      * Fyller betingelseslinje med informasjon
      *
6.3x c                   eval      d1prgr = jpprgr
     c                   eval      d1ogrp = jpogrp
     c                   eval      d1hgrp = jphgrp
     c                   eval      d1ugrp = jpugrp
     c                   eval      d1besk = *blank
     c                   eval      d1modn = jpmodn
     c                   eval      d1vare = jpvare
     c                   eval      d1nobb = 0
     c                   eval      d1beti = jpbeti
     c                   eval      d1fakt = 0
     c                   eval      d1ntpr = 0
     c                   eval      d1fdat = 0
     c                   eval      d1tdat = 0
     c                   eval      d1dato = 0
     c                   eval      d1akti = *blank
     c                   eval      d1rkod = *blank
     c                   eval      d1eusr = jpeusr
     c                   eval      d1edat = 0
      *
     c                   if        jpfdat <> *loval
     c     *dmy          move      jpfdat        d1fdat
     c                   endif
      *
     c                   if        jptdat <> *loval
     c     *dmy          move      jptdat        d1tdat
     c                   endif
      *
     c                   if        jpdato <> *loval
     c     *dmy          move      jpdato        d1dato
     c                   endif
      *
     c                   call      'JP720R'
     c                   parm                    w_firm
     c                   parm                    jpldor
6.3x c                   parm                    jpprgr
     c                   parm                    jpogrp
     c                   parm                    jphgrp
     c                   parm                    jpugrp
     c                   parm                    jpmodn
     c                   parm                    jpvare
     c                   parm                    jpvtyp
     c                   parm                    jplety
     c                   parm                    jprsts
     c                   parm                    jpline
     c                   parm                    d1fakt
     c                   parm                    d1ntpr
      *
     c                   if        jpakti = 1
     c                   eval      d1akti = 'A'
     c                   endif
      *
     c                   if        jprsts = 1
     c                   eval      d1rkod = 'L'
     c                   endif
      *
     c                   if        jpedat <> *loval
     c     *dmy          move      jpedat        d1edat
     c                   endif
      *
     c                   select
     c                   when      jpugrp > 0
     c                   eval      vugrl1_uogr = jpogrp
     c                   eval      vugrl1_uhgr = jphgrp
     c                   eval      vugrl1_uugr = jpugrp
     c     vugrl1_key    chain     vugrl1                             91
     c                   eval      d1besk = vgutxt
     c                   when      jphgrp > 0
     c                   eval      vhgrl1_hogr = jpogrp
     c                   eval      vhgrl1_hhgr = jphgrp
     c     vhgrl1_key    chain     vhgrl1                             91
     c                   eval      d1besk = vghtxt
     c                   when      jpogrp > 0
     c                   eval      vogrl1_oogr = jpogrp
     c     vogrl1_key    chain     vogrl1                             91
     c                   eval      d1besk = vgotxt
     c                   endsl
      *
     c                   if        jpmodn <> 0
     c                   eval      jmodl1_modn = jpmodn
     c     jmodl1_key    chain     jmodl1                             91
     c                   if        *in91 = *off
     c                   eval      d1modn = jumodn
     c                   eval      d1besk = jumte1
     c                   endif
     c                   endif
      *
     c                   if        jpvare <> *blank
     c                   eval      jvarl1_vare = jpvare
     c     jvarl1_key    chain     jvarl1                             91
     c                   if        *in91 = *off
     c                   eval      d1nobb = jvnobb
     c                   eval      d1besk = jvtek1
     c                   endif
     c                   endif
      *
      * Skriver betingelse
      *
     c                   exsr      skriv_bet
      *
      * Behandling dersom rabattelementer ikke er utelatt
      * Leser tilhørende rabattelementer, og skriver rabattelemet-linje
      *
     c                   if        d_valr = 0
      *
     c                   eval      jrael1_eldo = jpldor
6.3x c                   eval      jrael1_eprg = jpprgr
     c                   eval      jrael1_eogr = jpogrp
     c                   eval      jrael1_ehgr = jphgrp
     c                   eval      jrael1_eugr = jpugrp
     c                   eval      jrael1_emod = jpmodn
     c                   eval      jrael1_evar = jpvare
     c                   eval      jrael1_evty = jpvtyp
     c                   eval      jrael1_elet = jplety
     c                   eval      jrael1_ebrs = jprsts
     c                   eval      jrael1_ebli = jpline
     c     jrael1_key    setll     jrael1
     c     jrael1_key    reade     jrael1                                 91
     c                   dow       *in91 = *off
      *
     c                   eval      d2erae = jperae
     c                   eval      d2ereg = jpereg
     c                   eval      d2ever = jpever
     c                   eval      d2efda = 0
     c                   eval      d2etda = 0
     c                   eval      d2erko = *blank
     c                   eval      d2eeus = jpeeus
     c                   eval      d2eeda = 0
      *
     c                   if        jperst = 1
     c                   eval      d2erko = 'L'
     c                   endif
      *
     c                   if        jpefda <> *loval
     c     *dmy          move      jpefda        d2efda
     c                   endif
      *
     c                   if        jpetda <> *loval
     c     *dmy          move      jpetda        d2etda
     c                   endif
      *
     c                   if        jpeeda <> *loval
     c     *dmy          move      jpeeda        d2eeda
     c                   endif
      *
     c                   write     d2dtl                                30
     c                   exsr      overflow
      *
     c     jrael1_key    reade     jrael1                                 91
     c                   enddo
      *
     c                   endif
      *
     c     neste_bet     tag
      *
     c                   read      jbetl1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking om betingelse eller tilhørende rabattelement
      * er utgått
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_utgÅtt  begsr
      *
     c                   eval      b_utgÅtt = *off
      *
     c                   if        jpfdat <> *loval and
     c                             d_dato < jpfdat or
     c                             jptdat <> *loval and
     c                             d_dato > jptdat
     c                   eval      b_utgÅtt = *on
     c                   goto      end_utgÅtt
     c                   endif
      *
     c                   eval      jrael1_eldo = jpldor
5.3x c                   eval      jrael1_eprg = jpprgr
     c                   eval      jrael1_eogr = jpogrp
     c                   eval      jrael1_ehgr = jphgrp
     c                   eval      jrael1_eugr = jpugrp
     c                   eval      jrael1_emod = jpmodn
     c                   eval      jrael1_evar = jpvare
     c                   eval      jrael1_evty = jpvtyp
     c                   eval      jrael1_elet = jplety
     c                   eval      jrael1_ebrs = jprsts
     c                   eval      jrael1_ebli = jpline
     c     jrael1_key    setll     jrael1
     c     jrael1_key    reade     jrael1                                 91
     c                   dow       *in91 = *off
      *
     c                   if        jpefda <> *loval and
     c                             d_dato < jpefda or
     c                             jpetda <> *loval and
     c                             d_dato > jpetda
     c                   eval      b_utgÅtt = *on
     c                   goto      end_utgÅtt
     c                   endif
      *
     c     jrael1_key    reade     jrael1                                 91
     c                   enddo
      *
     c     end_utgÅtt    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift av betingelse (detaljlinje 1)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_bet     begsr
      *
      * Skriver blank linje ved brudd på betingelsesnivå
      *
     c                   if        d1ogrp <> s_ogrp or
     c                             d1hgrp <> s_hgrp or
     c                             d1ugrp <> s_ugrp or
     c                             d1modn <> s_modn or
     c                             d1vare <> s_vare
      *
     c                   write     space                                30
     c                   exsr      overflow
      *
     c                   eval      s_ogrp = d1ogrp
     c                   eval      s_hgrp = d1hgrp
     c                   eval      s_ugrp = d1ugrp
     c                   eval      s_modn = d1modn
     c                   eval      s_vare = d1vare
      *
     c                   endif
      *
      * Skriv detaljlinje
      *
     c                   write     d1dtl                                30
     c                   exsr      overflow
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift av leverandør (header A4)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv_lev     begsr
      *
     c                   eval      jlevl1_ldor = jpldor
     c     jlevl1_key    chain     jlevl1                             91
     c                   if        *in91 = *off
     c                   eval      a4lnav = jlnavn
     c                   endif
      *
     c                   if        b_fØrste = *on
     c                   eval      b_fØrste = *off
     c                   write     a4hdr                                30
     c                   else
     c                   write     a1hdr                                30
     c                   write     a3hdr                                30
     c                   write     a4hdr                                30
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overflow      begsr
      *
     c                   if        *in30 = *on
     c                   write     a1hdr                                30
     c                   write     a3hdr                                30
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Paramtre
      *
     c     *entry        plist
     c                   parm                    p_parm
      *
      * Key for les på betingelser
      *
     c     jbetl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jbetl1_ldor
      *
      * Key for les på rabattelementer
      *
     c     jrael1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jrael1_eldo
6.3x c                   kfld                    jrael1_eprg
     c                   kfld                    jrael1_eogr
     c                   kfld                    jrael1_ehgr
     c                   kfld                    jrael1_eugr
     c                   kfld                    jrael1_emod
     c                   kfld                    jrael1_evar
     c                   kfld                    jrael1_evty
     c                   kfld                    jrael1_elet
     c                   kfld                    jrael1_ebrs
     c                   kfld                    jrael1_ebli
      *
      * Key for oppslag firma
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag modul
      *
     c     jmodl1_key    klist
     c                   kfld                    jmodl1_modn
      *
      * Key for oppslag vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      *
      * Flytter parameter til datasturuktur
      *
     c                   eval      d_list = p_parm
      *
     c                   eval      w_firm = d_firm
     c                   eval      a4ldor = 999999
      *
     c                   endsr
