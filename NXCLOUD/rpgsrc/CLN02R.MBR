     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : Ehandel
      * Program . . . . : CLN01R
      * beskrivelse . . : Vedlikehold av kobling varegrupper
      *
      * Spesialversjon. :
      * Tilpasset for . : Mestergruppen
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       270916 ask  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fclnni1    if   e           k disk    rename(clnnst:clnni1r)
     fclnnir    if   e           k disk    rename(clnnst:clnnirr)
     fclnniu    uf a e           k disk    rename(clnnst:clnniur)
     fclnkir    if   e           k disk    rename(clnkst:clnkirr)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
      *
     fcln02d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d clnnir_nnka     s                   like(clnnka)
     d clnnir_nnog     s                   like(clnnog)
     d clnnir_nnhg     s                   like(clnnhg)
     d clnnir_nnug     s                   like(clnnug)
     d clnniu_nnka     s                   like(clnnka)
     d clnniu_nnog     s                   like(clnnog)
     d clnniu_nnhg     s                   like(clnnhg)
     d clnniu_nnug     s                   like(clnnug)
     d clnni1_nnka     s                   like(clnnka)
     d clnkir_nkka     s                   like(clnkka)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_kabc          s              1    inz(*off)
      *
     d w_firm          s                   like(clnnfi)
     d w_nnka          s                   like(clnnka)
6.34 d w_nktx          s             30
6.30 d w_udate         s               d   datfmt(*iso)
6.34 d w_stat          s              1
6.34 d w_disp          s              1
6.34 d w_frad          s               d   datfmt(*iso)
6.34 d w_tild          s               d   datfmt(*iso)
     d w_ogrp          s                   like(vgoogr)
     d w_hgrp          s                   like(vghhgr)
     d w_ugrp          s                   like(vguugr)
     d w_otxt          s                   like(vgotxt)
     d w_htxt          s                   like(vghtxt)
     d w_utxt          s                   like(vgutxt)
      *
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      sok_kat
     c                   eval      b2nnka = w_nnka
     c                   goto      pos
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *inkh
     c                   exsr      nobbkat
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c     pos           tag
     c                   if        b2nnka <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      clnni1_nnka = b1nnka
     c     clnni1_key    setll     clnni1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Nobbkategori
      *
     c                   if        b2nnka <> 0
     c                   eval      clnni1_nnka = b2nnka
     c     clnni1_key    setll     clnni1
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2nnka = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c2nntx = b1nntx
     c                   eval      c2nnka = b1nnka
     c                   eval      c2nnog = b1nnog
     c                   eval      c2nnhg = b1nnhg
     c                   eval      c2nnug = b1nnug
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1nnka = b1nnka
     c                   eval      d1nntx = b1nntx
     c                   eval      d1nnog = b1nnog
     c                   eval      d1nnhg = b1nnhg
     c                   eval      d1nnug = b1nnug
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c2nnka = b1nnka
     c                   eval      c2nntx = b1nntx
     c                   eval      c2nnog = b1nnog
     c                   eval      c2nnhg = b1nnhg
     c                   eval      c2nnug = b1nnug
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1nnka = 0
     c                   eval      c1nnog = 0
     c                   eval      c1nnhg = 0
     c                   eval      c1nnug = 0
     c                   eval      c1nntx = *blank
     c                   eval      c1ogtx = *blank
     c                   eval      c1hgtx = *blank
     c                   eval      c1ugtx = *blank
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Søking
      *
     c                   if        *inkd = *on
     c                   if        w_afld = 'C1NNKA'
     c                   exsr      sok_kat
     c                   eval      c1nnka = w_nnka
     c                   eval      c1nntx = w_nktx
     c                   goto      c1taga
     c                   endif
      *
     c                   if        w_afld = 'C1NNOG'
     c                   exsr      sok_ogr
     c                   eval      c1nnog = w_ogrp
     c                   eval      c1ogtx = w_otxt
     c                   goto      c1taga
     c                   endif
      *
     c                   if        w_afld = 'C1NNHG'
     c                   exsr      sok_hgr
     c                   eval      c1nnhg = w_hgrp
     c                   eval      c1hgtx = w_htxt
     c                   goto      c1taga
     c                   endif
      *
     c                   if        w_afld = 'C1NNUG'
     c                   exsr      sok_ugr
     c                   eval      c1nnug = w_ugrp
     c                   eval      c1ugtx = w_utxt
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      *
     c                   if        c1nnka = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      * Sender opp informasjonsbilde dersom overgruppe ikke er fylt ut
      *
     c                   if        c1nnog = 0
     c                   exsr      xc3msg
     c                   goto      c1taga
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      clnnir_nnka = c1nnka
     c                   eval      clnnir_nnog = c1nnog
     c                   eval      clnnir_nnhg = c1nnhg
     c                   eval      clnnir_nnug = c1nnug
     c     clnnir_key    chain     clnnir
     c                   if        %found
     c                   exsr      xc2msg
     c                   goto      c1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c2nnka = c1nnka
     c                   eval      c2nnog = c1nnog
     c                   eval      c2nnhg = c1nnhg
     c                   eval      c2nnug = c1nnug
     c                   exsr      xc2bld
      *
     c                   eval      c1nnog = 0
     c                   eval      c1nnhg = 0
     c                   eval      c1nnug = 0
     c                   eval      c1ogtx = *blank
     c                   eval      c1hgtx = *blank
     c                   eval      c1ugtx = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2nnka = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2msg        begsr
      *
     c                   exfmt     c2msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av melding dersom minimum overgruppe valgt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc3msg        begsr
      *
     c                   exfmt     c3msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      clnnir_nnka = c2nnka
     c                   eval      clnnir_nnog = c2nnog
     c                   eval      clnnir_nnhg = c2nnhg
     c                   eval      clnnir_nnug = c2nnug
     c     clnnir_key    chain     clnnir
      *
     c                   if        %found
     c     *dmy          move      clnnod        c2eoda
     c     *dmy          move      clnned        c2eeda
     c                   eval      c2eeus = clnneu
6.34 c                   endif
      *
     c                   if        c2nnog > 0
     c                   eval      w_ogrp = c2nnog
     c                   exsr      finn_ogr
     c                   eval      c2ogtx = w_otxt
     c                   endif
      *
     c                   if        c2nnhg > 0
     c                   eval      w_hgrp = c2nnhg
     c                   exsr      finn_hgr
     c                   eval      c2hgtx = w_htxt
     c                   endif
      *
     c                   if        c2nnug > 0
     c                   eval      w_ugrp = c2nnug
     c                   exsr      finn_ugr
     c                   eval      c2ugtx = w_utxt
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   clnniur
      *
     c                   eval      clnniu_nnka = c2nnka
     c                   eval      clnniu_nnog = c2nnog
     c                   eval      clnniu_nnhg = c2nnhg
     c                   eval      clnniu_nnug = c2nnug
     c     clnniu_key    chain     clnniur
      *
      *
     c                   time                    clnned
     c                   time                    clnnet
     c                   eval      clnneu = l_user
      *
     c                   if        %found
     c                   update    clnniur
     c                   else
     c                   eval      clnnfi = w_firm
     c                   eval      clnnka = c2nnka
     c                   eval      clnnog = c2nnog
     c                   eval      clnnhg = c2nnhg
     c                   eval      clnnug = c2nnug
     c                   time                    clnnod
6.10 c                   time                    clnnet
6.10 c                   eval      clnneu = l_user
     c                   write     clnniur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1nnka = clnnka
     c                   eval      b1nnog = clnnog
     c                   eval      b1nnhg = clnnhg
     c                   eval      b1nnug = clnnug
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      clnnir_nnka = d1nnka
     c                   eval      clnniu_nnog = d1nnog
     c                   eval      clnniu_nnhg = d1nnhg
     c                   eval      clnniu_nnug = d1nnug
     c     clnnir_key    chain     clnnir
      *
     c                   if        d1nnog > 0
     c                   eval      w_ogrp = d1nnog
     c                   exsr      finn_ogr
     c                   eval      d1ogtx = w_otxt
     c                   endif
      *
     c                   if        d1nnhg > 0
     c                   eval      w_hgrp = d1nnhg
     c                   exsr      finn_hgr
     c                   eval      d1hgtx = w_htxt
     c                   endif
      *
     c                   if        d1nnug > 0
     c                   eval      w_ugrp = d1nnug
     c                   exsr      finn_ugr
     c                   eval      d1ugtx = w_utxt
     c                   endif
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      clnniu_nnka = d1nnka
     c                   eval      clnniu_nnog = d1nnog
     c                   eval      clnniu_nnhg = d1nnhg
     c                   eval      clnniu_nnug = d1nnug
     c     clnniu_key    chain     clnniur
      *
     c                   delete    clnniur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      clnni1
      *
     c                   if        %eof or
     c                             clnnfi <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      clnni1_nnka = clnnka
      *
     c                   eval      b1valg = 0
     c                   eval      b1nnka = clnnka
     c                   eval      b1nnog = clnnog
     c                   eval      b1nnhg = clnnhg
     c                   eval      b1nnug = clnnug
      *
     c                   eval      w_nnka = b1nnka
     c                   exsr      finn_kat
     c                   eval      b1nntx = w_nktx
      *
     c                   eval      w_ogrp = b1nnog
     c                   exsr      finn_ogr
     c                   eval      b1vgtx = w_otxt
      *
     c                   if        b1nnhg > 0
     c                   eval      w_hgrp = b1nnhg
     c                   exsr      finn_hgr
     c                   eval      b1vgtx = w_htxt
     c                   endif
      *
     c                   if        b1nnug > 0
     c                   eval      w_ugrp = b1nnug
     c                   exsr      finn_ugr
     c                   eval      b1vgtx = w_utxt
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     clnni1_key    setgt     clnni1
     c                   readp     clnni1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     clnni1
      *
     c                   if        %eof or
     c                             clnnfi <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      clnni1_nnka = clnnka
      *
     c                   enddo
      *
     c                   if        %eof
     c     clnni1_key    setll     clnni1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne kategoritekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_kat      begsr
      *
     c                   eval      clnkir_nkka = w_nnka
     c     clnkir_key    chain     clnkir
     c                   if        %found
     c                   eval      w_nktx = clnktx
     c                   else
     c                   eval      w_nktx = *blank
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne overgruppetekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_ogr      begsr
      *
     c                   eval      vogrl1_oogr = w_ogrp
     c     vogrl1_key    chain     vogrl1
     c                   if        %found
     c                   eval      w_otxt = vgotxt
     c                   eval      b1vgtx = vgotxt
     c                   else
     c                   eval      w_otxt = *blank
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne hovedgruppetekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_hgr      begsr
      *
     c                   eval      vhgrl1_hogr = w_ogrp
     c                   eval      vhgrl1_hhgr = w_hgrp
     c     vhgrl1_key    chain     vhgrl1
     c                   if        %found
     c                   eval      w_htxt = vghtxt
     c                   eval      b1vgtx = vghtxt
     c                   else
     c                   eval      w_htxt = *blank
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne undergruppetekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_ugr      begsr
      *
     c                   eval      vugrl1_uogr = w_ogrp
     c                   eval      vugrl1_uhgr = w_hgrp
     c                   eval      vugrl1_uugr = w_ugrp
     c     vugrl1_key    chain     vugrl1
     c                   if        %found
     c                   eval      w_utxt = vgutxt
     c                   eval      b1vgtx = vgutxt
     c                   else
     c                   eval      w_utxt = *blank
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å søke kategori
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sok_kat       begsr
      *
     c                   call      'CLN11R'
     c                   parm                    w_firm
     c                   parm                    w_nnka
     c                   parm                    w_nktx
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å søke overgruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sok_ogr       begsr
      *
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    w_ogrp
     c                   parm                    w_otxt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å søke hovedgruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sok_hgr       begsr
      *
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    w_ogrp
     c                   parm                    w_hgrp
     c                   parm                    w_htxt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å søke undergruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sok_ugr       begsr
      *
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    w_ogrp
     c                   parm                    w_hgrp
     c                   parm                    w_ugrp
     c                   parm                    w_utxt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av nobb kategorier
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     nobbkat       begsr
      *
     c                   call      'CLN01R'
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for les på kjedekategori
      *
     c     clnnir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    clnnir_nnka
     c                   kfld                    clnnir_nnog
     c                   kfld                    clnnir_nnhg
     c                   kfld                    clnnir_nnug
      *
      * Key for oppdatering av kjedekategori
      *
     c     clnniu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    clnniu_nnka
     c                   kfld                    clnniu_nnog
     c                   kfld                    clnniu_nnhg
     c                   kfld                    clnniu_nnug
      *
      * Key for posisjonering på kjedekategori
      *
     c     clnni1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    clnni1_nnka
      *
      * Key for les på kjedekategori
      *
     c     clnkir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    clnkir_nkka
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
6.30 c                   time                    w_udate
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      clnni1_nnka = 0
     c     clnni1_key    setll     clnni1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
