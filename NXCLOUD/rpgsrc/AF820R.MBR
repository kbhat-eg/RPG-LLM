     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : aspgpl
      * Program . . . . : af820r
      * Beskrivelse . . : Leser inn filegrupper fra csv
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       171120 bof  Nytt program
7.01  *       190521 bof  utvidet med Miljø
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flwexpf    if   e           k disk
     fafgriu    uf a e           k disk    rename(afgrst:afgriur)
      *
      * Definering av datastruktur
      *
      *  Recordbeskrivelse -
      *
     d                 ds
     d d_inpu                       350
     d  d_gsrv                       12    overlay(d_inpu:1)
     d  d_gver                       10    overlay(d_inpu:13)
     d  d_gfgr                        3    overlay(d_inpu:23)
     d  d_gtxt                       35    overlay(d_inpu:26)
     d  d_antx                        6    overlay(d_inpu:62)
7.01 d  d_gmil                        3    overlay(d_inpu:68)

      *
      * Definering av parametre
      *
      *
      * Definering av variable i nøkler
      *
     d afgriu_gsrv     s                   like(afgsrv)
     d afgriu_gfgr     s                   like(afgfgr)
      *
      * Definering av variable
      *
     d b_ok            s              1    inz(*off)
     d len             s              3  0
     d st              s              3  0
     d w_6x            s              6
     d w_2x            s              2
     d w_udat          s               d   datfmt(*iso)
     d w_posi          s              4  0
     d w_leng          s              4  0
     d w_anta          s              6  0
     d w_exli          s                   like(exclin)
      *
      * Definering av konstanter
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
     d digits          c                   ' 0123456789'
     d c_null          s             15    inz('000000000000000')
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   read      lwexpf
     c                   dow       not %eof
      *
     c                   eval      w_exli = %trim(exclin)
     c                   eval      b_ok = *off
     c                   eval      d_inpu = *blank
     c                   exsr      utled_ds
      *
     c                   if        b_ok = *on
     c                   exsr      oppdater
     c                   endif
      *
     c                   read      lwexpf
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * oppdaterer filgruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
      * dann filegruppe
     c                   clear                   afgriur
      *
     c                   eval      afgriu_gsrv = d_gsrv
     c                   eval      afgriu_gfgr = d_gfgr
     c     afgriu_key    chain     afgriur
      *
     c                   eval      afgver = d_gver
7.01 c                   eval      afgmil = d_gmil
     c                   if        w_anta < 50
     c                   eval      afgakt = 1
     c                   else
     c                   eval      afgakt = 0
     c                   endif
     c                   eval      afgtxt = d_gtxt
     c                   if        %found
     c                   time                    afgeda
     c                   time                    afgeti
     c                   eval      afgeus =  l_user
     c                   update    afgriur
     c                   else
     c                   eval      afgsrv = d_gsrv
     c                   eval      afgfgr = d_gfgr
     c                   write     afgriur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utleding av datastruktur
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utled_ds      begsr
      *
     c     '"':' '       xlate     exclin        exclin
      *
      * Server
      *
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_gsrv = %trim(%subst(exclin:1:w_posi-1))
     c                   if        d_gsrv = *blank
     c                   leavesr
     c                   endif
     c                   if        d_gsrv <> 'EGUTVK' and
     c                             d_gsrv <> 'ASPUTVK' and
     c                             d_gsrv <> 'EGTEST' and
     c                             d_gsrv <> 'XLBYGG' and
     c                             d_gsrv <> 'SKATTUM' and
     c                             d_gsrv <> 'STANGESKOVEN' and
     c                             d_gsrv <> 'BYGGMAKK' and
     c                             d_gsrv <> 'BYGGNIB' and
     c                             d_gsrv <> 'OPTIMERA' and
     c                             d_gsrv <> 'MESTERGRUPPE' and
     c                             d_gsrv <> 'ASDRIFT' and
     c                             d_gsrv <> 'MAXBO' and
     c                             d_gsrv <> 'EGTEST'
     c                   leavesr
     c                   endif
     c                   if        d_gsrv = 'STANGESKOVEN'
     c                   eval      d_gsrv = 'STASKOV    '
     c                   endif
     c                   if        d_gsrv = 'MESTERGRUPPE'
     c                   eval      d_gsrv = 'MESTERGR   '
     c                   endif
     c                   if        d_gsrv = 'ASPUTVK'
7.01 c                   eval      d_gsrv = 'EGUTVK01    '
     c                   endif
      * MILJØ
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
7.01 c                   eval      d_gmil = %trim(%subst(exclin:1:w_posi-1))
      *
      * Beskrivelse   1
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
      *
      * versjon
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_gver = %trim(%subst(exclin:1:w_posi-1))
      *
      * db nivå
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
      *
      * filgruppe
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_gfgr = %trim(%subst(exclin:1:w_posi-1))
      *
      * bibliotek
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
      *
      * Beskrivelse   2
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_gtxt = %trim(%subst(exclin:1:w_posi-1))
      *
      * status
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
      *
      * antall dager etter ordre.
      *
     c                   eval      exclin = %subst(exclin:w_posi+1:512-w_posi)
     c                   eval      w_posi = %scan(';':exclin)
     c                   eval      d_antx = %trim(%subst(exclin:1:w_posi-1))
     c                   if        %check(digits : d_antx) = 0  and
     c                             d_antx <> *blank
     c                   eval      w_anta = %dec(%trim(d_antx):6:0)
     c                   else
     c                   eval      w_anta = 0
     c                   endif
      *
     c                   eval      b_ok = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *
      * Key for oppdatering av filegrupper
      *
     c     afgriu_key    klist
     c                   kfld                    afgriu_gsrv
     c                   kfld                    afgriu_gfgr
      *
     c                   endsr
