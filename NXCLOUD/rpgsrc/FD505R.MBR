     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FD505R
      * Beskrivelse . . : Ordre-linje, vis
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       010698 AMD  Nytt program
      *       110602 HKO  Gjort om til vindu, og lagt ut mer informasjon
      *       161203 HKO  Viser bruksrett-rabatt
5.70  * PRGR  121108 BOF  Utvidet med prisgruppe
6.11  * 6.10  290609 bof  Utivdet eannr til 14 og brukt std. oppslag
6.NU  * R6.30 260514 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.20  * 6.20  210415 bsa  Henter GTIN nummer fra varelinja
6.30  * R6.30 161014 bsa  Innfører F tast for visning av NOBB info. Erstatter
6.30  *                   koding i jacada.
8.01  *PRG2   090622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
      *
     ffd505d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av parametre
      *
     d p_firm          s                   like(fdfirm)
6.NU d p_numm          s                   like(fdnumm)
     d p_suff          s                   like(fdsuff)
     d p_line          s                   like(fdline)
6.30 d p_nobb          s              8
      *
      * Definering av variabler i nøkler
      *
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d fodtl1_line     s                   like(fdline)
     d fusrl1_user     s                   like(fbuser)
     d rlevl1_levr     s                   like(rllevr)
     d vvarl1_vare     s                   like(vvvare)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
      *
      * Definering av variable
      *
     d w_firm          s                   like(fdfirm)
     d w_raba          s                   like(fdrab1)
     d w_nett          s                   like(fdsums)
6.11 d w_east          s              1
6.11 d w_eann          s                   like(fdeann)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle bildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer skjermbildet
      *
     c                   eval      fohel1_numm = p_numm
     c                   eval      fohel1_suff = p_suff
     c     fohel1_key    chain     fohel1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      fodtl1_numm = p_numm
     c                   eval      fodtl1_suff = p_suff
     c                   eval      fodtl1_line = p_line
     c     fodtl1_key    chain     fodtl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      vvarl1_vare = fdvare
     c     vvarl1_key    chain     vvarl1
      *
6.11  *
6.11  * Kaller program for henting av ean-nr
6.11  *
6.11 c                   call      'VE711R'
6.11 c                   parm                    w_firm
6.11 c                   parm                    fdvare
6.11 c                   parm                    fdenh1
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_east
      *
     c                   if        fobdat <> *loval
     c     *dmy          move      fobdat        b1bdat
     c                   endif
      *
     c                   if        fdldat <> *loval
     c     *dmy          move      fdldat        b1ldat
     c                   endif
      *
     c                   eval      b1numm = fdnumm
     c                   eval      b1otyp = fdotyp
     c                   eval      b1ltyp = fdltyp
     c                   eval      b1vare = fdvare
     c                   eval      b1tek1 = fdtek1
     c                   eval      b1tek2 = fdtek2
     c                   eval      b1ogrp = fdogrp
     c                   eval      b1hgrp = fdhgrp
     c                   eval      b1ugrp = fdugrp
     c                   eval      b1utxt = *blank
     c                   eval      b1ldor = fdldor
     c                   eval      b1lnav = *blank
     c                   eval      b1lvar = fdlvar
     c                   eval      b1nobb = fdnobb
     c                   eval      b1nmod = vvnmod
6.20 c                   if        fdeann = 0
6.11 c                   eval      b1eann = w_eann
6.20 c                   else
6.20 c                   eval      b1eann = fdeann
6.20 c                   endif
     c                   eval      b1anta = fdanta
5.70 c                   eval      b1prgr = fdprgr
     c                   eval      b1enh1 = fdenh1
     c                   eval      b1kpri = fdkpri
     c                   eval      b1lety = fdlety
     c                   eval      b1kopr = fdkopr
     c                   eval      b1sapr = fdsapr
     c                   eval      b1rab1 = fdrab1
     c                   eval      b1rab2 = fdrab2
     c                   eval      b1brr1 = fdbrr1
     c                   eval      b1brr2 = fdbrr2
     c                   eval      b1dekn = fdpasl
     c                   eval      b1rkr1 = 0
     c                   eval      b1rkr2 = 0
     c                   eval      b1bkr1 = 0
     c                   eval      b1bkr2 = 0
      *
      * Linjerabatt - enhet
      *
     c                   eval      w_nett = fdsapr
      *
     c                   eval(h)   b1rkr1 = fdsapr * fdrab1 / 100
     c                   eval(h)   w_nett = w_nett - b1rkr1
     c                   eval(h)   b1rkr2 = w_nett * fdrab2 / 100
     c                   eval(h)   w_nett = w_nett - b1rkr2
     c                   eval(h)   b1bkr1 = w_nett * fdbrr1 / 100
     c                   eval(h)   w_nett = w_nett - b1bkr1
     c                   eval(h)   b1bkr2 = w_nett * fdbrr2 / 100
      *
     c                   eval      b1nett = fdsapr - b1rkr1 - b1rkr2 -
     c                                      b1bkr1 - b1bkr2
      *
      * Legger inn priser totalt
      *
     c                   eval      b1sumk = fdsumk
     c                   eval      b1sums = fdsums
     c                   eval      b1srk1 = 0
     c                   eval      b1srk2 = 0
     c                   eval      b1sbk1 = 0
     c                   eval      b1sbk2 = 0
      *
      * Linjerabatt - total
      *
     c                   eval      w_nett = fdsums
      *
     c                   eval(h)   b1srk1 = fdsums * fdrab1 / 100
     c                   eval(h)   w_nett = w_nett - b1srk1
     c                   eval(h)   b1srk2 = w_nett * fdrab2 / 100
     c                   eval(h)   w_nett = w_nett - b1srk2
     c                   eval(h)   b1sbk1 = w_nett * fdbrr1 / 100
     c                   eval(h)   w_nett = w_nett - b1sbk1
     c                   eval(h)   b1sbk2 = w_nett * fdbrr2 / 100
      *
     c                   eval      b1sumn = fdsums - b1srk1 - b1srk2 -
     c                                      b1sbk1 - b1sbk2
      *
      * Kostpriser og DG skal kun vises dersom dette er tillatt for bruker
      *
     c                   if        fbko01 = 0
     c                   eval      b1dekn = 0
     c                   eval      b1kopr = 0
     c                   eval      b1sumk = 0
     c                   endif
      *
      * Hent navn på leverandør
      *
     c                   eval      rlevl1_levr = fdldor
     c     rlevl1_key    chain     rlevl1
     c                   if        %found
     c                   eval      b1lnav = rlnavn
     c                   endif
      *
      * Hent beskrivelse av varegruppe
      *
     c                   eval      vugrl1_uogr = fdogrp
     c                   eval      vugrl1_uhgr = fdhgrp
     c                   eval      vugrl1_uugr = fdugrp
     c     vugrl1_key    chain     vugrl1
     c                   if        %found
     c                   eval      b1utxt = vgutxt
     c                   endif
      *
      * Behandler bildet
      *
     c     b1taga        tag
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   endsl
6.30  *
6.30  * F18=NOBB informasjon
6.30  *
6.30 c                   if        *inks = *on
6.30 c                   exsr      vis_nobb
6.30 c                   goto      b1taga
6.30 c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  * Vis NOBB informasjon for varen. Samme prinsipp som preview av utskrifter
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  *
6.30 c     vis_nobb      begsr
6.30 c
6.30 c                   eval      p_nobb = %Char(b1nobb)
6.30 c                   call      'AP633R'
6.30 c                   parm                    p_nobb
6.30  *
6.30 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
6.NU c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_line
      *
      * Key for oppslag på ordre-hode
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for oppslag på ordre-linje
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
     c                   kfld                    fodtl1_line
      *
      * Key for oppslag på bruker
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrl1_user
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
      * Hente inn brukerregister
      *
     c                   eval      fusrl1_user = l_user
     c     fusrl1_key    chain     fusrl1r
      *
      *
     c                   endsr
