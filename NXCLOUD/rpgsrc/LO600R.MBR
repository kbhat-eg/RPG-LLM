     H/TITLE  ASLAGR: Inntasting av parameter for utskrift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  System. . . . . : ASLAGR                                       *
      *  Program . . . . : LO600R                                       *
      *  Beskrivelse . . : Inntasting av parameter for utskrift         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  Spesialversjon. :                                              *
      *  Tilpasset for . :                                              *
      *                                                                 *
      * Ref.   Dato  Sign Endringsbeskrivelse                           *
      * ----- ------ ---- ----------------------------------------------*
5.04  * R5.04 211002 AMD  Lagt inn logikk for sending av e-mail
      *  6.10 180609 BSA  Oppdatert med sporingsinformasjon
pdf   *  6.10 240610 BSA  Håndteringen av mail skjer nå fra parameter  bilde 2
pdf   *                   for POA aktiverte rutiner.
6.11  *  6.10 051010 BSA  Må kunne skille mellom ny og gammel rutine for å legge
6.11  *                   ut e-mail.
6.12  *  6.10 050612 AMD  Lagt inn samme styring som i OFAK fra ordre -
6.12  *                   type for Felles/Direkte/Jobbkø. Parameter
6.12  *                   WPSVAR har derfor ingen funksjon lenger.
6.21  *  6.20 140812 bof  Lagt inn styring med bildevalg for e3valg
6.nu  *  6.30 120614 bof  Utvidet ihht. nummerutvidelse
6.22  *  6.20 291215 nho  Sjekk leveringsmåte
6.23  *  6.20 040115 nho  Må sjekke om leveringsmåte er et krav
6.30  *  6.30 070116 bsa  Endret ordlyden på ledetekst i skjermbildet (E3BLD)
7.xx  * 7.00  130916 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  270121 bsa  Endret logging.
      *
8.01  *K000   300123 bsa  Hvis flåtestyring, send oppdatering til transport
8.01  *K000               kontor.
8.02  *K000   060223 bsa  Flagg om sjekk på loggkode i transportlogging
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     FLO600D    CF   E             WORKSTN
      *
     FVOTYL1    IF   E           K DISK
     F                                     RENAME(VOTYPFR:VOTYL1R)
      *
     FLOHELU    UF   E           K DISK
     F                                     RENAME(LOHEPFR:LOHELUR)
     FLOHEL1    IF   E           K DISK
     F                                     RENAME(LOHEPFR:LOHEL1R)
     FLPLOLU    UF A E           K DISK
     F                                     RENAME(LPLOPFR:LPLOLUR)
     FLPLOL1    IF   E           K DISK
     F                                     RENAME(LPLOPFR:LPLOL1R)
pdf  fafopl1    if   e           k disk    rename(afoppfr:afopl1r)
pdf  fafpslr    if   e           k disk    rename(afpspfr:afpslrr)
pdf  fafapl1    if   e           k disk    rename(afappfr:afapl1r)
6.23 favall1    if   e           k disk    rename(avalpfr:avall1r)
7.01 flloxl1    if   e           k disk    rename(lloxpfR:lloxl1r)
      *
      *
      * Datastruktur
      *
     d                 ds
     d d_urec                        80
     d  d_firm                        3  0 overlay(d_urec)
6.nu d  d_numm                        8  0 overlay(d_urec:4)
6.nu d  d_suff                        2  0 overlay(d_urec:12)
6.nu d  d_kode                        1    overlay(d_urec:14)
6.nu d  d_date                         d   overlay(d_urec:15) datfmt(*iso)
6.nu d  d_time                         t   overlay(d_urec:25) timfmt(*iso)
6.nu d  d_user                       10    overlay(d_urec:33)
6.nu d  d_wsid                       10    overlay(d_urec:43)
      *
     D                UDS
pdf  d l_poff                584    584
pdf  d l_ruti                800    809
     D  DSWSID               901    910
     D  DSUSER               911    920
pdf  d l_filg                931    933
     D  DSSFIR               944    946  0
pdf  d l_avde                947    950  0
      *
      * Definering av øvrige variable
      *
     d w_stat          s              1
     d w_parm          s             80
6.11 d w_ruti          s                   like(l_ruti)
8.01 d w_reqs          s              5
8.01 d w_skaf          s              1  0
8.02 d w_logg          s              1  0
      *
6.11 d p_ruti          s                   like(l_ruti)
6.11 d p_pdf           s              1
      *
pdf  d afpslr_ufil     s                   like(l_filg)
pdf  d afpslr_uide     s                   like(afuide)
pdf  d afopl1_ruti     s                   like(l_ruti)
pdf  d afapl1_arut     s                   like(l_ruti)
pdf  d afapl1_afir     s                   like(afafir)
pdf  d afapl1_aavd     s                   like(afaavd)
6.23 d avall1_apgm     s                   like(avapgm)
6.23 d w_firm          s                   like(avafir)
7.01 d lloxl1_numm     s                   like(lmnumm)
7.01 d lloxl1_suff     s                   like(lmsuff)
7.01 d lloxl1_kode     s                   like(lmkode)
7.01 d lloxl1_date     s                   like(lmdate)
7.01 d lloxl1_time     s                   like(lmtime)
7.01 d b_mail          s              1    inz(*off)
      *
8.01 d u_flat          s              1    inz(*on)
      *
8.01  *
8.01  * Definering av eksterne prg
8.01  *
8.01   dcl-s co402_verdi1  char(1);
8.01   dcl-s co402_verdi2  char(2048);
8.01   DCL-PR CO402R EXTPGM('CO402R');
8.01     p_filgrp            char(3)     const;
8.01     p_firm              packed(3:0) const;
8.01     p_lager             packed(2:0) const;
8.01     p_nokkel            char(50)    const;
8.01     p_verdi1            char(1);
8.01     p_verdi2            char(2048);
8.01   END-PR;
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Velg utfra ORDRETYPE hvilket bilde som skal vises              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *  Hent opplysninger fra BESTILLING-HODE-REGISTER
      *
     C     HEDKEY        CHAIN     LOHEL1R                            90
     C  N90              MOVE      LOOTYP        WWOTYP
      *
      *  Hent opplysninger fra UTPLUKK-PARAM-REGISTER
      *       (Ordretype hentes kun om den finnes plukket tidligere)
      *
     C     PLOKEY        CHAIN     LPLOL1R                            90
     C  N90              MOVE      LKPOTY        WWOTYP
      *
      *  Hent opplysninger fra ORDRETYPE-REGISTER
      *
     C                   MOVE      WWOTYP        OTYTYP
     C     OTYKEY        CHAIN     VOTYL1R                            90
      *
      *  Utfra ordretype (akk.type) hopp til riktig bilde
      *
     C     VAOAKK        CABEQ     0             E2TAG
     C     VAOAKK        CABEQ     1             E3TAG
     C     VAOAKK        CABEQ     2             E4TAG
     C     VAOAKK        CABEQ     3             E5TAG
      *
     C                   GOTO      XSLUTT
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  E2 Behandler valg for utskrift av BESTILLINGSFORSLAG           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     E2TAG         TAG
      *
6.11 c                   eval      w_ruti = 'LH600    '
6.11 c                   exsr      sjk_proa
      *
      *  Nullstiller felter som benyttes
      *
     C                   MOVE      *ZERO         E2VALG
6.12 c                   move      vaofdj        e2svar
     C                   MOVE      *BLANK        E2OUTQ
      *
     C     E2TAGA        TAG
     C                   EXFMT     E2BLD
      *
      *  F3=Avslutt
      *
     C     *INKC         IFEQ      *ON
     C                   MOVE      '1'           WPIPOR
     C                   GOTO      XSLUTT
     C                   END
6.23  * Firmastyrt validering av utvalgte felter
6.23  *
6.23  *
6.23 c                   eval      avall1_apgm = 'LO102R'
6.23 c     avall1_key    setll     avall1
6.23 c     avall1_key    reade     avall1
6.23 c                   dow       not %eof(avall1)
6.23 c                   if        avaflt = 'LOLMAT'  and
6.23 c                             avaval = 1
6.22 C                   IF        LOLMAT = 0
6.22 C                   MOVE      *ON           *IN38
6.22 C                   GOTO      E2TAGA
6.22 C                   ENDIF
6.23 c                   endif
6.23 c     avall1_key    reade     avall1
6.23 c                   enddo
      *
      *  Sjekk om skriver er en gyldig device
      *
     C     E2OUTQ        IFNE      *BLANK
      *
     C                   MOVE      *BLANK        WSJEKK
     C                   CALL      'FQPRTC'
     C                   PARM                    WSJEKK
     C                   PARM                    E2OUTQ
      *
     C     WSJEKK        IFNE      *BLANK
     C                   MOVE      *ON           *IN31
     C                   GOTO      E2TAGA
     C                   END
     C                   END
      *
      *  Legger opplysninger over i felles-felter
      *
     C                   MOVE      E2VALG        WWPALT
     C                   MOVE      E2SVAR        WWSVAR
     C                   MOVE      E2OUTQ        WWOUTQ
      *
     C                   GOTO      START
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  E3 Behandler valg for utskrift av BESTILLING                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     E3TAG         TAG
      *
6.11 c                   eval      w_ruti = 'LBEST    '
6.11 c                   exsr      sjk_proa
      *
      *  Nullstiller felter som benyttes
      *
6.21 c                   if        vaobld = 0 or
6.21 c                             vaobld = 1
6.21 c                   eval      e3valg = vaobld
6.21 c                   else
     C                   Z-ADD     1             E3VALG
6.21 c                   endif
6.12 c                   move      vaofdj        e3svar
     C                   MOVE      *BLANK        E3OUTQ
      *
     C     E3TAGA        TAG
     C                   EXFMT     E3BLD
      *
      *  F3=Avslutt
      *
     C     *INKC         IFEQ      *ON
     C                   MOVE      '1'           WPIPOR
     C                   GOTO      XSLUTT
     C                   END
6.23  * Firmastyrt validering av utvalgte felter
6.23  *
6.23  *
6.23 c                   eval      avall1_apgm = 'LO102R'
6.23 c     avall1_key    setll     avall1
6.23 c     avall1_key    reade     avall1
6.23 c                   dow       not %eof(avall1)
6.23 c                   if        avaflt = 'LOLMAT'  and
6.23 c                             avaval = 1
6.22 C                   IF        LOLMAT = 0
6.22 C                   MOVE      *ON           *IN38
6.22 C                   GOTO      E3TAGA
6.23 c                   endif
6.22 C                   ENDIF
6.23 c     avall1_key    reade     avall1
6.23 c                   enddo
      *
      *  Sjekk om skriver er en gyldig device
      *
     C     E3OUTQ        IFNE      *BLANK
      *
     C                   MOVE      *BLANK        WSJEKK
     C                   CALL      'FQPRTC'
     C                   PARM                    WSJEKK
     C                   PARM                    E3OUTQ
      *
     C     WSJEKK        IFNE      *BLANK
     C                   MOVE      *ON           *IN31
     C                   GOTO      E3TAGA
     C                   END
     C                   END
      *
      *  Legger opplysninger over i felles-felter
      *
     C                   MOVE      E3VALG        WWPALT
     C                   MOVE      E3SVAR        WWSVAR
     C                   MOVE      E3OUTQ        WWOUTQ
5.04  *
5.04  *  Styring til e-mail
5.04  *
5.04 C                   if        e3svar = 3
5.04 C                   eval      e3svar = 1
5.04 C                   eval      e3outq = 'E_MAIL_ASP'
5.04 c                   eval      wwsvar = e3svar
5.04 c                   eval      wwoutq = e3outq
5.04 C                   endif
      *
7.01  * Logger i loghistorikk
7.01  *
7.01 c*                  eval      d_firm = wpfirm
7.01 c*                  eval      d_numm = wpnumm
7.01 c*                  eval      d_suff = wpsuff
7.01 c*                  eval      d_kode = '1'
7.01 c*                  eval      d_date = *loval
7.01 c*                  eval      d_time = *loval
7.01 c*                  eval      d_user = dsuser
7.01 c*                  eval      d_wsid = dswsid
7.01 c*                  eval      w_parm = d_urec
7.01  *
7.01 c*                  call      'LM900R'
7.01 c*                  parm                    w_stat
7.01 c*                  parm                    w_parm
5.01  *
     C                   GOTO      START
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  E4 Behandler valg for utskrift av INNGÅENDE PAKKSEDDEL         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     E4TAG         TAG
      *
6.11 c                   eval      w_ruti = 'LPAKK    '
6.11 c                   exsr      sjk_proa
      *
      *  Nullstiller felter som benyttes
      *
6.12 c                   move      vaofdj        E4SVAR
     C                   MOVE      *BLANK        E4OUTQ
      *
     C     E4TAGA        TAG
     C                   EXFMT     E4BLD
      *
      *  F3=Avslutt
      *
     C     *INKC         IFEQ      *ON
     C                   MOVE      '1'           WPIPOR
     C                   GOTO      XSLUTT
     C                   END
6.23  * Firmastyrt validering av utvalgte felter
6.23  *
6.23 c                   eval      avall1_apgm = 'LO102R'
6.23 c     avall1_key    setll     avall1
6.23 c     avall1_key    reade     avall1
6.23 c                   dow       not %eof(avall1)
6.23 c                   if        avaflt = 'LOLMAT'  and
6.23 c                             avaval = 1
6.22 C                   IF        LOLMAT = 0
6.22 C                   MOVE      *ON           *IN38
6.22 C                   GOTO      E4TAGA
6.23 c                   endif
6.22 C                   ENDIF
6.23 c     avall1_key    reade     avall1
6.23 c                   enddo
      *
      *  Sjekk om skriver er en gyldig device
      *
     C     E4OUTQ        IFNE      *BLANK
      *
     C                   MOVE      *BLANK        WSJEKK
     C                   CALL      'FQPRTC'
     C                   PARM                    WSJEKK
     C                   PARM                    E4OUTQ
      *
     C     WSJEKK        IFNE      *BLANK
     C                   MOVE      *ON           *IN31
     C                   GOTO      E4TAGA
     C                   END
     C                   END
      *
      *  Legger opplysninger over i felles-felter
      *
     C                   MOVE      *ZERO         WWPALT
     C                   MOVE      E4SVAR        WWSVAR
     C                   MOVE      E4OUTQ        WWOUTQ
      *
     C                   GOTO      START
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  E5 Behandler valg for utskrift av INNG_FAKTURA/KREDITNOTA      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     E5TAG         TAG
      *
6.11 c                   eval      w_ruti = 'LFAKT    '
6.11 c                   exsr      sjk_proa
      *
      *  Nullstiller felter som benyttes
      *
6.12 c                   move      vaofdj        e5svar
     C                   MOVE      *BLANK        E5OUTQ
      *
     C     E5TAGA        TAG
     C                   EXFMT     E5BLD
      *
      *  F3=Avslutt
      *
     C     *INKC         IFEQ      *ON
     C                   MOVE      '1'           WPIPOR
     C                   GOTO      XSLUTT
     C                   END
6.23  *
6.23 c                   eval      avall1_apgm = 'LO102R'
6.23 c     avall1_key    setll     avall1
6.23 c     avall1_key    reade     avall1
6.23 c                   dow       not %eof(avall1)
6.23 c                   if        avaflt = 'LOLMAT'  and
6.23 c                             avaval = 1
6.22 C                   IF        LOLMAT = 0
6.22 C                   MOVE      *ON           *IN38
6.22 C                   GOTO      E5TAGA
6.23 c                   endif
6.22 C                   ENDIF
6.23 c     avall1_key    reade     avall1
6.23 c                   enddo
      *
      *  Sjekk om skriver er en gyldig device
      *
     C     E5OUTQ        IFNE      *BLANK
      *
     C                   MOVE      *BLANK        WSJEKK
     C                   CALL      'FQPRTC'
     C                   PARM                    WSJEKK
     C                   PARM                    E5OUTQ
      *
     C     WSJEKK        IFNE      *BLANK
     C                   MOVE      *ON           *IN31
     C                   GOTO      E5TAGA
     C                   END
     C                   END
      *
      *  Legger opplysninger over i felles-felter
      *
     C                   MOVE      *ZERO         WWPALT
     C                   MOVE      E5SVAR        WWSVAR
     C                   MOVE      E5OUTQ        WWOUTQ
      *
     C                   GOTO      START
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  START Behandler oppdatering av BESTILLING-HODE-REGISTER        *
      *        samt utlegging/oppdatering av UTPLUKK-PARAM-REGISTER     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     START         TAG
      *
      *  Oppdater BESTILLING-HODE-REGISTER, BESTILLINGEN er nå til behandling
      *
     C     HEDKEY        CHAIN     LOHELUR                            90
     C     *IN90         IFEQ      *OFF
      *
      *  Dersom bestillingen allerde er plukket, skal ikke koden endres
      *
     C     LOKODE        IFNE      3
     C                   MOVE      2             LOKODE
     C                   END
      *
6.10 c                   time                    loedat
6.10 c                   time                    loetim
6.10 c                   eval      loeusr = dsuser
     C                   UPDATE    LOHELUR
     C                   END
      *
      *  Legg ut nye records til  BESTILLING-PLUKK-HODE-REGISTER
      *  Eventuelt oppdater, dersom bestillingen allerede er plukket
      *
     C     PLOKEY        CHAIN     LPLOLUR                            90
     C     *IN90         IFEQ      *ON
     C                   MOVE      PLOFIR        LKPFIR
     C                   MOVE      PLONUM        LKPONR
     C                   MOVE      PLOSUF        LKPSUF
     C                   MOVE      WWOTYP        LKPOTY
     C                   MOVE      DSWSID        LKPWSD
     C                   MOVE      DSUSER        LKPUSR
     C                   MOVE      WWPALT        LKPALT
     C                   MOVE      WWSVAR        LKPDIR
     C                   WRITE     LPLOLUR
     C                   ELSE
     C                   MOVE      WWPALT        LKPALT
     C                   MOVE      WWSVAR        LKPDIR
     C                   UPDATE    LPLOLUR
     C                   END
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Kaller opp utskriftsprogrammet, dersom direkte utskrift        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     WWSVAR        IFEQ      1
     C     WWSVAR        OREQ      2
     C                   CALL      'LO604R'
     C                   PARM                    WPSKOR
     C                   PARM                    PLOFIR
     C                   PARM                    PLONUM
     C                   PARM                    PLOSUF
     C                   PARM                    WWOTYP
     C                   PARM                    WWSVAR
     C                   PARM                    WWOUTQ
     C                   END
7.01  *
7.01  * Oppdatering av loggkode skjer her.
7.01  *
7.01 c                   eval      b_mail = *off
7.01 c                   if        vaoakk = 1
7.01 c                   eval      lloxl1_numm = hednum
7.01 c                   eval      lloxl1_suff = hedsuf
7.01 c                   eval      lloxl1_kode = 'M'
7.01 c                   eval      lloxl1_date = *loval
7.01 c                   eval      lloxl1_time = *loval
7.01 c     lloxl1_key    chain     lloxl1
7.01 c                   if        %found(lloxl1)
7.01 c                   eval      b_mail = *on
7.01 c                   endif
7.01 c                   endif
7.01
7.01  * Logger i loghistorikk
7.01  *
7.01 c                   eval      d_firm = wpfirm
7.01 c                   eval      d_numm = wpnumm
7.01 c                   eval      d_suff = wpsuff
7.01 c                   if        b_mail = *on
7.01 c                   eval      d_kode = '5'
7.01 c                   else
7.01 c                   eval      d_kode = '1'
7.01 c                   endif
7.01  *
7.01 c                   eval      d_date = *loval
7.01 c                   eval      d_time = *loval
7.01 c                   eval      d_user = dsuser
7.01 c                   eval      d_wsid = dswsid
7.01 c                   eval      w_parm = d_urec
7.01  *
7.01 c                   call      'LM900R'
7.01 c                   parm                    w_stat
7.01 c                   parm                    w_parm
8.01  *
8.01  * Hvis flåtestyring, sjekk om vi skal sende transportrequest.
8.01  *
8.01 c                   if        u_flat = *on and
8.01 c                             loornr <> 0
8.01  *
8.01 c                   eval      w_reqs = *blanks
8.01 c                   eval      w_skaf = 1
8.02 c                   eval      w_logg = 1
8.01 c                   call      'AP058R '
8.01 c                   parm                    w_firm
8.01 c                   parm                    loornr
8.01 c                   parm                    loorsu
8.01 c                   parm                    w_skaf
8.02 c                   parm                    w_logg
8.01 c                   parm                    w_reqs
8.01  *
8.01 c                   endif
7.01  *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSLUTT        TAG
      *
     C                   SETON                                        LR
     C                   RETURN
      *
6.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.11  *  Subrutine for initiering av program
6.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.11  *
6.11 c     sjk_proa      begsr
6.11  *
6.11 c                   eval      p_pdf ='0'
6.11 c                   eval      p_ruti= w_ruti
6.11  * Sjekk om PROA er aktivert
6.11 c                   call      'AP608R'
6.11 c                   parm                    p_ruti
6.11 c                   parm                    p_pdf
6.11  * p_pdf = '1' = PROA aktivert
6.11 c                   if        p_pdf = '0'
6.11 c                   eval      *in35 = *off
6.11 c                   endif
6.11  *
6.11 c                   endsr
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     CSR   *INZSR        BEGSR
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     C                   MOVE      *BLANK        WPSKOR            1
     C                   MOVE      *BLANK        WPIPOR            1
     C                   MOVE      *BLANK        WSJEKK            1
     C                   MOVE      *BLANK        WWOUTQ           10
pdf  c                   eval      *in35 = *on
      *
      *  Redefinering av felter
      *
     C     *LIKE         DEFINE    LOOTYP        WWOTYP
     C     *LIKE         DEFINE    LKPALT        WWPALT
     C     *LIKE         DEFINE    LKPDIR        WWSVAR
      *
      *  Redefinering av parameterfeltene
      *
     C     *LIKE         DEFINE    LOFIRM        WPFIRM
     C     *LIKE         DEFINE    LONUMM        WPNUMM
     C     *LIKE         DEFINE    LOSUFF        WPSUFF
      *
      *  Redefinering av nøkler
      *
     C     *LIKE         DEFINE    VAOFIR        OTYFIR
     C     *LIKE         DEFINE    VAOTYP        OTYTYP
      *
     C     *LIKE         DEFINE    LOFIRM        HEDFIR
     C     *LIKE         DEFINE    LONUMM        HEDNUM
     C     *LIKE         DEFINE    LOSUFF        HEDSUF
      *
     C     *LIKE         DEFINE    LKPFIR        PLOFIR
     C     *LIKE         DEFINE    LKPONR        PLONUM
     C     *LIKE         DEFINE    LKPSUF        PLOSUF
      *
      * Key for file : ORDRE-TYPE-REGISTER
      *
6.23  *
6.23  * Key for file : Firmastyrt validering
6.23  *
6.23 c     avall1_key    klist
6.23 c                   kfld                    w_firm
6.23 c                   kfld                    avall1_apgm
      *
     C     OTYKEY        KLIST
     C                   KFLD                    OTYFIR
     C                   KFLD                    OTYTYP
      *
      * Key for file : BESTILLING-HODE-REGISTER
      *
     C     HEDKEY        KLIST
     C                   KFLD                    HEDFIR
     C                   KFLD                    HEDNUM
     C                   KFLD                    HEDSUF
      *
      * Key for file : BESTILLING-PLUKK-HODE-REGISTER
      *
     C     PLOKEY        KLIST
     C                   KFLD                    PLOFIR
     C                   KFLD                    PLONUM
     C                   KFLD                    PLOSUF
7.01  *
7.01  * Key for file : LLOXPF sideregister for omm bestilling er sendt på mail
7.01  *
7.01 C     lloxl1_key    klist
7.01 C                   kfld                    w_firm
7.01 C                   kfld                    lloxl1_numm
7.01 C                   kfld                    lloxl1_suff
7.01 C                   kfld                    lloxl1_kode
7.01 C                   kfld                    lloxl1_date
7.01 C                   kfld                    lloxl1_time
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    WPIPOR
     C                   PARM                    WPFIRM
     C                   PARM                    WPNUMM
     C                   PARM                    WPSUFF
     C                   PARM                    WPSVAR            1 0
      *
      *  Legger inn firmanummer i nøkklene
      *
     C                   MOVE      WPFIRM        OTYFIR
     C                   MOVE      WPFIRM        HEDFIR
     C                   MOVE      WPFIRM        PLOFIR
6.23 C                   MOVE      WPFIRM        W_FIRM
      *
      *  Legger inn bestillingsnummer og suffix i nøkklene
      *
     C                   MOVE      WPNUMM        HEDNUM
     C                   MOVE      WPSUFF        HEDSUF
      *
     C                   MOVE      WPNUMM        PLONUM
     C                   MOVE      WPSUFF        PLOSUF
      *
      *  Nullstiller returvalg
      *
     C                   MOVE      *BLANK        WPIPOR
8.01  *
8.01  * Endring 8.01 - Koblet til ekstern flåtestyring
8.01  *
8.01   CallP CO402R(l_filg:w_firm:0:'EKSTERN_FLATESTYRING':
8.01                    co402_verdi1:co402_verdi2);
8.01   if co402_verdi1 = '1';
8.01     u_flat = *on;
8.01   endif;
      *
     CSR                 ENDSR
