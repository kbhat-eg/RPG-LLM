     H decedit('0.') datedit(*dmy-) option(*nodebugio)
     h DftActgrp(*No) Actgrp(*NEW)
      *
      /copy hspecsbnd
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : AP051R
      * Beskrivelse . . : Lese retur fra Profile Manager
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       200921 bsa  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fpromir    uf a e           k disk    rename(promst:promirr)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d promir_memb     s                   like(pmmemb)
     d promir_pkey     s                   like(pmpkey)
      *
     d w_id            s             10  0
     d w_version       s             10  0
     d w_code          s             50
     d w_latest        s              5
     d w_name          s            100
     d w_description   s            100
     d w_domident      s             20
     d w_creatdate     s             23
     d w_lastmodi      s             23
     d w_numbrules     s             10  0
     d w_state         s             10
     d w_pubdate       s             23
     d w_numartic      s             10  0
     d w_generated     s              5
      *
     d w_tell          s              5  0 inz(0)
      *
     d jsonData        s            256a   Varying
     d options         s            256a   Varying
      *
     d p_memb          s             10
     d p_api_input     s            256    varying
     d p_api_status    s              1
      *
     d b_read          s              1    inz(*off)
      *
      /copy prototypeb
      /copy usec
      *
      * Datastruktur for json fra rest API tjeneste
      *

       Dcl-ds data Dim(9999) qualified;
           id int(10);
           version int(10);
           code varchar(50);
           latest varchar(5);
           name varchar(100);
           description varchar(100);
           domainIdentifier varchar(20);
           creationDateTime varchar(23);
           lastModifiedDateTime varchar(23);
           numberOfRules int(10);
           state varchar(10);
           publishedDateTime varchar(23);
           numberOfAffectedArticles varchar(10);
           generated varchar(5);
          end-ds data;

          dcl-ds *N psds;
                count int(20) pos(372);
          end-ds;
      *
      * Så leser vi innholdet i json fila
      *
      /Free
      //   jsonData = %trim('/home/adbbms/pmanager/pm63742.response');
                jsonData = %trim(p_api_input);

                options  = 'doc=file +
                            case=any +
                            allowextra=yes +
                            path=JsonRoot/data';

                data-into data %Data( jsonData : options)
                            %Parser( 'YAJL/YAJLINTO'
                            : '{"document_name": "JsonRoot"}');

      /End-free
      *
     c                   if        count = 0
     c                   eval      p_api_status = '7'
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      p_api_status = '1'
     c                   eval      w_tell = w_tell + 1
     c                   dow       w_tell <= count
      *
      /Free
          //    if data(w_tell).id = 0;
          //    b_read = *off;
          //    endif;

                if  data(w_tell).id <> 0;
                    w_id = data(w_tell).id;
                    w_version = data(w_tell).version;
                    w_code = data(w_tell).code;
                    w_latest = data(w_tell).latest;
                    w_name = data(w_tell).name;
                    w_description = data(w_tell).description;
                    w_domident = data(w_tell).domainIdentifier;
                    w_creatdate = data(w_tell).creationDateTime;
                    w_lastmodi = data(w_tell).lastModifiedDateTime;
                    w_numbrules = data(w_tell).numberOfRules;
                    w_state = data(w_tell).state;
                    w_pubdate = data(w_tell).publishedDateTime;
                    if data(w_tell).numberOfAffectedArticles <> '*NULL';
                    w_numartic = %int(data(w_tell).numberOfAffectedArticles);
                    else;
                    w_numartic = 0;
                    endif;
                    w_generated = data(w_tell).generated;
                endif;
      /End-free
      *
      * Skriv til tabell
      *
     c                   if        %trim(w_state) = 'PUBLISHED'
      *
     c                   eval      promir_pkey = %char(w_code)
     c     promir_key    chain     promir
     c                   if        not %found
     c                   eval      pmmemb = promir_memb
     c                   eval      pmpkey = promir_pkey
      *
     c                   eval      pmnoit = w_numartic
     c                   eval      pmpmtx = w_name
      *
     c                   time                    pmodat
     c                   time                    pmotim
     c                   eval      pmousr = l_user
      *
     c                   write     promirr
     c                   endif
      *
     c                   endif
      *
     c                   eval      w_tell = w_tell + 1
      *
     c                   enddo
      *
     c     avslutt       tag
      *
      * Avslutt program
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Henter inn parameter
      *
     c     *entry        plist
     c                   parm                    p_memb
     c                   parm                    p_api_input
     c                   parm                    p_api_status
      *
      * Key for les av profile info
      *
     c     promir_key    klist
     c                   kfld                    promir_memb
     c                   kfld                    promir_pkey
      *
     c                   eval      promir_memb = p_memb
9.xx c*                  eval      promir_memb = 'AA00000001'
9.xx c*                  eval      p_api_input = '/home/adbbms/pmanager/+
9.xx c*                            pm63746.response'
      *
     c                   endsr
