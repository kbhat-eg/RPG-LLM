     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASPGPL
      * Program . . . . : AA018R
      * Beskrivelse . . : PDF parametre - avvik
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       280510 BSA  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fafapl1    if   e           k disk    rename(afappfr:afapl1r)
     fafaplr    if   e           k disk    rename(afappfr:afaplrr)
     fafopl1    if   e           k disk    rename(afoppfr:afopl1r)
     fafaplu    uf a e           k disk    rename(afappfr:afaplur)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     fra07l1    if   e           k disk    rename(ra07pfr:ra07l1r)
      *
     faa018d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d afapl1_afir     s                   like(afafir)
     d afapl1_aavd     s                   like(afaavd)
     d afaplr_afir     s                   like(afafir)
     d afaplr_aavd     s                   like(afaavd)
     d afaplu_afir     s                   like(afafir)
     d afaplu_aavd     s                   like(afaavd)
     d afirl1_ffir     s                   like(aaffir)
     d ra07l1_gfir     s                   like(ragfir)
     d ra07l1_gkod     s                   like(ragkod)
      *
     d s_afir          s                   like(afafir)
     d s_aavd          s                   like(afaavd)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_ruti          s                   like(afarut)
     d w_edit          s              1  0
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d p_gkod          s                   like(ragkod)
     d p_gtxt          s             20
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2afir <> *zero
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      afaplr_afir = b1afir
     c     afaplr_key    setll     afaplr
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Firma
      *
     c                   if        b2afir <> *zero
     c                   eval      w_seqe = *blank
     c                   eval      afaplr_afir = b2afir
     c     afaplr_key    setll     afaplr
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2afir = *zero
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1afir = b1afir
     c                   eval      c1aavd = b1aavd
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1afir = b1afir
     c                   eval      k1aavd = b1aavd
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1afir = b1afir
     c                   eval      d1aavd = b1aavd
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1afir = b1afir
     c                   eval      c1aavd = b1aavd
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1ruti = w_ruti
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
      *
      * Call program for AVDELING
      *
     c*                  if        w_afld = 'C1AAVD'
     c                   call      'RA507R'
     c                   parm                    c1afir
     c                   parm                    p_gkod
     c                   parm                    p_gtxt
      *
     c                   if        p_gkod <> *zero
     c                   move      p_gkod        c1aavd
     c                   move      p_gtxt        c1atxt
     c                   endif
     c*                  endif
      *
     c                   endif
      *
      * Validering
      *
     c                   eval      afirl1_ffir = c1afir
     c     afirl1_key    chain     afirl1
     c                   if        not %found
     c                   exsr      xc1msg
     c                   goto      c1taga
     c                   else
     c                   movel     aafnav        c1ftxt
     c                   endif
      *
     c                   eval      ra07l1_gfir = c1afir
     c                   eval      ra07l1_gkod = c1aavd
     c     ra07l1_key    chain     ra07l1
     c                   if        not %found
     c                   exsr      xc1msg
     c                   goto      c1taga
     c                   else
     c                   movel     ragtxt        c1atxt
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      afapl1_afir = c1afir
     c                   eval      afapl1_aavd = c1aavd
     c     afapl1_key    chain     afapl1
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1afir = *zero
     c                   eval      c1aavd = *zero
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2afir = *zero
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      * Hent inn tekster
     c                   eval      c2afir = c1afir
     c                   eval      afirl1_ffir = c1afir
     c     afirl1_key    chain     afirl1
     c                   if        not %found
     c                   eval      c2ftxt = *blank
     c                   else
     c                   movel     aafnav        c2ftxt
     c                   endif
      * Hnet inn tekster
     c                   eval      c2aavd = c1aavd
     c                   eval      ra07l1_gfir = c1afir
     c                   eval      ra07l1_gkod = c1aavd
     c     ra07l1_key    chain     ra07l1
     c                   if        not %found
     c                   eval      c2atxt = *blanks
     c                   else
     c                   movel     ragtxt        c2atxt
     c                   endif
      *
     c                   eval      afapl1_afir = c1afir
     c                   eval      afapl1_aavd = c1aavd
     c     afapl1_key    chain     afapl1
      *
     c                   if        %found
     c                   eval      c2updf = afapdf
     c                   eval      c2mail = afamai
     c                   eval      c2exlp = afaexl
     c                   eval      c2arki = afaarc
     c                   eval      c2skje = afaprv
     c                   eval      c2land = afalan
     c                   eval      c2xmlm = afaxlm
     c                   eval      c2jasm = afajas
     c                   eval      c2logo = afalog
     c                   else
      * Henter informasjon fra standard
     c     afopl1_key    chain     afopl1
     c                   if        %found
     c                   eval      c2updf = afapdf
     c                   eval      c2mail = afamai
     c                   eval      c2exlp = afaexl
     c                   eval      c2arki = afaarc
     c                   eval      c2skje = afaprv
     c                   eval      c2land = afalan
     c                   eval      c2xmlm = afaxlm
     c                   eval      c2jasm = afajas
     c                   eval      c2logo = afajas
     c                   else
     c                   eval      c2updf = 0
     c                   eval      c2mail = 0
     c                   eval      c2exlp = 0
     c                   eval      c2arki = 0
     c                   eval      c2skje = 0
     c                   eval      c2land = 0
     c                   eval      c2xmlm = *blanks
     c                   eval      c2jasm = *blanks
     c                   endif
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1afir = k1afir
     c                   eval      c1aavd = k1aavd
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c*                  if        c2mtxt = *blank
     c*                  eval      *in31 = *on
     c*                  goto      c2taga
     c*                  endif
      *
      * Oppdatering
      *
     c                   eval      afaplu_afir = c1afir
     c                   eval      afaplu_aavd = c1aavd
     c     afaplu_key    chain     afaplur
      * Oppdater med verdier
     c                   if        not %found
     c                   clear                   afaplur
     c                   endif
      *
     c                   eval      afapdf = c2updf
     c                   eval      afamai = c2mail
     c                   eval      afaexl = c2exlp
     c                   eval      afaarc = c2arki
     c                   eval      afaprv = c2skje
     c                   eval      afalan = c2land
     c                   eval      afaxlm = c2xmlm
     c                   eval      afajas = c2jasm
     c                   eval      afalog = c2logo
      *
     c                   time                    afaeda
     c                   time                    afaeti
     c                   eval      afaeus = l_user
      *
     c                   if        %found
     c                   update    afaplur
     c                   else
     c                   eval      afarut = w_ruti
     c                   eval      afafir = c1afir
     c                   eval      afaavd = c1aavd
     c                   time                    afaoda
     c                   time                    afaoti
     c                   eval      afaous = l_user
     c                   write     afaplur
     c                   endif
      *
      * Oppdater subfile
      *
     c*                  if        b1valg = 2
     c*                  eval      b1mtxt = axmtxt
     c*                  endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      afapl1_afir = d1afir
     c                   eval      afapl1_aavd = d1aavd
     c     afapl1_key    chain     afapl1
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      afaplu_afir = d1afir
     c                   eval      afaplu_aavd = d1aavd
     c     afaplu_key    chain     afaplur
      *
     c                   delete    afaplur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_afir = k1afir
     c                   eval      s_aavd = k1aavd
      *
      * Fyller kopierings-bildet
      *
     c                   eval      afaplr_afir = k1afir
     c                   eval      afaplr_aavd = k1aavd
     c     afaplr_key    chain     afaplr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   if        *inkd
      *
      * Call program for AVDELING
      *
     c*                  if        w_afld = 'K1AAVD'
     c                   call      'RA507R'
     c                   parm                    k1afir
     c                   parm                    p_gkod
     c                   parm                    p_gtxt
      *
     c                   if        p_gkod <> *zero
     c                   move      p_gkod        k1aavd
     c*                  move      p_gtxt        c1atxt
     c                   endif
     c*                  endif
      *
     c                   endif
      *
      * Validering
      *
     c                   eval      afirl1_ffir = k1afir
     c     afirl1_key    chain     afirl1
     c                   if        not %found
     c                   exsr      xc1msg
     c                   goto      k1taga
     c*                  else
     c*                  movel     aafnav        c1ftxt
     c                   endif
      *
     c                   eval      ra07l1_gfir = k1afir
     c                   eval      ra07l1_gkod = k1aavd
     c     ra07l1_key    chain     ra07l1
     c                   if        not %found
     c                   exsr      xc1msg
     c                   goto      k1taga
     c*                  else
     c*                  movel     ragtxt        c1atxt
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      afaplr_afir = k1afir
     c                   eval      afaplr_aavd = k1aavd
     c     afaplr_key    chain     afaplr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1afir = s_afir
     c                   eval      c1aavd = s_aavd
     c                   exsr      xc2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      afaplr
      *
     c                   if        %eof or
     c                             w_ruti <> afarut
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      ra07l1_gfir = afafir
     c                   eval      ra07l1_gkod = afaavd
     c     ra07l1_key    chain     ra07l1
     c                   if        not %found
     c                   eval      b1atxt = *blank
     c                   else
     c                   movel     ragtxt        b1atxt
     c                   endif
      *
     c                   eval      b1afir = afafir
     c                   eval      b1aavd = afaavd
     c                   eval      b1valg = 0
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     afaplr_key    setgt     afaplr
     c                   readp     afaplr
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     afaplr
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      afaplr_afir = afafir
      *
     c                   enddo
      *
     c                   if        %eof
     c     afaplr_key    setll     afaplr
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    w_ruti
     c                   parm                    w_edit
      *
      * Key for posisjonering på tilleggsregister
      *
     c     afapl1_key    klist
     c                   kfld                    w_ruti
     c                   kfld                    afapl1_afir
     c                   kfld                    afapl1_aavd
      *
      * Key for PDF register
      *
     c     afopl1_key    klist
     c                   kfld                    w_ruti
      *
      * Key for posisjonering
      *
     c     afaplr_key    klist
     c                   kfld                    w_ruti
     c                   kfld                    afaplr_afir
     c                   kfld                    afaplr_aavd
      *
      * Key for posisjonering
      *
     c     afaplu_key    klist
     c                   kfld                    w_ruti
     c                   kfld                    afaplu_afir
     c                   kfld                    afaplu_aavd
      *
      * Key for les - avvik
      *
     c     xfaplr_key    klist
     c                   kfld                    w_ruti
      *
      * Key for firma
      *
     c     afirl1_key    klist
     c                   kfld                    afirl1_ffir
      *
      * Key for avdeling
      *
     c     ra07l1_key    klist
     c                   kfld                    ra07l1_gfir
     c                   kfld                    ra07l1_gkod
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c*                  eval      *in22 = *on
     c                   eval      b1ruti = w_ruti
     c                   eval      c1ruti = w_ruti
     c                   eval      c2ruti = w_ruti
     c                   eval      d1ruti = w_ruti
     c                   eval      k1ruti = w_ruti
      *
     c                   if        w_edit = 0
     c                   eval      *in30 = *on
     c                   else
     c                   eval      *in30 = *off
     c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      afaplr_afir = 0
     c                   eval      afaplr_aavd = 0
     c     afaplr_key    setll     afaplr
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
