     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : RF220R
      * Beskrivelse . . : Kortregister, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       310505 KOB  Nytt program
      *       041105 GRo  Lagt til synkronisert oppdatering mot OFAK
6.10  * R6.10 230909 BSA  Lagt til sporingsfelter
6.11  *                   Legger ut flere korttyper på ordrelink register
6.11  *                   Sjekker kun mot registrerte kort på firmanivå.
6.11  *                   Legger ut info om sist avregnet.
6.12  * R6.10 140512 BSA  Lagt til GE kort
6.20  * R6.20 090513 Sst  Rettet logikk vedr finne neste sekvensnummer
6.21  * R6.20 180814 Sst  Lagt på sperretkode i c2-bildet
6.30  * R6.30 230615 bsa  Sjekker mot gyldige kort ved opprettelse
6.31  * R6.30 250416 nho  Protect ind ny ved korttype, må ha PR også
6.31  *                   ved vedlikehold (PR også på kundeprosjekt)
6.32  * R6.30 250416 nho  Parameter manglet inn i FL501R
6.33  * R6.30 090816 bsa  Endrer sjekk mot gyldige kort. Dagens sjekk fungerer ikke.
6.33  *                   (Ref 6.30)
6.34  * R6.30 250717 bsa  Legger defaultverdi i tildato for gyldighet. Setter fast
6.34  *                   1229, da dette er langt fram i tid.
7.xx  * 7.00  220120 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
8.00  * 8.00  250522 sst  Endret 'GE Kort' til santander samt sperret noen felter(*in41)
8.01  * K000  160123 bsa  For at kortet skal være tilgjengelig i kassa, må
8.01  * K000              kundetabellen også få endringsmerket. Kortet replikeres
8.01  * K000              ikke ut til kassa, før kunden er "oppdatert".
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frukrlr    if   e           k disk    rename(rukrpfr:rukrlrr)
     frukrl1    if   e           k disk    rename(rukrpfr:rukrl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
8.01 frkunlu    uf   e           k disk    rename(rkunpfr:rkunlur)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     frukrlu    uf a e           k disk    rename(rukrpfr:rukrlur)
6.30 fruksl1    if   e           k disk    rename(rukspfr:ruksl1r)
      *
     frf220d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(rukfir)
     d p_kund          s                   like(rukkun)
     d p_kpnr          s                   like(rukpro)
      *
     d p_ofir          s              3  0
     d p_okun          s              6  0
     d p_opro          s              6  0
     d p_okpn          s              6  0
     d p_otyp          s              2  0
     d p_otsq          s              4  0
     d p_oper          s              1
6.11 d p_skty          s              2  0
6.11 d p_slag          s              2  0
6.11 d p_sbes          s             30
      *
      * Definering av local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d rukrlr_kund     s                   like(rukkun)
     d rukrlr_kpro     s                   like(rukpro)
     d rukrlr_ktyp     s                   like(ruktyp)
     d rukrlr_ktsq     s                   like(ruktsq)
     d rukrl1_kund     s                   like(rukkun)
     d rukrl1_kpro     s                   like(rukpro)
     d rukrl1_ktyp     s                   like(ruktyp)
6.20 d rukrl1_ktsq     s                   like(ruktsq)
     d rkunl1_kund     s                   like(rkkund)
8.01 d rkunlu_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d rukrlu_kund     s                   like(rukkun)
     d rukrlu_kpro     s                   like(rukpro)
     d rukrlu_ktyp     s                   like(ruktyp)
     d rukrlu_ktsq     s                   like(ruktsq)
6.30 d ruksl1_skty     s                   like(ruskty)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
     d w_knav          s                   like(rknavn)
     d w_pnav          s                   like(flnavn)
     d w_ktyp          s              2  0
     d w_ktpn          s             25
6.32 d w_kjor          s              2  0
6.32 d w_kjnr          s              5  0
      *
     d w_seqe          s              1
     d b_valg_ok       s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_tlgx          s              1    inz(*off)
      *
     d w_firm          s                   like(rukfir)
     d w_kund          s                   like(rukkun)
     d w_kort          s             16
     d w_ddat          s               d   datfmt(*iso)
     d w_adr1          s             30
     d w_adr2          s             30
     d w_sted          s             30
     d w_kprs          s             30
     d w_tlfa          s             15
     d w_tlfp          s             15
     d w_tlfm          s             15
     d w_mail          s             50
     d w_orka          s              3  0
     d w_okun          s              6  0
     d w_okpr          s              6  0
     d w_opda          s               d
     d w_proj          s              6  0
     d w_posi          s              2  0
     d w_modu          s             10
     d w_disp          s              1
     d w_stat          s              1
      *
      * Definering av konstanter
      *
     d c_modu          c                   'KORT      '
7.xx d c_sfil          s              2  0 inz(14)
     d c_digi          c                   '0123456789 '
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * F1WIN  - F1 Vindu for spørring på valgalternativer (Hardkodet)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   if        b_tlgx = *off
     c                   goto      avslutt
     c                   endif
      *
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      bc2bld
     c                   eval      c2kkun = w_kund
     c                   exsr      xc2bld
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   if        b_valg_ok = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      rukrl1_kund = b1kkun
     c     rukrl1_key    setll     rukrl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      b_forn = *off
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2 or
     c                             b1valg = 5
     c                   eval      c2kkun = b1kkun
     c                   eval      c2kpro = b1kpro
     c                   eval      c2ktyp = b1ktyp
     c                   eval      c2ktsq = b1ktsq
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1kkun = b1kkun
     c                   eval      d1kpro = b1kpro
     c                   eval      d1ktyp = b1ktyp
     c                   eval      d1ktsq = b1ktsq
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
      * Fyller opp subfile
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      rukrl1
      *
     c                   if        %eof
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        rukfir <> w_firm or
     c                             rukkun <> w_kund
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
      * Skriver til subfile
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      rukrl1_kund = rukkun
      *
     c                   eval      b1valg = 0
     c                   eval      b1kkun = rukkun
     c                   eval      b1kpro = rukpro
     c                   eval      b1ktyp = ruktyp
     c                   eval      b1ktsq = ruktsq
      *
     c                   exsr      avt_tp_sh
      *
     c                   movel     rukhld        b1khld
     c                   move      rukobl        b1kobl
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle skjermbilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      rkunl1_kund = c2kkun
     c     rkunl1_key    chain     rkunl1
     c                   if        %found(rkunl1)
     c                   eval      c2navn = rknavn
     c                   else
     c                   eval      c2navn = *blank
     c                   endif
      *
     c                   if        c2kpro <> *zero
     c                   eval      fkprl1_kund = c2kkun
     c                   eval      fkprl1_kpro = c2kpro
     c     fkprl1_key    chain     fkprl1
     c                   if        %found(fkprl1)
     c                   eval      c2kpnv = flnavn
     c                   else
     c                   eval      c2kpnv = *blank
     c                   endif
     c                   endif
      *
8.00  *   Santander skal ikke vise utløpsdato og strekkode
8.00 c                   if        c2ktyp = 4
8.00 c                   eval      *in41 = *on
8.00 c                   else
8.00 c                   eval      *in41 = *off
8.00 c                   endif
8.00  *
     c                   eval      rukrlr_kund = c2kkun
     c                   eval      rukrlr_kpro = c2kpro
     c                   eval      rukrlr_ktyp = c2ktyp
     c                   eval      rukrlr_ktsq = c2ktsq
     c     rukrlr_key    chain     rukrlr
      *
     c                   if        %found(rukrlr)
     c                   eval      c2kknr = rukknr
     c                   eval      c2kstr = rukstr
     c                   eval      c2kexp = rukexp
     c                   eval      c2kobl = rukobl
6.21 c                   eval      c2kspr = rukspr
      *
     c                   exsr      avt_tp_fu
      *
     c                   eval      c2khld = rukhld
     c                   eval      c2eusr = rueusr
      *
     c                   if        ruedat <> *loval
     c     *dmy          move      ruedat        c2edat
     c                   endif
      *
6.11 c                   if        rukdat <> *loval
6.11 c     *dmy          move      rukdat        c2kdat
6.11 c     *hms          move      ruktim        c2ktim
6.11 c                   endif
      *
     c                   else
      *
     c                   eval      c2kknr = *zero
     c                   eval      c2kexp = *zero
     c                   eval      c2kobl = *zero
     c                   eval      c2ktpn = *blank
     c                   eval      c2khld = *blank
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = *zero
6.11 c                   eval      c2kstr = *zero
6.11 c                   eval      c2kdat = *zero
6.11 c                   eval      c2ktim = *zero
      *
     c                   endif
      *
      * Dersom 'vise' settes protect-indikatorer
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
6.31 c                   eval      *in36 = *on
8.00 c                   eval      *in41 = *on
     c                   endif
      *
6.31  * Dersom 'vedl' settes protect-indikatorer
6.31  *
6.31 c                   if        b1valg = 2
6.31 c                   eval      *in36 = *on
6.31 c                   endif
      * Behandler bildet
      *
     c     c2tagb        tag
      *
     c*                  write     c2cmd
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
6.31 c                   if        *in36 = '0'
     c                   if        *inkd
     c                   if        w_afld = 'C2KPRO'
     c                   eval      c2kpnv = *blank
     c                   call      'FL501R'
     c                   parm                    w_firm
     c                   parm                    w_kund
     c                   parm                    w_ddat
     c                   parm                    c2kpro
     c                   parm                    w_pnav
     c                   parm                    w_adr1
     c                   parm                    w_adr2
     c                   parm                    w_sted
     c                   parm                    w_kprs
     c                   parm                    w_tlfa
     c                   parm                    w_tlfp
     c                   parm                    w_tlfm
     c                   parm                    w_mail
     c                   parm                    w_orka
     c                   parm                    w_okun
     c                   parm                    w_okpr
     c                   parm                    w_opda
     c                   parm                    w_proj
6.32 c                   parm                    w_kjor
6.32 c                   parm                    w_kjnr
     c                   eval      c2kpnv = w_pnav
     c                   goto      c2tagb
     c                   endif
6.31 c                   endif
      *
6.31 c                   if        *in36 = '0'
     c                   if        w_afld = 'C2KTYP'
     c                   eval      c2ktyp = *zeros
6.11 c*                  eval      f1head = 'kort/avt.type'
6.11 c*                  eval      f1alth = 'Kort/avt.type'
6.11 c                   if        1 = 2
6.11 c                   exsr      xf1win1
6.11 c                   endif
6.11 c*                  eval      c2ktyp = w_ktyp
6.11 c*                  eval      c2ktpn = w_ktpn
6.11 c                   eval      p_skty = 0
6.11 c                   eval      p_slag = 0
6.11 c                   eval      p_sbes = *blank
6.11  *
6.11 c                   call      'RA532R'
6.11 c                   parm                    w_firm
6.11 c                   parm                    p_skty
6.11 c                   parm                    p_slag
6.11 c                   parm                    p_sbes
6.11 c                   eval      c2ktyp = p_skty
      *
     c                   goto      c2tagb
     c                   endif
6.31 c                   endif
      *
     c                   endif
      *
      * Validering
      *
     c                   if        c2ktyp = 0
     c                   eval      *in31  = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        c2kknr = *zero
     c                   eval      *in33  = *on
     c                   goto      c2tagb
     c                   endif
      *
6.34 c                   if        c2kexp = *zero
6.34 c                   eval      c2kexp = 1229
6.34 c                   endif
6.34  *
     c                   if        c2kexp = *zero
     c                   eval      *in34  = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        c2khld = *blank
     c                   eval      *in35  = *on
     c                   goto      c2tagb
     c                   endif
6.30  *
6.30  * Sjekk at kortet er opprettet som gyldig
6.30  *
6.30 c                   eval      *in31 = *on
6.30 c                   eval      ruksl1_skty = c2ktyp
6.30 c     ruksl1_key    setll     ruksl1
6.30 c     ruksl1_key    reade     ruksl1
6.30 c                   dow       not %eof
6.30 c                   eval      *in31 = *off
6.30 c                   leave
6.30 c                   enddo
6.33  *
6.33 c                   if        *in31 = *on
6.33 c                   goto      c2tagb
6.33 c                   endif
6.30  *
     c                   if        c2ktsq = *zero
     c                   exsr      finn_ktsq
     c                   endif
      *
      * Oppdaterer tabell
      *
     c                   clear                   rukrlur
      *
     c                   eval      rukrlu_kund = c2kkun
     c                   eval      rukrlu_kpro = c2kpro
     c                   eval      rukrlu_ktyp = c2ktyp
     c                   eval      rukrlu_ktsq = c2ktsq
     c     rukrlu_key    chain     rukrlur
      *
     c                   eval      rukknr = c2kknr
     c                   eval      rukstr = c2kstr
     c                   eval      rukexp = c2kexp
     c                   eval      rukobl = c2kobl
     c                   eval      rukhld = c2khld
6.10 c                   eval      rueusr = l_user
6.10 c                   time                    ruedat
6.10 c                   time                    ruetim
8.00  *
8.00 c                   if        c2ktyp = 4
8.00 c                   eval      rukexp = *zero
8.00 c                   eval      rukstr = *zero
8.00 c                   endif
      *
     c                   if        %found(rukrlu)
     c                   update    rukrlur
     c                   eval      p_oper = 'U'
     c                   else
     c                   eval      rukfir = w_firm
     c                   eval      rukkun = c2kkun
     c                   eval      rukpro = c2kpro
     c                   eval      ruktyp = c2ktyp
     c                   eval      ruktsq = c2ktsq
6.10 c                   eval      rukous = l_user
6.10 c                   time                    rukoti
6.10 c                   time                    rukoda
     c                   write     rukrlur
      *
8.01 c                   eval      rkunlu_kund = c2kkun
8.01 c     rkunlu_key    chain     rkunlu
8.01 c                   if        %found
8.01 c                   eval      rkesig = l_user
8.01 c                   time                    rkedat
8.01 c                   time                    rketim
8.01 c                   time                    rk2dpt
8.01 c                   eval      rkepgm = 'RF220R'
8.01 c                   update    rkunlur
8.01 c                   endif
      *
     c                   eval      b1kkun = c2kkun
     c                   exsr      forny
     c                   eval      p_oper = 'W'
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Oppdaterer i OFAK om aktuelle korttype
      *
     c                   if        ruktyp = 1 or
6.11 c                             ruktyp = 2 or
6.11 c                             ruktyp = 3 or
6.11 c                             ruktyp = 4 or
6.11 c                             ruktyp = 5 or
6.11 c                             ruktyp = 6
     c                   eval      p_ofir = w_firm
     c                   eval      p_okun = c2kkun
     c                   eval      p_opro = c2kpro
     c                   eval      p_okpn = *zero
     c                   eval      p_otyp = c2ktyp
     c                   eval      p_otsq = c2ktsq
      *
     c                   call      'RK112C'
     c                   parm                    p_ofir
     c                   parm                    p_okun
     c                   parm                    p_opro
     c                   parm                    p_okpn
     c                   parm                    p_otyp
     c                   parm                    p_otsq
     c                   parm                    p_oper
      *
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30 = *off
6.31 c                   eval      *in36 = *off
8.00 c                   eval      *in41 = *off
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å init. av skjermbilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bc2bld        begsr
      *
     c                   eval      c2kpro = *zero
     c                   eval      c2kpnv = *blank
     c                   eval      c2ktyp = *zero
     c                   eval      c2ktsq = *zero
     c                   eval      c2ktpn = *blank
     c                   eval      c2kknr = *zero
     c                   eval      c2kexp = *zero
     c                   eval      c2kobl = *zero
     c                   eval      c2khld = *blank
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = *zero
      *
     c                   endsr
      *
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     rukrl1_key    setgt     rukrl1
     c                   readp     rukrl1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     rukrl1
      *
     c                   if        %eof or
     c                             rukfir <> w_firm or
     c                             rukkun <> w_kund
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      rukrl1_kund = rukkun
      *
     c                   enddo
      *
     c                   if        %eof
     c     rukrl1_key    setll     rukrl1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_bck       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av hardkodet søk  (F1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xf1win1       begsr
      *
     c                   exsr      xf1nul
     c                   eval      f1alt1 = 'VISA Purchasing          '
     c                   eval      f1alt2 = 'Europay Purchasing       '
     c                   eval      f1alt3 = 'Maxbo Kort (DNB)         '
6.12 c                   eval      f1alt4 = 'Santander                '
     c                   eval      f1alt5 = 'BM Kort (DNB)            '
     c                   eval      f1alt6 = 'NBBL Fordelskort         '
     c                   eval      f1alt7 = '                         '
     c                   eval      f1alt8 = '                         '
      *
     c                   exfmt     f1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_f1win1
     c                   endif
      *
     c                   select
     c                   when      f1val1 = '1'
     c                   eval      w_ktyp = 01
     c                   eval      w_ktpn = f1alt1
     c                   when      f1val2 = '1'
     c                   eval      w_ktyp = 02
     c                   eval      w_ktpn = f1alt2
     c                   when      f1val3 = '1'
     c                   eval      w_ktyp = 03
     c                   eval      w_ktpn = f1alt3
     c                   when      f1val4 = '1'
     c                   eval      w_ktyp = 04
     c                   eval      w_ktpn = f1alt4
     c                   when      f1val5 = '1'
     c                   eval      w_ktyp = 05
     c                   eval      w_ktpn = f1alt5
     c                   when      f1val6 = '1'
     c                   eval      w_ktyp = 06
     c                   eval      w_ktpn = f1alt6
     c                   when      f1val7 = '1'
     c                   eval      w_ktyp = 07
     c                   eval      w_ktpn = f1alt7
     c                   when      f1val8 = '1'
     c                   eval      w_ktyp = 08
     c                   eval      w_ktpn = f1alt8
     c                   endsl
      *
     c     end_f1win1    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å blanke ut hardkodet spørrebilde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xf1nul        begsr
      *
     c                   eval      f1alt1 = *blank
     c                   eval      f1alt2 = *blank
     c                   eval      f1alt3 = *blank
     c                   eval      f1alt4 = *blank
     c                   eval      f1alt5 = *blank
     c                   eval      f1alt6 = *blank
     c                   eval      f1alt7 = *blank
     c                   eval      f1alt8 = *blank
     c                   eval      f1val1 = *blank
     c                   eval      f1val2 = *blank
     c                   eval      f1val3 = *blank
     c                   eval      f1val4 = *blank
     c                   eval      f1val5 = *blank
     c                   eval      f1val6 = *blank
     c                   eval      f1val7 = *blank
     c                   eval      f1val8 = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sette kort/avtaletype i klartekst (kort)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avt_tp_sh     begsr
      *
     c                   select
     c                   when      ruktyp = 1
     c                   eval      b1type = 'VISA Purchasing'
     c                   when      ruktyp = 2
     c                   eval      b1type = 'Europay Purchasing'
     c                   when      ruktyp = 3
     c                   eval      b1type = 'Maxbo Kort (DNB)'
     c                   when      ruktyp = 4
6.12 c                   eval      b1type = 'Santander   '
     c                   when      ruktyp = 5
     c                   eval      b1type = 'BM Kort (DNB)'
     c                   when      ruktyp = 6
     c                   eval      b1type = 'NBBL Fordelskort'
     c                   other
     c                   eval      b1type = 'Udefinert....'
     c                   endsl
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sette kort/avtaletype i klartekst (full)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avt_tp_fu     begsr
      *
     c                   select
     c                   when      ruktyp = 1
     c                   eval      c2ktpn = 'VISA Purchasing'
     c                   when      ruktyp = 2
     c                   eval      c2ktpn = 'Europay Purchasing'
     c                   when      ruktyp = 3
     c                   eval      c2ktpn = 'Maxbo Kort (DNB)'
     c                   when      ruktyp = 4
6.12 c                   eval      c2ktpn = 'Santander   '
     c                   when      ruktyp = 5
     c                   eval      c2ktpn = 'BM Kort (DNB)'
     c                   when      ruktyp = 6
     c                   eval      c2ktpn = 'NBBL Fordelskort'
     c                   other
     c                   eval      b1type = 'Udefinert....'
     c                   endsl
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne høyeste sekv.nr. på kunde/prosjekt/korttype
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_ktsq     begsr
      *
     c                   eval      rukrl1_kund = c2kkun
     c                   eval      rukrl1_kpro = c2kpro
     c                   eval      rukrl1_ktyp = c2ktyp
6.20 c                   eval      rukrl1_ktsq = *zero
      *
     c     rukrl1_key2   setll     rukrl1
6.20 c                   read      rukrl1r
      *
6.20 c                   dow       not %eof         and
6.20 c                             rukkun = c2kkun
6.20 c                   eval      c2ktsq = ruktsq + 1
6.20 c                   read      rukrl1r
     c                   enddo
      *
     c                   eval      c2ktsq = ruktsq + 1
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      rukrlr_kund = d1kkun
     c                   eval      rukrlr_kpro = d1kpro
     c                   eval      rukrlr_ktyp = d1ktyp
     c                   eval      rukrlr_ktsq = d1ktsq
     c     rukrlr_key    chain     rukrlr
      *
     c                   eval      rkunl1_kund = d1kkun
     c     rkunl1_key    chain     rkunl1
     c                   if        %found(rkunl1)
     c                   eval      d1navn = rknavn
     c                   else
     c                   eval      d1navn = *blank
     c                   endif
      *
     c                   if        d1kpro <> *zero
     c                   eval      fkprl1_kund = d1kkun
     c                   eval      fkprl1_kpro = d1kpro
     c     fkprl1_key    chain     fkprl1
     c                   if        %found(fkprl1)
     c                   eval      d1kpnv = flnavn
     c                   else
     c                   eval      d1kpnv = *blank
     c                   endif
     c                   endif
      *
     c                   eval      d1ktyp = b1ktyp
     c                   eval      d1khld = rukhld
     c                   eval      d1kknr = rukknr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
      * Sletter rad
      *
     c                   eval      rukrlu_kund = d1kkun
     c                   eval      rukrlu_kpro = d1kpro
     c                   eval      rukrlu_ktyp = d1ktyp
     c                   eval      rukrlu_ktsq = d1ktsq
     c     rukrlu_key    chain     rukrlur
      *
     c                   delete    rukrlur
      *
      * Sletter i OFAK om aktuelle korttype
      *
     c                   if        ruktyp = 1 or
6.11 c                             ruktyp = 2 or
6.11 c                             ruktyp = 3 or
6.11 c                             ruktyp = 4 or
6.11 c                             ruktyp = 5 or
6.11 c                             ruktyp = 6
     c                   eval      p_ofir = rukfir
     c                   eval      p_okun = rukkun
     c                   eval      p_opro = rukpro
     c                   eval      p_okpn = rukkpn
     c                   eval      p_otyp = ruktyp
     c                   eval      p_otsq = ruktsq
     c                   eval      p_oper = 'D'
      *
     c                   call      'RK112C'
     c                   parm                    p_ofir
     c                   parm                    p_okun
     c                   parm                    p_opro
     c                   parm                    p_okpn
     c                   parm                    p_otyp
     c                   parm                    p_otsq
     c                   parm                    p_oper
      *
     c                   endif
      *
     c                   eval      b_forn = *on
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kund
     c                   parm                    p_kpnr
      *
      * Key for oppslag på kort/fakt.avtale-register
      *
     c     rukrlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rukrlr_kund
     c                   kfld                    rukrlr_kpro
     c                   kfld                    rukrlr_ktyp
     c                   kfld                    rukrlr_ktsq
      *
      * Key for posisjonering på kunde
      *
     c     rukrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rukrl1_kund
      *
      * Key for les siste nr på kunde/prosjekt/korttype
      *
     c     rukrl1_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    rukrl1_kund
     c                   kfld                    rukrl1_kpro
     c                   kfld                    rukrl1_ktyp
6.20 c                   kfld                    rukrl1_ktsq
      *
      * Key for oppdat. av kort/fakt.avtale-register
      *
     c     rukrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rukrlu_kund
     c                   kfld                    rukrlu_kpro
     c                   kfld                    rukrlu_ktyp
     c                   kfld                    rukrlu_ktsq
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
8.01  *
8.01  * Key for oppslag på kunde
8.01  *
8.01 c     rkunlu_key    klist
8.01 c                   kfld                    w_firm
8.01 c                   kfld                    rkunlu_kund
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
6.30  *
6.30  * Key for sjekk av kort
6.30  *
6.30 c     ruksl1_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    ruksl1_skty
      *
      *
      * Henter parameter, og initierer skjermbildet
      *
     c                   time                    w_ddat
     c                   eval      w_firm = p_firm
     c                   eval      w_kund = p_kund
      *
     c                   eval      *in22 = *on
      *
      * Sjekker om tilleggsmodul er installert
      *
     c                   eval      w_modu = c_modu
     c                   eval      w_disp = '1'
     c                   eval      w_stat = '0'
     c                   eval      b_tlgx = *on
      *
     c                   call      'AX700R'
     c                   parm                    w_modu
     c                   parm                    w_disp
     c                   parm                    w_stat
      *
     c                   if        w_stat = '1'
     c                   eval      b_tlgx = *off
     c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rukrl1_kund = w_kund
     c     rukrl1_key    setll     rukrl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
