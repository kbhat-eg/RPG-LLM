     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO766R
      * Beskrivelse . . : Oppdatering av ordre-linjer fra ordre-hode
      *                   pr. vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       030114 bof  Tatt utgangspunk i FO764R
      *
8.01  * 8.xx  071022 bsa  Logger at jobben er kjørt og antall linjer som er
8.01  * 8.xx              oppdatert.
8.02  * 8.xx  071022 bsa  Oppdaterer ikke hvis det ikke er endringer
8.03  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohel1    uf   e           k disk    rename(fohepfr:fohel1r)
     ffodtlu    uf   e           k disk    rename(fodtpfr:fodtlur)
     ffodtlv    if   e           k disk    rename(fodtpfr:fodtlvr)
      *
      *
      * Definering av parametre
      *
     d p_firm          s                   like(fdfirm)
     d p_vare          s                   like(fdvare)
      *
      * Definering av variable
      *
     d w_firm          s                   like(fdfirm)
8.01 d w_anta          s              8  0
8.01 d w_kode          s              1
8.01 d w_fell          s              6
8.01 d w_type          s              2
8.01 d w_fast          s             10
8.01 d w_numm          s              8  0
8.01 d w_jnav          s             15
8.01 d w_jkey          s             41
8.01 d w_jstat         s              2  0
8.01 d w_jtext         s            200
8.01 d w_jtxt          s             35
      *
8.02 d b_oppd          s              1    inz(*off)
      *
      * Definering av variable i nøkler
      *
     d fodtlv_vare     s                   like(fdvare)
     d fodtlu_numm     s                   like(fdnumm)
     d fodtlu_suff     s                   like(fdsuff)
     d fodtlu_line     s                   like(fdline)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
      *
     D                UDS
      *
6.10 d l_user                911    920
     D  DSSFIR               944    946  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Oppdaterer ORDRE-LINJE-REGISTER med nye opplysninger           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c                   eval      fodtlv_vare = p_vare
     c     fodtlv_key    setll     fodtlv
     c     fodtlv_key    reade     fodtlv
      *
     c                   dow       not %eof(fodtlv)
      *
      *  Hent opplysninger fra ORDRE-HODE-REGISTER
      *
     c                   eval      fohel1_numm = fdnumm
     c                   eval      fohel1_suff = fdsuff
     c     fohel1_key    chain     fohel1
     c                   if        %found(fohel1)
      *
      *  Oppdaterer ORDRE-LINJE-REGISTER med nye opplysninger
      *
     c                   eval      fodtlu_numm = fdnumm
     c                   eval      fodtlu_suff = fdsuff
     c                   eval      fodtlu_line = fdline
     C     fodtlu_key    chain     fodtlu
     c                   if        %found(fodtlu)
      *
8.02 c                   eval      b_oppd = *off
      *
8.02 c                   if        fdavde <> foavde
     c                   eval      fdavde = foavde
8.02 c                   eval      b_oppd = *on
8.02 c                   endif
      *
8.02 c**                 eval      fdkund = fdkund
     c                   if        folkun <> 0
     c                   eval      fdkund = folkun
8.02 c                   eval      b_oppd = *on
     c                   endif
      *
8.02 c                   if        b_oppd = *on
6.10 c                   time                    fdedat
6.10 c                   time                    fdetim
6.10 c                   eval      fdeusr = l_user
     C                   update    fodtlur
8.01 c                   eval      w_anta = w_anta + 1
8.02 c                   else
8.02 c                   unlock    fodtlu
8.02 c                   endif
      *
     C                   endif
      *
     C                   endif
      *
     c     fodtlv_key    reade     fodtlv
      *
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSLUTT        TAG
      *
8.01  * Kvittering
8.01 c                   eval      w_jstat = 10
8.01 c                   eval      w_jtext = 'Oppdatert ' + %char(w_anta) +
8.01 c                                       'ordrelinjer'
8.01  *
8.01 c                   call      'AB705R'
8.01 c                   parm                    w_jkey
8.01 c                   parm                    w_jstat
8.01 c                   parm                    w_jtext
8.01  * Avslutt
8.01 c                   call      'AB710R'
8.01 c                   parm                    w_jkey
8.01  *
     C                   SETON                                        LR
     C                   RETURN
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     CSR   *INZSR        BEGSR
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
      *
      *  Key for file : ORDRE-detaljer
      *
     C     fodtlv_key    KLIST
     C                   KFLD                    w_firm
     C                   KFLD                    fodtlv_vare
      *
      *  Key for file : ORDRE-detaljer
      *
     C     fodtlu_key    KLIST
     C                   KFLD                    w_firm
     C                   KFLD                    fodtlu_numm
     C                   KFLD                    fodtlu_suff
     C                   KFLD                    fodtlu_line
      *
      *  Key for file : ORDRE-hode
      *
     C     fohel1_key    KLIST
     C                   KFLD                    w_firm
     C                   KFLD                    fohel1_numm
     C                   KFLD                    fohel1_suff
      *
      *  Legger inn firmanummer i nøkklene
      *
     c                   eval      w_firm = p_firm
8.01 c                   eval      w_anta = 0
      *
8.01 c                   eval      w_kode = '0'
8.01 c                   eval      w_firm = dssfir
8.01 c                   eval      w_fell = 'NXTKLI'
8.01 c                   eval      w_type = *blank
8.01 c                   eval      w_fast = *blank
8.01 c                   eval      w_numm = *zero
      *
8.01 c                   call      'AS100R'
8.01 c                   parm                    w_kode
8.01 c                   parm                    w_firm
8.01 c                   parm                    w_fell
8.01 c                   parm                    w_type
8.01 c                   parm                    w_fast
8.01 c                   parm                    w_numm
8.01  *
8.01  * Vi logger at jobben har startet
8.01 c                   eval      w_jnav = 'LOGG' + %char(w_numm)
8.01 c                   eval      w_jtxt = 'FO766 oppdatering av ordrelinjer !'
8.01  *
8.01 c                   call      'AB700R'
8.01 c                   parm                    w_jnav
8.01 c                   parm                    w_jkey
8.01 c                   parm                    w_jtxt
8.01  * Trigg start
8.01 c                   eval      w_jstat = 10
8.01 c                   eval      w_jtext = 'Masseoppdatering av ordrelinjer !'
8.01  *
8.01 c                   call      'AB705R'
8.01 c                   parm                    w_jkey
8.01 c                   parm                    w_jstat
8.01 c                   parm                    w_jtext
      *
     CSR                 ENDSR
