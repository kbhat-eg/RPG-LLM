     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : ND105R
      * Beskrivelse . . : Ekstern ordre-linje, hent feilstatus
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       040418 bsa  Nytt program
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     feddtl3    if   e           k disk    rename(eddtpfr:eddtl3r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vvfirm)
     d p_stat          s              1  0
     d p_fsys          s                   like(edfsys)
     d p_tell          s                   like(edtell)
     d p_line          s                   like(edLine)
     d p_lage          s                   like(vllage)
     d p_vare          s                   like(vvvare)
     d p_tek1          s                   like(vvtek1)
     d p_anta          s             11  3
     d p_enhe          s                   like(veenhe)
     d p_lety          s                   like(vylety)
     d p_ldat          s               d   datfmt(*iso)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vvenl1_vare     s                   like(vevare)
     d vvenl1_enhe     s                   like(veenhe)
     d vltpl1_lety     s                   like(vylety)
     d vlagl1_vare     s                   like(vlvare)
     d vlagl1_lage     s                   like(vllage)
     d eddtl3_fsys     s                   like(edfsys)
     d eddtl3_tell     s                   like(edtell)
     d eddtl3_line     s                   like(edline)
      *
      * Definering av variable
      *
     d w_stat          s              1
     d w_ltxt          s             20
     d w_ldat          s               d   datfmt(*iso)
     d w_udat          s               d   datfmt(*iso)
      *
     d w_firm          s                   like(vyfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      p_stat = 0
      *
     c                   eval      eddtl3_fsys = p_fsys
     c                   eval      eddtl3_tell = p_tell
     c                   eval      eddtl3_line = p_line
     c     eddtl3_key    chain     eddtl3
     c                   if        %found
     c                   goto      avslutt
     c                   endif
      *
      * Melding/tekstlinje
      *
     c                   if        p_vare = *blank and
     c                             p_tek1 <> *blank
     c                   goto      avslutt
     c                   endif
      *
      *
      * Varenummer
      *
     c                   if        p_vare = *blank
     c                   eval      p_stat = 1
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   eval      p_stat = 1
     c                   goto      avslutt
     c                   endif
      *
      * Antall
      *
     c                   if        p_anta <= 0
     c                   eval      p_stat = 2
     c                   goto      avslutt
     c                   endif
      *
      * Enhet
      *
     c                   eval      vvenl1_vare = p_vare
     c                   eval      vvenl1_enhe = p_enhe
     c     vvenl1_key    chain     vvenl1
     c                   if        not %found or
     c                             vesaen = 0
     c                   eval      p_stat = 3
     c                   goto      avslutt
     c                   endif
      *
      * Leveringstype
      *
     c                   if        p_lety <> 0
     c                   eval      vltpl1_lety = p_lety
     c     vltpl1_key    chain     vltpl1
     c                   if        not %found
     c                   eval      p_stat = 5
     c                   goto      avslutt
     c                   endif
     c                   endif
      *
      * Leveringsdato
      *
     c                   if        p_ldat <> *loval
     c                   time                    w_udat
     c                   if        p_ldat < w_udat
     c                   eval      p_stat = 6
     c                   goto      avslutt
     c                   endif
     c                   endif
      *
      * Lagervare
      *
     c                   if        faalag = 1                                   Lagerstyring
     c                   eval      vlagl1_vare = p_vare
     c                   eval      vlagl1_lage = p_lage
     c     vlagl1_key    chain     vlagl1
     c                   if        not %found or
     c                             vlvtyp <> 'L'
     c                   eval      p_stat = 8
     c                   goto      avslutt
     c                   endif
     c                   else                                                   Ikke lagerstyring
     c                   if        vvvtyp <> 'L'
     c                   eval      p_stat = 8
     c                   goto      avslutt
     c                   endif
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_stat
     c                   parm                    p_fsys
     c                   parm                    p_tell
     c                   parm                    p_line
     c                   parm                    p_lage
     c                   parm                    p_vare
     c                   parm                    p_tek1
     c                   parm                    p_anta
     c                   parm                    p_enhe
     c                   parm                    p_lety
     c                   parm                    p_ldat
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
      *
      * Key for oppslag på leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
      *
      * Key for oppslag på vare-lager
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
      *
      * Key for oppslag på eksterne ordrelinjer
      *
     c     eddtl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    eddtl3_fsys
     c                   kfld                    eddtl3_tell
     c                   kfld                    eddtl3_line
      *
      * Key for oppslag på ofak koderegister
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
      * Henter kode-informasjon
      *
     c     fstsl1_key    chain     fstsl1
      *
     c                   endsr
