     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JX420R
      * Beskrivelse . . : Vare-pris, slett priser for leverandør - parameter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       030605 HKO  Nytt program
6.20  *       010615 bof  Hvis prisgiver ikke finnes lenger, gi melding og
6.20  *                   tillatt at man kommer videre.
7.xx  * 7.00  020816 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
      *
     fjx420d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d jlevl1_ldor     s                   like(jlldor)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s              3  0
6 20  *
6.20 d w_m1            s             50    inz('Prisgiver finnes ikke, skal   -
6.20 d                                     priser likevel      ')
6.20 d w_m2            s             50    inz('slettes ?                     -
6.20 d                                                         ')
6.20 d w_sv            s              1
6.20 d w_in12          s              1  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send bilde
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc = *on or *inkl = *on
     c                   goto      avslutt
     c                   when      *inkd = *on
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
6.20  *
6.20  * kan overstyre om prisgiver er slettet
6.20  *
6.20 c                   if        *in32 = *on
6.20 c                   eval      w_sv = *blank
6.20 c                   call      'AA007R'
6.20 c                   parm                    w_m1
6.20 c                   parm                    w_m2
6.20 c                   parm                    w_sv
6.20 c                   parm                    w_in12
6.20 c                   if        w_sv =  'J'
6.20 c                   goto      rydd
6.20 c                   endif
6.20 c                   endif
      *
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
      * Kaller sletteprogram
      *
6.20 c     rydd          tag
     c                   call      'JX421R'
     c                   parm                    w_firm
     c                   parm                    b1pgiv
     c                   parm                    b1ldor
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   move      *on           *inlr
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Prisgivende leverandør
      *
     c                   if        w_afld = 'B1PGIV'
     c                   call      'JL500R'
     c                   parm                    b1pgiv
     c                   parm                    b1pnav
     c                   leavesr
     c                   endif
      *
      * Leverandør
      *
     c                   if        w_afld = 'B1LDOR'
     c                   call      'JL500R'
     c                   parm                    b1ldor
     c                   parm                    b1navn
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
     c                   eval      b1pnav = *blank
     c                   eval      b1navn = *blank
      *
      * Prisgivende leverandør
      *
     c                   if        b1pgiv = 0
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      jlevl1_ldor = b1pgiv
     c     jlevl1_key    chain     jlevl1
     c                   if        not %found
     c                   eval      *in32  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   eval      b1pnav = jlnavn
      *
      * Leverandør
      *
     c                   if        b1ldor <> 0
     c                   eval      jlevl1_ldor = b1ldor
     c     jlevl1_key    chain     jlevl1
     c                   if        not %found
     c                   eval      *in33  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      b1navn = jlnavn
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
