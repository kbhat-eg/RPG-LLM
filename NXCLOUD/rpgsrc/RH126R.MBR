      /TITLE  Overføring av saldo/budsjett
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RH126R                                              *
      * Beskrivelse . . : Overføring av saldo/budsjett                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
T5.30 * V5.30     101204  Hentet ikke faktor fra klasse/gruppe.          HFL
T5.30 * V5.40     040106  Endret beregning for å unngå desimaler         KOB
      * V5.40     040106  Korriger rutine for henting av faktor          KOB  *
6.10  * V6.10     210909  Lagt inn sporingsfelter                        BSA  *
6.31  *           040719  utvidet tabell i hht. utvidelse i RHSAPF       bof
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frhsal1    if   e           k disk    rename(rhsapfr:rhsal1r)
     frhsalu    uf a e           k disk    rename(rhsapfr:rhsalur)
     fraa3l1    if   e           k disk    rename(raa3pfr:raa3l1r)
     fra19lr    if   e           k disk    rename(ra19pfr:ra19lrr)
     fra20lr    if   e           k disk    rename(ra20pfr:ra20lrr)
      *
      *+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      *               T   A   B   E   L   L   E   R                      +
      *+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      *
6.31 d sal             s             15  2 dim(14)                              Saldo
     d gsa             s             11  2 dim(14)                              Saldo forrige record
     d nsa             s             11  2 dim(14)                              Saldo forrige record
      *
      *- - - - - - - - -*
      *  DATA STRUCTURE *
      *- - - - - - - - -*
      *
      *  Definisjon av bruddbegrep
      *
     d                 ds
     d nybrd                         16  0                                      Bruddb
     d  nyavd                         4  0 overlay(nybrd)                       Avdeli
     d  nykto                         5  0 overlay(nybrd:5)                     Konto
     d  nysps                         4  0 overlay(nybrd:10)                    Spesif
     d  nygrp                         2  0 overlay(nybrd:14)                    Gruppe
     d  nykla                         1  0 overlay(nybrd:16)                    Klasse
      *
     d                 ds
     d gmbrd                         16  0                                      Bruddb
     d  gmavd                         4  0 overlay(gmbrd)                       Avdeli
     d  gmkto                         5  0 overlay(gmbrd:5)                     Konto
     d  gmsps                         4  0 overlay(gmbrd:10)                    Spesif
     d  gmgrp                         2  0 overlay(gmbrd:14)                    Gruppe
     d  gmkla                         1  0 overlay(gmbrd:16)                    Klasse
      *
     d                 ds
     d w_kont                         5  0                                      Bruddb
     d  w_grp                         2  0 overlay(w_kont:2)                    Avdeli
     d  w_kla                         1  0 overlay(w_kont:2)                    Konto
      *
     d                uds
     d rhpfÅr                300    303  0
     d rhptÅr                         4  0
     d rhpavf                         4  0
     d rhpavt                         4  0
     d rhpspf                         4  0
     d rhpspt                         4  0
     d rhpktf                         5  0
     d rhpktt                         5  0
     d rhptyp                         1
     d rhpbfa                         1
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d wbfa            s                   like(rasbfa)
     d w_firm          s                   like(rifirm)
     d w_raar          s                   like(riraar)
     d w_type          s                   like(ritype)
     d w_nsa           s             11  0
      *
     d rhsalu_kont     s                   like(rikont)
     d rhsalu_spes     s                   like(rispes)
     d rhsalu_avde     s                   like(riavde)
     d rhsalu_raar     s                   like(riraar)
     d rhsalu_type     s                   like(ritype)
     d ra19lr_skod     s                   like(raskod)
     d ra20lr_tkod     s                   like(ratkod)
      *
     d p_skod          s              2
     d p_tkod          s              1
      *
      * Saldo-register - kuntorekkefølge
      *
     irhsal1r
     i              risa01                      sal(01)
     i              risa02                      sal(02)
     i              risa03                      sal(03)
     i              risa04                      sal(04)
     i              risa05                      sal(05)
     i              risa06                      sal(06)
     i              risa07                      sal(07)
     i              risa08                      sal(08)
     i              risa09                      sal(09)
     i              risa10                      sal(10)
     i              risa11                      sal(11)
     i              risa12                      sal(12)
     i              risa13                      sal(13)
     i              risa00                      sal(14)
      *
      /eject
      *
     c     dummy         tag
      *
      * Les saldo-records
      *
     c     rhsal1_key    setll     rhsal1r                                60
     c     rhsal1_key    reade     rhsal1r                                60
      *
     c                   dow       *in60 = *off                                          .....
      *
     c                   move      rifirm        w_firm
     c                   move      riavde        nyavd
     c                   move      rikont        nykto
     c                   move      rispes        nysps
     c                   move      riraar        w_raar
     c                   move      ritype        w_type
      *
      * Sjekk kontonr. fra/til
      *
     c                   if        nykto < rhpktf or                                     .....
     c                             nykto > rhpktt                                        .....
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
      * Sjekk kostnadsbærer fra/til
      *
     c                   if        nysps < rhpspf or                                     .....
     c                             nysps > rhpspt                                        .....
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
      * Sjekk avdeling fra/til
      *
     c                   if        nyavd < rhpavf or                                     .....
     c                             nyavd > rhpavt                                        .....
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
      * Sjekk om riktig år
      *
     c                   if        rhpfÅr <> w_raar                                      .....
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
      * Sjekk hvilke record-type skal behandles
      *
     c                   if        rhptyp = 'S' and                             SALDO    .....
     c                             w_type <> 1                                               .
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
     c                   if        rhptyp = 'B' and                             BUDSJETT .....
     c                             w_type <> 3                                               .
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
     c                   if        rhptyp = 'A' and                             ALT.BUDSJ.....
     c                             w_type <> 4                                               .
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
      * Test om oppdatering med faktor
      *
     c                   if        rhpbfa = 'J'                                          .....
      *
      * Bygg opp kontoklasse og kontogruppe
      *
     c                   move      rikont        w_kont
      *
     c                   eval      wbfa = 0                                                  .
      *
      * Henter omregningsfaktor kontogruppe
      *
     c                   move      w_grp         ra20lr_tkod
     c     ra20lr_key    chain     ra20lr                                        gruppe-
      *
     c                   if        %found(ra20lr)                               Kode
     c                   eval      wbfa = ratbfa                                Kontogruppe  .
     c                   endif                                                        ........
      *
      * Henter omregningsfaktor kontoklasse
      *
     c                   if        wbfa = 0                                     Kode
     c                   move      w_kla         ra19lr_skod
     c     ra19lr_key    chain     ra19lr                                        gruppe-
      *
     c                   if        %found(ra19lr)                               Kode
     c                   eval      wbfa = rasbfa                                  ikke       . .
     c                   endif                                                           .......
     c                   endif                                                           .......
      *
     c                   endif                                                           .......
      *
     c                   if        wbfa = 0                                              .....
     c                   eval      wbfa = 1.00                                               .
     c                   endif                                                           .....
      *
      * Beregn nytt beløp
      *
     c                   eval      nsa = sal * wbfa                                          .
      *
      * Oppdater budsjett
      *
     c                   eval      rhsalu_kont = rikont                                      .
     c                   eval      rhsalu_spes = rispes                                      .
     c                   eval      rhsalu_avde = riavde                                      .
     c                   eval      rhsalu_raar = rhptÅr                                      .
     c                   eval      rhsalu_type = 3                                           .
     c     rhsalu_key    chain     rhsalur                            60
      *
     c                   eval(h)   w_nsa  = nsa(1)
     c                   eval      risa01 = w_nsa
      *
     c                   eval(h)   w_nsa  = nsa(2)
     c                   eval      risa02 = w_nsa
      *
     c                   eval(h)   w_nsa  = nsa(3)
     c                   eval      risa03 = w_nsa
      *
     c                   eval(h)   w_nsa  = nsa(4)
     c                   eval      risa04 = w_nsa
      *
     c                   eval(h)   w_nsa  = nsa(5)
     c                   eval      risa05 = w_nsa
      *
     c                   eval(h)   w_nsa  = nsa(6)
     c                   eval      risa06 = w_nsa
      *
     c                   eval(h)   w_nsa  = nsa(7)
     c                   eval      risa07 = w_nsa
      *
     c                   eval(h)   w_nsa  = nsa(8)
     c                   eval      risa08 = w_nsa
      *
     c                   eval(h)   w_nsa  = nsa(9)
     c                   eval      risa09 = w_nsa
      *
     c                   eval(h)   w_nsa  = nsa(10)
     c                   eval      risa10 = w_nsa
      *
     c                   eval(h)   w_nsa  = nsa(11)
     c                   eval      risa11 = w_nsa
      *
     c                   eval(h)   w_nsa  = nsa(12)
     c                   eval      risa12 = w_nsa
      *
     c                   eval(h)   w_nsa  = nsa(13)
     c                   eval      risa13 = w_nsa
      *
     c                   eval      risa00 = *zero                                     IB = 0
      *
6.10 c                   time                    riedat
6.10 c                   time                    rietim
6.10 c                   eval      rieusr = l_user
      *
     c                   if        *in60 = *off
     c                   update    rhsalur
     c                   else
     c                   eval      riraar = rhptÅr                                           .
     c                   eval      ritype = 3                                                .
6.10 c                   time                    riodat
6.10 c                   time                    riotim
6.10 c                   eval      riousr = l_user
     c                   write     rhsalur
     c                   endif
      *
     c     neste         tag
      *
     c     rhsal1_key    reade     rhsal1r                                60
     c                   enddo
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on                                               .
      *
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Sett startverdier
      *
     c                   eval      nybrd = *zeros
     c                   eval      gmbrd = *zeros
     c                   eval      wbfa = 1.00
      *
     c                   move      l_firm        w_firm
      *
      * Hent antall siffer i kontonummer
      *
     c     raa3l1_key    chain     raa3l1
      *
      * Key for saldoregister - konto
      *
     c     rhsal1_key    klist
     c                   kfld                    w_firm
      *
      * Key for saldoregister - konto
      *
     c     rhsalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhsalu_kont
     c                   kfld                    rhsalu_spes
     c                   kfld                    rhsalu_avde
     c                   kfld                    rhsalu_raar
     c                   kfld                    rhsalu_type
      *
      * Key for statuskode 3
      *
     c     raa3l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for kontoklasse
      *
     c     ra19lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra19lr_skod
      *
      * Key for kontogruppe
      *
     c     ra20lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra20lr_tkod
      *
     c                   endsr
