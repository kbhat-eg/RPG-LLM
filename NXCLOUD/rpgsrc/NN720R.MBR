     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : NN720R
      * Beskrivelse . . : Nettbutikk, eksport av handleliste
      *                   (utgangspunkt i NN710R)
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       010806 BOF  Nytt program
5.70  * prgr  121208 bof  Utvidet med varetype lager
6.11  * 6.10  080709 bof  Utivdet eannr til 14 og brukt std. oppslag
6.20  * 6.20  021110 bof  Utvidet med utg.dato pr. lager
6.21  * 6.20  200511 bof  Utvidet vl710r med utg.dato og lev.nr
6.22  * 6.20  210813 bsa  Rekompilert grunnet endret ANBLPF
6.30  * 6.30  270515 bkv  lvar økt fra 15 til 20
6.31  * 6.30  080219 bof  Rekompilert grunnet endret ANBLPF
      *
8.01  *PRG2   160622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvshel3    if   e           k disk    rename(vshepfr:vshel3r)
     fvogrlr    if   e           k disk    rename(vogrpfr:vogrlrr)
     fvhgrlr    if   e           k disk    rename(vhgrpfr:vhgrlrr)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrpfr)
     fvpkol1    if   e           k disk    rename(vpkopfr:vpkol1r)
     fvvawpf    uf   e           k disk
     frlevpf    if   e           k disk    rename(rlevpfr:rlevpfr)
     fanbll3    if   e           k disk    rename(anblpfr:anbll3r)
     fanbhl1    if   e           k disk    rename(anbhpfr:anbhl1r)
      *
     fnetlist   uf a e           k disk
     fnetvrapp  o  a e             disk    rename(netvrapp:netvrappr)
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_fgr                 931    933
5.70 d l_lage               1001   1002  0
      *
      * Definering av datastrukturer
      *
      * Recordbeskrivelse - meldingshode
      *
     d                 ds
     d d_unh                         41
     d  d_unhrcd                      3    overlay(d_unh: 1) inz('UNH')
     d  d_unhfill1                    1    overlay(d_unh: 4) inz(';')
     d  d_unhrefno                   26    overlay(d_unh: 5)
     d  d_unhfill2                    1    overlay(d_unh:31) inz(';')
     d  d_unhtypeid                   6    overlay(d_unh:32) inz('IMLIMP')
     d  d_unhfill3                    1    overlay(d_unh:38) inz(';')
     d  d_unhversion                  3    overlay(d_unh:39) inz('1.0')
      *
      * Recordbeskrivelse - meldingsstart
      *
     d                 ds
     d d_bgm                         16
     d  d_bgmrcd                      3    overlay(d_bgm: 1) inz('BGM')
     d  d_bgmfill1                    1    overlay(d_bgm: 4) inz(';')
     d  d_bgmetypekod                 9    overlay(d_bgm: 5) inz('IMLITMIMP')
     d  d_bgmfill2                    2    overlay(d_bgm:14) inz(';;')
     d  d_bgmfunction                 1    overlay(d_bgm:16) inz('1')
      *
      * Recordbeskrivelse - organisasjon
      *
     d                 ds
     d d_org                        126
     d  d_orgrcd                      3    overlay(d_org: 1) inz('ORG')
     d  d_orgfill1                    1    overlay(d_org: 4) inz(';')
     d  d_orgid                      30    overlay(d_org: 5)
     d  d_orgfill2                    1    overlay(d_org:35) inz(';')
     d  d_orgstoreid                 30    overlay(d_org:36)
     d  d_orgfill3                    1    overlay(d_org:66) inz(';')
     d  d_orgname                    60    overlay(d_org:67)
      *
      * Recordbeskrivelse - listenavn
      *
     d                 ds
     d d_lna                         90
     d  d_lnarcd                      3    overlay(d_lna: 1) inz('LNA')
     d  d_lnafill1                    1    overlay(d_lna: 4) inz(';')
     d  d_lnasor                     10    overlay(d_lna: 5)
     d  d_lnafill2                    1    overlay(d_lna:15) inz(';')
     d  d_lnatex                     50    overlay(d_lna:16)
     d  d_lnafill3                    1    overlay(d_lna:66) inz(';')
     d  d_lnacus                      6  0 overlay(d_lna:67)
     d  d_lnafill4                    1    overlay(d_lna:73) inz(';')
     d  d_lnanwd                      8  0 overlay(d_lna:74)
     d  d_lnafill5                    1    overlay(d_lna:82) inz(';')
     d  d_lnachd                      8  0 overlay(d_lna:83)
      *
      * Recordbeskrivelse - vare
      *
     d                 ds
     d d_itm                        297
     d  d_itmrcd                      3    overlay(d_itm:  1) inz('ITM')
     d  d_itmfill1                    1    overlay(d_itm:  4) inz(';')
     d  d_itmnum                      8    overlay(d_itm:  5)
     d  d_itmfill2                    1    overlay(d_itm: 13) inz(';')
     d  d_itmtx1                     35    overlay(d_itm: 14)
     d  d_itmfill3                    1    overlay(d_itm: 49) inz(';')
     d  d_itmtx2                     35    overlay(d_itm: 50)
     d  d_itmfill4                    1    overlay(d_itm: 85) inz(';')
     d  d_itmogr                      2  0 overlay(d_itm: 86)
     d  d_itmfill5                    1    overlay(d_itm: 88) inz(';')
     d  d_itmhgr                      2  0 overlay(d_itm: 89)
     d  d_itmfill6                    1    overlay(d_itm: 91) inz(';')
     d  d_itmugr                      3  0 overlay(d_itm: 92)
     d  d_itmfill7                    1    overlay(d_itm: 95) inz(';')
     d  d_itmgtx                     50    overlay(d_itm: 96)
     d  d_itmfill8                    1    overlay(d_itm:146) inz(';')
     d  d_itmnob                      8  0 overlay(d_itm:147)
     d  d_itmfill9                    1    overlay(d_itm:155) inz(';')
     d  d_itmmod                      8  0 overlay(d_itm:156)
     d  d_itmfill10                   1    overlay(d_itm:164) inz(';')
     d  d_itmdel                      6  0 overlay(d_itm:165)
     d  d_itmfill11                   1    overlay(d_itm:171) inz(';')
     d  d_itmpro                     35    overlay(d_itm:172)
     d  d_itmfill12                   1    overlay(d_itm:207) inz(';')
     d  d_itmpnu                     15    overlay(d_itm:208)
     d  d_itmfill13                   1    overlay(d_itm:223) inz(';')
     d  d_itmean                     13  0 overlay(d_itm:224)
     d  d_itmfill14                   1    overlay(d_itm:237) inz(';')
     d  d_itmunt                      3    overlay(d_itm:238)
     d  d_itmfill15                   1    overlay(d_itm:241) inz(';')
     d  d_itmprc                     10    overlay(d_itm:242)
     d  d_itmfill16                   1    overlay(d_itm:252) inz(';')
     d  d_itmrgr                      7  0 overlay(d_itm:253)
     d  d_itmfill17                   1    overlay(d_itm:260) inz(';')
     d  d_itmrgt                     35    overlay(d_itm:261)
     d  d_itmfill18                   1    overlay(d_itm:296) inz(';')
     d  d_itmact                      1    overlay(d_itm:297)
      *
      * Recordbeskrivelse - meldingshale
      *
     d                 ds
     d d_unt                         43
     d  d_untrcd                      3    overlay(d_unt: 1) inz('UNT')
     d  d_untfill1                    1    overlay(d_unt: 4) inz(';')
     d  d_untnosegmen                12  0 overlay(d_unt: 5)
     d  d_untfill2                    1    overlay(d_unt:17) inz(';')
     d  d_untrefno                   26    overlay(d_unt:18)
      *
      *
      * Definering av variabler i nøkler
      *
     d vhgrlr_hogr                         like(vghogr)
     d vhgrlr_hhgr                         like(vghhgr)
      *
     d vogrlr_oogr                         like(vgoogr)
      *
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d vpkol1_pkod     s                   like(vapkod)
     d vshel3_type     s                   like(vctype)
     d rlevpf_levr     s                   like(rllevr)
     d anbll3_fgr      s                   like(aolfgr)
     d anbll3_fir      s                   like(aolfir)
     d vvawpf_xxx      s                   like(vwakey)
      *
      * Definering av variable
      *
     d b_pris          s              1    inz(*off)
     d b_svgr          s              1    inz(*off)
     d b_sldo          s              1    inz(*off)
     d b_stek          s              1    inz(*off)
6.20 d b_inlr          s              1    inz(*off)
      *
     d w_tmst          s               z
     d w_dato          s               d   datfmt(*iso)
6.20 d w_udat          s               d   datfmt(*iso)
6.21 d w_ldor          s                   like(vvldor)
     d w_tell          s             12  0
     d w_ponr          s              4
     d w_omre          s             12
     d w_hoyd          s             12
     d w_leng          s             12
     d w_bred          s             12
     d w_volu          s             12
     d w_vekt          s             12
     d w_sapr          s              9
     d w_vare_gml      s                   like(vvvare)
6.11 d w_east          s              1
6.11 d w_eann          s             14  0
      *
     d w_vsor          s                   like(vcvsor)
     d w_firm          s                   like(vvfirm)
5.70 d w_lage          s              2  0
6.21 d w_vtyp          s                   like(vvvtyp)
      *
      * Definering av parametre
     d p_stat          s              1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      p_stat = *blank
      *
      * Henter data om nettbutikk og eier. Avslutter hvis ingen finnes.
      *
     c                   eval      anbll3_fgr = l_fgr
     c                   eval      anbll3_fir = l_firm
     c     anbll3_key    chain     anbll3r
     c                   if        not %found
     c                   eval      p_stat = 'E'
     c                   goto      avslutt
     c                   endif
     c     aoleie        chain     anbhl1r
     c                   if        not %found
     c                   eval      p_stat = 'E'
     c                   goto      avslutt
     c                   endif
      *
      * Meldingshode
      *
     c                   time                    w_tmst
     c                   move      w_tmst        d_unhrefno
      *
     c                   eval      data = d_unh
     c                   write     netlistr
      *
      * Meldingsstart
      *
     c                   eval      data = d_bgm
     c                   write     netlistr
      *
      * Organisasjon
      *
     c                   eval      d_orgid = aoheie
     c                   eval      d_orgstoreid = aolbut
     c                   eval      d_orgname = aohnav
      *
     c                   eval      data = d_org
     c                   write     netlistr
      *
      * Leser varesortiment - finner type H
      *
     c                   eval      vshel3_type = 'H'
     c     vshel3_key    setll     vshel3
     c     vshel3_key    reade     vshel3
     c                   dow       not %eof(vshel3)
      *
     c                   eval      w_tell = w_tell + 1
      *
     c                   eval      d_lnasor = vcvsor
     c                   eval      d_lnatex = vcbesk
      *
     c                   eval      data = d_lna
     c                   write     netlistr
      *
      *
      *
      * Varesortiment - detaljer
      *
     c                   exsr      behandl_vsor
      *
     c     vshel3_key    reade     vshel3
     c                   enddo
      *
      * Meldingshale
      *
     c                   eval      d_untnosegmen = w_tell
     c                   eval      d_untrefno = d_unhrefno
      *
     c                   eval      data = d_unt
     c                   write     netlistr
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av utskrift - utplukk på varesortiment
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandl_vsor  begsr
      *
      * Kaller utplukksprogram
      *
     c                   eval      b_svgr = *on
     c                   eval      b_sldo = *on
     c                   eval      b_stek = *on
     c                   eval      w_vsor = vcvsor
      *
     c                   call      'VD700R'
     c                   parm                    w_firm
     c                   parm                    w_vsor
     c                   parm                    b_svgr
     c                   parm                    b_sldo
     c                   parm                    b_stek
6.20 c                   parm                    w_lage
      *
      * Leser utplukksfil som inneholder alle varer i sortimentet
      *
     c                   eval      w_vare_gml = *blank
      *
     c                   eval      vvawpf_xxx = *blank
     c     vvawpf_key    setll     vvawpf
     c                   read      vvawpf                                 90
     c                   dow       *in90 = *off
      *
     c                   if        vvvare <> w_vare_gml
     c                   exsr      behandling
     c                   endif
      *
     c                   eval      w_vare_gml = vvvare
     c                   delete    vvawpfr
      *
     c                   read      vvawpf                                 90
     c                   enddo
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vare m/enheter og priser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
      * Kun lagervarer behandles
      *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    w_lage
6.21 c                   parm                    w_vtyp
6.21 c                   parm                    w_udat
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
      *
6.21 c                   if        w_vtyp <> 'L'
     c                   goto      neste_vare
     c                   endif
      *
6.20 c                   if        w_udat <> *loval
6.20 c                   if        w_dato >= w_udat
     c                   goto      neste_vare
     c                   endif
     c                   endif
      *
     c                   if        %subst(vvtek1:1:1) = '*'
     c                   goto      neste_vare
     c                   endif
      *
     c                   eval      w_tell = w_tell + 1
      *
     c                   eval      d_itmnum = vvvare
     c                   eval      d_itmtx1 = vvtek1
     c                   eval      d_itmtx2 = vvtek2
     c                   eval      d_itmogr = vvogrp
     c                   eval      d_itmhgr = vvhgrp
     c                   eval      d_itmugr = vvugrp
     c                   eval      d_itmgtx = *blank
     c                   eval      d_itmnob = vvnobb
     c                   eval      d_itmmod = vvnmod
     c                   eval      d_itmdel = vvnlev
     c                   eval      d_itmpro = *blank
6.30 c*                  eval      d_itmpnu = vvlvar
6.30 c                   movel     vvlvar        d_itmpnu
     c                   eval      d_itmean = 0
     c                   eval      d_itmunt = vvenhe
     c                   eval      d_itmprc = *blank
     c                   eval      d_itmrgr = 0
     c                   eval      d_itmrgt = *blank
     c                   eval      d_itmact = 'Y'
      *
      * Varegruppetekst
      *
     c                   if        vvogrp = 0 and vvhgrp = 0 and
     c                             vvugrp = 0
     c                   goto      neste_vare
     c                   endif
     c                   eval      vogrlr_oogr = vvogrp
     c     vogrlr_key    chain     vogrlrr
     c                   if        not %found
     c                   eval      Data = 'Overgruppe ' +
     c                             %trim(%char(vvogrp)) +
     c                             '  *  *  finnes ikke. ' +
     c                             'Vare ' + %trim(vvvare) + ' ' +
     c                             %trim(vvtek1) + ' ble utelatt.'
     c                   write     netvrappr
     c                   eval      p_stat = 'R'
     c                   goto      neste_vare
     c                   endif
      *
     c                   eval      vhgrlr_hogr = vvogrp
     c                   eval      vhgrlr_hhgr = vvhgrp
     c     vhgrlr_key    chain     vhgrlrr
     c                   if        not %found
     c                   eval      Data = 'Hovedgruppe ' +
     c                             %trim(%char(vvogrp)) +
     c                             '  ' + %trim(%char(vvhgrp)) +
     c                             '  *  finnes ikke. Vare ' +
     c                             %trim(vvvare) + ' ' +
     c                             %trim(vvtek1) + ' ble utelatt.'
     c                   write     netvrappr
     c                   eval      p_stat = 'R'
     c                   goto      neste_vare
     c                   endif
      *
     c                   eval      vugrl1_uogr = vvogrp
     c                   eval      vugrl1_uhgr = vvhgrp
     c                   eval      vugrl1_uugr = vvugrp
     c     vugrl1_key    chain     vugrl1
     c                   if        %found
     c                   eval      d_itmgtx = vgutxt
     c                   else
     c                   eval      Data = 'Undergruppe ' +
     c                             %trim(%char(vvogrp)) +
     c                             '  ' + %trim(%char(vvhgrp)) +
     c                             '  ' + %trim(%char(vvugrp)) +
     c                             ' finnes ikke. Vare ' +
     c                             %trim(vvvare) + ' ' +
     c                             %trim(vvtek1) + ' ble utelatt.'
     c                   write     netvrappr
     c                   eval      p_stat = 'R'
     c                   goto      neste_vare
     c                   endif
      *
      * Leverandørnavn
      *
     c                   if        vvldor <> 0
     c                   eval      rlevpf_levr = vvldor
     c     rlevpf_key    chain     rlevpf
     c                   if        %found
     c                   eval      d_itmpro = rlnavn
     c                   endif
     c                   endif
      *
      * EAN-nummer
      *
6.11 c                   call      'VE712R'
6.11 c                   parm                    w_firm
6.11 c                   parm                    vvvare
6.11 c                   parm                    w_eann
6.11 c                   parm                    w_east
6.11 c                   if        w_east = '1'
6.11 c                   eval      d_itmean = w_eann
     c                   endif
      *
      * Priskode-beskrivelse
      *
     c                   eval      vpkol1_pkod = vvkpri
     c     vpkol1_key    chain     vpkol1
     c                   if        %found
     c                   eval      d_itmprc = vaptxt
     c                   endif
      *
      * Skriver til fil
      *
     c                   eval      data = d_itm
     c                   write     netlistr
      *
     c     neste_vare    tag
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_stat
      *
      * Key for oppslag på overgruppe
      *
     c     vogrlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrlr_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrlr_hogr
     c                   kfld                    vhgrlr_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på priskode
      *
     c     vpkol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vpkol1_pkod
      *
      * Key for les av varesortiment - heading
      *
     c     vshel3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vshel3_type
      *
      * Key for oppslag på leverandør
      *
     c     rlevpf_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevpf_levr
      *
      * Key for oppslag på nettbutikk
      *
     c     anbll3_key    klist
     c                   kfld                    anbll3_fgr
     c                   kfld                    anbll3_fir
      *
      * Key for les av arbeidsfil
      *
     c     vvawpf_key    klist
     c                   kfld                    vvawpf_xxx
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
5.70 c                   eval      w_lage = l_lage
      *
     c                   time                    w_dato
      *
     c                   endsr
