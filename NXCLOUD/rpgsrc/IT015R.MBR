     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASNAVIOKO
      * Program . . . . : IT015R
      * Beskrivelse . . : Leser lager og skriver til kø
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       240614 ask  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra10l1    if   e           k disk    rename(ra10pfr:ra10l1r)
      *
      * Local Data Area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d ra10l1_jkod     s                   like(rajkod)
      *
      *  Definering av datastrukturer for parameter
      *
     d d_parm          ds
     d  d_kode                        2
     d  d_besk                       40
     d  d_adr1                       30
     d  d_adr2                       30
     d  d_ponr                        4
     d  d_pste                       30
     d  d_teln                       11
     d  d_faxn                       11
     d  d_avde                        4
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_prog          s             40
     d w_comd          s             20
     d w_parm          s           2048
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_rpgp          s             10
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram - leser alle poster og kaller opp std skriveprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser alle poster på ra10PF
      *
     c                   eval      ra10l1_jkod = 0
     c     ra10l1_key    setll     ra10l1
      *
     c                   read      ra10l1
     c                   dow       not %eof and
     c                             rajfir = w_firm
      *
     c                   eval      d_kode = %triml(%editw(rajkod : ' 0'))
     c                   eval      d_besk = rajtxt
     c                   eval      d_adr1 = rajadr
     c                   eval      d_adr2 = rajad2
     c                   move      rajpnr        d_ponr
     c                   eval      d_pste = rajste
     c                   eval      d_teln = rajtlf
     c                   eval      d_faxn = rajfax
     c                   move      rajavd        d_avde
     c                   eval      w_parm = d_parm
      *
     c                   call      'IT001R'
     c                   parm                    w_firm
     c                   parm                    w_prog
     c                   parm                    w_comd
     c                   parm                    w_parm
     c                   parm                    w_rpgp
      *
     c                   read      ra10l1
     c                   enddo
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på valgt kode
      *
     c     ra10l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra10l1_jkod
      *
      * Setter inn parameterverdier
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_prog = 'NexstepImportLager'
     c                   eval      w_comd = 'Insert'
     c                   eval      w_rpgp = 'IT015R'
      *
     c                   endsr
