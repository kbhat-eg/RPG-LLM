     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FX798R
      * Beskrivelse . . : Kontrollprogram, kort kid -> fakturanummer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       220707 HFL  Nytt program for å finne fakturanr. av kort-kid.
6.30  *       030615 bof  utvidet kid-ref fra 7 til 9 + endr.vedr. utv.bil.nr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fakidl1    if   e           k disk    rename(akidpfr:akidl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffx798d    cf   e             workstn
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Datastruktur for lang kid
      *
     d                 ds
     d d_lang                        25
6.30 d  d_kund                        6  0 overlay(d_lang:7)
6.30 d  d_fakt                        8  0 overlay(d_lang:13)
      *
      * Datastruktur for kort kid
      *
     d                 ds
6.30 d d_kort                         9
6.30 d  d_kidr                        8  0 overlay(d_kort)
6.30 d  d_ksif                        1  0 overlay(d_kort:9)
      *
      * Definering av variabler i nøkler
      *
     d akidl1_kidr     s                   like(akkidr)
     d rkunl1_kund     s                   like(rkkund)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedrutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandling av Åpningsbildet
      *
     c                   exsr      xc1bld
      *
      * Avslutt program
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      c1kkid = *zero
     c                   eval      *in40 = *off
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
     c                   if        c1kkid = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Gå til neste bilde
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1kkid = *zero
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      *in40  = *off
     c                   eval      c2fakt = *zeros
     c                   eval      c2kund = *zeros
     c                   eval      c2aarr = *zeros
     c                   eval      c2navn = *blank                                            .
     c                   movel     c1kkid        d_kort
      *
      * Hent opplysninger fra KID-REGISTER
      *
     c                   eval      akidl1_kidr = d_kidr
     c     akidl1_key    chain     akidl1r
     c                   if        %found and
     c                             d_ksif = akksif
     c                   eval      d_lang = akkidd
     c                   eval      c2fakt = d_fakt
     c                   eval      c2kund = d_kund
     c                   eval      c2aarr = akaarr
      *
      * Hent opplysninger fra KUNDE-REGISTER
      *
     c                   eval      rkunl1_kund = c2kund
     c     rkunl1_key    chain     rkunl1r
     c                   if        %found
     c                   eval      c2navn = rknavn                                            .
     c                   else
     c                   eval      c2navn = 'Kunde ikke funnet'                               .
     c                   endif
      *
     c                   else
     c                   eval      *in40 = *on
     c                   endif
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl
     c                   goto      end_c2bld
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for file : KID-REGISTER
      *
     c     akidl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    akidl1_kidr
      *
      * Key for file : KUNDE-REGISTER
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
