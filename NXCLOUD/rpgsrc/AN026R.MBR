     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : AN025R
      * Beskrivelse . . : Leser systemfile for backup
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
6.11  *  6.10 030113 AMD  I dette programmet ble kode for savefile satt
6.11  *                   lik blank, da program-bibliotek ikke kunne
6.11  *                   kjøres ut på savefile.  Dette må nå være
6.11  *                   mulig, og koden blir derfor beholdt uten endr
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fasysl1    if   e           k disk    rename(asyspfr:asysl1r)
      *                                                                     -
      * Definering av parametere
      *                                                                     -
     d p_savf          s              1
      *                                                                     -
      * Definering av variable i nøkler
      *                                                                     -
     d asysl1_syst     s                   like(assyst)
      *                                                                     -
      * Definering av variable
      *                                                                     -
     d w_bibl          s                   like(assyst)
      *
      * ---------------------------------------------------------------
      * Filen som inneholder systemregister leses fra start til slutt.
      * For hver record som leses, sjekkes det om denne skal være med
      * på  backup.  Dersom den skal være med kalles et Cl-program
      * med to parametere: p_savf og w_bibl.
      * I dette tilfellet settes p_savf lik blank, da programbibliotek
      * ikke kjøres via savefiler
6.11  * Nei, det er ikke tilfelle lenger, savefile skal kunne benyttes
6.11  * også for programbibliotek
      * ---------------------------------------------------------------
      *
      * Save av bibliotek fra filen for systemer
      *
     c                   eval      w_bibl = *blank
     c                   eval      asysl1_syst = *blank
     c     asysl1_key    setll     asysl1
     c                   read      asysl1                                 62
      *
     c                   dow       *in62 = *off
     c                   if        asskod = 'B'
     c                   movel     assyst        w_bibl
     c                   call      'AN025C'
     c                   parm                    p_savf
     c                   parm                    w_bibl
     c                   endif
     c                   read      asysl1                                 62
     c                   enddo
      *
      * Avslutt program
      *
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *
6.11  * Sperret          eval      p_savf = *blank
      *
      * Key for bibliotekslister
      *
     c     asysl1_key    klist
     c                   kfld                    asysl1_syst
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_savf
      *
     c                   endsr
      *
