     h option(*nodebugio : *srcstmt) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LD102R
      * Beskrivelse . . : Bestilling-linje, spørring/registr. av varelinjer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       191102 HKO  Nytt program
      *       251103 HKO  Ekskluderer/inkluderer alle varer/kun lagervarer
      *                   Beholder posisjonering i subfile etter forny
      *                   Viser ikke utgåtte varer
      * V5.60 270308 KOB  Plasserer cursor i frisøkfeltet etter registrering av varelinje
prg   * 5.70  290908 sst  Prisgruppe
6.1   * 6.10  230910 bof  Justering av varetype i blaing
6.20  * 6.20  021110 bof  Utvidet med utg.dato pr. lager
6.21  * 6.20  091110 bof  Utvidet med status for C-sentrallagerv. P=periodev.
6.22  * 6.20  051210 bof  Hvis sentral-lagervare, settes farge lik rød
6.23  * 6.20  280410 bof  Gir melding hvis kostpris er endret på ordre
6.24  * 6.20  150211 bof  Sentral-lagervare kode for styring av farge
6.25  * 6.20  190511 bø   Utvidet vl710r med div. info
6.26  * 6.20  120112 bsa  Utvidet ld015r og vp750r med kampanje
6.27  * 6.20  220515 nho  Viser ikke  devaren dersom vare er utgått og det på lager ligger
      *                   utgått dato frem i tid
6.nu  * 6.30  260514 bof  Utvidelse ihht. nummerserieutvidelse
6.30  * 6.30  230914 bsa  Lev.varenr er utvidet i skjermbildet
6.31  *       180515 BKV  Endret LVAR pga økt fra 15 til 20 tegn
6.3q  *       260515 bof  Datoformat sql
6.31  * 6.30  190816 bof  Kaller VP751R istf. VP750R, med lev.nr+lager.
6.32  * 6.30  170918 bkv  Utvidet med trelast sortiment
6.33  * 6.30  121018 bof  justert, bruker leverandør fra bestilling.
7.xx  * 7.00  030316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  010419 bkv  Programmet kan initieres med verdi, da skal
7.01  *                   det fungere som varesøk.
7.02  * 7.00  020419 bkv  Filter på leverandør
7.03  * 7.00  231019 bkv  Utvider med 132 bilde
7.04  * 7.00  291119 bkv  Kode hvis beholding er for stort negativt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvsopf    if   e           k disk
     fvvarlr    if   e           k disk    rename(vvarpfr:vvarlrr)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarl2    if   e           k disk    rename(vvarpfr:vvarl2r)
     fvvarl3    if   e           k disk    rename(vvarpfr:vvarl3r)
     fvvarl4    if   e           k disk    rename(vvarpfr:vvarl4r)
     fvvarl6    if   e           k disk    rename(vvarpfr:vvarl6r)
     fvvarl7    if   e           k disk    rename(vvarpfr:vvarl7r)
     fvvarl8    if   e           k disk    rename(vvarpfr:vvarl8r)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     flusrl1    if   e           k disk    rename(lusrpfr:lusrl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
6.24 ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
6.32 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
7.03 fvlagpf    if   e           k disk
      *
     fld102d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
6.23  * Definering av array
6.23  *
6.23 d a_meld          s             50    dim(1) ctdata perrcd(1)
      *
      * Definering av parametre
      *
     d p_kode          s              1
     d p_firm          s                   like(lofirm)
     d p_numm          s                   like(lonumm)
     d p_suff          s                   like(losuff)
     d p_line          s                   like(ldline)
3.31 d p_kost          s              1
6.23 d p_orde          s              1
6.27 d w_d1at          s               d   datfmt(*iso)
7.01 d p_vare          s             35
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d vvarlr_vare     s                   like(vvvare)
     d vvarl1_vare     s                   like(vvvare)
     d vvarl2_tek1     s                   like(vvtek1)
     d vvarl3_nobb     s                   like(vvnobb)
     d vvarl4_lvar     s                   like(vvlvar)
     d vvarl6_ogrp     s                   like(vvogrp)
     d vvarl7_ogrp     s                   like(vvogrp)
     d vvarl7_hgrp     s                   like(vvhgrp)
     d vvarl8_ogrp     s                   like(vvogrp)
     d vvarl8_hgrp     s                   like(vvhgrp)
     d vvarl8_ugrp     s                   like(vvugrp)
     d lusrl1_user     s                   like(lbuser)
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              2
     d b_feil          s              1    inz(*off)
     d b_open_sok      s              1    inz(*off)
6.21 d b_sess          s              1    inz(*off)
6.21 d b_sent          s              1    inz(*off)
7.03 d* b_inlr          s              1    inz(*off)
7.03 d b_inlr          s              1    inz(*on)
      *
6.20 d w_ddat          s               d   datfmt(*iso)
6.20 d w_udat          s               d   datfmt(*iso)
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
     d w_ste1          s             35
     d w_ste2          s             35
     d w_ste3          s             35
     d w_ste4          s             35
     d w_ste5          s             35
     d w_posi          s              3  0
     d w_otxt          s             35
     d w_htxt          s             35
     d w_utxt          s             35
      *
     d s_stek          s             35
      *
     d w_firm          s                   like(vvfirm)
     d w_numm          s                   like(lonumm)
     d w_suff          s                   like(losuff)
     d w_line          s                   like(ldline)
      *
     d w_vare          s                   like(vvvare)
     d w_anta          s                   like(ldanta)
     d w_kopr          s                   like(ldkpny)
     d w_toti          s                   like(ldsumi)
     d w_totr          s                   like(ldsumr)
     d w_ldat          s                   like(ldldat)
     d w_lety          s                   like(ldlety)
5.70 d w_vtyp          s                   like(vvvtyp)
5.70 d w_avde          s              4  0
5.70 d w_prgr          s              1
6.23 d w_mld1          s             50
6.25 d w_ldor          s                   like(vvldor)
6.26 d w_kamp          s              5
6.32 d w_lv_nobb       s                   like(vvlnob)
6.32 d w_lv_modn       s                   like(vvlmod)
6.32 d w_lv_lldo       s                   like(vvlnle)
6.32 d w_lv_llva       s                   like(vvllva)
6.32 d w_undv          s              8  0
6.32 d w_logl          s             15
6.32 d w_jvqvnr        s                   like(vvvare)
6.33 d w_jvquno        s              8  0
7.01 d w_varesok       s             35
7.01 d b_dirs          s              1    inz(*off)
7.02 d w_ant           s              3  0
7.03 d w_len           s              3  0
7.03 d w_teller        s              3  0
7.03 d w_behl          s             11  3
7.03 d w_bbes          s                   like(vlbbes)
7.03 d w_text          s                   like(b1text)
7.03 d w_mini          s                   like(vlmini)
7.03 d w_maks          s                   like(vlmaks)
7.03 d w_bmen          s                   like(vlbmen)
7.03 d w_bmin          s                   like(vlbmin)
7.03 d w_omre          s             16 10
      *
     d s_ogrp          s                   like(vvogrp)
     d s_hgrp          s                   like(vvhgrp)
     d s_ugrp          s                   like(vvugrp)
      *
      * Konstanter
      *
7.03 d c_sfil          s              2  0 inz(18)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2 Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
6.3q C/End-Exec
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
7.03 c*                   when      *in11
7.03 c*                   exsr      bck_subfile
7.03 c*                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * F2=Registrering av tekstlinjer
      *
     c                   if        *inkb = *on
     c                   eval      w_line = w_line + 10
     c                   call      'LD110R'
     c                   parm                    p_kode
     c                   parm                    w_firm
     c                   parm                    w_numm
     c                   parm                    w_suff
     c                   parm                    w_line
     c                   parm                    laalty
     c                   goto      b2tagb
     c                   endif
7.02  *
7.02  * F7=Vis alle/Vis for leverandør
7.02  *
7.02 c                   if        *inkg = *on
7.02 c                   eval      *in85 = not *in85
7.02 c                   eval      b1tek1 = *blanks
7.03 c                   eval      b1text = *blanks
7.02 c                   eval      b1vare = *blanks
7.02 c                   eval      w_fcrn = 0
7.02 c                   exsr      forny
7.02 c                   goto      b2taga
7.02 c                   endif
      *
      * F9=Gjenta søk
      *
     c                   if        *inki = *on
     c                   eval      *in22 = *on
     c                   eval      b2ogrp = s_ogrp
     c                   eval      b2hgrp = s_hgrp
     c                   eval      b2ugrp = s_ugrp
     c                   eval      b2stek = s_stek
     c                   goto      b2tagb
     c                   endif
      *
      * F21=Bytt mellom å utelate og ta med alle varetyper
      *
     c                   if        *inkv = *on
      *
     c                   if        *in83 = *off
     c                   eval      *in83 = *on
     c                   else
     c                   eval      *in83 = *off
     c                   endif
      *
     c***                exsr      hent_total
     c                   exsr      forny                                        CRT SFL
      *
     c                   goto      b2taga
      *
     c                   endif
      *
      * F22=Bytt mellom å vise salgspris eksl. mva og inkl. mva
      *
     c                   if        *inkw = *on
      *
     c                   if        *in84 = *off
     c                   eval      *in84 = *on
     c                   else
     c                   eval      *in84 = *off
     c                   endif
      *
     c                   exsr      hent_total
     c                   exsr      forny                                        CRT SFL
      *
     c                   goto      b2taga
      *
     c                   endif
      *
      * F23=Ny posisjonering (Varenr/Nobbnr/Lev.varenr)
      *
     c                   if        *inkx = *on
      *
     c                   if        *in80 = *on
     c                   eval      *in80 = *off
     c                   eval      *in81 = *on
     c                   exsr      forny                                        CRT SFL
     c                   goto      b2taga
     c                   endif
      *
     c                   if        *in81 = *on
     c                   eval      *in81 = *off
     c                   eval      *in82 = *on
     c                   exsr      forny                                        CRT SFL
     c                   goto      b2taga
     c                   endif
      *
     c                   if        *in82 = *on
     c                   eval      *in82 = *off
     c                   eval      *in80 = *on
     c                   exsr      forny                                        CRT SFL
     c                   goto      b2taga
     c                   endif
      *
     c                   endif
      *
      * Søking
      *
     c                   if        b2ogrp <> 0 or
     c                             b2hgrp <> 0 or
     c                             b2ugrp <> 0 or
     c                             b2stek <> *blank
     c                   exsr      sØk
     c                   goto      b2tagb
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2tek1 <> *blank or
     c                             b2vare <> 0 or
     c                             b2nobb <> 0 or
     c                             b2lvar <> *blank
     c                   exsr      posisjoner
     c                   goto      b2taga
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c*                   goto      b2taga
7.01 c                   if        b_dirs = *off
7.01 c                   goto      b2taga
7.01 c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
6.23 c                   if        p_orde = '1'
6.23 c                   eval      w_mld1 = a_meld(1)
6.23 c                   call      'AA005R'
6.23 c                   parm                    w_mld1
6.23 c                   endif
      *
7.03 c                   eval      b_inlr = *off
7.03 c                   call      'VL710R'
7.03 c                   parm                    w_firm
7.03 c                   parm                    vvvare
7.03 c                   parm                    lolage
7.03 c                   parm                    w_vtyp
7.03 c                   parm                    w_udat
7.03 c                   parm                    w_ldor
7.03 c                   parm                    b_inlr
7.03 c                   parm                    w_lv_nobb
7.03 c                   parm                    w_lv_modn
7.03 c                   parm                    w_lv_lldo
7.03 c                   parm                    w_lv_llva
7.03 c                   call      'VL717R'
7.03 c                   parm                    w_firm
7.03 c                   parm                    vvvare
7.03 c                   parm                    lolage
7.03 c                   parm                    b1enhe
7.03 c                   parm                    w_vtyp
7.03 c                   parm                    w_udat
7.03 c                   parm                    w_ldor
7.03 c                   parm                    w_behl
7.03 c                   parm                    w_bbes
7.03 c                   parm                    w_mini
7.03 c                   parm                    w_maks
7.03 c                   parm                    w_bmen
7.03 c                   parm                    w_bmin
7.03 c                   parm                    w_omre
7.03 c                   parm                    b_inlr
7.03 c                   call      'VD771R'
7.03 C                   parm                    w_firm
7.03 C                   parm                    lolage
7.03 C                   parm                    vvvare
7.03 C                   parm                    b_sent
7.03 C                   parm                    b_inlr
7.03 c                   call      'VD772R'
7.03 C                   parm                    w_firm
7.03 C                   parm                    lolage
7.03 C                   parm                    vvvare
7.03 C                   parm                    b_sess
7.03 C                   parm                    b_inlr
      *
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'O'
     c     vvarl6_key    setll     vvarl6
     c                   when      w_seqe = 'H'
     c     vvarl7_key    setll     vvarl7
     c                   when      w_seqe = 'U'
     c     vvarl8_key    setll     vvarl8
     c                   when      w_seqe = 'SO' or
     c                             w_seqe = 'SH' or
     c                             w_seqe = 'SU' or
     c                             w_seqe = 'S '
     c                   eval      b_open_sok = *on
     c                   when      w_seqe = 'V'
     c                   eval      vvarl1_vare = b1vare
     c     vvarl1_key    setll     vvarl1
     c                   when      w_seqe = 'N'
     c                   eval      vvarl3_nobb = b1nobb
     c     vvarl3_key    setll     vvarl3
     c                   when      w_seqe = 'L'
     c                   eval      vvarl4_lvar = b1lvar
     c     vvarl4_key    setll     vvarl4
     c                   other
     c                   eval      vvarl2_tek1 = b1tek1
     c     vvarl2_key    setll     vvarl2
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for søking i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sØk           begsr
      *
      * Validering av varegruppe
      *
     c                   if        b2ogrp = 0 and
     c                             b2hgrp <> 0
     c                   eval      *in31 = *on
     c                   goto      end_sØk2
     c                   endif
      *
     c                   if        b2hgrp = 0 and
     c                             b2ugrp <> 0
     c                   eval      *in32 = *on
     c                   goto      end_sØk2
     c                   endif
      *
      * Tar vare på søk
      *
     c                   eval      s_ogrp     = b2ogrp
     c                   eval      s_hgrp     = b2hgrp
     c                   eval      s_ugrp     = b2ugrp
     c                   eval      s_stek     = b2stek
      *
      * Søk via søketekst
      *
     c                   if        b2stek <> *blank
      *
     c                   eval      b_open_sok = *on
      *
     c                   select
     c                   when      b2ogrp = 0 and
     c                             b2hgrp = 0 and
     c                             b2ugrp = 0
     c                   eval      w_seqe = 'S '
     c                   when      b2ogrp <> 0 and
     c                             b2hgrp = 0 and
     c                             b2ugrp = 0
     c                   eval      w_seqe = 'SO'
     c                   when      b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp = 0
     c                   eval      w_seqe = 'SH'
     c                   when      b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp <> 0
     c                   eval      w_seqe = 'SU'
     c                   endsl
      *
      * Utleder søketekster ut av søketekst
      *
     c                   call      'AS700R'
     c                   parm                    b2stek
     c                   parm                    w_ste1
     c                   parm                    w_ste2
     c                   parm                    w_ste3
     c                   parm                    w_ste4
     c                   parm                    w_ste5
      *
     c                   goto      end_sØk
      *
     c                   endif
      *
      * Søk på overgruppe
      *
     c                   if        b2ogrp <> 0 and
     c                             b2hgrp =  0 and
     c                             b2ugrp =  0
     c                   eval      w_seqe = 'O'
     c                   eval      vvarl6_ogrp = b2ogrp
     c     vvarl6_key    setll     vvarl6
     c                   goto      end_sØk
     c                   endif
      *
      * Søk på hovedgruppe
      *
     c                   if        b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp =  0
     c                   eval      w_seqe = 'H'
     c                   eval      vvarl7_ogrp = b2ogrp
     c                   eval      vvarl7_hgrp = b2hgrp
     c     vvarl7_key    setll     vvarl7
     c                   goto      end_sØk
     c                   endif
      *
      * Søk på undergruppe
      *
     c                   if        b2ogrp <> 0 and
     c                             b2hgrp <> 0 and
     c                             b2ugrp <> 0
     c                   eval      w_seqe = 'U'
     c                   eval      vvarl8_ogrp = b2ogrp
     c                   eval      vvarl8_hgrp = b2hgrp
     c                   eval      vvarl8_ugrp = b2ugrp
     c     vvarl8_key    setll     vvarl8
     c                   goto      end_sØk
     c                   endif
      *
     c     end_sØk       tag
      *
      * Blanker subfile, og fyller opp i.h.t. søk
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker søkefelt
      *
     c                   eval      b2ogrp = 0
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   eval      b2stek = *blank
      *
     c     end_sØk2      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Varetekst
      *
     c                   if        b2tek1 <> *blank
     c                   eval      w_seqe = *blank
     c                   eval      vvarl2_tek1 = b2tek1
     c     vvarl2_key    setll     vvarl2
     c                   goto      end_pos
     c                   endif
      *
      * Varenummer
      *
     c                   if        b2vare <> 0
     c                   eval      w_seqe = 'V'
     c                   eval      *in80 = *on
     c                   eval      *in81 = *off
     c                   eval      *in82 = *off
     c                   movel     b2vare        vvarl1_vare
     c     vvarl1_key    setll     vvarl1
     c                   goto      end_pos
     c                   endif
      *
      * Nobbnummer
      *
     c                   if        b2nobb <> 0
     c                   eval      w_seqe = 'N'
     c                   eval      *in80 = *off
     c                   eval      *in81 = *on
     c                   eval      *in82 = *off
     c                   eval      vvarl3_nobb = b2nobb
     c     vvarl3_key    setll     vvarl3
     c                   goto      end_pos
     c                   endif
      *
      * Lev. varenummer
      *
     c                   if        b2lvar <> *blank
     c                   eval      w_seqe = 'L'
     c                   eval      *in80 = *off
     c                   eval      *in81 = *off
     c                   eval      *in82 = *on
     c                   eval      vvarl4_lvar = b2lvar
     c     vvarl4_key    setll     vvarl4
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c***                eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2tek1 = *blank
     c                   eval      b2vare = 0
     c                   eval      b2nobb = 0
     c                   eval      b2lvar = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
5.60 c***                eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Antall er utfylt, linje skal registreres
      *
     c                   if        b1anta <> 0
     c                   exsr      registrer
7.03 c*                   move      '->'          b1tek1
7.03 c                   move      '->'          b1text
     c                   eval      b1anta = 0
6.32  * sjekk farge
6.32 c                   eval      w_jvqvnr = b1vare
6.32 c                   exsr      finn_logivar
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for registering av vare i bestilling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     registrer     begsr
      *
     c                   eval      w_line = w_line + 10
     c                   eval      w_vare = b1vare
     c                   eval      w_anta = b1anta
      *
      * Viser registeringbildet
      *
     c                   eval      w_lety = 0
6.23 c                   eval      p_orde = '0'
6.26 c                   eval      w_kamp = lokamp
      *
     c                   call      'LD105R'
     c                   parm                    p_kode
     c                   parm                    w_firm
     c                   parm                    w_numm
     c                   parm                    w_suff
     c                   parm                    w_line
     c                   parm                    laalty
     c                   parm                    w_lety
     c                   parm                    w_vare
     c                   parm                    w_anta
     c                   parm                    b1enhe
     c                   parm                    p_kost
6.23 c                   parm                    p_orde
6.26 c                   parm                    w_kamp
      *
      * Henter total sum for bestilling
      *
     c                   exsr      hent_total
      *
     c     end_reg       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
      * Initierer SQL for søk
      *
     c                   if        b_open_sok = *on
      *
     c                   select
     c                   when      w_seqe = 'SO'
     c/exec sql
     c+                  declare sok_o cursor for
     c+                  select vvsfir,
     c+                         vvsogr,
     c+                         vvstek,
     c+                         vvsvar
     c+                  from   vvsopf
     c+                  where  vvsfir = :w_firm and
     c+                         vvsogr = :b2ogrp and
     c+                         vvstek like :w_ste1
     c+                  order by vvsfir, vvsogr, vvstek
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_o
     c/end-exec
     c/exec sql
     c+                  open sok_o
     c/end-exec
      *
     c                   when      w_seqe = 'SH'
     c/exec sql
     c+                  declare sok_h cursor for
     c+                  select vvsfir,
     c+                         vvsogr,
     c+                         vvshgr,
     c+                         vvstek,
     c+                         vvsvar
     c+                  from   vvsopf
     c+                  where  vvsfir = :w_firm and
     c+                         vvsogr = :b2ogrp and
     c+                         vvshgr = :b2hgrp and
     c+                         vvstek like :w_ste1
     c+                  order by vvsfir, vvsogr, vvshgr, vvstek
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_h
     c/end-exec
     c/exec sql
     c+                  open sok_h
     c/end-exec
      *
     c                   when      w_seqe = 'SU'
     c/exec sql
     c+                  declare sok_u cursor for
     c+                  select vvsfir,
     c+                         vvsogr,
     c+                         vvshgr,
     c+                         vvsugr,
     c+                         vvstek,
     c+                         vvsvar
     c+                  from   vvsopf
     c+                  where  vvsfir = :w_firm and
     c+                         vvsogr = :b2ogrp and
     c+                         vvshgr = :b2hgrp and
     c+                         vvsugr = :b2ugrp and
     c+                         vvstek like :w_ste1
     c+                  order by vvsfir, vvsogr, vvshgr, vvsugr, vvstek
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_u
     c/end-exec
     c/exec sql
     c+                  open sok_u
     c/end-exec
      *
     c                   when      w_seqe = 'S '
     c/exec sql
     c+                  declare sok cursor for
     c+                  select vvsfir,
     c+                         vvstek,
     c+                         vvsvar
     c+                  from   vvsopf
     c+                  where  vvsfir = :w_firm and
     c+                         vvstek like :w_ste1
     c+                  order by vvsfir, vvstek
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok
     c/end-exec
     c/exec sql
     c+                  open sok
     c/end-exec
      *
     c                   endsl
      *
     c                   endif
      *
      * Fyller opp subfile
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'O'
     c     vvarl6_key    reade     vvarl6                                 15
     c                   when      w_seqe = 'H'
     c     vvarl7_key    reade     vvarl7                                 15
     c                   when      w_seqe = 'U'
     c     vvarl8_key    reade     vvarl8                                 15
     c                   when      w_seqe = 'SO'
     c/exec sql
     c+                  fetch next
     c+                  from  sok_o
     c+                  into  :vvsfir,
     c+                        :vvsogr,
     c+                        :vvstek,
     c+                        :vvsvar
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'SH'
     c/exec sql
     c+                  fetch next
     c+                  from  sok_h
     c+                  into  :vvsfir,
     c+                        :vvsogr,
     c+                        :vvshgr,
     c+                        :vvstek,
     c+                        :vvsvar
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'SU'
     c/exec sql
     c+                  fetch next
     c+                  from  sok_u
     c+                  into  :vvsfir,
     c+                        :vvsogr,
     c+                        :vvshgr,
     c+                        :vvsugr,
     c+                        :vvstek,
     c+                        :vvsvar
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'S '
     c/exec sql
     c+                  fetch next
     c+                  from  sok
     c+                  into  :vvsfir,
     c+                        :vvstek,
     c+                        :vvsvar
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'V'
     c                   read      vvarl1                                 15
     c                   when      w_seqe = 'N'
     c                   read      vvarl3                                 15
     c                   when      w_seqe = 'L'
     c                   read      vvarl4                                 15
     c                   other
     c                   read      vvarl2                                 15
     c                   endsl
      *
     c                   if        *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        w_seqe = 'SO' or
     c                             w_seqe = 'SH' or
     c                             w_seqe = 'SU' or
     c                             w_seqe = 'S '
      *
     c                   if        w_ste2 <> *blank
      *
     c                   eval      w_posi = %scan(%trim(w_ste2):%trim(vvstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
      *
     c                   if        w_ste3 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste3):%trim(vvstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste4 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste4):%trim(vvstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste5 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste5):%trim(vvstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c                   eval      vvarlr_vare = vvsvar
     c     vvarlr_key    chain     vvarlr                             91
     c                   if        *in91 = *on
     c                   iter
     c                   endif
      *
     c                   endif
      *
     c                   if        vvfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
7.02   if        *in85 = *on;
7.02     // viser kun for valg ldor enten hovedldor eller lager ldor
7.02     exec sql
7.02       with v(ant) as (
7.02          select count(*) as ant
7.02          from vvarpf where vvfirm = :w_firm and
7.02          vvvare = :vvvare and
7.02          vvldor = :loldor
7.02       ),
7.02       l(ant) as (
7.02         select count(*) as ant
7.02         from vlagpf where vlfirm = :w_firm and
7.02         vllage = :lolage and
7.02         vlvare = :vvvare and
7.02         VLBLDO = :loldor
7.02       )
7.02       select (v.ant+l.ant) into :w_ant from v,l;
7.02     if (w_ant = 0);
7.02       iter;
7.02     endif;
7.02   endif;
      *
6.25  *   Hent div. info fra lager
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    lolage
5.70 c                   parm                    w_vtyp
6.25 c                   parm                    w_udat
6.25 c                   parm                    w_ldor
6.25 c                   parm                    b_inlr
6.32 c                   parm                    w_lv_nobb
6.32 c                   parm                    w_lv_modn
6.32 c                   parm                    w_lv_lldo
6.32 c                   parm                    w_lv_llva
      *
6.33  * sjekker om logistikk-vare med best.leverandør
6.33 c/exec sql
6.33 c+ select vvlnob,
6.33 c+        vvllva,
6.33 c+        vvlmod
6.33 c+ into   :w_lv_nobb,
6.33 c+        :w_lv_llva,
6.33 c+        :w_lv_modn
6.33 c+ from   vvlepf
6.33 c+ where  vvlvnr = :vvvare and
6.33 c+        vvlldo = :loldor
6.33 c+ fetch first 1 rows only
6.33 c/end-exec
      *
     c                   if        *in83 = *off and
5.70 c                             w_vtyp <> 'L'
     c                   iter
     c                   endif
      *
6.20 c                   if        w_udat <> *loval and
6.20 c                             w_udat <= w_ddat
     c                   iter
     c                   endif
6.27  *
6.27 c                   time                    w_d1at
6.27 c                   if        vvudat <> *loval
6.27 c                   if        w_udat >= w_d1at and
6.27 c                             vvudat <= w_d1at
6.27 c                   iter
6.27 c                   endif
6.27 c                   endif
6.27  *
      *
      * Skriver til subfile
      *
     c                   eval      w_srrn = w_srrn + 1
      *
6.21 c                   eval      b1sess = *blank
6.21 c                   eval      b1sent = *blank
6.21 c                   eval      b1stat = *blank
6.21 c                   exsr      sjekk_vsor
6.21 c                   if        b_sess = *on
6.21 c                   eval      b1sess = 'P'
6.21 c                   endif
6.22 c                   eval      *in50  = *off
6.21 c                   if        b_sent = *on
6.21 c                   eval      b1sent = 'C'
6.24 c                   if        faafsl = 1
6.22 c                   eval      *in50  = *on
6.24 c                   endif
6.21 c                   endif
     c                   eval      b1tek1 = vvtek1
7.03 c                   eval      w_text = %trim(vvtek1)+' : '+%trim(vvtek2)
7.03 c                   movel     w_text        b1text
7.03 c                   eval      b1vtyp = w_vtyp
     c                   eval      b1vare = vvvare
6.32 c                   if        w_lv_nobb > 0
6.32 c                   eval      b1nobb = w_lv_nobb
6.32 c                   eval      b1lvar = w_lv_llva
6.32 c                   else
     c                   eval      b1nobb = vvnobb
     c                   eval      b1lvar = vvlvar
6.32 c                   endif
6.21  * legger alle koder i et felt
6.21 c                   eval      %subst(b1stat:1:1) = b1sent
6.21 c                   eval      %subst(b1stat:2:1) = b1sess
6.21 c                   eval      %subst(b1stat:3:1) = b1vtyp
      *
     c                   eval      w_lety = 0
6.26 c                   eval      w_kamp = lokamp
      *
6.31 c                   call      'VP751R'
     c                   parm                    w_firm
     c                   parm                    vvvare
5.70 c                   parm                    w_prgr
     c                   parm                    w_lety
     c                   parm                    lobdat
     c                   parm                    b1enhe
     c                   parm                    w_kopr
     c                   parm                    b1inpr
6.26 c                   parm                    w_kamp
6.31 c                   parm                    loldor
6.31 c                   parm                    lolage
7.03  *
7.03  * Hent beholding, og min og max
7.03  *
7.03 c                   call      'VL717R'
7.03 c                   parm                    w_firm
7.03 c                   parm                    vvvare
7.03 c                   parm                    lolage
7.03 c                   parm                    b1enhe
7.03 c                   parm                    w_vtyp
7.03 c                   parm                    w_udat
7.03 c                   parm                    w_ldor
7.03 c                   parm                    w_behl
7.03 c                   parm                    w_bbes
7.03 c                   parm                    w_mini
7.03 c                   parm                    w_maks
7.03 c                   parm                    w_bmen
7.03 c                   parm                    w_bmin
7.03 c                   parm                    w_omre
7.03 c                   parm                    b_inlr
7.03 c                   if        w_mini > 99999
7.03 c                   eval      b1mini = 99999
7.03 c                   else
7.03 c                   eval      b1mini = w_mini
7.03 c                   endif
7.03 c                   if        w_maks > 99999
7.03 c                   eval      b1maks = 99999
7.03 c                   else
7.03 c                   eval      b1maks = w_maks
7.03 c                   endif
7.03 c                   if        w_behl > 99999
7.03 c                   eval      b1behl = 99999
7.04 c                   elseif    w_behl < -99999
7.04 c                   eval      b1behl = -99999
7.03 c                   else
7.03 c                   eval      b1behl = w_behl
7.03 c                   endif
7.03 c                   if        w_bbes > 99999
7.03 c                   eval      b1bbes = 99999
7.03 c                   else
7.03 c                   eval      b1bbes = w_bbes
7.03 c                   endif
7.03 c                   if        w_bmin > 99999
7.03 c                   eval      b1bmin = 99999
7.03 c                   else
7.03 c                   eval      b1bmin = w_bmin
7.03 c                   endif
      *
     c                   select
     c                   when      *in80 = *on
     c                   eval      b1nypo = vvvare
     c                   when      *in81 = *on
6.32 c                   if        w_lv_nobb > 0
6.32 c                   eval      b1nypo = %editc(w_lv_nobb : 'Z')
6.32 c                   else
     c                   eval      b1nypo = %editc(vvnobb : 'Z')
6.32 c                   endif
     c                   when      *in82 = *on
6.31 c*                  eval      b1nypo = vvlvar
6.32 c                   if        w_lv_nobb > 0
6.32 c                   movel     w_lv_llva     b1nypo
6.32 c                   else
6.31 c                   movel     vvlvar        b1nypo
6.32 c                   endif
     c                   endsl
      *
     c                   if        *in84 = *on
     c                   eval      b1inpr = b1inpr * w_mvat
     c                   endif
      *
6.32  * sjekk farge
6.32 c                   eval      w_jvqvnr = vvvare
6.32 c                   exsr      finn_logivar
      *
7.03 c                   exsr      newlo_vask
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   eval      b_open_sok = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        w_seqe = 'O'  or
     c                             w_seqe = 'H'  or
     c                             w_seqe = 'U'  or
     c                             w_seqe = 'SO' or
     c                             w_seqe = 'SH' or
     c                             w_seqe = 'SU' or
     c                             w_seqe = 'S '
     c                   exsr      forny
     c                   goto      end_bck
     c                   endif
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'V'
     c     vvarl1_key    setgt     vvarl1
     c                   readp     vvarl1
     c                   when      w_seqe = 'N'
     c     vvarl3_key    setgt     vvarl3
     c                   readp     vvarl3
     c                   when      w_seqe = 'L'
     c     vvarl4_key    setgt     vvarl4
     c                   readp     vvarl4
     c                   other
     c     vvarl2_key    setgt     vvarl2
     c                   readp     vvarl2
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'V'
     c                   readp     vvarl1
     c                   when      w_seqe = 'N'
     c                   readp     vvarl3
     c                   when      w_seqe = 'L'
     c                   readp     vvarl4
     c                   other
     c                   readp     vvarl2
     c                   endsl
      *
     c                   if        %eof or
     c                             vvfirm <> w_firm
     c                   leave
     c                   endif
6.10  *   Hent varetype fra subprogram
6.10 c                   call      'VL710R'
6.10 c                   parm                    w_firm
6.10 c                   parm                    vvvare
6.10 c                   parm                    lolage
6.10 c                   parm                    w_vtyp
6.25 c                   parm                    w_udat
6.25 c                   parm                    w_ldor
6.25 c                   parm                    b_inlr
6.32 c                   parm                    w_lv_nobb
6.32 c                   parm                    w_lv_modn
6.32 c                   parm                    w_lv_lldo
6.32 c                   parm                    w_lv_llva
      *
6.10 c                   if        *in83 = *off and
6.10 c                             w_vtyp <> 'L'
6.10 c                   iter
6.10 c                   endif
      *
6.20 c                   if        w_udat <> *loval and
6.20 c                             w_udat <= w_ddat
     c                   iter
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'V'
     c                   eval      vvarl1_vare = vvvare
     c                   when      w_seqe = 'N'
6.32 c                   if        w_lv_nobb > 0
6.32 c                   eval      vvarl3_nobb = w_lv_nobb
6.32 c                   else
     c                   eval      vvarl3_nobb = vvnobb
6.32 c                   endif
     c                   when      w_seqe = 'L'
6.32 c                   if        w_lv_nobb > 0
6.32 c                   eval      vvarl4_lvar = w_lv_llva
6.32 c                   else
     c                   eval      vvarl4_lvar = vvlvar
6.32 c                   endif
     c                   other
     c                   eval      vvarl2_tek1 = vvtek1
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'V'
     c     vvarl1_key    setll     vvarl1
     c                   when      w_seqe = 'N'
     c     vvarl3_key    setll     vvarl3
     c                   when      w_seqe = 'L'
     c     vvarl4_key    setll     vvarl4
     c                   other
     c     vvarl2_key    setll     vvarl2
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_bck       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Overgruppe
      *
     c                   if        w_afld = 'B2OGRP'
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    w_otxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_afld = 'B2HGRP'
     c                   eval      b2ugrp = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    w_htxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_afld = 'B2UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    b2ugrp
     c                   parm                    w_utxt
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av bestillingstotal
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_total    begsr
      *
     c                   eval      w_toti = 0
     c                   eval      w_totr = 0
      *
     c     lodtl1_key    setll     lodtl1
     c     lodtl1_key    reade     lodtl1
     c                   dow       not %eof
     c                   eval      w_toti = w_toti + ldsumi
     c                   eval      w_totr = w_totr + ldsumr
     c     lodtl1_key    reade     lodtl1
     c                   enddo
      *
     c                   eval      b2toti = w_toti - w_totr
      *
     c                   endsr
6.21  *
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  * Subrutine for å sjekke om varen er i sortiment for C - sentrallagervarer
6.21  *                           eller i et sortiment for P - periodestyrt varer
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  *
6.21 c     sjekk_vsor    begsr
6.21  *
6.21 c                   eval      b_sent = *off
6.21 c                   eval      b_sess = *off
6.21  * sjekker sentral-lager-vare
6.21  *
6.21 c                   call      'VD771R'
6.21 C                   parm                    w_firm
6.21 C                   parm                    lolage
6.21 C                   parm                    vvvare
6.21 C                   parm                    b_sent
6.21 C                   parm                    b_inlr
6.21  * sjekker periodestyrt
6.21  *
6.21 c                   call      'VD772R'
6.21 C                   parm                    w_firm
6.21 C                   parm                    lolage
6.21 C                   parm                    vvvare
6.21 C                   parm                    b_sess
6.21 C                   parm                    b_inlr
6.21  *
6.21 c                   endsr
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * Finn info om logistikkvare for riktig prisgiver
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     finn_logivar  begsr
6.32  *
6.32 c                   eval      *in51 = *off
6.32 c                   eval      *in52 = *off
6.32 c
6.32 c                   if        %trim(w_jvqvnr) = ''
6.32 c                   leavesr
6.32 c                   endif
6.32 c
6.32 c                   eval      w_logl = ''
6.32 c                   eval      w_undv = 0
     c                   movel     w_jvqvnr      w_jvquno
6.32 c/exec sql
6.32 c+                  select jvquno, jvqvnr
6.32 c+                  into :w_undv, :w_logl
6.32 c+                  from jvklpf
6.32 c+                  where (jvquno = :w_jvquno or
6.32 c+                  jvqvnr = :w_jvqvnr)
6.32 c+                  and jvqfir = :w_firm
6.32 c+                  fetch first 1 rows only
6.32 c/end-exec
6.32 c                   if        w_logl = w_jvqvnr
6.32 c                   eval      *in52 = *on
6.32 c                   elseif    w_undv > 0
6.32 c                   eval      *in51 = *on
6.32 c                   endif
6.32 c
6.32 c                   endsr
7.03  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.03  * Newlook vask av data
7.03  *  Fjerner tegn som gjør at Newlook feiltolker
7.03  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.03  *
7.03   begsr newlo_vask;
7.03     for w_teller = 1 to 5 ;
7.03       w_len = %len(%trim(b1text));
7.03       if (w_len > 0);
7.03       b1text = %ScanRpl('.':'':b1text:w_len); // newlook tåler ikke . på slutten
7.03       endif;
7.03       w_len = %len(%trim(b1text));
7.03       if (w_len > 0);
7.03       b1text = %ScanRpl(':':'':b1text:w_len); // newlook tåler ikke : på slutten
7.03       endif;
7.03     endfor ;
7.03   endsr;
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_kode
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_line
3.31 c                   parm                    p_kost
6.23 c                   parm                    p_orde
7.01 c                   parm                    p_vare
7.01  *
7.01 c                   eval      w_varesok = p_vare
      *
      * Key for oppslag på vare
      *
     c     vvarlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlr_vare
      *
      * Key for posisjonering på varenummer
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for posisjonering på varetekst
      *
     c     vvarl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl2_tek1
      *
      * Key for posisjonering på nobb-nummer
      *
     c     vvarl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl3_nobb
      *
      * Key for posisjonering på leverandørs varenummer
      *
     c     vvarl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl4_lvar
      *
      * Key for søk på overgruppe
      *
     c     vvarl6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl6_ogrp
      *
      * Key for søk på overgruppe/hovedgruppe
      *
     c     vvarl7_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl7_ogrp
     c                   kfld                    vvarl7_hgrp
      *
      * Key for søk på overgruppe/hovedgruppe/undergruppe
      *
     c     vvarl8_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl8_ogrp
     c                   kfld                    vvarl8_hgrp
     c                   kfld                    vvarl8_ugrp
      *
      * Key for oppslag på status-register
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på bruker-register
      *
     c     lusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lusrl1_user
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på bestilling-hode
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for les på bestilling-linje
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
6.24  *
6.24  * Key for oppslag på status-register
6.24  *
6.24 c     fstsl1_key    klist
6.24 c                   kfld                    w_firm
      *
      *
      * Henter parametre
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_numm = p_numm
     c                   eval      w_suff = p_suff
     c                   eval      w_line = p_line
      *
      * Henter dagens dato
      *
6.20 c                   time                    w_ddat
      *
      * Hent opplysninger fra koderegister
      *
     c     lstsl1_key    chain     lstsl1r                            90
6.24  *
6.24 c     fstsl1_key    chain     fstsl1r                            90
      *
      * Henter opplysninger fra bruker-register
      *
     c                   eval      lusrl1_user = l_user
     c     lusrl1_key    chain     lusrl1                             90
      *
      * Hent opplysninger fra bestilling-hode-register
      *
     c     lohel1_key    chain     lohel1r                            90
      *
      * Finner moms-sats
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    lobdat
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
5.70  *
5.70  * Hent prisgruppe
5.70  *
5.70 c                   eval      w_avde = 0
5.70 c                   call      'VL712R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    lolage
5.70 c                   parm                    w_avde
5.70 c                   parm                    w_prgr
      *
      * Henter total sum for bestilling
      *
     c                   exsr      hent_total
      *
      * Fyller opp skjermbildet for første visning
      *
     c                   eval      rlevl1_levr = loldor
     c     rlevl1_key    chain     rlevl1                             90
     c                   eval      b2ldor = loldor
     c                   eval      b2lnav = rlnavn
      *
     c                   eval      *in22 = *on
     c                   eval      *in80 = *on
      *
     c     vvarl2_key    setll     vvarl2
      *
      * default filter på valgt leverandør
7.02 c                   eval      *in85 = not *in85
      *
7.01 c*                   exsr      crt_subfile
7.01 c                   if        w_varesok = *blanks
7.01 c                   exsr      crt_subfile
7.01 c                   else
7.01 c                   eval      b_dirs = *on
7.01 c                   eval      b2stek = w_varesok
7.01 c                   exsr      sØk
7.01 c                   endif
      *
     c                   endsr
**     M e l d i n g e r
Kostpris er endret på salgsordren                 <---
