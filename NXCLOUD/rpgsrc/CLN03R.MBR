     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : Ehandel
      * Program . . . . : CLN03R
      * beskrivelse . . : Vedlikehold av hovedleverandører
      *
      * Spesialversjon. :
      * Tilpasset for . : Mestergruppen
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       130916 ask  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fclnli1    if   e           k disk    rename(clnlst:clnli1r)
     fclnlir    if   e           k disk    rename(clnlst:clnlirr)
     fclnliu    uf a e           k disk    rename(clnlst:clnliur)
     fclnkir    if   e           k disk    rename(clnkst:clnkirr)
     frlevlr    if   e           k disk    rename(rlevpfr:rlevlrr)
      *
     fcln03d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d clnlir_nlka     s                   like(clnlka)
     d clnlir_nllv     s                   like(clnllv)
     d clnliu_nlka     s                   like(clnlka)
     d clnliu_nllv     s                   like(clnllv)
     d clnli1_nlka     s                   like(clnlka)
     d clnli1_nllv     s                   like(clnllv)
     d clnkir_nkka     s                   like(clnkka)
     d rlevlr_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_kabc          s              1    inz(*off)
      *
     d w_firm          s                   like(clnlfi)
6.30 d w_udate         s               d   datfmt(*iso)
6.34 d w_stat          s              1
6.34 d w_disp          s              1
6.34 d w_frad          s               d   datfmt(*iso)
6.34 d w_tild          s               d   datfmt(*iso)
     d w_nlka          s                   like(clnlka)
     d w_nlkaa         s              4
     d w_nllv          s                   like(clnllv)
     d w_nntx          s                   like(clnktx)
     d w_lnav          s                   like(rlnavn)
     d w_nktx          s                   like(clnktx)
      *
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      sok_kat
     c                   eval      b2nlka = w_nlka
     c                   goto      pos
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *inkh
     c                   exsr      nobbkat
     c                   goto      b2taga
     c                   when      *inki
     c                   exsr      nobbgrp
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c     pos           tag
     c                   if        b2nlka <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      clnli1_nlka = b1nlka
     c     clnli1_key    setll     clnli1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Nobbkategori
      *
     c                   if        b2nlka <> 0
     c                   eval      clnli1_nlka = b2nlka
     c     clnli1_key    setll     clnli1
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2nlka = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c2nlka = b1nlka
     c                   eval      c2nllv = b1nllv
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1nlka = b1nlka
     c                   eval      d1nllv = b1nllv
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c2nlka = b1nlka
     c                   eval      c2nllv = b1nllv
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1nlka = 0
     c                   eval      c1nntx = *blank
     c                   eval      c1nllv = 0
     c                   eval      c1nltx = *blank
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Søking
      *
     c                   if        *inkd = *on
     c                   if        w_afld = 'C1NLKA'
     c                   exsr      sok_kat
     c                   eval      c1nlka = w_nlka
     c                   eval      c1nntx = w_nktx
     c                   goto      c1taga
     c                   endif
      *
     c                   if        w_afld = 'C1NLLV'
     c                   exsr      sok_lev
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
     c                   if        c1nlka = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      clnlir_nlka = c1nlka
     c                   eval      clnlir_nllv = c1nllv
     c     clnlir_key    chain     clnlir
     c                   if        %found
     c                   exsr      xc2msg
     c                   goto      c1taga
     c                   endif
      *
      * Sjekk om leverandør er koblet til hovedkategori
      *
     c                   eval      w_nlka = c1nlka
     c                   exsr      sjekk_hkat
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c2nlka = c1nlka
     c                   eval      c2nllv = c1nllv
     c                   exsr      xc2bld
      *
     c                   eval      c1nlka = 0
     c                   eval      c1nntx = *blank
     c                   eval      c1nllv = 0
     c                   eval      c1nltx = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2nlka = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2msg        begsr
      *
     c                   exfmt     c2msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      clnlir_nlka = c2nlka
     c                   eval      clnlir_nllv = c2nllv
     c     clnlir_key    chain     clnlir
      *
     c                   if        %found
     c     *dmy          move      clnlod        c2eoda
     c     *dmy          move      clnled        c2eeda
     c                   eval      c2eeus = clnleu
6.34 c                   endif
      *
     c                   eval      w_nlka = c2nlka
     c                   exsr      finn_kat
     c                   eval      c2nntx = w_nntx
      *
     c                   eval      w_nllv = c2nllv
     c                   exsr      finn_lev
     c                   eval      c2nltx = w_lnav
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   clnliur
      *
     c                   eval      clnliu_nlka = c2nlka
     c                   eval      clnliu_nllv = c2nllv
     c     clnliu_key    chain     clnliur
      *
     c                   time                    clnled
     c                   time                    clnlet
     c                   eval      clnleu = l_user
      *
     c                   if        %found
     c                   update    clnliur
     c                   else
     c                   eval      clnlfi = w_firm
     c                   eval      clnlka = c2nlka
     c                   eval      clnllv = c2nllv
     c                   time                    clnlod
6.10 c                   time                    clnlet
6.10 c                   eval      clnleu = l_user
     c                   write     clnliur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1nlka = clnlka
     c*                  eval      b1nntx = clnltx
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      clnlir_nlka = d1nlka
     c                   eval      clnlir_nllv = d1nllv
     c     clnlir_key    chain     clnlir
      *
     c                   eval      w_nlka = d1nlka
     c                   exsr      finn_kat
     c                   eval      d1nntx = w_nntx
      *
     c                   eval      w_nllv = d1nllv
     c                   exsr      finn_lev
     c                   eval      d1nltx = w_lnav
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      clnliu_nlka = d1nlka
     c                   eval      clnliu_nllv = d1nllv
     c     clnliu_key    chain     clnliur
      *
     c                   delete    clnliur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      clnli1
      *
     c                   if        %eof or
     c                             clnlfi <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      clnli1_nlka = clnlka
      *
     c                   eval      b1valg = 0
     c                   eval      b1nlka = clnlka
     c                   eval      b1nllv = clnllv
      *
     c                   eval      w_nlka = b1nlka
     c                   exsr      finn_kat
     c                   eval      b1nntx = w_nntx
      *
     c                   eval      w_nllv = b1nllv
     c                   exsr      finn_lev
     c                   eval      b1nltx = w_lnav
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     clnli1_key    setgt     clnli1
     c                   readp     clnli1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     clnli1
      *
     c                   if        %eof or
     c                             clnlfi <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      clnli1_nlka = clnlka
      *
     c                   enddo
      *
     c                   if        %eof
     c     clnli1_key    setll     clnli1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke om leverandør er på hovedkategori
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_hkat    begsr
      *
     c                   eval      w_nlkaa = %char(c1nlka)
     c                   if        %subst(w_nlkaa:3:2) = '00'
     c                   goto      end_hkat
     c                   else
     c                   eval      w_nlkaa = %subst(w_nlkaa:1:2) + '00'
     c                   eval      w_nlka = %dec(w_nlkaa:4:0)
     c                   endif
      *
     c                   eval      clnliu_nlka = w_nlka
     c                   eval      clnliu_nllv = c1nllv
     c     clnliu_key    chain     clnliur
      *
     c                   if        not %found
     c                   clear                   clnliur
     c                   eval      clnleu = l_user
     c                   eval      clnlfi = w_firm
     c                   eval      clnlka = w_nlka
     c                   eval      clnllv = c1nllv
     c                   time                    clnlod
     c                   time                    clnled
     c                   time                    clnlet
6.10 c                   time                    clnlet
6.10 c                   eval      clnleu = l_user
     c                   write     clnliur
     c                   endif
      *
     c     end_hkat      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne kategoritekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_kat      begsr
      *
     c                   eval      clnkir_nkka = w_nlka
     c     clnkir_key    chain     clnkir
     c                   if        %found
     c                   eval      w_nntx = clnktx
     c                   else
     c                   eval      w_nntx = *blank
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne leverandørtekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_lev      begsr
      *
     c                   eval      rlevlr_levr = w_nllv
     c     rlevlr_key    chain     rlevlr
     c                   if        %found
     c                   eval      w_lnav = rlnavn
     c                   else
     c                   eval      w_lnav = *blank
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å søke kategoritekst
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sok_kat       begsr
      *
     c                   call      'CLN11R'
     c                   parm                    w_firm
     c                   parm                    w_nlka
     c                   parm                    w_nktx
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å søke på leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sok_lev       begsr
      *
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    c1nllv
     c                   parm                    c1nltx
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av nobb kategorier
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     nobbkat       begsr
      *
     c                   call      'CLN01R'
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av varegruppe koblinger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     nobbgrp       begsr
      *
     c                   call      'CLN02R'
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for les på hovedleverandør
      *
     c     clnlir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    clnlir_nlka
     c                   kfld                    clnlir_nllv
      *
      * Key for oppdatering av hovedleverandør
      *
     c     clnliu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    clnliu_nlka
     c                   kfld                    clnliu_nllv
      *
      * Key for posisjonering på hovedleverandør
      *
     c     clnli1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    clnli1_nlka
     c                   kfld                    clnli1_nllv
      *
      * Key for les på kjedekategori
      *
     c     clnkir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    clnkir_nkka
      *
      * Key for posisjonering på leverandør
      *
     c     rlevlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevlr_levr
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
6.30 c                   time                    w_udate
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      clnli1_nlka = 0
     c     clnli1_key    setll     clnli1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
