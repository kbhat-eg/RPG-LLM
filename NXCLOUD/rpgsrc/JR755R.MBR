     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JR755R
      * Beskrivelse . . : Leser fil på recordformat CHRIST (Christ
      *                   Engebretsen) og oppretter/oppdaterer priser
      *                   på eksisterende varer i LVR.
      *
      *                   Dersom en ny vare matcher mot EAN-nummer i LVR,
      *                   opprettes prisen som prisgivende leverandør på
      *                   varen.
      *
      *                   Prisoppdatering skjer på bakgrunn av match på
      *                   leverandørs varenummer.
      *
      *               NB! Behandler kun varer fra lager HE1
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       111099 HKO  Nytt program
      *       270300 HKO  Lagt ny logikk for oppdatering av priser
      *       211102 HKO  Skrevet om logikk til også å opprette pris på
      *                   nye varer på bakgrunn av EAN-nummer
      *       021202 HKO  Behandler kun lager HE1
      *       140905 HKO  Kaller program for oppdatering av søketekst
6.31  * 6.30  110714 bof  endret nøkkel i varepris - dato + lvar til jvlepf
6.32  * 6.30  180515 bkv  Endret LVAR pga økt fra 15 til 20 tegn
6.33  * 6.30  120815 bof  Hvis pakn.mangler for prisg.,dann ut fra vareeier(6.31 i BMP)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjrvapf    if   e           k disk
      *
     fjrapl1    if   e           k disk    rename(jrappfr:jrapl1r)
6.31 fjvlel2    if   e           k disk    rename(jvlepfr:jvlel2r)
     fjvarld    if   e           k disk    rename(jvarpfr:jvarldr)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fjvprlu    uf a e           k disk    rename(jvprpfr:jvprlur)
6.31 fjvlelu    uf a e           k disk    rename(jvlepfr:jvlelur)
6.33 fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
6.33 fjvpkl2    uf   e           k disk    rename(jvpkpfr:jvpkl2r)
6.33 fjvpklu    uf a e           k disk    rename(jvpkpfr:jvpklur)
      *
      * Definering av parametre
      *
     d p_list          s             32
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av parameterliste -inn
      *
     d                 ds
     d d_list                        32
     d  d_firm                        3  0 overlay(d_list)
     d  d_rapp                        2  0 overlay(d_list:4)
     d  d_dato                         d   overlay(d_list:6) datfmt(*iso)
      *
      * Definering av recordbeskrivelse - CHRIST
      *
     d                 ds
     d d_inpu                       165
     d  d_pdat                        6  0 overlay(d_inpu:1)                    Dato fra grossist
     d  d_udat                        6  0 overlay(d_inpu:7)                    Utgått dato vare
     d**d_vare                       13    overlay(d_inpu:13)                   Artikkelnr
     d  d_vare                        6    overlay(d_inpu:20)                   Artikkelnr
     d  d_evar                       13    overlay(d_inpu:26)                   Erstanings varenr
     d  d_prod                        5    overlay(d_inpu:39)                   Produsentnr
     d  d_pvar                       13    overlay(d_inpu:44)                   Prod. varenr
     d  d_eann                       13  0 overlay(d_inpu:57)                   EAN-nr
     d  d_hgrp                        2    overlay(d_inpu:70)                   Hovedgruppe
     d  d_ugrp                        3    overlay(d_inpu:72)                   Undergruppe
     d  d_agrp                        3    overlay(d_inpu:75)                   Artikkelgruppe
     d  d_tek1                       30    overlay(d_inpu:78)                   Benevnelse
     d  d_stor                        8    overlay(d_inpu:108)                  Størrelse
     d  d_enhe                        5    overlay(d_inpu:116)                  Enhet
     d  d_anta                        9  3 overlay(d_inpu:121)                  Antall i pakn.
     d  d_rkod                        4    overlay(d_inpu:130)                  Rabattkode
     d  d_inpr                        9  2 overlay(d_inpu:134)                  Innkjøpspris
     d  d_grpr                        9  2 overlay(d_inpu:143)                  Grunnpris
     d  d_sapr                        9  2 overlay(d_inpu:152)                  Salgspris
     d  d_stje                        1    overlay(d_inpu:161)                  Stjernemerking
     d  d_anbr                        1    overlay(d_inpu:162)                  Anbrekkstillegg
     d  d_lagr                        3    overlay(d_inpu:163)                  Lagersted
      *
      * Definering av variable i nøkkler
      *
     d jrapl1_rapp     s                   like(jrrapp)
6.31 d jvlel2_lldo     s                   like(jvlldo)
6.31 d jvlel2_llva     s                   like(jvllva)
     d jvarld_ean1     s                   like(jvean1)
     d jlevl1_ldor     s                   like(jlldor)
     d jvprlu_vare     s                   like(jxvare)
     d jvprlu_ldor     s                   like(jxldor)
6.31 d jvlelu_lvnr     s                   like(jvlvnr)
6.31 d jvlelu_lldo     s                   like(jvlldo)
6.33 d jvarl1_vare     s                   like(jvvare)
6.33 d jvpkl2_vare     s                   like(jwvare)
6.33 d jvpkl2_ldor     s                   like(jwldor)
      *
      * Definering av variable
      *
     d b_inlr          s              3    inz(*off)
      *
     d w_firm          s              3  0
      *
      * Definering av konstanter
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter rapportør-informasjon
      *
     c                   eval      jrapl1_rapp = d_rapp
     c     jrapl1_key    chain     jrapl1                             90
     c                   if        *in90 = *on
     c                   goto      avslutt
     c                   endif
      *
      * Henter leverandørinformasjon
      *
     c                   eval      jlevl1_ldor = jrldor
     c     jlevl1_key    chain     jlevl1                             90
      *
     c                   eval      jlnavn = %trim(jlnavn)
     c     Lo:Up         xlate     jlnavn        jlnavn
      *
      * Behandling
      *
     c                   exsr      behandling
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av varer/priser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   read      jrvapf                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      d_inpu = jrvrad
      *
     c                   if        d_lagr <> 'HE1' or
     c                             d_vare = *blank or
     c                             d_vare = '000000'
     c                   goto      les_neste
     c                   endif
      *
      * Behandling avhengig om prisen på varen finnes fra før
      *
     c                   eval      jxvare = *blank
      *
6.31 c                   eval      jvlel2_lldo = jrldor
6.31 c                   eval      jvlel2_llva = d_vare
6.31 c     jvlel2_key    chain     jvlel2                             91
     c                   if        *in91 = *off
     c                   exsr      endr_pris
     c                   else
     c                   exsr      oppr_pris
     c                   endif
6.33  *
6.33  * sjekker om pakning finnes, hvis ikke dann
6.33  *
6.33 c                   exsr      sjekk_pakn
      *
      * Oppdaterer søketekst
      *
     c                   if        jxvare <> *blank
     c                   eval      b_inlr = *on
     c                   call      'JV790R'
     c                   parm                    w_firm
     c                   parm                    jxvare
     c                   parm                    b_inlr
     c                   endif
      *
     c     les_neste     tag
      *
     c                   read      jrvapf                                 90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av ny vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppr_pris     begsr
      *
      * Leter etter vare med samme EAN-nummer
      *
     c                   if        d_eann = 0
     c                   goto      end_oppr
     c                   endif
      *
     c                   eval      jvarld_ean1 = d_eann
     c     jvarld_key    chain     jvarld                             91
     c                   if        *in91 = *on
     c                   goto      end_oppr
     c                   endif
      *
      * Sletter eventuelt eksisterende priser for leverandør på varen
      *
     c                   eval      jvprlu_vare = jvvare
     c                   eval      jvprlu_ldor = jrldor
     c     jvprlu_key    setll     jvprlu
     c     jvprlu_key    reade     jvprlur                                92
     c                   dow       *in92 = *off
     c                   delete    jvprlur
     c     jvprlu_key    reade     jvprlur                                92
     c                   enddo
      *
      * Oppretter pris på vare for leverandør
      *
     c                   clear                   jvprlur
      *
     c                   eval      jxvare = jvvare
     c                   eval      jxldor = jrldor
6.32 c*                  eval      jxlvar = d_vare
6.32 c                   movel     d_vare        jxlvar
     c                   eval      jxpris = d_inpr
     c                   eval      jxvalu = *blank
     c                   eval      jxgdat = d_dato
     c                   eval      jxtdat = *loval
     c                   eval      jxrsts = 1
     c                   eval      jxeusr = l_user
      *
     c                   time                    jxodat
     c                   time                    jxedat
     c                   time                    jxetim
      *
     c                   write     jvprlur
6.31  *
6.31  * Sletter eventuelt vare-leverandør
6.31  *
6.31 c                   eval      jvlelu_lvnr = jvvare
6.31 c                   eval      jvlelu_lldo = jrldor
6.31 c     jvlelu_key    setll     jvlelu
6.31 c     jvlelu_key    reade     jvlelur
6.31 c                   dow       not %eof
6.31 c                   delete    jvlelur
6.31 c     jvlelu_key    reade     jvlelur
6.31 c                   enddo
6.31  *
6.31  * Oppretter vare leverandør
6.31  *
6.31 c                   clear                   jvlelur
6.31  *
6.32 c*                  eval      jvlvar = jvvare
6.32 c                   movel     jvvare        jvlvar
6.31 c                   eval      jvlldo = jrldor
6.31 c                   eval      jvllva = d_vare
6.31 c                   eval      jvleus = l_user
6.31 c                   eval      jvlous = l_user
6.31  *
6.31 c                   time                    jvloda
6.31 c                   time                    jvleda
6.31 c                   time                    jvloti
6.31 c                   time                    jvleti
6.31 c                   write     jvlelur
      *
     c     end_oppr      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for endring av pris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     endr_pris     begsr
      *
     c                   if        d_inpr = jxpris
     c                   goto      end_endr
     c                   endif
      *
     c                   eval      jvprlu_vare = jxvare
     c                   eval      jvprlu_ldor = jrldor
     c     jvprlu_key    chain     jvprlur                            92
      *
6.32 c*                  eval      jxlvar = d_vare
6.32 c                   movel     d_vare        jxlvar
     c                   eval      jxpris = d_inpr
     c                   eval      jxgdat = d_dato
     c                   eval      jxtdat = *loval
     c                   eval      jxeusr = l_user
      *
     c                   time                    jxedat
     c                   time                    jxetim
      *
     c                   update    jvprlur
      *
     c     end_endr      endsr
6.33  *
6.33  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.33  * Subrutine for å evt. å danne pakning på prisgiver.
6.33  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.33  *
6.33 c     sjekk_pakn    begsr
6.33 c                   eval      jvarl1_vare = jxvare
6.23 c     jvarl1_key    chain     jvarl1
6.23 c                   if        not %found
6.33 c                   leavesr
6.33 c                   endif
6.33  *
6.33  * leser vare-pakning for å se at den finnes
6.33  *
6.33 c                   eval      jvpkl2_vare = jvvare
6.33 c                   eval      jvpkl2_ldor = jrldor
6.33 c     jvpkl2_key2   chain     jvpkl2
6.33 c                   if        %found
6.33 c                   leavesr
6.33 c                   endif
6.33  * leser vareeiers pakning
6.33 c                   eval      jvpkl2_vare = jvvare
6.33 c                   eval      jvpkl2_ldor = jvldor
6.33 c     jvpkl2_key2   setll     jvpkl2
6.33 c     jvpkl2_key2   reade     jvpkl2
6.33 c                   dow       not %eof(jvpkl2)
6.33 c                   eval      jwldor = jrldor
6.33 c                   eval      jwfdat = *loval
6.33 c                   eval      jwtdat = *loval
6.33 c                   time                    jwodat
6.33 c                   eval      jweusr  = l_user
6.33 c                   time                    jwedat
6.33 c                   time                    jwetim
6.33 c                   write     jvpklur
6.33  *
6.33 c     jvpkl2_key2   reade     jvpkl2
6.33 c                   enddo
6.33  *
6.33 C                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for oppslag på rapportør
      *
     c     jrapl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jrapl1_rapp
      *
      * Key for oppdatering av vare-pris
      *
     c     jvprlu_key    klist
     c                   kfld                    jvprlu_vare
     c                   kfld                    jvprlu_ldor
6.31  *
6.31  * Key for oppslag på vare-leverandør
6.31  *
6.31 c     jvlel2_key    klist
6.31 c                   kfld                    jvlel2_lldo
6.31 c                   kfld                    jvlel2_llva
6.31  *
6.31  * Key for oppdatering av vare-pris
6.31  *
6.31 c     jvlelu_key    klist
6.31 c                   kfld                    jvlelu_lvnr
6.31 c                   kfld                    jvlelu_lldo
      *
      * Key for oppslag på vare med EAN-nummer
      *
     c     jvarld_key    klist
     c                   kfld                    jvarld_ean1
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
6.33  *
6.33 c     jvarl1_key    klist
6.33 c                   kfld                    jvarl1_vare
6.33  *
6.33  * Key for les av vare-pakning
6.33  *
6.33 c     jvpkl2_key2   klist
6.33 c                   kfld                    jvpkl2_vare
6.33 c                   kfld                    jvpkl2_ldor
      *
      *
      * Fyller datastruktur fra parameter, og henter firma
      *
     c                   eval      d_list = p_list
     c                   eval      w_firm = d_firm
      *
     c                   endsr
