     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK789R                                              *
      * Beskrivelse . . : Avstemming kunder ==> transaksjoner                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      *           070703  Lagt inn avstemming mot hovedbok                    *
T5.30 * R5.30     050804  Saldo hovedbok kunne bli feil.                 HFL  *
T5.30 * R5.40     030406  Økt antall siffer i antall posteringer         KOB  *
T5.30 * R5.41     260606  Lagt inn reade av saldomatrise for å få med    KOB  *
T5.30 *                   alle konti ved kategorispesifikasjon           KOB  *
T5.30 * R5.41     260606  Lagt inn summering av hele saldomatrisen (1-13)KOB  *
6.30  *  6.30     150814  Innført mulighet for arkivering og PDF   Bsa  *
6.31  * R6.30 180816 bsa  Rokkert på rekkefølge av close printerfile
6.32  *       281117 nho  Saldo hovedbok skrev dobbel sum                     *
6.33  * V6.30 040719 bof  Utvidet saldofelter                            BSA
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frktrl1    if   e           k disk    rename(rktrpfr:rktrl1r)
     fra04l1    if   e           k disk    rename(ra04pfr:ra04l1r)
     fra11l1    if   e           k disk    rename(ra11pfr:ra11l1r)
     frhsal1    if   e           k disk    rename(rhsapfr:rhsal1r)
     frhsalr    if   e           k disk    rename(rhsapfr:rhsalrr)
     frk789p    o    e             printer oflind(*in30)
6.30 f                                     usropn
      *
      * Definering av Local data area
      *
     d                uds
6.30 d l_poff                584    584
6.30 d l_utqm                585    594
6.30 d l_bach                595    635
6.30 d l_arch                636    636  0
6.30 d l_skje                637    637  0
6.30 d l_land                638    638  0
6.30 d l_exlp                639    639  0
6.30 d l_mail                640    640  0
6.30 d l_ruti                800    809
6.30 d l_outq                810    819
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d w_firm          s                   like(rkfirm)
     d rktrl1_kund     s                   like(rnkund)
      *
     d rhsal1_kont     s                   like(rikont)
     d rhsal1_avde     s                   like(riavde)
     d rhsal1_spes     s                   like(rispes)
     d rhsal1_raar     s                   like(riraar)
     d rhsal1_type     s                   like(ritype)
     d rhsalr_kont     s                   like(rikont)
      *
     d n               s              2  0
6.33 d sal             s             15  2 dim(13)
6.30 d p_btyp          s            100
6.30 d w_year          s              4  0
6.30 d w_dato          s               d   datfmt(*iso)
      *
6.30 d splfname2       s             10a
6.30 d jobname2        s             10a
6.30 d username2       s             10a
6.30 d jobnbr2         s              6a
6.30 d splfnbr2        s             10i 0
6.30 d splfname3       s             10a
6.30 d jobname3        s             10a
6.30 d username3       s             10a
6.30 d jobnbr3         s              6a
6.30 d splfnbr3        s             10i 0
6.30 d p_tagg0         s             20
6.30 d p_tagg0val      s             50
6.30 d p_tagg1         s             20
6.30 d p_tagg1val      s             50
6.30 d p_tagg2         s             20
6.30 d p_tagg2val      s             50
6.30 d p_tagg3         s             20
6.30 d p_tagg3val      s             50
6.30 d p_tagg4         s             20
6.30 d p_tagg4val      s             50
6.30 d p_tagg5         s             20
6.30 d p_tagg5val      s             50
6.30 d p_tagg6         s             20
6.30 d p_tagg6val      s             50
6.30 d p_tagg7         s             20
6.30 d p_tagg7val      s             50
6.30 d p_tagg8         s             20
6.30 d p_tagg8val      s             50
6.30 d p_tagg9         s             20
6.30 d p_tagg9val      s             50
6.30 d p_refn          s             50
6.30 d p_dspl          s            100
      *
6.30 d qsprilsp        pr                  extpgm('QSPRILSP')
6.30 d rcvvar                     32767a   options(*varsize)
6.30 d rcvvarlen                     10i 0 const
6.30 d format                         8a   const
6.30 d errorcode                  32767a   options(*varsize)
      *
6.30 d sprl0100        ds
6.30 d  bytesrtn                     10i 0
6.30 d  bytesavl                     10i 0
6.30 d  splfname                     10a
6.30 d  jobname                      10a
6.30 d  username                     10a
6.30 d  jobnbr                        6a
6.30 d  splfnbr                      10i 0
6.30 d  systemname                    8a
6.30 d  createdate                    7a
6.30 d                                1a
6.30 d  createtime                    6a
      *
6.30 d errorcode       ds
6.30 d  bytesprov                    10i 0 inz(0)
6.30 d  byteavail                    10i 0 inz(0)
      *
     irhsal1r
      *  Saldo-register
     i              risa01                      sal(01)
     i              risa02                      sal(02)
     i              risa03                      sal(03)
     i              risa04                      sal(04)
     i              risa05                      sal(05)
     i              risa06                      sal(06)
     i              risa07                      sal(07)
     i              risa08                      sal(08)
     i              risa09                      sal(09)
     i              risa10                      sal(10)
     i              risa11                      sal(11)
     i              risa12                      sal(12)
     i              risa13                      sal(13)
      *
     irhsalrr
      *  Saldo-register
     i              risa01                      sal(01)
     i              risa02                      sal(02)
     i              risa03                      sal(03)
     i              risa04                      sal(04)
     i              risa05                      sal(05)
     i              risa06                      sal(06)
     i              risa07                      sal(07)
     i              risa08                      sal(08)
     i              risa09                      sal(09)
     i              risa10                      sal(10)
     i              risa11                      sal(11)
     i              risa12                      sal(12)
     i              risa13                      sal(13)
      *
      * Les kunder
      *
     c     rkunl1_key    setll     rkunl1
     c     rkunl1_key    reade     rkunl1                                 60
      *
     c                   dow       *in60 = *off
      *
     c                   eval      d1ksal = 0
     c                   eval      d1osal = 0
     c                   eval      d1rsal = 0
      *
     c                   exsr      les_poster                                   OPPR.BELØP
      *
     c                   eval      d1ksal = rksald
      *
      * Summer totalt
      *
     c                   add       d1ksal        t2ksal                         ADDER TIL TOTAL
     c                   add       d1osal        t2osal                             --- " ---
     c                   add       d1rsal        t2rsal                             --- " ---
      *
     c                   if        d1osal <> d1ksal or
     c                             d1rsal <> d1ksal
      *
     c                   add       1             t2antf
      *
      * Beregn differanser
      *
     c     d1ksal        sub       d1osal        d1odif
     c     d1ksal        sub       d1rsal        d1rdif
      *
      * Summer pr. kunde
      *
     c                   add       d1ksal        t1ksal                         ADDER TIL TOTAL
     c                   add       d1osal        t1osal                             --- " ---
     c                   add       d1rsal        t1rsal                             --- " ---
     c                   add       d1odif        t1odif                             --- " ---
     c                   add       d1rdif        t1rdif                             --- " ---
      *
     c                   write     d1det
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   eval      *in30 = *off
     c                   endif
      *
     c                   endif
      *
     c     rkunl1_key    reade     rkunl1                                 60
      *
     c                   enddo
      *
      * Hent hovedbokssaldo kunder
      *
     c     ra04l1_key    chain     ra04l1
      *
     c                   if        %found
     c                   eval      rhsalr_kont = radk05
      *
     c     rhsalr_key    setll     rhsalr
     c     rhsalr_key    reade     rhsalr                                 63
      *
     c                   dow       *in63 = *off
      *
     c                   if        riraar = *year and
     c                             ritype = 1
      *
      * Summer periode fra 1 - til denne periode
      *
     c                   eval      n = 1
      *
     c                   eval      t2hsal = t2hsal + risa00
      *
     c                   dou       n > 13
      *
     c                   eval      t2hsal = t2hsal + sal(n)
      *
     c                   eval      n = n + 1
     c                   enddo
      *
     c                   endif
      *
     c     rhsalr_key    reade     rhsalr                                 63
     c                   enddo
      *
     c                   endif
      *
      * Hent hovedbokssaldo kategorispesifikasjon
      *
     c     ra11l1_key    setll     ra11l1
     c     ra11l1_key    reade     ra11l1                                 62
      *
     c                   dow       *in62 = *off
6.32 c                   if        rakavd = *zero and
6.32 c                             raksps = *zero
6.32 c                   goto      nyles
6.32 c                   endif
      *
T5.30c                   if        rakkat = 'J' and
E5.30c                             rakkto > 0
     c                   eval      rhsal1_avde = rakavd
     c                   eval      rhsal1_kont = rakkto
     c                   eval      rhsal1_spes = raksps
     c                   eval      rhsal1_raar = *year
     c                   eval      rhsal1_type = 1
      *
     c     rhsal1_key    chain     rhsal1
      *
      * Summer periode fra 1 - til denne periode
      *
     c                   if        %found(rhsal1)
     c                   eval      n = 1
      *
     c                   eval      t2hsal = t2hsal + risa00
      *
     c                   dou       n > 13
      *
     c                   eval      t2hsal = t2hsal + sal(n)
      *
     c                   eval      n = n + 1
     c                   enddo
      *
     c                   endif
      *
     c                   endif
6.32  *
6.32 c     nyles         tag
      *
     c     ra11l1_key    reade     ra11l1                                 62
      *
     c                   enddo
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
      *
     c                   write     t1tot
      *
     c                   write     t2tot
      *
6.30 c                   if        l_poff = '1'
6.30 c                   exsr      spoolnbr
6.31 c                   close     rk789p
6.31 c                   exsr      xml_create
6.30 c                   exsr      pdf_preview
6.30  * Avslutt jobbiden
6.30 c                   call      'AB710R'
6.30 c                   parm                    l_bach
6.30 c                   endif
      *
     c/eject
      *
     c     les_poster    begsr
      *
      * Les posteringer
      *
     c                   eval      rktrl1_kund = rkkund
     c     rktrl1_key    setll     rktrl1
     c     rktrl1_key    reade     rktrl1                                 61
      *
     c                   dow       *in61 = *off
      *
     c                   add       rnbelØ        d1osal                         OPPR.BELØP
     c                   add       rnrest        d1rsal                         REST.BELØP
     c                   add       1             t2antt
      *
     c     rktrl1_key    reade     rktrl1                                 61
      *
     c                   enddo
      *
     c                   endsr
      *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30  * Finner spool som er generert av jobben
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30 c     spoolnbr      begsr
6.30  /Free
6.30
6.30           QSPRILSP( SPRL0100
6.30                   : %size(SPRL0100)
6.30                   : 'SPRL0100'
6.30                   : errorcode );
6.30  /End-free
6.30  *
6.30 c                   endsr
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30  * Kaller eget program for generering av xml fil. Eget pgm         *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30 c     xml_create    begsr
6.30  *
6.30 c                   eval      splfname2 = *blanks
6.30 c                   eval      splfname3 = *blanks
6.30 c                   eval      jobname2 = *blanks
6.30 c                   eval      jobname3 = *blanks
6.30 c                   eval      username2 = *blanks
6.30 c                   eval      username3 = *blanks
6.30 c                   eval      jobnbr2 = *blanks
6.30 c                   eval      jobnbr3 = *blanks
6.30 c                   eval      splfnbr2 = *zeros
6.30 c                   eval      splfnbr3 = *zeros
6.30 c                   eval      p_btyp = 'AVSTEMMINGSLISTE'
6.30 c                   eval      p_dspl = 'Avstemmingsliste - kunder '+
6.30 c                                      %char(w_dato)
6.30 c                   eval      p_refn = %char(w_year)+%char(w_dato)
6.30 c                   eval      p_tagg0 = 'KJOREDATO'
6.30 c                   eval      p_tagg0val = %char(w_dato)
6.30 c                   eval      p_tagg1 = *blanks
6.30 c                   eval      p_tagg1val = *blanks
6.30 c                   eval      p_tagg2 = *blanks
6.30 c                   eval      p_tagg2val = *blanks
6.30 c                   eval      p_tagg3 = *blanks
6.30 c                   eval      p_tagg3val = *blanks
6.30 c                   eval      p_tagg4 = *blanks
6.30 c                   eval      p_tagg4val = *blanks
6.30 c                   eval      p_tagg5 = *blanks
6.30 c                   eval      p_tagg5val = *blanks
6.30 c                   eval      p_tagg6 = *blanks
6.30 c                   eval      p_tagg6val = *blanks
6.30 c                   eval      p_tagg7 = *blanks
6.30 c                   eval      p_tagg7val = *blanks
6.30 c                   eval      p_tagg8 = *blanks
6.30 c                   eval      p_tagg8val = *blanks
6.30 c                   eval      p_tagg9 = *blanks
6.30 c                   eval      p_tagg9val = *blanks
      *
6.30 c                   call      'AP627R'
6.30 c                   PARM                    splfname
6.30 c                   PARM                    jobname
6.30 c                   PARM                    username
6.30 c                   PARM                    jobnbr
6.30 c                   PARM                    splfnbr
6.30 c                   PARM                    splfname2
6.30 c                   PARM                    jobname2
6.30 c                   PARM                    username2
6.30 c                   PARM                    jobnbr2
6.30 c                   PARM                    splfnbr2
6.30 c                   PARM                    splfname3
6.30 c                   PARM                    jobname3
6.30 c                   PARM                    username3
6.30 c                   PARM                    jobnbr3
6.30 c                   PARM                    splfnbr3
6.30 c                   parm                    p_tagg0
6.30 c                   parm                    p_tagg0val
6.30 c                   parm                    p_tagg1
6.30 c                   parm                    p_tagg1val
6.30 c                   parm                    p_tagg2
6.30 c                   parm                    p_tagg2val
6.30 c                   parm                    p_tagg3
6.30 c                   parm                    p_tagg3val
6.30 c                   parm                    p_tagg4
6.30 c                   parm                    p_tagg4val
6.30 c                   parm                    p_tagg5
6.30 c                   parm                    p_tagg5val
6.30 c                   parm                    p_tagg6
6.30 c                   parm                    p_tagg6val
6.30 c                   parm                    p_tagg7
6.30 c                   parm                    p_tagg7val
6.30 c                   parm                    p_tagg8
6.30 c                   parm                    p_tagg8val
6.30 c                   parm                    p_tagg9
6.30 c                   parm                    p_tagg9val
6.30 c                   parm                    l_bach
6.30 c                   parm                    p_btyp
6.30 c                   parm                    p_refn
6.30 c                   parm                    p_dspl
6.30  *
6.30 c                   endsr
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30  * Subrutiner for XML/jasper integrasjon - Skjermvisning           *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
6.30 c     pdf_preview   begsr
6.30 c                   if        l_skje = 1
6.30  * Vi kaller program for launch av PDF i browser
6.30 c                   call      'AP621R'
6.30 c                   parm                    l_bach
6.30  *
6.30 c                   endif
6.30  *
6.30 c                   endsr
6.30  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   eval      w_firm = l_firm
6.30 c                   open      rk789p
6.30 c                   eval      w_year = *year
6.30 c                   time                    w_dato
      *
     c                   write     a1hdr
      *
      * Key for kunder
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for posteringer - kunder
      *
     c     rktrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrl1_kund
      *
      * Key for faste konti
      *
     c     ra04l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for kundekategori
      *
     c     ra11l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for hovedbokssaldo
      *
     c     rhsal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhsal1_kont
     c                   kfld                    rhsal1_spes
     c                   kfld                    rhsal1_avde
     c                   kfld                    rhsal1_raar
     c                   kfld                    rhsal1_type
      *
      * Key for hovedbokssaldo
      *
     c     rhsalr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhsalr_kont
      *
     c                   endsr
