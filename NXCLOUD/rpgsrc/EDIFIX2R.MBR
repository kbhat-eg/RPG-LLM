     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System  . . . . : lager
      *  Program . . . . : EDIJOB
      *  Beskrivelse . . : Leser LWELPF og legger ut på LWEFPF
      *                    for en dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.
      * --------- ------  --------------------------------------------------
      *           181111  Nytt program                             ASK
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flwell2    if   e           k disk    rename(lwelpfr:lwell2r)
     flwefpf    uf a e           k disk    rename(lwefpfr:lwefpfr)
      *
      *
      *  UDS Local Data Area
      *
     d                uds
     d l_firm                944    946  0
      *
      *  Definering av variable i nøkler
      *
     d lwell2_logind   s                   like(logind)
     d lwell2_logtyp   s                   like(logtyp)
     d lwell2_logdat   s                   like(logdat)
      *
      *  Definering av variable
      *
     d w_dato          s              6  0
     d w_time          s              6  0
     d w_dokn          s             10
     d w_rest          s              1  0
     d w_isotim        s                   like(logtid)
     d w_recid         s              4
     d w_doktst        s             10
     d b_start         s              1
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Lese logfil for uthenting av meldingslinjer ihht. parameter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      b_start = *off
     c     *hms          move      w_time        w_isotim
      *
     c     lwell2_key    setll     lwell2
      *
      *  Lese file
      *
     c     lwell2_key    reade     lwell2
     c                   dow       not %eof
      *
      * Er tidspunkt valgt?
      *
     c                   if        w_time <> 0 and
     c                             logtid <> w_isotim
     c                   goto      les_neste
     c                   endif
      *
      * Er dokumentnummer valgt?
      *
     c                   eval      w_recid = %subst(logrec:1:4)
     c                   eval      w_doktst = %subst(logrec:42:10)
      *
     c                   if        w_dokn = *blank
     c                   eval      b_start = *on
     c                   else
     c                   if        w_recid ='ED01' and
     c                             w_doktst = w_dokn
     c                   eval      b_start = *on
     c                   endif
     c                   endif
      *
     c                   if        b_start = *off
     c                   goto      les_neste
     c                   endif
      *
      * Er resterende dokumenter valgt?
      *
     c                   if        b_start = *on and
     c                             w_dokn <> *blank and
     c                             w_rest <> 1 and
     c                             w_recid ='ED01' and
     c                             w_doktst <> w_dokn
     c                   goto      xslutt
     c                   endif
      *
     c                   eval      faklin = logrec
     c                   write     lwefpfr
      *
     c     les_neste     tag
     c     lwell2_key    reade     lwell2
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    w_dato
     c                   parm                    w_time
     c                   parm                    w_dokn
     c                   parm                    w_rest
      *
      * Key for lesing av loggfil
      *
     c     lwell2_key    klist
     c                   kfld                    lwell2_logind
     c                   kfld                    lwell2_logtyp
     c                   kfld                    lwell2_logdat
      *
      *  Legge inn opplysninger i nøklene
      *
     c                   eval      lwell2_logind = 'I'
     c                   eval      lwell2_logtyp = 'FAKT'
     c     *dmy          move      w_dato        lwell2_logdat
      *
     c                   endsr
