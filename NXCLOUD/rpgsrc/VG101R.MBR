     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VG101R
      * Beskrivelse . . : Hovedgruppe, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       140199 HKO  Nytt program
      * 6.10  260609 BSA  Oppdatert med sporingsinformasjon
7.xx  * 7.00  040216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.xx  * 7.01  040521 bof  Takle utvidet skjermbilde
      *
8.01  *K0000  141124 bsa  Snudd om håndtering av bredt skjermbilde i DDS
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvhgrl2    if   e           k disk    rename(vhgrpfr:vhgrl2r)
     fvhgrlr    if   e           k disk    rename(vhgrpfr:vhgrlrr)
     fvhgrlu    uf a e           k disk    rename(vhgrpfr:vhgrlur)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvkhvl1    if   e           k disk    rename(vkhvpfr:vkhvl1r)
     fvenhl1    if   e           k disk    rename(venhpfr:venhl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvugrlu    uf a e           k disk    rename(vugrpfr:vugrlur)
     fra26l1    if   e           k disk    rename(ra26pfr:ra26l1r)
      *
     fvg101d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vghfir)
     d p_ogrp          s                   like(vghogr)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vhgrl2_htxt     s                   like(vghtxt)
     d vhgrlr_hhgr     s                   like(vghhgr)
     d vhgrlu_hhgr     s                   like(vghhgr)
     d vogrl1_oogr     s                   like(vgoogr)
     d vkhvl1_kkod     s                   like(vakkod)
     d venhl1_eenh     s                   like(vaeenh)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrlu_uhgr     s                   like(vguhgr)
     d ra26l1_zsak     s                   like(razsak)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(vghfir)
     d w_ogrp          s                   like(vghogr)
     d w_ktxt          s                   like(vaktxt)
     d w_etxt          s                   like(vaetxt)
     d w_ztxt          s                   like(raztxt)
      *
     d s_hhgr          s                   like(vghhgr)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(12)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meleingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2hhgr <> 0 or
     c                             b2htxt <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      vhgrl2_htxt = b1htxt
     c     vhgrl2_key    setll     vhgrl2
     c                   other
     c                   eval      vhgrl1_hhgr = b1hhgr
     c     vhgrl1_key    setll     vhgrl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på hovedgruppe
      *
     c                   if        b2hhgr <> 0
     c                   eval      w_seqe = ' '
     c                   eval      vhgrl1_hhgr = b2hhgr
     c     vhgrl1_key    setll     vhgrl1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på beskrivelse
      *
     c                   if        b2htxt <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      vhgrl2_htxt = b2htxt
     c     vhgrl2_key    setll     vhgrl2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2hhgr = 0
     c                   eval      b2htxt = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1hhgr = b1hhgr
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1hhgr = b1hhgr
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1hhgr = b1hhgr
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1hhgr = b1hhgr
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vedlikehold av undergruppe
      *
     c                   if        b1valg = 7
     c                   call      'VG102R'
     c                   parm                    w_firm
     c                   parm                    w_ogrp
     c                   parm                    b1hhgr
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1hogr = w_ogrp
     c                   eval      c1otxt = b2otxt
      *
     c                   eval      c1hhgr = b2hhgr
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
     c                   if        c1hhgr = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      vhgrlr_hhgr = c1hhgr
     c     vhgrlr_key    chain     vhgrlr                             90
     c                   if        *in90 = *off
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1hhgr = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2hhgr = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      c1hogr = w_ogrp
     c                   eval      c1otxt = b2otxt
     c                   eval      c2ktxt = *blank
     c                   eval      c2ztxt = *blank
      *
     c                   eval      vhgrlr_hhgr = c1hhgr
     c     vhgrlr_key    chain     vhgrlr                             90
      *
     c                   if        *in90 = *off
     c                   eval      c2htxt = vghtxt
     c                   eval      c2hkhv = vghkhv
     c                   eval      c2hpra = vghpra
     c                   eval      c2hens = vghens
     c                   eval      c2henl = vghenl
     c                   eval      c2henp = vghenp
     c                   eval      c2heus = vgheus
     c     *dmy          move      vgheda        c2heda
      *
     c                   if        vghkhv <> *blank
     c                   eval      vkhvl1_kkod = vghkhv
     c     vkhvl1_key    chain     vkhvl1                             91
     c                   if        *in91 = *off
     c                   eval      c2ktxt = vaktxt
     c                   endif
     c                   endif
      *
     c                   if        vghpra <> *blank
     c                   eval      ra26l1_zsak = vghpra
     c     ra26l1_key    chain     ra26l1                             91
     c                   if        *in91 = *off
     c                   eval      c2ztxt = raztxt
     c                   endif
     c                   endif
      *
     c                   else
     c                   eval      c2htxt = *blank
     c                   eval      c2hkhv = *blank
     c                   eval      c2ktxt = *blank
     c                   eval      c2hpra = *blank
     c                   eval      c2ztxt = *blank
     c                   eval      c2hens = *blank
     c                   eval      c2henl = *blank
     c                   eval      c2henp = *blank
     c                   eval      c2heus = *blank
     c                   eval      c2heda = 0
     c                   eval      c2heus = *blank
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1hhgr = k1hhgr
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd = *on
      *
     c                   if        w_afld = 'C2HKHV'
     c                   call      'VA520R'
     c                   parm                    w_firm
     c                   parm                    c2hkhv
     c                   parm                    w_ktxt
     c                   eval      c2ktxt = w_ktxt
     c                   goto      c2taga
     c                   endif
      *
     c                   if        w_afld = 'C2HPRA'
     c                   call      'RA526R'
     c                   parm                    w_firm
     c                   parm                    c2hpra
     c                   parm                    w_ztxt
     c                   eval      c2ztxt = w_ztxt
     c                   goto      c2taga
     c                   endif
      *
     c                   if        w_afld = 'C2HENS'
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    c2hens
     c                   parm                    w_etxt
     c                   goto      c2taga
     c                   endif
      *
     c                   if        w_afld = 'C2HENL'
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    c2henl
     c                   parm                    w_etxt
     c                   goto      c2taga
     c                   endif
      *
     c                   if        w_afld = 'C2HENP'
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    c2henp
     c                   parm                    w_etxt
     c                   goto      c2taga
     c                   endif
      *
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2htxt = *blank
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   if        c2hkhv <> *blank
     c                   eval      vkhvl1_kkod = c2hkhv
     c     vkhvl1_key    chain     vkhvl1                             91
     c                   if        *in91 = *on
     c                   eval      *in32 = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
     c                   if        c2hpra <> *blank
     c                   eval      ra26l1_zsak = c2hpra
     c     ra26l1_key    chain     ra26l1                             91
     c                   if        *in91 = *on
     c                   eval      *in36 = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
     c                   if        c2hens <> *blank
     c                   eval      venhl1_eenh = c2hens
     c     venhl1_key    chain     venhl1                             91
     c                   if        *in91 = *on
     c                   eval      *in33 = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
     c                   if        c2henl <> *blank
     c                   eval      venhl1_eenh = c2henl
     c     venhl1_key    chain     venhl1                             91
     c                   if        *in91 = *on
     c                   eval      *in34 = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
     c                   if        c2henp <> *blank
     c                   eval      venhl1_eenh = c2henp
     c     venhl1_key    chain     venhl1                             91
     c                   if        *in91 = *on
     c                   eval      *in35 = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
      * Oppdatering av hovedgruppe
      *
     c                   clear                   vhgrlur
      *
     c                   eval      vhgrlu_hhgr = c1hhgr
     c     vhgrlu_key    chain     vhgrlur                            90
      *
     c                   eval      vghtxt = c2htxt
     c                   eval      vghkhv = c2hkhv
     c                   eval      vghpra = c2hpra
     c                   eval      vghens = c2hens
     c                   eval      vghenl = c2henl
     c                   eval      vghenp = c2henp
      *
     c                   time                    vgheda
     c                   time                    vgheti
     c                   eval      vgheus = l_user
      *
     c                   if        *in90 = *off
     c                   update    vhgrlur
     c                   else
     c                   eval      vghfir = w_firm
     c                   eval      vghogr = w_ogrp
     c                   eval      vghhgr = c1hhgr
6.10 c                   time                    vghoda
6.10 c                   time                    vghoti
6.10 c                   eval      vghous = l_user
     c                   write     vhgrlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1htxt = vghtxt
     c                   eval      b1hkhv = vghkhv
     c                   eval      b1hpra = vghpra
     c                   eval      b1hens = vghens
     c                   eval      b1henl = vghenl
     c                   eval      b1henp = vghenp
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      d1hogr = w_ogrp
      *
     c                   eval      vhgrlr_hhgr = d1hhgr
     c     vhgrlr_key    chain     vhgrlr                             90
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter hovedgruppe
      *
     c                   eval      vhgrlu_hhgr = d1hhgr
     c     vhgrlu_key    chain     vhgrlur                            90
     c                   delete    vhgrlur
      *
      * Sletter tilhørende undergrupper
      *
     c                   eval      vugrlu_uhgr = vhgrlu_hhgr
     c     vugrlu_key    setll     vugrlu
     c     vugrlu_key    reade     vugrlur                                91
     c                   dow       *in91 = *off
     c                   delete    vugrlur
     c     vugrlu_key    reade     vugrlur                                91
     c                   enddo
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_hhgr = k1hhgr
      *
      * Fyller kopierings-bildet
      *
     c                   eval      vhgrlr_hhgr = k1hhgr
     c     vhgrlr_key    chain     vhgrlr                             90
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1hhgr = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      vhgrlr_hhgr = k1hhgr
     c     vhgrlr_key    chain     vhgrlr                             90
     c                   if        *in90 = *off
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1hhgr = s_hhgr
     c                   exsr      xc2bld
      *
      * Dersom hovedgruppe er opprettet, kopieres undergrupper
      *
     c                   eval      vhgrlr_hhgr = k1hhgr
     c     vhgrlr_key    chain     vhgrlr                             90
     c                   if        *in90 = *off
      *
     c                   eval      vugrl1_uhgr = s_hhgr
     c     vugrl1_key    setll     vugrl1
     c     vugrl1_key    reade     vugrl1                                 91
     c                   dow       *in91 = *off
     c                   eval      vguhgr = k1hhgr
6.10 c                   time                    vguoda
6.10 c                   time                    vguoti
6.10 c                   eval      vguous = l_user
6.10 c                   time                    vgueda
6.10 c                   time                    vgueti
6.10 c                   eval      vgueus = l_user
     c                   write     vugrlur
     c     vugrl1_key    reade     vugrl1                                 91
     c                   enddo
      *
     c                   endif
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      vhgrl2
     c                   other
     c                   read      vhgrl1
     c                   endsl
      *
     c                   if        %eof or
     c                             vghfir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        vghogr <> w_ogrp
     c                   if        w_seqe = 'B'
     c                   iter
     c                   else
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      vhgrl2_htxt = vghtxt
     c                   other
     c                   eval      vhgrl1_hhgr = vghhgr
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1hhgr = vghhgr
     c                   eval      b1htxt = vghtxt
     c                   eval      b1hkhv = vghkhv
     c                   eval      b1hpra = vghpra
     c                   eval      b1hens = vghens
     c                   eval      b1henl = vghenl
     c                   eval      b1henp = vghenp
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     vhgrl2_key    setgt     vhgrl2
     c                   readp     vhgrl2
     c                   other
     c     vhgrl1_key    setgt     vhgrl1
     c                   readp     vhgrl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     vhgrl2
     c                   other
     c                   readp     vhgrl1
     c                   endsl
      *
     c                   if        %eof or
     c                             vghfir <> w_firm
     c                   leave
     c                   endif
      *
     c                   if        vghogr <> w_ogrp
     c                   if        w_seqe = 'B'
     c                   iter
     c                   else
     c                   leave
     c                   endif
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      vhgrl2_htxt = vghtxt
     c                   other
     c                   eval      vhgrl1_hhgr = vghhgr
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     vhgrl2_key    setll     vhgrl2
     c                   other
     c     vhgrl1_key    setll     vhgrl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ogrp
      *
      * Key for posisjonering på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ogrp
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for posisjonering på beskrivelse
      *
     c     vhgrl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl2_htxt
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ogrp
     c                   kfld                    vhgrlr_hhgr
      *
      * Key for oppdatering av hovedgruppe
      *
     c     vhgrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ogrp
     c                   kfld                    vhgrlu_hhgr
      *
      * Key for les på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på kontohenvisning
      *
     c     vkhvl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vkhvl1_kkod
      *
      * Key for oppslag på produktansvarlig
      *
     c     ra26l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra26l1_zsak
      *
      * Key for oppslag på enhet
      *
     c     venhl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    venhl1_eenh
      *
      * Key for les på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ogrp
     c                   kfld                    vugrl1_uhgr
      *
      * Key for oppdatering av undergruppe
      *
     c     vugrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_ogrp
     c                   kfld                    vugrlu_uhgr
      *
      *
      * Henter firma og overgruppe fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_ogrp = p_ogrp
      *
      * Initierer skjermbilde
      *
     c                   eval      *in22 = *on
      *
     c                   eval      b2hogr = w_ogrp
      *
     c                   eval      vogrl1_oogr = w_ogrp
     c     vogrl1_key    chain     vogrl1                             91
     c                   if        *in91 = *off
     c                   eval      b2otxt = vgotxt
     c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      vhgrl1_hhgr = 0
     c     vhgrl1_key    setll     vhgrl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
