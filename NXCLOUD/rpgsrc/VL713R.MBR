     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VL713R
      * Beskrivelse . . : Prisgruppe - sjekker om prisgruppen som kommer
      *                   finnes på valgt vare
      *
      *                   Brukes hvis kallende program har lager / avdeling
      *
      *
      *                   Input : prisgruppe, vare
      *                   Output: prisgruppe
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       241008 bof  Nytt program
6.20  *       220311 BOF  Utvidet med prisgiver på vare-pris
8.01  *PRG2   050722 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvpgrl1    if   e           k disk    rename(vpgrpfr:vpgrl1r)
     fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
6.20 fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
      * Definering av variable i nøkler
      *
     d vvprl1_vare     s                   like(vpvare)
6.20 d vvprl1_ldor     s                   like(vpldor)
5.70 d vvprl1_prgr     s                   like(vpprgr)
     d vvprl1_enhe     s                   like(vpenhe)
     d vvprl1_lety     s                   like(vplety)
6.20 d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av pamametre
      *
     d p_iprg          s                   like(vpprgr)
     d p_vare          s                   like(vpvare)
     d p_enhe          s                   like(vpenhe)
     d p_lety          s                   like(vplety)
     d p_oprg          s                   like(vpprgr)
     d p_firm          s                   like(vpfirm)
      *
     d b_pris          s              1    inz(*off)
6.20 d b_inlr          s              1    inz(*off)
      *
     d w_firm          s                   like(vpgfir)
     d w_dato          s                   like(vppdat)
6.20 d w_ldor          s                   like(vpldor)
6.20 d w_lage          s              2  0
      *
     d                uds
     d l_user                911    920
     d l_sfir                944    946  0
     d l_savd                947    950  0
     d l_lagr               1001   1002  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.20 c                   eval      vvarl1_vare = p_vare
6.20 c     vvarl1_key    chain     vvarl1
6.20  *
6.20 c                   eval      w_lage = l_lagr
6.20  *
6.20  * Henter leverandør for vare og lager
6.20  *
6.20 c                   call      'VL721R  '
6.20 c                   parm                    w_firm
6.20 c                   parm                    p_vare
6.20 c                   parm                    w_lage
6.20 c                   parm                    w_ldor
6.20 c                   parm                    b_inlr
      *
      *
      * Initierer output-parametre
      *
     c                   eval      p_oprg = *blank
      *
      * Henter pris for vare, prisgrp.,leveringstype og enhet
      *
     c                   eval      b_pris = *off
6.20  * 1. leverandør for lageret
6.20 c                   eval      vvprl1_ldor = w_ldor
6.20 c                   exsr      hent_pris
6.20 c                   if        b_pris = *off  and
6.20 c                             w_ldor <> vvldor
6.20  * 2. hovedleverandør
6.20 c                   eval      vvprl1_ldor = vvldor
6.20 c                   exsr      hent_pris
6.20 c                   if        b_pris = *off
6.20  * siste mulighet...
6.20 c                   eval      vvprl1_ldor = 0
6.20 c                   exsr      hent_pris
6.20 c                   endif
6.20 c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
      *
     c                   return
      *
6.20  *-----------------------------------------------------
6.20  * Henter priser
6.20  *-----------------------------------------------------
6.20  *
6.20 c     hent_pris     begsr
6.20 c                   eval      vvprl1_vare = p_vare
6.20 c                   eval      vvprl1_prgr = p_iprg
6.20 c                   eval      vvprl1_enhe = p_enhe
6.20 c                   eval      vvprl1_lety = p_lety
6.20 c     vvprl1_key    setll     vvprl1
6.20 c     vvprl1_key    reade     vvprl1                                 90
6.20 c                   dow       *in90 = *off
6.20  *
6.20 c                   eval      b_pris = *on
6.20 c                   eval      p_oprg = vpprgr
6.20  *
6.20 c                   if        w_dato >= vppdat
6.20 c                   leave
6.20 c                   endif
6.20  *
6.20 c     vvprl1_key    reade     vvprl1                                 90
6.20 c                   enddo
6.20  *
6.20 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_iprg
     c                   parm                    p_vare
     c                   parm                    p_enhe
     c                   parm                    p_lety
     c                   parm                    p_oprg
      *
      * Key for les på vare-pris-register
      *
     c     vvprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprl1_vare
6.20 c                   kfld                    vvprl1_ldor
5.70 c                   kfld                    vvprl1_prgr
     c                   kfld                    vvprl1_enhe
     c                   kfld                    vvprl1_lety
      *
     c                   eval      w_firm = p_firm
     c                   time                    w_dato
6.20  *
6.20  * Key for les på vare
6.20  *
6.20 c     vvarl1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    vvarl1_vare
      *
     c                   endsr
