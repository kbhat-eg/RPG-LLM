     h option(*nodebugio) datedit(*dmy) decedit('0,')
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : LI608R
      * Beskrivelse . . : Utskrift av EDI-Avviksliste
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       010404 ASK  Nytt program
      * 5.60  060208 TSV  Vidrekobling for avviksliste ordrebekreftelse
6.10  * R6.10 070909 BSA  Utskriften endret pga utvidelse av referansefelter.
6.nu  * r6.30 100614 bof  Sjekket mtp. nummerutvidelse
6.32  * 6.30  081018 bof  Utvidet med trelast sortiment
7.01  * R6.30 230317 BSA  GTIN nummer 14 siffer
      *
8.01  *K0000  220621 BSA  Utvidet meldingsnummer fra 6-8 siffer
      *
      *
      *
      * Bruk av linjenummersekvenser (2 første siffer i linjenr LIMLIN)
      *
      *    10 : Hodeinformasjon
      *    20 : Partsinformasjon
      *    30 : Referanser
      *    40 : Transportinformasjon
      *    50 : Linjer
      *    55 : Linjer fra best. (ikke match i EDI-melding)
      *    60 : Tillegg/fradrag hodenivå
      *    65 : Fritkstlinjer på hodenivå
      *    90 : Totaler
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     fledilr    if   e           k disk    rename(ledipfr:ledilrr)
     fledil1    if   e           k disk    rename(ledipfr:ledil1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarl3    if   e           k disk    rename(vvarpfr:vvarl3r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     faforl1    if   e           k disk    rename(aforpfr:aforl1r)
     fltoel1    if   e           k disk    rename(ltoepfr:ltoel1r)
      *
5.60 f*i608p    o    e             printer
5.60 fli608p    o    e             printer USROPN
      *
      * Parameter-inn
      *
     d                 ds
     d d_prec                        45
     d  d_firm                        3  0 overlay(d_prec)
     d  d_type                        4    overlay(d_prec:4)
8.01 d  d_mnum                        8  0 overlay(d_prec:8)
8.01 d  d_mlin                        6  0 overlay(d_prec:16)
8.01 d  d_kode                        1    overlay(d_prec:22)
      *
      * FAKTURA Hode
      *
     d d_fakh1         ds
     d  d_fh_dtyp                     3
     d  d_fh_fanr                    35
     d  d_fh_fako                     3
     d  d_fh_ldat                      d   datfmt(*iso)
     d  d_fh_best                    20
     d  d_fh_lord                    20
     d  d_fh_lfak                    20
     d  d_fh_sean                    13  0
     d  d_fh_snav                    30
     d  d_fh_fean                    13  0
     d  d_fh_fnav                    30
     d  d_fh_fad1                    30
     d  d_fh_fad2                    30
     d  d_fh_fste                    30
     d  d_fh_forg                    20
     d  d_fh_kref                    20
     d  d_fh_lref                    20
     d  d_fh_valk                     3
     d  d_fh_ffda                      d   datfmt(*iso)
     d  d_fh_levb                     3
     d  d_fh_forf                      d   datfmt(*iso)
      *
      * Tillegg/fradragslinjer
      *
     d d_fakh2         ds
     d  d_fh_tsig                     3
     d  d_fh_tkod                     3
     d  d_fh_ttek                    30
     d  d_fh_tpro                     4  2
     d  d_fh_tbel                     8  2
     d  d_fh_apro                     4  2
      *
      * Fritekst/hodelinje
      *
     d d_fakh3         ds
     d  d_fh_ftxq                     3
     d  d_fh_ftxt                    70
      *
      * Fakturareferanser/hode
      *
     d d_fakh4         ds
     d  d_fh_bstn                    35
     d  d_fh_ordr                    35
     d  d_fh_fakk                    35
     d  d_fh_paks                    35
     d  d_fh_kunr                    35
     d  d_fh_proj                    35
     d  d_fh_lvfa                    35
     d  d_fh_ofav                    35
     d  d_fh_plnr                    35
     d  d_fh_konr                    35
     d  d_fh_kidn                    35
      *
      * Faktura partsinfo./hode
      *
     d d_fakh5         ds
     d  d_fh_part                     3
     d  d_fh_pean                    13  0
     d  d_fh_pnav                    30
     d  d_fh_pad1                    30
     d  d_fh_pad2                    30
     d  d_fh_pste                    30
     d  d_fh_porg                    20
      *
      * Faktura transport/lev. - hode
      *
     d d_fakh6         ds
     d  d_fh_trmo                     3
     d  d_fh_trna                    35
     d  d_fh_lebe                     3
      *
      * FAKTURA Linje
      *
     d d_fakl          ds
     d  d_fl_llin                     6  0
7.01 d  d_fl_vean                    14  0
     d  d_fl_vnob                     8  0
     d  d_fl_vlev                    15
     d  d_fl_tek1                    35
     d  d_fl_tek2                    35
     d  d_fl_bkva                    15  3
     d  d_fl_benh                     3
     d  d_fl_lkva                    15  3
     d  d_fl_lenh                     3
     d  d_fl_fkva                    15  3
     d  d_fl_fenh                     3
     d  d_fl_bnet                    15  2
     d  d_fl_bbto                    15  2
     d  d_fl_prkv                     3
     d  d_fl_pris                    15  3
     d  d_fl_penh                     3
     d  d_fl_prpr                     9  0
     d  d_fl_bnum                    20
     d  d_fl_blin                     6  0
     d  d_fl_mvap                     4  2
     d  d_fl_sig1                     3
     d  d_fl_kod1                     3
     d  d_fl_bel1                     9  2
     d  d_fl_pro1                     5  2
     d  d_fl_sig2                     3
     d  d_fl_kod2                     3
     d  d_fl_bel2                     9  2
     d  d_fl_pro2                     5  2
     d  d_fl_sig3                     3
     d  d_fl_kod3                     3
     d  d_fl_bel3                     9  2
     d  d_fl_pro3                     5  2
     d  d_fl_sig4                     3
     d  d_fl_kod4                     3
     d  d_fl_bel4                     9  2
     d  d_fl_pro4                     5  2
     d  d_fl_sig5                     3
     d  d_fl_kod5                     3
     d  d_fl_bel5                     9  2
     d  d_fl_pro5                     5  2
     d  d_fl_sig6                     3
     d  d_fl_kod6                     3
     d  d_fl_bel6                     9  2
     d  d_fl_pro6                     5  2
      *
      * Fakturatotallinjer
      *
     d d_fakt          ds
     d  d_ft_ftot                    15  2
     d  d_ft_teks                    15  2
     d  d_ft_tmva                    15  2
     d  d_ft_mvgr                    15  2
     d  d_ft_veks                    15  2
     d  d_ft_vink                    15  2
     d  d_ft_stil                    15  2
     d  d_ft_sfra                    15  2
      *
      * Local Data Area LDA
      *
     d                uds
     d l_ruti                800    809
     d l_alin                856    858  0
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d ledilr_type     s                   like(litype)
     d ledilr_mnum     s                   like(limnum)
     d ledilr_mlin     s                   like(limlin)
     d ledil1_type     s                   like(litype)
     d ledil1_mnum     s                   like(limnum)
     d ledil1_mlin     s                   like(limlin)
     d lodtl1_numm     s                   like(ldnumm)
     d lodtl1_suff     s                   like(ldsuff)
     d lodtl1_line     s                   like(ldline)
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d rlevl1_levr     s                   like(rllevr)
     d vvarl1_vare     s                   like(vvvare)
     d vvarl3_nobb     s                   like(vvnobb)
     d aforl1_ruti     s                   like(afruti)
      *
      * Definering av øvrige variable
      *
     d w_firm          s                   like(lofirm)
     d w_enob          s                   like(vvnobb)
     d w_pris          s                   like(ldipny)
     d w_frad          s                   like(ldipny)
     d w_tfra          s                   like(ldipny)
     d w_ttil          s                   like(ldipny)
     d w_kpri          s             15  3
     d w_prec          s             45
     d w_line          s              1  0
     d w_blct          s              1  0
     d w_bbest         s             15  2
     d w_bfakt         s             15  2
     d w_nobb          s              8
6.32 d w_lv_nobb       s                   like(vvnobb)
     d b_feil          s              1    inz(*off)
     d b_diff          s              1    inz(*off)
      *
      * XML string konstanter
      *
     d c_krn_kode      c                   '381'
     d c_krn_head      c                   'EDI - Kreditnota'
     d c_fak_head      c                   'EDI - Faktura'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Definering av formater
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * A1HDR  - Heading A1 på rapport
      * D1DET  - Detail  D1 på rapport, fakturalinje
      * D2DET  - Detail  D2 på rapport, blank detaljlinje
      * D3DET  - Detail  D3 på rapport, underline
      * T1TOT  - Total   T1 på rapport, totallinjer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Styre-rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.32 C/Exec SQL
6.32 C+  Set Option DatFmt=*ISO
6.32 C/End-Exec
      *
5.60  * Vidrekobling for avviksliste ordrebekreftelse
 .    * sett kode = 'A' for avviksliste
 .   c     LI602PL       plist
 .   c                   parm                    d_prec
 .    *
5.60 c                   eval      *in39 = *off
 .    *
 .   c                   if        d_type = 'OBKR'
 .   c                   eval      d_kode = 'A'
 .   c                   call      'LI602R'      LI602PL
 .   c                   goto      end_pgm
5.60 c                   endif
      *
      * Hent EDI meldings-hode
      *
      *
     c                   if        d_type <> 'FAKT'
     c                   goto      end_pgm
     c                   endif
      *
5.60 c                   eval      *in39 = *on
5.60 c                   open      li608p
      *
     c     ledilr_key    chain     ledilr
     c                   if        not %found
     c                   goto      end_pgm
     c                   endif
     c                   eval      d_fakh1 = limeld
      *
      * Hent bestillings-hode
      *
     c                   eval      lohel1_numm = linumm
     c                   eval      lohel1_suff = lisuff
     c     lohel1_key    chain     lohel1r
     c                   if        not %found
     c                   eval      loldor = 0
     c                   endif
      *
      * Hent informasjon fra leverandør
      *
     c                   eval      rlevl1_levr = loldor
     c     rlevl1_key    chain     rlevl1r
     c                   if        not %found
     c                   eval      rlnavn = 'Ukjent leverandør'
     c                   endif
      *
      * Viser om kreditnota eller faktura
      *
     c                   if        d_fh_dtyp = c_krn_kode
     c                   eval      a1txt2 = c_krn_head
     c                   else
     c                   eval      a1txt2 = c_fak_head
     c                   endif
      *
      * Tilordne verdier til utskrift
      *
     c                   eval      a1fnav = d_fh_fnav
     c                   eval      a1fad1 = d_fh_fad1
     c                   eval      a1fad2 = d_fh_fad2
     c                   eval      a1fste = d_fh_fste
     c                   eval      a1vnav = d_fh_snav
     c                   eval      a1bnav = rlnavn
     c                   eval      a1vref = d_fh_kref
     c                   eval      a1dref = d_fh_lref
     c                   move      linumm        a1besn
     c                   movel     d_fh_fanr     a1fanr
     c                   eval      a1ornr = loornr
      *
      * Skrive heading
      *
     c     skriv_head    tag
      *
     c     *dmy          move      limdat        a1dato
      *
     c                   write     a1hdr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * D1DET Behandle detail D1
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Les varelinjer, finn tilleggsinformasjon og skriv ut.
      *
     c                   read      ledil1r
      *
     c                   dow       not %eof and
     c                             ledil1_mnum = limnum
      *
     c                   if        limlin < 500000
     c                   goto      ny_les
     c                   endif
      *
     c                   if        limlin < 550000
      *
     c*                  if        listat = *blank
     c*                  goto      ny_les
     c*                  endif
      *
     c                   eval      d_fakl = limeld
     c                   eval      d1nobb = d_fl_vnob
     c                   eval(h)   d1fant = d_fl_fkva
     c                   eval      d1tek1 = d_fl_tek1
     c                   eval      d1fenh = d_fl_fenh
      *
     c                   exsr      finn_pris
     c                   eval      d1fpri = w_pris
      *
     c                   exsr      sjekk_stat
      *
     c                   write     d1det                                30
     c                   write     d2det                                30
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
     c                   goto      ny_les
     c                   endif
      *
     c                   if        limlin < 600000
     c                   eval      d_fakl = limeld
     c                   exsr      les_best
      *
     c                   movel     ldvare        d3vare
     c                   eval      d3bant = ldanta
     c                   eval      d3benh = ldenh1
     c                   eval      d3bpri = ldipny
     c                   eval      d3tek1 = ldtek1
     c                   eval      d3sts1 = 'S'
      *
     c                   write     d3det                                30
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
     c                   goto      ny_les
     c                   endif
      *
     c                   if        limlin < 650000
     c                   eval      d_fakh2 = limeld
      *
     c                   exsr      hent_nobb
      *
     c                   eval      d4vare = w_enob
     c                   eval      d4bant = 1
     c                   eval      d4benh = 'STK'
     c                   if        d_fh_tsig = 'A  '
     c                   eval      d4bpri = d_fh_tbel * -1
     c                   else
     c                   eval      d4bpri = d_fh_tbel
     c                   endif
     c                   eval      d4tek1 = d_fh_ttek
     c                   eval      d4sts1 = 'T'
      *
     c                   write     d4det                                30
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   endif
     c                   goto      ny_les
      *
     c                   endif
      *
     c     ny_les        tag
     c                   read      ledil1r
      *
     c                   enddo
      *
      * Avslutt program
      *
     c     end_pgm       tag
     c                   eval      *inlr = *on
5.60 c   39              close     li608p
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for les av bestilingslinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     les_best      begsr
      *
     c                   eval      b_feil = *off
     c                   eval      lodtl1_numm = linumm
     c                   eval      lodtl1_suff = lisuff
     c                   eval      lodtl1_line = liline
     c     lodtl1_key    chain     lodtl1
     c                   if        not %found
     c                   eval      b_feil = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for setting av kontrollstatus
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_stat    begsr
      *
      * Nullstill statusfelter
      *
     c                   eval      d1sts1 = *blank
     c                   eval      d1sts2 = *blank
     c                   eval      d1sts3 = *blank
     c                   eval      d1sts4 = *blank
      *
      * Les bestilling og sjekk nobbnr
      *
     c                   exsr      les_best
      *
     c                   if        b_feil = *on
     c                   eval      d2vare = *blank
     c                   eval      d2bant = 0
     c                   eval      d2benh = *blank
     c                   eval      d2bpri = 0
     c                   eval      d2tek1 = *blank
     c                   eval      d1sts1 = 'U'
     c                   eval      d1sts2 = 'N'
     c                   goto      end_stat
     c                   endif
      *
     c                   movel     ldvare        d2vare
     c                   eval      d2bant = ldanta
     c                   eval      d2benh = ldenh1
     c                   eval      d2bpri = ldipny
     c                   eval      d2tek1 = ldtek1
      *
6.32  * sjekker om logistikk-vare
6.32 c                   eval      w_lv_nobb = 0
6.32 c/exec sql
6.32 c+ select vvlvnr
6.32 i+ into   :w_lv_nobb
6.32 c+ from   vvlepf
6.32 c+ where  vvlvnr = :ldnobb  and
6.32 c+        vvlldo = :ldldor
6.32 c+ fetch first 1 rows only
6.32 c/end-exec
     c                   if        ldlvre = 0
     c                   eval      vvarl1_vare = ldvare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   eval      d1sts1 = 'N'
     c                   endif
     c                   if        %found
     c                   move      d_fl_vnob     w_nobb
6.32 c                   if        w_lv_nobb <> 0
6.32 c                   if        w_lv_nobb <> d_fl_vnob
6.32 c                   eval      d1sts1 = 'N'
6.32 c                   endif
6.32 c                   else
     c                   if        vvnobb <> d_fl_vnob or
     c                             %subst(vvvare:1:8) <> w_nobb
     c                   eval      d1sts1 = 'N'
     c                   endif
     c                   endif
6.32 c                   endif
     c                   endif
      *
      * Sjekker pris og mengde
      *
     c                   if        b_feil = *off
     c                   if        d_fl_fkva <> ldanta
     c                   if        d1sts1 = *blank
     c                   eval      d1sts1 = 'M'
     c                   else
     c                   eval      d1sts2 = 'M'
     c                   endif
     c                   endif
      *
     c                   if        d_fl_fenh <> ldenh1
     c                   if        d1sts1 = *blank
     c                   eval      d1sts1 = 'E'
     c                   else
     c                   if        d1sts2 = *blank
     c                   eval      d1sts2 = 'E'
     c                   else
     c                   eval      d1sts3 = 'E'
     c                   endif
     c                   endif
     c                   endif
      *
     c                   if        d_fl_bnet <> 0 and
     c                             d_fl_bnet <> (ldipny * ldanta)
     c                   eval(h)   w_bbest = ldipny * ldanta
     c                   eval      w_bfakt = d_fl_bnet
     c                   exsr      sjk_toleranse
      *
     c                   if        b_diff = *on
     c                   if        d1sts1 = *blank
     c                   eval      d1sts1 = 'P'
     c                   else
     c                   if        d1sts2 = *blank
     c                   eval      d1sts2 = 'P'
     c                   else
     c                   if        d1sts3 = *blank
     c                   eval      d1sts3 = 'P'
     c                   else
     c                   eval      d1sts4 = 'P'
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c     end_stat      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne nobb-nummer på tillegg/fradrag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_nobb     begsr
      *
     c                   eval      w_enob = 0
      *
     c                   select
     c                   when      d_fh_tkod = 'FRA'
     c                   eval      w_enob = 26924696
     c                   when      d_fh_tkod = 'FC '
     c                   eval      w_enob = 26924696
     c                   when      d_fh_tkod = 'IN '
     c                   eval      w_enob = 26924696
     c                   when      d_fh_tkod = 'HD '
     c                   eval      w_enob = 26924761
     c                   when      d_fh_tkod = 'PI '
     c                   eval      w_enob = 26924803
     c                   when      d_fh_tkod = 'PBE'
     c                   eval      w_enob = 26924829
     c                   when      d_fh_tkod = 'PC '
     c                   eval      w_enob = 26924837
     c                   when      d_fh_tkod = 'ZEE'
     c                   eval      w_enob = 26924985
     c                   when      d_fh_tkod = 'RCH'
     c                   eval      w_enob = 26925024
     c                   when      d_fh_tkod = 'IS '
     c                   eval      w_enob = 26925032
     c                   endsl
      *
      *  Finnes nobbnr i eget vareregister
      *
     c                   eval      vvarl3_nobb = w_enob
     c     vvarl3_key    chain     vvarl3
     c                   if        not %found
     c                   eval      d4sts2 = 'N'
     c                   else
     c                   eval      d4sts2 = *blank
     c                   endif
      *
     c     end_nobb      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å beregne nettopris/nobb pris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_pris     begsr
      *
      * Nullstiller
      *
     c                   eval      w_tfra = 0
     c                   eval      w_ttil = 0
     c                   eval      w_kpri = 0
      *
      * Tillegg 1
      *
     c                   eval      w_frad = 0
     c                   if        d_fl_bel1  <> 0 and
     c                             d_fl_fkva  <> 0
     c                   eval(h)   w_frad = d_fl_bel1 / d_fl_fkva
     c                   else
     c                   if        d_fl_pro1 <> 0
     c                   eval(h)   w_frad = (d_fl_pris * d_fl_pro1 ) / 100
     c                   endif
     c                   endif
      *
     c                   if        d_fl_sig1 = 'A  '
     c                   eval      w_tfra = w_tfra + w_frad
     c                   else
     c                   eval      w_ttil = w_ttil + w_frad
     c                   endif
      *
      * Tillegg 2
      *
     c                   eval      w_kpri = d_fl_pris + w_ttil - w_tfra
     c                   eval      w_frad = 0
     c                   if        d_fl_bel2  <> 0 and
     c                             d_fl_fkva  <> 0
     c                   eval(h)   w_frad = d_fl_bel2 / d_fl_fkva
     c                   else
     c                   if        d_fl_pro2 <> 0
     c                   eval(h)   w_frad = (w_kpri * d_fl_pro2 ) / 100
     c                   endif
     c                   endif
      *
     c                   if        d_fl_sig2 = 'A  '
     c                   eval      w_tfra = w_tfra + w_frad
     c                   else
     c                   eval      w_ttil = w_ttil + w_frad
     c                   endif
      *
      * Tillegg 3
      *
     c                   eval      w_kpri = d_fl_pris + w_ttil - w_tfra
     c                   eval      w_frad = 0
     c                   if        d_fl_bel3  <> 0 and
     c                             d_fl_fkva  <> 0
     c                   eval(h)   w_frad = d_fl_bel3 / d_fl_fkva
     c                   else
     c                   if        d_fl_pro3 <> 0
     c                   eval(h)   w_frad = (w_kpri * d_fl_pro3 ) / 100
     c                   endif
     c                   endif
      *
     c                   if        d_fl_sig3 = 'A  '
     c                   eval      w_tfra = w_tfra + w_frad
     c                   else
     c                   eval      w_ttil = w_ttil + w_frad
     c                   endif
      *
      * Tillegg 4
      *
     c                   eval      w_kpri = d_fl_pris + w_ttil - w_tfra
     c                   eval      w_frad = 0
     c                   if        d_fl_bel4  <> 0 and
     c                             d_fl_fkva  <> 0
     c                   eval(h)   w_frad = d_fl_bel4 / d_fl_fkva
     c                   else
     c                   if        d_fl_pro4 <> 0
     c                   eval(h)   w_frad = (w_kpri * d_fl_pro4 ) / 100
     c                   endif
     c                   endif
      *
     c                   if        d_fl_sig4 = 'A  '
     c                   eval      w_tfra = w_tfra + w_frad
     c                   else
     c                   eval      w_ttil = w_ttil + w_frad
     c                   endif
      *
      * Tillegg 5
      *
     c                   eval      w_kpri = d_fl_pris + w_ttil - w_tfra
     c                   eval      w_frad = 0
     c                   if        d_fl_bel5  <> 0 and
     c                             d_fl_fkva  <> 0
     c                   eval(h)   w_frad = d_fl_bel5 / d_fl_fkva
     c                   else
     c                   if        d_fl_pro5 <> 0
     c                   eval(h)   w_frad = (w_kpri * d_fl_pro5 ) / 100
     c                   endif
     c                   endif
      *
     c                   if        d_fl_sig5 = 'A  '
     c                   eval      w_tfra = w_tfra + w_frad
     c                   else
     c                   eval      w_ttil = w_ttil + w_frad
     c                   endif
      *
      * Tillegg 6
      *
     c                   eval      w_kpri = d_fl_pris + w_ttil - w_tfra
     c                   eval      w_frad = 0
     c                   if        d_fl_bel6  <> 0 and
     c                             d_fl_fkva  <> 0
     c                   eval(h)   w_frad = d_fl_bel6 / d_fl_fkva
     c                   else
     c                   if        d_fl_pro6 <> 0
     c                   eval(h)   w_frad = (w_kpri * d_fl_pro6 ) / 100
     c                   endif
     c                   endif
      *
     c                   if        d_fl_sig6 = 'A  '
     c                   eval      w_tfra = w_tfra + w_frad
     c                   else
     c                   eval      w_ttil = w_ttil + w_frad
     c                   endif
      *
      * Nobb pris?
      *
     c                   if        d_fl_prkv = 'AAB'
     c                   eval      w_pris = d_fl_pris + w_ttil - w_tfra
     c                   else
     c                   eval      w_pris = d_fl_pris
     c                   endif
      *
      * Korr. - viser nettopris uten kalk. av tlg./fradrag uansett...
      *
     c                   if        d_fl_bnet <> *zero and
     c                             d_fl_fkva <> *zero
     c                   eval(h)   w_pris = d_fl_bnet / d_fl_fkva
     c                   endif
      *
     c     end_pris      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Sjekk om akseptabel prisdiff. ift. toleransegrenser/EDI
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_toleranse begsr
      *
     c                   eval      b_diff = *off
      *
      * Leser toleransetabell
      *
     c     ltoel1_key    chain     ltoel1
     c                   if        not %found
     c                   eval      b_diff = *on
     c                   leavesr
     c                   endif
      *
      * Sjekk om fakt.beløp lavere enn beløp under minste toleransegrense
      *
     c                   if        w_bfakt < laetg0
     c                   eval      b_diff = *on
     c                   leavesr
     c                   endif
      *
      * Sjekk om fakt.beløp lavere enn avviksgrense 1
      *
     c                   if        w_bfakt < laetg1
     c                   if        %abs(w_bfakt - w_bbest) > laeav1
     c                   eval      b_diff = *on
     c                   endif
     c                   leavesr
     c                   endif
      *
      * Sjekk om fakt.beløp lavere enn avviksgrense 2
      *
     c                   if        w_bfakt < laetg2
     c                   if        %abs(w_bfakt - w_bbest) > laeav2
     c                   eval      b_diff = *on
     c                   endif
     c                   leavesr
     c                   endif
      *
      * Sjekk om fakt.beløp lavere enn avviksgrense 3
      *
     c                   if        w_bfakt < laetg3
     c                   if        %abs(w_bfakt - w_bbest) > laeav3
     c                   eval      b_diff = *on
     c                   endif
     c                   leavesr
     c                   endif
      *
      * Sjekk om fakt.beløp lavere enn avviksgrense 4
      *
     c                   if        w_bfakt < laetg4
     c                   if        %abs(w_bfakt - w_bbest) > laeav4
     c                   eval      b_diff = *on
     c                   endif
     c                   leavesr
     c                   endif
      *
      * Sjekk om fakt.beløp høyere enn avviksgrense 4
      *
     c                   if        w_bfakt >= laetg4
     c                   if        %abs(w_bfakt - w_bbest) > laeav5
     c                   eval      b_diff = *on
     c                   endif
     c                   leavesr
     c                   endif
      *
     c     end_sjk       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    w_prec
     c                   eval      d_prec = w_prec
      *
      *  Key for les av MELDINGSLINJER
      *
     c     ledil1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ledil1_type
     c                   kfld                    ledil1_mnum
      *
      *  Key for oppslag på MELDINGSLINJER
      *
     c     ledilr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ledilr_type
     c                   kfld                    ledilr_mnum
     c                   kfld                    ledilr_mlin
      *
      * Key for les av BESTILLINGSHODE
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      * Key for les av BESTILLINGSLINJE
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtl1_numm
     c                   kfld                    lodtl1_suff
     c                   kfld                    lodtl1_line
      *
      * Key for les av LEVERANDØR
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for les av vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les av vare med nobbnr
      *
     c     vvarl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl3_nobb
      *
      * Key for les av FIRMA
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les av FORMULAR
      *
     c     aforl1_key    klist
     c                   kfld                    aforl1_ruti
      *
      * Key for oppslag på toleranse/EDI
      *
     c     ltoel1_key    klist
     c                   kfld                    w_firm
      *
      * Hente inn rutine informasjon
      *
     c                   eval      w_firm = d_firm
     c                   eval      aforl1_ruti = l_ruti
      *
     c     aforl1_key    chain     aforl1r
     c                   if        %found
     c                   eval      a1txt1 = aftxt1
     c                   endif
      *
      * Hent firma-opplysninger dersom de skal printes ut
      *
     c     afirl1_key    chain     afirl1r
     c                   if        %found and
     c                             afprfi = 1
     c                   eval      a1navn = aafnav
     c                   eval      a1adr1 = aafad1
     c                   eval      a1adr2 = aafad2
     c                   eval      a1sted = aafste
     c                   eval      *in31 = *on
     c                   endif
      *
      * Legger inn starverdier i nøkler
      *
     c                   eval      ledil1_type = d_type
     c                   eval      ledil1_mnum = d_mnum
     c                   eval      ledil1_mlin = 500000
     c                   eval      ledilr_type = d_type
     c                   eval      ledilr_mnum = d_mnum
     c                   eval      ledilr_mlin = d_mlin
     c     ledil1_key    setll     ledil1r
      *
     c                   endsr
