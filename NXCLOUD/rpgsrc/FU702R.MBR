     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FU702R
      * Beskrivelse . . : Sjekker om kode finnes fra før i loggen
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       280307 AMD  Nytt program
5.60  *       070108 bof  Utvidet nøkkel med år
6.NU  * R6.30 260514 bsa  Programmet gjennomgått mtp nummerutvidelser.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflogl1    if   e           k disk    rename(flogpfr:flogl1r)
      *
      * Definering av parametre
      *
     d p_aarr          s                   like(fuaarr)
6.NU d p_numm          s                   like(funumm)
     d p_suff          s                   like(fusuff)
     d p_kode          s                   like(fukode)
     d p_svar          s                   like(fukode)
      *
      * Definering av Local data area (LDA)
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d flogl1_aarr     s                   like(fuaarr)
     d flogl1_numm     s                   like(funumm)
     d flogl1_suff     s                   like(fusuff)
     d flogl1_kode     s                   like(fukode)
     d flogl1_date     s                   like(fudate)
     d flogl1_time     s                   like(futime)
      *
      * Definering av variable
      *
     d w_firm          s                   like(fufirm)
      *
      * --------------------------------------------------------------------
      * Hoved - logikk
      * --------------------------------------------------------------------
      *
      * Sjekker om en aktuell kode finnes
      * fra før i loggen og returnerer svaret
      *
     c                   exsr      hent_logg
      *
     c                   goto      avslutt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av aktuell kode i logg - filen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_logg     begsr
     c                   eval      p_svar = *blank
      *
      * Sjekker aktuell kode i logg - filen
      *
     c                   eval      flogl1_aarr = p_aarr
     c                   eval      flogl1_numm = p_numm
     c                   eval      flogl1_suff = p_suff
     c                   eval      flogl1_kode = p_kode
     c                   eval      flogl1_date = *hival
     c                   eval      flogl1_time = *hival
      *
     c     flogl1_key    setll     flogl1r
     c                   readp     flogl1r
      *
     c                   if        %eof
     c                   eval      p_svar = *blank
     c                   goto      end_sr
     c                   endif
      *
     c                   if        fuaarr = p_aarr and
     c                             funumm = p_numm and
     c                             fusuff = p_suff and
     c                             fukode = p_kode
     c                   eval      p_svar = 'J'
     c                   endif
      *
     c     end_sr        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_aarr
6.NU c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_kode
     c                   parm                    p_svar
      *
      * Key for logg - filen
      *
     c     flogl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    flogl1_aarr
     c                   kfld                    flogl1_numm
     c                   kfld                    flogl1_suff
     c                   kfld                    flogl1_kode
     c                   kfld                    flogl1_date
     c                   kfld                    flogl1_time
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
