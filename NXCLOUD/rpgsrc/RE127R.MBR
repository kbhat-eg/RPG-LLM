     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RE127R
      * beskrivelse . . : Legger ut posteringer fra Nexstep Arkiv
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       231052 KOB  Nytt program
      *       251206 KOB  Lagt inn test på kode for preregistrering
      *       251206 KOB  Henter bilagsnummer fra teller for inngående faktura
      *       091007 KOB  Lagt inn lagring av kid og regnskapsperiode i nytt reg
6.10  * V6.10 210909 BSA  Lagt inn sporingsfelter
6.20  *  6.20 011210 NHO  Prosjekt er alfa                               NHO  *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       30 = Protect av felter ved vise
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer
      *    60-69 = Fileindikatorer chain etc.
      *    70-79 = Arbeidsindikatorer i sub-rutiner
      *    80-89 = Arbeidsindikatorer ordinære
      *    90-99 = Benyttes ved std. sub-rutiner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fraa1l1    uf a e           k disk    rename(raa1pfr:raa1l1r)
     fraa4lu    uf a e           k disk    rename(raa4pfr:raa4lur)
     fra04l1    uf a e           k disk    rename(ra04pfr:ra04l1r)
     fra16l1    if   e           k disk    rename(ra16pfr:ra16l1r)
     fremspf    o  a e           k disk    rename(remspfr:remspfr)
     frkidpf    o  a e           k disk    rename(rkidpfr:rkidpfr)
      *
      *
      * Definering av variabler i nøkler
      *
     d raa4lu_4aar     s                   like(ra4aar)
     d ra16l1_pkod     s                   like(rapkod)
      *
      * Variabler
      *
      *
     d p_stat          s              1
     d p_tx20          s             20
     d p_akod          s                   like(rtbilk)
      *
     d w_dmaa          ds
     d w_dag                          2  0
     d w_mnd                          2  0
     d w_aar                          2  0
      *
     d w_dato          s              6  0
     d w_rper          s              2  0
     d w_raar          s              4  0
     d w_belo          s             11  2
      *
     d w_firm          s                   like(rtfirm)
     d w_biln          s                   like(rtbiln)
     d w_bilk          s                   like(rtbilk)
     d w_text          s                   like(rttext)
     d w_avde          s                   like(rtdavd)
     d w_kont          s                   like(rtdkto)
     d w_spes          s                   like(rtdsps)
     d w_mvak          s                   like(rtdmva)
     d w_kurs          s                   like(rapmku)
     d w_resn          s                   like(rtresn)
T550 d w_benr          s                   like(rybenr)
     d*ra1pre          s              1    inz('N')
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
     c                   eval      rtfirm = w_firm
     c                   eval      rtbunt = 0
     c                   eval      rtlinj = 0
     c                   eval      rtreid = 'T'
     c                   eval      rtstat = ' '
      *
      * Bilagsdato
      *
     c                   move      p_baar        w_aar
     c                   move      p_bmnd        w_mnd
     c                   move      p_bdag        w_dag
     c                   move      w_dmaa        w_dato
     c     *dmy          move      w_dato        rtbdat
      *
      * Valutakode/beløp
      *
     c                   if        p_valk <> 'NOK' and
     c                             p_valk <> ' '
     c                   eval      rtvalb = p_belo
     c                   eval      rtvalk = p_valk
     c                   else
     c                   eval      rtbelØ = p_belo
     c                   eval      rtvalb = 0
     c                   eval      rtvalk = ' '
     c                   endif
      *
      * Omregning fra Valuta til NOK
      *
     c                   if        p_valk <> 'NOK' and
     c                             p_valk <> ' '
     c                   move      p_valk        ra16l1_pkod
     c     ra16l1_key    chain     ra16l1
      *
     c                   if        %found(ra16l1)
      *
     c                   if        rtbdat > rapkda
     c                   eval      w_kurs = rapmku
     c                   else
     c                   eval      w_kurs = rapmkg
     c                   endif
      *
     c                   eval      rtbelØ = rtvalb * w_kurs
      *
      *  Snu fortegn hvis beløp negativt
      *
     c                   if        rtbelØ < 0
     c                   eval      rtbelØ = rtbelØ * -1
     c                   eval      rtvalb = rtvalb * -1
     c                   endif
      *
     c                   endif
      *
     c                   endif
      *
      * Regnskapsår/periode
      *
     c                   move      p_raar        rtraar
     c                   movel     p_rmnd        rtrper
     c                   move      p_raar        w_raar
      *
      * Hent bilagsnummer
      *
     c                   move      w_raar        raa4lu_4aar
     c     raa4lu_key    chain     raa4lu
     c
     c                   if        not%found(raa4lu)
     c                   eval      ra4fir = w_firm
     c                   eval      ra4aar = w_raar
     c                   eval      ra4sif = 1
     c                   write     raa4lur
     c                   else
     c                   eval      ra4sif = ra4sif + 1
     c                   update    raa4lur
     c                   endif
      *
     c                   eval      rtbiln = ra4sif
      *
     c                   move      p_resk        w_resn
T550 c                   move      p_benr        w_benr
      *
      * Faktura legges ut med negativt fortegn i flow
      *
     c                   if        w_belo <= 0
      *
      * Faktura
      * Debetkonto
      *
     c                   eval      rtdavd = rada21
     c                   eval      rtdkto = radk21
     c                   eval      rtdsps = rads21
     c                   eval      rtdmva = w_mvak
      *
      * Kreditkonto
      *
     c                   eval      rtcavd = 0
     c                   eval      rtckto = w_resn
     c                   eval      rtcsps = 0
     c                   eval      rtcmva = ' '
     c                   else
      *
      * Kreditnota
      * Debetkonto
      *
     c                   eval      rtdavd = 0
     c                   eval      rtdkto = w_resn
     c                   eval      rtdsps = 0
     c                   eval      rtdmva = ' '
      *
      * Kreditkonto
      *
     c                   eval      rtcavd = rada21
     c                   eval      rtckto = radk21
     c                   eval      rtcsps = rads21
     c                   eval      rtcmva = w_mvak
     c                   endif
      *
      * Prosjekt
      *
6.20 c**                 eval      rtpros = 0
6.20 c                   eval      rtpros = *blank
      *
      * Referansenr
      *
     c                   eval      rtrefn = 0
      *
      * Forfallsdato
      *
     c                   move      p_faar        w_aar
     c                   move      p_fmnd        w_mnd
     c                   move      p_fdag        w_dag
     c                   move      w_dmaa        w_dato
      *
     c     *dmy          move      w_dato        rtfdat
      *
      * Kontantrabatt
      *
     c                   eval      rtkrab = 0
      *
      * Aktivitet
      *
     c                   eval      rtakti = ' '
      *
      * Mengde/Mengdekode
      *
     c                   eval      rtmngd = 0
     c                   eval      rtmngk = 0
      *
      * Reskontrokonto
      *
     c                   eval      rtresn = w_resn
     c                   eval      rtresa = 0
     c                   eval      rtress = 0
     c                   eval      rtresm = ' '
      *
      * Virksomhet
      *
     c                   eval      rtvirk = ' '
      *
      * Attestasjonskode
      *
     c                   eval      rtatte = ' '
      *
      * Avkryssing
      *
     c                   eval      rtkrys = ' '
      *
      * Kid
      *
     c                   eval      rtkidi = %trimr(p_kidi)
      *
      * Bilagskode/tekst
      *
     c                   select
     c                   when      p_bilk = 'IF'
     c                   eval      rtbilk = 02
     c                   when      p_bilk = 'IK'
     c                   eval      rtbilk = 04
     c                   endsl
      *
     c                   move      rtbilk        p_akod
     c                   call      'RS701R'
     c                   parm                    p_stat
     c                   parm                    p_akod
     c                   parm                    p_tx20
      *
     c                   move      p_text        rttext
      *
      * Betalingsbetingelse
      *
     c                   eval      rtbetb = 0
      *
      * Bankreferanse
      *
     c                   eval      rtbref = ' '
      *
      * Deklarasjonskode
      *
     c                   eval      rtdeck = ' '
      *
      * Saldert/Krysset
      *
     c                   eval      rtautx = ' '
      *
      * Registreringsbilde
      *
     c                   eval      rtblde = ' '
      *
      * Leverandørens fakturanr.
      *
     c                   movel     p_lfak        rtlfak
      *
      * Sjekk om preregistreringsposter skal legges ut
      *
     c                   if        ra1pre = 'J' or
     c                             ra1pre = ' '
      *
     c                   write     remspfr
      *
     c                   else
      *
      * Hvis ikke, legg ut regnskapsperiode og KID  mm
      *
     C                   eval      ryfirm = w_firm
     c                   eval      rybiln = ra4sif
     C                   eval      ryraar = p_raar
     C                   eval      ryrper = p_rmnd
     c                   eval      rybdat = rtbdat
     c                   eval      ryfdat = rtfdat
     c                   eval      ryresn = w_resn
T550 c                   eval      rybenr = w_benr
     c                   eval      rylfak = p_lfak
     c                   eval      rybelo = p_belo
     c                   eval      rykidd = rtkidi
6.10 c                   time                    ryodat
6.10 c                   time                    ryotim
6.10 c                   move      l_user        ryousr
     c                   time                    ryedat
     c                   time                    ryetim
     c                   move      l_user        ryeusr
      *
     c                   write     rkidpfr
      *
     c                   endif
      *
      * Legg ut returfelter
      *
      * Valutakurs
      *
     c                   move      w_kurs        p_kurs
      *
      * Bilagsnummer
      *
     c                   move      rtbiln        p_bilag
      *
      * Bilagstekst
      *
     c                   movel     p_tx20        p_text
      *
     c                   eval      *inlr = '1'
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     C                   parm                    p_resk            6 0
     C                   parm                    p_lfak           20
     C                   parm                    p_kidi           25
     C                   parm                    p_belo            9 2
     C                   parm                    p_valk            3
6.20 C**                 parm                    p_pros            6 0
6.20 C                   parm                    p_pros            6
     C                   parm                    p_baar            4 0
     C                   parm                    p_bmnd            2 0
     C                   parm                    p_bdag            2 0
     C                   parm                    p_faar            4 0
     C                   parm                    p_fmnd            2 0
     C                   parm                    p_fdag            2 0
     C                   parm                    p_bilk            2
     C                   parm                    p_user           10
     C                   parm                    p_raar            4 0
     C                   parm                    p_rmnd            2 0
     C                   parm                    p_kurs            9 5
      *
6.NU C                   parm                    p_bilag           8 0
     C                   parm                    p_text           20
     C                   parm                    p_benr            6 0
      *
      * Key for å hente preregistreringskode
      *
     c     raa1l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for å hente interimskonto
      *
     c     ra04l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppdatering av sist brukte nummer
      *
     c     raa4lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    raa4lu_4aar
      *
      * Key for valutakoder
      *
     c     ra16l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra16l1_pkod
      *
     c                   move      l_firm        w_firm
      *
      * Henter preregistreringskode
      *
     c     raa1l1_key    chain     raa1l1
      *
      * Henter fast interimskonto
      *
     c     ra04l1_key    chain     ra04l1
      *
     c                   endsr
      *
