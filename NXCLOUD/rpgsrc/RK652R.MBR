     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK652R                                              *
      * Beskrivelse . . : Utskrift av betalingsvarsel-kontrolliste            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
T5.40 * V5.40     160806  Markerer når notat på bilaget                  HFL  *
 6.20 *           190215  Prosjekt er nå alfa mot notatblokk                  *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frkwbl2    if   e           k disk    rename(rkwbpfr:rkwbl2r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
T5.40frklal3    if   e           k disk    rename(rklapfr:rklal3r)
     frk652p    o    e             printer oflind(*in30)
      *
     d sub             s              3  0 dim(13)
      *
     d                uds
     d  pfirm                300    302  0
     d  pnavn                303    332
     d  puser                333    342
     d  pkdat                343    348  0
     d  praar                349    352  0
     d  prper                353    354  0
     d  pavdf                355    358  0
     d  pavdt                359    362  0
     d  pkatf                363    366  0
     d  pkatt                367    370  0
     d  pself                371    374  0
     d  pselt                375    378  0
     d  pknrf                379    384  0
     d  pknrt                385    390  0
     d  kun                  391    450  0 dim(10)
     d  pknr1                391    396  0
     d  pknr2                397    402  0
     d  pknr3                403    408  0
     d  pknr4                409    414  0
     d  pknr5                415    420  0
     d  pknr6                421    426  0
     d  pknr7                427    432  0
     d  pknr8                433    438  0
     d  pknr9                439    444  0
     d  pknr0                445    450  0
     d  pfdat                451    456  0
     d  plope                457    459  0
     d  ppkod                460    460
     d  ppros                461    463  1
     d  pgrns                464    465  0
     d  pgbyr                466    466
     d  pmini                467    468  0
     d  pbskr                469    469
     d  pegrn                470    470
     d  pajor                471    471
     d  ptext                472    472  0
     d  ppunr                473    473  0
     d  ppbeh                474    474
     d  palfa                475    475
     d  pdfgg                476    481  0
     d  pparam               300    490
      *
     d  l_wsid               901    910
     d  l_user               911    920
     d  l_fgrp               931    933
     d  l_firm               944    946  0
     d  l_fnav               951    980
      *
     d w_bdat          s              6  0
     d w_fdat          s              6  0
     d wkjdat          s              6  0
     d w_firm          s                   like(rnfirm)
     d w_kund_gml      s                   like(rnkund)
     d rkunl1_kund     s                   like(rnkund)
      *
T5.40d rklal3_syst     s                   like(rxsyst)
T5.40d rklal3_pros     s                   like(rxpros)
T5.40d rklal3_akti     s                   like(rxakti)
T5.40d rklal3_kund     s                   like(rxkont)
T5.40d rklal3_spes     s                   like(rxspes)
T5.40d rklal3_avde     s                   like(rxavde)
T5.40d rklal3_tist     s                   like(rxtist)
      *
     d w_forp          s             11  2
     d w_fort          s             11  2
     d n               s              3  0
      *
     c     dummy         tag
      *
     c     rkwbl2_key    setll     rkwbl2r                                61
     c     rkwbl2_key    reade     rkwbl2r                                61
      *
     c                   dow       *in61 = *off
      *
      * Brudd på kundenummer
      *
     c                   if        rnkund <> w_kund_gml
      *
     c                   if        w_kund_gml > *zeros
      *
     c                   write     t1tot
      *
     c                   endif
      *
     c                   eval      w_forp = 0
     c                   eval      w_komm = *blanks
      *
      * Les kunderegister
      *
     c                   eval      rkunl1_kund = rnkund
     c     rkunl1_key    chain     rkunl1                             10
      *
     c                   if        *in30 = *on                                               .
     c                   write     a1hdr                                                     .
     c                   eval      *in30 = *off                                              .
     c                   endif                                                           .....
      *
     c                   write     l1kun
      *
     c                   eval      w_kund_gml = rnkund
     c                   endif
      *
     c                   if        rksald <= 0 or
     c                             rkpneg = 'N'
     c                   goto      neste
     c                   endif
      *
      * Sjekk om reklamasjon eller 0 i restbeløp
      *
     c                   if        rnbilk = 89 or
     c                             rnrekl = 'J' or
     c                             rnbelØ = 0
     c                   goto      neste                                        BELØP NULL
     c                   endif
      *
      * Behandle bilagsdato og forfallsdato
      *
     c     *dmy          move      rnbdat        w_bdat                           BÅR
      *
     c                   if        rnfdat <> *loval                              KREDITPOSTER
     c                   eval      *in35 = *off                                   FFÅR
     c     *dmy          move      rnfdat        w_fdat                           FFÅR
     c                   else
     c                   eval      *in35 = *on                                    FFÅR
     c                   endif
      *
T5.40 * Sjekker om det finnes notatark på posten
      *
T5.40c                   exsr      notat
      *
      * Akkumuler totalt forfalt
      *
     c                   eval      w_forp = w_forp + rnrest                     FORFALT SALDO
     c                   eval      w_fort = w_fort + rnrest                     TOTALLT
      *
     c                   if        *in30 = *on                                               .
     c                   write     a1hdr                                                     .
     c                   eval      *in30 = *off                                              .
     c                   endif                                                           .....
      *
     c                   write     d1det
      *
     c     neste         tag
      *
     c     rkwbl2_key    reade     rkwbl2r                                61
     c                   enddo
      *
     c                   move      *on           *inlr
      *
     c                   write     t1tot
     c                   write     t2tot
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
T5.40 * Subrutine for å sjekke om notat på posten                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
T5.40c     notat         begsr
      *
T5.40 * Setter merke hvis det finnes notatblokk på posten
      *
T5.40c                   eval      *in73 = *off
T5.40c                   eval      rklal3_syst = 'K'                                         .
 6.20c**                 eval      rklal3_pros = *zeros                                      .
 6.20c                   eval      rklal3_pros = *blanks                                     .
T5.40c                   eval      rklal3_akti = *blanks                                     .
T5.40c                   eval      rklal3_kund = rnkund                                      .
T5.40c                   eval      rklal3_spes = *zero                                       .
T5.40c                   eval      rklal3_avde = *zero                                       .
T5.40c                   eval      rklal3_tist = rntist                                      .
T5.40c     rklal3_key    setll     rklal3                                 73
      *
T5.40c                   endsr
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c     rkwbl2_key    klist
     c                   kfld                    w_firm
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
T5.40 * Key for notatblokk postnivå
      *
T5.40c     rklal3_key    klist
T5.30c                   kfld                    w_firm
T5.40c                   kfld                    rklal3_syst
T5.40c                   kfld                    rklal3_pros
T5.40c                   kfld                    rklal3_akti
T5.40c                   kfld                    rklal3_kund
T5.40c                   kfld                    rklal3_spes
T5.40c                   kfld                    rklal3_avde
T5.40c                   kfld                    rklal3_tist
      *
     c                   eval      n = *zeros
     c                   move      l_firm        w_firm
      *
     c                   write     a1hdr
      *
     c                   endsr
      *
