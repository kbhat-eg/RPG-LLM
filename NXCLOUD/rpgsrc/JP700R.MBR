     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JP700R
      * Beskrivelse . . : Betingelse, oppdater fra Nobb Kontrakt - parameter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       030901 HKO  Nytt program
      *       300608 bof  Innlesing for flere filgrupper fra Master-nivå
6.31  *       070316 bof  Legg inn xxx/NOBB i mappen xxx = filgr.
7.xx  * 7.00  171116 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  *       250821 bof  Ny innlesing - xml - rutes til annen innlesing
7.01  *                   switch - NOBB_KONTR_XML
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     facphl1    if   e           k disk    rename(acphpfr:acphl1r)
     fjp700d    cf   e             workstn
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
      * Definering av variable i nøkler
      *
     d acphl1_hfgr     s                   like(achfgr)
     d acphl1_hfir     s                   like(achfir)
      *
     d w_firm          s                   like(achfir)
      *
7.01 d u_701           s              1    inz(*off)                            Endring 7.01
      *
     d                uds
5.54 d l_wsid                901    910
     d l_user                911    920
     d l_fgrp                931    933
5.54 d l_firm                944    946  0
     d l_fnav                951    980
      *
7.01  * Definering av eksterne prg
7.01  * BKV
7.01   dcl-s co402_verdi1  char(1);
7.01   dcl-s co402_verdi2  char(2048);
7.01   DCL-PR CO402R EXTPGM('CO402R');
7.01     p_filgrp            char(3)     const;
7.01     p_firm              packed(3:0) const;
7.01     p_lager             packed(2:0) const;
7.01     p_nokkel            char(50)    const;
7.01     p_verdi1            char(1);
7.01     p_verdi2            char(2048);
7.01   END-PR;
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
7.01 c                   if        u_701 = *on
7.01 c                   call      'JP705R'
7.01 c                   goto      avslutt
7.01 c                   endif
      *
      * Behandler bildet
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   goto      avslutt
     c                   endif
      *
      * Sjekker om gyldig filgruppe hvis masternivå er tastet
      *
     c                   if        b1fgrp <> *blank
     c                   eval      acphl1_hfgr  = b1fgrp
     c                   eval      acphl1_hfir  = w_firm
     c     acphl1_key    chain     acphl1
     c                   if        not %found(acphl1)
     c                   eval      *in31 = *on
     c                   goto      b1taga
     c                   endif
     c                   endif
      *
      * Kaller program for henting av data, avbryter dersom feil
      *
     c                   if        b1fldr = *blank
6.31 c                   eval      b1fldr = l_fgrp + '/' +
6.31 c                                      'NOBB'
     c                   endif
      *
     c                   call      'JP700C'
     c                   parm                    b1fldr
     c                   parm                    b_feil
      *
     c                   if        b_feil = *on
     c                   exfmt     b1msg
     c                   goto      b1taga
     c                   endif
      *
      * Kaller styreprogram
      *
     c                   if        b1fgrp = *blank
      * bare en filgruppe
     c                   call      'JP701R'
     c                   call      'JP702C'
     c                   parm                    b1fldr
     c                   parm                    l_fgrp
     c                   parm                    b_feil
     c                   else
      * flere filgr. ut fra master-filgr.
     c                   call      'JP702R'
     c                   parm                    w_firm
     c                   parm                    b1fgrp
     c                   parm                    b1fldr
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les på filkopiering-hode
      *
     c     acphl1_key    klist
     c                   kfld                    acphl1_hfgr
     c                   kfld                    acphl1_hfir
      *
      * Initierer skjermbildet
      *
6.31 c                   eval      b1fldr = l_fgrp + '/' +
6.31 c                                      'NOBB'
      *
     c                   eval      w_firm = l_firm
7.01  *
7.01  * Endring 7.01
7.01  *
7.01   CallP CO402R(l_fgrp:w_firm:0:'NOBB_KONTR_XML':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_701 = *on;
7.01   endif;
      *
     c                   endsr
