     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LH712R
      * Beskrivelse . . : Overføring til bestilling - overføringen
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       250100 ASK  Nytt program
      *       090101 HKO  Programmet hentet innkjøpsenhet m/pris selv om
      *                   enhet var registrert i bestillingsforslaget.
      *                   Henter nå innkjøpspris på registrert enhet, og
      *                   benytter innkjøpsenhet kun i de tilfeller hvor
      *                   enhet ikke er registrert
      *       160604 bø   Oppdatere best.forsl. med best.nr.
      *       070606 bø   Tester på firma
      *       090307 bof  Gjør alltid om til innkj.enhet hvis l.enh<>i.enh.
      *       221107 bof  Hent avd. fra lager når bestilling dannes
5.60  *       060109 bof  Gjør ikke om enhet hvis opprinn. er RADIOTERMenh.
5.70  * PRGR  021208 bof  Utvidet med prisgruppe
      * 6.10  170609 BSA  Oppdatert med sporingsinformasjon
6.10  * 6.10  020909 bsa  Referansfelter er utvidet fra 20 til 30 posisjoner
6.10  * 6.10              Hvis ref. hentes fra user, må vi 'venstrestille'
6.10  * 6.10  220909 ASK  Nye parametre til VL001R
6.11  * 6.10  161109 BSA  Nye parametre til VP753R. (prisgruppe)
6.12  * 6.10  090610 bof  Utvidet parm-felt  til VL001R
6.13  *       040712 bof  Justert rettelse vedr. vår ref.
6.14  *       090812 bof  Justert rettelse vedr. vår ref. II
6.20  *       240310 bof  Hvis best.forslag med ordrenr må ordrenr oppdat. best.
6.20  *       120410 bof  Hvis leveringstype = 1,så hentes kundens navn /adr.
6.21  *       010610 bof  Utvidet med test mot LVR,hvis skaffevare
6.22  *       130810 bof  Hvis VA vare,så settes LDLVRE (dvs. vare fra LVR)
6.22  *                   Har også oppdat varegrupper
6.23  *       040910 bof  Best.nr legges på ordre ved dann av bestilling
6.24  *       151210 bof  Utvidet med tekstlinjer på best.forslag--> bestilling
6.25  *       151210 bof  Hvis kundeordre så brukes selger fra bruker
6.26  *       291210 bof  Feilretting vedr. innk.pris fra RT bestillinger
6.27  * 6.20  231110 nho  Proj er nå alfa
6.28  *       010711 bof  Tar en clear på lohepf før man leggger inn nye best.
6.29  *       261011 bof  Hvis best.fors. fra ordre skal kost-og innk.pri hentes
      *                   fra ordre.
6.2A  * 6.20  120112 bsa  Utvidet VP750R og VP753R med kampanje
6.2b  * 6.20  050412 bof  Feilretting vedr. hente lager
6.2c  * 6.20  060612 bof  Hvis skaffevare skal ikke innk.pris hentes i VP750R
6.2d  *       160113 bof  Henter ordreinfo vha. onr på best.f.heading
6.2e  *       170113 bof  Feilretting mht. adresse2.
6.2f  *       250113 bof  Opprydding i logikk. kun et best.forslag
6.2f  *                   behandles hver gang.
6.2e  *       140313 bof  Lagt inn noe logikk for sjekk av ulogiske ting
6.2g  *       240913 bof  Skal ikke bruke forslagdato på bestilling
6.2h  *       301013 sst  Hente prosjekt fra ordre ved gen av bestilling
6.2i  *       181113 bsa  Sjekker om det er kundeprosjekt på ordren før
6.2i  *                   en henter adressen herfra. NB Påvirker 6.2H
6.2i  *                   Skal ikke gjøres på lagerordre.
6.2j  *       040116 bof  Hvis lev.typ = 1, ikke oppdater lager.
6.30  *       060214 bsa  Legger ut statuskode på linja, hvis det er
6.30  *                   erstatningsvare.
6.31  *       070214 bof  Sjekke om vare har kampanje, bruker evt. innkj.pris
6.32  *       120514 bkv  Henter gtin
6.num * 6.30  260513 bof  Utivelse ihht. nummerutvidelse
6.33  * 6.30  220914 bsa  Legger ut mvakode på linjenivå.
6.34  *       290515 bsa  Sjekker kun hvis BM modul
6.35  * 6.30  040615 bkv  Bestilling økt fra 6 til 8
6.36  * 6.30  301015 bof  Feilretting vedr. prisgruppe
6.37  * 6.30  190816 bof  Kaller VP751R istf. VP750R, med lev.nr+lager.
6.38  * 6.30  090617 nho  Avdeling fra forslaget skal overstyre avdel.
      *                   fra lageret
6.39  * 6.30  020117 bof  sjekk om enh er best.bar for lev.nr - behold
6.3a  * 6.30  160218 nho  Endring vedr. hent av innkjøpspris
6.3b  * 6.30  081018 bof  Utvidet med trelast sortiment
6.3c  * 6.30  270519 bof  Henter innkj.pris fra evr kampanje. (BM tilp.)
6.3d  * 6.30  050519 bkv  Henter prosjekt fra ordrehode også ved lety 0
8.01  * 8.00  280421 ask  Regner om innkjøpspris til valuta ved valuta på leverandør
8.02  * 8.00  110222 ask  Lagt inn danning av post i tilleggsregister LOTIST
      *                   dersom opprinnelseskode er 'B' (Blueridge)
8.03  * 8.00  010322 bsa  Kaller samme program som ved vedlikehold av linjer
8.03  * 8.00              med lager som parameter. (VP754 erstatter VP753)
8.04  *PRG2   150622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flfdtl2    if   e           k disk    rename(lfdtpfr:lfdtl2r)
     flfdtl1    if   e           k disk    rename(lfdtpfr:lfdtl1r)
     flfhel1    if   e           k disk    rename(lfhepfr:lfhel1r)
     flfhelu    uf   e           k disk    rename(lfhepfr:lfhelur)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     flohelu    uf a e           k disk    rename(lohepfr:lohelur)
     flodtlu    uf a e           k disk    rename(lodtpfr:lodtlur)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
     fvvenlr    if   e           k disk    rename(vvenpfr:vvenlrr)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
6.21 fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
6.20 ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
6.23 ffohelu    uf   e           k disk    rename(fohepfr:fohelur)
6.20 ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
6.20 ffodtlu    uf   e           k disk    rename(fodtpfr:fodtlur)
6.20 frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
6.33 fvmval1    if   e           k disk    rename(vmvapfr:vmval1r)
6.30 fvverl1    uf   e           k disk    rename(vverpfr:vverl1r)
      *
      * Parameter-inn
      *
     d                 ds
6.nu d d_prec                        17
6.nu d  d_fbes                        8  0 overlay(d_prec)
6.nu d  d_tbes                        8  0 overlay(d_prec:9)
6.nu d  d_sort                        1  0 overlay(d_prec:17)
      *
      * Datastruktur for lageroppdatering
      *
     d                 ds
6.nu d h_rec                        128
     d  h_vare                       15    overlay(h_rec)
     d  h_lage                        2  0 overlay(h_rec:16)
     d  h_dato                         d   overlay(h_rec:18) datfmt(*iso)
     d  h_time                         t   overlay(h_rec:28) timfmt(*iso)
     d  h_otyp                        2    overlay(h_rec:36)
     d  h_ltyp                        2    overlay(h_rec:38)
     d  h_anta                       11  3 overlay(h_rec:40)
     d  h_kopr                        9  2 overlay(h_rec:51)
     d  h_refr                       20    overlay(h_rec:60)
     d  h_syst                        1    overlay(h_rec:80)
     d  h_altk                        2    overlay(h_rec:81)
     d  h_rest                        1    overlay(h_rec:83)
     d  h_type                        1    overlay(h_rec:84)
     d  h_enhe                        3    overlay(h_rec:85)
     d  h_inlr                        1    overlay(h_rec:88)
     d  h_lety                        1    overlay(h_rec:89)
6.nu d  h_onum                        8  0 overlay(h_rec:90)
6.nu d  h_olin                        4  0 overlay(h_rec:98)
      *
      * Local Data Area
      *
     d                uds
      *
     d l_wsid                901    910
     d l_user                        10
6.3c d l_filg                931    933
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d lfdtl2_numm     s                   like(lfnumm)
     d lfdtl2_ldor     s                   like(lfldor)
     d lfdtl2_vare     s                   like(lfvare)
     d lfdtl2_line     s                   like(lfline)
     d lfdtl1_numm     s                   like(lfnumm)
     d lfhel1_numm     s                   like(lhnumm)
     d lfhelu_numm     s                   like(lhnumm)
     d lodtlu_line     s                   like(ldline)
     d lodtlu_numm     s                   like(ldnumm)
     d lodtlu_suff     s                   like(ldsuff)
     d lohelu_numm     s                   like(lonumm)
     d lohelu_suff     s                   like(losuff)
     d rlevl1_levr     s                   like(rllevr)
     d fusrl1_user     s                   like(fbuser)
     d vvenlr_vare     s                   like(vevare)
     d vvarl1_vare     s                   like(vvvare)
6.21 d jvarl1_vare     s                   like(jvvare)
6.20 d fohel1_numm     s                   like(fonumm)
6.20 d fohel1_suff     s                   like(fosuff)
6.23 d fohelu_numm     s                   like(fonumm)
6.23 d fohelu_suff     s                   like(fosuff)
6.20 d fodtl1_numm     s                   like(fdnumm)
6.20 d fodtl1_suff     s                   like(fdsuff)
6.20 d fodtl1_line     s                   like(fdline)
6.20 d fodtlu_numm     s                   like(fdnumm)
6.20 d fodtlu_suff     s                   like(fdsuff)
6.20 d fodtlu_line     s                   like(fdline)
6.20 d rkunl1_kund     s                   like(rkkund)
6.30 d vverl1_envr     s                   like(vvenvr)
6.33 d vmval1_mkod     s                   like(vamkod)
      *
      * Definering av variable
      *
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_firm          s                   like(lofirm)
     d w_fast          s             10
     d w_fell          s              6
     d w_kode          s              1
     d w_stat          s              1
6.39 d w_retk          s              1
     d w_skaf          s              1
     d w_enor          s              1
     d w_onum          s                   like(ldnumm)
     d w_toti          s                   like(lototi)
6.20 d w_totk          s             11  2
6.20 d w_totr          s             11  2
     d w_type          s              2
     d w_ldor          s                   like(lfldor)
     d w_lage          s                   like(lhlage)
     d w_avde          s                   like(lhavde)
6.2b d w_avd1          s                   like(lhavde)
     d w_fnum          s                   like(lfnumm)
6.nu d w_parm          s             17
     d w_lety          s              1  0
     d b_feil          s              1
6.24 d b_kord          s              1
6.2c d b_skaf          s              1    inz(*off)
     d w_ponr          s              4  0
     d w_eann          s             13  0
     d w_ponr_alf      s              4
     d w_sted          s                   like(lonavn)
     d w_adr1          s                   like(loadr1)
     d w_adr2          s                   like(loadr2)
     d w_pste          s                   like(losted)
6.nu d w_hrec          s            128
     d w_v001          s              1
     d w_omrl          s                   like(veomre)
     d w_omri          s                   like(veomre)
8.04 d w_prgr          s              2
6.35 d w_numa          s              8
5.02 d w_lina          s              4
6.13 d w_ikod          s                   like(lhselg)
6.13 d w_itxt          s             30
6.13 d w_ista          s              1
6.21 d w_nobb          s                   like(vvnobb)
6.22 d w_lvre          s                   like(ldlvre)
6.22 d w_ogrp          s                   like(ldogrp)
6.22 d w_hgrp          s                   like(ldhgrp)
6.22 d w_ugrp          s                   like(ldugrp)
6.2A d w_kamp          s              5
6.2h d w_proj          s                   like(foproj)
6.32 d w_vare          s                   like(lfvare)
6.32 d w_enhe          s                   like(lfenh1)
6.32 d w_gtin          s                   like(ldeann)
6.32 d w_psta          s              1
6.34 d w_disp          s              1
6.34 d w_msts          s              1
6.34 d w_modu          s             10
6.3a d b_inlr          s              1    inz(*off)
6.3a d w_kpny          s                   like(ldkpny)
6.3a d w_udat          s               d   datfmt(*iso)
6.3a d w_vtyp          s                   like(vvvtyp)

6.3b d w_utda          s                   like(lfedat)
6.3b d w_lv_nobb       s                   like(vvnobb)
6.3b d w_lv_modn       s                   like(vvnmod)
6.3b d w_lv_lldo       s                   like(vvldor)
6.3b d w_lv_llva       s                   like(vvlvar)
6.3c d w_vkai          s              9  2
8.01 d w_kurs          s             10  6
8.01 d w_odat          s               d   datfmt(*iso)
      *
     d b_hent_lag      s              1
6.20 d b_hent_ordre    s              1    inz(*off)
6.34 d b_bmpp          s              1    inz(*off)
      *
6.3c d u_63c           s              1    inz(*off)                            Endring 6.3c
6.3c  * Definering av eksterne prg
6.3c  *
6.3c   dcl-s co402_verdi1  char(1);
6.3c   dcl-s co402_verdi2  char(2048);
6.3c   DCL-PR CO402R EXTPGM('CO402R');
6.3c     p_filgrp            char(3)     const;
6.3c     p_firm              packed(3:0) const;
6.3c     p_lager             packed(2:0) const;
6.3c     p_nokkel            char(50)    const;
6.3c     p_verdi1            char(1);
6.3c     p_verdi2            char(2048);
6.3c   END-PR;
      *
6.34 d c_modu          c                   'BM_MODUL  '
      *
     C/Exec SQL
     C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR,
     C+             commit=*none
     C/End-Exec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram - leser poster fra bestillingsforslag-linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.24  *  henter bestillingsforslag heading
6.24 c                   eval      b_kord = *off
6.24 c                   eval      lfhel1_numm = d_fbes
6.24 c     lfhel1_key    chain     lfhel1r                            93
6.24 c                   if        lhornr > 0
6.24 c                   eval      b_kord = *on
6.24 c                   endif
      *
     c                   eval      b_hent_lag   = *off
6.33  * Hent info om leverandør pga mvakode
6.33 c                   exsr      hent_ldor
      *
     c                   exsr      hent_prisgr
      *
      *  henter nytt bestillingsnr
      *
6.2f c                   exsr      hent_ordrenr
6.2f  *
6.2f  * leser best.forslag detaljer
6.2f  *
6.24 c                   if        b_kord = *on
6.24 c                   read      lfdtl1r                                90
6.24 c                   else
     c                   read      lfdtl2r                                90
6.24 c                   endif
      *
     c                   dow       *in90 = *off
     c                   if        lffirm <> w_firm or
     c                             lfnumm > d_tbes
     c                   goto      les_slutt
     c                   endif
      *
6.24 c                   if        (lfldor <> 0 and
6.24 c                             lfanta > 0) or
6.24 c                             lfltyp = 'TX'
6.24 c                   exsr      ordre_linje
     c                   endif
6.24 c                   if        b_kord = *on
6.24 c                   read      lfdtl1r                                90
6.24 c                   else
6.24 c                   read      lfdtl2r                                90
6.24 c                   endif
     c                   enddo
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Slutt- oppgaver
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     les_slutt     tag
      *
     c                   exsr      ordre_hode
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
6.3c c                   if        u_63c = *on
6.3c c                   eval      b_inlr = *off
6.3c c                   call      'VT592R  '
6.3c c                   parm                    w_firm
6.3c c                   parm                    vvvare
6.3c c                   parm                    w_enhe
6.3c c                   parm                    w_vkai
6.3c c                   parm                    b_inlr
6.3c c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Legger ut ny ORDRE-LINJE
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ordre_linje   begsr
      *
      * Les vareinformasjon
      *
6.21 c                   eval      w_nobb = 0
6.2c c                   eval      b_skaf = *off
     c                   movel     lfvare        vvarl1_vare
     c     vvarl1_key    chain     vvarl1
6.21 c                   if        not %found(vvarl1)
6.21 c                   movel     lfvare        jvarl1_vare
6.21 c     jvarl1_key    chain     jvarl1
6.21 c                   if        %found(jvarl1)
6.2c c                   eval      b_skaf = *on
6.21 c                   eval      w_nobb = jvnobb
6.22 c                   eval      w_lvre = 1
6.22 c                   eval      w_ogrp = jvogrp
6.22 c                   eval      w_hgrp = jvhgrp
6.22 c                   eval      w_ugrp = jvugrp
6.21 c                   endif
6.21 c                   else
6.21 c                   eval      w_nobb = vvnobb
6.22 c                   eval      w_lvre = 0
6.22 c                   eval      w_ogrp = vvogrp
6.22 c                   eval      w_hgrp = vvhgrp
6.22 c                   eval      w_ugrp = vvugrp
6.21 c                   endif
      * henter info om logistikk-vare
6.3b c                   call      'VL710R'
6.3b c                   parm                    w_firm
6.3b c                   parm                    lfvare
6.3b c                   parm                    lflage
6.3b c                   parm                    w_vtyp
6.3b c                   parm                    w_utda
6.3b c                   parm                    w_ldor
6.3b c                   parm                    b_inlr
6.3b c                   parm                    w_lv_nobb
6.3b c                   parm                    w_lv_modn
6.3b c                   parm                    w_lv_lldo
6.3b c                   parm                    w_lv_llva
      *
      * Utlegging av rader på bestillings-linjer
      *
6.28 c                   clear                   lodtlur
     c                   eval      lodtlu_line = lodtlu_line + 10
     c     lodtlu_key    chain     lodtlur                            92
      *
     c                   eval      ldotyp = lhotyp
6.2e c                   if        lfltyp = 'TX' and
6.2e c                             lfvare = *blank
6.24 c                   eval      ldltyp = lfltyp
6.24 c                   else
     c                   eval      ldltyp = laalty
6.24 c                   endif
     c                   eval      ldvare = lfvare
     c                   eval      ldenh1 = lfenh1
6.3b c                   if        w_lv_nobb <> 0
6.3b c                   eval      ldnobb = w_lv_nobb
6.3b c                   eval      ldlvar = w_lv_llva
6.3b c                   else
6.21 c                   eval      ldnobb = w_nobb
6.3b c                   eval      ldlvar = lflvar
6.3b c                   endif
6.22 c                   eval      ldlvre = w_lvre
6.22 c                   eval      ldogrp = w_ogrp
6.22 c                   eval      ldhgrp = w_hgrp
6.22 c                   eval      ldugrp = w_ugrp
      *
6.2c c                   if        lhoppr <> 'O'
6.2c c                   if        b_skaf = *off
     c                   exsr      hent_inpri
6.2c c                   endif
6.2c c                   endif
      *
6.32 c                   exsr      hent_gtin
      *
6.31 c                   exsr      sjek_kamp
6.31  *
      *
6.2e c                   if        lhoppr =  'O'
6.20 c                   exsr      hent_orli
6.2e c                   endif
6.2d c                   eval      w_lage = lflage
     c                   if        b_hent_lag = *off
     c                   exsr      hent_lager
     c                   eval      b_hent_lag = *on
     c                   endif
      *
     c                   eval      ldlage = lflage
     c                   eval      w_lage = lflage
     c                   eval      ldldor = lfldor
6.38 c*                  eval      ldlvar = lflvar
      *
     c                   eval      ldldat = lhldat
6.38 c                   if        lhavde <> *zero
6.38 c                   eval      ldavde = lhavde
6.38 c                   else
     c                   eval      ldavde = w_avde
6.38 c                   endif
     c                   eval      ldkont = 0
     c                   eval      ldspes = 0
6.27 c**                 eval      ldproj = 0
6.27 c                   eval      ldproj = *blank
     c                   eval      ldakti = *blank
6.20 c                   eval      ldornu = lfornr
6.20 c                   eval      ldorsu = lforsu
6.20 c                   eval      ldorli = lforli
6.20 c                   eval      ldlety = lflety
6.20 c                   eval      ldldat = lfldat
      *
     c                   eval      ldtek1 = lftek1
     c                   eval      ldtek2 = lftek2
      *
5.60 c                   if        lhoppr <> 'R'
      *
     c                   if        ldenh1 <> lfenh1
     c                   exsr      hent_omr
     c                   if        w_omrl <> 0
     c                   eval      ldanta = lfanta * w_omrl / w_omri
     c                   else
     c                   eval      ldanta = lfanta
     c                   endif
     c                   else
     c                   eval      ldanta = lfanta
     c                   endif
5.60  *
5.60 c                   else
5.60 c                   eval      ldanta = lfanta
5.60 c                   endif
      *
6.26 c                   if        lhoppr =  'O' and
6.26 c                             %found(fodtl1)
6.2c c                   eval      ldipny = fdinpr
6.20 c                   eval      ldkpny = fdkopr
6.20  * innkjøpsprisen bestilling  settes lik kostpris på ordre
6.20  * Dette skal paramterstyres i 6.20
6.20  *
6.20 c                   if        laaenl = 1
6.20 c                   eval      ldipny = ldkpny
6.3a  *   Logikk hentet fra FO734R
6.3a c                   else
6.3a c***                endif
6.3a c***                endif
6.3a  *
6.3a c***                if        ldipny = *zero
6.3a c***                eval      ldipny = ldkpny
6.3.ac***                endif
      *
6.3a c                   if        ldipny = *zero
6.3a  *
6.3a  * Finner innkjøpspris fra register dersom den skal overstyre.
6.3a  * Gjelder bare lagervarer.
6.3a  *
6.3a c                   eval      vvarl1_vare = fdvare
6.3a c     vvarl1_key    chain     vvarl1r                            90
6.3a  *
6.3a c                   if        *in90 = *off
6.3a  * finner varetype
6.3a c                   call      'VL710R  '
6.3a c                   parm                    w_firm
6.3a c                   parm                    fdvare
6.3a c                   parm                    fdlage
6.3a c                   parm                    w_vtyp
6.3a c                   parm                    w_udat
6.3a c                   parm                    w_ldor
6.3a c                   parm                    b_inlr
6.3b c                   parm                    w_lv_nobb
6.3b c                   parm                    w_lv_modn
6.3b c                   parm                    w_lv_lldo
6.3b c                   parm                    w_lv_llva
6.3a c                   if        w_vtyp = 'L'
6.3a c                   eval      w_lety = 0
6.3a c                   eval      w_prgr = fdprgr
6.3a c                   eval      w_kamp = lokamp
6.3a  *
8.03 c**                 call      'VP753R'
8.03 c                   call      'VP754R'
6.3a c                   parm                    w_firm
6.3a c                   parm                    fdvare
6.3a c                   parm                    w_prgr
6.3a c                   parm                    fdenh1
6.3a c                   parm                    w_lety
6.3a c                   parm                    w_date
6.3a c                   parm                    w_kpny
6.3a c                   parm                    ldipny
6.3a c                   parm                    w_kamp
8.03 c                   parm                    lflage
6.3a c                   endif
6.3a c                   endif
6.3a c                   endif
6.3a c                   endif
6.3a c                   endif
6.3a  *
6.20 c                   if        ldipny = *zero
6.20 c                   eval      ldipny = ldkpny
6.20 c                   endif
      *
     c                   eval      ldrab1 = 0
     c                   eval      ldrab2 = 0
     c                   eval      ldsumi = 0
     c                   eval      ldsumr = 0
     c                   eval      ldsumk = 0
6.33  * Beregn mvakode
6.33 c                   if        rlmkod = 1
6.33 c                   eval      ldomva = '0'
6.33 c                   else
6.33 c                   if        w_lvre = 1
6.33 c                   eval      ldomva = '1'
6.33 c                   else
6.33  * Sjekk overstyring fra varen (default mva 25%)
6.33 c                   eval      ldomva = '1'
6.33 c                   eval      vmval1_mkod = vvkmva
6.33 c     vmval1_key    chain     vmval1
6.33 c                   if        %found
6.33 c                   eval      ldomva = vamkla
6.33 c                   else
6.33 c                   select
6.33 c                   when      vvkmva = 1
6.33 c                   eval      ldomva = '0'
6.33 c                   when      vvkmva = 2
6.33 c                   eval      ldomva = 'I'
6.33 c                   when      vvkmva = 3
6.33 c                   eval      ldomva = 'K'
6.33 c                   endsl
6.33 c                   endif
6.33 c                   endif
6.33 c                   endif
8.01  *
8.01  * Regner om innkjøpspris til utenlands valuta på leverandør med valuta
8.01  *
8.01 c                   if        rlvalk <> '   ' and
8.01 c                             rlvalk <> 'NOK'
8.01 c                   time                    w_odat
8.01 c                   exsr      valuta_pris
8.01 c                   eval(h)   ldipny = ldipny / w_kurs
8.01 c                   endif
      *
     c                   if        *in92 = *on
     c                   eval      ldfirm = w_firm
     c                   eval      ldnumm = lodtlu_numm
     c                   eval      ldsuff = lodtlu_suff
     c                   eval      ldline = lodtlu_line
6.10 c                   time                    ldodat
6.10 c                   time                    ldotim
6.10 c                   eval      ldousr = l_user
6.10 c                   time                    ldedat
6.10 c                   time                    ldetim
6.10 c                   eval      ldeusr = l_user
     c                   write     lodtlur
6.20 c                   eval      w_totk = w_totk + ldsumk
     c                   eval      w_toti = w_toti + (ldipny * ldanta)
6.20 c                   eval      w_totr = w_totr + ldsumr
6.2j c                   if        lflety <> 1
     c                   exsr      oppd_lager
6.2j c                   endif
6.2e c                   if        lhoppr =  'O'
6.20 c                   exsr      upd_orli
6.2e c                   endif
     c                   endif
6.30  * Hvis dette er en erstatningsvare, skal dette flagges
6.34  * Kun hvis det er BM modul
6.34 c                   if        b_bmpp = *on
6.30 c                   eval      vverl1_envr = ldvare
6.30 c     vverl1_key    chain     vverl1
6.30 c                   if        %found
6.30 c                   eval      vvantb = vvantb + 1
6.30 c                   update    vverl1r
6.30 c                   endif
6.34 c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Legger ut nytt ORDRE-HODE
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ordre_hode    begsr
      *
     c                   if        lodtlu_line = 0
     c                   goto      hode_slutt
     c                   endif
      *
6.28 c                   clear                   lohelur
     c     lohelu_key    chain     lohelur                            94
      *
6.20 c                   if        lhlety = 0
6.20 c                   exsr      hent_lager
6.20 c                   exsr      hent_logp
6.2h c**6.2I             exsr      hent_ordre
6.3d c*                   eval      w_proj = foproj
6.3d c/exec sql
6.3d c+ select ifnull(foproj, '') into :w_proj from fohepf  where
6.3d c+   fofirm = :w_firm and fonumm = :lhornr and fosuff = :lhorsu
6.3d c/end-exec
6.2h c                   clear                   fohel1r
6.20 c                   else
6.20 c                   exsr      hent_ordre
6.2h c                   eval      w_proj = foproj
6.20 c                   endif
     c                   exsr      hent_levr
     c                   exsr      hent_bruker
     c                   eval      loline = 0
     c                   eval      lobinr = 0
     c                   eval      lokref = 0
      *
     c                   eval      lootyp = lhotyp
     c                   eval      lolage = lhlage
6.25 c                   if        b_kord = *on and
6.25 c                             fbselg <> 0
6.25 c                   eval      loinnk = fbselg
6.25 c                   else
     c                   eval      loinnk = lhselg
6.25 c                   endif
6.38 c                   if        lhavde <> *zero
6.38 c                   eval      loavde = lhavde
6.38 c                   else
     c                   eval      loavde = w_avde
6.38 c                   endif
6.2g c*                  eval      lobdat = lhbdat
6.2g c                   time                    lobdat
     c                   eval      loldat = lhldat
      *
     c                   eval      lofkda = *loval
     c                   eval      loffda = *loval
     c                   eval      lomoda = *loval
     c                   eval      lolele = 0
      *
6.2f c                   eval      loldor = lhldor
      *
6.2e c**                 eval      loadr2 = *blank
      *
     c                   eval      lokont = 0
     c                   eval      lospes = 0
6.2h c**                 eval      loproj = *blank
6.2h c                   eval      loproj = w_proj
6.2h c                   eval      loarbo = w_proj
     c                   eval      loakti = *blank
      *
     c                   eval      lokspr = 0
6.10 c**                 eval      lovref = fbvref
6.10 c                   movel     fbvref        lovref
     c                   eval      lodref = *blank
     c                   eval      lorekv = *blank
     c                   eval      lolmtx = *blank
     c                   eval      lolbtx = *blank
      *
     c                   eval      lorabp = 0
6.20 c                   eval      loornr = lhornr
6.20 c                   eval      loorsu = lhorsu
     c                   eval      lonett = 0
     c                   eval      lobrut = 0
     c                   eval      lokoli = 0
     c                   eval      lokonr = 0
     c                   eval      lototx = 0
     c                   eval      lototf = 0
      *
     c                   eval      lototi = w_toti
     c                   eval      lototr = 0
     c                   eval      lototk = 0
      *
     c                   eval      lowsid = l_wsid
     c                   eval      louser = l_user
     c                   eval      lodate = w_date
     c                   eval      lotime = w_time
      *
     c                   eval      lokode = 0
     c                   eval      lobind = *blank
      *
     c                   eval      lokjor = 0
     c                   eval      losped = 0
      *
     c                   if        *in94 = *on
     c                   eval      lofirm = w_firm
     c                   eval      lonumm = lohelu_numm
     c                   eval      losuff = lohelu_suff
6.10 c                   time                    lodate
6.10 c                   time                    lotime
6.10 c                   eval      louser = l_user
6.10 c                   time                    loedat
6.10 c                   time                    loetim
6.10 c                   eval      loeusr = l_user
     c                   write     lohelur
     c                   endif
     c                   eval      w_toti = 0
6.20 c                   eval      w_totk = 0
6.20 c                   eval      w_totr = 0
8.02  *
8.02  * Legger ut post i LOTIST dersom opprinnelse er 'B' Blueridge
8.02  *
8.02   /Free
8.02    if lhoppr = 'B';
8.02      exec sql
8.02       insert into lotist
8.02         (lotfir, lotnum, lotsuf,lotko1)
8.02        values(:w_firm,:lonumm,:losuff,'B');
8.02    endif;
8.02   /End-free
      *
      * oppdaterer bestillingsforslag med bestillingsnummer - for bruk i LH100R
      *
6.2f c                   eval      lfhelu_numm = lhnumm
     c     lfhelu_key    chain     lfhelur
     c                   if        %found(lfhelu)
     c                   eval      lhbnum = lohelu_numm
6.10 c                   time                    lhedat
6.10 c                   time                    lhetim
6.10 c                   eval      lheusr = l_user
     c                   update    lfhelur
     c                   endif
      *
      * Rekalkulerer bestillingsverdier med std. program
      *
     c                   call      'LO730R'
     c                   parm                    w_enor
     c                   parm                    w_firm
     c                   parm                    lohelu_numm
     c                   parm                    lohelu_suff
6.23  *
6.23  * Oppaterer ordrehode med bestillingsnr.
6.23  *
6.2f c                   eval      fohelu_numm = lhornr
6.2f c                   eval      fohelu_suff = lhorsu
6.23 c     fohelu_key    chain     fohelu
6.23 c                   if        %found(fohelu)
6.23 c                   eval      fobenr = lohelu_numm
6.23 c                   eval      fobsuf = lohelu_suff
6.23 c                   update    fohelur
6.23 c                   endif
      *
     c     hode_slutt    endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter/Oppdatering av NUMMER-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_ordrenr  begsr
      *
     c                   eval      w_onum = 0
     c                   eval      w_kode = '0'
      *
     c                   eval      w_fell = laabrk
     c                   eval      w_type = *blank
     c                   eval      w_fast = *blank
      *
      *  Kaller opp subprogram for henting av nummer fra NUMMER-REGISTER
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
     c                   parm                    w_type
     c                   parm                    w_fast
     c                   parm                    w_onum
      *
     c                   eval      lohelu_numm = w_onum
     c                   eval      lodtlu_numm = w_onum
     c                   eval      lodtlu_line = 0
      *
     c                   endsr
6.32  *
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *  Henter gtin
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     hent_gtin     begsr
6.32  *
6.32 c                   eval      w_ldor = lfldor
6.32 c                   eval      w_vare = lfvare
6.32 c                   eval      w_enhe = lfenh1
6.32 c                   call      'VE713R'
6.32 c                   parm                    w_firm
6.32 c                   parm                    w_ldor
6.32 c                   parm                    w_vare
6.32 c                   parm                    w_enhe
6.32 c                   parm                    w_gtin
6.32 c                   parm                    w_psta
6.32  *
6.32 c                   eval      ldeann = w_gtin
6.32
6.32  *
6.32 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter innkjøpsenhet og priser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_inpri    begsr
      *
     c                   eval      w_lety = 0
6.2A c                   eval      w_kamp = lokamp
5.60  *
     c                   if        lhoppr <> 'R'
6.39  * sjekker om enhet er en best.bar enhet for leverandør
6.39 c                   eval      w_retk = *blank
6.39 c                   call      'VP757R'
6.39 c                   parm                    w_firm
6.39 c                   parm                    ldvare
6.39 c                   parm                    ldenh1
6.39 c                   parm                    lfldor
6.39 c                   parm                    w_retk
6.39  *
6.39 c                   if        w_retk = *blank
      *
6.37 c                   call      'VP751R'
     c                   parm                    w_firm
     c                   parm                    ldvare
5.70 c                   parm                    w_prgr
     c                   parm                    w_lety
     c                   parm                    lhbdat
     c                   parm                    ldenh1
     c                   parm                    ldkpny
     c                   parm                    ldipny
6.2A c                   parm                    w_kamp
6.37 c                   parm                    lfldor
6.37 c                   parm                    lflage
6.39 c                   else
6.39  * her hentes innkj.pris for den enhet som ervalgt
6.39 c**                 call      'VP753R'
8.03 c                   call      'VP754R'
6.39 c                   parm                    w_firm
6.39 c                   parm                    ldvare
6.39 c                   parm                    w_prgr
6.39 c                   parm                    ldenh1
6.39 c                   parm                    w_lety
6.39 c                   parm                    lhbdat
6.39 c                   parm                    ldkpny
6.39 c                   parm                    ldipny
6.39 c                   parm                    w_kamp
8.03 c                   parm                    lflage
6.39 c                   endif
      *
5.60 c                   else
      *
8.03 c**                 call      'VP753R'
8.03 c                   call      'VP754R'
5.60 c                   parm                    w_firm
5.60 c                   parm                    ldvare
6.11 c                   parm                    w_prgr
5.60 c                   parm                    ldenh1
5.60 c                   parm                    w_lety
5.60 c                   parm                    lhbdat
5.60 c                   parm                    ldkpny
5.60 c                   parm                    ldipny
6.2A c                   parm                    w_kamp
8.03 c                   parm                    lflage
5.60  *
5.60 c                   endif
      *
6.3c c                   if        u_63c = *on
6.3c  *
6.3c  * henter evt. innkjøp fram kampanje og innkjøpsperiode er
6.3c  * innenfor.
6.3c c                   eval      w_vkai = 0
6.3c c                   eval      b_inlr = *on
6.3c c                   call      'VT592R  '
6.3c c                   parm                    w_firm
6.3c c                   parm                    ldvare
6.3c c                   parm                    ldenh1
6.3c c                   parm                    w_vkai
6.3c c                   parm                    b_inlr
6.3c c                   if        w_vkai <> 0
6.3c c                   eval      ldipny = w_vkai
6.3c c                   endif
6.3c
6.3c c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter bestillingsforslag-hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.2f c*    hent_forsl    begsr
6.2f  *
6.2f c*                  eval      b_kord = *off
6 2f  *
6 2f c*                  eval      lfhel1_numm = lfnumm
6 2f c*    lfhel1_key    chain     lfhel1r                            93
6.2f c*                  if        lhornr > 0
6.2f c*                  eval      b_kord = *on
6.2f c*                  endif
6.2f  *
6.2f c*                  endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter lagerinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_lager    begsr
      *
     c                   call      'FÅ720R'
     c                   parm                    w_firm
     c                   parm                    w_lage
     c                   parm                    lonavn
     c                   parm                    loadr1
     c                   parm                    w_ponr
     c                   parm                    w_sted
     c                   parm                    w_avde
     c                   parm                    b_feil
      *
     c                   move      w_ponr        w_ponr_alf
     c                   eval      losted = w_ponr_alf + ' ' +
     c                                      %subst(w_sted:1:25)
     c                   endsr
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *  Henter ordre info
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     hent_ordre    begsr
6.20  *
6.2d c                   eval      fohel1_numm = lhornr
6.2d c                   eval      fohel1_suff = lhorsu
6.20 c     fohel1_key    chain     fohel1
6.20 c                   if        %found(fohel1)
6.2I  * Sjekk om det er kundeprosjekt på ordren, før adressen endres.
6.2I c**                 if        fokpro <> 0
6.2I  *(Flyttet)
6.20 c                   eval      lonavn = *blank
6.20 c                   eval      loadr1 = *blank
6.20 c                   eval      loadr2 = *blank
6.20 c                   eval      losted = *blank
6.2I  *
6.20 c                   if        fonavn <> *blank
6.22 c                   movel     fonavn        lonavn
6.20 c                   move      foadr1        loadr1
6.20 c                   move      foadr2        loadr2
6.20 c                   move      fosted        losted
6.20 c                   else
6.20 c                   exsr      hntfad
6.20 c                   movel     rknavn        lonavn
6.20 c                   move      rkgate        loadr1
6.20 c                   move      rkadr2        loadr2
6.20 c                   move      rksted        losted
6.20 c                   endif
6.2I c**                 endif
6.20 c                   endif
6.20  *
6.20 c                   endsr
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  * Sjekk/Hent fakturaadresse på kunde
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     hntfad        begsr
6.20  *
6.20 c                   eval      rkunl1_kund = fokund
6.20 c     rkunl1_key    chain     rkunl1r
6.20 c                   if        not %found
6.20 c                   eval      rknavn = *blank
6.20 c                   eval      rkgate = *blank
6.20 c                   eval      rkadr2 = *blank
6.20 c                   eval      rksted = *blank
6.20 c                   endif
6.20  *
6.20 c                   endsr
6.20  *
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *  Henter ordre - linjer
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     hent_orli     begsr
6.20  *
6.20 c                   eval      fodtl1_numm = lfornr
6.20 c                   eval      fodtl1_suff = lforsu
6.20 c                   eval      fodtl1_line = lforli
6.20 c     fodtl1_key    chain     fodtl1
6.20  *
6.20 c                   endsr
6.20  *
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *  Oppdatere ordre med antall bestilt
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     upd_orli      begsr
6.20  *
6.20 c                   eval      fodtlu_numm = lfornr
6.20 c                   eval      fodtlu_suff = lforsu
6.20 c                   eval      fodtlu_line = lforli
6.20 c     fodtlu_key    chain     fodtlu
6.20 c                   if        %found(fodtlu)
6.20 c                   eval      fdantb = lfanta
6.2f c                   eval      fdbenr = ldnumm
6.2f c                   eval      fdbsuf = ldsuff
6.2f c                   eval      fdblin = ldline
6.20 c                   update    fodtlur
6.20 c                   endif
6.20  *
6.20 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter logistikkpunkt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_logp     begsr
      *
     c                   eval      w_skaf = *blank
      *
     c                   call      'MJ500R'
     c                   parm                    w_stat
     c                   parm                    lhlage
     c                   parm                    lhavde
     c                   parm                    w_skaf
     c                   parm                    w_eann
     c                   parm                    w_sted
     c                   parm                    w_adr1
     c                   parm                    w_adr2
     c                   parm                    w_pste
      *
     c                   if        w_stat = *blank
     c                   eval      lonavn = w_sted
     c                   eval      loadr1 = w_adr1
     c                   eval      loadr2 = w_adr2
     c                   eval      losted = w_pste
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter leverandørinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_levr     begsr
      *
6.2f c                   eval      rlevl1_levr = lhldor
     c     rlevl1_key    chain     rlevl1r                            91
      *
     c                   if        *in91 = *off
     c                   movel     rlalfa        lolena
     c                   eval      lobetb = rlbetb
     c                   eval      lodist = rldist
     c                   eval      lolmat = rlform
     c                   eval      lolbet = rllbet
     c                   eval      lovalk = rlvalk
     c                   eval      lorkat = rlrabk
     c                   eval      lomkod = rlmkod
     c                   else
     c                   eval      rlalfa = *blank
     c                   eval      lobetb = 0
     c                   eval      lodist = 0
     c                   eval      lolmat = 0
     c                   eval      lolbet = 0
     c                   eval      lovalk = *blank
     c                   eval      lorkat = 0
     c                   eval      lomkod = 0
     c                   endif
      *
     c                   endsr
      *
6.33  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.33  *  Henter leverandørinformasjon
6.33  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.33  *
6.33 c     hent_ldor     begsr
6.33  *
6.33 c                   eval      rlevl1_levr = lhldor
6.33 c     rlevl1_key    chain     rlevl1r
6.33  *
6.33 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Henter brukerinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_bruker   begsr
      *
6.14 c                   eval      fbvref = *blank
      *
     c                   eval      fusrl1_user = lhuser
     c     fusrl1_key    chain     fusrl1                             60
6.13 c                   if        %found(fusrl1)
6.14 c                   if        fbvref = *blank
6.13 c                   movel     lhuser        fbvref
6.14 c                   endif
6.13 c                   else
6.13 c                   eval      w_itxt = *blank
6.13 c                   if        lhselg <> 0
6.13 c                   eval      w_ista = *blank
6.13 c                   eval      w_ikod = lhselg
6.13 c                   call      'RS709R  '
6.13 c                   parm                    w_ista
6.13 c                   parm                    w_ikod
6.13 c                   parm                    w_itxt
6.13 c                   movel     w_itxt        fbvref
6.13 c                   endif
     c                   endif
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Oppdatering av Lager                                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     oppd_lager    begsr
      *
6.24 c                   if        ldltyp = 'TX'
6.24 c                   leavesr
6.24 c                   endif
      *
      *  Legger over verdier i overførings-felter
      *
     c                   eval      h_vare = ldvare
     c                   eval      h_lage = ldlage
     c                   eval      h_enhe = ldenh1
     c                   eval      h_dato = *loval
     c                   eval      h_time = *loval
     c                   eval      h_otyp = lhotyp
     c                   eval      h_ltyp = ldltyp
      *
     c                   eval      h_anta = ldanta
     c                   eval      h_kopr = ldkpny
      *
     c                   eval      h_syst = 'L'
     c                   eval      h_altk = *blank
     c                   eval      h_rest = *blank
     c                   eval      h_lety = *blank
     c                   eval      h_onum = ldnumm
     c                   eval      h_olin = ldline
     c                   move      ldnumm        w_numa
     c                   move      ldline        w_lina
     c                   eval      h_refr = 'B:' + w_numa +
     c                                      ' L:' + w_lina
      *
      *  Kaller opp lageroppdaterings-program
      *
     c                   eval      w_hrec = h_rec
      *
     c                   call      'VL001R'
     c                   parm                    w_v001
     c                   parm                    w_hrec
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for å finne omregningsfaktor
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_omr      begsr
      *
     c                   eval      vvenlr_vare = lfvare
     c     vvenlr_key    setll     vvenlr
     c     vvenlr_key    reade     vvenlr                                 91
     c                   dow       *in91 = *off
     c                   if        lfenh1 = veenhe
     c                   eval      w_omrl = veomre
     c                   endif
     c                   if        ldenh1 = veenhe
     c                   eval      w_omri = veomre
     c                   endif
     c     vvenlr_key    reade     vvenlr                                 91
     c                   enddo
      *
     c                   if        w_omri = 0
     c                   eval      w_omri = 1
     c                   endif
      *
     c                   endsr
      *
5 70  *
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5 70  * Subrutine for å hente prisgruppe
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.70  *
5.70 c     hent_prisgr   begsr
5.70  *
6.2b c                   eval      w_avd1 = 0
5.70 c                   call      'VL712R'
5.70 c                   parm                    w_firm
6.36 c                   parm                    lhlage
6.2b c                   parm                    w_avd1
5.70 c                   parm                    w_prgr
5.70  *
5.70 c                   endsr
      *
      *
5 70  *
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5 70  * Subrutine for å sjekke om vare ligger på kampanje med
5.70  * aktuell innkjøpsperiode
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.70  *
5.70 c     sjek_kamp     begsr
5.70  *
6.2b c                   eval      w_avd1 = 0
5.70  *
5.70 c                   endsr
8 01  *
8.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8 01  * Subrutine for å beregne innkjøpspris i valuta
8.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
8.01  *
8.01 c     valuta_pris   begsr
8.01  *
8.01  *
8.01  * Henter låst valutakurs - finnes den ikke, hent løpende kurs
8.01  *
8.01 c                   eval      w_kurs = 1
8.01 c/exec sql
8.01 c+ select rlvkur
8.01 c+ into   :w_kurs
8.01 c+ from   ra60st
8.01 c+ where  :w_odat >= rlvfda and :w_odat <= rlvtda and :lhldor = rlvlev
8.01 c/end-exec
8.01 c                   if        sqlcod = 100
8.01 c/exec sql
8.01 c+ select rapmku
8.01 c+ into   :w_kurs
8.01 c+ from   ra16pf
8.01 c+ where  rapfir = :w_firm and rapkod = :rlvalk
8.01 c/end-exec
8.01 c                   endif
8.01 c                   if        sqlcod = 100
8.01 c                   eval      w_kurs = 1
8.01 c                   endif
8.01  *
8.01 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av STATUS
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for les av LEVERANDØR
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      *  Key for oppdatering av BESTILLINGS-HODE
      *
     c     lohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelu_numm
     c                   kfld                    lohelu_suff
      *
      *  Key for oppdatering av BESTILLINGS-LINJE
      *
     c     lodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lodtlu_numm
     c                   kfld                    lodtlu_suff
     c                   kfld                    lodtlu_line
      *
      *  Key for les av BESTILLING-FORSLAG-LINJE
      *
     c     lfdtl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lfdtl2_numm
     c                   kfld                    lfdtl2_ldor
     c                   kfld                    lfdtl2_vare
     c                   kfld                    lfdtl2_line
6.24  *
6.24  *  Key for les av BESTILLING-FORSLAG-LINJE
6.24  *
6.24 c     lfdtl1_key    klist
6.24 c                   kfld                    w_firm
6.24 c                   kfld                    lfdtl1_numm
      *
      *  Key les av BESTILLINGS-FORSLAG-HODE
      *
     c     lfhel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lfhel1_numm
      *
      *  Key oppdater av BESTILLINGS-FORSLAG-HODE
      *
     c     lfhelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lfhelu_numm
      *
      *  Key for les av GPL-BRUKER
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrl1_user
      *
      *  Key for les av VARE/ENHET
      *
     c     vvenlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenlr_vare
      *
      * Key for les av vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
6.21  *
6.21  * Key for les av vare LVR
6.21  *
6.21 c     jvarl1_key    klist
6.21 c                   kfld                    jvarl1_vare
6.20  *
6.20  * Key for les av ordrelinjer
6.20  *
6.20 c     fodtl1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    fodtl1_numm
6.20 c                   kfld                    fodtl1_suff
6.20 c                   kfld                    fodtl1_line
6.20  *
6.20  * Key for oppdat. av ordrelinjer
6.20  *
6.20 c     fodtlu_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    fodtlu_numm
6.20 c                   kfld                    fodtlu_suff
6.20 c                   kfld                    fodtlu_line
6.20  *
6.20  * Key for les av ordre-hode
6.20  *
6.20 c     fohel1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    fohel1_numm
6.20 c                   kfld                    fohel1_suff
6.23  *
6.23  * Key for oppdat av ordre
6.23  *
6.23 c     fohelu_key    klist
6.23 c                   kfld                    w_firm
6.23 c                   kfld                    fohelu_numm
6.23 c                   kfld                    fohelu_suff
6.20  *
6.20  * Key for les av kunde
6.20  *
6.20 c     rkunl1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    rkunl1_kund
6.30  *
6.30  *  Key for les av erstatningsvare
6.30  *
6.30 c     vverl1_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    vverl1_envr
6.33  *
6.33  * Key for oppdat av mva kode
6.33  *
6.33 c     vmval1_key    klist
6.33 c                   kfld                    w_firm
6.33 c                   kfld                    vmval1_mkod
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    w_parm
     c                   eval      d_prec = w_parm
      *
      *  Legge inn firmanummer i nøklene
      *
     c                   eval      w_firm = l_firm
      *
      *  Legger inn dagens dato
      *
     c                   time                    w_date
     c                   time                    w_time
6.34  * Sjekk om du har tilgang til BM modulen.
6.34 c                   eval      b_bmpp = *on
6.34 c                   eval      w_disp = '0'
6.34 c                   eval      w_msts = '0'
6.34 c                   eval      w_modu = c_modu
6.34 c                   call      'AX700R'
6.34 c                   parm                    w_modu
6.34 c                   parm                    w_disp
6.34 c                   parm                    w_msts
6.34  *
6.34 c                   if        w_msts = '1'
6.34 c                   eval      b_bmpp = *off
6.34 c                   endif
6.3c  *
6.3c  * Endring 6.3c
6.3c  *
6.3c   CallP CO402R(l_filg:w_firm:0:'TILB_INNKJ_PERIOD':
6.3c                    co402_verdi1:co402_verdi2);
6.3c   if co402_verdi1 = '1';
6.3c     u_63c = *on;
6.3c     *in50 = *on;
6.3c   endif;

      *
      *  Legger inn start/sluttverider for lesingen
      *
6.2f c*                  eval      w_fnum = 0
6.2f c*                  eval      w_ldor = 0
     c                   eval      lodtlu_line = 0
     c                   eval      lfdtl2_numm = d_fbes
6.24 c                   eval      lfdtl1_numm = d_fbes
     c                   eval      lfdtl2_ldor = 0
     c                   eval      lfdtl2_vare = *blank
     c                   eval      lodtlu_suff = 0
     c                   eval      lohelu_suff = 0
      *
     c     lfdtl2_key    setll     lfdtl2r
6.24 c     lfdtl1_key    setll     lfdtl1r
      *
      *  Hent opplysninger fra LAGER-STATUS-KODER
      *
     c     lstsl1_key    chain     lstsl1r                            69
      *
     c                   endsr
