     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JM116R
      * Beskrivelse . . : Vedlikehold av kampanjepriser, gruppebehandling
      *                   av varer - prisberegning.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       101097 HKO  Nytt program
7.xx  * 7.00  171116 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
      *
     fjm116d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_pldo          s                   like(jlldor)
     d p_rab1          s              4  2
     d p_rab2          s              4  2
     d p_pslk          s              4  2
     d p_raba          s              4  2
     d p_psls          s              4  2
     d p_vkai          s              9  2
     d p_vkpr          s              9  2
     d p_vkaa          s              9  2
     d p_retur_ok      s              1
      *
      * Definering av variable i nøkler
      *
     d jlevl1_ldor     s                   like(jlldor)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_ldor_alf      s              6
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Flytter parametre til skjermbilde-variable
      *
     c                   eval      b1pldo = p_pldo
     c                   eval      b1rab1 = p_rab1
     c                   eval      b1rab2 = p_rab2
     c                   eval      b1pslk = p_pslk
     c                   eval      b1raba = p_raba
     c                   eval      b1psls = p_psls
     c                   eval      b1vkai = p_vkai
     c                   eval      b1vkpr = p_vkpr
     c                   eval      b1vkaa = p_vkaa
      *
     c                   eval      p_retur_ok = *on
      *
     c     b1tag         tag
      *
      * Behandler bildet
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * F3=Avslutt
      *
     c                   if        *inkc or *inkl
     c                   eval      p_retur_ok = *off
     c                   goto      avslutt
     c                   endif
      *
      * F04=Spørring
      *
     c                   if        *inkd
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endif
      *
      * F13=Betingelser
      *
     c                   if        *inkm
     c                   exsr      betingelser
     c                   goto      b1taga
     c                   endif
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_inp
     c                   if        b_feil = *on
     c                   eval      b_feil = *off
     c                   goto      b1taga
     c                   endif
      *
      * Flytter parametre til skjermbilde-variable
      *
     c                   eval      p_pldo = b1pldo
     c                   eval      p_rab1 = b1rab1
     c                   eval      p_rab2 = b1rab2
     c                   eval      p_pslk = b1pslk
     c                   eval      p_raba = b1raba
     c                   eval      p_psls = b1psls
     c                   eval      p_vkai = b1vkai
     c                   eval      p_vkpr = b1vkpr
     c                   eval      p_vkaa = b1vkaa
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av spørringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Prisgivende leverandør
      *
     c                   if        w_afld = 'B1PLDO'
     c                   call      'JL500R'
     c                   parm                    b1pldo
     c                   parm                    b1plna
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine som kaller program for å vise betingelser for
      * leverandør eller prisgivende leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     betingelser   begsr
      *
     c                   if        b1pldo <> 0
      *
     c                   call      'JP102R'
     c                   parm                    w_firm
     c                   parm                    b1pldo
      *
     c                   else
      *
     c                   eval      w_ldor_alf = *blank
     c                   call      'JP101R'
     c                   parm                    w_ldor_alf
      *
     c                   endif
      *
     c     end_bet       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_inp     begsr
      *
     c                   eval      b_feil = *off
      *
      * Prisgivende leverandør
      *
     c                   eval      b1plna = *blank
     c                   if        b1pldo <> 0
     c                   eval      jlevl1_ldor = b1pldo
     c     jlevl1_key    chain     jlevl1                             36
     c                   if        *in36  = *on
     c                   eval      b_feil = *on
     c                   goto      endsjekk
     c                   endif
     c                   eval      b1plna = jlnavn
     c                   endif
      *
      * Rabatter/påslag og priser kan ikke oppgis samtidig
      *
     c                   if        b1rab1 <> 0 or
     c                             b1rab2 <> 0 or
     c                             b1raba <> 0 or
     c                             b1pslk <> 0 or
     c                             b1psls <> 0
     c                   if        b1vkai <> 0 or
     c                             b1vkpr <> 0 or
     c                             b1vkaa <> 0
     c                   eval      *in39  = *on
     c                   eval      b_feil = *on
     c                   goto      endsjekk
     c                   endif
     c                   endif
      *
      * Rabatter/påslag -inn
      *
     c                   if        b1rab2 <> 0 and
     c                             b1rab1 = 0
     c                   eval      *in40  = *on
     c                   eval      b_feil = *on
     c                   goto      endsjekk
     c                   endif
      *
      * Rabatter/påslag -ut
      *
     c                   if        b1raba <> 0 and
     c                             b1psls <> 0
     c                   eval      *in41  = *on
     c                   eval      b_feil = *on
     c                   goto      endsjekk
     c                   endif
      *
      * Priser
      *
     c                   if        b1vkaa <> 0 and
     c                             b1vkai = 0                                   Betingelse 1
     c                   eval      *in42  = *on
     c                   eval      b_feil = *on
     c                   goto      endsjekk
     c                   endif
      *
     c     endsjekk      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_pldo
     c                   parm                    p_rab1
     c                   parm                    p_rab2
     c                   parm                    p_pslk
     c                   parm                    p_raba
     c                   parm                    p_psls
     c                   parm                    p_vkai
     c                   parm                    p_vkpr
     c                   parm                    p_vkaa
     c                   parm                    p_retur_ok
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
