      /TITLE  Oppsplitting av posteringer
     h datedit(*dmy.) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RF105R                                              *
      * Beskrivelse . . : Oppsplitting av registrerte transaksjoner           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      *           251102  Nytt program                                   KOB  *
T5.00 * R5.00     250203  Restbeløp settes til null når OPDT=K (kundereskHFL  *
      * R5.20     100903  Lagt inn feltet RTLFAK                              *
T5.30 * R5.30     040604  Endret kontroll mot koderegistre fra CALL til       *
T5.30 *                   CHAIN. CALL-program sjekker firma i LDA. Det        *
T5.30 *                   ble feil hvis posteringer for flere firmaer i       *
T5.30 *                   samme kjøring.                                 HFL  *
      * R5.31     220205  Lagt kontroll om konto skal ha prosjekt, hvis       *
      *                   nei, blankes prosjekt og aktivitet på post     KOB  *
      * R5.31     220205  Lagt kontroll om konto skal ha avdeling, hvis       *
      *                   nei, blankes avdeling på post                  KOB  *
      * R5.41     261005  Fjernet fil som ikke ble brukt                 KOB  *
      * R5.42     280906  Valutakode og beløp legges ut i H-trans.       HFL  *
      * R5.50     060307  Kid-info. legges uti RJOUPF.                   HFL  *
      * R5.50     200407  Kid-info. rettet.                              HFL  *
6.20  * R6.20     251110  Pros er nå alfa, legger underprosjekt og       NHO  *
      *                   arb.ordre ut til RJOUPF                             *
6.nu  * R6.20     280515  Utvidelse av kort kid  (Kompileres)            NHO  *
7.01  *K00        060720  lagt på return + ab705r  + switchstyrt         bof  *
7.02  * R5.40     250208  Spes. Mestergruppen Kunder som får generalnota:     *
7.02  *                   Kreditnota skal ha samme forfallsdatoberegning      *
7.02  *                   som faktura.                                   HFL  *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frtripf    if   e           k disk    rename(rtripfr:rtripfr)
     frhovl1    if   e           k disk    rename(rhovpfr:rhovl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fraa4l1    if   e           k disk    rename(raa4pfr:raa4l1r)
     fraa1l1    if   e           k disk    rename(raa1pfr:raa1l1r)
T5.30fra01l1    if   e           k disk    rename(ra01pfr:ra01l1r)
     fra04l1    if   e           k disk    rename(ra04pfr:ra04l1r)
     fra05l1    if   e           k disk    rename(ra05pfr:ra05l1r)
     fra11l1    if   e           k disk    rename(ra11pfr:ra11l1r)
     fra14l1    if   e           k disk    rename(ra14pfr:ra14l1r)
T5.30fra16l1    if   e           k disk    rename(ra16pfr:ra16l1r)
     frjoupf    o    e           k disk
      *
      * KEY-REKKEFØLGE PÅ RJOUPF (POSTER UT)
      *
     d wssort          ds
     d  wsfirm                        3  0
     d  wsraar                        4  0
     d  wsrper                        2  0
     d  wstype                        1  0
     d  wsbunt                        5  0
     d  wslinj                        5  0
     d  wssequ                        1  0
6.NU d  wsbiln                        8  0
     d  wsbdat                         d   datfmt(*iso)
     d  wsdbkr                        1
     d  wslito                        4  0
      *
     d                uds
     d l_user                911    920
     d l_flgr                931    933
      *
     d wkkrab          s             11  2
     d wlkrab          s             11  2
     d wtpaar          s              2  0
     d wtpmnd          s              2  0
     d w_dbkr          s              1
610  d w_memb          s                   like(rjmemb)
      *
     d p_akod          s                   like(rtbilk)
     d p_betb          s                   like(rkbetb)
     d p_stat          s              1
     d p_tx20          s             20
     d p_txt1          s             20
     d p_txt2          s             20
      *
     d p_pkod          s                   like(rtvalk)
     d p_pmna          s             20
     d p_pkda          s                   like(rtbdat)
     d p_pomr          s             10  6
      *
     d p_bdat          s               d   datfmt(*iso)
     d p_fdat          s               d   datfmt(*iso)
     d w_edat          s               d   datfmt(*iso)
      *
     d w_firm          s                   like(rtfirm)
     d w_kont          s                   like(rtdkto)
     d w_spes          s                   like(rtdsps)
     d w_avde          s                   like(rtdavd)
     d w_rper_g        s                   like(rtrper)
     d w_mvak          s                   like(rtdmva)
     d w_4sgb          s                   like(ra4sgb)
     d w_belØ          s                   like(rtbelØ)
     d w_valk          s                   like(rtvalk)
     d w_krab          s                   like(rtkrab)
     d w_neto          s                   like(rjneto)
     d w_post          s              5  0
7.01 d w_jkey          s             41
7.01 d w_jnav          s             15
7.01 d w_jsta          s              2  0
7.01 d w_jmld          s            200
      *
      * Definering av variable i nøkler
      *
610  d rjoupf_nivo     s                   like(rtnivo)
610  d rjoupf_memb     s                   like(rtmemb)
     d rkunl1_kund     s                   like(rkkund)
     d rlevl1_levr     s                   like(rllevr)
     d rhovl1_kont     s                   like(rhkont)
     d rhovl1_spes     s                   like(rhspes)
     d rhovl1_avde     s                   like(rhavde)
T5.30d ra01l1_kod      s                   like(raakod)
     d ra05l1_ekod     s                   like(raekod)
     d ra11l1_kkod     s                   like(rakkod)
     d ra14l1_nkod     s                   like(rankod)
T5.30d ra16l1_kod      s                   like(rapkod)
     d raa4l1_4aar     s                   like(ra4aar)
      *
7.02 d u_s701          s              1    inz(*off)
7.01   dcl-s co402_verdi1  char(1);
7.01   dcl-s co402_verdi2  char(2048);
7.01   DCL-PR CO402R EXTPGM('CO402R');
7.01     p_filgrp            char(3)     const;
7.01     p_firm              packed(3:0) const;
7.01     p_lager             packed(2:0) const;
7.01     p_nokkel            char(50)    const;
7.01     p_verdi1            char(1);
7.01     p_verdi2            char(2048);
7.01   END-PR;
7.01 d u_logg          s              1    inz(*off)                            logging ajltpf
     c/eject
      *
7.01 c                   if        u_logg = *on
7.01 c                   eval      w_jsta = 10
7.01 c                   eval      w_jmld = 'Start RF105R        '
7.01 c                   eval      w_jnav = 'OKON_RF105R-'
7.01 c                   call      'AB700R'
7.01 c                   parm                    w_jnav
7.01 c                   parm                    w_jkey
7.01 c                   parm                    w_jmld
7.01 c                   eval      w_jsta = 10
7.01 c                   eval      w_jmld = 'Start RF105R       '
7.01 c                   call      'AB705R'
7.01 c                   parm                    w_jkey
7.01 c                   parm                    w_jsta
7.01 c                   parm                    w_jmld
7.01 c                   endif
7.01  *
     c     dummy         tag
      *
      * Les posteringer
      *
     c                   read      rtripf
      *
      * Hent firmainfo 1. post
      *
     c                   eval      w_firm = rtfirm
     c                   eval      w_rper_g = rtrper
      *
     c                   exsr      brd_firma
      *
     c                   dow       *in60 = *off
      *
      * Sjekk member
610   *
610  c                   if        rtmemb <> w_memb
610  c                   goto      nyles
610  c                   endif
610   *
      * Brudd på firma
      *
     c                   if        rtfirm <> w_firm
     c                   exsr      brd_firma
S5.30c*                  eval      w_firm = rtfirm
     c                   endif
      *
      * Brudd på regnskapsperiode
      *
     c                   if        rtrper <> w_rper_g and
     c                             w_rper_g <> *hival
     c                   exsr      brd_periode
     c                   eval      w_rper_g = rtrper
     c                   endif
      *
      * Flytt felt til output-record
      *
     c                   exsr      flytt_ut
      *
      * Debetkonto
      *
     c                   if        rtdkto <>  0
     c                   exsr      beh_debet
     c                   endif
      *
      * Kreditkonto
      *
     c                   if        rtckto <>  0
     c                   exsr      beh_kredit
     c                   endif
      *
     c     nyles         tag
      *
     c                   read      rtripf
      *
     c                   if        %eof
     c                   seton                                        60
     c                   endif
      *
     c                   enddo
      *
     c     slutt         tag
      *
     c                   exsr      brd_periode
      *
7.01 c                   if        u_logg = *on
7.01 c                   eval      w_jmld = 'Slutt RF105R før LR'
7.01 c                   call      'AB705R'
7.01 c                   parm                    w_jkey
7.01 c                   parm                    w_jsta
7.01 c                   parm                    w_jmld
7.01 c                   endif
7.01  *
     c                   seton                                        lr
7.01 c                   if        u_logg = *on
7.01 c                   eval      w_jsta = 10
7.01 c                   eval      w_jmld = 'Slutt RF105R etter LR'
7.01 c                   call      'AB705R'
7.01 c                   parm                    w_jkey
7.01 c                   parm                    w_jsta
7.01 c                   parm                    w_jmld
7.01 c                   endif
7.01 c                   return
      *
     c/eject
      *
      * Subrutine for behandling av debetkonto
      *
     c     beh_debet     begsr
      *
     c                   eval      w_dbkr = 'D'
     c                   eval      w_kont = rtdkto
     c                   eval      w_spes = rtdsps
     c                   eval      w_avde = rtdavd
     c                   eval      w_mvak = rtdmva
      *
     c                   select
     c                   when      rtdkto <= ra1hkt
     c                   exsr      beh_hovbok
      *
     c                   when      rtdkto > ra1hkt and rtdkto <= ra1hrs
     c                   exsr      beh_kunde
      *
     c                   when      rtdkto > ra1hrs
     c                   exsr      beh_lever
     c                   endsl
      *
     c                   endsr
     c/eject
      *
      * Subrutine for behandling av kreditkonto
      *
     c     beh_kredit    begsr
      *
     c                   eval      w_dbkr = 'K'
     c                   eval      w_kont = rtckto
     c                   eval      w_spes = rtcsps
     c                   eval      w_avde = rtcavd
     c                   eval      w_mvak = rtcmva
      *
      * Snu fortegn på kreditposter
      *
     c                   eval      w_belØ = 0 - w_belØ
     c                   eval      rtvalb = 0 - rtvalb
     c                   eval      rtmngd = 0 - rtmngd
     c                   eval      rjmvag = 0 - rjmvag
      *
     c                   select
     c                   when      rtckto <= ra1hkt
     c                   exsr      beh_hovbok
      *
     c                   when      rtckto > ra1hkt and rtckto <= ra1hrs
     c                   exsr      beh_kunde
      *
     c                   when      rtckto > ra1hrs
     c                   exsr      beh_lever
     c                   endsl
      *
     c                   endsr
      *
     c/eject
      *
      * Subrutine for flytting av felt til output
      *
     c     flytt_ut      begsr
      *
     c                   eval      rjflgr = l_flgr
     c                   eval      rjreid = *blanks
     c                   eval      rjfirm = rtfirm
     c                   eval      rjkont = *zeros
     c                   eval      rjspes = *zeros
     c                   eval      rjavde = *zeros
     c                   eval      rjmmrk = *blanks
     c                   eval      rjbdat = rtbdat
     c                   eval      rjbiln = rtbiln
     c                   eval      rjbill = rtbill
     c                   eval      w_dbkr = *blanks
     c                   eval      rjmvab = *zeros
     c                   eval      rjiavg = *zeros
     c                   eval      rjstar = *blanks
     c                   eval      rjmvak = *blanks
     c                   eval      rjfeik = *blanks
     c                   eval      rjtype = 1
     c                   eval      rjatte = rtatte
      *
     c                   eval      rjrper = rtrper
     c                   eval      rjraar = rtraar
      *
     c                   eval      rjbunt = rtbunt
     c                   eval      rjlinj = rtlinj
     c                   eval      rjrefn = rtrefn
     c                   eval      rjfdat = rtfdat
6.20 c**                 eval      rjpros = *zeros
6.20 c                   eval      rjpros = *blank
     c                   eval      rjmngd = rtmngd
     c                   eval      rjmngk = rtmngk
     c                   eval      rjresn = rtresn
     c                   eval      rjvirk = rtvirk
     c                   eval      rjmvag = rtgmva
     c                   eval      rjkrys = rtkrys
     c                   eval      rjakti = rtakti
6.20 c**SJEKK
6.20 c                   eval      rjupro = rtupro
6.20 c                   eval      rjarbo = rtarbo
     c                   eval      rjbetb = rtbetb
     c                   eval      rjbref = rtbref
T5.20c                   eval      rjlfak = rtlfak
     c                   eval      rjkidi = rtkidi
     c                   eval      rjopdt = rtautx
     c                   eval      rjblde = rtblde
T5.50c                   eval      rjbong = rtbong
      *
      * Behandle valuta
      *
     c                   eval      w_valk = rtvalk
      *
     c                   if        w_valk <> *blank and
     c                             rtvalb <> 0 and
     c                             rtbelØ = 0
S5.30c*                  eval      p_pkod = w_valk
S5.30c*                  eval      p_pkda = rjbdat
      *
E5.30c                   eval      ra16l1_kod = w_valk
E5.30c     ra16l1_key    chain     ra16l1
      *
T5.30c                   if        %found
      *
T5.30 * Finne hvilken kurs som skal benyttes
      *
T5.30c                   if        rjbdat >= rapkda
T5.30c                   eval      p_pomr = rapmku
T5.30c                   else
T5.30c                   eval      p_pomr = rapmkg
T5.30c                   endif
T5.30c                   endif
      *
     c                   eval(h)   w_belØ = rtvalb * p_pomr
     c                   eval(h)   w_krab = rtkrab * p_pomr
     c                   else
     c                   eval      w_belØ = rtbelØ
     c                   endif
      *
      * Sorteringsfelt
      *
     c                   eval      wssort = *zeros
     c                   eval      wsfirm = rtfirm
     c                   eval      wsraar = rtraar
     c                   eval      wsrper = rtrper
     c                   eval      wstype = rjtype
     c                   eval      wsbunt = rtbunt
     c                   eval      wslinj = rtlinj
     c                   eval      wssequ = 5
     c                   eval      wsbiln = rtbiln
     c                   eval      wsbdat = rtbdat
     c                   move      wssort        rjsort
     c                   endsr
      *
     c/eject
      *
      * Subrutine for behandling av kundeposter
      *
     c     beh_kunde     begsr
      *
     c                   eval      wkkrab = wkkrab + rtkrab
      *
     c                   eval      rkunl1_kund = w_kont
     c     rkunl1_key    chain     rkunl1                             62
      *
      * Beregn forfallsdato hvis debetpost
      *
     c                   if        w_dbkr = 'D'
     c                   if        rtfdat = *loval
      *
     c                   eval      p_bdat = rtbdat
     c                   eval      p_betb = rkbetb
      *
     c                   call      'RÅ703R'
     c                   parm                    w_firm
     c                   parm                    p_betb
     c                   parm                    p_bdat
     c                   parm                    p_stat
     c                   parm                    p_fdat
     c                   parm                    p_txt1
     c                   parm                    p_txt2
      *
     c                   move      p_fdat        rjfdat
      *
     c                   else
      *
     c                   move      rtfdat        rjfdat
      *
     c                   endif
     c                   endif
      *
      * Forfallsdato hvis kredit kundepost og ikke utfylt
      *
     c                   if        w_dbkr = 'K' and
     c                             rjfdat = *loval
     c                   move      rjbdat        rjfdat
     c                   endif
      *
7.02  *
7.02  * Forfallsdato kreditnota hvis kunden skal ha generalnota
7.02  *
7.02 c                   if        u_s701 = *on
7.02 c                   if        w_dbkr = 'K' and
7.02 c                             rkbetm = 3   and
7.02 c                             rjbilk = 03
7.02 c                   eval      p_bdat = rtbdat
7.02 c                   eval      p_betb = rkbetb
7.02  *
7.02 c                   call      'RÅ703R'
7.02 c                   parm                    w_firm
7.02 c                   parm                    p_betb
7.02 c                   parm                    p_bdat
7.02 c                   parm                    p_stat
7.02 c                   parm                    p_fdat
7.02 c                   parm                    p_txt1
7.02 c                   parm                    p_txt2
7.02  *
7.02 c                   move      p_fdat        rjfdat
7.02 c                   endif
7.02 c                   endif
     c                   eval      rjreid = 'K'
     c                   eval      rjkont = w_kont
     c                   eval      rjspes = *zeros
6.20 c**                 eval      rjpros = *zeros
6.20 c                   eval      rjpros = *blank
     c                   eval      rjakti = *blank
6.20 c                   eval      rjupro = *blank
6.20 c                   eval      rjarbo = *blank
     c                   eval      rjavde = w_avde
      *
     c                   eval      rjmvab = *zeros
     c                   eval      rjiavg = *zeros
     c                   eval      rjmvak = *blanks
     c                   eval      rjtype = 1
     c                   eval      rjvirk = rtvirk
     c                   eval      wstype = rjtype
     c                   eval      wsdbkr = w_dbkr
     c                   eval      wslito = 9999
     c                   move      wssort        rjsort
      *
     c                   eval      rjbilk = rtbilk
     c                   eval      rjtext = rttext
      *
     c                   if        rttext = *blanks
     c                   eval      p_akod = rtbilk
T5.30 * Hente  bilagstekst
E5.30c                   exsr      hbiltxt
     c                   endif
      *
     c                   eval      rjvalk = w_valk
     c                   eval      rjneto = w_belØ
     c                   eval      rjrstb = w_belØ
     c                   eval      rjvalb = rtvalb
     c                   eval      rjrstv = rtvalb
      *
T5.50c                   eval      rjkidr = rtkidr
T5.50c                   eval      rjksif = rtksif
T5.50c                   eval      rjkid2 = rtkid2
      *
T5.00c                   if        rjopdt = 'K'
T5.00c                   eval      rjrstb = *zeros
T5.00c                   eval      rjrstv = *zeros
T5.00c                   endif
      *
610  c                   eval      rjnivo = *blanks
610  c                   eval      rjmemb = w_memb
610  c                   time                    rjedat
610  c                   time                    rjetim
610  c                   eval      rjeusr = l_user
     c                   write     rjoupfr
      *
      * Kontantrabatt-postering legges ut
      *
     c                   if        rtkrab <> 0
     c                   eval      rjbilk = 96
     c                   eval      p_akod = rjbilk
      *
T5.30 * Hente  bilagstekst
E5.30c                   exsr      hbiltxt
      *
     c                   eval      rjvalk = w_valk
     c                   eval      rjneto = rtkrab
     c                   eval      rjrstb = rtkrab
     c                   eval      rjvalb = w_krab
     c                   eval      rjrstb = w_krab
      *
T5.00c                   if        rjopdt = 'K'
T5.00c                   eval      rjrstb = *zeros
T5.00c                   eval      rjrstv = *zeros
T5.00c                   endif
      *
T5.50c                   eval      rjkidi = *blank
T5.50c                   eval      rjkidr = *zeros
T5.50c                   eval      rjksif = *zeros
T5.50c                   eval      rjkid2 = *blank
      *
     c                   move      '    '        rjsort
     c                   move      'R'           rjopdt
     c                   eval      wslito = 0
     c                   move      wssort        rjsort
      *
610  c                   eval      rjnivo = *blanks
610  c                   eval      rjmemb = w_memb
610  c                   time                    rjedat
610  c                   time                    rjetim
610  c                   eval      rjeusr = l_user
     c                   write     rjoupfr
      *
     c                   endif
      *
     c                   endsr
      *
     c/eject
      *
      * Subrutine for behandling av leverandørposter
      *
     c     beh_lever     begsr
      *
     c                   eval      wlkrab = wlkrab + rtkrab
      *
     c                   eval      rlevl1_levr = w_kont
     c     rlevl1_key    chain     rlevl1                             63
      *
      * Beregn forfallsdato hvis kreditpost
      *
     c                   if        w_dbkr = 'K'
     c                   if        rtfdat = *loval
      *
     c                   eval      p_bdat = rtbdat
     c                   eval      p_betb = rlbetb
      *
     c                   call      'RÅ703R'
     c                   parm                    w_firm
     c                   parm                    p_betb
     c                   parm                    p_bdat
     c                   parm                    p_stat
     c                   parm                    p_fdat
     c                   parm                    p_txt1
     c                   parm                    p_txt2
      *
     c                   move      p_fdat        rjfdat
      *
     c                   else
     c                   move      rtfdat        rjfdat
     c                   endif
     c                   endif
      *
      * Forfallsdato hvis debet leverandørpost og ikke utfylt
      *
     c                   if        w_dbkr = 'D' and
     c                             rjfdat = *loval
     c                   eval      rjfdat = rjbdat
     c                   endif
      *
     c                   eval      rjreid = 'L'
     c                   eval      rjkont = w_kont
     c                   eval      rjspes = *zeros
6.20 c**                 eval      rjpros = *zeros
6.20 c                   eval      rjpros = *blank
     c                   eval      rjakti = *blank
6.20 c                   eval      rjupro = *blank
6.20 c                   eval      rjarbo = *blank
     c                   eval      rjavde = w_avde
      *
     c                   eval      rjmvab = *zeros
     c                   eval      rjiavg = *zeros
     c                   eval      rjmvak = *blanks
      *
     c                   eval      rjtype = 1
     c                   eval      rjvirk = rtvirk
      *
     c                   if        rtblde = '5'
     c                   move      rtress        rjress
     c                   move      rtresa        rjresa
     c                   move      rtresm        rjresm
     c                   endif
      *
     c                   eval      wstype = rjtype
     c                   eval      wsdbkr = w_dbkr
     c                   move      wssort        rjsort
      *
     c                   eval      rjbilk = rtbilk
     c                   eval      rjtext = rttext
      *
     c                   if        rttext = *blanks
     c                   eval      p_akod = rtbilk
T5.30 * Hente  bilagstekst
E5.30c                   exsr      hbiltxt
     c                   endif
      *
     c                   eval      rjvalk = w_valk
     c                   eval      rjneto = w_belØ
     c                   eval      rjrstb = w_belØ
     c                   eval      rjvalb = rtvalb
     c                   eval      rjrstv = rtvalb
      *
T5.50c                   eval      rjkidi = rtkidi
T5.50c                   eval      rjkidr = *zeros
T5.50c                   eval      rjksif = *zeros
T5.50c                   eval      rjkid2 = *blank
      *
T5.00c                   if        rjopdt = 'K'
T5.00c                   eval      rjrstb = *zeros
T5.00c                   eval      rjrstv = *zeros
T5.00c                   endif
      *
610  c                   eval      rjnivo = *blanks
610  c                   eval      rjmemb = w_memb
610  c                   time                    rjedat
610  c                   time                    rjetim
610  c                   eval      rjeusr = l_user
     c                   write     rjoupfr
      *
      * Kontantrabatt-postering legges ut
      *
     c                   if        rtkrab <> 0
     c                   eval      rjbilk = 96
     c                   eval      p_akod = rtbilk
T5.30 * Hente  bilagstekst
E5.30c                   exsr      hbiltxt
      *
     c                   eval      rjvalk = w_valk
     c                   eval      rjneto = rtkrab
     c                   eval      rjrstb = rtkrab
     c                   eval      rjvalb = w_krab
     c                   eval      rjrstv = w_krab
      *
T5.50c                   eval      rjkidi = *blank
T5.50c                   eval      rjkidr = *zeros
T5.50c                   eval      rjksif = *zeros
T5.50c                   eval      rjkid2 = *blank
      *
T5.00c                   if        rjopdt = 'K'
T5.00c                   eval      rjrstb = *zeros
T5.00c                   eval      rjrstv = *zeros
T5.00c                   endif
      *
     c                   move      '    '        rjsort
     c                   move      'R'           rjopdt
     c                   move      wssort        rjsort
      *
610  c                   eval      rjnivo = *blanks
610  c                   eval      rjmemb = w_memb
610  c                   time                    rjedat
610  c                   time                    rjetim
610  c                   eval      rjeusr = l_user
     c                   write     rjoupfr
      *
     c                   endif
      *
     c                   endsr
      *
     c/eject
      *
      * Behandle hovedbokskonto
      *
     c     beh_hovbok    begsr
      *
     c                   eval      rjbilk = rtbilk
     c                   eval      rjtext = rttext
      *
     c                   exsr      hnt_konto
      *
     c                   eval      rjmvak  = w_mvak
      *
      * Legg ut merke for overstyring av mva-kode
      *
     c                   if        rjmvak <> rhmvak and
     c                             rhmvao = 'J'
     c                   eval      rjstar = '*'
     c                   else
     c                   eval      rjstar = ' '
     c                   endif
      *
      * Hent mva-kode fra konto hvis ikke utfylt
      *
     c                   if        rjmvak  = *blank
     c                   eval      rjmvak  = rhmvak
     c                   endif
      *
     c                   eval      rjreid = 'H'
     c                   eval      rjkont = w_kont
     c                   eval      rjspes = w_spes
     c                   eval      rjavde = w_avde
     c                   eval      rjpros = rtpros
     c                   eval      rjakti = rtakti
6.20 c                   eval      rjupro = rtupro
6.20 c                   eval      rjarbo = rtarbo
      *
     c                   move      rtmngd        rjmngd
     c                   move      rtmngk        rjmngk
     c                   move      rtresn        rjresn
      *
      * Saldo/Årsoppgjørsposter skal ikke ha mva-kode
      *
     c                   if        rtbilk = 85 or
     c                             rtbilk = 98
     c                   move      '0'           rjmvak
     c                   move      '0'           rhmvak
     c                   move      'N'           rhmvao
     c                   endif
      *
     c                   eval      rjtype = 1
     c                   eval      wstype = rjtype
     c                   eval      wsdbkr = w_dbkr
     c                   move      wssort        rjsort
      *
      * Konto skal ikke ha valuta
      *
S5.42c*                  if        rhvalk = *blank
S5.42c*                  eval      rjvalk = *blank
S5.42c*                  eval      rjvalb = 0
S5.42c*                  eval      rjrstv = 0
S5.42c*                  endif
T5.42c                   eval      rjvalk = w_valk
T5.42c                   eval      rjvalb = rtvalb
      *
T5.50c                   eval      rjkidi = *blank
T5.50c                   eval      rjkidr = *zeros
T5.50c                   eval      rjksif = *zeros
T5.50c                   eval      rjkid2 = *blank
      *
      * Konto skal ikke ha prosjekt
      *
     c                   if        rhproj = 'N'
     c                   eval      rjakti = *blank
6.20 c**                 eval      rjpros = 0
6.20 c                   eval      rjpros = *blank
6.20 c                   eval      rjupro = *blank
6.20 c                   eval      rjarbo = *blank
     c                   endif
      *
      * Konto skal ikke ha avdeling
      *
     c                   if        rhavdk = 'N'
     c                   eval      rjavde = 0
     c                   endif
      *
     c                   eval      rjneto = w_belØ
     c                   eval      rjrstb = 0
      *
610  c                   eval      rjnivo = *blanks
610  c                   eval      rjmemb = w_memb
610  c                   time                    rjedat
610  c                   time                    rjetim
610  c                   eval      rjeusr = l_user
     c                   write     rjoupfr
      *
     c                   endsr
      *
     c/eject
      *
      * Brudd på firma
      *
     c     brd_firma     begsr
      *
     c                   eval      w_firm = rtfirm
      *
     c     raa1l1_key    chain     raa1l1                             61
      *
     c     ra04l1_key    chain     ra04l1                             61
      *
     c                   endsr
     c/space 3
      *
      * Brudd på periode
      *
     c     brd_periode   begsr
      *
      * Legg ut kontantrabatt
      *
     c                   exsr      beh_krab
      *
     c                   endsr
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for hente hovedbokskonto                              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     hnt_konto     begsr
      *
     c                   eval      rhovl1_kont = w_kont
     c                   eval      rhovl1_spes = w_spes
     c                   eval      rhovl1_avde = w_avde
     c     rhovl1_key    chain     rhovl1                             63
      *
     c                   if        *in63 = *on
     c                   eval      rhovl1_avde = *zeros
     c     rhovl1_key    chain     rhovl1                             63
      *
     c                   if        *in63 = *on
     c                   eval      rhovl1_spes = *zeros
     c     rhovl1_key    chain     rhovl1                             63
      *
     c                   endif
     c                   endif
      *
     c                   endsr
      *
     c/eject
      *
      * Subrutine for å behandle kontantrabatt
      *
     c     beh_krab      begsr
      *
     c                   move      'H'           rjreid
6.20 c**                 eval      rjpros = *zeros
6.20 c                   eval      rjpros = *blank
     c                   eval      rjakti = *blank
6.20 c                   eval      rjupro = *blank
6.20 c                   eval      rjarbo = *blank
      *
     c                   eval      rjbilk = 96
     c                   eval      p_akod = rjbilk
T5.30 * Hente  bilagstekst
E5.30c                   exsr      hbiltxt
      *
     c                   eval      rjmvab = *zeros
     c                   eval      rjiavg = *zeros
     c                   eval      rjmvak = *blank
     c                   eval      rjfeik = '0'
     c                   eval      rjtype = 2
     c                   eval      wstype = rjtype
     c                   eval      wssequ = 3
     c                   eval      rjfdat = rtfdat
     c                   eval      rjraar = rtraar
     c                   eval      rjrper = rtrper
      *
      * Hent mva-kode fra konto for kontantrabatt kunder
      *
     c                   eval      w_kont = radk07
     c                   eval      w_spes = rads07
     c                   eval      w_avde = rada07
     c                   exsr      hnt_konto
      *
     c                   if        wkkrab <> 0
      *
     c                   eval      rjkont = radk07
     c                   eval      rjspes = rads07
     c                   eval      rjavde = rada07
     c                   move      rhmvak        rjmvak
     c                   move      'D'           w_dbkr
E5.00c                   eval      rjneto = wkkrab * (-1)
E5.00c                   eval      rjrstb = wkkrab * (-1)
     c                   move      w_dbkr        wsdbkr
     c                   move      wssort        rjsort
      *
610  c                   eval      rjnivo = *blanks
610  c                   eval      rjmemb = w_memb
610  c                   time                    rjedat
610  c                   time                    rjetim
610  c                   eval      rjeusr = l_user
     c                   write     rjoupfr
      *
     c                   eval      wkkrab = *zeros
     c                   eval      w_post = w_post + 1
     c                   endif
      *
      * Hent mva-kode fra konto for kontantrabatt leverandører
      *
     c                   eval      w_kont = radk16
     c                   eval      w_spes = rads16
     c                   eval      w_avde = rada16
     c                   exsr      hnt_konto
      *
     c                   if        wlkrab <> 0
     c                   eval      rjkont = radk16
     c                   eval      rjspes = rads16
     c                   eval      rjavde = rada16
     c                   move      rhmvak        rjmvak
     c                   move      'D'           w_dbkr
E5.00c                   eval      rjneto = wlkrab * (-1)
E5.00c                   eval      rjrstb = wlkrab * (-1)
     c                   move      w_dbkr        wsdbkr
     c                   move      wssort        rjsort
      *
610  c                   eval      rjnivo = *blanks
610  c                   eval      rjmemb = w_memb
610  c                   time                    rjedat
610  c                   time                    rjetim
610  c                   eval      rjeusr = l_user
     c                   write     rjoupfr
      *
     c                   eval      wlkrab = *zeros
     c                   eval      w_post = w_post + 1
     c                   endif
      *
     c                   endsr
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
T5.30 * Subrutine for å hente bilagstekst                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
T5.30c     hbiltxt       begsr
      *
T5.30c                   eval      ra01l1_kod = p_akod
T5.30c     ra01l1_key    chain     ra01l1
      *
T5.30c                   if        %found
T5.30c                   eval      rjtext = raatxt
T5.30c                   else
T5.30c                   eval      rjtext = 'Koden er ugyldig'
T5.30c                   endif
T5.30c                   endsr
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
610   *
610  c     *entry        plist
610  c                   parm                    w_memb
      *
     c                   eval      w_rper_g = *hival
      *
      * Key for kontoplan
      *
     c     rhovl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovl1_kont
     c                   kfld                    rhovl1_spes
     c                   kfld                    rhovl1_avde
      *
      * Key for kunder
      *
E5.30c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for leverandører
      *
E5.30c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for statusopplysninger - koderegister
      *
     c     raa1l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for statusopplysninger - sist brukte nummer
      *
     c     raa4l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    raa4l1_4aar
      *
T5.30 * Key for bilagskode
      *
T5.30c     ra01l1_key    klist
T5.30c                   kfld                    w_firm
T5.30c                   kfld                    ra01l1_kod
      *
      * Key for faste konti
      *
     c     ra04l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for mva-koder
      *
     c     ra05l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra05l1_ekod
      *
      * Key for kundekategori
      *
     c     ra11l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra11l1_kkod
      *
      * Key for leverandørkategori
      *
     c     ra14l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra14l1_nkod
      *
T5.30 * Key for valutakode
      *
T5.30c     ra16l1_key    klist
T5.30c                   kfld                    w_firm
T5.30c                   kfld                    ra16l1_kod
      *
      * Key for posteringer ut rjoupf
      *
     c     keyjou        klist
     c                   kfld                    rjsort
      *
7.01  * Test om logging skal gjøres
7.01  *
7.01   CallP CO402R(l_flgr:w_firm:0:'RF-PROG LOGGING':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_logg = *on;
7.01   endif;
      *
7.02  *
7.02  * Spesiell forfallsberegning.
7.02  *
7.02   CallP CO402R(l_flgr:w_firm:0:'RF105_701':
7.02   co402_verdi1:co402_verdi2);
7.02   if co402_verdi1 = '1';
7.02     u_s701 = *on;
7.02   endif;
7.02  *
     c                   endsr
