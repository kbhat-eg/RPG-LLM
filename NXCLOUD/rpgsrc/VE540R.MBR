     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VE540R
      * Beskrivelse . . : Direkte vedlikehold av GTIN pr. vare/enhet/ldor
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       200510 bof  Nytt program
6.11  *       191010 bof  Lagt inn spørring på vare
6.13  *       121110 bsa  Lagt inn sortering på GTIN nummer
6.14  *       171210 bsa  Link til øvrige GTIN nummer for vare
6.20  *       010914 bof  Ved endr. gtinnr, kall oppdat av søkereg.
6.30  * v6.30 220814 bof  Filter for GTIN
6.31  *       220916 nho  Viser riktig leverandør dersom valg på
      *                   gtinnr.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvveplr    if   e           k disk    rename(vveppfr:vveplrr)
     fvveplu    uf a e           k disk    rename(vveppfr:vveplur)
     fveanlu    uf a e           k disk    rename(veanpfr:veanlur)
6.30 fveanlr    if   e           k disk    rename(veanpfr:veanlrr)
6.12 fvvepl2    if   e           k disk    rename(vveppfr:vvepl2r)
      *
     fve540d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d vvenl1_vare     s                   like(vevare)
     d vvarl1_vare     s                   like(vvvare)
     d rlevl1_ldor     s                   like(rllevr)
      *
     d vveplr_pldo     s                   like(vepldo)
     d vveplr_penh     s                   like(vepenh)
     d vveplr_pvar     s                   like(vepvar)
     d vveplu_pldo     s                   like(vepldo)
     d vveplu_penh     s                   like(vepenh)
     d vveplu_pvar     s                   like(vepvar)
     d veanlu_eann     s                   like(vxeann)
6.30 d veanlr_eann     s                   like(vxeann)
6.12 d vvepl2_pgti     s                   like(vepgti)
     d
      *
      * Definering av parameter
      *
6.30 d p_gtin          s                   like(vepgti)
      *
      * Definering av variabler
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_srrn_w        s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_enter         s              1    inz(*off)
     d b_inlr          s              1    inz(*off)
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
     d b_subf          s              1    inz(*off)
     d b_eaok          s              1    inz(*off)
      *
     d w_firm          s                   like(vefirm)
     d w_vare          s                   like(vevare)
6.14 d p_vare          s                   like(vvvare)
     d w_tek1          s                   like(vvtek1)
     d w_rkod          s              1
      *
     d s_pgti          s                   like(vepgti)
      *
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
6.30 c                   when      *inka
6.30 c                   exsr      xf1win
6.30 c                   exsr      forny
6.30 c                   goto      b2taga
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
6.14 c                   when      *inki
6.14 c                   call      'VE520R '
6.14 c                   exsr      forny
     c                   when      *inkj
     c                   call      'VX110R '
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2vare <> *blank or
6.13 c                             b2pgti <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   if        b_subf = *on
     c                   goto      b2taga
     c                   endif
      *
     c                   eval      b_enter = *on
      *
      * Validering av input-felter
      *
     c                   if        b_feil = *on
     c                   goto      b2tagb
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
6.13 c                   select
6.13 c                   when      w_seqe = ' '
     c     vvenl1_key    setll     vvenl1
6.13 c                   when      w_seqe = 'G'
6.13 c     vvepl2_key    setll     vvepl2
6.13 c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Vare
      *
     c                   if        b2vare <> *blank
     c                   eval      w_seqe = ' '
     c                   eval      vvenl1_vare = b2vare
     c     vvenl1_key    setll     vvenl1
     c                   goto      end_pos
     c                   endif
6.13  *
6.13  * GTIN nummer
6.13  *
6.13 c                   if        b2pgti <> 0
6.13 c                   eval      w_seqe = 'G'
6.13 c                   eval      vvepl2_pgti = b2pgti
6.13 c     vvepl2_key    setll     vvepl2
6.13 c                   goto      end_pos
6.13 c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2vare = *blank
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_subf = *off
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vis
      *
     c                   if        b1valg = 2
     c                   eval      c1pvar = b1vare
     c                   eval      c1penh = b1venh
     c                   eval      c1pldo = b1ldor
     c                   if        b1vare <> *blank and
     c                             b1venh <> *blank and
     c                             b1ldor <> 0
     c                   exsr      xc2bld
     c                   endif
     c                   eval      b_subf = *on
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
6.14  *
6.14  * Visning av GTIN nummer
6.14  *
6.14 c                   if        b1valg = 8
6.14 c                   eval      p_vare = *blanks
6.14 c                   movel     b1vare        p_vare
6.14 c                   call      'VE515R'
6.14 c                   parm                    w_firm
6.14 c                   parm                    p_vare
6.14 c                   eval      b1valg = 0
6.14 c                   update    b1sfl
6.14 c                   iter
6.14 c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * C2BLD Behandle bilde for ny - endring - vise
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
     c                   eval      b_eaok = *off
      *
     c                   eval      vveplr_pvar = c1pvar
     c                   eval      vveplr_penh = c1penh
     c                   eval      vveplr_pldo = c1pldo
     c     vveplr_key    chain     vveplr                             90
      *
     c                   if        *in90 = *off
6.10 c                   eval      c2pgti = vepgti
6.10 c                   eval      s_pgti = vepgti
6.11 c                   eval      c2pkla = vepkla
     c                   eval      c2pbes = vepbes
     c                   eval      c2peus = vepeus
     c                   if        vepoda <> *loval
     c     *dmy          move      vepoda        c2poda
     c                   else
     c                   eval      c2poda = 0
     c                   endif
     c                   if        vepeda <> *loval
     c     *dmy          move      vepeda        c2peda
     c                   else
     c                   eval      c2peda = 0
     c                   endif
     c                   else
6.10 c                   eval      c2pgti = 0
6.10 c                   eval      s_pgti = 0
     c                   eval      c2peus = *blank
     c                   eval      c2poda = 0
     c                   eval      c2peda = 0
     c                   endif
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
     c                   if        c2pgti = 0 and
     c                             c2pgti = s_pgti and
     c                             c2pkla = *blank and
     c                             c2pbes = 0
     c                   goto      end_c2bld
     c                   endif
      *
     c                   if        c2pgti <> 0
     c                   exsr      sjekk_ean
     c                   if        b_eaok = *off
     c                   eval      *in31 = *on
     c                   goto      c2tagb
     c                   endif
     c                   endif
6.11  *
6.11  * Sjekker om inntastet ean-nr allerede ligger på vare-enhet
6.11  *
     c                   if        c2pgti <> 0 and
     c                             c2pgti <> s_pgti
6.12 c                   eval      vvepl2_pgti = c2pgti
6.12 c     vvepl2_key    chain     vvepl2
6.12 c                   if        %found(vvepl2)
6.11 c                   eval      *in32 = *on
6.11 c                   goto      c2tagb
6.11 c                   endif
     c                   endif
      *
      *
      * Oppdatering
      *
     c                   if        c2pgti <> 0
     c                   clear                   vveplur
      *
     c                   eval      vveplu_pvar = c1pvar
     c                   eval      vveplu_penh = c1penh
     c                   eval      vveplu_pldo = c1pldo
     c     vveplu_key    chain     vveplur                            90
      *
6.10 c                   eval      vepgti = c2pgti
     c                   eval      vepeus = l_user
      *
     c                   time                    vepeda
     c                   time                    vepeti
      *
     c                   if        *in90 = *off
     c                   update    vveplur
     c                   else
     c                   eval      vepfir = w_firm
     c                   eval      vepvar = c1pvar
     c                   eval      vepenh = c1penh
     c                   eval      vepldo = c1pldo
     c                   time                    vepoda
     c                   time                    vepoti
     c                   eval      vepous = l_user
     c                   write     vveplur
     c                   endif
      *
     c                   endif
      *
      *  sjekker om den kan slettes
      *
     c                   if        c2pgti = 0 and
     c                             c2pbes = 0 and
     c                             c2pkla = *blank
     c                   eval      vveplu_pvar = c1pvar
     c                   eval      vveplu_penh = c1penh
     c                   eval      vveplu_pldo = c1pldo
     c     vveplu_key    chain     vveplu
     c                   if        %found(vveplu)
     c                   delete    vveplur
     c                   endif
     c                   endif
      *
     c                   if        s_pgti <> 0 and
     c                             s_pgti <> vepgti
     c                   exsr      upd_eann
     c                   endif
     c                   exsr      rydd_eann
      *
      * Oppdatering av søketekster
6.20  * I EVR + hver gang
6.20 c**                 if        s_pgti <> 0
6.20 c**                 call      'JV790R'
6.20 c                   call      'VV750R'
     c                   parm                    w_firm
     c                   parm                    vepvar
6.20 c**                 parm                    b_inlr
6.20 c**                 endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
6.10 c                   eval      b1pgti = c2pgti
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
6.30  *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  * F1 Behandle bilde for bla-alternativer
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  *
6.30 c     xf1win        begsr
6.30  *
6.30  *  Send Alt
6.30  *
6.30 c     xf1tag        tag
6.30  *
6.30 c     f1taga        tag
6.30 c                   exfmt     f1win
6.30  *
6.30  * Behandling av funksjonstaster
6.30  *
6.30 c                   select
6.30 c                   when      *inkc or *inkl
6.30 c                   goto      end_f1win
6.30 c                   endsl
6.30  *
6.30 c                   if        f1gtin <> 0
     c                   eval      *in50 = *off
6.30 c                   eval      veanlr_eann = f1gtin
6.30 c     veanlr_key    chain     veanlr
6.30 c                   if        %found(veanlr)
     c                   eval      *in50 = *on
6.30 c                   eval      b2text = 'GTIN finnes i gammelt EAN-reg'
6.30 C                   else
6.30 c                   eval      b2text = *blank
6.30 c                   endif
6.30 c                   eval      w_seqe = 'G'
6.30 c                   eval      vvepl2_pgti = f1gtin
6.30 c                   else
6.30 c                   eval      b2text = *blank
6.30 c                   eval      w_seqe = ' '
6.30 c                   eval      vvenl1_vare = *blank
6.30 c                   endif
6.30  *
6.30 c     end_f1win     endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Fyll opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = ' '
     c                   read      vvenl1
6.13 c                   when      w_seqe = 'G'
6.13 c                   read      vvepl2
     c                   endsl
6.13  *
6.13 c                   select
6.13 c                   when      w_seqe = ' '
6.13 c                   if        %eof or
6.13 c                             vefirm <> w_firm
6.13 c                   eval      *in15 = *on
6.13 c                   leave
6.13 c                   endif
6.13 c                   when      w_seqe = 'G'
6.13 c                   if        %eof or
6.13 c                             vepfir <> w_firm
6.13 c                   eval      *in15 = *on
6.13 c                   leave
6.13 c                   endif
     c                   endsl
      *
6.30  * filter
6.30  *
6.30 c                   if        w_seqe = 'G'and
6.30 c                             f1gtin <> 0 and
6.30 c                             vepgti <> f1gtin
6.30 c                   iter
6.30 c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = ' '
     c                   eval      vvenl1_vare = vevare
6.13 c                   eval      vvarl1_vare = vevare
6.13 c                   eval      b1vare = vevare
6.13 c                   eval      b1venh = veenhe
6.13 c                   when      w_seqe = 'G'
6.13 c                   eval      vvenl1_vare = vepvar
6.13 c                   eval      vvarl1_vare = vepvar
6.13 c                   eval      b1vare = vepvar
6.13 c                   eval      b1venh = vepenh
     c                   endsl
6.13 c**                 eval      vvarl1_vare = vevare
     c     vvarl1_key    chain     vvarl1
6.10 c                   eval      b1ldor = vvldor
     c                   eval      rlevl1_ldor = vvldor
6.31  * Hvis valg på getinnr. bruk  leverandørnr fra VVEPL2
6.31 c                   if        b2pgti <> 0
6.31 c                   eval      b1ldor = vepldo
6.31 c                   eval      rlevl1_ldor = vepldo
6.31 c                   endif
     c     rlevl1_key    chain     rlevl1
      *
6.10 c**6.13             eval      b1vare = vevare
     c                   movel     vvtek1        b1tek1
     c                   movel     rlnavn        b1lnav
6.10 c**6.13             eval      b1venh = veenhe
      *
6.13 c                   eval      b1pgti = 0
6.13 c                   select
6.13 c                   when      w_seqe = ' '
     c                   eval      vvenl1_vare = vevare
6.13 c**                 eval      b1pgti = 0
     c                   eval      vveplr_pvar = vevare
     c                   eval      vveplr_penh = veenhe
     c                   eval      vveplr_pldo = vvldor
     c     vveplr_key    chain     vveplr                             90
     c                   if        %found(vveplr) and
     c                             vepgti <> 0
     c                   eval      b1pgti = vepgti
     c                   endif
6.13 c                   when      w_seqe = 'G'
6.13 c                   eval      b1pgti = vepgti
6.13 c                   endsl
      *
     c                   write     b1sfl
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = ' '
     c     vvenl1_key    setgt     vvenl1
     c                   readp     vvenl1
6.13 c                   when      w_seqe = 'G'
6.13 c     vvepl2_key    setgt     vvepl2
6.13 c                   readp     vvepl2
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = ' '
     c                   readp     vvenl1
6.13 c                   when      w_seqe = 'G'
6.13 c                   readp     vvepl2
     c                   endsl
6.13  *
6.13 c                   select
6.13 c                   when      w_seqe = ' '
6.13 c                   if        %eof or
6.13 c                             vefirm <> w_firm
6.13 c                   eval      *in15 = *on
6.13 c                   leave
6.13 c                   endif
6.13 c                   when      w_seqe = 'G'
6.13 c                   if        %eof or
6.13 c                             vepfir <> w_firm
6.13 c                   eval      *in15 = *on
6.13 c                   leave
6.13 c                   endif
6.13 c                   endsl
      *
6.13 c                   select
6.13 c                   when      w_seqe = ' '
     c                   if        %eof or
     c                             vevare <> w_vare
     c                   leave
     c                   endif
6.13 c**                 when      w_seqe = 'G'
6.13 c**                 if        %eof or
6.13 c**                           vepgti <> w_pgti
6.13 c**                 leave
6.13 c**                 endif
6.13 c                   endsl
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = ' '
     c                   eval      vvenl1_vare = vevare
6.13 c                   when      w_seqe = 'G'
6.13 c                   eval      vvenl1_vare = vepvar
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = ' '
     c     vvenl1_key    setll     vvenl1
6.13 c                   when      w_seqe = 'G'
6.13 c     vvepl2_key    setll     vvepl2
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Varenummer
      *
6.11 c                   if        w_afld = 'B2VARE'
     c                   eval      w_vare = *blank
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_tek1
     c                   movel     w_vare        b2vare
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
6.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.11  * Subrutine for å legge ut gammel gtin  til veanpf
6.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.11  *
6.11 c     upd_eann      begsr
6.11  *
     c                   if        s_pgti <> 0
6.11  *
     c                   clear                   veanlur
6.16 c                   eval      veanlu_eann = s_pgti
6.11 c     veanlu_key    chain     veanlu
     c                   eval      vxvare = vepvar
     c                   eval      vxenhe = vepenh
     c                   time                    vxtime
     c                   time                    vxedat
     c                   time                    vxetim
     c                   eval      vxeusr = l_user
6.11 c                   if        not %found(veanlu)
     c                   eval      vxfirm = w_firm
     c                   eval      vxeann = s_pgti
     c                   time                    vxodat
     c                   time                    vxotim
     c                   eval      vxousr = l_user
6.11 c                   write     veanlur
     c                   else
6.11 c                   update    veanlur
6.11 c                   endif
6.11  *
6.11 c                   endif
6.11  *
6.11 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å rydde ean-nr på veanpf som ligger på vvenpf
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     rydd_eann     begsr
      *
     c                   eval      veanlu_eann = vepgti
     c     veanlu_key    chain     veanlu
     c                   if        %found(veanlu)
     c                   delete    veanlur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke om gyldig ean-nr er angitt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_ean     begsr
      *
     c                   eval      w_rkod = '1'
     c                   call      'FÅ900R'
     c                   parm                    c2pgti
     c                   parm                    w_rkod
      *
     c                   if        w_rkod = ' '
     c                   eval      b_eaok = *on
     c                   endif
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
6.30  *
6.30  * Parametre
6.30  *
6.30 c     *entry        plist
6.30 c                   parm                    p_gtin
      *
      * Key for posisjonering på pakning vare
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
      *
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på lev.
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_ldor
      *
      * Key for oppslag på pvar-penhing
      *
     c     vveplr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vveplr_pvar
     c                   kfld                    vveplr_penh
     c                   kfld                    vveplr_pldo
      *
      * Key for oppdatering av pvar-penhing
      *
     c     vveplu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vveplu_pvar
     c                   kfld                    vveplu_penh
     c                   kfld                    vveplu_pldo
      *
      * Key for oppdatering av EAN
      *
     c     veanlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    veanlu_eann
6.30  *
6.30  * Key for les av EAN
6.30  *
6.30 c     veanlr_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    veanlr_eann
6.11  *
6.11  * Key for oppslag på vare-enhet
6.11  *
6.12 c     vvepl2_key    klist
6.11 c                   kfld                    w_firm
6.12 c                   kfld                    vvepl2_pgti
      *
      *
      * Henter firma og vare fra parameter
      *
     c                   eval      w_firm = l_firm
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      *in22 = *on
      *
6.30 c                   if        p_gtin <> 0
     c                   eval      *in50 = *off
6.30 c                   eval      veanlr_eann = p_gtin
6.30 c     veanlr_key    chain     veanlr
6.30 c                   if        %found(veanlr)
     c                   eval      *in50 = *on
6.30 c                   eval      b2text = 'GTIN finnes i gammelt EAN-reg'
6.30 C                   else
6.30 c                   eval      b2text = *blank
6.30 c                   endif
6.30 c                   eval      w_seqe = 'G'
6.30 c                   eval      f1gtin = p_gtin
6.30 c                   eval      vvepl2_pgti = p_gtin
6.30 c     vvepl2_key    setll     vvepl2
6.30 c                   else
     c                   eval      vvenl1_vare = *blank
     c     vvenl1_key    setll     vvenl1
6.30 c                   endif
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
