      /title  ASPGPL Datorutine, retur av start- / sluttdato for år/uke
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASPGPL
      * Program . . . . : AX710R
      * Beskrivelse . . : Datorutine, retur av start- / sluttdato for år/uke
      *
      *                   Parametre :
      *                   Inn: År/ukenr.
      *                   Ut : Fra-dato (man.), til-dato (søn.), status
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       140904 GRO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Definering av parametre
      *
     d p_aarr          s              4  0
     d p_week          s              2  0
     d p_fdat          s               d
     d p_tdat          s               d
     d p_stat          s              1
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_aarr          s              4
     d w_wdat          s               d   datfmt(*iso)
     d w_wday          s             11p 0
     d w_wddw          s             11p 0
     d w_wdax          s             10
     d w_monday        s               d   datfmt(*iso)
     d                                     inz(d'2001-12-31')
     d w_mon1          s               d   datfmt(*iso)
     d w_monw          s               d   datfmt(*iso)
     d w_sunw          s               d   datfmt(*iso)
     d w_wdtn          s               d   datfmt(*iso)
     d w_wdnx          s             10
     d w_naar          s              4  0
     d w_naax          s              4
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogam
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Sjekker at år/uke er utfyllt - hvis ikke returneres status '1'
      *
     c                   if        p_aarr < 1 or
     c                             p_week < 1 or
     c                             p_week > 53
     c                   eval      p_stat = '1'
     c                   goto      avslutt
     c                   endif
      *
      * Finn dagnr. for 4.jan. - ISO-std.: Alltid i uke nr 1....
      *
     c                   eval      w_wdat = p_fdat
     c                   move      w_wdat        w_wdax
     c                   move      p_aarr        w_aarr
     c                   eval      %subst(w_wdax:1:4) = w_aarr
     c                   eval      %subst(w_wdax:9:2) = '04'
     c                   move      w_wdax        w_wdat
      *
     c                   eval      w_wdnx = w_wdax
     c                   eval      w_naar = p_aarr + 1
     c                   move      w_naar        w_naax
     c                   eval      %subst(w_wdnx:1:4) = w_naax
     c                   eval      %subst(w_wdnx:9:2) = '04'
     c                   move      w_wdnx        w_wdtn
      *
      * Finner ukedag for 4.jan. i aktuelt år.
      *
     c                   eval      w_wday = %diff(w_wdat:w_monday:*days)
     c                   div       7             w_wday
     c                   mvr                     w_wday
     c                   eval      w_wday = w_wday + 1
      *
     c                   if        w_wday <= 0
     c                   eval      w_wday = w_wday + 7
     c                   endif
      *
      * Finner dato for mandag i uke 1
      *
     c                   if        w_wday = 1
     c                   eval      w_mon1 = w_wdat
     c                   else
     c                   eval      w_wday = w_wday - 1
     c     w_wdat        subdur    w_wday : *d   w_mon1
     c                   endif
      *
      * Finner dato for mandag (dag 1) i aktuell uke (param.)
      *
     c                   eval      w_wddw = (p_week - 1) * 7
     c     w_mon1        adddur    w_wddw : *d   w_monw
      *
      * Finner dato for søndag (dag 7) i aktuell uke (param.)
      *
     c     w_monw        adddur    6 : *d        w_sunw
      *
     c                   eval      p_fdat = w_monw
     c                   eval      p_tdat = w_sunw
     c                   eval      p_stat = '0'
      *
      * Sjekk om param.= uke 53 og denne ikke finnes (dvs uke 1; året etter)
      *
     c                   if        p_week = 53
     c                   if        p_tdat >= w_wdtn
     c                   eval      p_aarr = p_aarr + 1
     c                   eval      p_week = 1
     c                   endif
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_aarr
     c                   parm                    p_week
     c                   parm                    p_fdat
     c                   parm                    p_tdat
     c                   parm                    p_stat
      *
      * Initierer variable
      *
     c                   eval      p_stat = *blank
     c                   eval      p_fdat = *loval
     c                   eval      p_tdat = *loval
      *
     c                   endsr
