     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VT110R
      * Beskrivelse . . : Tilbudspriser, vedlikehold for vare
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       180599 HKO  Nytt program
5.70  * PRGR  301108 BOF  Utvidet med prisgruppe
      * 6.10  260609 BSA  Oppdatert med sporingsinformasjon
6.20  * 6.20  110511 NHO  Endring key på filer
6.21  * 6.21  210212 bof  Viser innkjøpspriser
6.22  * 6.20  080312 bof  Utvidet kall til VP715r
7.xx  * 7.00  030216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvtillr    if   e           k disk    rename(vtilpfr:vtillrr)
     fvtillu    uf a e           k disk    rename(vtilpfr:vtillur)
     fvtill1    if   e           k disk    rename(vtilpfr:vtill1r)
     fvtill2    if   e           k disk    rename(vtilpfr:vtill2r)
     fvtill3    if   e           k disk    rename(vtilpfr:vtill3r)
     fvtill4    if   e           k disk    rename(vtilpfr:vtill4r)
     fvtill5    if   e           k disk    rename(vtilpfr:vtill5r)
5.70 fvtill6    if   e           k disk    rename(vtilpfr:vtill6r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
5.70 fra30l1    if   e           k disk    rename(ra30pfr:ra30l1r)
     fvenhl1    if   e           k disk    rename(venhpfr:venhl1r)
      *
     fvt110d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vtfirm)
     d p_vare          s                   like(vtvare)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
6.22 d l_lagr               1001   1002  0
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variable i nøkler
      *
6.20 d vtillr_ogrp     s                   like(vtogrp)
6.20 d vtillr_hgrp     s                   like(vthgrp)
6.20 d vtillr_ugrp     s                   like(vtugrp)
6.20 d vtillr_ldor     s                   like(vtldor)
     d vtillr_vare     s                   like(vtvare)
5.70 d vtillr_prgr     s                   like(vtprgr)
     d vtillr_lety     s                   like(vtlety)
     d vtillr_fdat     s                   like(vtfdat)
     d vtillr_ftim     s                   like(vtftim)
     d vtillr_tdat     s                   like(vttdat)
     d vtillr_ttim     s                   like(vtttim)
6.20 d vtillu_ogrp     s                   like(vtogrp)
6.20 d vtillu_hgrp     s                   like(vthgrp)
6.20 d vtillu_ugrp     s                   like(vtugrp)
6.20 d vtillu_ldor     s                   like(vtldor)
     d vtillu_vare     s                   like(vtvare)
5.70 d vtillu_prgr     s                   like(vtprgr)
     d vtillu_lety     s                   like(vtlety)
     d vtillu_fdat     s                   like(vtfdat)
     d vtillu_ftim     s                   like(vtftim)
     d vtillu_tdat     s                   like(vttdat)
     d vtillu_ttim     s                   like(vtttim)
6.20 d vtill1_ogrp     s                   like(vtogrp)
6.20 d vtill1_hgrp     s                   like(vthgrp)
6.20 d vtill1_ugrp     s                   like(vtugrp)
6.20 d vtill1_ldor     s                   like(vtldor)
     d vtill1_vare     s                   like(vtvare)
     d vtill2_lety     s                   like(vtlety)
     d vtill3_fdat     s                   like(vtfdat)
     d vtill4_tdat     s                   like(vttdat)
     d vtill5_kamp     s                   like(vtkamp)
5.70 d vtill6_prgr     s                   like(vtprgr)
     d vvarl1_vare     s                   like(vvvare)
     d vltpl1_lety     s                   like(vylety)
5.70 d ra30l1_dkod     s                   like(redkod)
     d venhl1_eenh     s                   like(vaeenh)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_udat          s               d   datfmt(*iso)
     d w_fdat          s              6  0
     d w_ftim          s              6  0
     d w_tdat          s              6  0
     d w_ttim          s              6  0
      *
     d s_fdat          s              6  0
     d s_fimt          s              4  0
     d s_tdat          s              6  0
     d s_timt          s              4  0
      *
     d w_firm          s                   like(vtfirm)
     d w_lety          s                   like(vtlety)
5.70 d w_prgr          s                   like(vtprgr)
     d w_ltxt          s                   like(vybesk)
5.70 d w_ptxt          s                   like(redtxt)
     d w_etxt          s                   like(vaetxt)
     d w_bup1          s                   like(vttip1)
     d w_bup2          s                   like(vttip2)
     d w_bup3          s                   like(vttip3)
      *
     d w_fdat_w        s                   like(vtfdat)
     d w_ftim_w        s                   like(vtftim)
     d w_tdat_w        s                   like(vttdat)
     d w_ttim_w        s                   like(vtttim)
6.22 d w_lagr          s              2  0
      *
     d s_lety          s                   like(vtlety)
5.70 d s_prgr          s                   like(vtprgr)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(12)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meleingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2lety <> 0 or
5.70 c                             b2prgr <> *blank or
     c                             b2fdat <> 0 or
     c                             b2tdat <> 0 or
     c                             b2kamp <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   eval      vtill2_lety = b1lety
     c     vtill2_key    setll     vtill2
     c                   when      w_seqe = 'F'
     c     *dmy          move      b1fdat        vtill3_fdat
     c     vtill3_key    setll     vtill3
     c                   when      w_seqe = 'T'
     c     *dmy          move      b1tdat        vtill4_tdat
     c     vtill4_key    setll     vtill4
     c                   when      w_seqe = 'K'
     c                   eval      vtill5_kamp = b1kamp
     c     vtill5_key    setll     vtill5
5.70 c                   when      w_seqe = 'P'
5.70 c                   eval      vtill6_prgr = b1prgr
5.70 c     vtill6_key    setll     vtill6
     c                   other
     c     vtill1_key    setll     vtill1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på leveringstype
      *
     c                   if        b2lety <> 0
     c                   eval      w_seqe = 'L'
     c                   eval      vtill2_lety = b2lety
     c     vtill2_key    setll     vtill2
     c                   goto      end_pos
     c                   endif
5.70  *
5.70  * Posisjoner startverdi på prisgruppe
5.70  *
5.70 c                   if        b2prgr <> *blank
5.70 c                   eval      w_seqe = 'P'
5.70 c                   eval      vtill6_prgr = b2prgr
5.70 c     vtill6_key    setll     vtill6
5.70 c                   goto      end_pos
5.70 c                   endif
      *
      * Posisjoner startverdi på fra-dato
      *
     c                   if        b2fdat <> 0
     c                   eval      w_seqe = 'F'
     c     *dmy          test(d)                 b2fdat                 80
     c                   if        *in80 = *on
     c                   eval      vtill3_fdat = *hival
     c                   else
     c     *dmy          move      b2fdat        vtill3_fdat
     c                   endif
     c     vtill3_key    setll     vtill3
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på til-dato
      *
     c                   if        b2tdat <> 0
     c                   eval      w_seqe = 'T'
     c     *dmy          test(d)                 b2tdat                 80
     c                   if        *in80 = *on
     c                   eval      vtill4_tdat = *hival
     c                   else
     c     *dmy          move      b2tdat        vtill4_tdat
     c                   endif
     c     vtill4_key    setll     vtill4
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på kampanje
      *
     c                   if        b2kamp <> *blank
     c                   eval      w_seqe = 'K'
     c                   eval      vtill5_kamp = b2kamp
     c     vtill5_key    setll     vtill5
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2lety = 0
5.70 c                   eval      b2prgr = *blank
     c                   eval      b2fdat = 0
     c                   eval      b2tdat = 0
     c                   eval      b2kamp = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1lety = b1lety
5.70 c                   eval      c1prgr = b1prgr
     c                   eval      c1fdat = b1fdat
     c                   eval      c1ftim = b1ftim
     c                   eval      c1tdat = b1tdat
     c                   eval      c1ttim = b1ttim
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1lety = b1lety
5.70 c                   eval      k1prgr = b1prgr
     c                   eval      k1fdat = b1fdat
     c                   eval      k1ftim = b1ftim
     c                   eval      k1tdat = b1tdat
     c                   eval      k1ttim = b1ttim
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1lety = b1lety
5.70 c                   eval      d1prgr = b1prgr
     c                   eval      d1fdat = b1fdat
     c                   eval      d1ftim = b1ftim
     c                   eval      d1tdat = b1tdat
     c                   eval      d1ttim = b1ttim
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1lety = b1lety
5.70 c                   eval      c1prgr = b1prgr
     c                   eval      c1fdat = b1fdat
     c                   eval      c1ftim = b1ftim
     c                   eval      c1tdat = b1tdat
     c                   eval      c1ttim = b1ttim
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1lety = b2lety
5.70 c                   eval      c1prgr = b2prgr
     c                   eval      c1fdat = b2fdat
     c                   eval      c1ftim = 0
     c                   eval      c1tdat = b2tdat
     c                   eval      c1ttim = 2400
      *
     c                   exsr      hent_info
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      c1taga
     c                   endsl
      *
     c                   if        c1lety = 0 and
5.70 c                             c1prgr = *blank and
     c                             c1fdat = 0 and
     c                             c1tdat = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Sjekker input
      *
     c                   if        c1ttim = 0
     c                   eval      c1ttim = 2400
     c                   endif
      *
     c                   eval      w_lety = c1lety
5.70 c                   eval      w_prgr = c1prgr
     c                   eval      w_fdat = c1fdat
     c                   eval      w_ftim = c1ftim * 100
     c                   eval      w_tdat = c1tdat
     c                   eval      w_ttim = c1ttim * 100
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      c1taga
     c                   endif
      *
      * Sender opp informasjonsbildet dersom rad finnes fra før
      *
     c                   eval      vtillr_vare = p_vare
     c                   eval      vtillr_lety = c1lety
5.07 c                   eval      vtillr_prgr = c1prgr
     c     *dmy          move      c1fdat        vtillr_fdat
     c     *dmy          move      c1tdat        vtillr_tdat
     c                   eval      w_ftim = c1ftim * 100
     c     *hms          move      w_ftim        vtillr_ftim
     c                   eval      w_ttim = c1ttim * 100
     c     *hms          move      w_ttim        vtillr_ttim
     c     vtillr_key    chain     vtillr                             90
     c                   if        *in90 = *off
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1lety = 0
5.70 c                   eval      c1prgr = *blank
     c                   eval      c1ltxt = *blank
5.70 c                   eval      c1ptxt = *blank
     c                   eval      c1fdat = 0
     c                   eval      c1ftim = 0
     c                   eval      c1tdat = 0
     c                   eval      c1ttim = 2400
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2lety = 0
5.70 c                   eval      b2prgr = *blank
     c                   eval      b2fdat = 0
     c                   eval      b2tdat = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * C2BLD Behandle bilde for ny - endring - vise
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      vtillr_vare = p_vare
     c                   eval      vtillr_lety = c1lety
5.70 c                   eval      vtillr_prgr = c1prgr
     c     *dmy          move      c1fdat        vtillr_fdat
     c     *dmy          move      c1tdat        vtillr_tdat
     c                   eval      w_ftim = c1ftim * 100
     c     *hms          move      w_ftim        vtillr_ftim
     c                   eval      w_ttim = c1ttim * 100
     c     *hms          move      w_ttim        vtillr_ttim
     c     vtillr_key    chain     vtillr                             90
      *
     c                   if        *in90 = *off
      *
     c                   eval      *in80  = *off
     c                   eval      c2kamp = vtkamp
     c                   eval      c2enh1 = vtenh1
     c                   eval      c2tip1 = vttip1
     c                   eval      c2kop1 = vtkop1
6.21 c                   eval      c2inp1 = vtinp1
     c                   eval      c2enh2 = vtenh2
     c                   eval      c2tip2 = vttip2
     c                   eval      c2kop2 = vtkop2
6.21 c                   eval      c2inp2 = vtinp2
     c                   eval      c2enh3 = vtenh3
     c                   eval      c2tip3 = vttip3
     c                   eval      c2kop3 = vtkop3
6.21 c                   eval      c2inp3 = vtinp3
     c                   eval      c2eusr = vteusr
      *
     c                   eval      c2sap1 = 0
     c                   eval      c2sap2 = 0
     c                   eval      c2omr2 = 0
     c                   eval      c2sap3 = 0
     c                   eval      c2omr3 = 0
      *
     c     *dmy          move      vtedat        c2edat
      *
     c                   else
      *
     c                   eval      *in80  = *on
     c                   eval      c2kamp = *blank
     c                   eval      c2tip1 = 0
     c                   eval      c2kop1 = 0
     c                   eval      c2tip2 = 0
     c                   eval      c2kop2 = 0
6.21 c                   eval      c2inp2 = 0
     c                   eval      c2tip3 = 0
     c                   eval      c2kop3 = 0
6.21 c                   eval      c2inp3 = 0
     c                   eval      c2edat = 0
     c                   eval      c2eusr = *blank
      *
     c                   call      'VP715R'
     c                   parm                    w_firm
     c                   parm                    p_vare
5.70 c                   parm                    c1prgr
6.22 c                   parm                    w_lagr
     c                   parm                    c1lety
     c                   parm                    w_udat
     c                   parm                    c2enh1
     c                   parm                    c2sap1
     c                   parm                    w_bup1
     c                   parm                    c2enh2
     c                   parm                    c2sap2
     c                   parm                    w_bup2
     c                   parm                    c2omr2
     c                   parm                    c2enh3
     c                   parm                    c2sap3
     c                   parm                    w_bup3
     c                   parm                    c2omr3
      *
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1lety = k1lety
5.70 c                   eval      c1prgr = k1prgr
     c                   eval      c1fdat = k1fdat
     c                   eval      c1ftim = k1ftim
     c                   eval      c1tdat = k1tdat
     c                   eval      c1ttim = k1ttim
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
      * Henter beskrivelse av nivå
      *
     c                   exsr      hent_info
      *
      * Behandler bildet
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd = *on
      *
     c                   if        w_afld = 'C2ENH1'
     c                   call      'VE550R'
     c                   parm                    w_firm
     c                   parm                    p_vare
     c                   parm                    c2enh1
     c                   parm                    w_etxt
     c                   goto      c2taga
     c                   endif
      *
     c                   if        w_afld = 'C2ENH2'
     c                   call      'VE550R'
     c                   parm                    w_firm
     c                   parm                    p_vare
     c                   parm                    c2enh2
     c                   parm                    w_etxt
     c                   goto      c2taga
     c                   endif
      *
     c                   if        w_afld = 'C2ENH3'
     c                   call      'VE550R'
     c                   parm                    w_firm
     c                   parm                    p_vare
     c                   parm                    c2enh3
     c                   parm                    w_etxt
     c                   goto      c2taga
     c                   endif
      *
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2tip1 = 0 and
     c                             c2tip2 = 0 and
     c                             c2tip3 = 0
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   if        c2enh1 <> *blank
     c                   eval      venhl1_eenh = c2enh1
     c     venhl1_key    chain     venhl1                             91
     c                   if        *in91 = *on
     c                   eval      *in32 = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
     c                   if        c2tip1 <> 0 and
     c                             c2enh1 = *blank
     c                   eval      *in33 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   if        c2enh2 <> *blank
     c                   eval      venhl1_eenh = c2enh2
     c     venhl1_key    chain     venhl1                             91
     c                   if        *in91 = *on
     c                   eval      *in34 = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
     c                   if        c2tip2 <> 0 and
     c                             c2enh2 = *blank
     c                   eval      *in35 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   if        c2enh3 <> *blank
     c                   eval      venhl1_eenh = c2enh3
     c     venhl1_key    chain     venhl1                             91
     c                   if        *in91 = *on
     c                   eval      *in36 = *on
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
     c                   if        c2tip3 <> 0 and
     c                             c2enh3 = *blank
     c                   eval      *in37 = *on
     c                   goto      c2taga
     c                   endif
      *
      * Organiserer input
      *
     c                   if        c2tip1 = 0
     c                   eval      c2enh1 = *blank
     c                   eval      c2kop1 = 0
6.21 c                   eval      c2inp1 = 0
     c                   endif
      *
     c                   if        c2tip2 = 0
     c                   eval      c2enh2 = *blank
     c                   eval      c2kop2 = 0
6.21 c                   eval      c2inp2 = 0
     c                   endif
      *
     c                   if        c2tip3 = 0
     c                   eval      c2enh3 = *blank
     c                   eval      c2kop3 = 0
6.21 c                   eval      c2inp3 = 0
     c                   endif
      *
     c                   if        c2tip2 = 0 and
     c                             c2tip3 <> 0
     c                   eval      c2enh2 = c2enh3
     c                   eval      c2tip2 = c2tip3
     c                   eval      c2kop2 = c2kop3
6.21 c                   eval      c2inp2 = c2inp3
     c                   eval      c2enh3 = *blank
     c                   eval      c2tip3 = 0
     c                   eval      c2kop3 = 0
6.21 c                   eval      c2inp3 = 0
     c                   endif
      *
     c                   if        c2tip1 = 0 and
     c                             c2tip2 <> 0
     c                   eval      c2enh1 = c2enh2
     c                   eval      c2tip1 = c2tip2
     c                   eval      c2kop1 = c2kop2
6.21 c                   eval      c2inp1 = c2inp2
     c                   eval      c2enh2 = c2enh3
     c                   eval      c2tip2 = c2tip3
     c                   eval      c2kop2 = c2kop3
6.21 c                   eval      c2inp2 = c2inp3
     c                   eval      c2enh3 = *blank
     c                   eval      c2tip3 = 0
     c                   eval      c2kop3 = 0
6.21 c                   eval      c2inp3 = 0
     c                   endif
      *
      * Oppdatering av rad
      *
     c                   clear                   vtillur
      *
     c                   eval      vtillu_vare = p_vare
     c                   eval      vtillu_lety = c1lety
5.70 c                   eval      vtillu_prgr = c1prgr
     c     *dmy          move      c1fdat        vtillu_fdat
     c     *dmy          move      c1tdat        vtillu_tdat
     c                   eval      w_ftim = c1ftim * 100
     c     *hms          move      w_ftim        vtillu_ftim
     c                   eval      w_ttim = c1ttim * 100
     c     *hms          move      w_ttim        vtillu_ttim
     c     vtillu_key    chain     vtillur                            90
      *
     c                   eval      vtkamp = c2kamp
     c                   eval      vtenh1 = c2enh1
     c                   eval      vttip1 = c2tip1
     c                   eval      vtkop1 = c2kop1
6.21 c                   eval      vtinp1 = c2inp1
     c                   eval      vtenh2 = c2enh2
     c                   eval      vttip2 = c2tip2
     c                   eval      vtkop2 = c2kop2
6.21 c                   eval      vtinp2 = c2inp2
     c                   eval      vtenh3 = c2enh3
     c                   eval      vttip3 = c2tip3
     c                   eval      vtkop3 = c2kop3
6.21 c                   eval      vtinp3 = c2inp3
     c                   eval      vteusr = l_user
      *
     c                   time                    vtetim
     c                   time                    vtedat
      *
     c                   if        *in90 = *off
     c                   update    vtillur
     c                   else
     c                   eval      vtfirm = w_firm
     c                   eval      vtvare = p_vare
     c                   eval      vtlety = c1lety
5.70 c                   eval      vtprgr = c1prgr
     c     *dmy          move      c1fdat        vtfdat
     c     *dmy          move      c1tdat        vttdat
     c                   eval      w_ftim = c1ftim * 100
     c     *hms          move      w_ftim        vtftim
     c                   eval      w_ttim = c1ttim * 100
     c     *hms          move      w_ttim        vtttim
6.10 c                   time                    vtodat
6.10 c                   time                    vtotim
6.10 c                   eval      vtousr = l_user
     c                   write     vtillur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1kamp = c2kamp
     c                   eval      b1enh1 = c2enh1
     c                   eval      b1tip1 = c2tip1
     c                   eval      b1kop1 = c2kop1
     c                   eval      b1enh2 = c2enh2
     c                   eval      b1enh3 = c2enh3
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      d1vare = b2vare
      *
     c                   eval      vtillr_vare = p_vare
     c                   eval      vtillr_lety = d1lety
5.70 c                   eval      vtillr_prgr = d1prgr
     c     *dmy          move      d1fdat        vtillr_fdat
     c     *dmy          move      d1tdat        vtillr_tdat
     c                   eval      w_ftim = d1ftim * 100
     c     *hms          move      w_ftim        vtillr_ftim
     c                   eval      w_ttim = d1ttim * 100
     c     *hms          move      w_ttim        vtillr_ttim
     c     vtillr_key    chain     vtillr                             90
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      vtillu_vare = p_vare
     c                   eval      vtillu_lety = d1lety
5.70 c                   eval      vtillu_prgr = d1prgr
     c     *dmy          move      d1fdat        vtillu_fdat
     c     *dmy          move      d1tdat        vtillu_tdat
     c                   eval      w_ftim = d1ftim * 100
     c     *hms          move      w_ftim        vtillu_ftim
     c                   eval      w_ttim = d1ttim * 100
     c     *hms          move      w_ttim        vtillu_ttim
     c     vtillu_key    chain     vtillur                            90
     c                   delete    vtillur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_lety = k1lety
5.70 c                   eval      s_prgr = k1prgr
     c                   eval      s_fdat = k1fdat
     c                   eval      s_fimt = k1ftim
     c                   eval      s_tdat = k1tdat
     c                   eval      s_timt = k1ttim
      *
      * Fyller kopierings-bildet
      *
     c                   eval      vtillr_vare = p_vare
     c                   eval      vtillr_lety = k1lety
5.70 c                   eval      vtillr_prgr = k1prgr
     c     *dmy          move      k1fdat        vtillr_fdat
     c     *dmy          move      k1tdat        vtillr_tdat
     c                   eval      w_ftim = k1ftim * 100
     c     *hms          move      w_ftim        vtillr_ftim
     c                   eval      w_ttim = k1ttim * 100
     c     *hms          move      w_ttim        vtillr_ttim
     c     vtillr_key    chain     vtillr                             90
      *
     c                   eval      k2fdat = k1fdat
     c                   eval      k2ftim = k1ftim
     c                   eval      k2tdat = k1tdat
     c                   eval      k2ttim = k1ttim
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd = *on
     c                   exsr      spØrring
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker input
      *
     c                   if        k1ttim = 0
     c                   eval      k1ttim = 2400
     c                   endif
      *
     c                   eval      w_lety = k1lety
5.70 c                   eval      w_prgr = k1prgr
     c                   eval      w_fdat = k1fdat
     c                   eval      w_ftim = k1ftim * 100
     c                   eval      w_tdat = k1tdat
     c                   eval      w_ttim = k1ttim * 100
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      vtillr_vare = p_vare
     c                   eval      vtillr_lety = k1lety
5.70 c                   eval      vtillr_prgr = k1prgr
     c     *dmy          move      k1fdat        vtillr_fdat
     c     *dmy          move      k1tdat        vtillr_tdat
     c                   eval      w_ftim = k1ftim * 100
     c     *hms          move      w_ftim        vtillr_ftim
     c                   eval      w_ttim = k1ttim * 100
     c     *hms          move      w_ttim        vtillr_ttim
     c     vtillr_key    chain     vtillr                             90
     c                   if        *in90 = *off
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1lety = s_lety
5.70 c                   eval      c1prgr = s_prgr
     c                   eval      c1fdat = s_fdat
     c                   eval      c1ftim = s_fimt
     c                   eval      c1tdat = s_tdat
     c                   eval      c1ttim = s_timt
      *
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Fyll opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   read      vtill2
     c                   when      w_seqe = 'F'
     c                   read      vtill3
     c                   when      w_seqe = 'T'
     c                   read      vtill4
     c                   when      w_seqe = 'K'
     c                   read      vtill5
     c                   when      w_seqe = 'P'
     c                   read      vtill6
     c                   other
     c     vtill1_key    reade     vtill1
     c                   endsl
      *
     c                   if        %eof or
     c                             vtfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        vtvare <> p_vare
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   eval      vtill2_lety = vtlety
     c                   when      w_seqe = 'F'
     c                   eval      vtill3_fdat = vtfdat
     c                   when      w_seqe = 'T'
     c                   eval      vtill4_tdat = vttdat
     c                   when      w_seqe = 'K'
     c                   eval      vtill5_kamp = vtkamp
5.70 c                   when      w_seqe = 'P'
5.70 c                   eval      vtill6_prgr = vtprgr
     c                   endsl
      *
     c                   eval      b1lety = vtlety
5.70 c                   eval      b1prgr = vtprgr
     c                   eval      b1kamp = vtkamp
     c                   eval      b1enh1 = vtenh1
     c                   eval      b1tip1 = vttip1
     c                   eval      b1kop1 = vtkop1
     c                   eval      b1enh2 = vtenh2
     c                   eval      b1enh3 = vtenh3
      *
     c     *dmy          move      vtfdat        b1fdat
     c     *dmy          move      vttdat        b1tdat
      *
     c     *hms          move      vtftim        w_ftim
     c                   movel     w_ftim        b1ftim
     c     *hms          move      vtttim        w_ttim
     c                   movel     w_ttim        b1ttim
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'L'
     c     vtill2_key    setgt     vtill2
     c                   readp     vtill2
     c                   when      w_seqe = 'F'
     c     vtill3_key    setgt     vtill3
     c                   readp     vtill3
     c                   when      w_seqe = 'T'
     c     vtill4_key    setgt     vtill4
     c                   readp     vtill4
     c                   when      w_seqe = 'K'
     c     vtill5_key    setgt     vtill5
     c                   readp     vtill5
     c                   when      w_seqe = 'P'
     c     vtill6_key    setgt     vtill6
     c                   readp     vtill6
     c                   other
     c     vtill1_key    setgt     vtill1
     c                   readp     vtill1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   readp     vtill2
     c                   when      w_seqe = 'F'
     c                   readp     vtill3
     c                   when      w_seqe = 'T'
     c                   readp     vtill4
     c                   when      w_seqe = 'K'
     c                   readp     vtill5
     c                   when      w_seqe = 'P'
     c                   readp     vtill6
     c                   other
     c                   readp     vtill1
     c                   endsl
      *
     c                   if        %eof or
     c                             vtfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   if        vtvare <> p_vare
     c                   iter
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'L'
     c                   eval      vtill2_lety = vtlety
     c                   when      w_seqe = 'F'
     c                   eval      vtill3_fdat = vtfdat
     c                   when      w_seqe = 'T'
     c                   eval      vtill4_tdat = vttdat
     c                   when      w_seqe = 'K'
     c                   eval      vtill5_kamp = vtkamp
5.70 c                   when      w_seqe = 'P'
5.70 c                   eval      vtill6_prgr = vtprgr
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'L'
     c     vtill2_key    setll     vtill2
     c                   when      w_seqe = 'F'
     c     vtill3_key    setll     vtill3
     c                   when      w_seqe = 'T'
     c     vtill4_key    setll     vtill4
     c                   when      w_seqe = 'K'
     c     vtill5_key    setll     vtill5
     c                   when      w_seqe = 'P'
     c     vtill6_key    setll     vtill6
     c                   other
     c     vtill1_key    setll     vtill1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
5.70  *
5.70  * Prisgruppe
5.70  *
5.70 c                   if        w_afld = 'B2PRGR'
5.70 c                   call      'RA530R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    b2prgr
5.70 c                   parm                    w_ptxt
5.70 c                   goto      end_spØrr
5.70 c                   endif
5.70  *
5.70 c                   if        w_afld = 'C1PRGR'
5.70 c                   call      'RA530R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    c1prgr
5.70 c                   parm                    c1ptxt
5.70 c                   goto      end_spØrr
5.70 c                   endif
5.70  *
5.70 c                   if        w_afld = 'K1PRGR'
5.70 c                   call      'RA530R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    k1prgr
5.70 c                   parm                    w_ptxt
5.70 c                   goto      end_spØrr
5.70 c                   endif
5.70  *
      *
      * Leveringstype
      *
     c                   if        w_afld = 'B2LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    b2lety
     c                   parm                    w_ltxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    c1lety
     c                   parm                    c1ltxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    k1lety
     c                   parm                    w_ltxt
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker inntastede verdier, og henter beskrivelser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
     c                   eval      w_ltxt = *blank
      *
      * Sjekk om komplett tilbudspris-nivå
      *
     c                   if        w_fdat = 0 or
     c                             w_tdat = 0
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
5.70  *
5.70  *Prisgruppe
5.70  *
5.70 c                   if        w_prgr <> *blank
5.70 c                   eval      ra30l1_dkod = w_prgr
5.70 c     ra30l1_key    chain     ra30l1                             43
5.70 c                   if        *in43 = *on
5.70 c                   eval      b_feil = *on
5.70 c                   goto      end_sjekk
5.70 c                   endif
5.70 c                   eval      w_ptxt = redtxt
5.70 c                   endif
      *
      * Leveringstype
      *
     c                   if        w_lety <> 0
     c                   eval      vltpl1_lety = w_lety
     c     vltpl1_key    chain     vltpl1                             36
     c                   if        *in36 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      w_ltxt = vybesk
     c                   endif
      *
      * Dato/tidspunkt
      *
     c     *dmy          test(d)                 w_fdat                 37
     c                   if        *in37 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     *hms          test(t)                 w_ftim                 38
     c                   if        *in38 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     *dmy          test(d)                 w_tdat                 39
     c                   if        *in39 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     *hms          test(t)                 w_ttim                 40
     c                   if        *in40 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     *dmy          move      w_fdat        w_fdat_w
     c     *dmy          move      w_tdat        w_tdat_w
      *
     c                   if        w_fdat_w > w_tdat_w
     c                   eval      *in41  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   if        w_fdat_w = w_tdat_w
     c     *hms          move      w_ftim        w_ftim_w
     c     *hms          move      w_ttim        w_ttim_w
     c                   if        w_ftim_w > w_ttim_w
     c                   eval      *in42  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter beskrivelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_info     begsr
      *
     c                   eval      c1vare = p_vare
     c                   eval      c1tek1 = b2tek1
     c                   eval      c1ltxt = *blank
5.70 c                   eval      c1ptxt = *blank
5.70  *
5.70  * prisgruppe
5.70  *
5.70 c                   if        c1prgr <> *blank
5.70 c                   eval      ra30l1_dkod = c1prgr
5.70 c     ra30l1_key    chain     ra30l1                             91
5.70 c                   if        *in91 = *off
5.70 c                   eval      c1ptxt = redtxt
5.70 c                   endif
5.70 c                   endif
      *
      * Leveringstype
      *
     c                   if        c1lety <> 0
     c                   eval      vltpl1_lety = c1lety
     c     vltpl1_key    chain     vltpl1                             91
     c                   if        *in91 = *off
     c                   eval      c1ltxt = vybesk
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
      *
      * Key for oppslag på tilbudspriser
      *
     c     vtillr_key    klist
     c                   kfld                    w_firm
6.20 c                   kfld                    vtillr_ogrp
6.20 c                   kfld                    vtillr_hgrp
6.20 c                   kfld                    vtillr_ugrp
6.20 c                   kfld                    vtillr_ldor
     c                   kfld                    vtillr_vare
5.70 c                   kfld                    vtillr_prgr
     c                   kfld                    vtillr_lety
     c                   kfld                    vtillr_fdat
     c                   kfld                    vtillr_ftim
     c                   kfld                    vtillr_tdat
     c                   kfld                    vtillr_ttim
      *
      * Key for oppdatering av tilbudspriser
      *
     c     vtillu_key    klist
     c                   kfld                    w_firm
6.20 c                   kfld                    vtillu_ogrp
6.20 c                   kfld                    vtillu_hgrp
6.20 c                   kfld                    vtillu_ugrp
6.20 c                   kfld                    vtillu_ldor
     c                   kfld                    vtillu_vare
5.70 c                   kfld                    vtillu_prgr
     c                   kfld                    vtillu_lety
     c                   kfld                    vtillu_fdat
     c                   kfld                    vtillu_ftim
     c                   kfld                    vtillu_tdat
     c                   kfld                    vtillu_ttim
      *
      * Key for posisjonering på vare
      *
     c     vtill1_key    klist
     c                   kfld                    w_firm
6.20 c                   kfld                    vtill1_ogrp
6.20 c                   kfld                    vtill1_hgrp
6.20 c                   kfld                    vtill1_ugrp
6.20 c                   kfld                    vtill1_ldor
     c                   kfld                    vtill1_vare
      *
      * Key for posisjonering på leveringstype
      *
     c     vtill2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vtill2_lety
      *
      * Key for posisjonering på fra-dato
      *
     c     vtill3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vtill3_fdat
      *
      * Key for posisjonering på til-dato
      *
     c     vtill4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vtill4_tdat
      *
      * Key for posisjonering på kampanje
      *
     c     vtill5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vtill5_kamp
5.70  *
5.70  * Key for posisjonering på prisgruppe
5.70  *
5.70 c     vtill6_key    klist
5.70 c                   kfld                    w_firm
5.70 c                   kfld                    vtill6_prgr
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
5.70  *
5.70  * Key for oppslag på prisgruppe
5.70  *
5.70 c     ra30l1_key    klist
5.70 c                   kfld                    w_firm
5.70 c                   kfld                    ra30l1_dkod
      *
      * Key for oppslag på enhet
      *
     c     venhl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    venhl1_eenh
      *
      *
      * Henter firma fra parametre
      *
     c                   eval      w_firm = p_firm
6.22 c                   eval      w_lagr = l_lagr
      *
      * Initierer skjermbilde
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1                             90
     c                   if        *in90 = *off
     c                   eval      b2vare = vvvare
     c                   eval      b2tek1 = vvtek1
     c                   endif
      *
     c                   eval      *in22 = *on
      *
      * Henter dages dato
      *
     c                   time                    w_udat
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      vtill1_vare = p_vare
     c     vtill1_key    setll     vtill1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
