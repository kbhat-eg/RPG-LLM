     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FL400R
      * Beskrivelse . . : Kundeprosjekt, sletting av kundeprosjekt
      *                   m/relasjoner
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       130499 HKO  Nytt program
      *
8.01  *K000   090221 bsa  Sletter også tilleggsregister
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffkprlu    uf   e           k disk    rename(fkprpfr:fkprlur)
     ffkpslu    uf   e           k disk    rename(fkpspfr:fkpslur)
     ffrabl3    uf   e           k disk    rename(frabpfr:frabl3r)
     ffpril3    uf   e           k disk    rename(fpripfr:fpril3r)
8.01 ffkp3iu    uf   e           k disk    rename(fkp3st:fkp3iur)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(flfirm)
     d p_kund          s                   like(flkund)
     d p_kpro          s                   like(flkpro)
      *
      * Definering av variable
      *
     d w_firm          s                   like(flfirm)
     d w_kund          s                   like(flkund)
     d w_kpro          s                   like(flkpro)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Sletter kundeprosjekt
      *
     c     key01         chain     fkprlur                            90
     c                   if        *in90 = *off
     c                   delete    fkprlur
     c                   endif
      *
      * Sletter søketekst
      *
     c     key01         chain     fkpslur                            90
     c                   if        *in90 = *off
     c                   delete    fkpslur
     c                   endif
      *
      * Sletter rabattmatrise
      *
     c     key01         setll     frabl3
     c     key01         reade     frabl3r                                90
     c                   dow       *in90 = *off
     c                   delete    frabl3r
     c     key01         reade     frabl3r                                90
     c                   enddo
      *
      * Sletter spesialpriser
      *
     c     key01         setll     fpril3
     c     key01         reade     fpril3r                                90
     c                   dow       *in90 = *off
     c                   delete    fpril3r
     c     key01         reade     fpril3r                                90
     c                   enddo
8.01  *
8.01  * Sletter tilleggsregister
8.01  *
8.01 c     key01         chain     fkp3iur
8.01 c                   if        %found
8.01 c                   delete    fkp3iur
8.01 c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kund
     c                   parm                    p_kpro
      *
      * Key for les på kundeprosjekt
      *
     c     key01         klist
     c                   kfld                    w_firm
     c                   kfld                    w_kund
     c                   kfld                    w_kpro
      *
      *
      * Henter parameter, og initierer nøkkel
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_kund = p_kund
     c                   eval      w_kpro = p_kpro
      *
     c                   endsr
