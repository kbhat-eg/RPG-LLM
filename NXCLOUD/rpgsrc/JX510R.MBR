     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JX510R
      * Beskrivelse . . : Vare-pris, vise
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       221196 HKO  Nytt program
      *       190900 HKO  Skrevet om til ny standard
      *       310505 HKO  Viser EAN-nr og cross docking
      *       111207 bof  utvidet varepris med gammel pris
6.10  *       270809 bof  Fjernet ean-nr fra vare-pris og pakn.000
6.11  *       120510 bof  Gjeninnført JXEANN, skal utgå etterhvert
6.30  *       160714 bof  Utvidet nøkkel med fradato,fjernet gtin fra bilde
7.xx  * 7.00  290716 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvprlr    if   e           k disk    rename(jvprpfr:jvprlrr)
     fjvprl1    if   e           k disk    rename(jvprpfr:jvprl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fjvpkl1    if   e           k disk    rename(jvpkpfr:jvpkl1r)
6.30 fjvlelr    if   e           k disk    rename(jvlepfr:jvlelrr)
      *
     fjx510d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_vare          s                   like(jxvare)
      *
      * Definering av Local data area
      *
     d                uds
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d jvprlr_ldor     s                   like(jxldor)
6.30 d jvprlr_gdat     s                   like(jxgdat)
     d jvprlr_rsts     s                   like(jxrsts)
     d jvprl1_ldor     s                   like(jxldor)
     d jvarl1_vare     s                   like(jvvare)
     d jlevl1_ldor     s                   like(jlldor)
     d jvpkl1_vare     s                   like(jwvare)
     d jvpkl1_pakn     s                   like(jwpakn)
6.30 d jvlelr_lldo     s                   like(jvlldo)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
      *
     d w_vare          s                   like(jxvare)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(11)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2ldor <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      jvprl1_ldor = b1ldor
     c     jvprl1_key    setll     jvprl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Leverandør
      *
     c                   if        b2ldor <> 0
     c                   eval      w_seqe = *blank
     c                   eval      jvprl1_ldor = b2ldor
     c     jvprl1_key    setll     jvprl1
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2ldor = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1vare = b2vare
     c                   eval      c1ldor = b1ldor
6.30 c                   eval      c1gdat = b1gdat
     c                   eval      c1rsts = b1rsts
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      jvprlr_ldor = c1ldor
6.30 c     *dmy          move      c1gdat        jvprlr_gdat
     c                   eval      jvprlr_rsts = c1rsts
     c     jvprlr_key    chain     jvprlr                             90
      *
     c                   eval      c2pris = jxpris
     c                   eval      c2gpri = jxgpri
6.30 c*                  eval      c2gdat = 0
     c                   eval      c2tdat = 0
6.30 c*                  eval      c2lvar = jxlvar
6.30 c*                  eval      c2eann = jxeann
     c                   eval      c2valu = jxvalu
     c                   eval      c2crdo = jxcrdo
     c                   eval      c2eusr = jxeusr
      *
     c                   if        jxgdat <> *loval
     c     *dmy          move      jxgdat        c1gdat
     c                   endif
      *
     c                   if        jxtdat <> *loval
     c     *dmy          move      jxtdat        c2tdat
     c                   endif
      *
     c     *dmy          move      jxodat        c2odat
     c     *dmy          move      jxedat        c2edat
      *
6.30 c                   eval      jvlelr_lldo = c1ldor
6.30 c     jvlelr_key    chain     jvlelr                             90
6.30 c                   if        %found(jvlelr)
6.30 c                   eval      c2lvar = jvllva
6.30 c                   else
6.30 c                   eval      c2lvar = *blank
6.30 c                   endif
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      jvprl1                                 15
      *
     c                   if        *in15
     c                   leave
     c                   endif
      *
     c                   if        jxvare <> w_vare
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      jvprl1_ldor = jxldor
      *
     c                   eval      b1ldor = jxldor
     c                   eval      b1pris = jxpris
     c                   eval      b1gdat = 0
     c*                  eval      b1lvar = jxlvar
     c                   eval      b1crdo = jxcrdo
     c                   eval      b1rsts = jxrsts
      *
     c                   if        jxgdat <> *loval
     c     *dmy          move      jxgdat        b1gdat
     c                   endif
6.34  *
6.34  * Henter leverandørs varenr
6.34  *
6.34 c                   eval      jvlelr_lldo = jxldor
6.34 c     jvlelr_key    chain     jvlelr
6.34 c                   if        %found
6.34 c                   movel     jvllva        b1lvar
6.34 c                   endif
      *
      * Henter leverandørnavn
      *
     c                   eval      jlevl1_ldor = jxldor
     c     jlevl1_key    chain     jlevl1
     c                   if        %found
     c                   eval      b1lnav = jlnavn
     c                   else
     c                   eval      b1lnav = *blank
     c                   endif
      *
      * Merker rader som er opprettet lokalt
      *
     c                   if        jxrsts = 1
     c                   eval      b1rkod = 'L'
     c                   else
     c                   eval      b1rkod = *blank
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     jvprl1_key    setgt     jvprl1
     c                   readp     jvprl1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     jvprl1
      *
     c                   if        %eof or
     c                             jxvare <> w_vare
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      jvprl1_ldor = jxldor
      *
     c                   enddo
      *
     c                   if        %eof
     c     jvprl1_key    setll     jvprl1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_vare
      *
      * Key for les på vare-pris
      *
     c     jvprl1_key    klist
     c                   kfld                    w_vare
     c                   kfld                    jvprl1_ldor
      *
      * Key for oppslag på vare-pris
      *
     c     jvprlr_key    klist
     c                   kfld                    w_vare
     c                   kfld                    jvprlr_ldor
     c                   kfld                    jvprlr_gdat
     c                   kfld                    jvprlr_rsts
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på vare-pakning
      *
     c     jvpkl1_key    klist
     c                   kfld                    jvpkl1_vare
     c                   kfld                    jvpkl1_pakn
6.30  *
6.30  * Key for les på vare-leverandør
6.30  *
6.30 c     jvlelr_key    klist
6.30 c                   kfld                    w_vare
6.30 c                   kfld                    jvlelr_lldo
      *
      *
      * Henter vare fra parameter
      *
     c                   eval      w_vare = p_vare
      *
      * Henter vareinformasjon, leverandørnavn or prisenhet
      *
     c                   eval      jvarl1_vare = p_vare
     c     jvarl1_key    chain     jvarl1
     c                   if        %found
     c                   eval      b2vare = jvvare
     c*                  eval      b2tek1 = jvtek1
     c                   movel     jvtek1        b2tek1
     c                   eval      b2numm = jvldor
     c                   eval      b2enhe = jvpenh
     c                   eval      jlevl1_ldor = jvldor
     c     jlevl1_key    chain     jlevl1
     c                   if        %found
     c                   eval      b2lnav = jlnavn
     c                   endif
     c                   endif
      *
      *
      * Posisjonerer i databasen, og oppretter subfile
      *
     c                   eval      *in22 = *on
      *
     c     jvprl1_key    setll     jvprl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
