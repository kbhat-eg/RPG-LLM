     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : RY845R
      * Beskrivelse . . : VGRP: Bytter varegruppe på forpakninger
      *
      *                   Leser gjennom forpakninger, og bytter varegruppe
      *                   i.h.t. vareregister
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       251000 HKO  Nytt program
      *       290208 bof  Utvidet forpakning med modulnr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjforlu    uf   e           k disk    rename(jforpfr:jforlur)
      *
      * Definering av LDA (Local Data Area)
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d jvarl1_vare     s                   like(jvvare)
      *
      * Definering av variable
      *
     d w_firm          s                   like(jzfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Påslag
      *
     c     key01         setll     jforlu
     c     key01         reade     jforlur                                90
     c                   dow       *in90 = *off
      *
     c                   if        jzvare <> *blank
     c                   eval      jvarl1_vare = jzvare
     c     jvarl1_key    chain     jvarl1                             91
     c                   if        *in91 = *off
     c                   eval      jzogrp = jvogrp
     c                   eval      jzhgrp = jvhgrp
     c                   eval      jzugrp = jvugrp
     c                   endif
     c                   endif
      *
     c                   update    jforlur
      *
     c     key01         reade     jforlur                                90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for les på firma
      *
     c     key01         klist
     c                   kfld                    w_firm
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
