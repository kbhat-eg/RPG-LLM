     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RR622R                                              *
      * Beskrivelse . . : Utskrift av resultatrapport                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * R3.30     130401  Lagt inn utvalg på inntil 24 avdelinger         KO  *
      * R3.30     130202  Korrigert utskrift av avd. på første side       KO  *
      * R5.00     181002  Lagt om til eksternt definert print             KO  *
      * R5.10     060503  Fjernet utskrift av selekteringsparametre       KO  *
      * R5.41     090506  Siste linje ble ikke skrevet ved linjenr.99999  KO  *
      * R5.41     290506  Tekst kortet ned til 30 pos.                    HFL *
6.20  * R6.20     250211  Avdelingsstekst utvidet til 30 posisjoner       bsa *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frrf0l1    if   e           k disk    rename(rrf0pfr:rrf0l1r)
     frrf5l1    if   e           k disk    rename(rrf5pfr:rrf5l1r)
     frrf9l1    if   e           k disk    rename(rrf9pfr:rrf9l1r)
     frrf9l2    if   e           k disk    rename(rrf9pfr:rrf9l2r)
     frr622p    o    e             printer oflind(*in30)
      *+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      *               T   A   B   E   L   L   E   R                      +
      *+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      *
     d pav             s              4  0 dim(24)
     d uav             s              4  0 dim(8)
     d sal             s             11  2 dim(14)
      *
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      * Data struktur
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      *
     d                 ds
     d nybrd                          9  0                                      Bruddb
     d  nylin                         5  0 overlay(nybrd)                       Linje
     d  nyavd                         4  0 overlay(nybrd:6)                     Avd
      *
     d                 ds
     d gmbrd                          9  0                                      Bruddb
     d  gmlin                         5  0 overlay(gmbrd)                       Linje
     d  gmavd                         4  0 overlay(gmbrd:6)                     Avd
      *
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      * LOCAL DATA
      *++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
     d                uds
     d rhprap                300    301  0
     d rhpkda                         6  0
     d rhprÅr                         4  0
     d rhpper                         2  0
     d rhppef                         2  0
     d rhppet                         2  0
     d rhpavf                         4  0
     d rhpavt                         4  0
     d rhpsrt                         1
     d rhpsml                         1
     d rhpavv                         1
     d rhphel                         1
     d rhplin                         1
     d rhanul                         1
     d rhpnul                         1
     d rhpa01                         4  0
     d rhpa02                         4  0
     d rhpa03                         4  0
     d rhpa04                         4  0
     d rhpa05                         4  0
     d rhpa06                         4  0
     d rhpa07                         4  0
     d rhpa08                         4  0
     d rhpa09                         4  0
     d rhpa10                         4  0
     d rhpa11                         4  0
     d rhpa12                         4  0
     d rhpa13                         4  0
     d rhpa14                         4  0
     d rhpa15                         4  0
     d rhpa16                         4  0
     d rhpa17                         4  0
     d rhpa18                         4  0
     d rhpa19                         4  0
     d rhpa20                         4  0
     d rhpa21                         4  0
     d rhpa22                         4  0
     d rhpa23                         4  0
     d rhpa24                         4  0
      *
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d bev             s                   like(rr9hia)
     d bevp            s                   like(rr9hia)
     d bevp0           s              9  0
     d bevp1           s              7  0
     d difh            s                   like(rr9hia)
     d difh0           s              9  0
     d difh1           s              7  0
     d difp            s                   like(rr9hia)
     d difp0           s              9  0
     d difp1           s              7  0
     d gm9abl          s                   like(rr9abl)
     d gm9avd          s                   like(rr9avd)
     d gm9kod          s                   like(rr9kod)
     d gm9lin          s                   like(rr9lin)
     d gm9nul          s                   like(rr9nul)
     d gm9sln          s                   like(rr9sln)
     d gm9sml          s                   like(rr9sln)
     d gm9txt          s                   like(rr9txt)
     d l1beÅr          s                   like(rr9hia)
     d l1befj          s                   like(rr9hia)
     d l1bevp          s                   like(rr9hia)
     d l1buÅr          s                   like(rr9hia)
     d l1bufj          s                   like(rr9hia)
     d l1busÅ          s                   like(rr9hia)
     d l1busf          s                   like(rr9hia)
     d l1saÅr          s                   like(rr9hia)
     d l1safj          s                   like(rr9hia)
     d l1salh          s                   like(rr9hia)
     d l1samh          s                   like(rr9hia)
     d l1samp          s                   like(rr9hia)
     d n               s              2  0
     d m               s              2  0
     d x               s              2  0
     d tstavd          s              7  0
     d ny9abl          s                   like(rr9abl)
     d ny9avd          s                   like(rr9avd)
     d ny9kod          s                   like(rr9kod)
     d ny9lin          s                   like(rr9lin)
     d ny9nul          s                   like(rr9nul)
     d ny9sln          s                   like(rr9sln)
     d ny9sml          s                   like(rr9sln)
     d ny9txt          s                   like(rr9txt)
     d prfjo           s                   like(rhprÅr)
     d sisskr          s                   like(rr9lin)
     d proh            s              7  1
     d prop            s              7  1
     d pslnh           s              5  1
     d pslnp           s              5  1
     d salh            s                   like(rr9hia)
     d salh0           s              9  0
     d salh1           s              7  0
     d samh            s                   like(rr9hia)
     d samh0           s              9  0
     d samh1           s              7  0
     d samp            s                   like(rr9hia)
     d samp0           s              9  0
     d samp1           s              7  0
     d slnh            s                   like(rr9hia)
     d slnp            s                   like(rr9hia)
     d sum             s                   like(rr9hia)
     d wpro1           s              9  4
      *
     d w_firm          s                   like(rr9fir)
     d w_stat          s              1
6.20 d w_gtxt          s             30
     d w_gkod          s                   like(rr9avd)
     d w_0rap          s                   like(rr0rap)
     d w_0txt          s                   like(rr0txt)
     d w_5abl          s                   like(rr5abl)
     d w_5kod          s                   like(rr5kod)
     d w_5lin          s                   like(rr5lin)
     d w_5rap          s                   like(rr5rap)
     d w_5txt          s                   like(rr5txt)
     d w_9avd          s                   like(rr9avd)
     d w_9typ          s                   like(rr9typ)
      *
     d rrf9l2_9rap     s                   like(rr9rap)
     d rrf5l1_5rap     s                   like(rr5rap)
     d rrf0l1_0rap     s                   like(rr0rap)
      *
     d rrf9l1_9rap     s                   like(rr9rap)
      *
      /eject
      *
     irrf9l1r
      *
      *  Resultatregister - Linjerekkefølge
      *
     i              rr9s01                      sal(01)
     i              rr9s02                      sal(02)
     i              rr9s03                      sal(03)
     i              rr9s04                      sal(04)
     i              rr9s05                      sal(05)
     i              rr9s06                      sal(06)
     i              rr9s07                      sal(07)
     i              rr9s08                      sal(08)
     i              rr9s09                      sal(09)
     i              rr9s10                      sal(10)
     i              rr9s11                      sal(11)
     i              rr9s12                      sal(12)
     i              rr9s13                      sal(13)
     i              rr9s00                      sal(14)
     irrf9l2r
      *
      *  Resultatregister - Avdelingsrekkefølge
      *
     i              rr9s01                      sal(01)
     i              rr9s02                      sal(02)
     i              rr9s03                      sal(03)
     i              rr9s04                      sal(04)
     i              rr9s05                      sal(05)
     i              rr9s06                      sal(06)
     i              rr9s07                      sal(07)
     i              rr9s08                      sal(08)
     i              rr9s09                      sal(09)
     i              rr9s10                      sal(10)
     i              rr9s11                      sal(11)
     i              rr9s12                      sal(12)
     i              rr9s13                      sal(13)
     i              rr9s00                      sal(14)
      *
      /eject
      *
      * Les saldo-records
      *
     c                   select
     c                   when      rhpsrt = ' '
     c                   eval      rrf9l1_9rap = rhprap
     c     rrf9l1_key    setll     rrf9l1r                                                   .
     c     rrf9l1_key    reade     rrf9l1r                                60
     c                   other
     c                   eval      rrf9l2_9rap = rhprap
     c     rrf9l2_key    setll     rrf9l2r                                                   .
     c     rrf9l2_key    reade     rrf9l2r                                60
     c                   endsl
      *
     c                   dow       *in60 = *off
      *
      * Sjekk om avdeling 0000 med
      *
     c                   if        rhanul = 'N' and                                      .....
     c                             rr9avd = *zeros                                       .....
     c                   goto      neste                                                     .
     c                   endif                                                           .....
      *
     c                   move      rr9typ        w_9typ
      *
      * Sjekk avdeling fra/til
      *
     c                   if        rr9avd >= rhpavf and                                  .....
     c                             rr9avd <= rhpavt                                      .....
     c                   goto      avdx1                                                     .
     c                   endif                                                           .....
      *
      * Hvis avdeling ligger utenfor
      * fra - til,
      * sjekk mot tabell
      *
      * Hvis valg ikke utfylt -                                         .
      * les neste postering                                             .
      *                                                                 .
     c                   if        tstavd = *zeros                                       ... .
     c                   goto      neste                                                   . .
     c                   endif                                                           ... .
      *                                                                 .
      * Hvis valg utfylt -                                              .
      * sjekk mot tabell                                                .
      *                                                                 .
     c                   eval      n = 1                                                     .
     c                   dou       n > 24                                                ... .
      *                                                               . .
     c                   if        pav(n) <> *zeros                                        . .
      *                                                               . .
     c                   if        rr9avd = pav(n)                                         . .
     c                   goto      avdx1                                                   . .
     c                   endif                                                             . .
      *                                                               . .
     c                   endif                                                             . .
      *                                                               . .
     c                   eval      n = n + 1                                               . .
     c                   enddo                                                           ... .
      *                                                                 .
      * Avdeling ikke i tabell -                                           .
      * les neste postering                                             .
      *                                                                 .
     c                   goto      neste                                                     .
      *                                                                 .
     c     avdx1         tag                                                                 .
      *                                                                 .
      /eject
      *
      * Sjekk om brudd i kontrollbegrepet
      *
     c                   move      rr9lin        nylin
     c                   move      rr9avd        nyavd
     c                   move      rr9lin        ny9lin
     c                   move      rr9txt        ny9txt
     c                   move      rr9abl        ny9abl
     c                   move      rr9kod        ny9kod
     c                   move      rr9nul        ny9nul
     c                   move      rr9sln        ny9sln
     c                   move      rr9avd        ny9avd
      *
      * Første gang - ikke test på brudd
      *
     c                   if        gmbrd = *zeros                                        .....
     c                   move      nybrd         gmbrd                                       .
     c                   move      *blank        gm9tx1
E5.40c                   movel     ny9txt        gm9tx1
     c                   move      *blank        gm9tx2
E5.40c                   movel     ny9txt        gm9tx2
     c                   move      ny9lin        gm9lin
     c                   move      ny9abl        gm9abl
     c                   move      ny9kod        gm9kod
     c                   move      ny9nul        gm9nul
     c                   move      ny9sln        gm9sln
     c                   move      ny9avd        gm9avd
      *
      * Skriv heading på første side
      *
     c                   if        rhpsrt = 'A'
     c                   eval      w_gkod = ny9avd
      *
     c                   call      'RS707R'
     c                   parm                    w_stat
     c                   parm                    w_gkod
     c                   parm                    w_gtxt
      *
     c                   endif
      *
     c                   write     head1
      *
     c                   endif
      *
      * Dersom linje-rekkefølge, nullstill avd i bruddbegrep
      *
     c                   if        rhpsrt = ' '
     c                   eval      nyavd = *zeros
     c                   endif
      *
      * Test hele bruddbegrepet - hvis ikke brudd, summer laveste nivå
      *
     c                   if        nybrd = gmbrd
     c                   exsr      rsum
     c                   goto      neste
     c                   endif
      /eject
      *
     c     behand        tag
      *
      * Brudd på linje
      *
     c                   if        nylin <> gmlin
     c                   exsr      brlin
     c                   endif
      *
      * Brudd på avdeling eller LR - (kun hvis sortert på avdeling)
      *
     c                   if        rhpsrt = 'A'
     c                             and nyavd <> gmavd
     c                   exsr      bravd
     c                   endif
      *
      * Summer til L1-nivå
      *
     c                   exsr      rsum
      *
      * Flytt nytt brudd-begrep til gammelt
      *
     c                   move      nybrd         gmbrd
E5.40c                   movel     ny9txt        gm9tx1
E5.40c                   movel     ny9txt        gm9tx2
     c                   move      ny9lin        gm9lin
     c                   move      ny9abl        gm9abl
     c                   move      ny9kod        gm9kod
     c                   move      ny9nul        gm9nul
     c                   move      ny9sln        gm9sln
     c                   move      ny9avd        gm9avd
      *
     c     neste         tag
      *
     c                   select
     c                   when      rhpsrt = ' '
     c     rrf9l1_key    reade     rrf9l1r                                60
     c                   other
     c     rrf9l2_key    reade     rrf9l2r                                60
     c                   endsl
      *
     c                   enddo
      *
      /Space 3
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
      *
      * Brudd på linje
      *
     c                   move      *hival        nybrd
      *
     c                   if        nylin <> gmlin or
     c                             nylin = *hival
     c                   exsr      brlin
     c                   endif
      *
      * Brudd på avdeling eller LR - (kun hvis sortert på avdeling)
      *
     c                   if        rhpsrt = 'A'
     c                             and nyavd <> gmavd
     c                   exsr      bravd
     c                   endif
      *
      * Summer til L1-nivå
      *
     c***                exsr      rsum
      *
     c                   move      *hival        gm9lin
      *
     c                   exsr      brtxt
      *
      /eject
      ****************************************************************
      *   Subrutine for behandling av alle aktuelle recordtyper      *
      ****************************************************************
      *
     c     rsum          begsr
      *
      * Summer til denne periode og hittil i år
      *
     c                   exsr      hsum
      *
      * Recordtype 1  - virkelig
      *
     c                   if        w_9typ = 1
      *
      * Saldo i år
      *
     c                   if        rr9aar = rhprÅr
     c                   eval      l1beÅr = l1beÅr + bev
     c                   eval      l1saÅr = l1saÅr + sum
     c                   else
      *
      * Saldo i fjor
      *
     c                   if        rr9aar = prfjo
     c                   eval      l1befj = l1befj + bev
     c                   eval      l1safj = l1safj + sum
     c                   endif
     c                   endif
      *
     c                   endif
      *
      * Recordtype 3  - budsjett
      *
     c                   if        w_9typ = 3
      *
      * Budsjett i år
      *
     c                   if        rr9aar = rhprÅr
     c                   eval      l1buÅr = l1buÅr + bev
     c                   eval      l1busÅ = l1busÅ + sum
     c                   else
      *
      * Budsjett i fjor
      *
     c                   if        rr9aar = prfjo
     c                   eval      l1bufj = l1bufj + bev
     c                   eval      l1busf = l1busf + sum
     c                   endif
      *
     c                   endif
      *
     c                   endif
      *
      * Recordtype 4  - alternativt budsjett
      *
     c                   if        w_9typ = 4
      *
      * Budsjett i år
      *
     c                   if        rr9aar = rhprÅr
     c                   eval      l1buÅr = l1buÅr + bev
     c                   eval      l1busÅ = l1busÅ + sum
     c                   else
      *
      * Budsjett i fjor
      *
     c                   if        rr9aar = prfjo
     c                   eval      l1bufj = l1bufj + bev
     c                   eval      l1busf = l1busf + sum
     c                   endif
      *
     c                   endif
      *
     c                   endif
      *
     c                   endsr
      /eject
      ****************************************************************
      *   Subrutine for beregning av denne periode og hittil         *
      ****************************************************************
      *
     c     hsum          begsr
      *
     c                   eval      bev = 0
     c                   eval      sum = 0
      *
     c                   eval      n = 1
      *
     c                   eval      sum = sum + sal(14)
      *
      * Summer periode fra 1 - til denne periode
      *
     c                   dou       n > rhpper
      *
     c                   eval      sum = sum + sal(n)
      *
      * Summer bevegelse for periode fra  - til
      *
     c                   if        n >= rhppef and
     c                             n <= rhppet
     c                   eval      bev = bev + sal(n)
     c                   endif
      *
     c                   eval      n = n + 1
     c                   enddo
      *
     c                   endsr
      /eject
      ****************************************************************
      *   Subrutine for beregning ved brudd på linje                 *
      ****************************************************************
      *
     c     brlin         begsr
      *
      * Behandle tekstlinje
      *
     c                   exsr      brtxt
      *
      * Non-print total
      *
     c                   if        gm9kod = 'N'
     c                   exsr      l1null
     c                   goto      xxlin
     c                   endif
      *
      * Nullstill uten utskrift
      *
     c                   if        gm9nul = 'N'
     c                   exsr      l1null
     c                   goto      xxlin
     c                   endif
      *
     c                   eval      bevp = l1beÅr
     c                   eval      salh = l1saÅr
      *
      * Sammenligning mot budsjett
      *
     c                   if        rhpsml = 'B'
     c                   eval      samp = l1buÅr
     c                   eval      samh = l1busÅ
     c                   endif
      *
      * Sammenligning mot fjorår
      *
     c                   if        rhpsml = 'F'
     c                   eval      samp = l1befj
     c                   eval      samh = l1safj
     c                   endif
      *
      * Sammenligning mot gjennomsnitt
      *
     c                   if        rhpsml = 'G'
     c                   eval(h)   samp = salh / rhpper
     c                   eval      samh = salh
     c                   endif
      *
      * Beregn differanser
      *
     c                   eval      difp = bevp - samp
     c                   eval      difh = salh - samh
      *
      * Hent sammenligningsgrunnlag på linje
      *
     c                   if        gm9sln <> ny9sml
     c                   exsr      brsln
     c                   move      gm9sln        ny9sml
     c                   endif
      *
      * Beregn brukt/oppnådd/sml.linje
      *
     c                   exsr      beregn
      *
     c                   exsr      l1null
      *
      * Test om null-linjer skal skrives
      *
     c                   if        rhpnul = 'N' and
     c                             gm9kod = 'K' and
     c                             salh = *zeros and
     c                             bevp = *zeros and
     c                             samh = *zeros and
     c                             samp = *zeros
     c                   goto      skrbll
     c                   endif
      *
      * Ved null-linjer skal skrives = J, skal likevel ikke
      *          linjer skrives som har 0 i antall blanke.
      *
     c                   if        rhpnul = 'J' and
     c                             gm9kod = 'K' and
     c                             gm9abl = '0' and
     c                             salh = *zeros and
     c                             bevp = *zeros and
     c                             samh = *zeros and
     c                             samp = *zeros
     c                   goto      skrbll
     c                   endif
      *
      * Beløp skal skrives i hele
      * kroner på rapporten
      *
     c                   if        rhphel = 'H'
     c                   eval(h)   bevp0 = bevp
     c                   eval(h)   samp0 = samp
     c                   eval(h)   difp0 = difp
     c                   eval(h)   salh0 = salh
     c                   eval(h)   samh0 = samh
     c                   eval(h)   difh0 = difh
     c                   endif
      *
      * Beløp skal skrives i hele
      * tusen på rapporten
      *
     c                   if        rhphel = 'T'
     c                   eval(h)   bevp1 = bevp / 1000
     c                   eval(h)   samp1 = samp / 1000
     c                   eval(h)   difp1 = difp / 1000
     c                   eval(h)   salh1 = salh / 1000
     c                   eval(h)   samh1 = samh / 1000
     c                   eval(h)   difh1 = difh / 1000
     c                   endif
      *
      * Skriv linje
      *
     c                   write     skrlin
     c                   eval      sisskr = gm9lin
      *
     c     skrbll        tag
      *
     c                   if        *in30 = *on
     c                   write     head1
     c                   eval      *in30 = *off
     c                   endif
      *
      * Skriv blanke linjer
      *
     c                   if        gm9abl = '1'
     c                   write     skrbl1
     c                   endif
      *
     c                   if        gm9abl = '2'
     c                   write     skrbl2
     c                   endif
      *
     c                   if        gm9abl = '3'
     c                   write     skrbl3
     c                   endif
      *
      * Skriv ny side
      *
     c                   if        gm9abl = 'S'
     c                   write     head1
     c                   endif
      *
      * Nullstill arbeidsfelt på L1-nivå
      *
     c     xxlin         tag
      *
     c                   eval      bevp = *zeros
     c                   eval      difp = *zeros
     c                   eval      prop = *zeros
     c                   eval      salh = *zeros
     c                   eval      samh = *zeros
     c                   eval      difh = *zeros
     c                   eval      proh = *zeros
      *
     c                   endsr
      /eject
      ****************************************************************
      *   Subrutine for behandling av tekstlinjer                    *
      ****************************************************************
      *
     c     brtxt         begsr
      *
      * Behandle tekstlinje
      *
      * Sjekk om det finnes tekstlinjer
      * med lavere nummer som skal skrives
      *
     c                   dow       w_5lin < gm9lin
      *
      * Test om nulllinjer skal skrives
      *
     c                   if        rhpnul = 'N' and
     c                             rhpsrt = ' ' and
     c                             w_5kod = 'K'
     c                   goto      skrblt
     c                   endif
      *
      *      også de med 0 i antall blanke
      *
     c                   if        rhpnul = 'J' and
     c                             rhpsrt = ' ' and
     c                             w_5kod = 'K' and
     c                             w_5abl = '0'
     c                   goto      skrblt
     c                   endif
      *
     c                   if        rr5lin = sisskr
     c                   goto      brhxlt
     c                   endif
      *
      * Skriv tekstlinje
      *
     c                   write     skrtxt
      *
     c     skrblt        tag
      *
     c                   if        *in30 = *on
     c                   write     head1
     c                   eval      *in30 = *off
     c                   endif
      *
      * Skriv blanke linjer
      *
     c                   if        w_5abl = '1'
     c                   write     skrbl1
     c                   endif
      *
     c                   if        w_5abl = '2'
     c                   write     skrbl2
     c                   endif
      *
     c                   if        w_5abl = '3'
     c                   write     skrbl3
     c                   endif
      *
      * Skriv ny side
      *
     c                   if        w_5abl = 'S'
     c                   write     head1
     c                   endif
      *
      * Hent ny tekstlinje
      *
     c     brhxlt        tag
      *
     c                   exsr      xltxt
      *
     c                   enddo
      *
     c                   endsr
      /eject
      ****************************************************************
      *   Subrutine for beregning ved brudd på avdeling              *
      ****************************************************************
      *
     c     bravd         begsr
      *
      * Hent avdelingstekst fra koderegisteret
      *
     c                   move      gmavd         w_9avd
     c                   eval      ny9sml = *zeros
      *
     c                   move      gm9avd        w_gkod
      *
     c                   call      'RS707R'
     c                   parm                    w_stat
     c                   parm                    w_gkod
     c                   parm                    w_gtxt
      *
      * Skriv heading og linje
      *
     c                   if        *in30 = *on
     c                   write     head1
     c                   eval      *in30 = *off
     c                   endif
      *
      * Hent ny avdelingstekst fra koderegisteret og skriv ny heading
      *
     c  nlr              do
      *
     c                   move      nyavd         w_9avd
     c                   move      ny9avd        w_gkod
      *
     c                   call      'RS707R'
     c                   parm                    w_stat
     c                   parm                    w_gkod
     c                   parm                    w_gtxt
      *
     c                   write     head1
      *
      * Start forfra på tekst
      *
     c                   move      rhprap        rr5rap
     c                   eval      rr5lin = *zero
     c     rrf5l1_key    setll     rrf5l1
      *
      * Hent første tekstlinje
      *
     c                   exsr      xltxt
      *
     c                   enddo
      *
     c                   endsr
      /eject
      *
      ****************************************************************
      *   Subrutine for beregning av brukt/oppnådd/sml.linje         *
      ****************************************************************
      *
     c     beregn        begsr
      *
      * Denne periode
      *
     c                   eval      *in16 = *off
     c                   eval      *in17 = *off
      *
     c                   if        samp <> *zero
     c                   select
      *
     c                   when      (difp / samp) * 100 + 100 >= 999999,9                     .
     c                   eval      prop = 999999,9
      *
     c                   when      (difp / samp) * 100 + 100 <= -999999,9
     c                   eval      prop = -999999,9
      *
     c                   other
     c                   eval(h)   prop = (difp / samp) * 100 + 100
     c                   endsl
     c                   else
     c                   if        samp = *zero
     c                   eval      *in16 = *on
     c                   endif
     c                   endif
      *
     c                   if        slnp <> *zero
     c                   select
      *
     c                   when      (bevp / slnp) * 100 >= 9999,9
     c                   eval      pslnp = 9999,9
      *
     c                   when      (bevp / slnp) * 100 <= -9999,9
     c                   eval      pslnp = -9999,9
      *
     c                   other
     c                   eval(h)   pslnp = (bevp / slnp) * 100
     c                   endsl
     c                   else
     c                   eval      pslnp = *zero
     c                   endif
      *
      * Hittil
      *
     c                   if        samh <> *zero
     c                   select
      *
     c                   when      (difh / samh) * 100 + 100 >= 999999,9
     c                   eval      proh = 999999,9
      *
     c                   when      (difh / samh) * 100 + 100 <= -999999,9
     c                   eval      proh = -999999,9
      *
     c                   other
     c                   eval(h)   proh = (difh / samh) * 100 + 100
     c                   endsl
     c                   else
     c                   if        samh = *zero
     c                   eval      *in17 = *on
     c                   endif
     c                   endif
      *
     c                   if        slnh <> *zero
     c                   select
      *
     c                   when      (salh / slnh) * 100 >= 9999,9
     c                   eval      pslnh = 9999,9
      *
     c                   when      (salh / slnh) * 100 <= -9999,9
     c                   eval      pslnh = -9999,9
      *
     c                   other
     c                   eval(h)   pslnh = (salh / slnh) * 100
     c                   endsl
     c                   else
     c                   eval      pslnh = *zero
     c                   endif
      *
     c                   endsr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Hent tekstlinjer fra rrf5l1                                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xltxt         begsr
      *
     c     xlles         tag
      *
     c                   read      rrf5l1r                                61
      *
     c                   if        *in61 = *on or
     c                             rr5fir > w_firm or
     c                             rr5rap > rhprap
     c                   move      *hival        w_5lin
     c                   goto      xlend
     c                   endif
      *
     c                   if        rr5lin = rr9lin or
     c                             rr5lin = gm9lin or
     c                             rr5kod = 'N'
     c                   goto      xlles
     c                   endif
      *
     c                   move      rr5lin        w_5lin
     c                   move      rr5txt        w_5tx1
     c                   move      rr5txt        w_5tx2
     c                   move      rr5abl        w_5abl
     c                   move      rr5kod        w_5kod
      *
     c     xlend         tag
      *                                                                 .
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for sammenligning mot linje                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     brsln         begsr
      *
     c                   eval      slnp = *zeros
     c                   eval      slnh = *zeros
      *
     c                   call      'RR612R'
     c                   parm                    gm9avd
     c                   parm                    gm9sln
     c                   parm                    slnp
     c                   parm                    slnh
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for nullstilling av arbeidsfelt                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     l1null        begsr
      *
     c                   eval      l1bevp = *zeros
     c                   eval      l1salh = *zeros
     c                   eval      l1samp = *zeros
     c                   eval      l1samh = *zeros
      *
     c                   eval      l1beÅr = *zeros
     c                   eval      l1befj = *zeros
      *
     c                   eval      l1buÅr = *zeros
     c                   eval      l1bufj = *zeros
      *
     c                   eval      l1saÅr = *zeros
     c                   eval      l1safj = *zeros
      *
     c                   eval      l1busÅ = *zeros
     c                   eval      l1busf = *zeros
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Behandle parameter
      *
     c                   eval      prfjo = rhprÅr - 1
      *
     c                   if        rhpsml = 'B'
     c                   eval      *in11 = *on
     c                   endif
      *
     c                   if        rhpsml = 'F'
     c                   eval      *in12 = *on
     c                   endif
      *
     c                   if        rhpsml = 'G'
     c                   eval      *in13 = *on
     c                   endif
      *
     c                   if        rhpavv = 'D'
     c                   eval      *in14 = *on
     c                   endif
      *
     c                   if        rhpavv = 'P'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   if        rhpsrt = 'A'
     c                   eval      *in18 = *on
     c                   endif
      *
     c                   if        rhppef <> rhppet
     c                   eval      *in19 = *on
     c                   endif
      *
     c                   if        rhphel = ' '
     c                   eval      *in20 = *on
     c                   endif
      *
     c                   if        rhphel = 'H'
     c                   eval      *in21 = *on
     c                   endif
      *
     c                   if        rhphel = 'T'
     c                   eval      *in22 = *on
     c                   endif
      *
     c                   if        rhplin = 'N'
     c                   eval      *in23 = *on
     c                   endif
      *
      * Sett startverdier
      *
     c                   eval      gm9tx1 = *blank
     c                   eval      gm9tx2 = *blank
     c                   eval      gm9lin = *zero
     c                   eval      nybrd = *zero
     c                   eval      gmbrd = *zero
      *
     c                   move      l_firm        w_firm
      *
      * Hent rapportheading
      *
     c                   eval      w_0rap = rhprap
     c                   eval      rrf0l1_0rap = rhprap
     c     rrf0l1_key    chain     rrf0l1                             63
      *
     c                   if        *in63 = *off
     c                   move      rr0txt        w_0txt
     c                   else
     c                   eval      w_0txt = *blank
     c                   endif
      *
      * Hent første tekstlinje
      *
     c                   eval      rr5rap = rhprap
     c                   eval      rr5lin = *zero
     c     rrf5l1_key    setll     rrf5l1r
      *
     c                   exsr      xltxt
      *
      *  Flytt utvalgte avdelinger til tabell
      *
     c                   move      rhpa01        pav(01)
     c                   move      rhpa02        pav(02)
     c                   move      rhpa03        pav(03)
     c                   move      rhpa04        pav(04)
     c                   move      rhpa05        pav(05)
     c                   move      rhpa06        pav(06)
     c                   move      rhpa07        pav(07)
     c                   move      rhpa08        pav(08)
     c                   move      rhpa09        pav(09)
     c                   move      rhpa10        pav(10)
     c                   move      rhpa11        pav(11)
     c                   move      rhpa12        pav(12)
     c                   move      rhpa13        pav(13)
     c                   move      rhpa14        pav(14)
     c                   move      rhpa15        pav(15)
     c                   move      rhpa16        pav(16)
     c                   move      rhpa17        pav(17)
     c                   move      rhpa18        pav(18)
     c                   move      rhpa19        pav(19)
     c                   move      rhpa20        pav(20)
     c                   move      rhpa21        pav(21)
     c                   move      rhpa22        pav(22)
     c                   move      rhpa23        pav(23)
     c                   move      rhpa24        pav(24)
      *
      * Sjekk om avdelingsvalg utfylt
      *
     c                   xfoot     pav           tstavd
      *
      * Key for RESULTAREGISTER - HEADING
      *
     c     rrf0l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rrf0l1_0rap
      *
      * Key for tekstlinjer
      *
     c     rrf5l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rr5rap
      *
      * Key for resultatregister - linje
      *
     c     rrf9l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rrf9l1_9rap
      *
      * Key for resultatregister - avdeling
      *
     c     rrf9l2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rrf9l2_9rap
      *
     c                   endsr
