     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JS700R
      * Beskrivelse . . : Søketekst, oppdaterer søketekst m/stikkord
      *
      *                   Sletter og oppretter søketekster basert på
      *                   varetekst, produsentnavn, vareeiernavn, navn
      *                   på prisgivende leverandør og stikkord
      *
      *                   LR-flagg må være lik '1' for at *inLR ikke skal
      *                   settes
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       230899 HKO  Nytt program
      *       140905 HKO  Fjernet hjelpefelter fra søketekst-tabell
6.30  * 6.30  270314 bof  feilretting + test om varepris VA er utgått
6.31  * 6.30  020816 bof  Feilretting vedr. test på til prisdato.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjsoklu    uf a e           k disk    rename(jsokpfr:jsoklur)
     fjsokl1    if   e           k disk    rename(jsokpfr:jsokl1r)
     fjprol1    if   e           k disk    rename(jpropfr:jprol1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fjvprl1    if   e           k disk    rename(jvprpfr:jvprl1r)
     fjmstl1    if   e           k disk    rename(jmstpfr:jmstl1r)
      *
      * Definering av parametre
      *
     d p_vare          s                   like(jsvare)
     d p_modn          s                   like(jusmod)
     d p_tek1          s             35
     d p_prod          s                   like(jqprod)
     d p_ldor          s                   like(jlldor)
     d p_inlr          s              1
      *
      * Definering av variable i nøkler
      *
     d jsoklu_vare     s                   like(jsvare)
     d jsokl1_vare     s                   like(jsvare)
     d jprol1_prod     s                   like(jqprod)
     d jlevl1_ldor     s                   like(jlldor)
     d jvprl1_vare     s                   like(jxvare)
     d jmstl1_modn     s                   like(jusmod)
      *
      * Definering av variable
      *
     d w_tek1          s             36
     d w_stek          s             35
     d w_leng          s              2  0
     d w_lnav_4        s              4
     d w_pnav_4        s              4
     d w_dato          s                   like(jxtdat)
      *
     d x               s              2  0
     d y               s              2  0
     d lengde          s              2  0
     d ordskille       s              1
      *
      * Definering av konstanter
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Sletter alle uten egeninnlagte søketekster. Disse oppdaterers
      * m.h.p. varegruppe
      *
     c                   eval      jsoklu_vare = p_vare
     c     jsoklu_key    setll     jsoklu
     c     jsoklu_key    reade     jsoklur
     c                   dow       not %eof
     c                   if        jsokod <> 'L'
     c                   delete    jsoklur
     c                   else
     c                   unlock    jsoklu
     c                   endif
     c     jsoklu_key    reade     jsoklur
     c                   enddo
      *
      * Initierer variable
      *
     c                   eval      x = 0
     c                   eval      y = 0
     c                   eval      lengde = 0
     c                   eval      ordskille = *blank
     c                   eval      w_pnav_4 = *blank
     c                   eval      w_lnav_4 = *blank
     c                   eval      w_tek1 = *blank
     c                   eval      w_stek = *blank
     c                   eval      w_leng = 0
      *
      * Initierer felles-kolonner i søketekst-registeret
      *
     c                   eval      jsvare = p_vare
      *
      * Legger ut søketekst lik navn på produsent
      *
     c                   if        p_prod <> 0
      *
     c                   eval      jprol1_prod = p_prod
     c     jprol1_key    chain     jprol1
     c                   if        %found
     c                   eval      jsokod = 'P'
     c     Lo:Up         xlate     jqnavn        jqnavn
     c                   eval      jsstek = jqnavn
     c                   write     jsoklur
     c                   eval      w_pnav_4 = jqnavn
     c                   endif
     c                   endif
      *
      * Legger ut søketekst lik navn på vareeier/leverandør
      * En forutsetning er imidlertid at de fire første bokstavene ikke
      * er lik med produsentnavn
      *
     c                   if        p_ldor <> 0
      *
     c                   eval      jlevl1_ldor = p_ldor
     c     jlevl1_key    chain     jlevl1
     c                   if        %found
      *
     c     Lo:Up         xlate     jlnavn        jlnavn
      *
     c                   eval      w_lnav_4 = jlnavn
      *
     c                   if        w_pnav_4 <> w_lnav_4
     c                   eval      jsokod = 'P'
     c                   eval      jsstek = jlnavn
     c                   write     jsoklur
     c                   endif
      *
     c                   endif
     c                   endif
      *
      * Legger ut søketekst lik navn på alle prisgivende leverandører
      * En forutsetning er imidlertid at de fire første bokstavene ikke
      * er lik med produsentnavn eller vareeier-navn
      *
     c                   eval      jvprl1_vare = p_vare
     c     jvprl1_key    setll     jvprl1
     c     jvprl1_key    reade     jvprl1
     c                   dow       not %eof
6.30 c                   if        (jxtdat =  *loval) or
6.30 c                             (jxtdat <> *loval and
6.31 c                             jxtdat >= w_dato)
      *
     c                   if        jxldor <> 0
      *
     c                   eval      jlevl1_ldor = jxldor
     c     jlevl1_key    chain     jlevl1
     c                   if        %found
      *
     c     Lo:Up         xlate     jlnavn        jlnavn
      *
     c                   if        %subst(jlnavn:1:4) <> w_pnav_4 and
     c                             %subst(jlnavn:1:4) <> w_lnav_4
     c                   eval      jsokod = 'P'
     c                   eval      jsstek = jlnavn
     c                   write     jsoklur
     c                   endif
      *
     c                   endif
     c                   endif
      *
6.30 c                   endif
     c     jvprl1_key    reade     jvprl1
     c                   enddo
      *
      * Legger ut søketekster fra stikkord-register. Forutsetter at teksten
      * ikke allerede finnes som søketekst
      *
     c                   if        p_modn <> 0
      *
     c                   eval      jmstl1_modn = p_modn
     c     jmstl1_key    setll     jmstl1
     c     jmstl1_key    reade     jmstl1
     c                   dow       not %eof
      *
     c                   eval      jsokl1_vare = p_vare
     c     jsokl1_key    setll     jsokl1
     c     jsokl1_key    reade     jsokl1
     c                   dow       not %eof
     c                   if        jsstek = jusord or
     c                             jsokod = 'P' and
     c                             %subst(jsstek:1:4) = %subst(jusord:1:4)
     c                   goto      neste_ord1
     c                   endif
     c     jsokl1_key    reade     jsokl1
     c                   enddo
      *
     c                   eval      jsokod = 'M'
     c                   eval      jsstek = jusord
     c                   write     jsoklur
      *
     c     neste_ord1    tag
      *
     c     jmstl1_key    reade     jmstl1
     c                   enddo
      *
     c                   endif
      *
      * Avbryter dersom varetekst ikke er utfylt
      *
     c                   if        p_tek1 = *blank
     c                   goto      avslutt
     c                   endif
      *
      * Legger ut søketekst lik varetekst-1
      *
     c                   eval      jsokod = 'V'
     c                   eval      jsstek = p_tek1
      *
     c                   write     jsoklur
      *
      * Legger ut søketekster som andel av varetekst-1                      .....)
      * Ved brudd på mellomrom (space) legges tekst fra start eller forrige
      * mellomrom ut som søketekst. En forutsetning er imidlertid at lengden
      * på teksten er større enn to posisjoner og at teksten ikke allerede
      * finnes som søketekst. Første ord i vareteksten legges ikke ut som
      * søketekst
      *
     c                   eval      w_tek1 = p_tek1
     c                   eval      w_leng = %len(%trim(p_tek1))
      *
     c                   eval      x = 1
     c                   eval      y = 1
     c                   eval      lengde = 1
     c                   eval      ordskille = *blank
      *
     c     ordskille     scan      w_tek1:x      x                        90
     c                   eval      y = x
      *
     c                   dow       *in90 = *on
      *
     c                   if        x >= w_leng
     c                   leave
     c                   endif
      *
     c     ordskille     scan      w_tek1:x      x                        90
     c                   if        *in90 = *off
     c                   leave
     c                   endif
      *
     c                   eval      lengde = x - y
      *
     c                   if        lengde > 2
     c                   eval      w_stek = %subst(w_tek1:y:lengde)
     c                   if        w_stek = *blank
     c                   leave
     c                   endif
      *
     c                   eval      jsokl1_vare = p_vare
     c     jsokl1_key    setll     jsokl1
     c     jsokl1_key    reade     jsokl1
     c                   dow       not %eof
     c                   if        jsstek = w_stek or
     c                             jsokod = 'P' and
     c                             %subst(jsstek:1:4) = %subst(w_stek:1:4)
     c                   goto      neste_ord2
     c                   endif
     c     jsokl1_key    reade     jsokl1
     c                   enddo
      *
     c                   eval      jsokod = 'V'
     c                   eval      jsstek = w_stek
     c                   write     jsoklur
     c                   endif
      *
     c     neste_ord2    tag
      *
     c                   eval      x = x + 1
     c                   eval      y = x
      *
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   if        p_inlr <> *on
     c                   eval      *inlr = *on
     c                   endif
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_vare
     c                   parm                    p_modn
     c                   parm                    p_tek1
     c                   parm                    p_prod
     c                   parm                    p_ldor
     c                   parm                    p_inlr
      *
      * Key for oppdatering av søketekst
      *
     c     jsoklu_key    klist
     c                   kfld                    jsoklu_vare
      *
      * Key for oppslag på søketekst
      *
     c     jsokl1_key    klist
     c                   kfld                    jsokl1_vare
      *
      * Key for oppslag på produsent
      *
     c     jprol1_key    klist
     c                   kfld                    jprol1_prod
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på vare-pris
      *
     c     jvprl1_key    klist
     c                   kfld                    jvprl1_vare
      *
      * Key for oppslag på stikkord
      *
     c     jmstl1_key    klist
     c                   kfld                    jmstl1_modn
      *
6.30 c                   time                    w_dato
      *
     c                   endsr
