      /TITLE  Utplukk fra historisk lagerbok
     h datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Program . . . . : LE282R                                        *
      * Beskrivelse . . : Utplukk fra historisk lagerbok                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      *                                                                 *
5.70  *  PRGR     021208  Utvidet med prisgruppe                   bof
6.21  * 6.20      190511  Utvidet vl710r med div. info             bof
6.30  * 6.30  170918 bkv  Utvidet med trelast sortiment
      *                                                                 *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fvhisl1    if   e           k disk    rename(vhispfr:vhisl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     flwa3l1    o    e             disk    rename(lwa3pfr:lwa3l1r)
6.30 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
5.70 d w_vtyp          s                   like(vvvtyp)
6.21 d w_udat          s                   like(vvudat)
6.21 d w_ldor          s                   like(vvldor)
6.21 d b_inlr          s              1    inz(*off)
6.30 d w_lv_nobb       s                   like(vvlnob)
6.30 d w_lv_modn       s                   like(vvlmod)
6.30 d w_lv_lldo       s                   like(vvlnle)
6.30 d w_lv_llva       s                   like(vvllva)
      /eject
      ********************  I N P U T  S E C T I O N  ********************
      *
     d                 ds
     d wlakey                        64
     d                 ds
      *                                                      SORT-BEGREP
     d sorflt                        45
     d  sor001                       15    overlay(sorflt)
     d  sor002                       15    overlay(sorflt:16)
     d  sor003                       15    overlay(sorflt:31)
      *
     d                uds
      *
     d lcfvar                300    314
     d lctvar                        15
     d lcflag                         2  0
     d lctlag                         2  0
     d lcfogr                         2  0
     d lctogr                         2  0
     d lcfhgr                         2  0
     d lcthgr                         2  0
     d lcfugr                         3  0
     d lctugr                         3  0
     d lcfdat                        10d   datfmt(*iso)
     d lctdat                        10d   datfmt(*iso)
     d lcsyst                         1
     d lctype                         2
     d lcinut                         1
     d lcsrt1                         1  0
     d lcsrt2                         1  0
     d lcsrt3                         1  0
     d lcsrt4                         1  0
      *
     d lcuser                911    920
     d lcfirm                944    946  0
     d lcnavn                951    980
      *
     d wwfirm          s                   like(vvfirm)
     d wwlage          s                   like(vhlage)
     d wwtrda          s              8  0
     d wwvare          s                   like(vvvare)
      *
      /EJECT
      *
      *---------------------------------------------------------------
      *  Behandle lagerrecords
      *---------------------------------------------------------------
      *
     c     les           tag
      *
     c                   read      vhisl1                                 80
     c                   if        *in80 = *on
     c                   goto      slutt
     c                   endif
      *
      * Sjekk riktig firma
      *
     c                   if        vhfirm <> lcfirm
     c                   goto      les
     c                   endif
      *
      * Sjekk varenr. fra - til
      *
     c                   if        vhvare < lcfvar
     c                             or vhvare > lctvar
     c                   goto      les
     c                   endif
      *
      * Sjekk lagernr. fra - til
      *
     c                   if        vhlage < lcflag
     c                             or vhlage > lctlag
     c                   goto      les
     c                   endif
      /EJECT
      *
      * Sjekk om transaksjoner for denne dato skal være med
      *
     c                   if        vhdato < lcfdat
     c                             or vhdato > lctdat
     c                   goto      les
     c                   endif
      *
      * Sjekk om transaksjoner for dette system skal være med
      *
     c                   if        lcsyst <> *blank
     c                   if        lcsyst <> vhsyst
     c                   goto      les
     c                   endif
     c                   endif
      *
      * Sjekk om transaksjoner for denne kode skal være med
      *
     c                   if        lcinut <> *blank
     c                   if        lcinut <> vhkode
     c                   goto      les
     c                   endif
     c                   endif
      *
      * Sjekk om transaksjoner for denne type skal være med
      *
     c                   if        lctype <> *blank
HOFF c                   if        lctype <> vhkode
     c                   goto      les
     c                   endif
     c                   endif
      /EJECT
      *
      * Sjekk om varenr. i registeret
      *
     c                   move      vhvare        wwvare
     c     varkey        chain     vvarl1                             80
     c                   if        *in80 = *on
     c                   goto      les
     c                   endif
      *
      * Sjekk om overruppe fra - til
      *
     c                   if        vvogrp < lcfogr
     c                             or vvogrp > lctogr
     c                   goto      les
     c                   endif
      *
      * Sjekk om hovedgruppe fra - til
      *
     c                   if        vvhgrp < lcfhgr
     c                             or vvhgrp > lcthgr
     c                   goto      les
     c                   endif
      *
      * henter varetype
      *
5.70 c                   call      'VL710R'
5.70 c                   parm                    vhfirm
5.70 c                   parm                    vhvare
5.70 c                   parm                    vhlage
5.70 c                   parm                    w_vtyp
6.21 c                   parm                    w_udat
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
      *
      * Sjekk om skaffevare
      *
5.70 c                   if        w_vtyp = 'S'
     c                   goto      les
     c                   endif
      *
      * Sjekk om strukturvare
      *
     c                   if        vvkstr = 20
     c                   goto      les
     c                   endif
      /eject
      *
      * Bygg opp sorteringsbegrep
      *
      *
      *  BLANKER SORTERINGSFELT
      *
     c                   eval      sorflt = *blank
      *
      *  VALG. NR. 1
      *
     c                   if        lcsrt1 = 1
     c                   movel     vhvare        sor001
     c                   endif
      *
     c                   if        lcsrt1 = 2
     c                   movel     vhvare        sor002
     c                   endif
      *
     c                   if        lcsrt1 = 3
     c                   movel     vhvare        sor003
     c                   endif
      *
      *  VALG. NR. 2
      *
     c                   if        lcsrt2 = 1
     c                   movel     vhlage        sor001
     c                   endif
      *
     c                   if        lcsrt2 = 2
     c                   movel     vhlage        sor002
     c                   endif
      *
     c                   if        lcsrt2 = 3
     c                   movel     vhlage        sor003
     c                   endif
      *
      *  VALG. NR. 3
      *
     c                   if        lcsrt3 = 1
     c                   movel     vhdato        sor001
     c                   move      vhtime        sor001
     c                   endif
      *
     c                   if        lcsrt3 = 2
     c                   movel     vhdato        sor002
     c                   move      vhtime        sor002
     c                   endif
      *
     c                   if        lcsrt3 = 3
     c                   movel     vhdato        sor003
     c                   move      vhtime        sor003
     c                   endif
      *
      *  VALG. NR. 4
      *
     c                   if        lcsrt4 = 1
     c                   movel     vhkode        sor001
     c                   endif
      *
     c                   if        lcsrt4 = 2
     c                   movel     vhkode        sor002
     c                   endif
      *
     c                   if        lcsrt4 = 3
     c                   movel     vhkode        sor003
     c                   endif
      *
     c                   movel     sorflt        wlakey
      *
     c                   eval      mhfirm = vhfirm
     c                   eval      mhvare = vhvare
     c                   eval      mhlage = vhlage
     c                   eval      mhdato = vhdato
     c                   eval      mhtime = vhtime
     c                   eval      mhuser = vhuser
     c                   eval      mhsyst = vhsyst
     c                   eval      mhkode = vhkode
     c                   eval      mhtype = vhtype
     c                   eval      mhrefr = vhrefr
     c                   eval      mhbehl = vhbehl
     c                   eval      mhanta = vhanta
     c                   eval      mhenhl = vhenhl
     c                   eval      mhkpri = vhkpri

     c                   write     lwa3l1r
      *
     c                   goto      les
      *
      /SPACE 3
      *-------------------------
      *  END OF JOB
      *-------------------------
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      *
     c                   eval      wwtrda = *zero
      *
      * Legge inn firmanummer
      *
     c                   move      lcfirm        wwfirm
      *
      * Key for vareregister
      *
     c     varkey        klist
     c                   kfld                    wwfirm
     c                   kfld                    wwvare
      *
      * Key for historisk lagerbok
      *
     c                   endsr
