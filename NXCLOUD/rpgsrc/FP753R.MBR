     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FP753R
      * Beskrivelse . . : Spesialpris, innlesing fra fil - NIB prisformat
      *
      * Programmet leser valgt fil, og oppretter spesialpriser for valgt
      * rabattkategori, kunde eller kundeprosjekt. Det forutsettes at filen
      * er på NIB prisformat.
      *
      * Vare identifiseres via Nobbnr. Enten ved at varen finnes i
      * vareregisteret, eller som en alternativ vare.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       270105 HKO  Nytt program
      *       110405 HKO  Behandler alternative varer
      *                   Lagt inn påslag på leveringstype
      *       020805 HKO  Endret les mot vare-enhet for å finne salgsenheter
5.70  * PRGR  151108 bof  Utvidet med prisgruppe
      * 6.10  250609 BSA  Oppdatert med sporingsinformasjon
6.nu  * 6.30  240614 bof  Gjennomgått mtp. nummerutvidelse
6.31  * 6.30  081018 bof  Utvidet med trelast sortiment
6.32  *       110119 bof  Skal takle både nobbnr og varenummer
6.33  *       110119 bof  Hvis butikkpris=dir.pris ikke les inn dir.pris
8.01  *PRG2   100622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflvwpf    uf   e           k disk    rename(flvwpfr:flvwpfr)
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarl3    if   e           k disk    rename(vvarpfr:vvarl3r)
     fvvaal1    if   e           k disk    rename(vvaapfr:vvaal1r)
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
     ffpril3    uf   e           k disk    rename(fpripfr:fpril3r)
     ffprilu    uf a e           k disk    rename(fpripfr:fprilur)
     ffprnlu    uf a e           k disk    rename(fprnpfr:fprnlur)
      *
      * Definering av Local data area *LDA
      *
     d                uds
     d l_user                911    920
      *
      *  Recordbeskrivelse NIB prisformat
      *
     d                 ds
     d d_list                       341
     d  d_nobb                        8  0 overlay(d_list:  1)
     d  d_nob1                        8  0 overlay(d_list:  9)
     d  d_nob2                        8  0 overlay(d_list: 17)
     d  d_nob3                        8  0 overlay(d_list: 25)
     d  d_tek1                       35    overlay(d_list: 33)
     d  d_tek2                       35    overlay(d_list: 68)
     d  d_vgrp                        7    overlay(d_list:103)
     d   d_ogrp                       2  0 overlay(d_vgrp:1)
     d   d_hgrp                       2  0 overlay(d_vgrp:3)
     d   d_ugrp                       3  0 overlay(d_vgrp:5)
     d  d_gtxt                       35    overlay(d_list:110)
     d  d_delt                        6  0 overlay(d_list:145)
     d  d_lnav                       35    overlay(d_list:151)
     d  d_enhe                        3    overlay(d_list:186)
     d  d_nopr                        9  2 overlay(d_list:189)
     d  d_ntpa                        9  2 overlay(d_list:198)
     d  d_ntla                        9  2 overlay(d_list:207)
     d  d_ntdi                        9  2 overlay(d_list:216)
     d  d_fdat                        8  0 overlay(d_list:225)
     d  d_tdat                        8  0 overlay(d_list:233)
     d  d_kfra                        1  0 overlay(d_list:241)
     d  d_lbet                       40    overlay(d_list:242)
     d  d_merk                       60    overlay(d_list:282)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(fpfirm)
5.70 d p_prgr          s                   like(fpprgr)
     d p_rkat          s                   like(fprkat)
     d p_kund          s                   like(fpkund)
     d p_kpro          s                   like(fpkpro)
     d p_let1          s                   like(fplety)
     d p_pas1          s              5  2
     d p_let2          s                   like(fplety)
     d p_pas2          s              5  2
     d p_let3          s                   like(fplety)
     d p_pas3          s              5  2
     d p_let4          s                   like(fplety)
     d p_pas4          s              5  2
     d p_slet          s              1  0
      *
      * Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vvarl3_nobb     s                   like(vvnobb)
     d vvaal1_anob     s                   like(vvanob)
     d vvenl2_vare     s                   like(vevare)
     d vvenl2_saen     s                   like(vesaen)
     d fpril3_kund     s                   like(fpkund)
     d fpril3_kpro     s                   like(fpkpro)
5.70 d fprilu_prgr     s                   like(fpprgr)
     d fprilu_rkat     s                   like(fprkat)
     d fprilu_kkat     s                   like(fpkkat)
     d fprilu_kund     s                   like(fpkund)
     d fprilu_kpro     s                   like(fpkpro)
     d fprilu_vare     s                   like(fpvare)
     d fprilu_enhe     s                   like(fpenhe)
     d fprilu_lety     s                   like(fplety)
     d fprnlu_nkun     s                   like(fpnkun)
     d fprnlu_nkpr     s                   like(fpnkpr)
     d fprnlu_nvar     s                   like(fpnvar)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_dato          s               d   datfmt(*iso)
      *
     d w_firm          s                   like(fpfirm)
     d w_enh1          s                   like(veenhe)
     d w_enh2          s                   like(veenhe)
     d w_enh3          s                   like(veenhe)
     d w_omr1          s                   like(veomre)
     d w_omr2          s                   like(veomre)
     d w_omr3          s                   like(veomre)
     d w_sal1          s                   like(fpsapr)
     d w_sal2          s                   like(fpsapr)
     d w_sal3          s                   like(fpsapr)
     d w_sad1          s                   like(fpsapr)
     d w_sad2          s                   like(fpsapr)
     d w_sad3          s                   like(fpsapr)
     d w_lety          s                   like(fplety)
     d w_pri1          s                   like(fpsapr)
     d w_pri2          s                   like(fpsapr)
     d w_pri3          s                   like(fpsapr)
6.31 d w_lv_vare       s                   like(vvvare)
      *
      * Konstanter
      *
     d c_form          s              1    inz('3')
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.31 C/Exec SQL
6.31 C+  Set Option DatFmt=*ISO
6.31 C/End-Exec
      * Dersom sletting er valgt, slettes alle eksisterende spesialpriser
      * for rabattkategori eller kunde/kundeprosjekt
      *
     c                   if        p_slet = 1
      *
     c                   if        p_rkat <> 0
5.70 c                   eval      fprilu_prgr = p_prgr
     c                   eval      fprilu_rkat = p_rkat
     c     fprilu_key2   setll     fprilu
     c     fprilu_key2   reade     fprilur
     c                   dow       not %eof
     c                   delete    fprilur
     c     fprilu_key2   reade     fprilur
     c                   enddo
     c                   else
      *
     c                   eval      fpril3_kund = p_kund
     c                   eval      fpril3_kpro = p_kpro
     c     fpril3_key    setll     fpril3
     c     fpril3_key    reade     fpril3r
     c                   dow       not %eof
     c                   delete    fpril3r
     c     fpril3_key    reade     fpril3r
     c                   enddo
      *
     c                   eval      fprnlu_nkun = p_kund
     c                   eval      fprnlu_nkpr = p_kpro
     c     fprnlu_key2   setll     fprnlu
     c     fprnlu_key2   reade     fprnlur
     c                   dow       not %eof
     c                   delete    fprnlur
     c     fprnlu_key2   reade     fprnlur
     c                   enddo
      *
     c                   endif
      *
     c                   endif
      *
      * Leser og oppretter/oppdaterer spesialpriser
      *
     c                   read      flvwpfr
     c                   dow       not %eof
     c                   exsr      behandling
     c                   read      flvwpfr
     c                   enddo
      *
      * Kaller program i VA over varer som ikke ble lest inn i
      * spesialpris-registeret.
      *
     c                   call      'FO719C'
     c                   parm                    c_form
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vare med spesialpris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   eval      b_feil = *off
      *
     c                   eval      d_list = fwirad
      *
      * Avbryter dersom nobb-nummer, enhet eller pris ikke er utfylt
      *
     c                   if        d_nobb = 0 or
     c                             d_enhe = *blank or
     c                             (d_ntpa = 0 and d_ntla = 0 and d_ntdi = 0)
     c                   leavesr
     c                   endif
      *
      * Dersom vare finnes oppdateres spesialpris for vare
      * Oppdaterer også spesialpris for alle alternative varer
      *
6.31  * sjekker om logistikk-vare
6.31 c                   eval      w_lv_vare = *blank
6.31 c/exec sql
6.31 c+ select vvlvnr
6.31 c+ into   :w_lv_vare
6.31 c+ from   vvlepf
6.31 c+ where  vvlnob = :d_nobb
6.31 c+ fetch first 1 rows only
6.31 c/end-exec
      *
6.31 c                   if        w_lv_vare <> *blank
6.31 c                   eval      vvarl1_vare = w_lv_vare
6.31 c                   else
6.32 c                   movel     d_nobb        vvarl1_vare
6.32 c                   endif
6.32 c     vvarl1_key    chain     vvarl1
     c                   if        not %found and
     c                             d_nob1 <> 0
6.31 c/exec sql
6.31 c+ select vvlvnr
6.31 c+ into   :w_lv_vare
6.31 c+ from   vvlepf
6.31 c+ where  vvlnob = :d_nob1
6.31 c+ fetch first 1 rows only
6.31 c/end-exec
6.31 c                   if        w_lv_vare <> *blank
6.31 c                   eval      vvarl1_vare = w_lv_vare
6.31 c                   else
6.32 c                   movel     d_nob1        vvarl1_vare
6.32 c                   endif
6.32 c     vvarl1_key    chain     vvarl1
     c                   if        not %found and
     c                             d_nob2 <> 0
6.31 c/exec sql
6.31 c+ select vvlvnr
6.31 c+ into   :w_lv_vare
6.31 c+ from   vvlepf
6.31 c+ where  vvlnob = :d_nob2
6.31 c+ fetch first 1 rows only
6.31 c/end-exec
6.31 c                   if        w_lv_vare <> *blank
6.31 c                   eval      vvarl1_vare = w_lv_vare
6.31 c                   else
6.32 c                   movel     d_nob2        vvarl1_vare
6.32 c                   endif
6.32 c     vvarl1_key    chain     vvarl1
     c                   if        not %found and
     c                             d_nob3 <> 0
6.31 c/exec sql
6.31 c+ select vvlvnr
6.31 c+ into   :w_lv_vare
6.31 c+ from   vvlepf
6.31 c+ where  vvlnob = :d_nob3
6.31 c+ fetch first 1 rows only
6.31 c/end-exec
6.31 c                   if        w_lv_vare <> *blank
6.31 c                   eval      vvarl1_vare = w_lv_vare
6.31 c                   else
6.32 c                   movel     d_nob3        vvarl1_vare
6.32 c                   endif
6.32 c     vvarl1_key    chain     vvarl1
     c                   endif
     c                   endif
     c                   endif

      *
     c                   if        %found
     c                   exsr      oppdater
     c                   endif
      *
     c                   eval      vvaal1_anob = d_nobb
     c     vvaal1_key    setll     vvaal1
     c     vvaal1_key    reade     vvaal1
      *
     c                   if        %eof and
     c                             not %found(vvarl3)
     c                   leavesr
     c                   endif
      *
     c                   dow       not %eof
     c                   if        vvaava <> *blank
     c                   eval      vvarl1_vare = vvaava
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   exsr      oppdater
     c                   endif
     c                   endif
     c     vvaal1_key    reade     vvaal1
     c                   enddo
      *
      * Sletter rad i arbeidsfil
      *
     c                   if        b_feil = *off
     c                   delete    flvwpfr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting/oppdatering av spesialpriser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
      * Henter tre salgsenheter fra varen
      *
     c                   eval      w_enh1 = *blank
     c                   eval      w_enh2 = *blank
     c                   eval      w_enh3 = *blank
     c                   eval      w_omr1 = 0
     c                   eval      w_omr2 = 0
     c                   eval      w_omr3 = 0
      *
     c                   eval      vvenl2_vare = vvvare
     c                   eval      vvenl2_saen = 1
     c     vvenl2_key    setll     vvenl2
     c     vvenl2_key2   reade     vvenl2
     c                   dow       not %eof
      *
     c                   select
     c                   when      w_enh1 = *blank
     c                   eval      w_enh1 = veenhe
     c                   eval      w_omr1 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 = *blank
     c                   eval      w_enh2 = veenhe
     c                   eval      w_omr2 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 <> *blank and
     c                             w_enh3 = *blank
     c                   eval      w_enh3 = veenhe
     c                   eval      w_omr3 = veomre
     c                   leave
     c                   endsl
      *
     c     vvenl2_key2   reade     vvenl2
     c                   enddo
      *
      * Avbryter dersom enhet ikke finnes på varen
      *
     c                   if        w_enh1 <> d_enhe and
     c                             w_enh2 <> d_enhe and
     c                             w_enh3 <> d_enhe
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
      * Finner salgspris for alle salgsenheter
      *
     c                   eval      w_sal1 = 0
     c                   eval      w_sal2 = 0
     c                   eval      w_sal3 = 0
     c                   eval      w_sad1 = 0
     c                   eval      w_sad2 = 0
     c                   eval      w_sad3 = 0
      *
     c                   select
     c                   when      d_enhe = w_enh1
     c                   eval      w_sal1 = d_ntla
     c                   eval      w_sal2 = w_sal1 * w_omr2 / w_omr1
     c                   eval      w_sal3 = w_sal1 * w_omr3 / w_omr1
     c                   eval      w_sad1 = d_ntdi
     c                   eval      w_sad2 = w_sad1 * w_omr2 / w_omr1
     c                   eval      w_sad3 = w_sad1 * w_omr3 / w_omr1
     c                   when      d_enhe = w_enh2
     c                   eval      w_sal2 = d_ntla
     c                   eval      w_sal1 = w_sal2 * w_omr1 / w_omr2
     c                   eval      w_sal3 = w_sal2 * w_omr3 / w_omr2
     c                   eval      w_sad2 = d_ntdi
     c                   eval      w_sad1 = w_sad2 * w_omr1 / w_omr2
     c                   eval      w_sad3 = w_sad2 * w_omr3 / w_omr2
     c                   when      d_enhe = w_enh3
     c                   eval      w_sal3 = d_ntla
     c                   eval      w_sal1 = w_sal3 * w_omr1 / w_omr3
     c                   eval      w_sal2 = w_sal3 * w_omr2 / w_omr3
     c                   eval      w_sad3 = d_ntdi
     c                   eval      w_sad1 = w_sad3 * w_omr1 / w_omr3
     c                   eval      w_sad2 = w_sad3 * w_omr2 / w_omr3
     c                   endsl
      *
      * Oppdaterer/oppretter spesialpris
      *
5.70 c                   eval      fprilu_prgr = p_prgr
     c                   eval      fprilu_rkat = p_rkat
     c                   eval      fprilu_kkat = 0
     c                   eval      fprilu_kund = p_kund
     c                   eval      fprilu_kpro = p_kpro
     c                   eval      fprilu_vare = vvvare
      *
      * Lagerpris
      *
     c                   if        d_ntla <> 0
     c                   eval      w_lety = 0
     c                   eval      w_pri1 = w_sal1
     c                   eval      w_pri2 = w_sal2
     c                   eval      w_pri3 = w_sal3
     c                   exsr      oppd_levtype
     c                   endif
      *
      * Direktelevert pris
      *
6.33 c                   if        d_ntdi <> 0 and
6.33 c                             d_ntdi <> d_ntla
     c                   eval      w_lety = 1
     c                   eval      w_pri1 = w_sad1
     c                   eval      w_pri2 = w_sad2
     c                   eval      w_pri3 = w_sad3
     c                   exsr      oppd_levtype
     c                   endif
      *
      * Priser på egne leveringstyper
      *
     c                   if        p_let1 <> 0 and
     c                             p_let1 <> 1
     c                   eval      w_lety = p_let1
     c                   eval      w_pri1 = w_sal1
     c                   eval      w_pri2 = w_sal2
     c                   eval      w_pri3 = w_sal3
     c                   exsr      oppd_levtype
     c                   endif
      *
     c                   if        p_let2 <> 0 and
     c                             p_let2 <> 1
     c                   eval      w_lety = p_let2
     c                   eval      w_pri1 = w_sal1
     c                   eval      w_pri2 = w_sal2
     c                   eval      w_pri3 = w_sal3
     c                   exsr      oppd_levtype
     c                   endif
      *
     c                   if        p_let3 <> 0 and
     c                             p_let3 <> 1
     c                   eval      w_lety = p_let3
     c                   eval      w_pri1 = w_sal1
     c                   eval      w_pri2 = w_sal2
     c                   eval      w_pri3 = w_sal3
     c                   exsr      oppd_levtype
     c                   endif
      *
     c                   if        p_let4 <> 0 and
     c                             p_let4 <> 1
     c                   eval      w_lety = p_let4
     c                   eval      w_pri1 = w_sal1
     c                   eval      w_pri2 = w_sal2
     c                   eval      w_pri3 = w_sal3
     c                   exsr      oppd_levtype
     c                   endif
      *
      * Notat
      *
     c                   if        p_kund <> 0 and
     c                             (d_lbet <> *blank or d_merk <> *blank)
      *
     c                   clear                   fprnlur
      *
     c                   eval      fprnlu_nkun = p_kund
     c                   eval      fprnlu_nkpr = p_kpro
     c                   eval      fprnlu_nvar = vvvare
     c     fprnlu_key    chain     fprnlur
      *
     c                   eval      fpnkun = p_kund
     c                   eval      fpnkpr = p_kpro
     c                   eval      fpnvar = vvvare
     c                   eval      fpnno1 = %subst(d_lbet:1:20)
     c                   eval      fpnno2 = %subst(d_lbet:21:20)
     c                   eval      fpnno3 = %subst(d_merk:1:20)
     c                   eval      fpnno4 = %subst(d_merk:21:20)
     c                   eval      fpnno5 = %subst(d_merk:41:20)
      *
     c                   eval      fpneus = l_user
     c                   time                    fpneda
     c                   time                    fpneti
      *
     c                   if        %found(fprnlu)
     c                   update    fprnlur
     c                   else
     c                   time                    fpnoda
     c                   write     fprnlur
     c                   endif
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering/oppretting av spesialpris på leveringstype
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_levtype  begsr
      *
      * Korrigerer pris med påslag på leveringstype
      *
     c                   select
     c                   when      p_let1 = w_lety and p_pas1 <> 0
     c                   eval(h)   w_pri1 = w_pri1 + (w_pri1 * p_pas1 / 100)
     c                   eval(h)   w_pri2 = w_pri2 + (w_pri2 * p_pas1 / 100)
     c                   eval(h)   w_pri3 = w_pri3 + (w_pri3 * p_pas1 / 100)
     c                   when      p_let2 = w_lety and p_pas2 <> 0
     c                   eval(h)   w_pri1 = w_pri1 + (w_pri1 * p_pas2 / 100)
     c                   eval(h)   w_pri2 = w_pri2 + (w_pri2 * p_pas2 / 100)
     c                   eval(h)   w_pri3 = w_pri3 + (w_pri3 * p_pas2 / 100)
     c                   when      p_let3 = w_lety and p_pas3 <> 0
     c                   eval(h)   w_pri1 = w_pri1 + (w_pri1 * p_pas3 / 100)
     c                   eval(h)   w_pri2 = w_pri2 + (w_pri2 * p_pas3 / 100)
     c                   eval(h)   w_pri3 = w_pri3 + (w_pri3 * p_pas3 / 100)
     c                   when      p_let4 = w_lety and p_pas4 <> 0
     c                   eval(h)   w_pri1 = w_pri1 + (w_pri1 * p_pas4 / 100)
     c                   eval(h)   w_pri2 = w_pri2 + (w_pri2 * p_pas4 / 100)
     c                   eval(h)   w_pri3 = w_pri3 + (w_pri3 * p_pas4 / 100)
     c                   endsl
      *
      * - enhet 1
      *
     c                   clear                   fprilur
      *
     c                   eval      fprilu_enhe = w_enh1
     c                   eval      fprilu_lety = w_lety
     c     fprilu_key    chain     fprilur
      *
     c                   eval      fpfirm = w_firm
5.70 c                   eval      fpprgr = p_prgr
     c                   eval      fprkat = p_rkat
     c                   eval      fpkkat = 0
     c                   eval      fpkund = p_kund
     c                   eval      fpkpro = p_kpro
     c                   eval      fpvare = vvvare
     c                   eval      fpenhe = w_enh1
     c                   eval      fplety = w_lety
     c                   eval      fpfdat = *loval
     c                   eval      fptdat = *loval
     c                   eval      fpsapr = w_pri1
     c                   eval      fprab1 = 0
     c                   eval      fprab2 = 0
     c                   eval      fpkopr = 0
     c                   eval      fpnumm = 0
      *
     c                   eval      fpeusr = l_user
     c                   time                    fpedat
     c                   time                    fpetim
      *
     c                   if        %found(fprilu)
6.10 c                   time                    fpedat
6.10 c                   time                    fpetim
6.10 c                   eval      fpeusr = l_user
     c                   update    fprilur
     c                   else
6.10 c                   time                    fpodat
6.10 c                   time                    fpotim
6.10 c                   eval      fpousr = l_user
6.10 c                   time                    fpedat
6.10 c                   time                    fpetim
6.10 c                   eval      fpeusr = l_user
     c                   write     fprilur
     c                   endif
      *
      * - enhet 2
      *
     c                   if        w_enh2 <> *blank
      *
     c                   clear                   fprilur
      *
     c                   eval      fprilu_enhe = w_enh2
     c     fprilu_key    chain     fprilur
      *
     c                   eval      fpfirm = w_firm
5.70 c                   eval      fpprgr = p_prgr
     c                   eval      fprkat = p_rkat
     c                   eval      fpkkat = 0
     c                   eval      fpkund = p_kund
     c                   eval      fpkpro = p_kpro
     c                   eval      fpvare = vvvare
     c                   eval      fpenhe = w_enh2
     c                   eval      fplety = w_lety
     c                   eval      fpfdat = *loval
     c                   eval      fptdat = *loval
     c                   eval      fpsapr = w_pri2
     c                   eval      fprab1 = 0
     c                   eval      fprab2 = 0
     c                   eval      fpkopr = 0
     c                   eval      fpnumm = 0
      *
     c                   eval      fpeusr = l_user
     c                   time                    fpedat
     c                   time                    fpetim
      *
     c                   if        %found(fprilu)
6.10 c                   time                    fpedat
6.10 c                   time                    fpetim
6.10 c                   eval      fpeusr = l_user
     c                   update    fprilur
     c                   else
6.10 c                   time                    fpodat
6.10 c                   time                    fpotim
6.10 c                   eval      fpousr = l_user
6.10 c                   time                    fpedat
6.10 c                   time                    fpetim
6.10 c                   eval      fpeusr = l_user
     c                   write     fprilur
     c                   endif
      *
     c                   endif
      *
      * - enhet 3
      *
     c                   if        w_enh3 <> *blank
      *
     c                   clear                   fprilur
      *
     c                   eval      fprilu_enhe = w_enh3
     c     fprilu_key    chain     fprilur
      *
     c                   eval      fpfirm = w_firm
5.70 c                   eval      fpprgr = p_prgr
     c                   eval      fprkat = p_rkat
     c                   eval      fpkkat = 0
     c                   eval      fpkund = p_kund
     c                   eval      fpkpro = p_kpro
     c                   eval      fpvare = vvvare
     c                   eval      fpenhe = w_enh3
     c                   eval      fplety = w_lety
     c                   eval      fpfdat = *loval
     c                   eval      fptdat = *loval
     c                   eval      fpsapr = w_pri3
     c                   eval      fprab1 = 0
     c                   eval      fprab2 = 0
     c                   eval      fpkopr = 0
     c                   eval      fpnumm = 0
      *
     c                   eval      fpeusr = l_user
     c                   time                    fpedat
     c                   time                    fpetim
      *
     c                   if        %found(fprilu)
6.10 c                   time                    fpedat
6.10 c                   time                    fpetim
6.10 c                   eval      fpeusr = l_user
     c                   update    fprilur
     c                   else
6.10 c                   time                    fpodat
6.10 c                   time                    fpotim
6.10 c                   eval      fpousr = l_user
6.10 c                   time                    fpedat
6.10 c                   time                    fpetim
6.10 c                   eval      fpeusr = l_user
     c                   write     fprilur
     c                   endif
      *
     c                   endif
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
5.70 c                   parm                    p_prgr
     c                   parm                    p_rkat
     c                   parm                    p_kund
     c                   parm                    p_kpro
     c                   parm                    p_slet
     c                   parm                    p_let1
     c                   parm                    p_pas1
     c                   parm                    p_let2
     c                   parm                    p_pas2
     c                   parm                    p_let3
     c                   parm                    p_pas3
     c                   parm                    p_let4
     c                   parm                    p_pas4
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
     c     vvarl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl3_nobb
      *
      * Key for oppslag på alternativ vare
      *
     c     vvaal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvaal1_anob
      *
      * Key for oppdatering av spesialpris
      *
     c     fpril3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fpril3_kund
     c                   kfld                    fpril3_kpro
      *
     c     fprilu_key    klist
     c                   kfld                    w_firm
5.70 c                   kfld                    fprilu_prgr
     c                   kfld                    fprilu_rkat
     c                   kfld                    fprilu_kkat
     c                   kfld                    fprilu_kund
     c                   kfld                    fprilu_kpro
     c                   kfld                    fprilu_vare
     c                   kfld                    fprilu_enhe
     c                   kfld                    fprilu_lety
      *
     c     fprilu_key2   klist
     c                   kfld                    w_firm
5.70 c                   kfld                    fprilu_prgr
     c                   kfld                    fprilu_rkat
      *
      * Key for oppdatering av spesialpris notat
      *
     c     fprnlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fprnlu_nkun
     c                   kfld                    fprnlu_nkpr
     c                   kfld                    fprnlu_nvar
      *
     c     fprnlu_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    fprnlu_nkun
     c                   kfld                    fprnlu_nkpr
      *
      * Key for les på vare-enhet
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
     c                   kfld                    vvenl2_saen
      *
     c     vvenl2_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
