     H DATEDIT(*DMY)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  System. . . . . : ASLAGR                                       *
      *  Program . . . . : LO604R                                       *
      *  Beskrivelse . . : Start utkjøring av bestilling/innkjøpspapirer*
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  Spesialversjon. :                                              *
      *  Tilpasset for . :                                              *
      *                                                                 *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.*
      *  --------- ------  ---------------------------------------------*
6.20  *  6.20      070513  Feil meldingsnummer benyttet i displayfile   *
6.nu  *  6.30      050614  Utvidelser mtp. nummerutvidelse          bof
6.31  *  6.30 bof  210516  Får ikke taste inn lavere periode enn "avsluttet periode"
7.xx  * 7.xx  140218  bkv  Justeringer basert på ny GUI og regler
7.00  *  K00       220420  Utvidet skjermbile                      bsa  *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     FVOTYL1    IF   E           K DISK    RENAME(VOTYPFR:VOTYL1R)
     FLSTSL1    IF   E           K DISK    RENAME(LSTSPFR:LSTSL1R)
6.31 fraa1l1    if   e           k disk    rename(raa1pfr:raa1l1r)
     FLPM1LU    UF A E           K DISK    RENAME(LPM1PFR:LPM1LUR)
      *
     FLO604D    CF   E             WORKSTN
      *
     D                 DS
     D  LDAMBR                 1     10
     D  LDABOK                 1      1
     D  LDAFIR                 2      4  0
     D  LDALST                 5     10  0
      *
      *
     D                UDS
     D  DSWSID               901    910
     D  DSUSER               911    920
     D  DSSFIR               944    946  0
      *
      * Definering av variable
      *
     d wwdato_6        s              6  0
     d wwdato          s               d   datfmt(*iso)
     d WWPERI          s              4  0
     d WWPERI_ALF      s              8
6.31 d w_sp_aar        s                   like(ra1aar)
6.31 d w_sp_mnd        s                   like(ra1per)
6.31 d w_sp_per        s              6  0
6.31 d w_peri          s              6  0
6.31 d w_mnd1          s              2  0
6.31 d w_aar1          s              4  0
7.xx d w_ot02          s                   like(laaot2)
7.xx d w_ot03          s                   like(laaot3)
7.xx d w_ot04          s                   like(laaot4)
7.xx d w_ot05          s                   like(laaot5)
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Velg om det skal kjøres direkte utskrift                       *
      *  Direkte utskrift kjøres dersom bestillingsnummer er utfylt     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     WPNUMM        IFNE      *ZERO
     C                   MOVE      WPOTYP        F1OTYP
     C                   GOTO      F2TAGA
     C                   END
      *
     C                   MOVE      *ZERO         WPDIRK
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  F1 Behandler valg av utskrift                                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     F1TAGA        TAG
     C                   MOVE      *ZERO         F1VALG
      *
      *  Send alt
      *
     C     F1TAGB        TAG
     C                   EXFMT     F1BLD
      *
      *  F3=Gå tilbake
      *
     C     *INKC         CABEQ     *ON           XSLUTT
      *
      *  F4=Legg jobb i jobbkø
      *
     C     *INKD         IFEQ      *ON
     C                   MOVE      2             WPDIRK
     C                   END
      *
      *  Sjekk,  Finnes ordretype(til) i kode-register
      *
     C     F1OTYP        IFNE      *BLANK
     C                   MOVE      F1OTYP        OTYTYP
     C     OTYKEY        CHAIN     VOTYL1R                            90
     C     *IN90         IFEQ      *ON
     C                   MOVE      *ON           *IN31
     C                   GOTO      F1TAGB
     C                   ENDIF
     C                   ENDIF
      *
      *  Sjekk,  Benytt ordre-type, eller velg en fra listen
      *          av forhånsdefinerte ordre-typer
      *
     C     F1OTYP        IFEQ      *BLANK
     C     F1VALG        IFEQ      1
7.xx C*                  MOVE      F1OT02        F1OTYP
7.xx c                   eval      F1OTYP = %subst(F1OT02:4:2)
     C                   END
     C     F1VALG        IFEQ      2
7.xx C*                   MOVE      F1OT03        F1OTYP
7.xx c                   eval      F1OTYP = %subst(F1OT03:4:2)
     C                   END
     C     F1VALG        IFEQ      3
7.xx C*                   MOVE      F1OT04        F1OTYP
7.xx c                   eval      F1OTYP = %subst(F1OT04:4:2)
     C                   END
     C     F1VALG        IFEQ      4
7.xx C*                   MOVE      F1OT05        F1OTYP
7.xx c                   eval      F1OTYP = %subst(F1OT05:4:2)
     C                   END
     C                   END
      *
     C                   GOTO      F2TAGA
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  F2 Innlegging av periode og faktura-dato                       *
      *     Bildet vises bare dersom akk.type=3                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     F2TAGA        TAG
      *
      *  Hent opplysninger fra ORDRETYPE-REGISTER
      *
     C                   MOVE      F1OTYP        OTYTYP
     C     OTYKEY        CHAIN     VOTYL1R                            90
      *
      *  Sjekk, Er dette en Faktura/Kreditnota, hvis ikke, avslutt
      *
     C     VAOAKK        IFNE      3
     C                   GOTO      START
     C                   END
      *
      *  Nullstiller periode og fakturadato
      *
     C                   MOVE      UDATE         F2PERI
     C                   MOVE      UDATE         F2DATO
      *
      *  Send alt
      *
     C     F2TAGB        TAG
     C                   EXFMT     F2BLD
      *
      *  F3=Gå tilabke
      *
     C     *INKC         CABEQ     *ON           XSLUTT
      *
      *  F4=Legg jobb i jobbkø
      *
     C     *INKD         IFEQ      *ON
     C                   MOVE      2             WPDIRK
     C                   END
      *
      *  Sjekk, Er periode en gyldig periode
      *
     c                   move      f2peri        wwdato_6
     c                   movel     01            wwdato_6
     c     *dmy          test(d)                 wwdato_6               31
     c                   if        *in31 = *on
     c                   goto      f2tagb
     c                   endif
      *
      *  Sjekk, Er fakturadato en gyldig dato
      *
     c     *dmy          test(d)                 f2dato                 32
     c                   if        *in32 = *on
     c                   goto      f2tagb
     c                   endif
      *
      *  Sjekk, Er fakturadato innefor periode
      *
     C     *INKS         IFEQ      *OFF
     c                   move      f2dato        wwperi
     C     wwperi        IFNE      f2peri
     C                   MOVE      *ON           *IN33
     C                   GOTO      F2TAGB
     C                   END
     C                   END
6.31  *
6.31  * Sjekk om periode er lavere enn "avsluttet periode" fra status
6.31  *
6.31 c                   if        w_sp_per <> 0
6.31 c                   move      f2peri        w_aar1
6.31 c                   movel     20            w_aar1
6.31 c                   movel     f2peri        w_mnd1
6.31 c                   movel     w_aar1        w_peri
6.31 c                   move      w_mnd1        w_peri
6.31 c                   if        w_peri <= w_sp_per
6.31 c                   eval      *in36 = *on
6.31 c                   goto      f2tagb
6.31 c                   endif
6.31 c                   endif
      *
     C                   GOTO      START
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  START Behandler danning av parameter før utskrift              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     START         TAG
      *
      *  Kaller opp subrutine for utlegging av parameter, for
      *  start av utskrift (WWVALG=Internt listenummer fra 0 til 3)
      *
     C                   MOVE      *ZERO         WWVALG
     C                   EXSR      PARAM
      *
      *  Hopp ut og start utkjøring, dersom kun en bestilling er valgt
      *
     C     WPNUMM        IFNE      *ZERO
     C                   GOTO      NESTE
     C                   END
      *
      *  BESTILLINGSFORSLAG : Subrutinen skal ikke kjøres ekstra
      *
     C     VAOAKK        IFEQ      0
     C                   GOTO      NESTE
     C                   END
      *
      *  BESTILLING : Subrutinen skal ikke kjøres ekstra
      *
     C     VAOAKK        IFEQ      1
     C                   GOTO      NESTE
     C                   END
      *
      *  PAKKSEDDEL : Subrutinen skal ikke kjøres
      *
     C     VAOAKK        IFEQ      2
     C                   GOTO      NESTE
     C                   END
      *
      *  FAKTURA/KREDITNOTA: Subrutinen skal ikke kjøres
      *
     C     VAOAKK        IFEQ      3
     C                   GOTO      NESTE
     C                   END
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  NESTE Kaller opp 'CL'-programmet for å starte utskriften       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     NESTE         TAG
      *
     C                   CALL      'LO112C'
     C                   PARM                    LDAMBR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSLUTT        TAG
      *
     C                   SETON                                        LR
     C                   RETURN
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Oppdaterer  BESTILLING-PLUKK-PARAM1-REGISTER                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     PARAM         BEGSR
      *
     C                   MOVE      WWVALG        PM1LST
     C     PM1KEY        CHAIN     LPM1LUR                            90
      *
     C                   MOVE      WPOUTQ        LK1UTQ
     C                   MOVE      F1OTYP        LK1OTY
     C                   MOVE      WPNUMM        LK1ONR
     C                   MOVE      WPSUFF        LK1SUF
     C                   MOVE      WPDIRK        LK1VG0
     C                   MOVE      WWVALG        LK1VG1
     C                   MOVE      F1VLG2        LK1VG2
     C                   MOVE      F1VLG3        LK1VG3
      *
     c                   eval      lk1per = *loval
     c                   eval      lk1dat = *loval
      *
     c                   if        f2peri <> 0
     c                   move      f2peri        wwdato_6
     c                   movel     01            wwdato_6
     c     *dmy          move      wwdato_6      wwdato
     c     *iso0         move      wwdato        wwperi_alf
     c                   movel     wwperi_alf    lk1per
     c                   endif
      *
     c                   if        f2dato <> 0
     c     *dmy          move      f2dato        lk1dat
     c                   endif
      *
     C     *IN90         IFEQ      *OFF
     C                   UPDATE    LPM1LUR
     C                   ELSE
     C                   MOVE      PM1FIR        LK1FIR
     C                   MOVE      DSWSID        LK1WSD
     C                   MOVE      DSUSER        LK1USR
     C                   MOVE      WWVALG        LK1LST
     C                   WRITE     LPM1LUR
     C                   END
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     CSR   *INZSR        BEGSR
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     C                   MOVE      *ZERO         WCQNNN            1
     C                   MOVE      *ZERO         WCNUMM            6 0
      *
      *  Redefinering av felter
      *
     C     *LIKE         DEFINE    LK1VG1        WWVALG
      *
      *  Redefinering av nøkler
      *
     C     *LIKE         DEFINE    LAAFIR        STSFIR
      *
     C     *LIKE         DEFINE    VAOFIR        OTYFIR
     C     *LIKE         DEFINE    VAOTYP        OTYTYP
      *
     C     *LIKE         DEFINE    LK1FIR        PM1FIR
     C     *LIKE         DEFINE    LK1WSD        PM1WSD
     C     *LIKE         DEFINE    LK1USR        PM1USR
     C     *LIKE         DEFINE    LK1LST        PM1LST
      *
      * Key for file : LAGER-STATUS-KODE-REGISTER
      *
     C     STSKEY        KLIST
     C                   KFLD                    STSFIR
      *
      * Key for file : ORDRETYPE-REGISTER
      *
     C     OTYKEY        KLIST
     C                   KFLD                    OTYFIR
     C                   KFLD                    OTYTYP
      *
      * Key for file : PLUKK-1-LINJE-REGISTER
      *
     C     PM1KEY        KLIST
     C                   KFLD                    PM1FIR
     C                   KFLD                    PM1WSD
     C                   KFLD                    PM1USR
     C                   KFLD                    PM1LST
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    WPSKOR            1
     C                   PARM                    WPFIRM            3 0
6.nu C                   PARM                    WPNUMM            8 0
     C                   PARM                    WPSUFF            2 0
     C                   PARM                    WPOTYP            2
     C                   PARM                    WPDIRK            1 0
     C                   PARM                    WPOUTQ           10
6.31  *
6.31  * Key for file : Kode - status 1
6.31  *
6.31 c     raa1l1_key    klist
6.31 c                   kfld                    wpfirm
      *
      *  Legger inn firmanummer i nøkklene
      *
     C                   MOVE      WPFIRM        STSFIR
     C                   MOVE      WPFIRM        OTYFIR
     C                   MOVE      WPFIRM        PM1FIR
     C                   MOVE      WPFIRM        LDAFIR
      *
      *  Legger inn opplysninger i nøkklene
      *
     C                   MOVE      DSWSID        PM1WSD
     C                   MOVE      DSUSER        PM1USR
      *
      *  Hent opplysninger fra LAGER-STATUS-KODE-REGISTER
      *
     C     STSKEY        CHAIN     LSTSL1R                            90
     C     *IN90         IFEQ      *OFF
7.xx C                   MOVE      LAAOT2        w_ot02
7.xx C                   MOVE      LAAOT3        w_ot03
7.xx C                   MOVE      LAAOT4        w_ot04
7.xx C                   MOVE      LAAOT5        w_ot05
     C                   END
7.xx C                   eval      F1OT02 = '1= '+w_ot02+' Bestilling,'
7.xx C                   eval      F1OT03 = '2= '+w_ot03+' Pakkseddel,'
7.xx C                   eval      F1OT04 = '3= '+w_ot04+' Inngående faktura,'
7.xx C                   eval      F1OT05 = '4= '+w_ot05+' Kreditnota'
      *
      *
      *  Henter siste nummer for File-member
      *
     C                   CALL      'LÅ900R'
     C                   PARM                    WCQNNN
     C                   PARM                    WCNUMM
      *
     C                   MOVE      'A'           LDABOK
     C                   MOVE      WCNUMM        LDALST
6.31 c     raa1l1_key    chain     raa1l1r                            90
6.31  *
6.31 c                   if        %found(raa1l1)
6.31 c                   eval      W_sp_mnd = ra1per
6.31 c                   eval      w_sp_aar = ra1aar
6.31 c                   eval      w_sp_per = w_sp_aar * 100 + w_sp_mnd
6.31 c                   else
6.31 c                   eval      w_sp_per = *zero
6.31 c                   endif
      *
     CSR                 ENDSR
