     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VV700R
      * Beskrivelse . . : Vare, returnerer neste ledige varenummer
      *
      *                   Programmet sjekker at 'neste ledige varenummer'
      *                   ikke finnes i register over slettede varer.
      *
      *                   Parametre inn: Firmanummer
      *                                  Varenummer for posisjonering
      *                             ut:  Neste ledige varenummer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       201098 HKO  Nytt program
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvasl1    if   e           k disk    rename(vvaspfr:vvasl1r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vvfirm)
     d p_pvar          s                   like(vvvare)
     d p_vare          s                   like(vvvare)
      *
      * Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vvasl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vvfirm)
      *
     d w_vare_lest     s              8  0
     d w_pvar          s              8  0
     d w_vare          s              8  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer parametre
      *
     c                   eval      p_vare = *blank
      *
      * Finner neste ledige varenummer
      *
     c                   movel     p_pvar        w_pvar
     c                   if        w_pvar > 0
     c                   eval      w_pvar = w_pvar - 1
     c                   endif
      *
     c                   movel     w_pvar        vvarl1_vare
     c     vvarl1_key    setll     vvarl1
     c                   read      vvarl1                                 90
     c                   dow       *in90 = *off
      *
     c                   if        vvfirm <> p_firm
     c                   leave
     c                   endif
      *
     c                   movel     vvvare        w_vare_lest
      *
     c                   eval      w_vare = w_pvar + 1
      *
     c                   dow       w_vare < w_vare_lest
     c                   movel     w_vare        vvasl1_vare
     c     vvasl1_key    chain     vvasl1                             91
     c                   if        *in91 = *on
     c                   leave
     c                   endif
     c                   eval      w_vare = w_vare + 1
     c                   enddo
      *
     c                   if        w_vare < w_vare_lest
     c                   movel     w_vare        p_vare
     c                   leave
     c                   endif
      *
     c                   eval      w_pvar = w_vare_lest
      *
     c                   read      vvarl1                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_pvar
     c                   parm                    p_vare
      *
      * Key for les på vareregister etter varenummer
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på vare-slettet-register
      *
     c     vvasl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvasl1_vare
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
