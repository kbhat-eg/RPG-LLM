     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VE725R
      * Beskrivelse . . : Henter varenr, nobbnr + enhet ut fra gitt eannr
      *
      *
      *                   Input : firma, eannr.
      *                   Output: vare, enhet,nobb, status (1 = ok)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       010612 bsa  Nytt program
6.30  *       190615 bsa  Omskrivning ifbm endring av pakning i VA
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvepl2    if   e           k disk    rename(vveppfr:vvepl2r)
     fveanl1    if   e           k disk    rename(veanpfr:veanl1r)
6.30 f*jvppl2    if   e           k disk    rename(jvpppfr:jvppl2r)
6.30 fjvpkl3    if   e           k disk    rename(jvpkpfr:jvpkl3r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
      * Definering av variable i nøkler
      *
     d vvepl2_pfir     s                   like(vepfir)
     d vvepl2_pgti     s                   like(vepgti)
     d veanl1_firm     s                   like(vxfirm)
     d veanl1_eann     s                   like(vxeann)
     d vvarl1_vare     s                   like(vvvare)
     d jvarl1_vare     s                   like(jvvare)
6.30 d*jvppl2_pgti     s                   like(jwpgti)
6.30 d jvpkl3_gtin     s                   like(jwgtin)
      *
      * Definering av pamametre
      *
     d p_firm          s                   like(vxfirm)
     d p_eann          s                   like(vxeann)
     d p_vare          s                   like(vxvare)
     d p_enhe          s                   like(vxenhe)
     d p_nobb          s                   like(vvnobb)
     d p_stat          s              1
      *
      *
     d                uds
     d l_user                911    920
     d l_sfir                944    946  0
     d l_savd                947    950  0
     d l_lagr               1001   1002  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      * Initierer output-parametre
      *
     c                   eval      p_vare = *blank
     c                   eval      p_enhe = *blank
     c                   eval      p_nobb = *zero
     c                   eval      p_stat = *blank
      *
     c                   if        p_eann = 0
     c                   goto      avslutt
     c                   endif
      *
      * Henter varenr + evt. enhet
      *
     c                   eval      vvepl2_pfir = p_firm
     c                   eval      vvepl2_pgti = p_eann
     c     vvepl2_key    chain     vvepl2
     c                   if        %found(vvepl2)
     c                   eval      p_vare = vepvar
     c                   eval      p_enhe = vepenh
     c                   eval      p_stat = '1'
     c                   endif
      *
     c                   if        p_stat = *blank
     c                   eval      veanl1_firm = p_firm
     c                   eval      veanl1_eann = p_eann
     c     veanl1_key    chain     veanl1
     c                   if        %found(veanl1)
     c                   eval      p_vare = vxvare
     c                   eval      p_stat = '1'
     c                   endif
     c                   endif
      * Hent nobbnummer hvis varenummer er funnet via gtin
     c                   if        p_stat = '1'
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found(vvarl1)
     c                   eval      p_nobb = vvnobb
     c                   endif
     c                   endif
      * Forsøker å hente GTIN via VA
     c                   if        p_stat = *blank
     c*                  eval      jvppl2_pgti = p_eann
     c*    jvppl2_key    chain     jvppl2
     c*                  if        %found(jvppl2)
     c*                  eval      p_vare = jwpvar
6.30 c                   eval      jvpkl3_gtin = p_eann
6.30 c     jvpkl3_key    chain     jvpkl3
6.30 c                   if        %found(jvpkl3)
6.30 c                   eval      p_vare = jwvare
     c                   eval      p_stat = '1'
     c                   endif
      * Hent nobbnummer hvis varenummer er funnet via gtin
     c                   eval      jvarl1_vare = p_vare
     c     jvarl1_key    chain     jvarl1
     c                   if        %found(jvarl1)
     c                   eval      p_nobb = jvnobb
     c                   endif
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_eann
     c                   parm                    p_vare
     c                   parm                    p_enhe
     c                   parm                    p_nobb
     c                   parm                    p_stat
      *
      * Key for les på vareenhets-register
      *
     c     vvepl2_key    klist
     c                   kfld                    vvepl2_pfir
     c                   kfld                    vvepl2_pgti
      *
      * Key for les på ean-register
      *
     c     veanl1_key    klist
     c                   kfld                    veanl1_firm
     c                   kfld                    veanl1_eann
      *
      * Key for les på vareregister.
      *
     c     vvarl1_key    klist
     c                   kfld                    p_firm
     c                   kfld                    vvarl1_vare
6.30  *
6.30  * Key for les på GTIN register VA
6.30  *
6.30 c*    jvppl2_key    klist
6.30 c*                  kfld                    jvppl2_pgti
6.30  *
6.30  * Key for les på pakningsregister
6.30  *
6.30 c     jvpkl3_key    klist
6.30 c                   kfld                    jvpkl3_gtin
      *
      * Key for les på vareregister VA
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      *
     c                   endsr
