     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : VE718R
      * Beskrivelse . . : Sjekker dubletter av GTIN nummer
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       240512 bsa  Nytt program
6.10  *       020712 bsa  Utvidet til å teste på forskjellige enheter
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvepl2    if   e           k disk    rename(vveppfr:vvepl2r)
     fveanlr    if   e           k disk    rename(veanpfr:veanlrr)
      *
      * Definering av parametre
      *
     d p_gtin          s                   like(vepgti)
     d p_vare          s                   like(vepvar)
     d p_stat          s              1
      *
6.10 d b_enhe          s              1    inz(*off)
      *
     d p_anta          s              6  0
     d w_pgti          s                   like(vepgti)
     d w_pvar          s                   like(vepvar)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d vvepl2_pgti     s                   like(vepgti)
     d veanlr_eann     s                   like(vxeann)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vepfir)
     d w_dato          s               d   datfmt(*iso)
6.10 d w_enhe          s              3
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer output-parametre
      *
     c                   eval      p_stat = *blank
6.10 c                   eval      b_enhe = *off
6.10 c                   eval      w_enhe = *blank
      *
      * Leser VVEP for å finne GTIN dupbletter
      *
     c                   eval      vvepl2_pgti = p_gtin
     c     vvepl2_key    setll     vvepl2
     c     vvepl2_key    reade     vvepl2
     c                   dow       not %eof
6.10  *
6.10 c                   if        b_enhe = *off
6.10 c                   eval      w_enhe = vepenh
6.10 c                   eval      b_enhe = *on
6.10 c                   else
6.10 c                   if        w_enhe <> vepenh and
6.10 c                             p_stat = *blank
6.10 c                   eval      p_stat = '3'
6.10 c                   endif
6.10 c                   endif
      *
      *
      *
     c                   if        p_vare <> vepvar
     c                   eval      p_stat = '1'
     c                   endif
      *
     c     vvepl2_key    reade     vvepl2
     c                   enddo
      *
      * Leser VEAN for å finne GTIN dupbletter
      *
     c                   eval      vvepl2_pgti = p_gtin
     c     vvepl2_key    setll     veanlr
     c     vvepl2_key    reade     veanlr
     c                   dow       not %eof
      *
     c                   if        p_vare <> vxvare
     c                   eval      p_stat = '2'
     c                   endif
      *
     c     vvepl2_key    reade     veanlr
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_gtin
     c                   parm                    p_vare
     c                   parm                    p_stat
      *
      * Key for les på EDI-melding
      *
     c     vvepl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvepl2_pgti
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
