     h dftactgrp(*no)
      /free
        dcl-s firma    packed(3);
        dcl-s ngnornr  char(15);
        dcl-s pickertext char(1024);
        dcl-s drivertext char(1024);
        dcl-s url      varchar(1024);
        dcl-s token    varchar(2048);
        dcl-s response varchar(20000);
        dcl-s response2 varchar(2000);
        dcl-s params   varchar(1024);
        dcl-s httpstatus packed(8:0);
        dcl-s status   char(1);
        dcl-s message  char(50);
        //Get url
        exec SQL
          select trim(afudss) into :url from afpspf
           where afuide='ngnOrdUrl' limit 1;
        exec SQL
          select trim(config_value) into :token from company_settings
           where company=:firma and
                 config_key='CLOUD_API_KEY' limit 1;
        //Put together some config for the REST call
        params = '{"sslTolerate":"true", ' +
                   '"signalErrors":"false", ' +
                   '"header":"eg-apps-token,' + %trim(token)+'", ' +
                   '"header":"Accept,application/json", ' +
                   '"header":"Content-Type,application/json"' +
                  '}';
        url = url + '/api/orders/' +
                    %trim(ngnornr) + '/';
        //Call the REST endpoint
        exec SQL
          select * into :response, :response2
           from table(HTTP_GET_VERBOSE(trim(:url),
           trim(:params)));
        exec SQL
          VALUES (JSON_VALUE(:response2,
          'strict $.HTTP_STATUS_CODE'
            RETURNING integer)) into :httpstatus;
        if httpstatus >= 400;
         status = 'E';
         exec SQL
          VALUES (JSON_VALUE(:response2,
          'strict $.message'
            RETURNING char(50))) into :message;
        else;
         status = '';
         exec SQL
          VALUES (JSON_VALUE(:response,
          'strict $.warehouseEmployeeText'
            RETURNING varchar(1024))) into :pickertext;
         exec SQL
          VALUES (JSON_VALUE(:response,
          'strict $.transportEmployeeText'
            RETURNING varchar(1024))) into :drivertext;
        endif;
        *inlr = *on;
        return;
      /end-free
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    firma
     c                   parm                    ngnornr
     c                   parm                    pickertext
     c                   parm                    drivertext
     c                   parm                    status
     c                   parm                    message
      *
     c                   endsr
