     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : FW729R
      * Beskrivelse . . : Kontroll-liste
      *
      *                   Legger ut alle priser som det er endring på
      *                   Regler :
      *                    - sjekk  priser på alle enheter som ligger på vare-en
      *                    - Sjekker prisdato på fil mot det som ligger
      *                    - Forkaster varer med alfa tegn  i varenr.
      *                    - Forkaster varer med feil varegruppe
      *                    - Forkaster varer med feil enhet
      *
      *
      * Spesialversjon. : Leser fil på INTEG.DAT format fra Optimera
      * Tilpasset for . : Optimera
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       090309 bof  Nytt program
5.61  *       171009 bof  Feil-liste på varer som ikke blir lest inn
5.62  *       221009 bof  Heneter pris fra pos. 67 istedet for pos 3
5.63  *       221009 bof  Ekstra test på basisenhet
5.64  *       021209 bof  Ekstra test på for høye priser
5.65  *       041209 bof  Setter prisdato lik dagens dato for å takle
      *                   forskjellige dato på direkte pris og lagerpris
5.66  *       040310 bof  Tar ean-nr også fra enhet
5.67  *       040310 bof  Tar ean-nr også fra pris
5.68  *       010610 bof  Legger ut nobb-lev.også
5.69  *       310810 bof  lagt på en kolonne m/mva, bare vise salgs1
5.70  *       310810 bof  lat på en kolonne for DG
5.71  *       081010 bof  Lagt på en ekstralinje /rette feil mht.0 i varenr
6.10  *       221210 bsa  Utvider GTIN kode til 14, og legger inn prisgruppe
6.20  *       290811 bof  Utvider vvprpf med leverandør
8.01  *PRG2   100622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflvwpf    if   e           k disk
      *
     fvenhl1    if   e           k disk    rename(venhpfr:venhl1r)
     fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvvenl5    if   e           k disk    rename(vvenpfr:vvenl5r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
5.63 fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
      *
     ffw729p    o    e             printer
      *
      * Array
      *
     d a_erec          s            297    dim(99)
     d a_prec          s            297    dim(99)
      *
      * Definering av parametre
      *
      *
      * Definering av Local Data Area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Datastrukturer
      *
      * Definering av varerecord
      *
     d                 ds
     d d_vrec                       297
     d  d_nobb                        8  0 overlay(d_vrec:3)
     d  d_modn                        8  0 overlay(d_vrec:12)
     d  d_vgrp                        7    overlay(d_vrec:21)
     d   d_ogrp                       2  0 overlay(d_vgrp)
     d   d_hgrp                       2  0 overlay(d_vgrp:3)
     d   d_ugrp                       3  0 overlay(d_vgrp:5)
     d  d_tek1                       35    overlay(d_vrec:30)
     d  d_tek2                       35    overlay(d_vrec:68)
     d  d_mte1                       35    overlay(d_vrec:106)
     d  d_mte2                       35    overlay(d_vrec:144)
6.10 d**d_eann                       13  0 overlay(d_vrec:181)
6.10 d  d_eann                       14  0 overlay(d_vrec:181)
     d  d_pvar                       15    overlay(d_vrec:196)
     d  d_lvar                       15    overlay(d_vrec:214)
     d  d_ldor                        6  0 overlay(d_vrec:231)
     d  d_prod                        6  0 overlay(d_vrec:238)
     d  d_adat                        6  0 overlay(d_vrec:247)
     d  d_udat                        6  0 overlay(d_vrec:256)
     d  d_merk                       30    overlay(d_vrec:264)
     d  d_leve_alf                    1    overlay(d_vrec:297)
     d   d_leve                       1  0 overlay(d_leve_alf:1)
      *
      * Definering av pakningsrecord
      *
     d                 ds
     d d_erec                       297
     d  d_pakn                        3    overlay(d_erec:4)
     d  d_enhe                        3    overlay(d_erec:10)
     d  d_bred                        6  0 overlay(d_erec:23)
     d  d_leng                        6  0 overlay(d_erec:30)
     d  d_hoyd                        6  0 overlay(d_erec:37)
     d  d_vekt                        9  0 overlay(d_erec:44)
     d  d_volu                        8  5 overlay(d_erec:54)
     d  d_anta                        8  2 overlay(d_erec:63)
6.10 d**d_eean                       13  0 overlay(d_erec:72)
6.10 d  d_eean                       14  0 overlay(d_erec:72)
     d  d_ofak                       10  4 overlay(d_erec:86)
     d  d_edun                       14  0 overlay(d_erec:97)
     d  d_best                        1  0 overlay(d_erec:112)
     d  d_paks                        3    overlay(d_erec:115)
     d  d_antp_alf                    8    overlay(d_erec:120)
     d   d_antp                       8  0 overlay(d_antp_alf:1)
     d  d_psei_alf                    1    overlay(d_erec:129)
     d   d_psei                       1  0 overlay(d_psei_alf:1)
      *
      * Definering av prisrecord
      *
     d                 ds
     d d_prec                       297
     d  d_pris                        9  2 overlay(d_prec:3)
     d  d_gdat                        6  0 overlay(d_prec:15)
     d  d_pldo                        6  0 overlay(d_prec:22)
     d  d_plva                       15    overlay(d_prec:30)
     d  d_ipri                        9  2 overlay(d_prec:47)
     d  d_kpri                        9  2 overlay(d_prec:57)
     d  d_spri                        9  2 overlay(d_prec:67)
     d  d_tdat                        6  0 overlay(d_prec:79)
     d  d_valu                        3    overlay(d_prec:86)
     d  d_ppva                       15    overlay(d_prec:91)
5.67 d**d_pean_alf                   13    overlay(d_prec:108)
6.10 d  d_pean_alf                   14    overlay(d_prec:108)
6.10 d   d_pean                      14  0 overlay(d_pean_alf:1)
      *
      * Definering av variable i nøkler
      *
     d vvenl2_vare     s                   like(vevare)
     d vvenl2_saen     s                   like(vesaen)
     d vvenl5_vare     s                   like(vevare)
     d vvprl1_vare     s                   like(vpvare)
6.20 d vvprl1_ldor     s                   like(vpldor)
6.10 d vvprl1_prgr     s                   like(vpprgr)
     d vvprl1_enhe     s                   like(vpenhe)
     d vvprl1_lety     s                   like(vplety)
     d vvprl1_pdat     s                   like(vppdat)
5.63 d vvarl1_vare     s                   like(vvvare)
     d jvarl1_vare     s                   like(jvvare)
     d jlevl1_ldor     s                   like(jlldor)
     d venhl1_eenh     s                   like(vaeenh)
     d vogrl1_ogrp     s                   like(vgoogr)
     d vhgrl1_ogrp     s                   like(vgoogr)
     d vhgrl1_hgrp     s                   like(vghhgr)
     d vugrl1_ogrp     s                   like(vgoogr)
     d vugrl1_hgrp     s                   like(vghhgr)
     d vugrl1_ugrp     s                   like(vguugr)
      *
      * Definering av variable
      *
     d b_frst          s              1    inz(*off)
     d b_inlr          s              1
     d b_ok            s              1
     d b_pris          s              1
      *
     d w_firm          s              3  0
     d w_rect          s              1
     d x               s              2  0
     d w_etel          s              2  0
     d w_ptel          s              2  0
     d w_dat6          s              6  0
     d w_dato          s                   like(vppdat)
     d w_enh1          s                   like(veenhe)
     d w_enh2          s                   like(veenhe)
     d w_enh3          s                   like(veenhe)
     d w_enh4          s                   like(veenhe)
     d w_enh5          s                   like(veenhe)
     d w_enhp          s                   like(veenhe)
     d w_enh_000       s                   like(veenhe)
     d w_omr1          s                   like(veomre)
     d w_omr2          s                   like(veomre)
     d w_omr3          s                   like(veomre)
     d w_omr4          s                   like(veomre)
     d w_omr5          s                   like(veomre)
     d w_omre          s                   like(veomre)
     d w_stel          s                   like(vesaen)
      *
     d w_gdat          s                   like(vppdat)
     d w_penh          s                   like(vpenhe)
     d b_pdup          s              1
     d w_vare          s                   like(vvvare)
     d w_sapr          s                   like(vpsapr)
     d w_kopr          s                   like(vpkopr)
     d w_inpr          s                   like(vpinpr)
     d w_bupr          s                   like(vpbupr)
     d w_stat          s              1
     d w_timestamp     s             12  0
     d w_pris          s             11  2
     d w_tell          s              6  0
     d w_dgkr          s                   like(vpsapr)
     d w_dgpr          s             11  2
8.01 d w_prgr          s              2
5.53  *
5.53  * Parametre til RS205R - hent mva %
5.53  *
5.53 d w_mvak          s              1
5.53 d w_mvtx          s             30
5.53 d w_mvas          s              6  2
5.53 d w_mvat          s              5  4
5.53 d w_mvaf          s             10  9
5.53 d w_bpro          s              5  2
5.53 d w_date          s               d   datfmt(*iso)
      *
     d digits          c                   ' 0123456789'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      b_frst = *on
     c                   eval      w_etel = 0
     c                   eval      w_ptel = 0
     c                   eval      w_enh_000 = *blank
     c                   eval      w_tell = 0
      *
     c                   exsr      hode
      *
      * Leser integrasjonsfil
      *
     c                   read      flvwpf
     c                   dow       not %eof
      *
     c                   eval      w_rect = %subst(fwirad:1:1)
      *
     c                   if        w_rect = '1' and
     c                             b_frst = *off
     c                   exsr      behandling
     c                   eval      w_enh_000 = *blank
     c                   endif
      *
     c                   select
     c                   when      w_rect = '1'
     c                   eval      d_vrec = fwirad
     c                   eval      b_frst = *off
     c                   when      w_rect = '2'
     c                   eval      w_etel = w_etel + 1
     c                   eval      a_erec(w_etel) = fwirad
     c                   if        %subst(fwirad:4:3) = '000'
     c                   eval      w_enh_000 = %subst(fwirad:10:3)
     c                   endif
     c                   when      w_rect = '3'
     c                   eval      w_ptel = w_ptel + 1
     c                   eval      a_prec(w_ptel) = fwirad
     c                   endsl
      *
     c                   read      flvwpf
     c                   enddo
      *
      * Behandler siste vare-forsendelse
      *
     c                   if        b_frst = *off and
     c                             d_vrec <> *blank
     c                   exsr      behandling
     c                   endif
      *
      *skriver antall varer
     c                   eval      t1tell = w_tell
     c                   write     t1tot                                30
     c                   exsr      overflow
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vare-forsendelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
      * Vare
      *
     c                   exsr      vare
      *
     c                   if        b_ok = *off
     c                   goto      end_behandle
     c                   endif
      *
      * Pakning
      *
     c                   if        w_etel > 0
     c                   exsr      pakning
     c                   endif
      *
      * Pris
      *
     c                   if        w_ptel > 0
     c                   exsr      pris
     c                   endif
      *
     c     end_behandle  tag
      *
     c                   eval      w_etel = 0
     c                   eval      w_ptel = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av varerecord
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vare          begsr
      *
     c                   eval      w_vare = *blank
     c                   eval      b_ok = *off
      *
     c                   if        d_nobb = 0
     c                   eval      d1feil = 'Varenr = 0 '
     c                   eval      w_stat = 'V'
     c                   exsr      feil
     c                   goto      avslutt_vare
     c                   endif
      *
      * Sjekker mulige feilsituasjoner
      *
      * sjekker om varegrupper er gyldig -  overgruppe
     c                   eval      vogrl1_ogrp = d_ogrp
     c     vogrl1_key    chain     vogrl1
     c                   if        not %found(vogrl1)
     c                   eval      d1feil = 'Feil overgruppe ' +
     c                                      %char(d_ogrp)
     c                   eval      w_stat = 'V'
     c                   exsr      feil
     c                   leavesr
     c                   endif
      * overgruppe
     c                   eval      vhgrl1_ogrp = d_ogrp
     c                   eval      vhgrl1_hgrp = d_hgrp
     c     vhgrl1_key    chain     vhgrl1
     c                   if        not %found(vhgrl1)
     c                   eval      d1feil = 'Feil hovedgruppe ' +
     c                                      %char(d_hgrp)
     c                   eval      w_stat = 'V'
     c                   exsr      feil
     c                   leavesr
     c                   endif
      * undergruppe
     c                   eval      vugrl1_ogrp = d_ogrp
     c                   eval      vugrl1_hgrp = d_hgrp
     c                   eval      vugrl1_ugrp = d_ugrp
     c     vugrl1_key    chain     vugrl1
     c                   if        not %found(vugrl1)
     c                   eval      d1feil = 'Feil undergruppe ' +
     c                                      %char(d_ugrp)
     c                   eval      w_stat = 'V'
     c                   exsr      feil
     c                   leavesr
     c                   endif
      *
5.63  * sjekker om basis-enhet er forskjellige fra evt. eksisterende
5.63  * vare
5.63  *
5.63 c                   movel     d_nobb        vvarl1_vare
5.63 c     vvarl1_key    chain     vvarl1
5.63 c                   if        %found(vvarl1)  and
     c                             vvenhe <> w_enh_000
5.63 c                   eval      d1feil = 'Feil basisenhet på fil V ' +
5.63 c                                      w_enh_000
5.63 c                   eval      w_stat = 'V'
5.63 c                   exsr      feil
5.63 c                   leavesr
5.63 c                   endif
      *
      * sjekker om basis-enhet finnes i enhetsregisteret
      *
     c                   eval      venhl1_eenh = w_enh_000
     c     venhl1_key    chain     venhl1
     c                   if        not %found(venhl1)
     c                   eval      d1feil = 'Feil basisenhet på fil E' +
     c                                      w_enh_000
     c                   eval      w_stat = 'V'
     c                   exsr      feil
     c                   leavesr
     c                   endif
      *
      * Finner reskontro leverandør
     c                   eval      jlevl1_ldor = d_ldor
     c     jlevl1_key    chain     jlevl1
     c                   if        not  %found(jlevl1)
     c                   eval      d1feil = 'Feil leverandør  ' +
     c                                      %char(d_ldor)
     c                   eval      w_stat = 'V'
     c                   exsr      feil
     c                   goto      avslutt_vare
     c                   endif
     c                   if        jlenum = 0
     c                   eval      d1feil = 'Leverandør er ikke knytttet ' +
     c                                      %char(d_ldor)
     c                   eval      w_stat = 'V'
     c                   exsr      feil
     c                   goto      avslutt_vare
     c                   endif
      *
     c                   if        d_nobb < 10000000
      * ikke nobbnr.
     c                   movel     d_nobb        w_vare
     c                   eval      d_nobb = 0
     c                   eval      d_modn = 0
      *
     c                   else
      * sannsynligvis nobb-nr
     c                   movel     d_nobb        jvarl1_vare
     c     jvarl1_key    chain     jvarl1
     c                   if        %found(jvarl1)
     c                   movel     jvnobb        w_vare
     c                   eval      d_nobb = jvnobb
     c                   eval      d_modn = jvmodn
     c                   else
     c                   movel     d_nobb        w_vare
     c                   eval      d_nobb = 0
     c                   eval      d_modn = 0
     c                   endif
     c                   endif
     c                   if        %check(digits : w_vare) <> 0
     c                   eval      d1feil = 'Ugyldig varenr.        ' +
     c                                      w_vare
     c                   eval      w_stat = 'V'
     c                   exsr      feil
     c                   goto      avslutt_vare
     c                   endif
      *
     c                   eval      b_ok = *on
      *
     c     avslutt_vare  endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av pakningsrecord
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     pakning       begsr
      *
     c                   eval      w_stel = 0
     c                   eval      x = 0
      *
     c                   do        w_etel
      *
     c                   eval      x = x + 1
     c                   eval      d_erec = a_erec(x)
      *
     c                   eval      d_antp_alf = %xlate(' ':'0':d_antp_alf)
     c                   eval      d_psei_alf = %xlate(' ':'0':d_psei_alf)
      *
      * sjekker om basis-enhet finnes i enhetsregisteret
      *
     c                   eval      venhl1_eenh = d_enhe
     c     venhl1_key    chain     venhl1
     c                   if        not %found(venhl1)
     c                   eval      d1feil = 'Ugyldig enhet          ' +
     c                                      d_enhe
     c                   eval      w_stat = 'P'
     c                   exsr      feil
     c                   leavesr
     c                   endif
5.66  *
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av prisrecord
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     pris          begsr
      *
     c                   exsr      finn_enhet
      *
     c                   eval      x = 0
      *
     c                   do        w_ptel
      *
     c                   eval      x = x + 1
     c                   eval      d_prec = a_prec(x)
      *
      * sjekker at prisen inneholder minst salgspris og kostpris
      *
     c                   if        d_spri > 0 and
     c                             d_kpri > 0
      *
      * pr. enhet
      *
      *
     c                   if        d_ppva = *blank
     c                   eval      d_ppva = d_plva
     c                   endif
      *
     c                   if        w_enh1 <> *blank
     c                   eval      w_enhp = w_enh1
     c                   eval      w_omre = w_omr1
     c                   exsr      pris_enhet
     c                   leavesr
     c                   endif
      *
     c                   if        w_enh2 <> *blank
     c                   eval      w_enhp = w_enh2
     c                   eval      w_omre = w_omr2
     c                   exsr      pris_enhet
     c                   leavesr
     c                   endif
      *
     c                   if        w_enh3 <> *blank
     c                   eval      w_enhp = w_enh3
     c                   eval      w_omre = w_omr3
     c                   exsr      pris_enhet
     c                   leavesr
     c                   endif
      *
     c                   if        w_enh4 <> *blank
     c                   eval      w_enhp = w_enh4
     c                   eval      w_omre = w_omr4
     c                   exsr      pris_enhet
     c                   leavesr
     c                   endif
      *
     c                   if        w_enh5 <> *blank
     c                   eval      w_enhp = w_enh5
     c                   eval      w_omre = w_omr5
     c                   exsr      pris_enhet
     c                   leavesr
     c                   endif
      *
     c                   endif
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle hver enhet i prisregisteret
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     pris_enhet    begsr
      *
      * sjekker om prisen kan være for høy
      * test salgsris
     c                   if        d_spri > 999999,99
     c                   eval      d1feil = 'For høy salgspris '
     c                   eval      w_stat = 'P'
     c                   exsr      feil
     c                   leavesr
     c                   endif
     c                   eval(h)   w_pris = d_spri * w_omre
     c                   if        w_pris > 999999,99
     c                   eval      d1feil = 'For høy salgspris '
     c                   eval      w_stat = 'P'
     c                   exsr      feil
     c                   leavesr
     c                   endif
      * test kostpris
     c                   if        d_kpri > 999999,99
     c                   eval      d1feil = 'For høy kostpris  '
     c                   eval      w_stat = 'P'
     c                   exsr      feil
     c                   leavesr
     c                   endif
     c                   eval(h)   w_pris = d_kpri * w_omre
     c                   if        w_pris > 999999,99
     c                   eval      d1feil = 'For høy kostpris  '
     c                   eval      w_stat = 'P'
     c                   exsr      feil
     c                   leavesr
     c                   endif
      * test innkjøpspris
     c                   if        d_ipri > 999999,99
     c                   eval      d1feil = 'For høy kostpris  '
     c                   eval      w_stat = 'P'
     c                   exsr      feil
     c                   leavesr
     c                   endif
     c                   eval(h)   w_pris = d_ipri * w_omre
     c                   if        w_pris > 999999,99
     c                   eval      d1feil = 'For høy innk.pris '
     c                   eval      w_stat = 'P'
     c                   exsr      feil
     c                   leavesr
     c                   endif
      *
     c                   eval(h)   w_sapr = d_spri * w_omre
     c                   eval      w_kopr = d_kpri * w_omre
     c                   eval      w_inpr = d_ipri * w_omre
      *
     c                   eval      b_pris = *off
      *
     c                   eval      vvprl1_vare = vvvare
6.20 c                   eval      vvprl1_ldor = vvldor
6.10 c                   eval      vvprl1_prgr = w_prgr
     c                   eval      vvprl1_enhe = w_enhp
     c                   eval      vvprl1_lety = 0
     c     vvprl1_key    setll     vvprl1
     c     vvprl1_key    reade     vvprl1                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      b_pris = *on
      *
     c                   if        w_dato >= vppdat
     c                   leave
     c                   endif
      *
     c     vvprl1_key    reade     vvprl1                                 90
     c                   enddo
      *
     c                   if        b_pris =  *on
     c                   if        vpsapr <> w_sapr  or
     c                             vpkopr <> w_kopr  or
     c                             vpinpr <> w_inpr
      * Så beregner vi dekningsgrad osv
     c                   eval      w_dgkr = w_sapr - w_kopr
     c                   if        w_kopr <> 0 and
     c                             w_sapr <> 0
     c                   eval      w_dgpr = (w_dgkr/w_sapr)*100
     c                   if        w_dgpr >= 1000
     c                   eval      w_dgpr = 999,99
     c                   endif
     c                   eval      d2dgpr = w_dgpr
     c                   else
     c                   eval      d2dgpr = 0
     c                   endif
     c                   eval      d2saim = w_sapr * w_mvat
     c                   eval      d2feil = 'Prisendring'
     c                   movel     d_tek1        d2tek1
     c                   eval      d2enhe = w_enhp
5.71 c                   movel     vvvare        d2vare
     c                   eval      d2sapr = w_sapr
     c                   eval      d2kopr = w_kopr
     c                   eval      d2inpr = w_inpr
     c                   write     d2dtl                                30
     c                   exsr      overflow
     c                   eval      d3feil = 'Gammel pris'
     c                   eval      d3saim = vpsapr * w_mvat
     c                   eval      w_dgkr = vpsapr - vpkopr
     c                   if        vpkopr <> 0 and
     c                             vpsapr <> 0
     c                   eval      w_dgpr = (w_dgkr/vpsapr)*100
     c                   if        w_dgpr >= 1000
     c                   eval      w_dgpr = 999,99
     c                   endif
     c                   eval      d3dgpr = w_dgpr
     c                   else
     c                   eval      d3dgpr = 0
     c                   endif
     c                   eval      d3sapr = vpsapr
     c                   eval      d3kopr = vpkopr
     c                   eval      d3inpr = vpinpr
     c                   write     d3dtl                                30
     c                   exsr      overflow
      * teller
     c                   if        w_Enhp = w_enh1
     c                   eval      w_tell = w_tell + 1
     c                   endif
      *
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne inntil tre salgsenheter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_enhet    begsr
      *
      * Finner tre salgsenheter på varen
      *
     c                   eval      w_enh1 = *blank
     c                   eval      w_enh2 = *blank
     c                   eval      w_enh3 = *blank
     c                   eval      w_enh4 = *blank
     c                   eval      w_enh5 = *blank
     c                   eval      w_omr1 = 0
     c                   eval      w_omr2 = 0
     c                   eval      w_omr3 = 0
     c                   eval      w_omr4 = 0
     c                   eval      w_omr5 = 0
      *
     c                   eval      vvenl2_vare = vvvare
     c                   eval      vvenl2_saen = 1
     c     vvenl2_key    setll     vvenl2
5.41 c     vvenl2_key2   reade     vvenl2
5.41 c                   dow       not %eof and
5.41 c                             vesaen > 0
      *
     c                   select
     c                   when      w_enh1 = *blank
     c                   eval      w_enh1 = veenhe
     c                   eval      w_omr1 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 = *blank
     c                   eval      w_enh2 = veenhe
     c                   eval      w_omr2 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 <> *blank and
     c                             w_enh3 = *blank
     c                   eval      w_enh3 = veenhe
     c                   eval      w_omr3 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 <> *blank and
     c                             w_enh3 <> *blank and
     c                             w_enh4 = *blank
     c                   eval      w_enh4 = veenhe
     c                   eval      w_omr4 = veomre
     c                   when      w_enh1 <> *blank and
     c                             w_enh2 <> *blank and
     c                             w_enh3 <> *blank and
     c                             w_enh4 <> *blank and
     c                             w_enh5 = *blank
     c                   eval      w_enh5 = veenhe
     c                   eval      w_omr5 = veomre
     c                   leave
     c                   endsl
      *
5.41 c     vvenl2_key2   reade     vvenl2
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å skrive ut feil-liste
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     feil          begsr
      *
      *
     c                   eval      d1stat = w_stat
     c                   eval      d1tek1 = d_tek1
     c                   movel     d_nobb        d1vare
      *
      * Skriv detaljlinje
      *
     c                   write     d1dtl                                30
     c                   exsr      overflow
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering og utskrift av hode-opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode          begsr
      *
      * Legger inn arb.stasjon og bruker i faste felter
      *
     c                   eval      a1firm = w_firm
     c                   eval      a1user = l_user
     c                   eval      a1wsid = l_wsid
     c                   time                    w_timestamp
     c                   move      w_timestamp   a1date
     c                   movel     w_timestamp   a1time
      *
      * Henter firmaopplysninger
      *
     c     afirl1_key    chain     afirl1r                            90
     c                   eval      a1fnav = aafnav
      *
      * Skriver heading
      *
     c                   write     a1hdr
     c                   write     a3hdr
      *
     c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overflow      begsr
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   write     a3hdr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *
      * Key for les på vare-enhet
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
     c                   kfld                    vvenl2_saen
      *
      * Key for les på vare-enhet
      *
     c     vvenl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl5_vare
5.41  *
5.41  * Key2 for les på vare-enhet
5.41  *
5.41 c     vvenl2_key2   klist
5.41 c                   kfld                    w_firm
5.41 c                   kfld                    vvenl2_vare
      *
      * Key for les av vare-pris
      *
     c     vvprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprl1_vare
6.20 c                   kfld                    vvprl1_ldor
6.10 c                   kfld                    vvprl1_prgr
     c                   kfld                    vvprl1_enhe
     c                   kfld                    vvprl1_lety
      *
      * Key for oppdatering av enhetsregister
      *
     c     venhl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    venhl1_eenh
      *
      * Key for les av varegrupper
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_ogrp
     c                   kfld                    vugrl1_hgrp
     c                   kfld                    vugrl1_ugrp
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_ogrp
     c                   kfld                    vhgrl1_hgrp
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_ogrp
      *
      * Key for les av vare på VA
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
5.63  *
5.63  * Key for les av vare
5.63  *
5.63 c     vvarl1_key    klist
5.63 c                   kfld                    w_firm
5.63 c                   kfld                    vvarl1_vare
      *
      * Key for les av leverandør i VA
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppslag på firmaregister
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      * Henter firma fra LDA
     c                   eval      w_firm = l_firm
     c                   time                    w_dato
      *
5.53 c                   eval      w_stat = *blank
5.53 c                   eval      w_mvak = '3'
5.53 c                   time                    w_date
5.53  *
5.53 c                   call      'RS205R'
5.53 c                   parm                    w_stat
5.53 c                   parm                    w_mvak
5.53 c                   parm                    w_mvtx
5.53 c                   parm                    w_date
5.53 c                   parm                    w_mvas
5.53 c                   parm                    w_mvat
5.53 c                   parm                    w_mvaf
5.53 c                   parm                    w_bpro
      *
6.10  * Henter prisgruppe
6.10 c                   call      'VL711R    '
6.10 c                   parm                    w_prgr
      *
     c                   endsr
