     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JR779R
      * Beskrivelse . . : Leser fil på recordformat LØVENSKIOL (Løvenskiold
      *                   Vækerø) og oppretter og oppdaterer priser på
      *                   eksisterende varer i LVR.
      *
      *                   Dersom en ny vare matcher mot EAN-nummer i LVR,
      *                   opprettes prisen som prisgivende leverandør på
      *                   varen.
      *
      *                   Prisoppdatering skjer på bakgrunn av match på
      *                   leverandørs varenummer.
      *
      *               NB! Behandler kun varer fra lager FOS
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       021202 HKO  Nytt program
      *       261103 GRO  Oppretter varer som ikke finnes i LVR
      *       140905 HKO  Byttet program for oppdatering av søktekster
6.11  *       271090 bof  Lagt inn oppd. av JVPPPF
6.12  *       081010 bof  Lagt til JVPENH til jvarlu
6.13  *       030413 bof  Ekstra test på ean-nr
6.14  *       120413 bof  Utvidet lev.varenr, ifølge Løvenskiold
6.30  * 6.30  280113 bof  Omskriving i forb. endring pakning VA (fjernet
6.30  *                   jvpppf)
6.31  * 6.30  110714 bof  endret nøkkel i varepris - dato + lvar til jvlepf
6.32  * 6.30  180515 bkv  Endret LVAR pga økt fra 15 til 20 tegn
6.33  * 6.30  120815 bof  Hvis pakn.mangler for prisg.,dann ut fra vareeier(6.31 i BMP)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjrvapf    if   e           k disk
      *
     fjrapl1    if   e           k disk    rename(jrappfr:jrapl1r)
6.31 fjvlel2    if   e           k disk    rename(jvlepfr:jvlel2r)
     fjvprl2    if   e           k disk    rename(jvprpfr:jvprl2r)
     fjvarld    if   e           k disk    rename(jvarpfr:jvarldr)
     fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
     fjvarl8    if   e           k disk    rename(jvarpfr:jvarl8r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fjvprlu    uf a e           k disk    rename(jvprpfr:jvprlur)
     fjvarlu    uf a e           k disk    rename(jvarpfr:jvarlur)
     fjvdtlu    o  a e           k disk    rename(jvdtpfr:jvdtlur)
     fjstslu    uf   e           k disk    rename(jstspfr:jstslur)
     fjvpklu    uf a e           k disk    rename(jvpkpfr:jvpklur)
6.31 fjvlelu    uf a e           k disk    rename(jvlepfr:jvlelur)
6.33 fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
6.33 fjvpkl2    uf   e           k disk    rename(jvpkpfr:jvpkl2r)
      *
      * Definering av parametre
      *
     d p_list          s             32
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av parameterliste -inn
      *
     d                 ds
     d d_list                        32
     d  d_firm                        3  0 overlay(d_list)
     d  d_rapp                        2  0 overlay(d_list:4)
     d  d_dato                         d   overlay(d_list:6) datfmt(*iso)
      *
      * Definering av recordbeskrivelse - LØVENSKIOL
      *
     d                 ds
     d d_inpu                       165
     d  d_pdat                        6  0 overlay(d_inpu:1)
     d  d_udat                        6  0 overlay(d_inpu:7)
     d**d_vare                       13    overlay(d_inpu:13)
6.14 d  d_vare                        7    overlay(d_inpu:19)
     d  d_evar                       13    overlay(d_inpu:26)
     d  d_prod                        5    overlay(d_inpu:39)
     d  d_pvar                       13    overlay(d_inpu:44)
     d    d_nobb                      8    overlay(d_pvar:1)
     d  d_eann                       13  0 overlay(d_inpu:57)
6.13 d  d_eanx                       13    overlay(d_inpu:57)
     d  d_hgrp                        2    overlay(d_inpu:70)
     d  d_ugrp                        3    overlay(d_inpu:72)
     d  d_agrp                        3    overlay(d_inpu:75)
     d  d_tek1                       30    overlay(d_inpu:78)
     d  d_stor                        8    overlay(d_inpu:108)
     d  d_enhe                        5    overlay(d_inpu:116)
     d  d_anta                        9  3 overlay(d_inpu:121)
     d  d_rkod                        4    overlay(d_inpu:130)
     d  d_inpr                        9  2 overlay(d_inpu:134)
     d  d_grpr                        9  2 overlay(d_inpu:143)
     d  d_sapr                        9  2 overlay(d_inpu:152)
     d  d_stje                        1    overlay(d_inpu:161)
     d  d_anbr                        1    overlay(d_inpu:162)
     d  d_lagr                        3    overlay(d_inpu:163)
      *
      * Definering av variable i nøkkler
      *
     d jrapl1_rapp     s                   like(jrrapp)
6.31 d jvlel2_lldo     s                   like(jvlldo)
6.31 d jvlel2_llva     s                   like(jvllva)
     d jvprl2_ldor     s                   like(jxldor)
     d jvprl2_lvar     s                   like(jxlvar)
     d jvarld_ean1     s                   like(jvean1)
     d jvarl3_nobb     s                   like(jvnobb)
     d jvarl8_ldor     s                   like(jvldor)
     d jvarl8_lvar     s                   like(jvlvar)
     d jlevl1_ldor     s                   like(jlldor)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d jvprlu_vare     s                   like(jxvare)
     d jvprlu_ldor     s                   like(jxldor)
6.31 d jvlelu_lvnr     s                   like(jvlvnr)
6.31 d jvlelu_lldo     s                   like(jvlldo)
6.33 d jvarl1_vare     s                   like(jvvare)
6.33 d jvpkl2_vare     s                   like(jwvare)
6.33 d jvpkl2_ldor     s                   like(jwldor)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_nobb          s                   like(jvnobb)
     d w_ogrp          s              2
     d w_hgrp          s              2
     d w_ugrp          s              3
6.14 d w_vare          s              7
     d w_ogrp_n        s                   like(jvogrp)
     d w_hgrp_n        s                   like(jvhgrp)
     d w_ugrp_n        s                   like(jvugrp)
      *
     d b_vare          s              1    inz(*off)
     d b_inlr          s              1
      *
      * Definering av konstanter
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
     d digits          c                   ' 0123456789'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter rapportør-informasjon
      *
     c                   eval      jrapl1_rapp = d_rapp
     c     jrapl1_key    chain     jrapl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
      * Henter leverandørinformasjon
      *
     c                   eval      jlevl1_ldor = jrldor
     c     jlevl1_key    chain     jlevl1
      *
     c                   eval      jlnavn = %trim(jlnavn)
     c     Lo:Up         xlate     jlnavn        jlnavn
      *
      * Behandling
      *
     c                   exsr      behandling
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av varer/priser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   read      jrvapf
     c                   dow       not %eof
      *
     c                   eval      d_inpu = jrvrad
      *
     c                   if        d_lagr <> 'FOS' or
     c                             d_vare = *blank or
     c                             d_vare = '000000'
     c                   goto      les_neste
     c                   endif
6.14  *
6.14  * skal takle både varenr med lengde 6 eller 7 (venstrestilt)
6.14 c                   if        %subst(d_vare:1:1) = '0'
6.14 c                   eval      w_vare = *blank
6.14 c                   eval      w_vare = %trim(%subst(d_vare:2:6))
6.14 c                   eval      d_vare = w_vare
6.14 c                   endif
      *
     c                   exsr      finn_vare
      *
      * Behandling avhengig om varen finnes fra før eller ikke
      *
     c                   if        b_vare = *off
     c                   exsr      ny_vare
      *
     c                   else
      *
6.31 c                   eval      jvlel2_lldo = jrldor
6.31 c                   eval      jvlel2_llva = jvvare
6.31 c     jvlel2_key    chain     jvlel2
      *
      * Behandling avhengig om prisen på varen finnes fra før
      *
     c                   if        %found
     c                   exsr      endr_pris
     c                   else
     c                   exsr      oppr_pris
     c                   endif
      *
     c                   endif
6.33  *
6.33  * sjekker om pakning finnes, hvis ikke dann
6.33  *
6.33 c                   exsr      sjekk_pakn
      *
     c     les_neste     tag
      *
     c                   read      jrvapf
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for les av lokalt varereg.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_vare     begsr
      *
     c                   eval      b_vare = *off
     c                   clear                   jvarlur
      *
     c     ' ':'0'       xlate     d_pvar        d_pvar
     c                   if        d_nobb <> *blank
     c     digits        check     d_nobb
     c                   if        not %found
     c                   move      d_nobb        jvarl3_nobb
     c                   move      d_nobb        w_nobb
     c                   if        w_nobb <> *zero
     c     jvarl3_key    chain     jvarl3
     c                   if        %found
     c                   eval      b_vare = *on
     c                   leavesr
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   eval      jvarl8_ldor = jrldor
     c                   eval      jvarl8_lvar = d_vare
     c     jvarl8_key    chain     jvarl8
     c                   if        %found
     c                   eval      b_vare = *on
     c                   leavesr
     c                   endif
      *
6.13 c                   if        %len(%trim(d_eanx)) = 13
     c                   eval      jvarld_ean1 = d_eann
     c     jvarld_key    chain     jvarld
     c                   if        %found
     c                   eval      b_vare = *on
     c                   leavesr
     c                   endif
6.13 c                   endif
      *
     c     end_finn      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av ny pris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppr_pris     begsr
      *
      * Sletter eventuelt eksisterende priser for leverandør på varen
      *
     c                   eval      jvprlu_vare = jvvare
     c                   eval      jvprlu_ldor = jrldor
     c     jvprlu_key    setll     jvprlu
     c     jvprlu_key    reade     jvprlur
     c                   dow       not %eof
     c                   delete    jvprlur
     c     jvprlu_key    reade     jvprlur
     c                   enddo
      *
      * Oppretter pris på vare for leverandør
      *
     c                   clear                   jvprlur
      *
     c                   eval      jxvare = jvvare
     c                   eval      jxldor = jrldor
6.32 c*                  eval      jxlvar = d_vare
6.32 c                   movel     d_vare        jxlvar
     c                   eval      jxpris = d_inpr
     c                   eval      jxvalu = *blank
     c                   eval      jxgdat = d_dato
     c                   eval      jxtdat = *loval
     c                   eval      jxrsts = 1
     c                   eval      jxeusr = l_user
      *
     c                   time                    jxodat
     c                   time                    jxedat
     c                   time                    jxetim
      *
     c                   write     jvprlur
6.31  *
6.31  * Sletter eventuelt vare-leverandør
6.31  *
6.31 c                   eval      jvlelu_lvnr = jvvare
6.31 c                   eval      jvlelu_lldo = jrldor
6.31 c     jvlelu_key    setll     jvlelu
6.31 c     jvlelu_key    reade     jvlelur
6.31 c                   dow       not %eof
6.31 c                   delete    jvlelur
6.31 c     jvlelu_key    reade     jvlelur
6.31 c                   enddo
6.31  *
6.31  * Oppretter vare leverandør
6.31  *
6.31 c                   clear                   jvlelur
6.31  *
6.31 c                   eval      jvlvnr = jvvare
6.31 c                   eval      jvlldo = jrldor
6.32 c                   movel     d_vare        jvllva
6.31 c                   eval      jvleus = l_user
6.31 c                   eval      jvlous = l_user
6.31  *
6.31 c                   time                    jvloda
6.31 c                   time                    jvleda
6.31 c                   time                    jvloti
6.31 c                   time                    jvleti
6.31 c                   write     jvlelur
      *
      * Kaller program for oppretting/oppdatering av søketekster
      *
     c                   exsr      oppd_soktxt
      *
     c     end_oppr      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for endring av pris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     endr_pris     begsr
      *
     c                   if        d_inpr = jxpris
     c                   goto      end_endr
     c                   endif
      *
     c                   eval      jvprlu_vare = jxvare
     c                   eval      jvprlu_ldor = jrldor
     c     jvprlu_key    chain     jvprlur
      *
6.32 c*                  eval      jxlvar = d_vare
6.32 c                   movel     d_vare        jxlvar
     c                   eval      jxpris = d_inpr
     c                   eval      jxgdat = d_dato
     c                   eval      jxtdat = *loval
     c                   eval      jxeusr = l_user
      *
     c                   time                    jxedat
     c                   time                    jxetim
      *
     c                   update    jvprlur
      *
     c     end_endr      endsr
6.33  *
6.33  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.33  * Subrutine for å evt. å danne pakning på prisgiver.
6.33  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.33  *
6.33 c     sjekk_pakn    begsr
6.33 c                   eval      jvarl1_vare = jxvare
6.23 c     jvarl1_key    chain     jvarl1
6.23 c                   if        not %found
6.33 c                   leavesr
6.33 c                   endif
6.33  *
6.33  * leser vare-pakning for å se at den finnes
6.33  *
6.33 c                   eval      jvpkl2_vare = jvvare
6.33 c                   eval      jvpkl2_ldor = jrldor
6.33 c     jvpkl2_key2   chain     jvpkl2
6.33 c                   if        %found
6.33 c                   leavesr
6.33 c                   endif
6.33  * leser vareeiers pakning
6.33 c                   eval      jvpkl2_vare = jvvare
6.33 c                   eval      jvpkl2_ldor = jvldor
6.33 c     jvpkl2_key2   setll     jvpkl2
6.33 c     jvpkl2_key2   reade     jvpkl2
6.33 c                   dow       not %eof(jvpkl2)
6.33 c                   eval      jwldor = jrldor
6.33 c                   eval      jwfdat = *loval
6.33 c                   eval      jwtdat = *loval
6.33 c                   time                    jwodat
6.33 c                   eval      jweusr  = l_user
6.33 c                   time                    jwedat
6.33 c                   time                    jwetim
6.33 c                   write     jvpklur
6.33  *
6.33 c     jvpkl2_key2   reade     jvpkl2
6.33 c                   enddo
6.33  *
6.33 C                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av ny vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ny_vare       begsr
      *
     c                   eval      jvstat = 2
      *
      * Henter neste ledige varenummer, og oppdaterer statusregisteret
      *
     c     jstslu_key    chain     jstslu
     c                   eval      jaavsi = jaavsi + 1
     c                   update    jstslur
      *
     c                   movel     jaavsi        jvvare
      *
      * Oppdaterer varegruppe
      *
     c                   eval      jvogrp = 0
     c                   eval      jvhgrp = 0
     c                   eval      jvugrp = 0
     c                   eval      w_ogrp = %trim(d_hgrp)
     c                   eval      w_hgrp = %trim(d_ugrp)
     c                   eval      w_ugrp = %trim(d_agrp)
     c     ' ':'0'       xlate     w_ogrp        w_ogrp
     c     ' ':'0'       xlate     w_hgrp        w_hgrp
     c     ' ':'0'       xlate     w_ugrp        w_ugrp
     c                   move      w_ogrp        w_ogrp_n
     c                   move      w_hgrp        w_hgrp_n
     c                   move      w_ugrp        w_ugrp_n
      *
     c                   eval      vugrl1_uogr = w_ogrp_n
     c                   eval      vugrl1_uhgr = w_hgrp_n
     c                   eval      vugrl1_uugr = w_ugrp_n
     c     vugrl1_key    chain     vugrl1
     c                   if        %found
     c                   eval      jvogrp = w_ogrp_n
     c                   eval      jvhgrp = w_hgrp_n
     c                   eval      jvugrp = w_ugrp_n
     c                   endif
      *
     c                   eval      jvtek1 = d_tek1
     c                   eval      jvldor = jrldor
     c                   eval      jvlvar = d_vare
6.12 c                   eval      jvpenh= %trim(d_enhe)
     c                   eval      jvprod = 0
     c                   eval      jvpvar = *blank
     c                   eval      jvean1 = d_eann
     c                   eval      jvnobb = w_nobb
     c                   eval      jveusr = l_user
      *
      *
      * Opprettelsesdato
      *
     c                   time                    jvodat
     c                   time                    jvadat
     c                   time                    jvetim
     c                   time                    jvedat
      *
     c                   write     jvarlur
      *
      * Oppretter vare i vare-detalj-register
      *
     c                   clear                   jvdtlur
     c                   eval      jvdfir = w_firm
     c                   eval      jvdvar = jvvare
     c                   eval      jvdhko = 1
     c                   write     jvdtlur
      *
      * Oppretter vare-forpakning
      *
     c                   exsr      ny_pakn
      *
      * Oppretter vare-pris
      *
     c                   exsr      ny_pris
      *
      * Kaller program for oppretting/oppdatering av søketekster
      *
     c                   exsr      oppd_soktxt
      *
     c     end_ny        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av ny vare-forpakning
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ny_pakn       begsr
      *
     c                   clear                   jvpklur
      *
     c                   eval      jwvare = jvvare
6.30 c                   eval      jwgtin = jrldor
     c                   eval      jwpakn = '000'
6.30 c                   eval      jwbest = 1
     c                   eval      jwenhe = %trim(d_enhe)
     c                   eval      jwofak = 1
     c                   eval      jwrsts = 0
     c                   eval      jweusr = l_user
      *
     c                   time                    jwodat
     c                   time                    jwedat
     c                   time                    jwetim
      *
     c                   write     jvpklur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av vare-pris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ny_pris       begsr
      *
     c                   clear                   jvprlur
      *
     c                   eval      jxvare = jvvare
     c                   eval      jxldor = jrldor
6.32 c                   movel     d_vare        jxlvar
     c                   eval      jxpris = d_inpr
     c                   eval      jxvalu = *blank
     c                   eval      jxgdat = d_dato
     c                   eval      jxtdat = *loval
     c                   eval      jxrsts = 0
     c                   eval      jxeusr = l_user
      *
     c                   time                    jxodat
     c                   time                    jxedat
     c                   time                    jxetim
      *
     c                   write     jvprlur
6.31  *
6.31  * Oppretter vare leverandør
6.31  *
6.31 c                   clear                   jvlelur
6.31  *
6.31 c                   eval      jvlvnr = jvvare
6.31 c                   eval      jvlldo = jrldor
6.32 c                   movel     d_vare        jvllva
6.31 c                   eval      jvleus = l_user
6.31 c                   eval      jvlous = l_user
6.31  *
6.31 c                   time                    jvloda
6.31 c                   time                    jvleda
6.31 c                   time                    jvloti
6.31 c                   time                    jvleti
6.31 c                   write     jvlelur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kall av prog. som oppdat. søketekster
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_soktxt   begsr
      *
     c                   eval      b_inlr = *on
      *
     c                   call      'JV790R'
     c                   parm                    w_firm
     c                   parm                    jvvare
     c                   parm                    b_inlr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for oppslag på rapportør
      *
     c     jrapl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jrapl1_rapp
      *
      * Key for oppslag på vare-pris
      *
     c     jvprl2_key    klist
     c                   kfld                    jvprl2_ldor
     c                   kfld                    jvprl2_lvar
      *
      * Key for oppdatering av vare-pris
      *
     c     jvprlu_key    klist
     c                   kfld                    jvprlu_vare
     c                   kfld                    jvprlu_ldor
      *
      * Key for oppslag på vare med EAN-nummer
      *
     c     jvarld_key    klist
     c                   kfld                    jvarld_ean1
      *
      * Key for posisjonering på Nobb-nummer
      *
     c     jvarl3_key    klist
     c                   kfld                    jvarl3_nobb
      *
      * Key for oppslag på ldor/lvar
      *
     c     jvarl8_key    klist
     c                   kfld                    jvarl8_ldor
     c                   kfld                    jvarl8_lvar
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      * Key for oppdatering av kode/status
      *
     c     jstslu_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
6.31  *
6.31  * Key for oppslag på vare-leverandør
6.31  *
6.31 c     jvlel2_key    klist
6.31 c                   kfld                    jvlel2_lldo
6.31 c                   kfld                    jvlel2_llva
6.31  *
6.31  * Key for oppdatering av vare-pris
6.31  *
6.31 c     jvlelu_key    klist
6.31 c                   kfld                    jvlelu_lvnr
6.31 c                   kfld                    jvlelu_lldo
6.33  *
6.33 c     jvarl1_key    klist
6.33 c                   kfld                    jvarl1_vare
6.33  *
6.33  * Key for les av vare-pakning
6.33  *
6.33 c     jvpkl2_key2   klist
6.33 c                   kfld                    jvpkl2_vare
6.33 c                   kfld                    jvpkl2_ldor
      *
      *
      * Fyller datastruktur fra parameter, og henter firma
      *
     c                   eval      d_list = p_list
     c                   eval      w_firm = d_firm
      *
     c                   endsr
