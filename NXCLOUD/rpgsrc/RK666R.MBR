     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK666R                                              *
      * Beskrivelse . . : Utskrift av rentenota A4 m/giro                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * R5.20     260803  Ny løsning for å kunne benytte kortere KID     KOB
      * R5.20     280803  Lagt inn rutine for styring av firmaopplysn.   KOB  *
T5.20 * V5.20     170204  Feil firma i RBUNPF når <> 0.                  HFL  *
T5.20 * V5.30     060404  Lagt ut kjøredato på rapport                   KOB  *
T5.30 * V5.30     130704  Feil ved overflow korrigert                    HFL  *
T5.30 * V5.30     150704  Ikke samsvar mellom KID på rentenota og giro.  HFL  *
5.49  * R5.49 140206 AMD  Lagt inn dummyparameter v/call til AK700R
6.10  * R6.10 010909 NHO  Endringer i fil
6.12  * R6.12 271010 NHO  Member ble ikke lagt ut i RBUN og RTRA
6.13  * R6.20 180410 NHO  Flytter blank til RTPROS
6.nu  * R6.30 140814 nho  Endring vedr kid, sett etter FO616R                 *
6.nu  * R6.20     280515  Utvidelse av kort kid                          NHO  *
      *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frkwbl2    if   e           k disk    rename(rkwbpfr:rkwbl2r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     faforl1    if   e           k disk    rename(aforpfr:aforl1r)
     fraa2l1    if   e           k disk    rename(raa2pfr:raa2l1r)
     fraa4l1    if   e           k disk    rename(raa4pfr:raa4l1r)
     fraa4lu    uf a e           k disk    rename(raa4pfr:raa4lur)
     fra04l1    if   e           k disk    rename(ra04pfr:ra04l1r)
     fra11l1    if   e           k disk    rename(ra11pfr:ra11l1r)
     fra12l1    if   e           k disk    rename(ra12pfr:ra12l1r)
     fra70l1    if   e           k disk    rename(ra70pfr:ra70l1r)
     frbunlu    uf a e           k disk    rename(rbunpfr:rbunlur)
     frtrapf    o  a e           k disk    rename(rtrapfr:rtrapfr)
     frgirpf    o  a e           k disk    rename(rgirpfr:rgirpfr)
     frk666p    o    e             printer infds(d_infds)
      *
      * Formulartekster 5 språk
      *
     d btab            s             12    dim(5)                               Bankgiro
     d ktab            s             12    dim(5)                               Kundenr.
     d dtab            s              8    dim(5)                               Dato
     d stab            s             12    dim(5)                               Side nr.
     d rtab            s             30    dim(5)                               Rentenota
     d ftab            s              8    dim(5)                               Forfall
      *
     d fo1             s             79    dim(5)                               Bankgiro ........
     d fo2             s             79    dim(5)                               Rentenota........
     d fo3             s             79    dim(5)                               I henhold til....
     d fo4             s             79    dim(5)                               foretatt for.....
     d fo5             s             79    dim(5)                               Rentebelastning .
     d fo6             s             79    dim(5)                               Faktura  Dato....
     d fo7             s             79    dim(5)                               Rentebelastning .
     d fo8             s             79    dim(5)                               Beløpet er bel...
     d fo9             s             79    dim(5)                               Fri tekstlinje...
     d fo10            s             79    dim(5)                               Fri tekstlinje...
      *
     d                 ds
     d for01                         79                                         Splitte linje 1
     d  forb                         12    overlay(for01:5)
     d  fork                         12    overlay(for01:39)
     d  ford                          8    overlay(for01:54)
     d  fors                         12    overlay(for01:65)
      *
     d                 ds
     d for02                         79                                         Splitte linje 2
     d  forr                         30    overlay(for02:22)
     d  forf                          8    overlay(for02:54)
      *
      *  LDA området
      *
     d                uds
     d  pfirm                300    302  0
     d  pnavn                303    332
     d  puser                333    342
     d  pkdat                343    348  0
     d  praar                349    352  0
     d  prper                353    354  0
     d  pavdf                355    358  0
     d  pavdt                359    362  0
     d  pkatf                363    366  0
     d  pkatt                367    370  0
     d  pself                371    374  0
     d  pselt                375    378  0
     d  pknrf                379    384  0
     d  pknrt                385    390  0
     d  kun                  391    450  0 dim(10)
     d  pknr1                391    396  0
     d  pknr2                397    402  0
     d  pknr3                403    408  0
     d  pknr4                409    414  0
     d  pknr5                415    420  0
     d  pknr6                421    426  0
     d  pknr7                427    432  0
     d  pknr8                433    438  0
     d  pknr9                439    444  0
     d  pknr0                445    450  0
     d  pfdat                451    456  0
     d  plope                457    459  0
     d  ppkod                460    460
     d  pgrns                464    465  0
     d  pgbyr                466    466
     d  pmini                467    468  0
     d  pbskr                469    469
     d  pegrn                470    470
     d  pajor                471    471
     d  ptext                472    472  0
     d  ppunr                473    473  0
     d  ppbeh                474    474
     d  palfa                475    475
     d  pdfgg                476    481  0
     d  pgeby                482    484  0
     d  ppros                485    488  2
     d  pparam               300    490
      *
     d l_ruti                800    809
     d l_hex1                760    779
     d l_hex2                780    799
     d l_sfir                845    845
     d l_alin                856    858  0
     d l_oflw                860    862  0
     d l_kidk                883    883  0
     d l_ocrk                885    885  0
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Informasjon fra printer-file
      *
     d d_infds         ds
     d  d_linnbr             367    368B 0
     d  d_pagnbr             369    372B 0
      *
      * Datastruktur for kid-feltet
      *
     d                 ds
     d d_kkid                        21
     d  d_kkus                       20    overlay(d_kkid)
     d   d_kfir                       3  0 overlay(d_kkus)
     d   d_kkod                       1  0 overlay(d_kkus:4)
6.nu d   d_ktyp                       1  0 overlay(d_kkus:5)
6.nu d   d_kled                       1  0 overlay(d_kkus:6)
6.nu d   d_kkto                       6  0 overlay(d_kkus:7)
6.nu d   d_kbil                       8  0 overlay(d_kkus:13)
     d  d_kmod                        1  0 overlay(d_kkid:21)
      *
     d akr             s              1    dim(9)
      *
     d l               s              2  0
     d n               s              2  0
     d w_linj          s              5  0
      *
     d w_firm          s                   like(rkfirm)
     d w_kund_gml      s                   like(rkkund)
     d w_kont          s              4  0
     d w_avde          s              4  0
     d w_spes          s              4  0
     d w_rent          s             11  2
6.NU d w_nota          s              8  0
     d w_aprt          s              1  0
      *
     d w_prfi          s              1  0
     d w_ffir          s              3  0
     d w_fnav          s             30
     d w_fad1          s             30
     d w_fad2          s             30
     d w_fste          s             30
     d w_fftx          s             30
     d w_tfax          s              8
     d w_ftlf          s             11
     d w_ffax          s             11
     d w_tbnk          s             10
     d w_fbgi          s             13
      *
6.nu d*w_kidr          s              6  0
6.nu d w_kidr          s              8  0
     d w_ksif          s              1  0
     d w_kkus          s             24
     d w_kkid          s             21
     d w_belØ          s             11  2
5.49 d w_kki2          s             24
610  d rbunlu_nivo     s                   like(rsnivo)
610  d rbunlu_memb     s                   like(rsmemb)
610  d w_memb          s                   like(rsmemb)
      *
     d p_stat          s              1
     d p_akod          s              2  0
     d p_atxt          s             20
      *
      * Definering av variabler i nøkler
      *
     d rkunl1_kund     s                   like(rkkund)
     d ra11l1_kkod     s                   like(rakkod)
     d ra12l1_lkod     s                   like(ralkod)
     d ra70l1_0kod     s                   like(r70kod)
     d ra70l1_0spr     s                   like(r70spr)
     d ra70l1_0lin     s                   like(r70lin)
     d rbunlu_bunt     s                   like(rsbunt)
     d aforl1_ruti     s                   like(afruti)
     d raa4l1_4aar     s                   like(ra4aar)
     d raa4lu_4aar     s                   like(ra4aar)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av formater                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede formater
      * - - - - - - - - - -
      *
      * P1GIR  - Bankgiro hode beløpet gjelder
      * P2GIR  - Bankgiro hode gebyr
      * P3GIR  - Bankgiro hode rabatt
      * P4GIR  - Bankgiro hode navn/adresse
      * P5GIR  - Bankgiro beløpslinje
      *
      *
     c/eject
      *
     c     dummy         tag
      *
     c     rkwbl2_key    setll     rkwbl2                                 60
     c     rkwbl2_key    reade     rkwbl2                                 60
      *
     c                   dow       *in60 = *off
      *
      * Brudd på kunde
      *
     c                   if        rnkund <> w_kund_gml
      *
     c                   if        w_kund_gml <> *hival
     c                   exsr      brd_kund
     c                   endif
      *
     c                   exsr      ny_kund
     c                   eval      w_kund_gml = rnkund
     c                   endif
      *
      * Klargjør detalj-linje for utskrift
      *
     c                   eval      d1biln = rnbiln
     c                   eval      d1text = rntext
     c                   eval      d1belØ = rwrgrl
      *
     c     *dmy          move      rnbdat        d1bdat
     c     *dmy          move      rwrfda        d1fdat
     c     *dmy          move      rwrtda        d1tdat
      *
     c                   eval      d1dagr = rwdagr
     c                   eval      d1rent = rwrent
      *
     c                   eval      w_rent = w_rent + d1rent
      *
      * Skrive detaljlinje
      *
     c                   write(e)  d1det
     c                   exsr      overflow
      *
     c     rkwbl2_key    reade     rkwbl2                                 60
     c                   enddo
      *
     c/space 3
      *
     c     slutt         tag
      *
      * Skriv avslutning siste kunde
      *
      * Skriv bankgiro-fil
      *
     c                   exsr      skr_giro
      *
      * Skriv postering
      *
     c                   exsr      skr_post
      *
      * Skriv slutten
      *
     c                   exsr      skr_slut
      *
      * Oppdater sist brukte rentenotanr.
      *
     c                   eval      raa4lu_4aar = praar
     c     raa4lu_key    chain     raa4lu
      *
     c                   eval      ra4sno = w_nota
      *
     c                   if        %found
     c                   update    raa4lur
     c                   else
     c                   eval      ra4fir = w_firm
     c                   eval      ra4aar = praar
     c                   write     raa4lur
     c                   endif
      *
     c                   seton                                        lr
      *
     c/eject
      *
      * Brudd på kundenummer
      *
     c     ny_kund       begsr
      *
      * Blanker felter
      *
     c                   eval      *in15 = *off
     c                   eval      w_rent = 0
      *
     c                   eval      w_nota = w_nota + 1
     c                   eval      *in79 = *off
      *
      * Les kunderegister
      *
     c                   eval      raltxt = *blank
     c                   eval      a1land = *blank
      *
     c                   eval      rkunl1_kund = rnkund
     c     rkunl1_key    chain     rkunl1                             10
      *
      *  Hent inn avd. opplysninger dersom
      *  kode for det er satt i rutineregister
      *
     c                   if        afprfi = 2
     c                   eval      w_prfi = afprfi
     c                   eval      w_ffir = w_firm
     c                   eval      w_avde = rkavde
      *
     c                   call      'RÅ707R'
     c                   parm                    w_prfi
     c                   parm                    w_ffir
     c                   parm                    w_avde
     c                   parm                    w_fnav
     c                   parm                    w_fad1
     c                   parm                    w_fad2
     c                   parm                    w_fste
     c                   parm                    w_fftx
     c                   parm                    w_tfax
     c                   parm                    w_ftlf
     c                   parm                    w_ffax
     c                   parm                    w_tbnk
     c                   parm                    w_fbgi
      *
     c                   eval      a1fnav = w_fnav
     c                   eval      a1fad1 = w_fad1
     c                   eval      a1fad2 = w_fad2
     c                   eval      a1fste = w_fste
     c                   eval      a1fftx = w_fftx
     c                   eval      a1tfax = w_tfax
     c                   eval      a1ftlf = w_ftlf
     c                   eval      a1ffax = w_ffax
     c                   eval      a1tbnk = w_tbnk
     c                   eval      a1fbgi = w_fbgi
      *
     c                   eval      a5fbgi = w_fbgi
     c                   eval      a6fbgi = w_fbgi
      *
     c                   eval      a4fnav = w_fnav
     c                   eval      a4fad1 = w_fad1
     c                   eval      a4fad2 = w_fad2
     c                   eval      a4fste = w_fste
      *
      * Skal firmanavn droppes på faktura/bankgiro ?
      *
     c                   if        w_aprt = 1
     c                   eval      a1fnav = *blank
     c                   eval      a1fad1 = *blank
     c                   eval      a1fad2 = *blank
     c                   eval      a1fste = *blank
     c                   eval      a1fftx = *blank
     c                   eval      a1tfax = *blank
     c                   eval      a1ftlf = *blank
     c                   eval      a1ffax = *blank
     c                   eval      a1tbnk = *blank
     c                   eval      a1fbgi = *blank
     c                   endif
      *
     c                   if        w_aprt = 2
     c                   eval      a4fnav = *blank
     c                   eval      a4fad1 = *blank
     c                   eval      a4fad2 = *blank
     c                   eval      a4fste = *blank
     c                   endif
      *
     c                   endif
      *
      * Klargjør kundedata for utskrift
      *
     c                   move      rkkund        a1kund
     c                   move      rknavn        a1navn
     c                   move      rkgate        a1gate
     c                   move      rkadr2        a1adr2
     c                   move      rksted        a1sted
      *
      *  Klargjør kundedata for giro-utskrift
      *
     c                   move      rkfirm        rgfirm
     c                   move      rkkund        rgkund
     c                   move      rknavn        rgnavn
     c                   move      rkgate        rggate
     c                   move      rkadr2        rgadr2
     c                   move      rksted        rgsted
     c                   move      rkpers        rgpers
     c                   move      rktlfn        rgtlfn
     c                   move      rktlfx        rgtlfx
     c                   move      rkbgir        rgbgir
     c                   move      rklagr        rglagr
     c                   move      rkslgr        rgslgr
     c                   move      rkdist        rgdist
     c                   move      rkkatg        rgkatg
     c                   move      rkbetb        rgbetb
     c                   move      rkbetm        rgbetm
     c                   move      rkfrie        rgfrie
     c                   move      rkavde        rgavde
     c                   move      rkftnr        rgftnr
      *
      * Henter avvikende rentesats fra kundekategori
      *
     c                   eval      ra11l1_kkod = rkkatg
     c     ra11l1_key    chain     ra11l1
      *
     c                   if        %found and
     c                             rakavp <> 0
     c                   eval      w_rpro= rakavp
     c                   else
     c                   eval      w_rpro= ppros
     c                   endif
      *
      * Hente landnavn og språk
      *
     c                   if        rkland <> *blank
     c                   eval      ra12l1_lkod = rkland
     c     ra12l1_key    chain     ra12l1r
      *
     c                   if        %found
     c                   movel     raltxt        a1land
     c                   endif
     c                   else
     c                   eval      ralspr = 0
     c                   eval      a1land = *blank
     c                   endif
      *
      * Klargjør linjer i topp med ledetekst
      *
     c                   if        ralspr < 1 or
     c                             ralspr > 5
     c                   move      1             ralspr
     c                   endif
      *
     c                   if        ralspr > 1
     c                   eval      *in79 = *on
     c                   endif
      *
     c                   eval      kutxt = ktab(ralspr)
     c                   eval      datxt = dtab(ralspr)
     c                   eval      sitxt = stab(ralspr)
     c                   eval      retxt = rtab(ralspr)
     c                   eval      fftxt = ftab(ralspr)
      *
     c                   movea     fo3(ralspr)   t1tl3
     c                   movea     fo4(ralspr)   t1tl4
     c                   movea     fo5(ralspr)   t1tl5
     c                   movea     fo6(ralspr)   t1tl6
     c                   movea     fo7(ralspr)   t1tl7
     c                   movea     fo8(ralspr)   t1tl8
     c                   movea     fo9(ralspr)   t1tl9
     c                   movea     fo10(ralspr)  t1tl10
      *
     c                   eval      a1side = 1
      *
      * Skrive heading av betalingsvarsel pr. kunde
      *
     c                   write(e)  a1hdr
      *
     c                   endsr
      *
     c/eject
      *
      * Subrutine for avslutning av kunde
      *
     c     brd_kund      begsr
      *
      * Skriv bankgiro-fil
      *
     c                   exsr      skr_giro
      *
      * Skriv postering
      *
     c                   exsr      skr_post
      *
      * Skriv slutten
      *
     c                   exsr      skr_slut
      *
     c                   endsr
      *
     c/eject
      *
     c     skr_giro      begsr
      *
      * Type giro R = Nota
      *
     c     *dmy          move      pkdat         rgbdat
     c     *dmy          move      pfdat         rgfdat
      *
     c                   eval      rgbiln = w_nota
      *
      * Legg ut rente
      *
     c                   eval      rgakti = 'R'
     c                   eval      rgbelØ = w_rent
      *
     c                   write     rgirpfr
      *
     c                   endsr
     c/eject
      *
      * Posterings-grunnlag
      *
     c     skr_post      begsr
      *
     c                   eval      w_linj = w_linj + 1                          Linjenr.
     c                   move      'T'           rtreid                         Rec.-id
     c                   move      w_firm        rtfirm                         Firma
     c                   move      rsbunt        rtbunt                         Bunt-nr.
     c                   move      w_linj        rtlinj                         Linjenr.
     c                   eval      rtstat = *blank                              Buntstatus
     c     *dmy          move      pkdat         rtbdat                         Bilag-dato
     c                   move      praar         rtraar                         Regn.år
     c                   movel     prper         rtrper                         Periode
     c                   move      w_nota        rtbiln                         Bilag-nr.
     c                   eval      rtdavd = *zeros                              Debet-avd.
     c                   move      rkkund        rtdkto                         Debet-kto.
     c                   eval      rtdsps = *zeros                              Debet-spes.
     c                   eval      rtdmva = *zeros                              Debet-mva.
      *
     c                   eval      rtcavd = w_avde                              Kredit-avd.
     c                   eval      rtckto = w_kont                              Kredit-kto.
     c                   eval      rtcsps = w_spes                              Kredit-spes.
     c                   eval      rtcmva = *zeros                              Kredit-mva.
6.13 c**                 eval      rtpros = *zeros                              Prosjekt
6.13 c                   eval      rtpros = *blank                              Prosjekt
     c                   eval      rtrefn = *zeros                              Referanse
     c     *dmy          move      pfdat         rtfdat                         Forfall
      *
     c                   eval      rtbelØ = w_rent                              Beløp
      *
     c                   eval      rtvalk = *blank                              Valuta-kode
      *
     c                   eval      rtakti = *blank                              Aktivitet
     c                   eval      rtmngd = *zeros                              Mengde
     c                   eval      rtmngk = *zeros                              Mengde-kode
     c                   eval      rtresn = rkkund                              Reskontronr.
     c                   eval      rtvirk = *blank                              Virksomhet
     c                   eval      rtatte = *zeros                              Attestert
     c                   eval      rtkrys = *blank                              Avkryssing
     c                   eval      rtkidi = *blank                              KID-referanse
     c                   eval      rtbilk = 97                                  Bilagskode
     c                   eval      rttext = p_atxt                              Bilagstekst
     c                   eval      rtbetb = 0                                   Betalingbet.
     c                   eval      rtbref = *blank                              Bank-referanse
6.12 c                   eval      rtmemb = w_memb
      *
     c                   write     rtrapfr
      *
     c                   endsr
      *
     c/eject
      *
      * Skriv slutten på betalingsvarsel
      *
     c     skr_slut      begsr
      *
      * Skrive totaler pr. kunde/rentenota
      *
     c                   eval      t1rent = w_rent
      *
KOB  c                   exsr      xkidd
      *
     c                   exsr      totaler
      *
     c                   eval      t1rent = 0
      *
      * Skrive bankgiro
      *
     c                   eval      a1txt1 = 'Beløpet gjelder nota'
     c                   eval      a1belØ = w_rent
     c                   eval      w_belØ = w_rent
      *
     c                   move      w_nota        a1biln
     c                   move      pfdat         a1fdat
     c                   move      pkdat         a1bdat
     c                   z-add     w_belØ        a1belØ
     c                   move      *zeros        a2rent
      *
      * Navn/Adresse
      *
     c                   move      rkkund        a4kund
     c                   move      rknavn        a4navn
     c                   move      rkgate        a4gate
     c                   move      rkadr2        a4adr2
     c                   move      rksted        a4sted
      *
      * Beregne modulus 10 på beløpsfeltet
      *
     c                   move      *blank        d_kkus
     c                   move      w_belØ        w_kkus
      *
     c                   call      'AK720R'
     c                   parm                    w_kkus
     c                   parm                    w_ksif
      *
     c                   move      w_ksif        a5bsif
      *
      * Splitte kr/øre
      *
     c                   movel     w_belØ        a5kron
     c                   move      w_belØ        a5Ører
      *
      * Fjerne foranstilte nuller
      * i et alfanummerisk felt
      *
     c                   movea     a5kron        akr
     c                   z-add     1             n
      *
     c                   dow       akr(n) = '0'
     c                   move      ' '           akr(n)
     c                   add       1             n
     c     n             cabgt     9             xsorti
     c                   enddo
      *
     c     xsorti        tag
      *
     c                   movea     akr(1)        a5kron
      *
     c                   exsr      xprint
      *
     c                   endsr
      *
     c/eject
      *
      * Subrutine for klargjøring
      *
     c     klargj        begsr
      *
      * Leser inn formulartekster
      *
     c                   eval      btab = *blank
     c                   eval      ktab = *blank
     c                   eval      dtab = *blank
     c                   eval      stab = *blank
     c                   eval      rtab = *blank
     c                   eval      ftab = *blank
      *
     c                   eval      fo1 = *blank
     c                   eval      fo2 = *blank
     c                   eval      fo3 = *blank
     c                   eval      fo4 = *blank
     c                   eval      fo5 = *blank
     c                   eval      fo6 = *blank
     c                   eval      fo7 = *blank
     c                   eval      fo8 = *blank
     c                   eval      fo9 = *blank
     c                   eval      fo10 = *blank
      *
      * Hent formulartekster fra koderegister
      * for de land som er lagt inn
      *
     c                   eval      n = 1
     c                   dou       n > 5
     c                   exsr      leslin
     c                   eval      n = n + 1
     c                   enddo
      *
      * Behandler statusrecord nr. 04
      *
     c                   eval      raa4l1_4aar = praar
     c     raa4l1_key    chain     raa4l1
     c                   if        %found
     c                   eval      w_nota = ra4sno
     c                   else
     c                   eval      w_nota = 0
     c                   endif
      *
      * Behandler faste bilagskoder
      *
     c                   move      97            p_akod
     c                   call      'RS701R'
     c                   parm                    p_stat
     c                   parm                    p_akod
     c                   parm                    p_atxt
      *
     c                   if        p_stat = 'N'
     c                   eval      p_atxt = 'Rente/purregebyr'
     c                   endif
      *
      * Hent hovedbokskonto for renter
      *
     c     ra04l1_key    chain     ra04l1
      *
     c                   if        %found
     c                   eval      w_kont = radk11
     c                   eval      w_avde = rada11
     c                   eval      w_spes = rads11
     c                   endif
      *
      *  Finn ledig buntnr. for rente-poster
      *
610  c                   eval      rbunlu_nivo = *blank
610  c                   eval      rbunlu_memb = w_memb
     c                   eval      rbunlu_bunt = 99999
     c     rbunlu_key    setgt     rbunlu
     c                   readp     rbunlu                                 61
      *
     c   61              eval      rsbunt = 1
     c  n61              eval      rsbunt = rsbunt + 1
      *
     c                   move      praar         rsraar
     c                   movel     prper         rsrper
     c                   move      w_linj        rssist
     c                   move      udate         rspdat
      *
     c                   eval      rtbelØ = 0
E5.20c                   eval      rsfirm = w_firm                              Firma
     c                   eval      rsstat = *blank
     c                   eval      rsfØrs = 1
     c                   eval      rsbsum = *zeros
     c                   eval      rspsum = *zeros
     c                   eval      rspdeb = *zeros
     c                   eval      rspkre = *zeros
      *
610  c                   eval      rsnivo = *blank
610  c                   eval      rsmemb = w_memb
610  c                   time                    rsedat
610  c                   time                    rsetim
610  c                   eval      rseusr = l_user
     c                   write     rbunlur
      *
     c                   endsr
     c/eject
      *
      * Subrutine for innlesing av formulartekster
      *
     c     leslin        begsr
      *
     c                   eval      l = 1
      *
     c                   dou       l > 10
      *
     c                   eval      ra70l1_0kod = 73
     c                   eval      ra70l1_0spr = n
     c                   eval      ra70l1_0lin = l
     c     ra70l1_key    chain     ra70l1r
      *
     c                   if        %found
     c                   select
      *
     c                   when      l = 1
     c                   move      r70ltx        fo1(r70spr)
     c                   move      fo1(r70spr)   for01
     c                   move      forb          btab(r70spr)
     c                   move      fork          ktab(r70spr)
     c                   move      ford          dtab(r70spr)
     c                   move      fors          stab(r70spr)
      *
     c                   when      l = 2
     c                   move      r70ltx        fo2(r70spr)
     c                   move      fo2(r70spr)   for02
     c                   move      forr          rtab(r70spr)
     c                   move      forf          ftab(r70spr)
      *
     c                   when      l = 3
     c                   move      r70ltx        fo3(r70spr)
      *
     c                   when      l = 4
     c                   move      r70ltx        fo4(r70spr)
      *
     c                   when      l = 5
     c                   move      r70ltx        fo5(r70spr)
      *
     c                   when      l = 6
     c                   move      r70ltx        fo6(r70spr)
      *
     c                   when      l = 7
     c                   move      r70ltx        fo7(r70spr)
      *
     c                   when      l = 8
     c                   move      r70ltx        fo8(r70spr)
      *
     c                   when      l = 9
     c                   move      r70ltx        fo9(r70spr)
      *
     c                   when      l = 10
     c                   move      r70ltx        fo10(r70spr)
      *
     c                   endsl
     c                   endif
      *
     c                   eval      l = l + 1
     c                   enddo
      *
     c                   endsr
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * XKIDD Klargjør kidd                                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xkidd         begsr
      *
      * Løsning for å kunne benytte kortere kidd-nummer,
      *
      * Bygger opp kiddfelt (lang versjon)
      *
     c                   eval      d_kfir = w_firm
     c                   eval      d_kkod = 2                                   Rentenota i kid
     c                   eval      d_kled = *zero
6.nu c                   eval      d_ktyp = *zero
     c                   eval      d_kkto = rkkund
     c                   eval      d_kbil = w_nota
     c                   eval      w_kkus = d_kkus
     c                   eval      w_ksif = *zero
      *
      * Beregning av sjekksiffer på kiddfelt (lang versjon)
      *
     c                   call      'AK720R'
     c                   parm                    w_kkus
     c                   parm                    w_ksif
      *
     c                   eval      d_kmod = w_ksif
     c                   eval      w_kkid = d_kkid
      *
      * Oppdatering av kidd-referanse-fil
      *
     c                   select
     c                   when      l_kidk = 0
     c                   move      *blank        a5kkid
T5.30c                   eval      w_kkid = *blanks
      *
     c                   when      l_kidk = 1
     c                   eval      a5kkid = %trim(w_kkid)
      *
     c                   when      l_kidk = 2
     c                   call      'AK700R'
     c                   parm                    w_firm
     c                   parm                    w_kkid
     c                   parm                    w_kidr
     c                   parm                    w_ksif
5.49 c                   parm                    w_kki2
      *
     c                   move      *blank        d_kkid
     c                   move      w_kidr        d_kkus
     c                   move      w_ksif        d_kmod
     c                   eval      w_kkid = d_kkid
     c                   eval      a5kkid = %trim(w_kkid)
      *
     c                   endsl
      *
     c                   endsr
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * XPRINT Skriver bankgiro                                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xprint        begsr
      *
      * Skriv beløpet gjelder
      *
     c                   write(e)  p1gir
      *
      * Skriv gebyrlinje
      *
     c                   if        a2rent > *zero
     c                   write(e)  p2gir
     c                   endif
      *
      * Skriv rabattlinje
      *
     c                   if        a3rabb > *zero
     c                   write(e)  p3gir
     c                   endif
      *
      * Skriv firmanavn
      *
     c                   write(e)  p4gir
      *
      * Skrive giro H-linje dersom ikke OCR
      *
     c                   if        l_ocrk = 0
     c                   write(e)  p5gir
     c                   endif
      *
      * Skrive giro H-linje dersom OCR og 4224
      *
     c                   if        l_ocrk = 1
     c                   write(e)  p6gir
     c                   endif
      *
      * Skrive giro H-linje dersom OCR og Axix-boks
      *
     c                   if        l_ocrk = 2
     c                   move      l_hex1        p1hex1
     c                   move      l_hex2        p1hex2
     c                   write(e)  p7gir
     c                   endif
      *
      * Skrive bankgiro H-linje dersom 5224 skriver
      *
     c                   if        l_ocrk = 3
     c                   write(e)  p8gir
     c                   endif
      *
      * Skrive bankgiro H-linje dersom OCR og Nett-print
      *
     c                   if        l_ocrk = 4
     c                   move      l_hex1        p1hex1
     c                   move      l_hex2        p1hex2
     c                   write(e)  p9gir
     c                   endif
      *
     c                   endsr
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for utskrift av totaler                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     totaler       begsr
      *
     c                   write(e)  t1tot
     c                   exsr      overflow
      *
     c                   write(e)  t2tot
     c                   exsr      overflow
      *
     c                   write(e)  t3tot
     c                   exsr      overflow
      *
     c                   write(e)  t4tot
     c                   exsr      overflow
      *
T5.30c                   if        a5kkid <> *blanks
     c                   write(e)  t5tot
     c                   exsr      overflow
T5.30c                   endif
      *
     c                   endsr
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for kontroll av overflow                              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     overflow      begsr
      *
      * Er det overflow ?
      *
     c                   if        %error = '1'
     c                   eval      a1side = a1side + 1
     c                   write(e)  a1hdr
     c                   endif
      *
     c                   endsr
      *
     c/space 3
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
6.12 c     *entry        plist
6.12 c                   parm                    w_memb
6.12  *
     c                   eval      w_kund_gml = *hival
      *
     c                   eval      rgtext = 'Rente/purregebyr'
      *
      * Key for transaksjonsregister
      *
     c     rkwbl2_key    klist
     c                   kfld                    w_firm
      *
      * Key for kunderegister
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for rutine-register
      *
     c     aforl1_key    klist
     c                   kfld                    aforl1_ruti
      *
      * Key for statuskode 4
      *
     c     raa4l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    raa4l1_4aar
      *
      * Key for statuskode 4
      *
     c     raa4lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    raa4lu_4aar
      *
      * Key for faste konti
      *
     c     ra04l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for kundekategori
      *
     c     ra11l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra11l1_kkod
      *
      * Key for landkoder
      *
     c     ra12l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra12l1_lkod
      *
      * Key for formular-register
      *
     c     ra70l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra70l1_0kod
     c                   kfld                    ra70l1_0spr
     c                   kfld                    ra70l1_0lin
      *
      * Key for buntregister
      *
     c     rbunlu_key    klist
610  c                   kfld                    rbunlu_nivo
610  c                   kfld                    rbunlu_memb
     c                   kfld                    w_firm
     c                   kfld                    rbunlu_bunt
      *
     c                   eval      w_firm = l_firm
      *
      * Hente inn formular-register
      *
     c                   eval      aforl1_ruti = l_ruti
     c     aforl1_key    chain     aforl1r
      *
     c                   if        not%found
     c                   eval      afinst = 1
     c                   eval      afprfi = 1
     c                   endif
      *
      * Instillinger av blankett
      *
     c                   if        afinst = 1
E5.30c                   write     a0hdr                                88
T5.30c*                  exsr      overflow
     c                   endif
      *
      *  Hent inn firma opplysninger dersom
      *  kode for det er satt i rutineregister
      *
      *  NB NB NB
      *  Når parameteret w_aprt er tilgjengelig via rutineregisteret
      *  må koden nedenfor tilpasses
      *
     c                   if        afprfi = 1 or
NB   c                             afprfi = 0
     c                   eval      w_prfi = afprfi
     c                   eval      w_ffir = w_firm
     c                   eval      w_avde = rkavde
      *
     c                   call      'RÅ707R'
     c                   parm                    w_prfi
     c                   parm                    w_ffir
     c                   parm                    w_avde
     c                   parm                    w_fnav
     c                   parm                    w_fad1
     c                   parm                    w_fad2
     c                   parm                    w_fste
     c                   parm                    w_fftx
     c                   parm                    w_tfax
     c                   parm                    w_ftlf
     c                   parm                    w_ffax
     c                   parm                    w_tbnk
     c                   parm                    w_fbgi
      *
NB   c                   if        afprfi = 1
     c                   eval      a1fnav = w_fnav
     c                   eval      a1fad1 = w_fad1
     c                   eval      a1fad2 = w_fad2
     c                   eval      a1fste = w_fste
     c                   eval      a1fftx = w_fftx
     c                   eval      a1tfax = w_tfax
     c                   eval      a1ftlf = w_ftlf
     c                   eval      a1ffax = w_ffax
     c                   eval      a1tbnk = w_tbnk
     c                   eval      a1fbgi = w_fbgi
      *
     c                   eval      a5fbgi = w_fbgi
     c                   eval      a6fbgi = w_fbgi
      *
     c                   eval      a4fnav = w_fnav
     c                   eval      a4fad1 = w_fad1
     c                   eval      a4fad2 = w_fad2
     c                   eval      a4fste = w_fste
NB   c                   endif
      *
      * Skal firmanavn droppes på faktura/bankgiro ?
      *
     c                   if        w_aprt = 1
     c                   eval      a1fnav = *blank
     c                   eval      a1fad1 = *blank
     c                   eval      a1fad2 = *blank
     c                   eval      a1fste = *blank
     c                   eval      a1fftx = *blank
     c                   eval      a1tfax = *blank
     c                   eval      a1ftlf = *blank
     c                   eval      a1ffax = *blank
     c                   eval      a1tbnk = *blank
     c                   eval      a1fbgi = *blank
     c                   endif
      *
     c                   if        w_aprt = 2
     c                   eval      a4fnav = *blank
     c                   eval      a4fad1 = *blank
     c                   eval      a4fad2 = *blank
     c                   eval      a4fste = *blank
     c                   endif
      *
     c                   endif
      *
     c                   eval      a1dato = pkdat
      *
     c                   exsr      klargj
      *
     c                   endsr
