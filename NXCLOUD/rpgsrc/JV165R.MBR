     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JV165R
      * Beskrivelse . . : Vare, registrering fra ordre - kalkulasjon
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       010604 HKO  Nytt program
      *       150905 HKO  Lagt inn valgt leveringstype samt varetype og
      *                   leveringstype for betingelsesnivå og
      *                   påslagsnivå som input-parameter
      *                   Endret parameterliste ved kall av JV751R
5.61  * R5.61 180608 AMD  Lagt inn manglende dato i parameterliste
6.21  * R6.20 300614 bof  Endringer vedr. kalkyle fra nobb-pris
6.3x  * R6.20 250915 bof  Utvidet med prisgruppe
6.31  * R6.20 180416 bof  Utvidet med parm til jv751r
6.32  * R6.20 200921 bof  Hvis endring av i-fak og påsl salgspr=nobbpris
6.32  *                   ikke endre salgspris
7.01  * K000  200921 bof  Nytt parm i JX500R, tar med dato
8.01  *PRG2   140622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
6.21 fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
      *
     fjv165d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_vare          s                   like(jvvare)
8.01 d p_prgr          s              2
     d p_lety          s              1  0
     d p_kpri          s              1
     d p_enhe          s              3
     d p_enh1          s              3
     d p_enh2          s              3
     d p_enh3          s              3
     d p_ompr          s             12  6
     d p_omp1          s             12  6
     d p_omp2          s             12  6
     d p_omp3          s             12  6
     d p_omv2          s             12  6
     d p_omv3          s             12  6
     d p_ldor          s                   like(jvldor)
     d p_lnav          s             35
     d p_lvar          s                   like(jvlvar)
     d p_eldo          s              6  0
     d p_grpr          s              9  2
     d p_gdat          s               d   datfmt(*iso)
     d p_inpr          s              9  2
     d p_kopr          s              9  2
     d p_sapr          s              9  2
     d p_bldo          s                   like(jvldor)
     d p_bogr          s                   like(jvogrp)
     d p_bhgr          s                   like(jvhgrp)
     d p_bugr          s                   like(jvugrp)
     d p_bmod          s                   like(jvmodn)
     d p_bvar          s                   like(jvvare)
     d p_bvty          s              1
     d p_blet          s              1  0
     d p_bbet          s             35
     d p_ntpr          s              9  2
     d p_ifak          s              8  6
     d p_pogr          s                   like(jvogrp)
     d p_phgr          s                   like(jvhgrp)
     d p_pugr          s                   like(jvugrp)
     d p_pldo          s                   like(jvldor)
     d p_pmod          s                   like(jvmodn)
     d p_pvar          s                   like(jvvare)
     d p_pkpr          s              1
     d p_pvty          s              1
     d p_plet          s              1  0
     d p_kfak          s              8  6
     d p_kbel          s              7  2
     d p_sfak          s              8  6
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av variabler i nøkler
      *
     d jvarl1_vare     s                   like(jvvare)
6.21 d jlevl1_ldor     s                   like(jlldor)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_gdat          s               d   datfmt(*iso)
     d w_enh1          s              3
     d w_enh2          s              3
     d w_enh3          s              3
     d w_ompr          s             12  6
     d w_omp1          s             12  6
     d w_omp2          s             12  6
     d w_omp3          s             12  6
     d w_omv2          s             12  6
     d w_omv3          s             12  6
     d w_osts          s              1  0
     d w_ntpr          s              9  2
     d w_grpr          s              9  2
     d w_dekn          s              5  2
5.61 d w_odat          s               d   datfmt(*iso)
6.31 d w_prut          s              1
6.31 d w_pgbe          s              1
6.31 d w_pgfr          s              1
6.31 d w_ffak          s              8  6
6.31 d w_inp2          s              9  2
6.32 d w_sano          s              1  0
6.32 d w_nobb          s              1  0
6.32 d b_avik          s              1    inz(*off)
      *
     d w_bvar          s                   like(jvvare)
     d w_pvar          s                   like(jvvare)
7.01 d w_xdato         s               d   datfmt(*iso)
7.01 d w_dato          s               d   datfmt(*iso)
      *
     d s_ifak          s              8  6
     d s_kbel          s              9  2
     d s_kfak          s              8  6
     d s_sfak          s              8  6
     d s_ntpr          s              9  2
     d s_inpr          s              9  2
      *
     d s_ldor          s                   like(jvldor)
      *
      *
6.32 C/Exec SQL
6.32 C+  Set Option DatFmt=*ISO
6.32 C/End-Exec
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter vareinformasjon
      *
     c                   eval      jvarl1_vare = p_vare
     c     jvarl1_key    chain     jvarl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
      * Viser registreringbildet
      *
     c                   exsr      xc1win
      *
     c                   if        *inkc or *inkl
     c                   goto      avslutt
     c                   endif
      *
      * Returnerer valgte/kalkulerte verdier som paramerte
      *
     c                   eval      p_ldor = c1ldor
     c                   eval      p_lnav = c1lnav
     c                   eval      p_lvar = c1lvar
     c                   eval      p_eldo = c1enum
     c                   eval      p_grpr = c1grpr
     c                   eval      p_gdat = *loval
     c                   eval      p_inpr = c1inpr
     c                   eval      p_kopr = c1kopr
     c                   eval      p_sapr = c1sapr
     c                   eval      p_bldo = c1bldo
     c                   eval      p_bogr = c1bogr
     c                   eval      p_bhgr = c1bhgr
     c                   eval      p_bugr = c1bugr
     c                   eval      p_bmod = c1bmod
     c                   eval      p_bvar = c1bvar
     c                   eval      p_bvty = c1bvty
     c                   eval      p_blet = c1blet
     c                   eval      p_bbet = c1bbet
     c                   eval      p_ntpr = c1ntpr
     c                   eval      p_ifak = c1ifak
     c                   eval      p_pogr = c1pogr
     c                   eval      p_phgr = c1phgr
     c                   eval      p_pugr = c1pugr
     c                   eval      p_pldo = c1pldo
     c                   eval      p_pmod = c1pmod
     c                   eval      p_pvar = c1pvar
     c                   eval      p_pkpr = c1pkpr
     c                   eval      p_pvty = c1pvty
     c                   eval      p_plet = c1plet
     c                   eval      p_kfak = c1kfak
     c                   eval      p_kbel = c1kbel
     c                   eval      p_sfak = c1sfak
      *
     c                   if        c1gdat <> 0
     c     *dmy          move      c1gdat        p_gdat
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * C1 Behandle bilde for kalkulering av priser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1win        begsr
      *
      * Initierer skjermbilde
      *
     c                   eval      w_enh1 = p_enh1
     c                   eval      w_enh2 = p_enh2
     c                   eval      w_enh3 = p_enh3
     c                   eval      w_ompr = p_ompr
     c                   eval      w_omp1 = p_omp1
     c                   eval      w_omp2 = p_omp2
     c                   eval      w_omp3 = p_omp3
     c                   eval      w_omv2 = p_omv2
     c                   eval      w_omv3 = p_omv3
      *
     c                   eval      c1vare = p_vare
     c                   eval      c1kpri = p_kpri
     c                   eval      c1enhe = p_enhe
     c                   eval      c1ldor = p_ldor
     c                   eval      c1lnav = p_lnav
     c                   eval      c1lvar = p_lvar
     c                   eval      c1enum = p_eldo
     c                   eval      c1grpr = p_grpr
     c                   eval      c1gdat = 0
     c                   eval      c1inpr = p_inpr
     c                   eval      c1kopr = p_kopr
     c                   eval      c1sapr = p_sapr
     c                   eval      c1bldo = p_bldo
     c                   eval      c1bogr = p_bogr
     c                   eval      c1bhgr = p_bhgr
     c                   eval      c1bugr = p_bugr
     c                   eval      c1bmod = p_bmod
     c                   eval      c1bvar = p_bvar
     c                   eval      c1bvty = p_bvty
     c                   eval      c1blet = p_blet
     c                   eval      c1bbet = p_bbet
     c                   eval      c1ntpr = p_ntpr
     c                   eval      c1ifak = p_ifak
     c                   eval      c1pogr = p_pogr
     c                   eval      c1phgr = p_phgr
     c                   eval      c1pugr = p_pugr
     c                   eval      c1pldo = p_pldo
     c                   eval      c1pmod = p_pmod
     c                   eval      c1pvar = p_pvar
     c                   eval      c1pkpr = p_pkpr
     c                   eval      c1pvty = p_pvty
     c                   eval      c1plet = p_plet
     c                   eval      c1kfak = p_kfak
     c                   eval      c1kbel = p_kbel
     c                   eval      c1sfak = p_sfak
      *
     c                   eval      c1nobb = jvnobb
     c                   eval      c1modn = jvmodn
      *
     c                   if        p_gdat <> *loval
     c     *dmy          move      p_gdat        c1gdat
     c                   endif
      *
     c     c1taga        tag
      *
      * Tar vare på gamle verdier
      *
     c                   eval      s_ldor = c1ldor
      *
     c                   eval      s_ntpr = c1ntpr
     c                   eval      s_ifak = c1ifak
     c                   eval      s_kfak = c1kfak
     c                   eval      s_kbel = c1kbel
     c                   eval      s_inpr = c1inpr
     c                   eval      s_sfak = c1sfak
      *
      * Viser bildet
      *
     c     c1tagb        tag
      *
     c                   exfmt     c1win
      *
      * F3=Gå tilbake
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * F13=Velg prisgivende leverandør
      *
     c                   if        *inkm = *on
     c                   exsr      prisgiver
     c                   goto      c1tagb
     c                   endif
      *
      * F14=Velg betingelse
      *
     c                   if        *inkn = *on
     c                   exsr      betingelse
     c                   goto      c1tagb
     c                   endif
      *
      * Sjekk kostpris
      *
     c                   if        c1kopr = 0 and
     c                             c1inpr = 0
     c                   eval      *in38 = *on
     c                   goto      c1tagb
     c                   endif
      *
      * Kalkulerer nye priser eller faktorer ved endring av priser eller
      * faktorer
      * Bildet vises på nytt
      *
     c                   if        c1ntpr <> s_ntpr or
     c                             c1ifak <> s_ifak or
     c                             c1kfak <> s_kfak or
     c                             c1kbel <> s_kbel or
     c                             c1sfak <> s_sfak
6.32 c                   exsr      sjekk_pasl
     c                   exsr      kalk_priser
     c                   goto      c1tagb
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Velger prisgivende leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     prisgiver     begsr
      *
7.01 c                   eval      w_xdato = *loval
      *
     c                   call      'JX500R'
     c                   parm                    jvvare
     c                   parm                    c1ldor
     c                   parm                    c1lnav
     c                   parm                    w_grpr
7.01 c                   parm                    w_xdato
      *
     c                   if        c1ldor <> s_ldor
     c                   exsr      hent_grunnlag
     c                   exsr      kalk_priser
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av grunnlagsinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_grunnlag begsr
      *
7.01 c                   eval      w_dato = w_odat
7.01 c                   if        w_xdato <> *loval
7.01 c                   eval      w_dato = w_xdato
7.01 c                   endif
      * Henter pris og vareinformasjon, og initierer skjermbildet
      *
     c                   call      'JV751R'
     c                   parm                    w_firm
     c                   parm                    jvvare
     c                   parm                    p_prgr
     c                   parm                    p_lety
7.01 c                   parm                    w_dato
     c                   parm                    w_osts
     c                   parm                    c1ldor
     c                   parm                    c1lnav
     c                   parm                    c1lvar
     c                   parm                    c1enum
     c                   parm                    c1grpr
     c                   parm                    w_gdat
     c                   parm                    w_enh1
     c                   parm                    w_enh2
     c                   parm                    w_enh3
     c                   parm                    w_omp1
     c                   parm                    w_omp2
     c                   parm                    w_omp3
     c                   parm                    w_omv2
     c                   parm                    w_omv3
     c                   parm                    c1kpri
     c                   parm                    c1bldo
     c                   parm                    c1bogr
     c                   parm                    c1bhgr
     c                   parm                    c1bugr
     c                   parm                    c1bmod
     c                   parm                    w_bvar
     c                   parm                    c1bvty
     c                   parm                    c1blet
     c                   parm                    c1bbet
     c                   parm                    c1ntpr
     c                   parm                    c1ifak
     c                   parm                    c1pogr
     c                   parm                    c1phgr
     c                   parm                    c1pugr
     c                   parm                    c1pldo
     c                   parm                    c1pmod
     c                   parm                    w_pvar
     c                   parm                    c1pkpr
     c                   parm                    c1pvty
     c                   parm                    c1plet
     c                   parm                    c1kfak
     c                   parm                    c1kbel
     c                   parm                    c1sfak
     c                   parm                    c1inpr
     c                   parm                    c1kopr
     c                   parm                    c1sapr
     c                   parm                    w_dekn
6.31 c                   parm                    w_prut
6.31 c                   parm                    w_pgbe
6.31 c                   parm                    w_pgfr
6.31 c                   parm                    w_ffak
6.31 c                   parm                    w_inp2
      *
     c                   eval      c1bvar = w_bvar
     c                   eval      c1pvar = w_pvar
      *
     c                   eval      c1gdat = 0
     c                   if        w_gdat <> *loval
     c     *dmy          move      w_gdat        c1gdat
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter ny betingelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     betingelse    begsr
      *
     c                   call      'JP505R'
     c                   parm                    w_firm
     c                   parm                    c1ldor
6.3x c                   parm                    p_prgr
     c                   parm                    jvvare
     c                   parm                    c1bldo
     c                   parm                    c1bogr
     c                   parm                    c1bhgr
     c                   parm                    c1bugr
     c                   parm                    c1bmod
     c                   parm                    w_bvar
     c                   parm                    c1bbet
     c                   parm                    w_ntpr
     c                   parm                    c1ifak
      *
     c                   eval      c1bvar = w_bvar
      *
     c                   select
     c                   when      c1enhe = w_enh1
     c                   eval      c1ntpr = w_ntpr
     c                   when      c1enhe = w_enh2
     c                   eval      c1ntpr = w_ntpr * w_omv2
     c                   when      c1enhe = w_enh3
     c                   eval      c1ntpr = w_ntpr * w_omv3
     c                   endsl
      *
      * Kalkulerer ny salgspris
      *
     c                   exsr      kalk_priser
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kalkulasjon salgspris og faktorer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kalk_priser   begsr
      *
      * Finner omregningsfaktor pris for enhet
      *
     c                   select
     c                   when      c1enhe = w_enh1
     c                   eval      w_ompr = w_omp1
     c                   when      c1enhe = w_enh2
     c                   eval(h)   w_ompr = w_omp2 / w_omv2
     c                   when      c1enhe = w_enh3
     c                   eval(h)   w_ompr = w_omp3 / w_omv3
     c                   endsl
      *
      * Nettopris/innkjøpspris-faktor
      *
     c                   if        c1ntpr <> 0
     c                   eval      c1inpr = c1ntpr
     c                   else
     c                   eval(h)   c1inpr = c1grpr * c1ifak
     c                   endif
      *
     c                   if        c1inpr = 0
     c                   eval      c1inpr = c1kopr
     c                   endif
      *
      * Endret kostpris
      *
     c                   if        c1inpr <> s_inpr or
     c                             c1kfak <> s_kfak or
     c                             c1kbel <> s_kbel
     c                   eval(h)   c1kopr = (c1inpr * c1kfak) + c1kbel
     c                   endif
6.21  *
6.21 c                   eval      jlevl1_ldor = c1ldor
6.21 c     jlevl1_key    chain     jlevl1
      *
      * Kalkulerer ny salgspris dersom grunnlaget for denne er endret
      *
     c                   if        c1ntpr <> s_ntpr or
     c                             c1ifak <> s_ifak or
     c                             c1kfak <> s_kfak or
     c                             c1kbel <> s_kbel
6.32 c                   if        w_sano = 1
6.32 c                   eval      c1sapr = c1grpr * c1sfak * w_ompr
6.32 c                   else
6.32 c                   if        jlkkal = 1 or w_nobb = 1
6.21 c                   eval(h)   c1sapr = c1grpr * c1sfak * w_ompr
6.21 c                   else
     c                   eval(h)   c1sapr = c1kopr * c1sfak * w_ompr
6.21 c                   endif
     c                   endif
6.32 c                   endif
      *
      * Endring av salgsprisfaktor
      *
     c                   select
     c                   when      c1sfak <> s_sfak
6.32 c                   if        w_sano = 1
6.32 c                   eval      c1sapr = c1grpr * w_ompr
6.32 c                   else
6.32 c                   if        jlkkal = 1 or w_nobb = 1
6.21 c                   eval(h)   c1sapr = c1grpr * c1sfak * w_ompr
6.21 c                   else
     c                   eval(h)   c1sapr = c1kopr * c1sfak * w_ompr
6.21 c                   endif
6.21 c                   endif
     c                   when      c1sapr = 0
6.32 c                   if        w_sano = 1
6.32 c                   eval      c1sapr = c1grpr * w_ompr
6.32 c                   else
6.32 c                   if        jlkkal = 1 or w_nobb = 1
6.21 c                   eval(h)   c1sapr = c1grpr * c1sfak * w_ompr
6.21 c                   else
     c                   eval(h)   c1sapr = c1kopr * c1sfak * w_ompr
6.21 c                   endif
6.32 c                   endif
     c                   endsl
      *
      * Tar vare på verdier i endrede felter
      *
     c                   eval      s_ntpr = c1ntpr
     c                   eval      s_ifak = c1ifak
     c                   eval      s_kfak = c1kfak
     c                   eval      s_kbel = c1kbel
     c                   eval      s_sfak = c1sfak
     c                   eval      s_inpr = c1inpr
      *
     c                   endsr
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  * Subrutine for å sjekke om påslag er salgspris = nobbpris
6.32  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.32  *
6.32 c     sjekk_pasl    begsr
6.32  * sjekk først m/prisgruppe
6.32 c/exec sql
6.32 c+ select jcnobb, jcsano
6.32 c+ into   :w_nobb, :w_sano
6.32 c+ from   jpfapf
6.32 c+ where  jcprgr = :p_prgr
6.32 c+    and jcogrp = :p_pogr
6.32 c+    and jchgrp = :p_phgr
6.32 c+    and jcugrp = :p_pugr
6.32 c+    and jcldor = :p_pldo
6.32 c+    and jcmodn = :p_pmod
6.32 c+    and jcvare = :p_pvar
6.32 c+    and jckpri = :p_pkpr
6.32 c+    and jcvtyp = :p_pvty
6.32 c+    and jclety = :p_plet
6.32 c+    fetch first 1 rows only
6.32 c/end-exec
6.32 c                   if        sqlcod = 0
6.32 c                   goto      pasl_funn
6.32 c                   endif
6.32  *
6.32 c/exec sql
6.32 c+ select jcnobb, jcsano
6.32 c+ into   :w_nobb, :w_sano
6.32 c+ from   jpfapf
6.32 c+ where  jcogrp = :p_pogr
6.32 c+    and jchgrp = :p_phgr
6.32 c+    and jcugrp = :p_pugr
6.32 c+    and jcldor = :p_pldo
6.32 c+    and jcmodn = :p_pmod
6.32 c+    and jcvare = :p_pvar
6.32 c+    and jckpri = :p_pkpr
6.32 c+    and jcvtyp = :p_pvty
6.32 c+    and jclety = :p_plet
6.32 c+    fetch first 1 rows only
6.32 c/end-exec
6.32 c                   if        sqlcod = 100 or sqlstt = '02000'
6.32 c                   leavesr
6.32 c                   endif
6.32 c                   if        sqlcod <> 0
6.32 c                   leavesr
6.32 c                   endif
6.32  *
6.32 c     pasl_funn     tag
6.32 c                   if        w_sano = 1 or
     c                             w_nobb = 1
6.32 c                   eval      b_avik = *on
6.32 c                   endif
6.32  *
6.32 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_prgr
     c                   parm                    p_lety
     c                   parm                    p_kpri
     c                   parm                    p_enhe
     c                   parm                    p_enh1
     c                   parm                    p_enh2
     c                   parm                    p_enh3
     c                   parm                    p_ompr
     c                   parm                    p_omp1
     c                   parm                    p_omp2
     c                   parm                    p_omp3
     c                   parm                    p_omv2
     c                   parm                    p_omv3
     c                   parm                    p_ldor
     c                   parm                    p_lnav
     c                   parm                    p_lvar
     c                   parm                    p_eldo
     c                   parm                    p_grpr
     c                   parm                    p_gdat
     c                   parm                    p_inpr
     c                   parm                    p_kopr
     c                   parm                    p_sapr
     c                   parm                    p_bldo
     c                   parm                    p_bogr
     c                   parm                    p_bhgr
     c                   parm                    p_bugr
     c                   parm                    p_bmod
     c                   parm                    p_bvar
     c                   parm                    p_bvty
     c                   parm                    p_blet
     c                   parm                    p_bbet
     c                   parm                    p_ntpr
     c                   parm                    p_ifak
     c                   parm                    p_pogr
     c                   parm                    p_phgr
     c                   parm                    p_pugr
     c                   parm                    p_pldo
     c                   parm                    p_pmod
     c                   parm                    p_pvar
     c                   parm                    p_pkpr
     c                   parm                    p_pvty
     c                   parm                    p_plet
     c                   parm                    p_kfak
     c                   parm                    p_kbel
     c                   parm                    p_sfak
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      *
      *
      * Henter parametre
      *
     c                   eval      w_firm = p_firm
6.32 c                   eval      b_avik = *off
      *
     c                   endsr
