     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : LE280R
      * Beskrivelse . . : Utskrift av historisk lagerbok - parameter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.
      * --------- ------  -------------------------------------------------
7.xx  * 7.00 190516  bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  BRUK AV INDIKATORER
      *
      *       KA = F1  = Valg av firma
      *       KC = F3  = Exit
      *
      *       LR = Avslutt program
      *       30 = Protect av felter ved vise
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer
      *    60-69 = Fileindikatorer chain etc.
      *    70-79 = Arbeidsindikatorer i sub-rutiner
      *    80-89 = Arbeidsindikatorer ordinære
      *    90-99 = Benyttes ved std. sub-rutiner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fll635d    cf   e             workstn
      *
      *  Local Data Area
      *
     d                uds
      *
     d lcfvar                300    314
     d lctvar                        15
     d lcflag                         2  0
     d lctlag                         2  0
     d lcfogr                         2  0
     d lctogr                         2  0
     d lcfhgr                         2  0
     d lcthgr                         2  0
     d lcfugr                         3  0
     d lctugr                         3  0
     d lcfdat                        10d   datfmt(*iso)
     d lctdat                        10d   datfmt(*iso)
     d lcsyst                         1
     d lctype                         2
     d lcinut                         1
     d lcsrt1                         1  0
     d lcsrt2                         1  0
     d lcsrt3                         1  0
     d lcsrt4                         1  0
      *
     d lcuser                911    920
     d lcfirm                944    946  0
     d l_fnav                951    980
      *
     d wpin03          s              1  0
      *
     d w_firm          s              3  0
     d w_jtxt          s             20
     d w_lage          s              2  0
     d w_retk          s              1
      *
     c     dummy         tag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av felter                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * A1BLD  - A1 Bilde for valg av parametre
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A1BLD Behandle bilde for valg av utskriftskriteria              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     a1taga        tag
      *
     c                   exfmt     a1bld
      *
      * Gå ut
      *
     c                   if        *inkc = *on
     c                   move      1             wpin03
     c                   goto      xslutt
     c                   endif
      *
      * Kontroll av valg
      *
     c                   if        a1fvar > a1tvar
     c                   move      *on           *in30
     c                   goto      a1taga
     c                   endif
      *
      *  Sjekk,  Finnes lager i KODE-REGISTER
      *
     c                   if        a1flag > *zeros
     c                   eval      w_lage = a1flag
     c                   exsr      sblagr
     c                   if        w_retk <> *blank
     c                   eval      *in42 = *on
     c                   goto      a1taga
     c                   endif
     c                   endif
      *
     c                   if        a1tlag < 99
     c                   eval      w_lage = a1tlag
     c                   exsr      sblagr
     c                   if        w_retk <> *blank
     c                   eval      *in43 = *on
     c                   goto      a1taga
     c                   endif
     c                   endif
      *
     c                   if        a1flag > a1tlag
     c                   move      *on           *in31
     c                   goto      a1taga
     c                   endif
      *
     c                   if        a1fogr > a1togr
     c                   move      *on           *in32
     c                   goto      a1taga
     c                   endif
      *
     c                   if        a1fhgr > a1thgr
     c                   move      *on           *in33
     c                   goto      a1taga
     c                   endif
      *
      * Test fra-dato
      *
     c     *dmy          test(d)                 a1fdat                 34
     c                   if        *in34 = *on
     c                   goto      a1taga
     c                   endif
      *
      * Test til-dato
      *
     c     *dmy          test(d)                 a1tdat                 34
     c                   if        *in34 = *on
     c                   goto      a1taga
     c                   endif
      *
     c                   if        a1fdat > a1tdat
     c                   move      *on           *in34
     c                   goto      a1taga
     c                   endif
      *
     c                   if        a1syst <> ' '
     c                             and a1syst <> 'F'
     c                             and a1syst <> 'L'
     c                             and a1syst <> 'B'
     c                   move      *on           *in35
     c                   goto      a1taga
     c                   endif
      *
     c                   if        a1type = '**'
     c                   move      *on           *in36
     c                   goto      a1taga
     c                   endif
      *
     c                   if        a1inut <> ' '
     c                             and a1inut <> 'I'
     c                             and a1inut <> 'U'
     c                   move      *on           *in37
     c                   goto      a1taga
     c                   endif
      *
     c                   if        a1srt1 > 3
     c                   move      *on           *in38
     c                   goto      a1taga
     c                   endif
      *
     c                   if        a1srt2 > 3
     c                   move      *on           *in39
     c                   goto      a1taga
     c                   endif
      *
     c                   if        a1srt3 > 3
     c                   move      *on           *in40
     c                   goto      a1taga
     c                   endif
      *
     c                   if        a1srt4 > 3
     c                   move      *on           *in41
     c                   goto      a1taga
     c                   endif
      *
      * Skriv parameter til Local
      *
     c                   movel     a1fvar        lcfvar
     c                   movel     a1tvar        lctvar
     c                   move      a1flag        lcflag
     c                   move      a1tlag        lctlag
     c                   move      a1fogr        lcfogr
     c                   move      a1togr        lctogr
     c                   move      a1fhgr        lcfhgr
     c                   move      a1thgr        lcthgr
     c                   move      a1fugr        lcfugr
     c                   move      a1tugr        lctugr
      *
     c                   if        a1fdat <> 0
     c     *dmy          move      a1fdat        lcfdat
     c                   endif
      *
     c                   if        a1tdat <> 0
     c     *dmy          move      a1tdat        lctdat
     c                   endif
      *
     c                   move      a1syst        lcsyst
     c                   move      a1type        lctype
     c                   move      a1inut        lcinut
     c                   move      a1srt1        lcsrt1
     c                   move      a1srt2        lcsrt2
     c                   move      a1srt3        lcsrt3
     c                   move      a1srt4        lcsrt4
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Sjekk/Hent tekst for lager                                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     sblagr        begsr
      *
     c                   call      'RS710R'
     c                   parm                    w_retk
     c                   parm                    w_lage
     c                   parm                    w_jtxt
      *
      *
     c                   endsr
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Setter standardverdier for
      * felt i parameterbildet
      *
     c                   eval      a1fvar = *zeros
     c                   movel     '99999999'    a1tvar
     c                   eval      a1flag = *zeros
     c                   eval      a1tlag = 99
     c                   eval      a1fogr = *zeros
     c                   eval      a1togr = 99
     c                   eval      a1fhgr = *zeros
     c                   eval      a1thgr = 99
     c                   eval      a1fugr = *zeros
     c                   eval      a1tugr = 999
     c                   eval      a1fdat = *zeros
     c                   eval      a1tdat = *zeros
     c                   eval      lcfdat = *loval
     c                   eval      lctdat = *hival
     c                   eval      lcsyst = *blank
     c                   eval      lctype = *blank
     c                   eval      lcinut = *blank
     c                   eval      lcsrt1 = *zeros
     c                   eval      lcsrt2 = *zeros
     c                   eval      lcsrt3 = *zeros
     c                   eval      lcsrt4 = *zeros
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    wpin03
      *
     c                   endsr
