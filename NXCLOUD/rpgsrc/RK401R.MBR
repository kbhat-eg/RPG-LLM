      /TITLE  Sletting av kunder
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK401R                                        *
      * Beskrivelse . . : Sletting av kunder                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
T5.00 * V5.00     230403  Kunder lagt inn etter xx.xx.xx blir ikke slettetHFL *
T5.40 * V5.40     181105  "Eldre enn dato" testet på feil dato.     HFL *
5.60  * R5.60 AMD 271107  Lagt inn løsning for kontroll av grense for
      *                   almenningsrabatter
6.20  * R6.20 bsa 140611  Memosaldo lagt ut på egen fil
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
6.20 frkmel1    if   e           k disk    rename(rkmepfr:rkmel1r)
     frkunlu    uf   e           k disk    rename(rkunpfr:rkunlur)
6.20 frkmelu    uf   e           k disk    rename(rkmepfr:rkmelur)
     frksolu    uf   e           k disk    rename(rksopfr:rksolur)
     frktrl1    if   e           k disk    rename(rktrpfr:rktrl1r)
     frk401p    o    e             printer oflind(*in30)
      *
      * Definering av variable i nøkler
      *
     d rkunlu_kund     s                   like(rkkund)
6.20 d rkmel1_kund     s                   like(rkkund)
     d rksolu_skun     s                   like(rkskun)
     d rktrl1_kund     s                   like(rnkund)
      *
      * Definering av Local data area
      *
     d                uds
      *
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d                 ds
     d w_dato                         6  0
     d  w_dag                         2  0 overlay(w_dato)
     d  w_mnd                         2  0 overlay(w_dato:3)
     d  w_aar                         2  0 overlay(w_dato:5)
      *
     d w_type          s              1
     d w_edat          s               d   datfmt(*iso)
T5.00d w_ndat          s               d   datfmt(*iso)
     d w_sdat          s               d   datfmt(*iso)
      *
     d w_fast          s              6  0
      *
     d w_tota          s              7  0
     d w_anta          s              5  0
     d w_firm          s                   like(rkfirm)
     d w_kund          s                   like(rkkund)
     d w_slett         s              1
      *
      * Les kunderegister
      *
     c     rkunl1_key    setll     rkunl1                                                    .
     c     rkunl1_key    reade     rkunl1                                 90
      *
     c                   dow       *in90 = *off
      *
     c                   exsr      kontroll
      *
     c                   if        w_slett = ' '
      *
     c                   exsr      utskrift
      *
     c                   if        w_type  = 'N'
      *
     c                   eval      rkunlu_kund = rkkund
     c     rkunlu_key    chain     rkunlu                             90
     c                   if        *in90 = *off
     c                   delete    rkunlur
     c                   endif
      *
     c                   eval      rksolu_skun = rkkund
     c     rksolu_key    chain     rksolu                             90
     c                   if        *in90 = *off
     c                   delete    rksolur
     c                   endif
6.20  * Memosaldo
6.20 c     rkunlu_key    chain     rkmelu                             90
6.20 c                   if        *in90 = *off
6.20 c                   delete    rkmelur
6.20 c                   endif
      *
     c                   endif
     c                   endif
      *
     c     rkunl1_key    reade     rkunl1                                 90
     c                   enddo
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
     c                   eval      t1tota = w_tota
     c                   eval      t1anta = w_anta
      *
     c                   write     t1tot
      *
      * Call til subrutine for close av filer
      *
     c                   eval      w_slett = 'S'
     c                   eval      w_kund  = *zeros
     c                   call      'RK792C'
     c                   parm                    w_firm
     c                   parm                    rkkund
     c                   parm                    w_slett
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for kontroll
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kontroll      begsr
      *
      * Tell opp antall kunder
      *
     c                   eval      w_tota = w_tota + 1
     c                   eval      w_slett = ' '
6.20  * Hent inn info om memosaldo
6.20 c                   eval      rkmel1_kund = rkkund
6.20 c     rkmel1_key    chain     rkmel1                             90
      *
      * Sjekk siste transaksjonsdato
      *
E5.40c                   if        rktdat  > w_edat
     c                   eval      w_slett = 'X'
     c                   endif
      *
T5.00 * Sjekk kunde lagt inn dato
      *
T5.00c                   eval      w_sdat  = rkndat
      *
T5.00c                   if        w_sdat  > w_ndat
T5.00c                   eval      w_slett = 'X'
T5.00c                   endif
      *
      * Sjekk om saldo = 0
      *
     c                   if        rksald <> 0
     c                   eval      w_slett = 'X'
     c                   endif
      *
      * Sjekk om memo-saldo = 0
      *
5.60 c                   if        rkmems <> 0 or
5.60 c                             rkmema <> 0
     c                   eval      w_slett = 'X'
     c                   endif
      *
      * Sjekk om det finnes posteringer på kunde
      *
     c                   if        w_slett = *blank
     c                   eval      rktrl1_kund = rkkund
     c     rktrl1_key    chain     rktrl1                             91
     c                   if        *in91 = *off
     c                   eval      w_slett = 'X'
     c                   endif
     c                   endif
      *
      * Sjekk om det finnes ordre på kunde
      *
     c                   if        w_slett = *blank
     c                   eval      w_kund = rkkund
     c                   call      'RK792C'
     c                   parm                    w_firm
     c                   parm                    w_kund
     c                   parm                    w_slett
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utskrift      begsr
      *
      * Tell antall kunder som kan/blir slettet
      *
     c                   eval      w_anta = w_anta + 1
      *
      * Sjekker om overflow
      *
     c                   if        *in30 = *on                                               .
     c                   write     a1hdr                                                     .
     c                   eval      *in30 = *off                                              .
     c                   endif                                                           .....
      *
     c                   write     d1det
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    w_type
     c                   parm                    w_edat
T5.00c                   parm                    w_ndat
      *
      * Key for kunderegister
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppdatering av kunde
      *
     c     rkunlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunlu_kund
6.20  *
6.20  * Key for oppslag etter memosaldo
6.20  *
6.20 c     rkmel1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    rkmel1_kund
      *
      * Key for oppdatering av kunde-søketekst
      *
     c     rksolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rksolu_skun
      *
      * Key for oppslag på kundeposter
      *
     c     rktrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrl1_kund
      *
     c                   eval      w_firm = l_firm
      *
     c                   if        w_type = 'J'
     c                   eval      *in11 = *on
     c                   else
     c                   eval      *in11 = *off
     c                   endif
      *
      * Skriv heading på første side
      *
     c                   write     a1hdr
      *
     c                   endsr
      *
