      /TITLE  ASOFAK: Utskrift av kontroll-liste, plukker ordres
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO644R
      * Beskrivelse . . : Utskrift av kjøre-liste, plukker ordres
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * V5.50 070307 KOB  Endret datotest til leveringsdato istedet for ordredato
      * V5.50 070307 KOB  Lagt inn ny test dersom ordretype er valgt
5.53  * R5.53 230307 AMD  Utvidet sorteringsbegrep slik at både Navn og
      *                   Adresse 1 kan skape brudd
6.nu  * R6.nu 190614 bof  Endret i hht. nummerutvidelse
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffotxl1    if   e           k disk    rename(fotxpfr:fotxl1r)
     f*plol1    if   e           k disk    rename(fplopfr:fplol1r)
     ffpsolu    uf a e           k disk    rename(fpsopfr:fpsolur)
      *
     d                 ds
      *                                                      SORTERING
6.nu d sorter                       114
     d  sor001                        2  0 overlay(sorter)
     d  sor002                        5  0 overlay(sorter:03)
     d  sor003                        6  0 overlay(sorter:08)
     d  sor004                       30    overlay(sorter:14)
6.nu d  sor005                        8  0 overlay(sorter:44)
6.nu d  sor006                        2  0 overlay(sorter:52)
      *
     d                 ds
      *                                                      Parameter-inn
     d ldprec                        64
     d  ldotyp                        2    overlay(ldprec)
     d  ldfdat                       10d   datfmt(*iso)
     d                                     overlay(ldprec:03)
     d  ldtdat                       10d   datfmt(*iso)
     d                                     overlay(ldprec:13)
     d  ldfavd                        4  0 overlay(ldprec:23)
     d  ldtavd                        4  0 overlay(ldprec:27)
     d  ldfkjo                        2  0 overlay(ldprec:31)
     d  ldtkjo                        2  0 overlay(ldprec:33)
     d  ldvari                        1  0 overlay(ldprec:35)
      *
      * - - - - - - - - - - - - - - - - - - *
      *   Priser      :  Overføringsfelter  *
      * - - - - - - - - - - - - - - - - - - *
      *
     d                 ds
      *                                                     Record til sbr
     d hirec                         80
     d  hifirm                        3  0 overlay(hirec)
     d  hirkat                        3  0 overlay(hirec:4)
     d  hikund                        6  0 overlay(hirec:7)
     d  hikpro                        6  0 overlay(hirec:13)
     d  hivare                       15    overlay(hirec:19)
     d  hilety                        1  0 overlay(hirec:34)
     d  hienhe                        3    overlay(hirec:35)
     d  hidato                         d   overlay(hirec:38) datfmt(*iso)
     d  hitime                         t   overlay(hirec:48) timfmt(*iso)
     d  hinumm                        6  0 overlay(hirec:56)
     d  hikode                        1    overlay(hirec:62)
     d  hidekn                        5  2 overlay(hirec:63)
     d  hitilb                        1    overlay(hirec:68)
     d  hiinlr                        1    overlay(hirec:69)
      *
      *                                                     Record fra sbr
     d                 ds
     d horec                         64
     d  holety                        1  0 overlay(horec)
     d  hokpri                        1    overlay(horec:2)
     d  hooppr                        9  2 overlay(horec:3)
     d  hosapr                        9  2 overlay(horec:12)
     d  hokopr                        9  2 overlay(horec:21)
     d  horab1                        5  2 overlay(horec:30)
     d  horab2                        5  2 overlay(horec:35)
     d  hodekn                        5  2 overlay(horec:40)
     d  hopros                        5  2 overlay(horec:45)
      *
      * Definering av variable
      *
     d w_dato_alf      s              8
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *               :  Local Data Area                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     d                uds
      *
     d dswsid                901    910
     d dsuser                        10
     d dssfir                944    946  0
      *
     d hedfir          s                   like(fofirm)
     d hednum          s                   like(fonumm)
     d hedsuf          s                   like(fosuff)
     d linfir          s                   like(fdfirm)
     d linlin          s                   like(fdline)
     d linnum          s                   like(fdnumm)
     d linsuf          s                   like(fdsuff)
     d psofir          s                   like(fksfir)
     d psolin          s                   like(fkslin)
     d psonum          s                   like(fksonr)
     d psosuf          s                   like(fkssuf)
     d psotll          s                   like(fkstll)
     d tstbyt          s              1
     d txtfir          s                   like(ftfirm)
     d txtlin          s                   like(ftline)
     d txtnum          s                   like(ftnumm)
     d txtsuf          s                   like(ftsuff)
     d wstatg          s              1
     d wwparm          s             64
     d wwwtst          s              1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandle utplukk, Setter inn riktige parameterverdier          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c                   move      wwparm        ldprec
      *
     c                   eval      hednum = *zero
     c                   eval      hedsuf = *zero
      *
     c     hedkey        setll     fohel1r
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Leser poster fra register for utplukks-test                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     start         tag
      *
     c                   move      *off          *in90
     c                   read      fohel1r                                90
     c     *in90         cabeq     *on           slutt
      *
      *  Hvis ordretype er valgt sjekk, ellers ta med alle typer
      *  Sjekk   Er ordretype lik valgt,           Hvis nei les neste post
      *
     c                   if        ldotyp <> ' '
     c     footyp        cabne     ldotyp        start
     c                   endif
      *
      *  Sjekk   Er Datoene innenfor grensene,     Hvis nei les neste post
      *
     c                   if        foldat < ldfdat or
     c                             foldat > ldtdat
     c                   goto      start
     c                   endif
      *
      *  Sjekk   Er avdeling riktig, Ellers hopp til Slutt
      *
     c                   if        foavde < ldfavd or
     c                             foavde > ldtavd
     c                   goto      start
     c                   endif
      *
      *  Sjekk   Er kjørerute riktig, Ellers hopp til Slutt
      *
     c                   if        fokjor < ldfkjo or
     c                             fokjor > ldtkjo
     c                   goto      start
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandle utlegging av records til Plukk/Sort-register          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Legger inn ordrenummer og suffix i nøkklene
      *
     c                   move      fonumm        hednum
     c                   move      fosuff        hedsuf
      *
     c                   move      fonumm        linnum
     c                   move      fosuff        linsuf
      *
     c                   move      fonumm        txtnum
     c                   move      fosuff        txtsuf
      *
     c                   move      fonumm        psonum
     c                   move      fosuff        psosuf
      *
      *  Nullstiller sorteringsbegrep pr. ordre
      *
     c                   eval      sorter = *blank
      *
      *  Legger inn faste verdier i sorteringsbegrep
      *
     c                   move      fokjor        sor001
     c                   move      fokjnr        sor002
     c                   move      fokund        sor003
     c                   movel     fonavn        sor004
     c                   movel     fonumm        sor005
     c                   movel     fosuff        sor006
      *
      *
      *  Henter linjer/records fra de enkelte registre
      *
      * - - - - - - - - - - - - - - - - - - - - - - - *
      *  Nøkkel for utplukks/sortert register er      *
      *                                               *
      *  FKSFIR  Firma                                *
      *  FKSSRT  Sorteringsbegrep                     *
      *  FKSONR  Ordrenummer                          *
      *  FKSSUF  Suffix                               *
      *  FKSTLL  Rekkefølgekode  : Hode         = 0   *
      *  FKSLIN  Linjenummer     : Detaljlinjer = 3   *
      *                          : Tekst, Foran = 2   *
      *                          : Tekst, Etter = 4   *
      * - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Blanker hjelpevariabel for test for detaljlinjer
      *
     c                   eval      tstbyt = *blank
      *
      *  Henter detalj-linjer
      *
     c                   exsr      linje
      *
      *  Henter bare Heading + tekstlinjer dersom varelinjer finnes
      *
     c                   if        tstbyt <> *blank
     c                   exsr      ohead
     c                   exsr      foran
     c                   exsr      etter
     c                   endif
      *
      *  Velg neste Ordre
      *
     c                   goto      start
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Henter ordre-heading og legger ut til sorterings-register      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     ohead         begsr
      *
     c                   eval      psotll = *zero
     c                   eval      psolin = *zero
     c     fpsolu_key    chain     fpsolur                            92
      *
     c                   move      'H'           fkskod
     c                   movel     ldotyp        fksoty
     c***                move      ldval1        fksalt
     c                   movel     sorter        fkssrt
     c****               movel     ldval7        fksdiv
     c***                move      ldpros        fksdiv
      *
     c                   if        *in92 = *off
     c                   update    fpsolur
     c                   else
     c                   move      psofir        fksfir
     c                   move      psonum        fksonr
     c                   move      psosuf        fkssuf
     c                   move      psotll        fkstll
     c                   move      psolin        fkslin
     c                   write     fpsolur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Henter ordre-linjer og legger ut til sorterings-register       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     linje         begsr
      *
     c                   eval      linlin = *zero
      *
     c     linkey        setll     fodtl1r
      *
     c     linje1        tag
     c                   move      *off          *in90
     c                   read      fodtl1r                                90
     c                   if        *in90 = *off
     c                   if        fdfirm = linfir
     c                   if        fdnumm = linnum
     c                   if        fdsuff = linsuf
      *
      *  Er dette en tekstlinje ?
      *
     c***                if        ldval7 = 1
     c***                          or ldpros <> *zero
     c***  fdvare        cabeq     *blank        linje1
     c***                endif
      *
      *
      *  Dersom sjekk for varelinjer kall opp subrutine
      *
     c***                if        ldval7 = 1
     c***                          or ldpros <> *zero
     c***                exsr      sjekk
     c***                if        wwwtst = *blank
     c***                goto      linje1
     c***                endif
     c***                endif
      *
     c                   eval      psotll = 3
     c                   eval      psolin = fdline
     c     fpsolu_key    chain     fpsolur                            92
      *
     c                   move      'X'           tstbyt
      *
     c                   move      'V'           fkskod
     c                   movel     ldotyp        fksoty
     c***                move      ldval1        fksalt
     c                   movel     sorter        fkssrt
      *
     c                   if        *in92 = *off
     c                   update    fpsolur
     c                   else
     c                   move      psofir        fksfir
     c                   move      psonum        fksonr
     c                   move      psosuf        fkssuf
     c                   move      psotll        fkstll
     c                   move      psolin        fkslin
     c                   write     fpsolur
     c                   endif
      *
     c                   goto      linje1
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Henter ordre-tekst (FORAN) og legger ut til sorterings-register*
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     foran         begsr
      *
     c                   eval      txtlin = *zero
      *
     c     txtkey        setll     fotxl1r
      *
     c     foran1        tag
     c                   move      *off          *in90
     c                   read      fotxl1r                                90
     c                   if        *in90 = *off
     c                   if        ftfirm = txtfir
     c                   if        ftnumm = txtnum
     c                   if        ftsuff = txtsuf
     c                   if        ftline <= 4999
      *
     c                   eval      psotll = 2
     c                   eval      psolin = ftline
     c     fpsolu_key    chain     fpsolur                            92
      *
     c                   move      'K'           fkskod
     c                   movel     ldotyp        fksoty
     c***                move      ldval1        fksalt
     c                   movel     sorter        fkssrt
      *
     c                   if        *in92 = *off
     c                   update    fpsolur
     c                   else
     c                   move      psofir        fksfir
     c                   move      psonum        fksonr
     c                   move      psosuf        fkssuf
     c                   move      psotll        fkstll
     c                   move      psolin        fkslin
     c                   write     fpsolur
     c                   endif
      *
     c                   goto      foran1
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Henter ordre-tekst (ETTER) og legger ut til sorterings-register*
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     etter         begsr
      *
     c                   move      5000          txtlin
      *
     c     txtkey        setll     fotxl1r
      *
     c     etter1        tag
     c                   move      *off          *in90
     c                   read      fotxl1r                                90
     c                   if        *in90 = *off
     c                   if        ftfirm = txtfir
     c                   if        ftnumm = txtnum
     c                   if        ftsuff = txtsuf
     c                   if        ftline <= 9999
      *
     c                   eval      psotll = 4
     c                   eval      psolin = ftline
     c     fpsolu_key    chain     fpsolur                            92
      *
     c                   move      'K'           fkskod
     c                   movel     ldotyp        fksoty
     c***                move      ldval1        fksalt
     c                   movel     sorter        fkssrt
      *
     c                   if        *in92 = *off
     c                   update    fpsolur
     c                   else
     c                   move      psofir        fksfir
     c                   move      psonum        fksonr
     c                   move      psosuf        fkssuf
     c                   move      psotll        fkstll
     c                   move      psolin        fkslin
     c                   write     fpsolur
     c                   endif
      *
     c                   goto      etter1
     c                   endif
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     *inzsr        begsr
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     c                   eval      wstatg = *blank
     c                   eval      tstbyt = *blank
     c                   eval      wwwtst = *blank
      *
      *  Key for file : ORDRE-HODE-REGISTER
      *
     c     hedkey        klist
     c                   kfld                    hedfir
     c                   kfld                    hednum
     c                   kfld                    hedsuf
      *
      *  Key for file : ORDRE-LINJE-REGISTER
      *
     c     linkey        klist
     c                   kfld                    linfir
     c                   kfld                    linnum
     c                   kfld                    linsuf
     c                   kfld                    linlin
      *
      *  Key for file : ORDRE-TEKST-REGISTER
      *
     c     txtkey        klist
     c                   kfld                    txtfir
     c                   kfld                    txtnum
     c                   kfld                    txtsuf
     c                   kfld                    txtlin
      *
      *  Key for file : UTPLUKK-PARAM-REGISTER
      *
     c*    plokey        klist
     c*                  kfld                    plofir
     c*                  kfld                    plonum
     c*                  kfld                    plosuf
      *
      *  Key for file : PLUKK-S-LINJE-REGISTER
      *
     c     fpsolu_key    klist
     c                   kfld                    psofir
     c                   kfld                    psonum
     c                   kfld                    psosuf
     c                   kfld                    psotll
     c                   kfld                    psolin
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     *entry        plist
     c                   parm                    wwparm
      *
      *  Legge inn firmanummer i nøkklene
      *
     c                   move      dssfir        hedfir
     c                   move      dssfir        linfir
     c                   move      dssfir        txtfir
     c*                  move      dssfir        plofir
     c                   move      dssfir        psofir
      *
     c                   endsr
