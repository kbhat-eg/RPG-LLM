     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON
      * Program . . . . : RK713R
      * Beskrivelse . . : Overføring til Nordea-format
      *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       040805 AMD  Nytt program
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fraa9l1    if   e           k disk    rename(raa9pfr:raa9l1r)
     frefal1    if   e           k disk    rename(refapfr:refal1r)
     frekul1    if   e           k disk    rename(rekupfr:rekul1r)
     fref2pf    uf a e             disk
     frek2pf    uf a e             disk
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable
      *
     d w_sted          s             25
     d w_klie          s              4  0
     d w_bsum          s             13  2
     d w_btyp          s              1  0
     d w_date          s               d   datfmt(*iso)
     d w_dato          s              6  0
      *
      * Definering av variabler i nøkler
      *
     d w_firm          s                   like(rnfirm)
      *
      * Datastruktur for fakturalinjer
      *
     d                 ds
     d d_frec_1                     256
     d  d_aart_1                      1  0 overlay(d_frec_1:1)
     d  d_bunt_1                      5  0 overlay(d_frec_1:2)
     d  d_kund_1                      7  0 overlay(d_frec_1:7)
6.NU d  d_fakt_1                      8  0 overlay(d_frec_1:14)
6.NU d  d_bdat_1                      6  0 overlay(d_frec_1:22)
6.NU d  d_forf_1                      6  0 overlay(d_frec_1:28)
6.NU d  d_fbel_1                     11  2 overlay(d_frec_1:33)
6.NU d  d_fill_1                     37    overlay(d_frec_1:45)
      *
      * Datastruktur for kundetabell
      *
     d                 ds
     d d_krec_2                     256
     d  d_kode_2                      1    overlay(d_krec_2:1)
     d  d_vers_2                      4  0 overlay(d_krec_2:2)
     d  d_klie_2                      4  0 overlay(d_krec_2:6)
     d  d_kund_2                      9  0 overlay(d_krec_2:10)
     d  d_ftak_2                     11  0 overlay(d_krec_2:19)
     d  d_nav1_2                     35    overlay(d_krec_2:30)
     d  d_nav2_2                     20    overlay(d_krec_2:65)
     d  d_adr1_2                     30    overlay(d_krec_2:85)
     d  d_ponr_2                      4  0 overlay(d_krec_2:115)
     d  d_sted_2                     23    overlay(d_krec_2:119)
     d  d_kper_2                     20    overlay(d_krec_2:142)
     d  d_tlfn_2                     12    overlay(d_krec_2:162)
     d  d_land_2                     20    overlay(d_krec_2:174)
     d  d_lana_2                      2    overlay(d_krec_2:194)
     d  d_lann_2                      3  0 overlay(d_krec_2:196)
     d  d_bgir_2                     11  0 overlay(d_krec_2:199)
     d  d_dise_2                      4  0 overlay(d_krec_2:210)
     d  d_nav3_2                     35    overlay(d_krec_2:214)
      *
      * Datastruktur for bunthode
      *
     d                 ds
     d d_frec_3                      80
     d  d_aart_3                      1  0 overlay(d_frec_3:1)
     d  d_bunt_3                      5  0 overlay(d_frec_3:2)
     d  d_klie_3                      4  0 overlay(d_frec_3:7)
     d  d_dbfc_3                      3  0 overlay(d_frec_3:11)
     d  d_ktyp_3                      1    overlay(d_frec_3:14)
     d  d_kliv_3                      2  0 overlay(d_frec_3:15)
     d  d_bunv_3                      2  0 overlay(d_frec_3:17)
     d  d_ntrn_3                      6  0 overlay(d_frec_3:19)
     d  d_btyp_3                      1  0 overlay(d_frec_3:25)
     d  d_bdat_3                      6  0 overlay(d_frec_3:26)
     d  d_bsum_3                     13  2 overlay(d_frec_3:32)
     d  d_fill_3                     36    overlay(d_frec_3:45)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Kaller opp program for
      * å summere opp fakturabeløp
      *
     c                   call      'RK714R'
     c                   parm                    w_bsum
     c                   parm                    w_btyp
      *
      * Legger ut bunt hode
      *
     c                   exsr      xopd_bunt
      *
      * Lese Kunde-transaksjoner
      *
     c                   read      refal1
     c                   dow       not %eof
      *
      * Oppdater faktura-tabell
      *
     c                   exsr      xopd_fakt
     c                   read      refal1
      *
     c                   enddo
      *
      * Lese Kunde-register
      *
     c                   read      rekul1
     c                   dow       not %eof
      *
      * Oppdater kunde-tabell
      *
     c                   exsr      xopd_kund
     c                   read      rekul1
      *
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatering av bunt-post
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xopd_bunt     begsr
      *
      * Bunt-record
      *
     c                   if        w_bsum < 0
     c                   eval      w_bsum = w_bsum * -1
     c                   endif
      *
     c                   if        w_btyp   = 1
     c                   eval      d_bunt_3 = 00001
     c                   else
     c                   eval      d_bunt_3 = 00002
     c                   endif
      *
     c                   eval      d_aart_3 = 1
     c                   eval      d_klie_3 = w_klie
     c                   eval      d_dbfc_3 = 530
     c                   eval      d_ktyp_3 = 'D'
     c                   eval      d_kliv_3 = 01
     c                   eval      d_bunv_3 = 01
     c                   eval      d_ntrn_3 = 0
     c                   eval      d_btyp_3 = w_btyp
     c                   eval      d_bdat_3 = w_dato
     c                   eval      d_bsum_3 = w_bsum
     c                   eval      d_fill_3 = *blank
     c                   eval      refrec   = d_frec_3
      *
     c                   write     ref2pfr
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatering av fil for faktura-poster
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xopd_fakt     begsr
      *
      * Fakturaposter
      *
     c                   if        w_btyp   = 1
     c                   eval      d_bunt_1 = 00001
     c                   else
     c                   eval      d_bunt_1 = 00002
     c                   endif
      *
     c                   eval      d_aart_1 = 2
     c                   eval      d_kund_1 = rnkund
     c                   eval      d_fakt_1 = rnbiln
     c                   eval      d_fill_1 = *blank
      *
      * Snu fortegn på beløp
      * dersom negativt
      *
     c                   if        rnbelØ   < 0
     c                   eval      d_fbel_1 = rnbelØ * -1
     c                   else
     c                   eval      d_fbel_1 = rnbelØ
     c                   endif
      *
      * Snu dato
      *
     c                   eval      d_bdat_1 = 0
     c                   eval      d_forf_1 = 0
      *
     c                   if        rnbdat   <> *loval
     c     *dmy          move      rnbdat        d_bdat_1
     c                   endif
      *
     c                   if        rnfdat   <> *loval
     c     *dmy          move      rnfdat        d_forf_1
     c                   endif
      *
     c                   eval      refrec   = d_frec_1
     c                   write     ref2pfr
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatering av fil for kunde-tabell
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xopd_kund     begsr
      *
      * Kunde-tabell
      *
     c                   move      rksted        w_sted
      *
     c                   eval      d_kode_2 = 'K'
     c                   eval      d_vers_2 = 9409
     c                   eval      d_klie_2 = w_klie
     c                   eval      d_kund_2 = rkkund
     c                   eval      d_ftak_2 = rkftnr
     c                   eval      d_nav1_2 = rknavn
     c                   eval      d_nav2_2 = *blank
     c                   eval      d_adr1_2 = rkgate
     c                   eval      d_ponr_2 = rkponr
     c                   eval      d_sted_2 = w_sted
     c                   eval      d_kper_2 = *blank
     c                   eval      d_tlfn_2 = rktlfn
     c                   eval      d_land_2 = *blank
     c                   eval      d_lana_2 = *blank
     c                   eval      d_lann_2 = 0
     c                   eval      d_bgir_2 = 0
     c                   eval      d_dise_2 = 0
     c                   eval      d_nav3_2 = *blank
      *
      * Oppdater kunde-tabell Nordea
      *
     c                   eval      rekrec   = d_krec_2
     c                   write     rek2pfr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for statustabell factoring
      *
     c     raa9l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for kundetransaksjoner
      *
     c     refal1_key    klist
     c                   kfld                    w_firm
      *
      * Key for kunderegister
      *
     c     rekul1_key    klist
     c                   kfld                    w_firm
      *
      * Legge inn firmanummer
      *
     c                   eval      w_firm = l_firm
      *
      * Finne dagens dato
      *
     c                   time                    w_date
     c     *dmy          move      w_date        w_dato
      *
      * Hent inn klientnummer
      *
     c     raa9l1_key    chain     raa9l1r
     c                   if        %found
     c                   eval      w_klie = ra9kli
     c                   endif
      *
     c                   endsr
      *
