     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JM550R
      * Beskrivelse . . : Spørring/utplukk kampanjevarer, parameterprogram
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       130397 HKO  Nytt program
7.xx  * 7.00  171116 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjkahl1    if   e           k disk    rename(jkahpfr:jkahl1r)
      *
     fjm550d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_user          s             10
      *
     d p_firm          s                   like(jmfirm)
     d p_kamp          s                   like(jmkamp)
7.01 d p_prgr          s                   like(jmprgr)
      *
      * Definering av variabler i nøkler
      *
     d jkahl1_kamp     s                   like(jmkamp)
      *
      * Definering av variable
      *
     d w_feil          s              1    inz(*off)
     d w_firm          s                   like(jmfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     b1tag         tag
      *
      * Behandler bildet
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_inp
     c                   if        w_feil = *on
     c                   eval      w_feil = *off
     c                   goto      b1taga
     c                   endif
      *
      * Kaller søke/utplukksprogram
      *
     c                   call      'JM551R'
     c                   parm                    w_firm
     c                   parm                    b1kamp
7.01 c                   parm                    b1prgr
     c                   parm                    p_kamp
     c                   parm                    p_user
      *
     c                   goto      b1taga
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av spørringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Kampanje
      *
     c                   if        w_afld = 'B1KAMP'
     c                   call      'JM500R'
     c                   parm                    w_firm
     c                   parm                    b1kamp
     c                   parm                    b1besk
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_inp     begsr
      *
     c                   eval      w_feil = *off
      *
      * Kampanje
      *
     c                   eval      b1besk = *blank
     c                   if        b1kamp = *blank
     c                   eval      *in31  = *on
     c                   eval      w_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   eval      jkahl1_kamp = b1kamp
     c     jkahl1_key    chain     jkahl1                             32
     c                   if        *in32  = *on
     c                   eval      w_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      b1besk = jmbesk
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kamp
7.01 c                   parm                    p_prgr
     c                   parm                    p_user
      *
      * Key for oppslg på kampanje-hode
      *
     c     jkahl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jkahl1_kamp
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
