     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VA167R
      * Beskrivelse . . : Bonussatser pr. bonustype
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       260911 BSA  Nytt program
6.20  * 6.10  050312 bsa  Akseptere negative bonussatser
6.21  * 6.10  050312 bsa  Feil i håndtering av skifte av år
6.nu  * R6.30 030614 bsa  Programmet gjennomgått mtp nummerutvidelser.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fekbsiu    uf a e           k disk    rename(ekbsst:ekbsiur)
     fekbsir    if   e           k disk    rename(ekbsst:ekbsirr)
     fekbsi1    if   e           k disk    rename(ekbsst:ekbsi1r)
     fvbotl1    if   e           k disk    rename(vbotpfr:vbotl1r)
      *
     fva167d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_boty          s                   like(eksbot)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d ekbsir_sbot     s                   like(eksbot)
     d ekbsir_saar     s                   like(eksaar)
      *
     d ekbsiu_sbot     s                   like(eksbot)
     d ekbsiu_saar     s                   like(eksaar)
     d ekbsiu_sbgf     s                   like(eksbgf)
     d ekbsiu_sbgt     s                   like(eksbgt)
      *
     d ekbsi1_sbot     s                   like(eksbot)
     d ekbsi1_saar     s                   like(eksaar)
     d ekbsi1_sbgf     s                   like(eksbgf)
     d ekbsi1_sbgt     s                   like(eksbgt)
      *
     d vbotl1_tbot     s                   like(vatbot)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
      *
     d b_feil          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_oppr          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_subf          s              1    inz(*off)
      *
     d w_firm          s                   like(eksfir)
     d w_boty          s                   like(eksbot)
      *
     d s_sbgf          s                   like(eksbgf)
     d s_sbgt          s                   like(eksbgt)
6.21 d s_saar          s                   like(eksaar)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(12)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inka
     c                   exsr      xh1win
6.21 c                   goto      b2taga
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile                                  CRT SFL
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   if        b_subf = *on
     c                   goto      b2taga
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c     ekbsir_key    setll     ekbsir
      *
     c                   exsr      clr_subfile                                  TØM SFL
     c                   exsr      crt_subfile                                  CRT SFL
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
     c                   eval      b_subf = *off
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      b_subf = *on
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1sbgf = b1sbgf
     c                   eval      c1sbgt = b1sbgt
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1sbgf = b1sbgf
     c                   eval      k1sbgt = b1sbgt
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c**                 eval      d1sbgf = b1sbgf
     c**                 eval      d1sbgt = b1sbgt
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1sbgf = b1sbgf
     c                   eval      c1sbgt = b1sbgt
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1boty = b2boty
     c                   eval      c1btxt = b2btxt
     c                   eval      c1saar = b2saar
     c                   eval      c1sbgf = 0
     c                   eval      c1sbgt = 0
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      c1taga
     c                   endsl
      *
      * Validering
      *
     c                   if        c1sbgf > c1sbgt
     c                   eval      *in31 = *on
     c                   goto      c1taga
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      ekbsi1_sbgf = c1sbgf
     c                   eval      ekbsi1_sbgt = c1sbgt
     c     ekbsi1_key    chain     ekbsi1
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c     end_c1bld     tag
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   eval      b_oppr = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbilde legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle skjermbilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   if        c1boty = *blank
     c                   eval      c1boty = b2boty
     c                   eval      c1btxt = b2btxt
     c                   eval      c1saar = b2saar
     c                   endif
      *
     c                   eval      ekbsi1_sbgf = c1sbgf
     c                   eval      ekbsi1_sbgt = c1sbgt
     c     ekbsi1_key    chain     ekbsi1
      *
     c                   if        %found
     c                   eval      c2sbpr = eksbpr
     c                   eval      c2smrk = eksmrk
     c                   eval      c2sktb = eksktb
     c                   eval      c2seus = ekseus
     c     *dmy          move      eksoda        c2soda
     c     *dmy          move      ekseda        c2seda
     c                   eval      c2seus = ekseus
     c                   else
     c                   eval      c2sbpr = 0
     c                   eval      c2smrk = *blank
     c                   eval      c2sktb = 0
     c                   eval      c2soda = 0
     c                   eval      c2seda = 0
     c                   eval      c2seus = *blank
     c                   endif
      *
      * Kopiering
      *
     c                   if        b1valg = 3
     c                   eval      c1sbgf = k1sbgf
     c                   eval      c1sbgt = k1sbgt
     c                   endif
      *
      * Sperrer felter dersom 'vise'
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Validering
      *
     c                   if        c2sbpr = 0 and
     c                             c2smrk = *blank
     c                   eval      *in31 = *on
     c                   goto      c2tagb
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   ekbsiur
      *
     c                   eval      ekbsiu_sbgf = c1sbgf
     c                   eval      ekbsiu_sbgt = c1sbgt
     c     ekbsiu_key    chain     ekbsiur
      *
     c                   eval      eksbpr = c2sbpr
     c                   eval      eksmrk = c2smrk
     c                   eval      eksktb = c2sktb
     c                   eval      ekseus = l_user
      *
     c                   time                    ekseda
     c                   time                    ekseti
      *
      * Endrer/oppretter rad.
      *
     c                   if        %found
     c                   update    ekbsiur
     c                   else
      *
     c                   eval      eksfir = w_firm
     c                   eval      eksbot = p_boty
     c                   eval      eksaar = c1saar
     c                   eval      eksbgf = c1sbgf
     c                   eval      eksbgt = c1sbgt
      *
     c                   time                    eksoda
     c                   time                    eksoti
6.10 c                   eval      eksous = l_user
     c                   write     ekbsiur
      *
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1sbpr = eksbpr
     c                   eval      b1smrk = eksmrk
     c                   eval      b1sktb = eksktb
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * xh1win  Bilde for behandling av bla-alternativer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xh1win        begsr
      *
     c     h1taga        tag
      *
     c                   write     h1win
     c                   exfmt     h1win
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      end_h1win
     c                   endsl
      *
     c                   if        h1saar = 0
     c                   eval      *in31 = *on
     c                   goto      h1taga
     c                   endif
      *
     c                   if        h1saar < 2000
     c                   eval      *in32 = *on
     c                   goto      h1taga
     c                   endif
      *
6.21 c                   if        h1saar <> b2saar
     c                   eval      b2saar = h1saar
6.21 c                   eval      ekbsi1_saar = h1saar
6.21 c                   eval      ekbsiu_saar = h1saar
6.21 c                   eval      ekbsir_saar = h1saar
6.21 c                   eval      s_saar = h1saar
6.21 c                   exsr      forny
6.21 c                   endif
      *
     c     end_h1win     endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      ekbsi1_sbgf = b1sbgf
     c                   eval      ekbsi1_sbgt = b1sbgt
     c     ekbsi1_key    chain     ekbsi1
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
     c                   eval      ekbsiu_sbgf = b1sbgf
     c                   eval      ekbsiu_sbgt = b1sbgt
     c     ekbsiu_key    chain     ekbsiur
     c                   delete    ekbsiur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_sbgf = k1sbgf
     c                   eval      s_sbgt = k1sbgt
      *
      * Fyller kopierings-bildet
      *
     c                   eval      ekbsi1_sbgf = k1sbgf
     c                   eval      ekbsi1_sbgt = k1sbgt
     c     ekbsi1_key    chain     ekbsi1
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   exsr      spØrring
     c                   goto      k1taga
     c                   endif
      *
      * Validering
      *
     c                   if        k1sbgf > k1sbgt
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      ekbsi1_sbgf = k1sbgf
     c                   eval      ekbsi1_sbgt = k1sbgt
     c     ekbsi1_key    chain     ekbsi1
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1sbgf = s_sbgf
     c                   eval      c1sbgt = s_sbgt
      *
     c                   exsr      xc2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil                     SFL END
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      ekbsir                                 15
      *
     c                   if        *in15                                        EOF
     c                   leave
     c                   endif
      *
     c                   if        eksbot <> p_boty
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
6.21 c                   if        s_saar <> eksaar
6.21 c                   eval      *in15 = *on
6.21 c                   leave
6.21 c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1sbgf = eksbgf
     c                   eval      b1sbgt = eksbgt
     c                   eval      b1sbpr = eksbpr
     c                   eval      b1smrk = eksmrk
     c                   eval      b1sktb = eksktb
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_boty
      *
      * Key for oppslag på kunde bonuselement
      *
     c     ekbsiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ekbsiu_sbot
     c                   kfld                    ekbsiu_saar
     c                   kfld                    ekbsiu_sbgf
     c                   kfld                    ekbsiu_sbgt
      *
      * Key for oppslag på kunde bonuselement
      *
     c     ekbsi1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ekbsi1_sbot
     c                   kfld                    ekbsi1_saar
     c                   kfld                    ekbsi1_sbgf
     c                   kfld                    ekbsi1_sbgt
      *
      * Key for oppslag på kunde bonuselement
      *
     c     ekbsir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ekbsir_sbot
     c                   kfld                    ekbsir_saar
      *
      * Key for oppslag på bonustype
      *
     c     vbotl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vbotl1_tbot
      *
      * Henter parametre, og fyller opp skjermbilde og subfile
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      b2boty = p_boty
      *
      * Bonustype
      *
     c                   if        b2boty <> *blank
     c                   eval      vbotl1_tbot = p_boty
     c     vbotl1_key    chain     vbotl1
     c                   if        %found
     c                   eval      b2btxt = vatbes
     c                   endif
     c                   endif
      *
     c                   eval      ekbsi1_sbot = p_boty
     c                   eval      ekbsi1_saar = *year
     c                   eval      ekbsiu_sbot = p_boty
     c                   eval      ekbsiu_saar = *year
     c                   eval      ekbsir_saar = *year
     c                   eval      ekbsir_sbot = p_boty
     c                   eval      b2saar = *year
6.21 c                   eval      s_saar = *year
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     ekbsir_key    setll     ekbsir
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
