     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASNAVIOKO
      * Program . . . . : IT011R
      * Beskrivelse . . : Leser betalingsbetingelser og skriver til kø
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       230614 ask  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra03l1    if   e           k disk    rename(ra03pfr:ra03l1r)
      *
      * Local Data Area
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variabler i nøkler
      *
     d ra03l1_ckod     s                   like(rackod)
      *
      *  Definering av datastrukturer for parameter
      *
     d d_parm          ds
     d  d_kode                        2
     d  d_besk                       40
     d  d_frim                        3
     d  d_dnet                        3
     d  d_ra1d                        3
     d  d_ra1p                        5
     d  d_ra2d                        3
     d  d_ra2p                        5
     d  d_ra3d                        3
     d  d_ra3p                        5
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_prog          s             40
     d w_comd          s             20
     d w_parm          s           2048
     d w_date          s               d   datfmt(*iso)
     d w_time          s               t   timfmt(*iso)
     d w_rpgp          s             10
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram - leser alle poster og kaller opp std skriveprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser alle poster på RA03PF
      *
     c                   eval      ra03l1_ckod = 00
     c     ra03l1_key    setll     ra03l1
      *
     c                   read      ra03l1
     c                   dow       not %eof and
     c                             racfir = w_firm
      *
     c                   eval      d_kode = %triml(%editw(rackod : '  0'))
     c                   eval      d_besk = ractxt
     c                   eval      d_frim = racjan
     c                   eval      d_dnet = %editw(racdag : ' 0 ')
     c                   eval      d_ra1d = %editw(racda1 : ' 0 ')
     c                   eval      d_ra1p = %editw(racrab : '0 ,  ')
     c                   eval      d_ra2d = %editw(racda2 : ' 0 ')
     c                   eval      d_ra2p = %editw(racra2 : '0 ,  ')
     c                   eval      d_ra3d = %editw(racda3 : ' 0 ')
     c                   eval      d_ra3p = %editw(racra3 : '0 ,  ')
     c                   eval      w_parm = d_parm
      *
     c                   call      'IT001R'
     c                   parm                    w_firm
     c                   parm                    w_prog
     c                   parm                    w_comd
     c                   parm                    w_parm
     c                   parm                    w_rpgp
      *
     c                   read      ra03l1
     c                   enddo
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på valgt kode
      *
     c     ra03l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra03l1_ckod
      *
      * Setter inn parameterverdier
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_prog = 'NexstepImportBetalingsbetingelser'
     c                   eval      w_comd = 'Insert'
     c                   eval      w_rpgp = 'IT011R'
      *
     c                   endsr
