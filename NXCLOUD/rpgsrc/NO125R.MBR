     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : NO125R
      * Beskrivelse . . : Ekstern ordre-hode, overføring til ordre
      *
      * Spesialversjon. : Overfører ordre fra Winner tegneprogram
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       270319 ASK  Nytt program
7.01  * K000  291220 bsa  Endrer logikken på hvordan vår ref oppdateres
7.02  * K000  160321 bsa  Hvis fakturakunde må denne hentes inn og
7.02  * K000              legges på ordrehodet.
      *
8.01  * 8.xx  200122 bsa  Skriver tekst som har kommet som melding til
8.01  * 8.xx              toptekst på ordren som opprettes. NB Forutsetter
8.01  * 8.xx              at bestillingen har kommet elektronisk.
8.02  * 8.xx  240222 bsa  Henter inn evnt leveringsbetingelse fra EDI, og
8.02  * 8.xx              legger den som forslag på ordren (FOLBTX)
8.03  *PRG2   220622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
8.04  * K000  130824 bsa  Henter nummerserie basert på feil ordretype.
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fnohel1    if   e           k disk    rename(nohepfr:nohel1r)
     fnodtl1    if   e           k disk    rename(nodtpfr:nodtl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
6.22 frkmel1    if   e           k disk    rename(rkmepfr:rkmel1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fnohelu    uf   e           k disk    rename(nohepfr:nohelur)
     ffohelu    uf a e           k disk    rename(fohepfr:fohelur)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
6.26 fausrl1    if   e           k disk    rename(ausrpfr:ausrl1r)
7.01 fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
8.01 ffotxlu    uf a e           k disk    rename(fotxpfr:fotxlur)
8.02 fedh2l1    if   e           k disk    rename(edh2pfr:edh2l1r)
      *
     fno120d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_firm          s                   like(nofirm)
     d p_fsys          s                   like(nofsys)
     d p_tell          s                   like(notell)
      *
      * Definering av Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
      *
      * Definering av variabler i nøkler
      *
     d nohel1_fsys     s                   like(nofsys)
     d nohel1_tell     s                   like(notell)
     d nodtl1_fsys     s                   like(ndfsys)
     d nodtl1_tell     s                   like(ndtell)
     d votyl1_otyp     s                   like(vaotyp)
     d fusrl1_user     s                   like(fbuser)
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d nohelu_fsys     s                   like(nofsys)
     d nohelu_tell     s                   like(notell)
     d fohelu_numm     s                   like(fonumm)
     d fohelu_suff     s                   like(fosuff)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d fodtl1_line     s                   like(fdline)
6.26 d ausrl1_user     s                   like(abuser)
7.01 d ra09l1_ikod     s                   like(raikod)
8.01 d fotxlu_numm     s                   like(ftnumm)
8.01 d fotxlu_suff     s                   like(ftsuff)
8.01 d fotxlu_line     s                   like(ftline)
      *
      * Definering av variable
      *
     d b_sper          s              1    inz(*off)
      *
     d w_kode          s              1
     d w_fast          s             10
     d w_sald          s             11  2
5.60 d w_sal2          s             11  2
     d w_tots          s             11  2
     d w_totr          s             11  2
     d w_totk          s             11  2
5.60 d w_tota          s             11  2
5.60 d w_totn          s             11  2
5.60 d w_tot1          s             11  2
5.60 d w_tot2          s             11  2
     d w_fell          s              6
     d w_valg          s              2  0
     d w_text          s             70
     d w_nref          s              6  0
6.33 d w_kgrs          s             11  0
      *
6.26 d s_avde          s              4  0
7.02 d s_kund          s                   like(rkkund)
      *
     d w_firm          s                   like(nofirm)
6.nu d w_numm          s                   like(fonumm)
     d w_suff          s                   like(fosuff)
     d w_otyp          s                   like(footyp)
     d w_line          s                   like(fdline)
5.32 d w_onum          s                   like(vaonum)
5.32 d w_type          s                   like(vaonum)
5.53  *
5.53  * Parametre til RS205R - hent mva %
5.53  *
5.53 d w_stat          s              1
5.53 d w_mvak          s              1
5.53 d w_mvtx          s             30
5.53 d w_mvas          s              6  2
5.53 d w_mvat          s              5  4
5.53 d w_mvaf          s             10  9
5.53 d w_bpro          s              5  2
5.53 d w_date          s               d   datfmt(*iso)
5.50 d w_gral          s                   like(rkgral)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Avbryter dersom ekstern ordre ikke finnes
      *
     c                   eval      nohel1_fsys = p_fsys
     c                   eval      nohel1_tell = p_tell
     c     nohel1_key    chain     nohel1
     c                   if        not %found
     c                   goto      avslutt
6.25 c                   else
6.25 c                   eval      nohelu_fsys = p_fsys
6.25 c                   eval      nohelu_tell = p_tell
6.25 c     nohelu_key    chain     nohelur
6.25 c                   eval      nonumm = *hival
6.25 c                   update    nohelur
     c                   endif
      *
      * Hent ordretype
      *
     c                   eval      w_otyp = faaot8
     c                   if        w_otyp = *blank
     c                   eval      w_otyp = faaot2
     c                   endif
      *
     c                   eval      b1otyp = w_otyp
      *
      * Henter informasjon og kredittsperre og kredittgrense
      * Ved kredittsperre stoppes videre overføring
      *
     c                   eval      rkunl1_kund = nokund
     c     rkunl1_key    chain     rkunl1
6.22  * Henter inn memosaldo
6.22 c     rkunl1_key    chain     rkmel1
      *
6.21 c                   if        rkkres = 1
     c                   eval      b_sper = *on
     c                   eval      *in41 = *on
     c                   endif
      *
     c                   if        rkgrns <> 0
6.33 c                   eval      w_kgrs = rkgrns * 1000
5.53 c                   eval      w_sald = rksald + (rkmems * w_mvat)
6.33 c**                 if        w_sald > rkgrns
6.33 c                   if        w_sald > w_kgrs
     c                   eval      *in42 = *on
     c                   endif
     c                   endif
5.50  *
5.50  * Sjekk beløpsgrense almenning fra kunde dersom utfylt
5.50  *
5.50 c                   if        rkgral <> 0
5.50 c                   eval      w_gral = rkgral
5.50  *
5.50 c                   else
5.50  *
5.50  * Ellers hent beløpsgrense almenning
5.50  *
5.50 c                   call      'FO106R'
5.50 c                   parm                    nokund
5.50 c                   parm                    w_gral
5.50  *
5.50 c                   endif
5.50  *
5.50 c                   if        w_gral <> 0
5.50 c                   eval      w_sald = rkkjal + rkmema
5.50 c                   if        w_sald > w_gral
5.50 c                   eval      *in43 = *on
5.50 c                   endif
5.50 c                   endif
      *
      * Behandler bildet
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * Avbryter ved kredittsperre
      *
     c                   if        b_sper = *on
     c                   goto      avslutt
     c                   endif
      *
      * F3=Avslutt
      *
     c                   if        *inkc or *inkl
6.25 c                   eval      nohelu_fsys = p_fsys
6.25 c                   eval      nohelu_tell = p_tell
6.25 c     nohelu_key    chain     nohelur
6.25 c                   eval      nonumm = 0
6.25 c                   update    nohelur
     c                   goto      avslutt
     c                   endif
      *
      * F4=Spørring
      *
     c                   if        *inkd
     c                   call      'VA550R'
     c                   parm                    w_firm
     c                   parm                    b1otyp
     c                   goto      b1taga
     c                   endif
      *
      * Validering av ordretype
      *
     c                   if        b1otyp = *blank
     c                   eval      b1otyp = w_otyp
     c                   endif
      *
     c                   eval      votyl1_otyp = b1otyp
     c     votyl1_key    chain     votyl1
     c                   if        not %found
3.32 c                   eval      *in31 = *on
     c                   goto      b1taga
     c                   endif
      *
3.32 c                   if        vaosys <> 0
3.32 c                   eval      *in32 = *on
     c                   goto      b1taga
3.32 c                   endif
      *
8.04 c                   eval      w_otyp = b1otyp
      *
      * Oppretter ordre-hode
      *
     c                   exsr      ordre_hode
      *
      * Oppretter ordre-linje
      *
     c                   exsr      ordre_linje
      *
      * Oppdater ordrehode og memosaldo med verdier fra ordrelinjer
      *
     c                   exsr      oppdat_ordre
      *
      * Oppdater ekstern ordre-hode med ordrenummer
      *
     c                   eval      nohelu_fsys = p_fsys
     c                   eval      nohelu_tell = p_tell
     c     nohelu_key    chain     nohelur
     c                   if        %found
     c                   eval      nonumm = fonumm
6.10 c                   time                    noedat
6.10 c                   time                    noetim
6.10 c                   eval      noeusr = l_user
     c                   update    nohelur
     c                   endif
      *
      * Kaller program for ordrebehandling
      *
     c                   eval      w_valg = 2
      *
     c                   call      'FO100R'
     c                   parm                    w_valg
     c                   parm                    fonumm
     c                   parm                    fosuff
      *
      * Kaller program for å sende ordrebekreftelse til nettbutikk
      *
     c                   if        p_fsys = 'NBU'
6.24 c                   eval      w_nref = %int(%trim(nonref))
     c                   call      'NN750C'
     c                   parm                    w_firm
     c                   parm                    w_nref
6.nu c                   parm                    fonumm
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppretter ordre-hode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ordre_hode    begsr
      *
      * Hent neste ledig nummer i.h.t. ordretype
      *
     c                   exsr      hent_nummer
8.02  *
8.02  * Slå opp mot evnt tilleggsregister på mottatt EDI medling
8.02  *
8.02 c                   if        nofsys = 'EDI' or
8.02 c                             nofsys = 'EHF' or
8.02 c                             nofsys = 'SMK'
8.02  *
8.02 c     nohel1_key    chain     edh2l1
8.02 c                   if        not %found
8.02 c                   eval      e2lbex = *blanks
8.02 c                   endif
8.02  *
8.02 c                   endif
      *
      * Initierer fil
      *
     c                   eval      fosuff = 00
     c                   eval      fomkod = 0
     c                   eval      fobind = 'N'
      *
     c                   eval      fofirm = nofirm
6.nu c                   eval      fonumm = w_numm
     c                   eval      footyp = b1otyp
     c                   eval      folety = nolety
8.02 c                   eval      folbtx = e2lbex
     c                   eval      fobdat = *loval
     c                   eval      foldat = noldat
     c                   eval      fobetb = 0
     c                   eval      foselg = 0
6.23 c**                 eval      folmat = 0
6.23 c                   eval      folmat = nolmat
     c                   eval      fokund = nokund
     c                   eval      fokpro = nokpro
     c                   eval      fonavn = nonavn
     c                   eval      foadr1 = noadr1
     c                   eval      foadr2 = noadr2
     c                   eval      fosted = %trim(noponr) + ' ' + nosted
6.20 c**                 eval      foproj = 0
6.20 c                   eval      foproj = *blank
     c                   eval      foakti = *blank
     c                   eval      fovref = *blank
     c                   eval      fodref = nodref
     c                   eval      forekv = norekv
     c                   eval      fodate = *loval
     c                   eval      fotime = *loval
     c                   eval      forkat = 0
     c                   eval      fonref = nonref
     c                   eval      fofsys = nofsys
5.40 c                   eval      foavde = noavde
6.26  * Hvis avdeling er 0, hent fra bruker eller kunde
6.26 c                   if        foavde = 0
6.26 c                   if        s_avde <> 0
6.26 c                   eval      foavde = s_avde
6.26 c                   else
6.26 c                   eval      foavde = rkavde
6.26 c                   endif
6.26 c                   endif
6.26  *
5.40 c                   eval      folage = nolage
      *
     c                   eval      w_suff = fosuff
      *
     c                   time                    fobdat
     c                   time                    fodate
     c                   time                    fotime
      *
      * Henter kundeinformasjon
      *
7.02 c                   eval      s_kund = 0
     c                   eval      rkunl1_kund = nokund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
      *
     c                   eval      fobetb = rkbetb
     c                   eval      fodist = rkdist
6.23 c**                 eval      folmat = rkform
     c                   eval      folbet = rklbet
     c                   eval      fovalk = rkvalk
     c                   eval      forkat = rkrabk
     c                   eval      fosped = rksped
     c                   eval      fokjor = rkkjØr
     c                   eval      fomkod = rkmkod
     c                   eval      fokobk = rkordb
     c                   eval      fokgfa = rkfagb
     c                   eval      fokgfr = rkfrgb
     c                   eval      fokgek = rkekgb
     c                   eval      forabp = rkorab
     c                   eval      foneto = rkneto
5.61 c                   eval      fofaty = rkfaty
      *
7.01 c**                 if        rkslgr <> 0
     c                   eval      foselg = rkslgr
7.01 c**                 endif
      *
     c                   if        fonavn = *blank and
     c                             foadr1 = *blank and
     c                             foadr2 = *blank
     c                   eval      fonavn = rknavn
     c                   eval      foadr1 = rkgate
     c                   eval      foadr2 = rkadr2
     c                   eval      fosted = rksted
     c                   endif
7.02  *
7.02  * Sjekk om det er fakturakunde på kunden
7.02  *
7.02 c                   if        rkfaku <> 0
7.02 c                   eval      s_kund = rkfaku
7.02 c                   eval      rkunl1_kund = rkfaku
7.02 c     rkunl1_key    chain     rkunl1
7.02 c                   if        %found
7.02 c                   eval      fobetb = rkbetb
7.02 c                   eval      fodist = rkdist
7.02 c                   eval      folbet = rklbet
7.02 c                   eval      fovalk = rkvalk
7.02 c                   eval      forkat = rkrabk
7.02 c                   eval      fosped = rksped
7.02 c                   eval      fokjor = rkkjØr
7.02 c                   eval      fomkod = rkmkod
7.02 c                   eval      fokobk = rkordb
7.02 c                   eval      fokgfa = rkfagb
7.02 c                   eval      fokgfr = rkfrgb
7.02 c                   eval      fokgek = rkekgb
7.02 c                   eval      forabp = rkorab
7.02 c                   eval      foneto = rkneto
7.02 c                   eval      fofaty = rkfaty
7.02 c                   eval      foselg = rkslgr
7.02 c                   eval      fokund = s_kund
7.02 c                   eval      folkun = nokund
7.02 c                   eval      fokuna = rkalfa
7.02 c                   endif
7.02 c                   endif
      *
     c                   endif
7.01  *
7.01  * Endel av koden er flyttet - logikken blir slik
7.01  * Selger hentes fra tilknyttet brukeren
7.01  * Slår opp på selger, og henter navnet på selger og
7.01  * legger som vår ref på ordren.
7.01  * Hvis selger ikke ligger på bruker, hentes den fra kunden.
      *
      * Hent brukerinformasjon
      *
     c                   if        nouser <> *blank
     c                   eval      fusrl1_user = nouser
     c                   else
     c                   eval      fusrl1_user = l_user
     c                   endif
      *
     c     fusrl1_key    chain     fusrl1
     c                   if        %found
7.01 c**                 eval      fovref = fbvref
7.01 c                   if        fbselg <> 0
     c                   eval      foselg = fbselg
7.01 c                   endif
     c                   endif
7.01  *
7.01  * Henter vår ref fra selger
7.01  *
7.01 c                   eval      ra09l1_ikod = foselg
7.01 c     ra09l1_key    chain     ra09l1r
7.01 c                   if        %found
7.01 c                   eval      fovref = raitxt
7.01 c                   endif
      *
      * Henter kundeprosjekt-informasjon
      *
     c                   if        nokpro <> 0
     c                   eval      fkprl1_kund = nokund
     c                   eval      fkprl1_kpro = nokpro
     c     fkprl1_key    chain     fkprl1
     c                   if        %found
     c                   eval      foproj = flproj
     c                   if        florka <> 0
     c                   eval      forkat = florka
     c                   endif
5.61 c                   if        flfaty <> 0
5.61 c                   eval      fofaty = flfaty
5.61 c                   endif
6.31 c                   eval      fobetb = flbetb
6.31 c                   eval      fomkod = flmkod
6.31 c                   eval      fokgek = flekgb
6.31 c                   eval      foneto = flneto
6.31 c                   eval      fokgfa = flfagb
     c                   endif
      *
     c                   endif
      *
      * Skriver til fil
      *
6.32 c                   eval      fouser = l_user
6.10 c                   time                    fodate
6.10 c                   time                    fotime
6.10 c                   eval      fooous = l_user
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   write     fohelur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppretter ordre-linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ordre_linje   begsr
      *
      * Henter høyeste linjenummer fra ordre-linje
      *
     c                   exsr      hent_linjenr
      *
      * Dersom det ligger melding på ekstern ordre-hode, splittes denne og
      * legges ut som tekstlinjer
      *
     c                   if        nomeld <> *blank
      *
8.01 c                   if        nofsys <> 'EDI' and
8.01 c                             nofsys <> 'EHF' and
8.01 c                             nofsys <> 'SMK'
      *
     c                   eval      w_text = %subst(nomeld:1:70)
     c                   eval      w_line = w_line + 10
     c                   call      'FD111R'
     c                   parm                    w_firm
     c                   parm                    fonumm
     c                   parm                    fosuff
     c                   parm                    w_line
     c                   parm                    faatty
     c                   parm                    w_text
      *
     c                   if        %subst(nomeld:71:70) <> *blank
     c                   eval      w_text = %subst(nomeld:71:70)
     c                   eval      w_line = w_line + 10
     c                   call      'FD111R'
     c                   parm                    w_firm
     c                   parm                    fonumm
     c                   parm                    fosuff
     c                   parm                    w_line
     c                   parm                    faatty
     c                   parm                    w_text
     c                   endif
      *
     c                   if        %subst(nomeld:141:70) <> *blank
     c                   eval      w_text = %subst(nomeld:141:70)
     c                   eval      w_line = w_line + 10
     c                   call      'FD111R'
     c                   parm                    w_firm
     c                   parm                    fonumm
     c                   parm                    fosuff
     c                   parm                    w_line
     c                   parm                    faatty
     c                   parm                    w_text
     c                   endif
      *
     c                   if        %subst(nomeld:211:46) <> *blank
     c                   eval      w_text = %subst(nomeld:211:46)
     c                   eval      w_line = w_line + 10
     c                   call      'FD111R'
     c                   parm                    w_firm
     c                   parm                    fonumm
     c                   parm                    fosuff
     c                   parm                    w_line
     c                   parm                    faatty
     c                   parm                    w_text
     c                   endif
8.01  * Hvis elektronisk, legg det ut som topptekst
8.01 c                   else
8.01  *
8.01 c                   eval      w_text = %subst(nomeld:1:70)
8.01 c                   eval      w_line = 1010
8.01  *
8.01 c                   eval      fotxlu_numm = fonumm
8.01 c                   eval      fotxlu_suff = fosuff
8.01 c                   eval      fotxlu_line = w_line
8.01 c     fotxlu_key    chain     fotxlu
8.01 c                   if        not %found
8.01 c                   eval      ftfirm = fofirm
8.01 c                   eval      ftnumm = fonumm
8.01 c                   eval      ftsuff = fosuff
8.01 c                   eval      ftline = w_line
8.01 c                   eval      fttext = w_text
8.01  *
8.01 c                   time                    ftodat
8.01 c                   time                    ftotim
8.01 c                   eval      ftousr = l_user
8.01 c                   time                    ftedat
8.01 c                   time                    ftetim
8.01 c                   eval      fteusr = l_user
8.01  *
8.01 c                   write     fotxlur
8.01 c                   endif
      *
8.01 c                   if        %subst(nomeld:71:70) <> *blank
8.01 c                   eval      w_text = %subst(nomeld:71:70)
8.01 c                   eval      w_line = w_line + 10
8.01  *
8.01 c                   eval      fotxlu_numm = fonumm
8.01 c                   eval      fotxlu_suff = fosuff
8.01 c                   eval      fotxlu_line = w_line
8.01 c     fotxlu_key    chain     fotxlu
8.01 c                   if        not %found
8.01 c                   eval      ftfirm = fofirm
8.01 c                   eval      ftnumm = fonumm
8.01 c                   eval      ftsuff = fosuff
8.01 c                   eval      ftline = w_line
8.01 c                   eval      fttext = w_text
8.01  *
8.01 c                   time                    ftodat
8.01 c                   time                    ftotim
8.01 c                   eval      ftousr = l_user
8.01 c                   time                    ftedat
8.01 c                   time                    ftetim
8.01 c                   eval      fteusr = l_user
8.01  *
8.01 c                   write     fotxlur
8.01 c                   endif
8.01 c                   endif
      *
8.01 c                   if        %subst(nomeld:141:70) <> *blank
8.01 c                   eval      w_text = %subst(nomeld:141:70)
8.01 c                   eval      w_line = w_line + 10
8.01  *
8.01 c                   eval      fotxlu_numm = fonumm
8.01 c                   eval      fotxlu_suff = fosuff
8.01 c                   eval      fotxlu_line = w_line
8.01 c     fotxlu_key    chain     fotxlu
8.01 c                   if        not %found
8.01 c                   eval      ftfirm = fofirm
8.01 c                   eval      ftnumm = fonumm
8.01 c                   eval      ftsuff = fosuff
8.01 c                   eval      ftline = w_line
8.01 c                   eval      fttext = w_text
8.01  *
8.01 c                   time                    ftodat
8.01 c                   time                    ftotim
8.01 c                   eval      ftousr = l_user
8.01 c                   time                    ftedat
8.01 c                   time                    ftetim
8.01 c                   eval      fteusr = l_user
8.01  *
8.01 c                   write     fotxlur
8.01 c                   endif
8.01 c                   endif
      *
8.01 c                   if        %subst(nomeld:211:46) <> *blank
8.01 c                   eval      w_text = %subst(nomeld:211:46)
8.01 c                   eval      w_line = w_line + 10
8.01  *
8.01 c                   eval      fotxlu_numm = fonumm
8.01 c                   eval      fotxlu_suff = fosuff
8.01 c                   eval      fotxlu_line = w_line
8.01 c     fotxlu_key    chain     fotxlu
8.01 c                   if        not %found
8.01 c                   eval      ftfirm = fofirm
8.01 c                   eval      ftnumm = fonumm
8.01 c                   eval      ftsuff = fosuff
8.01 c                   eval      ftline = w_line
8.01 c                   eval      fttext = w_text
8.01  *
8.01 c                   time                    ftodat
8.01 c                   time                    ftotim
8.01 c                   eval      ftousr = l_user
8.01 c                   time                    ftedat
8.01 c                   time                    ftetim
8.01 c                   eval      fteusr = l_user
8.01  *
8.01 c                   write     fotxlur
8.01 c                   endif
8.01 c                   endif
      *
8.01 c                   endif
      *
     c                   endif
      *
      * Leser alle ekstern ordre-linjer, og legger disse ut som ordre-linjer
      *
     c                   eval      nodtl1_fsys = p_fsys
     c                   eval      nodtl1_tell = p_tell
     c     nodtl1_key    setll     nodtl1
     c     nodtl1_key    reade     nodtl1
     c                   dow       not %eof
      *
     c                   eval      w_kode = *blank
     c                   eval      w_line = w_line + 10
      *
     c                   if        ndvare <> *blank
     c                   call      'FD118R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    fonumm
     c                   parm                    fosuff
     c                   parm                    w_line
     c                   parm                    faalty
     c                   parm                    ndlety
     c                   parm                    ndvare
     c                   parm                    ndanta
     c                   parm                    ndenhe
     c                   parm                    ndldat
     c                   parm                    ndtek1
     c                   parm                    ndtek2
     c                   parm                    ndntpr
     c*                  parm                    ndkpri
     c*                  parm                    ndrab1
     c*                  parm                    ndrab2
     c*                  parm                    ndlvnr
     c                   else
     c                   eval      w_text = ndtek1 + ndtek2
     c                   call      'FD111R'
     c                   parm                    w_firm
     c                   parm                    fonumm
     c                   parm                    fosuff
     c                   parm                    w_line
     c                   parm                    faatty
     c                   parm                    w_text
     c                   endif
      *
     c     nodtl1_key    reade     nodtl1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter inn ordrenummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_nummer   begsr
      *
     c                   eval      w_kode = '0'
     c                   eval      w_fell = faaork
     c                   eval      w_fast = l_wsid
5.32 c                   eval      w_type = b1otyp
5.32  *
5.32  * Hente inn eventuell overstyrt
5.32  * ordretype for nummerserie
5.32  *
5.32 c                   call      'VA752R'
5.32 c                   parm                    w_firm
5.32 c                   parm                    b1otyp
5.32 c                   parm                    w_onum
5.32  *
5.32 c                   if        w_onum <> *blank
5.32 c                   eval      w_type = w_onum
5.32 c                   endif
      *
      * Kaller subprogram for henting av nummer fra nummer-register
      *
     c                   call      'AS100R'
     c                   parm                    w_kode
     c                   parm                    w_firm
     c                   parm                    w_fell
5.32 c                   parm                    w_type
     c                   parm                    w_fast
6.nu c                   parm                    w_numm
6.27  *
6.27  * sjekker at ordrenr ikke har tidligere relasjoner liggende
6.27  *
6.27 c                   call      'FO415R '
6.27 c                   parm                    w_firm
6.27 c                   parm                    w_numm
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å hente høyeste linjenummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_linjenr  begsr
      *
     c                   eval      fodtl1_numm = fonumm
     c                   eval      fodtl1_suff = fosuff
     c                   eval      fodtl1_line = 9999
     c     fodtl1_key    setll     fodtl1
     c                   readp     fodtl1
     c                   if        not %eof and
     c                             fdfirm = w_firm and
     c                             fdnumm = fonumm and
     c                             fdsuff = fosuff
     c                   eval      w_line = fdline
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer ordre og memosaldo
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdat_ordre  begsr
      *
      * Henter ordretotal
      *
     c                   eval      w_tots = 0
     c                   eval      w_totr = 0
     c                   eval      w_totk = 0
5.60 c                   eval      w_tota = 0
5.60 c                   eval      w_totn = 0
5.60 c                   eval      w_tot1 = 0
5.60 c                   eval      w_tot2 = 0
      *
     c     fodtl1_key2   setll     fodtl1
     c     fodtl1_key2   reade     fodtl1
     c                   dow       not %eof
     c                   eval      w_tots = w_tots + fdsums
     c                   eval      w_totr = w_totr + fdsumr
     c                   eval      w_totk = w_totk + fdsumk
5.60  *
5.60 c                   eval      w_totn = fdsums
5.60 c                   eval      w_tot1 = 0
5.60 c                   eval      w_tot2 = 0
5.60  *
5.60 c                   if        fdrab1 <> 0
5.60 c                   eval      w_tot1 = w_totn * fdrab1 / 100
5.60 c                   eval      w_totn = w_totn - w_tot1
5.60 c                   endif
5.60  *
5.60 c                   if        fdrab2 <> 0
5.60 c                   eval      w_tot2 = w_totn * fdrab2 / 100
5.60 c                   eval      w_totn = w_totn - w_tot2
5.60 c                   endif
5.60  *
5.60  * NB!  w_tot1
5.60  * og   w_tot2 brukes om
5.60  * igjen og null-stilles
5.60  *
5.60 c                   eval      w_tot1 = 0
5.60 c                   eval      w_tot2 = 0
5.60  *
5.60 c                   if        fdbrr1 <> 0
5.60 c                   eval      w_tot1 = w_totn * fdbrr1 / 100
5.60 c                   eval      w_totn = w_totn - w_tot1
5.60 c                   endif
5.60  *
5.60 c                   if        fdbrr2 <> 0
5.60 c                   eval      w_tot2 = w_totn * fdbrr2 / 100
5.60 c                   eval      w_totn = w_totn - w_tot2
5.60 c                   endif
5.60  *
5.60 c                   eval      w_tota = w_tota + w_tot1
5.60 c                   eval      w_tota = w_tota + w_tot2
5.60  *
     c     fodtl1_key2   reade     fodtl1
     c                   enddo
      *
      * Beregner justeringsbeløp, og oppdaterer memosaldo
      *
     c                   eval      w_kode = *blank
     c                   eval      w_sald = w_tots - w_totr
5.60  *
5.60 c                   eval      w_sal2 = w_tota
      *
     c                   call      'FA922R'
     c                   parm                    w_firm
     c                   parm                    footyp
     c                   parm                    fokund
     c                   parm                    w_sald
5.60 c                   parm                    w_sal2
      *
      * Oppdaterer ordre-hode med ny saldo
      *
     c                   eval      fohelu_numm = fonumm
     c                   eval      fohelu_suff = fosuff
     c     fohelu_key    chain     fohelur
      *
     c                   eval      fotots = w_tots
     c                   eval      fototr = w_totr
     c                   eval      fototk = w_totk
5.60 c                   eval      fobrra = w_tota
      *
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   update    fohelur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_fsys
     c                   parm                    p_tell
      *
      * Key for oppslag på ekstern ordre-hode
      *
     c     nohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    nohel1_fsys
     c                   kfld                    nohel1_tell
      *
      * Key for les på ekstern ordre-linje
      *
     c     nodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    nodtl1_fsys
     c                   kfld                    nodtl1_tell
      *
      * Key for oppslag på ordre-type
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for oppslag på ordre-status
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på ordre-bruker
      *
     c     fusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fusrl1_user
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppdatering av ekstern ordre-hode
      *
     c     nohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    nohelu_fsys
     c                   kfld                    nohelu_tell
      *
      * Key for oppdatering av ordre-hode
      *
     c     fohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelu_numm
     c                   kfld                    fohelu_suff
      *
      * Key for oppslag på ordre-linje
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
     c                   kfld                    fodtl1_line
      *
     c     fodtl1_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
6.26  *
6.26 c     ausrl1_key    klist
6.26 c                   kfld                    ausrl1_user
7.01  *
7.01  * Key for oppslag på selger
7.01  *
7.01 c     ra09l1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    ra09l1_ikod
8.01  *
8.01  * Key for topp/bunn tekst linjer
8.01  *
8.01 c     fotxlu_key    klist
8.01 c                   kfld                    w_firm
8.01 c                   kfld                    fotxlu_numm
8.01 c                   kfld                    fotxlu_suff
8.01 c                   kfld                    fotxlu_line
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
      * Henter informasjon fra ordre-status
      *
     c     fstsl1_key    chain     fstsl1
5.53  *
5.53  * Hent mva % for beregning av memosaldo inkl. mva
5.53  *
5.53 c                   eval      w_stat = *blank
5.53 c                   eval      w_mvak = '3'
5.53 c                   time                    w_date
5.53  *
5.53 c                   call      'RS205R'
5.53 c                   parm                    w_stat
5.53 c                   parm                    w_mvak
5.53 c                   parm                    w_mvtx
5.53 c                   parm                    w_date
5.53 c                   parm                    w_mvas
5.53 c                   parm                    w_mvat
5.53 c                   parm                    w_mvaf
5.53 c                   parm                    w_bpro
6.26  *
6.26  * Henter opplysninger fra BRUKER-REGISTERENE
6.26  *
6.26 c                   eval      ausrl1_user = l_user
6.26 c     ausrl1_key    chain     ausrl1
6.26 c                   if        %found(ausrl1)
6.26 c                   eval      s_avde = absavd
6.26 c                   else
6.26 c                   eval      s_avde = 0
6.26 c                   endif
      *
     c                   endsr
