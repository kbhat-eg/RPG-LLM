      /TITLE  ASLAGR Korrigering av periode på historisk fakturaregister
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LX101R
      * Beskrivelse . . : Leser LEDI og oppdaterer LOHE og SIHE med meldingsnummer
      *                   fra EDI. Da kan vi linke inn til EDI
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       160112 bsa  Nytt program
6.nu  * R6.30 120614 bof  Gjennomgått mtp. nummerutvidelse
      *
8.01  *       220621 bsa  Utvidet meldingsnummer fra 6-8 siffer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fledilr    if   e           k disk    rename(ledipfr:ledilrr)
     fsihelk    uf   e           k disk    rename(sihepfr:sihelkr)
     flohelu    uf   e           k disk    rename(lohepfr:lohelur)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Definering av variable i nøkler
      *
     d sihelk_numm     s                   like(shnumm)
     d lohelu_numm     s                   like(lonumm)
     d lohelu_suff     s                   like(losuff)
     d w_firm          s                   like(l_firm)
      *
      *  Local Data Area
      *
     d                uds
     D l_firm                944    946  0
     d l_user                911    920
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser register, og tester om periode er feil
      *
     c     ledilr_key    setll     ledilr
     c     ledilr_key    reade     ledilr                                 90
     c                   dow       *in90 = *off
      * Hvis bestillingsnummer er funnet, sjekk om det ligger inne
     c                   if        linumm > 0 and
     c                             limlin = 100001
      * Sjekk først bestillinger
     c                   eval      lohelu_numm = linumm
     c                   eval      lohelu_suff = lisuff
     c     lohelu_key    chain     lohelu
     c                   if        %found
     c                   eval      lomnum = limnum
     c                   update    lohelur
     c                   endif
      * Sjekk også historikken
     c                   eval      sihelk_numm = linumm
     c     sihelk_key    setll     sihelk
     c     sihelk_key    reade     sihelk
     c                   dow       not %eof(sihelk)
     c                   if        lisuff = shsuff
     c                   eval      shmnum = limnum
     c                   update    sihelkr
     c                   endif
     c     sihelk_key    reade     sihelk
     c                   enddo
      *
     c                   endif
      *
     c     ledilr_key    reade     ledilr                                 90
     c                   enddo
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på edi
      *
     c     ledilr_key    klist
     c                   kfld                    w_firm
      *
      * Key for posisjonering på bestillingsregister
      *
     c     lohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lohelu_numm
     c                   kfld                    lohelu_suff
      *
      * Key for posisjonering på innkjøpsstatistikk
      *
     c     sihelk_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sihelk_numm
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
