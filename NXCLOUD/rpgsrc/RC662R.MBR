     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RC662R                                              *
      * Beskrivelse . . : Inkasso - oppdatering av returposter                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
T5.00 * R5.00     141102  Rettet timestampfeil.                           HFL *
T5.00 * R6.10     130912  Programmet skrevet om for ver. 6.10 uten merk.  ASK *
6.nu  * R6.30      190615 Endringer mtp nummerutvidelser                 bsa  *
7.01  * R6.30     110815  Lagt inn konnvertering av referanse på poster fra MGR
7.02  * R6.30     220816  Endret forskyvning på innhentet saksnummer      ask *
7.03  * R6.30 sst 020318  Tilpasset Svea som inkassobyrå  Konto rcoKTO/1506   *
7.04  * R6.30 sst 180618  Bilagskode 22 = Lindorf,  rc0bik = Svea      1506   *
7.05  * R6.30 sst 181218  Fjernet spes. hente vilagskode fra rc0bik    1506   *
7.06  * R6.30 sst 210319  Collectio,  Nytt format på innbetalinger
7.07  * R6.30 sst 140519  Innbet.  Lindorf ikke postere 'I', bare 'F'
7.08  * R6.30 sst 200519  Innbet.  Lindorf sammenslåing av saker
7.09  * R6.30 sst 250919  Innbet.  Lindorf rcn3fn = KORRR ikke behandl?
7.10  * R6.30 sst 141019  Innbet.  Lindorf rcn3fn = '0       ' rettes
7.11  * R6.30 sst 061119  Test om fakturanr er numerisk, hvis ikke 0000
7.12  * R6.30 sst 050220  Løsning på provisjon m/riktig postering Lindorf
7.13  * R6.30 sst 220120  Sjekk om prov.feltet er numerisk                    *
7.14  * R6.30 sst 090620  Legg ut melding om at Økonomi må oppdateres         *
7.15  * R6.30 sst 090620  Hoppe over records som mangler 'ZXCN' ifølge Sveinung
7.16  * R7.00 sst 281020  Bilagskode hentes alltid fra RCA0Pf hvis finnes.    *
7.16  * R7.00 sst 281020  Dersom det kjøres flere ink.firmaer må dette tilpasses hver gang.
7.17  * R7.00 sst 231120  Justert slik at I ikke legges ut som innbetaling  Bare F.
7.18  * R8.00 bsa 241022  Jobben går i postlås. Forsøker løse problemet.
      *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frcnrpf    up   e           k disk
     frcnflu    uf   e           k disk    rename(rcnfpfr:rcnflur)
7.08 frcnflr    uf   e           k disk    rename(rcnfpfr:rcnflrr)
7.08 frcnfl2    if   e           k disk    rename(rcnfpfr:rcnfl2r)
     frca0l1    if   e           k disk    rename(rca0pfr:rca0l1r)
     fraa3l1    if   e           k disk    rename(raa3pfr:raa3l1r)
     fra04l1    if   e           k disk    rename(ra04pfr:ra04l1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frjoupf    o    e           k disk
     frc662p    o    e             printer oflind(*in30)
      *
     d per             s              2  0 dim(13) ctdata perrcd(13)
7.13 d digit           c                   '0123456789.,'
      *
     d                uds
      *
7.14 d l_okon                  1      1
     d l_wsid                901    910
     d l_firm                944    946  0
     d l_fnav                951    980
     d l_user                911    920
      *
      * Returer - Kvitteringsretur for innleste saker
      *
     d                 ds
      *                   pos.
     d rcn1ld                        80
     d  rcn1rf                       20    overlay(rcn1ld)
      *                    01
     d   rcn1id                       4    overlay(rcn1rf)
      *                    05
     d   rcn1fg                       3    overlay(rcn1rf:5)
      *                    08
     d   rcn1fi                       3  0 overlay(rcn1rf:8)
      *                    11
     d   rcn1ku                       6  0 overlay(rcn1rf:11)
      *                    17(Ikke i bruk)
     d   rcn1lØ                       4    overlay(rcn1rf:17)
      *                    21
6.nu d  rcn1fr                       22    overlay(rcn1ld:21)
      *                    21
6.nu d   rcn1fa                       8  0 overlay(rcn1fr)
      *                    29
6.nu d** rcn1tx                      14    overlay(rcn1fr)
6.nu d   rcn1tx                      14    overlay(rcn1fr:9)
      *                    43
6.nu d**rcn1sn                        8    overlay(rcn1ld:43)
      *                    41
7.02 d  rcn1sn                        8    overlay(rcn1ld:41)
      *                    51
6.nu d  rcn1hs                       10  2 overlay(rcn1ld:51)
      *
      * Returer - Innbetalinger og avsluttede saker
7.06 d                 ds
     d rcn3ld                       130
     d  rcn3ty                        1    overlay(rcn3ld)
     d  rcn3kn                       12    overlay(rcn3ld:2)
     d  rcn3rf                       20    overlay(rcn3ld:14)
     d   rcn3id                       4    overlay(rcn3rf)
     d   rcn3fg                       3    overlay(rcn3rf:5)
     d   rcn3fi                       3  0 overlay(rcn3rf:8)
     d   rcn3ku                       6  0 overlay(rcn3rf:11)
     d   rcn3lØ                       4    overlay(rcn3rf:17)
     d  rcn3sn                        8    overlay(rcn3ld:34)
7.03 d  rcn3fn                        8  0 overlay(rcn3ld:42)
7.03 d  rcn3fx                        8    overlay(rcn3ld:42)
     d  rcn3hs                       10  2 overlay(rcn3ld:62)
     d  rcn3om                       10  2 overlay(rcn3ld:72)
     d  rcn3rt                       10  2 overlay(rcn3ld:82)
     d  rcn3pr                       10  2 overlay(rcn3ld:92)
     d  rcn3da                        8    overlay(rcn3ld:102)
     d  rcn3av                        1    overlay(rcn3ld:110)
     d  rcn3ov                        1    overlay(rcn3ld:111)
     d  rcn3f1                        9    overlay(rcn3ld:112)
7.08 d  rcn3ss               112    115
7.08 d  rcn3ns               116    123
      *
     d                 ds
     d wwdato                         8  0
     d  wwaar                         4  0 overlay(wwdato)
     d   wwaar2                       2  0 overlay(wwaar:3)
     d  wwmnd                         2  0 overlay(wwdato:5)
     d  wwdag                         2  0 overlay(wwdato:7)
      *
     d                 ds
      *       KEY-REKKEFØLGE PÅ RJOUPF (POSTER UT)
     d wssort                  2     37
     d  wsfirm                        3  0 overlay(wssort)
     d  wsbref                       12    overlay(wssort:4)
     d  wsbdat                        8  0 overlay(wssort:16)
     d   wsbaar                       4  0 overlay(wsbdat)
     d   wsbmnd                       2  0 overlay(wsbdat:5)
     d   wsbdag                       2  0 overlay(wsbdat:7)
      *
     d rcn40w          s             40
     d wttxt1          s             14
     d w_firm          s                   like(ra3fir)
     d w_alfa          s              1
610  d w_memb          s                   like(rjmemb)
     d rkunl1_kund     s                   like(rkkund)
610  d w_sumb          s                   like(rjneto)
7.04 d w_inkf          s             10
7.06 d w_belx          s             10
7.06 d i               s              5  0
7.06 d w_ant           s              5  0
      *
     d rcnflu_nref     s                   like(rcnref)
     d rcnflu_nsak     s                   like(rcnsak)
7.06  *
7.06 d rcnflr_firm     s                   like(rcnfir)
7.06 d rcnflr_kund     s                   like(rcnask)
7.06 d rcnflr_biln     s                   like(rcnasb)
7.08  *
7.08 d rcnfl2_firm     s                   like(rcnfir)
7.08 d rcnfl2_nsak     s                   like(rcnsak)
7.11  *
7.11 d digits          c                   '0123456789'
      *
      * Behandling
      *
     c                   eval      d1komm = *blank
     c                   eval      d2komm = *blank
     c                   eval      d3komm = *blank
7.06  *
7.06  * Spes Collectio,  Annet format på innbetalinger
7.06 c                   if        rc0inf = 'Collectio'
7.06 c                   select
7.06 c                   when      %subst(rcn130:1:3) = '01;'
7.06 c                   goto      neste
7.06  *
7.06 c                   when      %subst(rcn130:1:3) = '02;'
7.15  *
7.15 c                   if        %scan('ZXCN':rcn130) = 0
7.15 c                   goto      neste
7.15 c                   endif
7.15  *
7.06 c                   eval      rcn3ld = *blank
7.06 c                   eval      i = %scan('      ':rcn130)
7.06 c                   eval      %subst(rcn130:i:6) = '0;0;0;'
7.06  *
7.06 c                   eval      rcn3ty = 'I'
7.06  *
7.06 c                   eval      i = %scan(';':rcn130)
7.06 c                   eval      rcn130 = %subst(rcn130:i+1:130-i)
7.06  *   Kundenr hos inkassofirma
7.06 c                   eval      i = %scan(';':rcn130)
7.06 c                   eval      rcn3kn = %trim(%subst(rcn130:1:i-1))
7.06 c                   eval      rcn130 = %subst(rcn130:i+1:130-i)
7.06  *   Fakturanr
7.06 c                   eval      i = %scan(';':rcn130)
7.06 c                   eval      rcn3fx = %trim(%subst(rcn130:1:i-1)      )
7.06 c                   eval      rcn130 = %subst(rcn130:i+1:130-i)
7.06  *   Referans
7.06 c                   eval      i = %scan(';':rcn130)
7.06 c                   eval      rcn3rf = %trim(%subst(rcn130:1:i-1))
7.06 c                   eval      rcn130 = %subst(rcn130:i+1:130-i)
7.06  *   Dato
7.06 c                   eval      i = %scan(';':rcn130)
7.06 c                   eval      rcn3da = %trim(%subst(rcn130:1:i-1))
7.06 c                   eval      rcn130 = %subst(rcn130:i+1:130-i)
7.06  *   Beløp innbetalt
7.06 c                   eval      i = %scan(';':rcn130)
7.06 c                   eval      w_belx = %trim(%subst(rcn130:1:i-1))
7.06 c                   eval      rcn130 = %subst(rcn130:i+1:130-i)
7.06 c                   eval      rcn3hs = %dec(w_belx:10:2)
7.06  *   Beløp omkostninger
7.06 c*                  eval      i = %scan(';':rcn130)
7.06 c*                  eval      w_belx = %trim(%subst(rcn130:1:i-1))
7.06 c*                  eval      rcn130 = %subst(rcn130:i+1:130-i)
7.06 c*                  eval      rcn3om = %dec(w_belx:10:2)
7.06  *   Beløp renter
7.06 c                   eval      i = %scan(';':rcn130)
7.06 c                   eval      w_belx = %trim(%subst(rcn130:1:i-1))
7.06 c                   eval      rcn130 = %subst(rcn130:i+1:130-i)
7.06 c                   eval      rcn3rt = %dec(w_belx:10:2)
7.06  *   Beløp provisjon
7.06 c                   eval      i = %scan(';':rcn130)
7.06 c                   eval      w_belx = %trim(%subst(rcn130:1:i-1))
7.06 c                   eval      rcn130 = %subst(rcn130:i+1:130-i)
7.13 c                   if        %check(digit:w_belx) = 0
7.13 c                   eval      rcn3pr = %dec(w_belx:10:2)
7.13 c                   else
7.13 c                   eval      rcn3pr = *zero
7.13 c                   endif
7.06  *   Hvis Collectio mnagler saksnr.
7.06 c                   if        rcn3sn = *blank
7.06 c                   eval      rcnflr_kund = rcn3ku
7.06 c                   eval      rcnflr_biln = rcn3fn
7.06 c     rcnflr_key    chain     rcnflr
7.06 c                   if        %found(rcnflr)
7.06 c                   eval      rcn3sn = rcnsak
7.18 c                   unlock    rcnflr
7.06 c                   endif
7.06 c                   endif
     c                   goto      videre
7.06  *
7.06  *
7.06 c                   when      %subst(rcn130:1:3) = '03;'
7.06 c                   goto      neste
7.06  *
7.06 c                   other
7.06  *
7.06 c                   endsl
7.06 c                   endif
7.06  *
7.06 c                   movel     rcn130        rcn1ld
7.06 c                   movel     rcn130        rcn3ld
      *
7.01 c                   if        rcn1fg = 'MGR'
7.01 c                   eval      rcn1fg = 'G01'
7.01 c                   endif
7.01 c                   if        rcn3fg = 'MGR'
7.01 c                   eval      rcn3fg = 'G01'
7.01 c                   endif
      *
     c     videre        tag
      *
      * Behandle kvitteringretur
      *
      *
     c                   if        rcn1id = 'ZXCN'
     c                   exsr      subcn1
     c                   goto      neste
     c                   endif
      *
      * Behandle innbetaling og avslutning
      *
     c                   if        rcn3id <> 'ZXCN'
     c                   goto      neste
     c                   endif
7.09  *
7.09  *  Lindorf legger ut KORRR i fakturanr dersom noe er feil hos
7.09  *          dem.  Denne transen skal vel ikke vi ha selv om detrp
7.09  *          muligens skyldes en feil i det vi har sendt ut.
7.09  *
7.09 c                   if        rc0inf = 'Lindorf'
7.09 c                             and rcn3fx = 'KORRR'
7.09 c                   goto      neste
7.09 c                   endif
7.10  * Spes Lindorf, Legger ut '0       ' i faktnr. erstatte med 00000000
7.10 c                   if        rc0inf = 'Lindorf'
7.10 c                             and rcn3fx = '0       '
7.10 c                   eval      rcn3fx = '00000000'
7.10 c                   endif
      *
      * Korriger dato
      *
     c                   movel     rcn3da        w_alfa
     c                   if        w_alfa = 'K'
     c                   move      *zeros        rcn3da
     c                   move      *year         wwaar
     c                   move      *month        wwmnd
     c                   move      *day          wwdag
     c                   move      wwdato        rcn3da
     c                   endif
7.08  *
7.08  *   Sammenslåing av saker (Lindorf foreløpig)
7.08  *
7.08 c                   if        rcn3ss = 'S15'
7.08 c                   eval      rcnfl2_firm = l_firm
7.08 c                   eval      rcnfl2_nsak = rcn3sn
7.08 c     rcnfl2_key    setll     rcnfl2
7.08 c     rcnfl2_key    reade     rcnfl2
7.08 c                   dow       not %eof(rcnfl2)
7.08 c**********         unlock    rcnflu
7.08 c                   eval      rcnflr_kund = rcnask
7.08 c                   eval      rcnflr_biln = rcnasb
7.08 c     rcnflr_key    chain     rcnflr
7.08 c                   if        %found(rcnflr)
7.08 c                             and rcn3sn = rcnsak
7.08 c                   eval      rcnsak = rcn3ns
7.08 c                   update    rcnflrr
7.08 c                   endif
7.08 c     rcnfl2_key    reade     rcnfl2
7.08 c                   enddo
7.08 c                   goto      neste
7.08 c                   endif
      *
      *   Innbetaling / Avslutning
      *
7.06 c                   if           rcn3ty = 'F'
7.06 c                             or rcn3ty = 'I'
7.06 c                             or rcn3ty = 'A'
     c                   exsr      subcn3
     c                   endif
      *
      * Hvis innbetaling
      *
     c                   if        rcn3ty = 'F' or
     c                             rcn3ty = 'I'
     c                   exsr      subpos
     c                   endif
      *
     c     neste         tag
      *
     c                   delete    rcnrpfr
      *
      * Kvitterings-retur - Oppdater  saks-nr.
      *
     c     subcn1        begsr
      *
     c                   move      rcn1rf        rcnflu_nref
     c                   move      *blank        rcnflu_nsak
     c     rcnflu_key    setll     rcnflu
      *
     c     flerfa        tag
      *
     c                   eval      *in61 = *off
     c                   read      rcnflu                                 61
      *
     c                   if        *in61 = *on
     c                   goto      neste
     c                   endif
      *
     c                   if        rcn1rf = rcnref
      *
     c                   if        rcnasb = rcn1fa
     c                   move      rcn1sn        rcnsak
     c                   update    rcnflur
     c                   goto      skriv_kvit
     c                   endif
      *
     c                   goto      flerfa
      *
     c                   endif
      *
      * Skriv kvittering
      *
     c     skriv_kvit    tag
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   eval      *in30 = *off
     c                   endif
      *
     c                   move      rcn1ku        d2kund
     c                   move      rcn1sn        d2nsak
     c                   move      rcn1hs        d2stol
      *
     c                   move      rcn1ku        rkunl1_kund
     c     rkunl1_key    chain     rkunl1r                              60
     c                   movel     rknavn        d2navn
     c                   eval      d2komm = 'Retur saksnummer'
      *
     c                   write     d2det
      *
     c                   endsr
      *
      *  INNBETALINGER/AVSLUTTNINGER
      *                                      sven
     c     subcn3        begsr
      *
     c                   move      rcn3rf        rcnflu_nref
     c                   move      rcn3sn        rcnflu_nsak
7.11  *
7.11 c                   if        %check(digits : rcn3fx) > 0
7.11 c                   eval      rcn3fn = *zero
7.11 c                   endif
      *
     c     rcnflu_key    setll     rcnflu
     c     rcnflu_key    reade     rcnflu                                 61
      *
     c                   dow       *in61 = *off
      *
7.06 c                   if           rcn3ty = 'F'
7.06 c                             or rcn3ty = 'I'
     c                   if        %subst(rcn3ld:42:8) = *blank
     c                   eval      rcn3fn = *zero
     c                   endif
     c                   if        rcnasb =  rcn3fn
      *
     c                   eval      rcninb = rcninb + rcn3hs
     c                   eval      rcnomk = rcnomk
     c                   eval      rcnrnt = rcn3rt
     c                   eval      rcnprv = rcn3pr
T5.00c                   move      rcn3da        wwdato
T5.00c                   move      wwdato        rcndat
     c                   update    rcnflur
     c                   endif
     c                   endif
      *
     c                   if        rcn3ty = 'A'
     c                   move      rcn3ty        rcnsta
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   eval      *in30 = *off
     c                   endif
      *
     c                   move      rcn3ku        d3kund
     c                   move      rcn3sn        d3nsak
     c                   eval      d3stol = rcnsto
      *
     c                   move      rcn3da        wwdato
T5.00c                   move      wwdato        rjbdat
     c     *dmy          move      rjbdat        d3bdat
      *
     c                   move      rcn3ku        rkunl1_kund
     c     rkunl1_key    chain     rkunl1r                              60
     c                   movel     rknavn        d3navn
     c                   eval      d3komm = 'Saken er avsluttet'
      *
     c                   write     d3det
      *
S5.00c*    *dmy          move      rcn3da        rcndat
T5.00c                   move      rcn3da        wwdato
T5.00c                   move      wwdato        rcndat
     c                   update    rcnflur
      *
     c                   endif
      *
     c     endcn3        tag
      *
     c     rcnflu_key    reade     rcnflu                                 61
      *
     c                   enddo
      *
     c                   endsr
      *
     c/eject
      *
      *  INNBETALINGER - Generer poster til regnskap/reskontro
      *
     c     subpos        begsr
      *
      * Innbetaling til kunde
      *
     c                   eval      w_sumb = 0
      *
      *  Start F/I
     c                   if           rcn3ty = 'F'
7.07 c                             or rcn3ty = 'I'
     c                   move      'K'           rjreid
     c                   move      w_firm        rjfirm
     c                   move      w_firm        wsfirm
     c                   move      rcn3ku        rjkont
     c                   move      rcn3ku        rjresn
     c                   eval      rjbiln = *zeros
     c                   move      rcn3fn        rjrefn
     c                   eval      rjmvak = *blank
     c                   eval      rjbetb = *zeros
     c                   move      rcn3sn        wttxt1
     c                   movel     wttxt1        rjtext
     c                   eval      rjtype = *zero
     c                   move      rcn3da        wwdato
S5.00c*    *dmy          move      rcn3da        rjbdat
T5.00c                   move      wwdato        rjbdat
     c                   move      rcn3da        wsbdat
     c                   move      rcn3da        rjbref
     c                   move      rcn3da        wsbref
      *
     c                   move      per(wwmnd)    rjrper
     c                   move      wwaar2        rjraar
      *
     c                   if        wwmnd < ra3stp
     c                   eval      rjraar = rjraar - 1
     c                   endif
      *
     c                   move      wwaar         wsbaar
     c                   move      wwmnd         wsbmnd
     c                   move      wwdag         wsbdag
     c                   move      rcn3da        wsbdat
     c                   move      wssort        rjsort
      *                                                                 .
     c                   if        rcn3hs <> *zero
7.07 c                   if        rcn3ty = 'I'
7.07 c                             and rc0inf = 'Lindorf'
7.07 c                   goto      ikkeinnb
7.07 c                   endif
      *                                                                 .
     c                   if        rc0bik <> *zero
7.04 c*                  if        rc0inf = 'Svea  '
7.04 c                   move      rc0bik        rjbilk
7.04 c*                  else
7.04 c*                  move      22            rjbilk
7.04 c*                  endif
7.04 c                   else
     c                   move      87            rjbilk
     c                   endif
      *                                                                 .
     c                   move      870000        rjbiln
     c                   eval      rjneto = 0 - rcn3hs
     c                   eval      w_sumb = w_sumb + rcn3hs
     c                   move      870000        wssort
     c                   move      wssort        rjsort
610  c                   eval      rjnivo = *blank
610  c                   eval      rjmemb = w_memb
610  c                   time                    rjedat
610  c                   time                    rjetim
610  c                   eval      rjeusr = l_user
7.14 c                   eval      l_okon = 'J'
     c                   write     rjoupfr
7.07 c                   endif
     c** Slutt HS
     c                   endif
7.07 c     ikkeinnb      tag
      *
      * Skal renter behandles ?
      * Reskontro føring av renter er tatt bort.
      *
     c** Start Rente
     c                   if        rc0prt = 'J'
      *
      * Innbetalt rente til kundereskontro
      *
     c                   if        rcn3rt <> *zero
     c                   eval      rjreid = 'K'
     c                   eval      rjrefn = *zeros
      *                                                                 .
     c                   if        rc0bik <> *zero
9.04 c                   if        w_inkf = 'Svea    '
     c                   move      rc0bik        rjbilk
9.04 c                   else
9.04 c                   move      22            rjbilk
9.04 c                   endif
     c                   else
     c                   move      87            rjbilk
     c                   endif
      *                                                                 .
     c                   move      rcn3ku        rjkont
     c                   move      rcn3ku        rjresn
     c                   move      970000        rjbiln
     c                   eval      rjneto = 0 - rcn3rt
     c                   eval      w_sumb = w_sumb + rcn3rt
     c                   move      rcn3da        wwdato
     c                   move      wwaar         wsbaar
     c                   move      wwmnd         wsbmnd
     c                   move      wwdag         wsbdag
     c                   move      rcn3da        wsbdat
     c                   move      wssort        rjsort
     c                   move      970000        wssort
     c                   move      wssort        rjsort
T5.00c                   move      wwdato        rjbdat
     c                   move      per(wwmnd)    rjrper
     c                   move      wwaar2        rjraar
610  c                   eval      rjnivo = *blank
610  c                   eval      rjmemb = w_memb
610  c                   time                    rjedat
610  c                   time                    rjetim
610  c                   eval      rjeusr = l_user
     c*                  write     rjoupfr
      *
      * Rentebelastning til kundereskontro
      *
     c                   eval      rjreid = 'K'
     c                   eval      rjrefn = *zeros
     c                   move      97            rjbilk
     c                   move      970010        rjbiln
     c                   eval      rjneto = rcn3rt
     c                   move      970010        wssort
     c                   move      wssort        rjsort
610  c                   eval      rjnivo = *blank
610  c                   eval      rjmemb = w_memb
610  c                   time                    rjedat
610  c                   time                    rjetim
610  c                   eval      rjeusr = l_user
     c*                  write     rjoupfr
      *
      * Rente til hovedbok
      *
     c                   eval      rjreid = 'H'
     c                   eval      rjkont = radk11
     c                   eval      rjspes = rads11
     c                   eval      rjavde = rada11
     c                   eval      rjrefn = *zeros
     c                   move      97            rjbilk
     c                   move      970000        rjbiln
     c                   eval      rjneto = 0 - rcn3rt
     c                   move      970010        wssort
     c                   move      wssort        rjsort
610  c                   eval      rjnivo = *blank
610  c                   eval      rjmemb = w_memb
610  c                   time                    rjedat
610  c                   time                    rjetim
610  c                   eval      rjeusr = l_user
7.14 c                   eval      l_okon = 'J'
     c                   write     rjoupfr
     c** Slutt Rente     endif
     c                   endif
      *
     c** Slutt Rente     endif
     c                   endif
      *
      * Provisjon til hovedbok
      *
     c** Start Prov      if        rcn3pr <> *zero
     c                   if        rcn3pr <> *zero
     c                   eval      rjreid = 'H'
     c                   eval      rjkont = radk19
     c                   eval      rjspes = rads19
     c                   eval      rjavde = rada19
     c                   eval      rjrefn = *zeros
     c                   move      83            rjbilk
     c                   move      830000        rjbiln
     c                   eval(m)   rjmvab = rcn3pr * 25 / 100
     c                   eval      rjneto = rcn3pr + rjmvab
7.12 c                   if        rc0inf = 'Lindorf'
7.12 c                   eval      w_sumb = w_sumb - rcn3pr
7.12 c                   else
7.12 c                   eval      w_sumb = w_sumb + rcn3pr
7.12 c                   endif
     c                   move      830000        wssort
     c                   move      wssort        rjsort
610  c                   eval      rjnivo = *blank
610  c                   eval      rjmemb = w_memb
610  c                   time                    rjedat
610  c                   time                    rjetim
610  c                   eval      rjeusr = l_user
7.14 c                   eval      l_okon = 'J'
     c                   write     rjoupfr
     c                   endif
      *
      * Netto til kundereskontro
      *
     c                   eval      rjneto = w_sumb
      *
     c                   if        rjneto <> *zero
     c                   eval      rjreid = 'K'
9.04 c                   if        rcn3kn = 'K49258'
9.04 c                   eval      rjkont = 1505
9.04 c                   eval      rjbilk = 22
9.04 c                   else
     c                   move      rc0bik        rjbilk
     c                   eval      rjkont = rc0kto
9.04 c                   endif
     c                   eval      rjspes = *zeros
     c                   eval      rjavde = *zeros
     c                   eval      rjrefn = *zeros
     c                   move      840000        rjbiln
     c                   eval      rjmvab = *zeros
     c                   move      840000        wssort
     c                   move      wssort        rjsort
610  c                   eval      rjnivo = *blank
610  c                   eval      rjmemb = w_memb
610  c                   time                    rjedat
610  c                   time                    rjetim
610  c                   eval      rjeusr = l_user
7.14 c                   eval      l_okon = 'J'
     c                   write     rjoupfr
     c                   endif
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   eval      *in30 = *off
     c                   endif
      *
     c                   move      rcn3ku        d1kund
     c                   move      rcn3sn        d1nsak
7.06 c                   if        rjbdat <> *loval
     c     *dmy          move      rjbdat        d1bdat
7.06 c                   else
     c                   move      rcn3da        wwdato
7.06 c                   move      wwdato        rjbdat
7.06 c     *dmy          move      rjbdat        d1bdat
7.06 c                   endif
     c                   move      rcn3hs        d1stol
     c                   move      rcn3rt        d1rent
     c                   move      rcn3pr        d1prov
      *
     c                   move      rcn3ku        rkunl1_kund
     c     rkunl1_key    chain     rkunl1r                              60
     c                   movel     rknavn        d1navn
      *
     c                   write     d1det
      *  Slutt F/I
     c**                 endif
      *
     c                   endsr
      *
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
610  c     *entry        plist
610  c                   parm                    w_memb
7.04 c                   parm                    w_inkf
      *
     c                   eval      wttxt1 = *blank
     c                   movel     'Saknr.  '    wttxt1
      *
      * Legge inn firma
      *
     c                   move      l_firm        w_firm
      *
      * Hent STATUSKODE 3
      *
     c     raa3l1_key    chain     raa3l1                             61
      *
     c                   if        *in61 = *off
     c                   move      ra3p01        per(01)
     c                   move      ra3p02        per(02)
     c                   move      ra3p03        per(03)
     c                   move      ra3p04        per(04)
     c                   move      ra3p05        per(05)
     c                   move      ra3p06        per(06)
     c                   move      ra3p07        per(07)
     c                   move      ra3p08        per(08)
     c                   move      ra3p09        per(09)
     c                   move      ra3p10        per(10)
     c                   move      ra3p11        per(11)
     c                   move      ra3p12        per(12)
     c                   move      ra3p13        per(13)
     c                   endif
      *
     c                   if        *in61 = *on
     c                   move      01            ra3stp
     c                   endif
      *
      * Hent faste konti
      *
     c     ra04l1_key    chain     ra04l1                             61
      *
      * Hent statuskode inkasso
      *
     c     rca0l1_key    chain     rca0l1                             61
      *
     c                   write     a1hdr
      *
      * Key for rapporterings-file
      *
     c     rcnflu_key    klist
     c                   kfld                    rcnflu_nref
     c                   kfld                    rcnflu_nsak
7.06  *
7.06  * Key for rapporterings-file
7.06  *
7.06 c     rcnflr_key    klist
7.06 c                   kfld                    rcnflr_firm
7.06 c                   kfld                    rcnflr_kund
9 07 c                   kfld                    rcnflr_biln
7.08  *
7.08  * Key for rapporterings-file
7.08  *
7.08 c     rcnfl2_key    klist
7.08 c                   kfld                    rcnfl2_firm
7.08 c                   kfld                    rcnfl2_nsak
      *
      * Key for statuskode 3
      *
     c     raa3l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for faste konti
      *
     c     ra04l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for statuskode inkasso
      *
     c     rca0l1_key    klist
     c                   kfld                    w_firm
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
     c                   endsr
      *
** TAB - NORMALT REGNSKAPSÅR
01020304050607080910111213
