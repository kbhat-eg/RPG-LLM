     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FP750R
      * Beskrivelse . . : Spesialpris, innlesing fra fil - parameter
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       121101 HKO  Nytt program
      *       020603 HKO  Lagt inn valg av rabattkategori
      *       191003 HKO  Lagt inn valg av format
      *       270105 HKO  Lagt inn NIB prisformat
      *       110405 HKO  Lagt inn påslag på leveringstype
      *       230207 BOF  Lagt inn Proff partner prisformat
5.70  * PRGR  151108 BOF  Utvidet med prisgruppe
6.30  * PRGR  110515 BOF  Utvidet med BM csv format
6.31  *       140519 BOF  Utvidet med BM hirarki csv format
7.xx  * 7.00  290916 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  *       141220 bof  Ikke krav om input på kunde/rkat (info i csv)
8.01  *PRG2   100622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra06l1    if   e           k disk    rename(ra06pfr:ra06l1r)
     fra30l1    if   e           k disk    rename(ra30pfr:ra30l1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
      *
     ffp750d    cf   e             workstn
      *
      * Definering av variabler i nøkler
      *
     d ra06l1_fkod     s                   like(rafkod)
5.70 d ra30l1_dkod     s                   like(redkod)
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d vltpl1_lety     s                   like(vylety)
      *
      * Definering av Local data area
      *
     d                uds
6.31 d l_filg                931    933
     d l_firm                944    946  0
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(rkfirm)
     d w_rtxt          s                   like(raftxt)
     d w_knav          s                   like(rknavn)
     d w_pnav          s                   like(flnavn)
     d w_besk          s                   like(vybesk)
      *
6.21 d u_631           s              1    inz(*off)                            Endring 7.10
7.01 d u_701           s              1    inz(*off)                            Endring 7.01
      *
6.31  * Definering av eksterne prg
6.31  *
6.31   dcl-s co402_verdi1  char(1);
6.31   dcl-s co402_verdi2  char(2048);
6.31   DCL-PR CO402R EXTPGM('CO402R');
6.31     p_filgrp            char(3)     const;
6.31     p_firm              packed(3:0) const;
6.31     p_lager             packed(2:0) const;
6.31     p_nokkel            char(50)    const;
6.31     p_verdi1            char(1);
6.31     p_verdi2            char(2048);
6.31   END-PR;
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle åpningsbildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Behandler bildet
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endsl
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
      * Kaller program for henting av data, avbryter dersom feil
      *
     c                   if        b1fldr = *blank
     c                   eval      b1fldr = 'PRIS_INN'
     c                   endif
      *
     c                   call      'FP750C'
     c                   parm                    b1file
     c                   parm                    b1fldr
     c                   parm                    b_feil
      *
     c                   if        b_feil = *on
     c                   exfmt     b1msg
     c                   goto      b1taga
     c                   endif
      *
      * Kaller styreprogram
      *
     c                   select
     c                   when      b1form = 1
     c                   call      'FP751R'
     c                   parm                    w_firm
     c                   parm                    b1prgr
     c                   parm                    b1rkat
     c                   parm                    b1kund
     c                   parm                    b1kpro
     c                   parm                    b1slet
     c                   when      b1form = 2
     c                   call      'FP752R'
     c                   parm                    w_firm
     c                   parm                    b1prgr
     c                   parm                    b1rkat
     c                   parm                    b1kund
     c                   parm                    b1kpro
     c                   parm                    b1slet
     c                   when      b1form = 3
     c                   call      'FP753R'
     c                   parm                    w_firm
     c                   parm                    b1prgr
     c                   parm                    b1rkat
     c                   parm                    b1kund
     c                   parm                    b1kpro
     c                   parm                    b1slet
     c                   parm                    b1let1
     c                   parm                    b1pas1
     c                   parm                    b1let2
     c                   parm                    b1pas2
     c                   parm                    b1let3
     c                   parm                    b1pas4
     c                   parm                    b1let4
     c                   parm                    b1pas4
     c                   when      b1form = 4
     c                   call      'FP754R'
     c                   parm                    w_firm
     c                   parm                    b1prgr
     c                   parm                    b1rkat
     c                   parm                    b1kund
     c                   parm                    b1kpro
     c                   parm                    b1slet
6.30 c                   when      b1form = 5
6.30 c                   call      'FP755R'
6.30 c                   parm                    w_firm
6.30 c                   parm                    b1prgr
6.30 c                   parm                    b1rkat
6.30 c                   parm                    b1kund
6.30 c                   parm                    b1kpro
6.30 c                   parm                    b1slet
6.31 c                   when      b1form = 6
6.31 c                   if        u_631 = *on
6.31 c                   call      'FP762R'
6.31 c                   parm                    w_firm
6.31 c                   parm                    b1prgr
6.31 c                   parm                    b1rkat
6.31 c                   parm                    b1kund
6.31 c                   parm                    b1kpro
6.31 c                   parm                    b1slet
6.31 c                   endif
     c                   endsl
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Prisgruppe
      *
     c                   if        w_afld = 'B1PRGR'
     c                   call      'RA530R'
     c                   parm                    w_firm
     c                   parm                    b1prgr
     c                   parm                    w_knav
     c                   leavesr
     c                   endif
      *
      * Rabattkategori
      *
     c                   if        w_afld = 'B1RKAT'
     c                   call      'RA506R'
     c                   parm                    w_firm
     c                   parm                    b1rkat
     c                   parm                    w_knav
     c                   leavesr
     c                   endif
      *
      * Kunde
      *
     c                   if        w_afld = 'B1KUND'
     c                   eval      b1kpro = 0
     c                   call      'RK500R'
     c                   parm                    w_firm
     c                   parm                    b1kund
     c                   parm                    w_knav
     c                   leavesr
     c                   endif
      *
      * Kundeprosjekt
      *
     c                   if        w_afld = 'B1KPRO'
     c                   call      'FL500R'
     c                   parm                    w_firm
     c                   parm                    b1kund
     c                   parm                    b1kpro
     c                   parm                    w_knav
     c                   parm                    w_pnav
     c                   leavesr
     c                   endif
      *
      * Leveringstype
      *
     c                   if        w_afld = 'B1LET1'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    b1let1
     c                   parm                    w_besk
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'B1LET2'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    b1let2
     c                   parm                    w_besk
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'B1LET3'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    b1let3
     c                   parm                    w_besk
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'B1LET4'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    b1let4
     c                   parm                    w_besk
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
7.01 c                   if        u_701 = *off
     c                   if        b1rkat = 0 and
     c                             b1kund = 0
     c                   eval      *in31 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b1rkat <> 0 and
     c                             b1kund <> 0
     c                   eval      *in32 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
7.01 c                   else
7.01 c                   if        b1form <> 5
7.01 c                   if        b1rkat = 0 and
7.01 c                             b1kund = 0
7.01 c                   eval      *in31 = *on
7.01 c                   eval      b_feil = *on
7.01 c                   leavesr
7.01 c                   endif
7.01  *
7.01 c                   if        b1rkat <> 0 and
7.01 c                             b1kund <> 0
7.01 c                   eval      *in32 = *on
7.01 c                   eval      b_feil = *on
7.01 c                   leavesr
7.01 c                   endif
7.01 c                   endif
7.01 c                   endif
      *
     c                   if        b1prgr <> *blank
     c                   eval      ra30l1_dkod = b1prgr
     c     ra30l1_key    chain     ra30l1                             41
     c                   if        *in41 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b1rkat <> 0
     c                   eval      ra06l1_fkod = b1rkat
     c     ra06l1_key    chain     ra06l1                             33
     c                   if        *in33 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b1kund <> 0
     c                   eval      rkunl1_kund = b1kund
     c     rkunl1_key    chain     rkunl1                             34
     c                   if        *in34 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b1kpro <> 0
     c                   eval      fkprl1_kund = b1kund
     c                   eval      fkprl1_kpro = b1kpro
     c     fkprl1_key    chain     fkprl1                             35
     c                   if        *in35 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b1file = *blank
     c                   eval      *in36  = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        b1let1 <> 0
     c                   eval      vltpl1_lety = b1let1
     c     vltpl1_key    chain     vltpl1
     c                   if        not %found
     c                   eval      *in37 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b1let2 <> 0
     c                   eval      vltpl1_lety = b1let2
     c     vltpl1_key    chain     vltpl1
     c                   if        not %found
     c                   eval      *in38 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b1let3 <> 0
     c                   eval      vltpl1_lety = b1let3
     c     vltpl1_key    chain     vltpl1
     c                   if        not %found
     c                   eval      *in39 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        b1let4 <> 0
     c                   eval      vltpl1_lety = b1let4
     c     vltpl1_key    chain     vltpl1
     c                   if        not %found
     c                   eval      *in40 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på rabattkategori
      *
     c     ra06l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra06l1_fkod
      *
      * Key for oppslag på prisgruppe
      *
     c     ra30l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra30l1_dkod
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
      * Key for oppslag på leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
6.31  *
6.31  * Endring 6.31
6.31  *
6.31   CallP CO402R(l_filg:w_firm:0:'FP750R_631':
6.31                    co402_verdi1:co402_verdi2);
6.31   if co402_verdi1 = '1';
6.31     u_631 = *on;
6.31     *in50 = *on;
6.31   endif;
7.01  *
7.01  * Endring 7.01
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'FP750R_701':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_701 = *on;
7.01   endif;

      *
      *
      * Initierer skjermbildet
      *
     c                   eval      b1form = 1
     c                   eval      b1fldr = 'PRIS_INN'
      *
     c                   endsr
