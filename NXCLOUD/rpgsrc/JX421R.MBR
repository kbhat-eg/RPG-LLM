     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JX421R
      * Beskrivelse . . : Vare-pris, slett priser for leverandør
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       030605 HKO  Nytt program
6.30  *       160714 bof  Utvidelse av nøkkel vare-pris
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvarl5    if   e           k disk    rename(jvarpfr:jvarl5r)
     fjvprl2    uf   e           k disk    rename(jvprpfr:jvprl2r)
     fjvprlu    uf   e           k disk    rename(jvprpfr:jvprlur)
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_pgiv          s                   like(jxldor)
     d p_ldor          s                   like(jvldor)
      *
      * Definering av variabler i nøkler
      *
     d jvarl5_ldor     s                   like(jvldor)
     d jvprl2_ldor     s                   like(jxldor)
     d jvprlu_vare     s                   like(jxvare)
     d jvprlu_ldor     s                   like(jxldor)
      *
      * Definering av variable
      *
     d b_inlr          s              1    inz(*off)
      *
     d w_firm          s              3  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Sletter priser uavhengig av vareeier
      *
     c                   if        p_ldor = 0
     c                   eval      jvprl2_ldor = p_pgiv
     c     jvprl2_key    setll     jvprl2
     c     jvprl2_key    reade     jvprl2r
     c                   dow       not %eof
     c                   delete    jvprl2r
     c     jvprl2_key    reade     jvprl2r
     c                   enddo
     c                   goto      avslutt
     c                   endif
      *
      * Sletter priser med test mot valgt vareeier
      *
     c                   eval      jvarl5_ldor = p_ldor
     c     jvarl5_key    setll     jvarl5
     c     jvarl5_key    reade     jvarl5
     c                   dow       not %eof
      *
     c                   eval      jvprlu_vare = jvvare
     c                   eval      jvprlu_ldor = p_pgiv
     c     jvprlu_key    setll     jvprlu
     c     jvprlu_key    reade     jvprlur
     c                   dow       not %eof
     c                   delete    jvprlur
     c     jvprlu_key    reade     jvprlur
     c                   enddo
      *
     c     jvarl5_key    reade     jvarl5
     c                   enddo
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_pgiv
     c                   parm                    p_ldor
      *
      * Key for les på vare
      *
     c     jvarl5_key    klist
     c                   kfld                    jvarl5_ldor
      *
      * Key for les og sletting av vare-pris
      *
     c     jvprl2_key    klist
     c                   kfld                    jvprl2_ldor
      *
      * Key for les og sletting av vare-pris
      *
     c     jvprlu_key    klist
     c                   kfld                    jvprlu_vare
     c                   kfld                    jvprlu_ldor
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
