      /TITLE  ASOFAK: Inntasting av parameter for ordre-utskrift
     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO600R
      * Beskrivelse . . : Inntasting av parameter for ordre-utskrift
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       090301 AMD  Programmet er skrevet om til standard ILE
      * R3.31 090301 AMD  Lagt inn melding dersom bruker ikke har tilgang
      *                   til å sette igang fakturering
5.01  * R5.01 060502 AMD  Lagt inn melding dersom det forsøkes gjenutskrift
5.01  *                   av pakkseddel, og bruker ikke har lov til det.
5.02  * R5.02 140502 AMD  Bygget inn brukerstyrt valg av tekster i bildet
5.02  *                   for utskrift av interne ordrepapirer
5.03  * R5.03 150502 AMD  Kode for Felles / direkte / jobbkø, hentes fra
5.03  *                   ordretype, og legges inn i skjermbildet.
5.04  * R5.04 211002 AMD  Lagt inn logikk for sending av e-mail
5.41  * R5.41 160605 AMD  Endring av kontroll på skrevet pakkseddel
      * 6.10  240609 BSA  Oppdatert med sporingsinformasjon
pdf   * 6.10  150610 BSA  Håndteringen av mail skjer nå fra parameterbilde 2
pdf   *                   for POA aktiverte rutiner.
6.11  * 6.10  051010 BSA  Må kunne skille mellom ny og gammel rutine for å legge
6.11  *                   ut e-mail.
6.20  * 6.20  220813 BSA  Send feilmelding hvis email er valgt og PROA er aktivert
6.NU  * R6.30 260514 bsa  Programmet gjennomgått mtp nummerutvidelser.
7.xx  * 7.00  290116 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  111119 bsa  Mulighet for å sende ordrebekreftelse på EDI
7.02  * 7.00  140220 bsa  Mulighet for EXCEL
7.03  * 7.00  280120 bsa  Mulighet for å sende ordrebekreftelse uten pris
7.04  * 7.00  050320 bsa  Endret sjekk ved lesing av loggposter.
7.05  * 7.00  060320 bsa  Valg ved utlegg tok ikke høyde for valg 5.
7.06  * 7.00  200320 bsa  Justere sjekk mot om den har kommet elektronisk.
7.07  * 7.00  150420 bsa  Switchstyre endring 7.03
7.08  * K000  260520 bsa  Feil i sjekk mot avsender og EDI
7.09  * K000  190121 bsa  Legger ut default '1' som verdi ved utskrift
7.09  * K000              av tilbud. (Skattum)
7.10  * K000  090210 bof  Hvis faktura/kreditnota og direkte så skal
7.10  * K000              fakturatype settes lik 0
7.11  * K000  301116 ask  Logger dersom tilbud (Ordretype T) skrives ut.
      *
8.02  * K000  051121 bsa  Sørger for at det ikke er tomme linjer på
8.02  * K000              valg.
8.03  * K000  060122 bsa  Skal akseptere å skrive ut ordrebekreftelse
8.03  * K000              uten at bestilling er mottatt elektronisk.
8.03  * K000              Ligger som spesial pga denne endring 8.03
8.04  * K000  070923 bsa  Ikke legg ut kvittering hvis overføring går
8.04  * K000              som den skal.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     ffo600d    cf   e             workstn
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
5.02 fvbldl1    if   e           k disk    rename(vbldpfr:vbldl1r)
     ffohelu    uf   e           k disk    rename(fohepfr:fohelur)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffplolu    uf a e           k disk    rename(fplopfr:fplolur)
     ffplol1    if   e           k disk    rename(fplopfr:fplol1r)
3.31 ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
7.01 fedhel2    if   e           k disk    rename(edhepfr:edhel2r)
7.01 frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
7.01 fflogl3    if   e           k disk    rename(flogpfr:flogl3r)
      *
      * Definering av local data area
      *
     d                uds
pdf  d l_poff                584    584
pdf  d l_ruti                800    809
     d l_wsid                901    910
     d l_user                911    920
pdf  d l_filg                931    933
     d l_sfir                944    946  0
pdf  d l_avde                947    950  0
      *
      * Definering av parametere
      *
     d p_firm          s                   like(fofirm)
6.NU d p_numm          s                   like(fonumm)
     d p_suff          s                   like(fosuff)
     d p_ipor          s                   like(w_alf1)
6.11 d p_ruti          s                   like(l_ruti)
6.11 d p_pdf           s              1
      *
      * Definering av variable i nøkler
      *
     d w_firm          s                   like(fofirm)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fplol1_numm     s                   like(fkponr)
     d fplol1_suff     s                   like(fkpsuf)
     d votyl1_otyp     s                   like(vaotyp)
5.02 d vbldl1_balt     s                   like(vabalt)
3.31 d fusrl1_user     s                   like(fbuser)
7.01 d edhel2_fsys     s                   like(eofsys)
7.01 d edhel2_numm     s                   like(eonumm)
7.01 d rkunl1_kund     s                   like(rkkund)
7.01 d flogl3_aarr     s                   like(fuaarr)
7.01 d flogl3_numm     s                   like(funumm)
      *
7.01 d                 ds
7.01 d d_urec                        80
7.01 d  d_firm                        3  0 overlay(d_urec)
7.01 d  d_aarr                        4  0 overlay(d_urec:4)
7.01 d  d_numm                        8  0 overlay(d_urec:8)
7.01 d  d_suff                        2  0 overlay(d_urec:16)
7.01 d  d_kode                        1    overlay(d_urec:18)
7.01 d  d_date                         d   overlay(d_urec:19) datfmt(*iso)
7.01 d  d_time                         t   overlay(d_urec:29) timfmt(*iso)
7.01 d  d_user                       10    overlay(d_urec:37)
7.01 d  d_wsid                       10    overlay(d_urec:47)
      *
      * Definering av variable
      *
     d w_alf1          s              1
     d w_skor          s              1
     d w_sjek          s              1
     d w_outq          s             10
     d w_otyp          s                   like(footyp)
     d w_palt          s                   like(fkpalt)
     d w_svar          s                   like(fkpdir)
6.11 d w_ruti          s                   like(l_ruti)
7.01 d w_stat          s              1
7.01 d w_memb          s             10
7.01 d w_stmp_alf      s             26
7.01 d w_stmp          s               z
7.02 d w_fgrp          s              3
7.02 d w_user          s             10
7.11 d w_aktp          s             15
7.11 d w_sgru          s             60
7.11 d w_slko          s              2  0
7.11 d w_kkko          s              2  0
8.04 d w_ssta          s              1
      *
      * Eksternt program
7.00   dcl-s co402_verdi1  char(1);
7.00   dcl-s co402_verdi2  char(2048);
7.00   DCL-PR CO402R EXTPGM('CO402R');
7.00     p_filgrp            char(3)     const;
7.00     p_firm              packed(3:0) const;
7.00     p_lager             packed(2:0) const;
7.00     p_nokkel            char(50)    const;
7.00     p_verdi1            char(1);
7.00     p_verdi2            char(2048);
7.00   END-PR;
7.02 d u_702           s              1    inz(*off)
7.07 d u_707           s              1    inz(*off)
7.09 d u_709           s              1    inz(*off)
7.10 d u_710           s              1    inz(*off)                            Nulle fakturatype
8.03 d u_803           s              1    inz(*off)                            Elektr EDI
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Velg utfra ORDRETYPE hvilket bilde som skal vises
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
      *  Hent opplysninger fra ORDRE-HODE-REGISTER
      *
     c     fohel1_key    chain     fohel1r                            90
     c                   if        *in90 = *off
     c                   eval      w_otyp = footyp
     c                   endif
      *
      *  Hent opplysninger fra UTPLUKK-PARAM-REGISTER
      *       (Ordretype hentes kun om den finnes plukket tidligere)
      *
     c     fplol1_key    chain     fplol1r                            90
     c                   if        *in90 = *off
     c                   eval      w_otyp = fkpoty
     c                   endif
      *
      *  Hent opplysninger fra ORDRETYPE-REGISTER
      *
     c                   eval      votyl1_otyp = w_otyp
     c     votyl1_key    chain     votyl1r                            90
5.03  *
      *  Utfra ordretype (akk.type) hopp til riktig bilde
      *
     c     vaoakk        cabeq     0             e2tag
     c     vaoakk        cabeq     1             e3tag
     c     vaoakk        cabeq     2             e4tag
     c     vaoakk        cabeq     3             e5tag
      *
     c                   goto      xslutt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  E2 Behandler valg for utskrift av TILBUD
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     e2tag         tag
      *
6.11 c                   eval      w_ruti = 'FTILB    '
6.11 c                   exsr      sjk_proa
      *
      *  Nullstiller felter som benyttes
      *
     c                   eval      e2valg = *zero
7.09 c                   if        u_709 = *on
7.09 c                   eval      e2valg = 1
7.09 c                   endif
     c                   eval      e2svar = vaofdj
     c                   eval      e2outq = *blank
      *
     c     e2taga        tag
     c                   exfmt     e2bld
      *
      *  F3=Avslutt
      *
     c                   if        *inkc  = *on
     c                   eval      p_ipor = '1'
     c                   goto      xslutt
     c                   endif
      *
      *  Sjekk om skriver er en gyldig device
      *
     c                   if        e2outq <> *blank
      *
     c                   eval      w_sjek = *blank
     c                   call      'FQPRTC'
     c                   parm                    w_sjek
     c                   parm                    e2outq
      *
     c                   if        w_sjek <> *blank
     c                   eval      *in31 = *on
     c                   goto      e2taga
     c                   endif
     c                   endif
6.20  * Send feilmelding hvis valg 3 og PROA er aktivert.
6.20 c                   if        *in35  = *on and
6.20 c                             e2svar = 3
6.20 c                   eval      *in36 = *on
6.20 c                   goto      e2taga
6.20 c                   endif
      *
      *  Legger opplysninger over i felles-felter
      *
     c                   eval      w_palt = e2valg
     c                   eval      w_svar = e2svar
     c                   eval      w_outq = e2outq
5.04  *
5.04  *  Styring til e-mail
5.04  *
5.04 C                   if        e2svar = 3
5.04 C                   eval      e2svar = 1
5.04 C                   eval      e2outq = 'E_MAIL_ASP'
5.04 c                   eval      w_svar = e2svar
5.04 c                   eval      w_outq = e2outq
5.04 C                   endif
7.02  *
7.02  *  Legger ut en excel-fil
7.02  *
7.02 c                   if        e2svar = 5
7.02 c                   eval      e2svar = 0
7.02 c                   eval      w_svar = e2svar
7.02 c                   eval      w_outq = e2outq
7.02 c                   eval      w_user = l_user
7.02 c                   eval      w_fgrp = l_filg
7.02 c                   call      'FO606C'
7.02 c                   parm                    p_firm
7.02 c                   parm                    p_numm
7.02 c                   parm                    p_suff
7.02 c                   parm                    w_user
7.02 c                   parm                    w_fgrp
7.02 c                   goto      xslutt
7.02 c                   endif
7.11  *
7.11  *  Dersom ordretype T logges oppretting av tilbud i loggfile
7.11  *
7.11 c                   if        footyp = 'T '
7.11 c                   eval      w_aktp = 'UTSKRIFT '
7.11 c                   eval      w_slko = 0
7.11 c                   eval      w_kkko = 0
7.11 c                   eval      w_sgru = *blank
7.11  *
7.11 c                   call      'XL700R'
7.11 c                   parm                    fonumm
7.11 c                   parm                    w_aktp
7.11 c                   parm                    w_slko
7.11 c                   parm                    w_kkko
7.11 c                   parm                    w_sgru
7.11 c                   endif
      *
     c                   goto      start
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  E3 Behandler valg for utskrift av ORDREBEKREFTELSE/PLUKKLISTE
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     e3tag         tag
      *
6.11 c                   eval      w_ruti = 'FOBEK    '
6.11 c                   exsr      sjk_proa
      *
      *  Nullstiller felter som benyttes
      *
     c                   eval      e3valg = *zero
     c                   eval      e3svar = vaofdj
     c                   eval      e3outq = *blank
5.02 c                   eval      e3btx1 = *blank
5.02 c                   eval      e3btx2 = *blank
5.02  *
5.02  *  Henter inn tekster fra fil for bilde-
5.02  *  alternativer dersom det er benyttet
5.02  *
5.02 c                   eval      vbldl1_balt = vaobld
5.02 c     vbldl1_key    chain     vbldl1r
5.02  *
5.02 c                   if        %found
5.02 c                   if        vabtx1 <> *blank
5.02 c                   eval      e3btx1 = '0=' + vabtx1
5.02 c                   endif
5.02 c                   if        vabtx2 <> *blank
5.02 c                   eval      e3btx2 = '1=' + vabtx2
5.02 c                   endif
5.02 c                   else
5.02 c                   eval      e3btx1 = '0=Ordrebekreftelse'
5.02 c                   eval      e3btx2 = '1=Plukkliste'
5.02 c                   endif
8.02  * Denne er flyttet
7.07 c                   if        u_707 = *on
8.02 c                   if        e3btx2= *blanks
8.02 c                   eval      e3btx2 = '2=Ord.bekr. uten pris'
8.02 c                   else
7.03 c                   eval      e3btx3 = '2=Ord.bekr. uten pris'
8.02 c                   endif
7.07 c                   endif
      *
     c     e3taga        tag
     c                   exfmt     e3bld
      *
      *  F3=Avslutt
      *
     c                   if        *inkc  = *on
     c                   eval      p_ipor = '1'
     c                   goto      xslutt
     c                   endif
5.02  *
5.02  *  Sjekk om alternativ 1 er gyldig,
5.02  *  eller om bare 0 skal kunne benyttes
5.02  *
5.02 c                   if        e3btx2 = *blank
5.02 c                   if        e3valg = 1
5.02 c                   eval      *in34  = *on
5.02 c                   goto      e3taga
5.02 c                   endif
5.02 c                   endif
      *
      *  Sjekk om skriver er en gyldig device
      *
     c                   if        e3outq <> *blank
      *
     c                   eval      w_sjek = *blank
     c                   call      'FQPRTC'
     c                   parm                    w_sjek
     c                   parm                    e3outq
      *
     c                   if        w_sjek <> *blank
     c                   eval      *in31 = *on
     c                   goto      e3taga
     c                   endif
     c                   endif
6.20  * Send feilmelding hvis valg 3 og PROA er aktivert.
6.20 c                   if        *in35  = *on and
6.20 c                             e3svar = 3
6.20 c                   eval      *in36 = *on
6.20 c                   goto      e3taga
6.20 c                   endif
7.01  *
7.01  * Sjekk om ordre kan sendes som EDI
7.01  *
7.01 c                   if        e3svar = 4
7.01 c                   exsr      sjk_EDIbest
8.03 c**                 if        *in37 = *on or
8.03 c**                           *in38 = *on
8.03 c                   if        *in37 = *on
7.01 c                   goto      e3tag
7.01 c                   endif
8.03 c                   if        *in38 = *on and
8.03 c                             u_803 = *off
8.03 c                   goto      e3tag
8.03 c                   endif
7.01 c                   endif
      *
      *  Legger opplysninger over i felles-felter
      *
     c                   eval      w_palt = e3valg
     c                   eval      w_svar = e3svar
     c                   eval      w_outq = e3outq
5.04  *
5.04  *  Styring til e-mail
5.04  *
5.04 C                   if        e3svar = 3
5.04 C                   eval      e3svar = 1
5.04 C                   eval      e3outq = 'E_MAIL_ASP'
5.04 C                   eval      w_svar = e3svar
5.04 C                   eval      w_outq = e3outq
5.04 C                   endif
7.02  *
7.02  *  Legger ut en excel-fil
7.02  *
7.02 c                   if        e3svar = 5
7.02 c                   eval      e3svar = 0
7.02 c                   eval      w_svar = e3svar
7.02 c                   eval      w_outq = e3outq
7.02 c                   eval      w_user = l_user
7.02 c                   eval      w_fgrp = l_filg
7.02 c                   call      'FO606C'
7.02 c                   parm                    p_firm
7.02 c                   parm                    p_numm
7.02 c                   parm                    p_suff
7.02 c                   parm                    w_user
7.02 c                   parm                    w_fgrp
7.02 c                   goto      xslutt
7.02 c                   endif
      *
     c                   goto      start
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  E4 Behandler valg for utskrift av PAKKSEDDEL
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     e4tag         tag
      *
6.11 c                   eval      w_ruti = 'FPAKK    '
6.11 c                   exsr      sjk_proa
      *
      *  Nullstiller felter som benyttes
      *
     c                   eval      e4svar = vaofdj
     c                   eval      e4outq = *blank
      *
     c     e4taga        tag
     c                   exfmt     e4bld
      *
      *  F3=Avslutt
      *
     c                   if        *inkc  = *on
     c                   eval      p_ipor = '1'
     c                   goto      xslutt
     c                   endif
5.01  *
5.01  *  Kontroll om bruker har lov til å starte
5.01  *  gjenutskrift av pakkseddel
5.01  *
5.41 c                   if        fbko09 <> 1
5.01 c                   if        fopaks =  1
5.01 c                   eval      *in33  = *on
5.01 c                   goto      e4taga
5.01 c                   endif
5.01 c                   endif
      *
      *  Sjekk om skriver er en gyldig device
      *
     c                   if        e4outq <> *blank
      *
     c                   eval      w_sjek = *blank
     c                   call      'FQPRTC'
     c                   parm                    w_sjek
     c                   parm                    e4outq
      *
     c                   if        w_sjek <> *blank
     c                   eval      *in31 = *on
     c                   goto      e4taga
     c                   endif
     c                   endif
      *
      *  Legger opplysninger over i felles-felter
      *
     c                   eval      w_palt = *zero
     c                   eval      w_svar = e4svar
     c                   eval      w_outq = e4outq
      *
     c                   goto      start
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  E5 Behandler valg for utskrift av FAKTURA/KREDITNOTA
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     e5tag         tag
      *
6.11 c                   eval      w_ruti = 'FFAKK    '
6.11 c                   exsr      sjk_proa
      *
      *  Nullstiller felter som benyttes
      *
     c                   eval      e5svar = vaofdj
     c                   eval      e5outq = *blank
      *
     c     e5taga        tag
     c                   exfmt     e5bld
      *
      *  F3=Avslutt
      *
     c                   if        *inkc  = *on
     c                   eval      p_ipor = '1'
     c                   goto      xslutt
     c                   endif
3.31  *
3.31  *  Kontroll om bruker har lov til å sette igang fakturering.
3.31  *
3.31 c                   if        fbko08 <> 1
3.31 c                   eval      *in33  = *on
3.31 c                   goto      e5taga
3.31 c                   endif
      *
      *  Sjekk om skriver er en gyldig device
      *
     c                   if        e5outq <> *blank
      *
     c                   eval      w_sjek = *blank
     c                   call      'FQPRTC'
     c                   parm                    w_sjek
     c                   parm                    e5outq
      *
     c                   if        w_sjek <> *blank
     c                   eval      *in31 = *on
     c                   goto      e5taga
     c                   endif
     c                   endif
7.10  *
7.10  *  Hvis direkte utskrift,skal fakturatype settes lik 0
7.10  *
7.10 c                   if        u_710 = *on
7.10 c                   if        e5svar = 1
7.10 c     fohel1_key    chain     fohelur
7.10 c                   if        %found(fohelu)
7.10 c                   eval      fofaty = 0
7.10 c                   update    fohelur
7.10 c                   endif
7.10 c                   endif
7.10 c                   endif
      *
      *  Legger opplysninger over i felles-felter
      *
     c                   eval      w_palt = *zero
     c                   eval      w_svar = e5svar
     c                   eval      w_outq = e5outq
      *
     c                   goto      start
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  START Behandler oppdatering av ORDRE-HODE-REGISTER
      *        samt utlegging/oppdatering av UTPLUKK-PARAM-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     start         tag
      *
      *  Oppdater ORDRE-HODE-REGISTER, ordren er nå til behandling
      *
     c                   if        fokode <> 3
     c                   call      'FO709R'
     c                   parm                    fohel1_numm
     c                   parm                    fohel1_suff
     c                   endif
      *
      *  Dersom ordren allerede er plukket, skal ikke koden endres
      *
     c     fohel1_key    chain     fohelur                            90
     c                   if        *in90  = *off
      *
     c                   if        fokode <> 3
     c                   eval      fokode = 2
     c                   endif
      *
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   update    fohelur
     c                   endif
      *
      *  Legg ut nye records til  ORDRE-PLUKK-HODE-REGISTER
      *  Eventuelt oppdater, dersom ordren allerede er plukket
      *
     c     fplol1_key    chain     fplolur                            90
     c                   if        *in90  = *on
     c                   eval      fkpfir = w_firm
     c                   eval      fkponr = fplol1_numm
     c                   eval      fkpsuf = fplol1_suff
     c                   eval      fkpoty = w_otyp
     c                   eval      fkpwsd = l_wsid
     c                   eval      fkpusr = l_user
     c                   eval      fkpalt = w_palt
     c                   eval      fkpdir = w_svar
     c                   write     fplolur
     c                   else
     c                   eval      fkpalt = w_palt
     c                   eval      fkpdir = w_svar
     c                   update    fplolur
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Kaller opp utskriftsprogrammet, dersom direkte utskrift
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   if        w_svar = 1 or
     c                             w_svar = 2
     c                   call      'FO604R'
     c                   parm                    w_skor
     c                   parm                    w_firm
     c                   parm                    fplol1_numm
     c                   parm                    fplol1_suff
     c                   parm                    w_otyp
     c                   parm                    w_svar
     c                   parm                    w_outq
     c                   endif
7.01  * Hvis EDI, kalles eget program
7.01 c                   if        w_svar = 4
7.01  *
7.01 c                   eval      flogl3_aarr = %subdt(fobdat:*years)
7.01 c                   eval      flogl3_numm = fonumm
7.01 c     flogl3_key    setll     flogl3
7.01 c     flogl3_key    reade     flogl3
7.04 c                   if        not %found or
7.04 c                             funumm <> fonumm
7.01 c                   goto      send
7.01 c                   endif
7.01  *
7.01 c                   dow       not %eof
7.01  *
7.01 c                   if        fukode = '1'
7.01 c                   eval      a1stat = 'Elektr. bekr. sendt - 10=Omkjøring'
7.01 c                   exsr      overf_stat
7.01 c                   if        *inkj = *on
7.01 c                   goto      send
7.01 c                   endif
7.01 c                   else
7.01 c                   goto      send
7.01 c                   endif
7.01  *
7.01 c     flogl3_key    reade     flogl3
7.01 c                   enddo
7.01  *
7.01 c                   goto      no_send
7.01  *
7.01 c     send          tag
7.01  *
7.01 c                   eval      w_stat = '0'
8.04 c                   eval      w_ssta = '0'
7.01 c                   exsr      crt_memb
7.01  *
8.03 c                   if        *in38 = *off
7.01 c                   call      'LO765C'
7.01 c                   parm                    w_memb
7.01 c                   parm                    fplol1_numm
7.01 c                   parm                    fplol1_suff
7.01  *
8.03 c                   else
8.03  *
8.03 c                   call      'LO769C'
8.03 c                   parm                    w_memb
8.03 c                   parm                    fplol1_numm
8.03 c                   parm                    fplol1_suff
8.03  *
8.03 c                   endif
7.01  * Kall FTP overføring
7.01 c                   call      'LO766C'
7.01 c                   parm                    w_memb
7.01 c                   parm                    w_stat
7.01  *
8.04 c                   eval      w_ssta = w_stat
7.01  *
7.01 c                   if        w_stat = *off
7.01 c                   eval      a1stat = 'Ordrebekreftelse er sendt!'
7.01 c                   else
7.01 c                   eval      a1stat = 'Ordrebekreftelse er IKKE sendt!'
7.01 c                   endif
7.01  *
7.01 c                   exsr      oppd_logg
7.01  *
8.04 c                   if        w_ssta <> *off
7.01 c                   exsr      overf_stat
8.04 c                   endif
7.01  *
7.01 c     no_send       tag
7.01  * Sett behandlingskode tilbake
7.01 c     fohel1_key    chain     fohelur
7.01 c                   if        %found
7.01 c                   eval      fokode = 0
7.01 c                   time                    foedat
7.01 c                   time                    foetim
7.01 c                   eval      foeusr = l_user
7.01 c                   update    fohelur
7.01 c                   endif
7.01  *
7.01 c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
6.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.11  *  Subrutine for initiering av program
6.11  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.11  *
6.11 c     sjk_proa      begsr
6.11  *
6.20 c                   eval      *in36 = *off
6.11 c                   eval      p_pdf ='0'
6.11 c                   eval      p_ruti= w_ruti
6.11  * Sjekk om PROA er aktivert
6.11 c                   call      'AP608R'
6.11 c                   parm                    p_ruti
6.11 c                   parm                    p_pdf
6.11  * p_pdf = '1' = PROA aktivert
6.11 c                   if        p_pdf = '1'
6.11 c                   eval      *in35 = *on
6.11 c                   endif
6.11  *
6.11 c                   endsr
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *  Subrutine for sjekk om ordre kan sendes på EDI
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01 c     sjk_EDIbest   begsr
7.01  *
7.01 c                   eval      *in37 = *off
7.01 c                   eval      *in38 = *off
7.01  * Hent inn info fra kunde, og sjekk om alt er på "stell".
7.01 c                   eval      rkunl1_kund = fokund
7.01 c     rkunl1_key    chain     rkunl1
7.01 c                   if        %found
7.01 c                   if        rkeank = 0
7.01 c                   eval      *in37 = *on
7.01 c                   endif
7.01 c                   if        rkordb <> 1
7.01 c                   eval      *in37 = *on
7.01 c                   endif
7.01  * Sjekk om ordren er kommet via EDI bestilling
7.01 c                   eval      edhel2_fsys = 'EDI'
7.01 c                   eval      edhel2_numm = fonumm
7.01 c     edhel2_key    chain     edhel2
7.01 c                   if        not %found
7.06 c                   eval      edhel2_fsys = 'SMK'
7.06 c     edhel2_key    chain     edhel2
7.06 c                   if        not %found
7.06 c                   eval      edhel2_fsys = 'EHF'
7.06 c     edhel2_key    chain     edhel2
7.06 c                   if        not %found
7.01 c                   eval      *in38 = *on
7.01 c                   endif
7.08 c**                 else
7.08 c**                 eval      *in37 = *on
7.06 c                   endif
7.06 c                   endif
7.01 c                   endif
7.01  *
7.01 c                   endsr
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *  Subrutine for vise status overføring
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01 c     overf_stat    begsr
7.01  * Hent inn mottaker
7.01 c                   eval      a1kund = 0
7.01 c                   eval      a1ktxt = *blanks
7.01 c                   eval      rkunl1_kund = fokund
7.01 c     rkunl1_key    chain     rkunl1
7.01 c                   if        %found
7.01 c                   eval      a1ktxt = rknavn
7.01 c                   eval      a1kund = fokund
7.01 c                   endif
7.01  *
7.01 c                   exfmt     a1bld
7.01  *
7.01 c                   endsr
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *  Subrutine for bygging av file-member
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01 c     crt_memb      begsr
7.01  *
7.01 c                   time                    w_stmp
7.01 c                   move      w_stmp        w_stmp_alf
7.01 c                   eval      w_memb = 'W' +
7.01 c                                      %subst(w_stmp_alf:12:2) +
7.01 c                                      %subst(w_stmp_alf:15:2) +
7.01 c                                      %subst(w_stmp_alf:18:2) +
7.01 c                                      %subst(w_stmp_alf:21:3)
7.01  *
7.01 c                   endsr
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *  Subrutine for å opprette logg for ordrebekreftelse
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01 c     oppd_logg     begsr
7.01  *
7.01  * Oppdaterer loggfile med at
7.01  * ordrebekreftelse er kjørt
7.01  *
7.01 c                   eval      d_firm = l_sfir
7.01 c                   eval      d_user = l_user
7.01 c                   eval      d_wsid = l_wsid
7.01 c                   eval      d_aarr = %subdt(fobdat:*years)
7.01 c                   eval      d_numm = fonumm
7.01 c                   eval      d_suff = fosuff
7.01 c                   eval      d_kode = '1'
7.01 c                   time                    d_date
7.01 c                   time                    d_time
7.01  *
7.01  * Kaller program for oppdatering
7.01  *
7.01 c                   call      'FU900R'
7.01 c                   parm                    w_stat
7.01 c                   parm                    d_urec
7.01  *
7.01 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     c                   eval      w_skor = *blank
     c                   eval      w_sjek = *blank
     c                   eval      w_outq = *blank
pdf  c                   eval      *in35 = *off
      *
      * Key for file : ORDRE-TYPE-REGISTER
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
5.02  *
5.02  * Key for file : BILDE-ALTERNATIVER
5.02  *
5.02 c     vbldl1_key    klist
5.02 c                   kfld                    w_firm
5.02 c                   kfld                    vbldl1_balt
      *
      * Key for file : ORDRE-HODE-REGISTER
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for file : ORDRE-PLUKK-HODE-REGISTER
      *
     c     fplol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fplol1_numm
     c                   kfld                    fplol1_suff
3.31  *
3.31  * Key for brukerregister
3.31  *
3.31 c     fusrl1_key    klist
3.31 c                   kfld                    w_firm
3.31 c                   kfld                    fusrl1_user
7.01  *
7.01  * Key for file : ORDRE-HODE-REGISTER
7.01  *
7.01 c     edhel2_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    edhel2_fsys
7.01 c                   kfld                    edhel2_numm
7.01  *
7.01  * Key for file : Kunde-REGISTER
7.01  *
7.01 c     rkunl1_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    rkunl1_kund
7.01  *
7.01  * Key for file : Logg-REGISTER
7.01  *
7.01 c     flogl3_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    flogl3_aarr
7.01 c                   kfld                    flogl3_numm
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_ipor
     c                   parm                    p_firm
6.NU c                   parm                    p_numm
     c                   parm                    p_suff
      *
      *  Legger inn firmanummer i nøkklene
      *
     c                   eval      w_firm      = p_firm
      *
      *  Legger inn ordrenummer og suffix i nøkklene
      *
     c                   eval      fohel1_numm = p_numm
     c                   eval      fohel1_suff = p_suff
     c                   eval      fplol1_numm = p_numm
     c                   eval      fplol1_suff = p_suff
3.31  *
3.31  * Hente inn brukerregister
3.31  *
3.31 c                   eval      fusrl1_user = l_user
3.31 c     fusrl1_key    chain     fusrl1r                            90
      *
      * Nullstiller returvalg
      *
     c                   eval      p_ipor = *blank
      *
7.02  *
7.02  * Endring 7.02
7.02  *
7.02   CallP CO402R(l_filg:w_firm:0:'FO600R_702':
7.02                    co402_verdi1:co402_verdi2);
7.02   if co402_verdi1 = '1';
7.02     u_702 = *on;
7.02     *in40 = *on;
7.02   endif;
7.07  *
7.07  * Endring 7.07 - Ordrebekreftelse uten pris
7.07  *
7.07   CallP CO402R(l_filg:w_firm:0:'FO600R_707':
7.07                    co402_verdi1:co402_verdi2);
7.07   if co402_verdi1 = '1';
7.07     u_707 = *on;
7.07   endif;
7.09  *
7.09  * Endring 7.09 - Legger default ut verdi 1 som valg på utskrift av tilbud
7.09  *
7.09   CallP CO402R(l_filg:w_firm:0:'FO600R_709':
7.09                    co402_verdi1:co402_verdi2);
7.09   if co402_verdi1 = '1';
7.09     u_709 = *on;
7.09   endif;
7.10  *
7.10  * Hvis faktura/kreditnota og direkte skal sette fakturatype = 0
7.10  *
7.10   CallP CO402R(l_filg:w_firm:0:'FO600_710':
7.10   co402_verdi1:co402_verdi2);
7.10   if co402_verdi1 = '1';
7.10     u_710 = *on;
7.10   endif;
8.03  *
8.03  * Skal det aksepteres å sende elektronisk ordrebekreftelse uten
8.03  * å ha mottatt elektronisk bestilling.
8.03  *
8.03   CallP CO402R(l_filg:w_firm:0:'FO600_803':
8.03   co402_verdi1:co402_verdi2);
8.03   if co402_verdi1 = '1';
8.03     u_803 = *on;
8.03   endif;
      *
     c                   endsr
