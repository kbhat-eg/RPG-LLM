      /TITLE  ASOKON Sp√∏rring v.h.a. rapportter (Utvalg)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : AF708R
      * Program . . . . : Samle filer til ett member
      * Beskrivelse . . : Samler filer fra FTP henting til ett member
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       150702 MHA  Nytt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     faflfpf    if a e             disk      rename(aflfpf:aflfpfr)
     faftpl1    if a e           k disk      rename(aftppfr:aftpl1r)
      *
      * Definering av parametre
     d p_status        s              1
     d p_navn          s             10a
     d p_tfil          s             10a
     d p_tmbr          s             10a
      *
     d                uds
     d l_firm                944    946  0
      *
      * Definering av variabler
     d w_pos           s              2s 0
     d w_fil           s             10a
     d w_mbr           s             10a
     d w_pcfil         s             50a
     d w_cmdstr        s            200a
     d w_cmdlen        s             15p 5 inz(200)
     d w_tmpfil        s                   like(w_fil) inz(*blank)
      *
     d aftpl1_navn     s                   like(afnavn)
      *
     d w_firm          s                   like(affirm)
      *
      /eject
     c     aftpl1_key    chain     aftpl1r
     c                   if        not %found
     c                   eval      p_status = 'N'
     c                   goto      slutt
     c                   endif
     c                   if        %trim(aftype) <> 'GET' or
     c                             affilt = *blank or
     c                             aflibt = 'J'
     c                   eval      p_status = 'E'
     c                   goto      slutt
     c                   endif
      *
      * Henter ut filnavn og membernavn
      *
     c                   eval      w_pos = %scan('.' : affilt)
     c                   if        w_pos > 0
     c                   eval      p_tfil = %trim(
     c                             %subst(affilt : 1 : w_pos - 1))
     c                   eval      p_tmbr = %trim(
     c                             %subst(affilt : w_pos + 1))
     c                   else
     c                   eval      p_tfil = %trim(
     c                             %subst(affilt : 1 : w_pos - 1))
     c                   eval      p_tmbr = '*FIRST'
     c                   endif
      *
     c                   if        aftype = 'A'
     c                   exsr      AS2AS
     c                   else
     c                   exsr      PC2AS
     c                   endif
      *
     c                   if        %error
     c                   eval      p_status = 'E'
     c                   endif
      *
     c     slutt         tag
     c                   eval      *inlr = *on
     c                   return
      *
      *
      *
     c     AS2AS         begsr
      *
      * Looper evig til %EOF
     c                   dou       p_navn = *blank
     c                   read      aflfpfr
     c                   if        %eof
     c                   if        w_tmpfil <> *blank
     c                   eval      w_cmdstr = 'DLTF FILE(*CURLIB/' +
     c                             %trim(w_tmpfil) + ')'
     c                   call(e)   'QCMDEXC'
     c                   parm                    w_cmdstr
     c                   parm                    w_cmdlen
     c                   endif
     c                   leave
     c                   endif
      *
     c                   eval      w_pos = %scan('.' : SrcDta)
     c                   eval      w_fil = %trim(
     c                             %subst(SrcDta : 1 : w_pos - 1))
     c                   eval      w_mbr = %trim(
     c                             %subst(SrcDta : w_pos + 1))
      *
      * Kopiere member til felles fil/member
     c                   eval      w_cmdstr = 'CPYF FROMFILE(*CURLIB/' +
     c                             %trim(w_fil) + ') TOFILE(*CURLIB/' +
     c                             %trim(p_tfil) + ') FROMMBR(' +
     c                             %trim(w_mbr) + ') TOMBR(' +
     c                             %trim(p_tmbr) + ') MBROPT(*ADD) ' +
     c                             'FMTOPT(*NOCHK)'
     c                   call(e)   'QCMDEXC'
     c                   parm                    w_cmdstr
     c                   parm                    w_cmdlen
      * Slette opprinnelig member
     c                   eval      w_cmdstr = 'RMVM FILE(*CURLIB/' +
     c                             %trim(w_fil) + ') MBR(' +
     c                             %trim(w_mbr) + ')'
     c                   call(e)   'QCMDEXC'
     c                   parm                    w_cmdstr
     c                   parm                    w_cmdlen
      *
     c                   if        w_tmpfil <> w_fil and
     c                             w_tmpfil <> *blank
     c                   eval      w_cmdstr = 'DLTF FILE(*CURLIB/' +
     c                             %trim(w_tmpfil) + ')'
     c                   call(e)   'QCMDEXC'
     c                   parm                    w_cmdstr
     c                   parm                    w_cmdlen
     c                   endif
     c                   eval      w_tmpfil = w_fil
     c                   enddo
      *
     c                   endsr
      *
      *
      *
     c     PC2AS         begsr
      *
      * Looper evig til %EOF
      *
     c                   dou       p_navn = *blank
     c                   read      aflfpfr
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      w_pcfil = %trim(SrcDta)
      *
      * Kopiere pcfil til felles fil/member
      *
     c                   eval      w_cmdstr = 'CPYFRMSTMF FROMSTMF(''' +
     c                             '/temp/' + %trim(w_pcfil) + ''') TOMBR(''' +
     c                             '/qsys.lib/' + %trim(aflocd) +
     c                             '.lib/' + %trim(p_tfil) + '.file/' +
     c                             %trim(p_tmbr) + '.mbr'') MBROPT(' +
     c                             '*ADD)'
     c                   call(e)   'QCMDEXC'
     c                   parm                    w_cmdstr
     c                   parm                    w_cmdlen
      *
      * Slette opprinnelig member
      *
     c                   eval      w_cmdstr = 'DEL OBJLNK(''/temp/' +
     c                             %trim(w_pcfil) + ''')'
     c                   call(e)   'QCMDEXC'
     c                   parm                    w_cmdstr
     c                   parm                    w_cmdlen
      *
     c                   enddo
     c
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
     c     *entry        plist
     c                   parm                    p_navn
     c                   parm                    p_tfil
     c                   parm                    p_tmbr
     c                   parm                    p_status
      *
     c     aftpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    aftpl1_navn
      *
     c                   eval      aftpl1_navn = p_navn
     c                   eval      w_firm = l_firm
      *
     c                   endsr
      *
