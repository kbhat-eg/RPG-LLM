     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VE722R
      * Beskrivelse . . : Henter GTIN+lev.varenr ut fra gitt varenr + enhet + leverandør
      *
      *                   Input : firma, leverandør, varenr., enhet
      *                   Output: GTIN nr,Lev.varenr., status (1 = ok)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       020715 bof  Nytt program (tatt utg.punkt i VE713R
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarlr    if   e           k disk    rename(vvarpfr:vvarlrr)
     fvveplr    if   e           k disk    rename(vveppfr:vveplrr)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjvpkl1    if   e           k disk    rename(jvpkpfr:jvpkl1r)
     fjlevl3    if   e           k disk    rename(jlevpfr:jlevl3r)
     fjvlel1    if   e           k disk    rename(jvlepfr:jvlel1r)
      *
      * Definering av variable i nøkler
      *
     d vvarlr_firm     s                   like(vvfirm)
     d vvarlr_vare     s                   like(vvvare)
     d vveplr_pfir     s                   like(vepfir)
     d vveplr_pvar     s                   like(vepvar)
     d vveplr_penh     s                   like(vepenh)
     d vveplr_pldo     s                   like(vepldo)
     d vvepl1_pfir     s                   like(vepfir)
     d vvepl1_pvar     s                   like(vepvar)
     d vvepl1_penh     s                   like(vepenh)
     d jvpkl1_vare     s                   like(jwvare)
     d jvpkl1_ldor     s                   like(jwldor)
     d jvarl1_vare     s                   like(jvvare)
     d jlevl3_enum     s                   like(jlenum)
     d jvlel1_lvnr     s                   like(jvlvnr)
     d jvlel1_lldo     s                   like(jvlldo)
      *
      * Definering av pamametre
      *
     d p_firm          s                   like(vepfir)
     d p_eann          s                   like(vepgti)
     d p_vare          s                   like(vepvar)
     d p_ldor          s                   like(vvldor)
     d p_enhe          s                   like(vepenh)
     d p_lvar          s                   like(vvlvar)
     d p_stat          s              1
      *
     d w_ldor          s                   like(vvldor)
      *
     d                uds
     d l_user                911    920
     d l_sfir                944    946  0
     d l_savd                947    950  0
     d l_lagr               1001   1002  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer output-parametre
      *
     c                   eval      p_eann = 0
     c                   eval      p_lvar = *blank
     c                   eval      p_stat = *blank
      *
     c                   eval      vvarlr_firm = p_firm
     c                   eval      vvarlr_vare = p_vare
     c     vvarlr_key    chain     vvarlr
     c                   if        not %found(vvarlr)
     c                   goto      sjk_VA
     c                   endif
      *
     c                   if        p_ldor <> 0
      * Henter GTIN kode fra VVEP på angitt enhet og leverandør
      *
     c                   eval      vveplr_pfir = p_firm
     c                   eval      vveplr_pvar = p_vare
     c                   eval      vveplr_penh = p_enhe
     c                   eval      vveplr_pldo = p_ldor
     c     vveplr_key    chain     vveplr
     c                   if        %found(vveplr) and
     c                             vepgti <> 0
     c                   eval      p_eann = vepgti
     c                   eval      p_stat = '1'
     c                   goto      les_lvar
     c                   endif
     c                   endif
      *
      * Sjekk en gang til på VVEP uavhengig av leverandør, men med angitt enhet
      *
     c                   eval      vvepl1_pfir = p_firm
     c                   eval      vvepl1_pvar = p_vare
     c                   eval      vvepl1_penh = p_enhe
     c     vvepl1_key    setll     vveplr
     c     vvepl1_key    reade     vveplr                                 91
     c                   dow       *in91 = *off
      * Har vi match på leverandør ?
     c                   if        p_enhe = vepenh and
     c                             vepgti <> 0
6.10 c                   eval      p_eann = vepgti
6.10 c                   eval      p_stat = '1'
6.10 c                   goto      les_lvar
     c                   endif
     c     vvepl1_key    reade     vveplr                                 91
     c                   enddo
      *
     c                   goto      sjk_gtinVA
      *
      * -----------------------------------------
      * VA håndtering                           *
      * -----------------------------------------
      *
     c     sjk_VA        tag
      *
     c                   eval      jvarl1_vare = p_vare
     c     jvarl1_key    chain     jvarl1
     c                   if        not %found(jvarl1)
     c                   goto      les_lvar
     c                   endif
      *
     c     sjk_gtinVA    tag
      *
      * Hvis ikke funnet - sjekk mot vareadministrasjon.
      * Først forutsetter vi match på leverandør
      *
     c                   eval      jvpkl1_vare = p_vare
     c                   eval      jvpkl1_ldor = w_ldor
     c     jvpkl1_key    setll     jvpkl1
     c     jvpkl1_key    reade     jvpkl1
     c                   dow       not %eof(jvpkl1)
     c                   if        jwldor = w_ldor and
     c                             jwenhe = p_enhe and
     c                             jwgtin <> 0
      * Har vi match på enhet, hent enheten fra
     c                   eval      p_eann = jwgtin
     c                   eval      p_stat = '1'
     c                   goto      les_lvar
     c                   endif
     c     jvpkl1_key    reade     jvpkl1
     c                   enddo
      *
      * Prøver til slutt uten krav om leverandør
      *
     c                   eval      jvpkl1_vare = p_vare
     c     jvpkl1_key2   setll     jvpkl1
     c     jvpkl1_key2   reade     jvpkl1
     c                   dow       not %eof(jvpkl1)
     c                   if        jwgtin <> 0   and
     c                             jwenhe = p_enhe
     c                   eval      p_eann = jwgtin
6.30 c                   eval      p_stat = '1'
6.30 c                   goto      les_lvar
6.30 c                   endif
     c     jvpkl1_key2   reade     jvpkl1
     c                   enddo
      *
     c     les_lvar      tag
      *
      * finner lev.varenr
     c                   eval      p_lvar = vvlvar
     c                   eval      jvlel1_lvnr = p_vare
     c                   eval      jvlel1_lldo = w_ldor
     c     jvlel1_key    chain     jvlel1
     c                   if        %found(jvlel1)  and
     c                             jvllva <> *blank
     c                   movel     jvllva        p_lvar
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
     c                   parm                    p_vare
     c                   parm                    p_enhe
     c                   parm                    p_eann
     c                   parm                    p_lvar
     c                   parm                    p_stat
      *
      * Key for les på vareenhets-register
      *
     c     vveplr_key    klist
     c                   kfld                    vveplr_pfir
     c                   kfld                    vveplr_pvar
     c                   kfld                    vveplr_penh
     c                   kfld                    vveplr_pldo
      *
      * Key for les på vareregister
      *
     c     vvarlr_key    klist
     c                   kfld                    vvarlr_firm
     c                   kfld                    vvarlr_vare
      *
      * Key for les på VVEP-register
      *
     c     vvepl1_key    klist
     c                   kfld                    vvepl1_pfir
     c                   kfld                    vvepl1_pvar
     c                   kfld                    vvepl1_penh
      *
      * Key for les på JVPK-register
      *
     c     jvpkl1_key    klist
     c                   kfld                    jvpkl1_vare
     c                   kfld                    jvpkl1_ldor
      * Key for les på JVPK-register
      *
     c     jvpkl1_key2   klist
     c                   kfld                    jvpkl1_vare
      *
      * Key for les på JVAR-register
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for les på leverandør i VA
      *
     c     jlevl3_key    klist
     c                   kfld                    jlevl3_enum
      *
      * Key for les på vare-leverandør i VA
      *
     c     jvlel1_key    klist
     c                   kfld                    jvlel1_lvnr
     c                   kfld                    jvlel1_lldo
      *
      * leser leverandør i VA
     c                   eval      w_ldor = p_ldor
     c                   eval      jlevl3_enum = p_ldor
     c     jlevl3_key    chain     jlevl3
     c                   if        %found
     c                   eval      w_ldor = jlldor
     c                   endif
      *
     c                   endsr
      *
