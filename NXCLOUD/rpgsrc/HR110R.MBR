     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : HR110R
      * Beskrivelse . . : Radioterminal trans, bestilling - param
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       121000 HKO  Nytt program
      * V5.60 230308 KOB  Lagt inn mulighet for registrering av selger
6.10  * V6.10 260710 bsa  Henter selger fra brukerregister. Sjekker på
6.10  *                   at kode må legges inn og er gyldig.
6.11  *       081210 bof  Leverandører som er sperret skal ikke kunne brukes
6.30  *       280814 bof  6.11 utvidet også med test på passiv.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
6.10 ffusrl1    if   e           k disk    rename(fusrpfr:fusrl1r)
      *
     fhr110d    cf   e             workstn
      *
      * Local Data Area
      *
     d                uds
6.10 d l_user                911    920
     d l_firm                944    946  0
     d l_avde                947    950  0
     d l_lage               1001   1002  0
      *
      * Definering av variable i nøkler
      *
     d votyl1_otyp     s                   like(vaotyp)
     d rlevl1_levr     s                   like(rllevr)
6.10 d fusrl1_user     s                   like(l_user)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_adrs          s             30
     d w_ponr          s              4  0
     d w_pste          s             30
     d w_avde          s              4  0
     d w_bdat          s               d   datfmt(*iso)
     d w_ldat          s               d   datfmt(*iso)
     d w_kode          s              1
     d w_stat          s              1
     d w_tims          s               z
      *
     d w_firm          s                   like(vaofir)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer skjermbildet
      *
     c     b1tag         tag
      *
     c                   eval      b1otyp = 'B '
     c                   eval      b1lage = l_lage
     c                   eval      b1avde = l_avde
6.10 c                   eval      b1selg = fbselg
      *
     c     b1tagb        tag
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b1tagb
     c                   endsl
      *
      * Sjekker input
      *
     c                   exsr      sjekk_otyp
     c                   if        b_feil = *on
     c                   goto      b1tagb
     c                   endif
      *
     c                   exsr      sjekk_ldor
     c                   if        b_feil = *on
     c                   goto      b1tagb
     c                   endif
      *
     c                   exsr      sjekk_lage
     c                   if        b_feil = *on
     c                   goto      b1tagb
     c                   endif
      *
     c                   exsr      sjekk_avde
     c                   if        b_feil = *on
     c                   goto      b1tagb
     c                   endif
V5.60 *
V5.60c                   exsr      sjekk_selg
V5.60c                   if        b_feil = *on
V5.60c                   goto      b1tagb
V5.60c                   endif
      *
     c                   exsr      sjekk_dato
     c                   if        b_feil = *on
     c                   goto      b1tagb
     c                   endif
      *
      * Finner unik nøkkel
      *
     c                   eval      w_kode = 'B'
     c                   time                    w_tims
      *
      * Kaller registeringsprogram
      *
     c                   eval       w_bdat = *loval
     c                   eval       w_ldat = *loval
      *
     c                   if        b1bdat <> 0
     c     *dmy          move      b1bdat        w_bdat
     c                   endif
      *
     c                   if        b1ldat <> 0
     c     *dmy          move      b1ldat        w_ldat
     c                   endif
      *
     c                   call      'HR111R'
     c                   parm                    w_firm
     c                   parm                    w_kode
     c                   parm                    w_tims
     c                   parm                    b1otyp
     c                   parm                    b1ldor
     c                   parm                    b1lage
     c                   parm                    w_bdat
     c                   parm                    w_ldat
     c                   parm                    b1avde
V5.60c                   parm                    b1selg
      *
     c                   eval      b1ldor = 0
     c                   eval      b1lnav = *blank
      *
     c                   goto      b1tag
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Ordretype
      *
     c                   if        wactfe = 'B1OTYP'
     c                   call      'VA558R'
     c                   parm                    w_firm
     c                   parm                    b1otyp
     c                   goto      end_spØrr
     c                   endif
      *
      * Leverandør
      *
     c                   if        wactfe = 'B1LDOR'
     c                   call      'RL580R'
     c                   parm                    w_firm
     c                   parm                    b1ldor
     c                   exsr      sjekk_ldor
     c                   goto      end_spØrr
     c                   endif
      *
      * Lager
      *
     c                   if        wactfe = 'B1LAGE'
     c                   call      'RA580R'
     c                   parm                    w_firm
     c                   parm                    b1lage
     c                   parm                    b1ltxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Avdeling
      *
     c                   if        wactfe = 'B1AVDE'
     c                   call      'RA582R'
     c                   parm                    w_firm
     c                   parm                    b1avde
     c                   parm                    b1atxt
     c                   goto      end_spØrr
     c                   endif
V5.60 *
V5.60 * Selger
V5.60 *
V5.60c                   if        wactfe = 'B1SELG'
V5.60c                   call      'RA589R'
V5.60c                   parm                    w_firm
V5.60c                   parm                    b1selg
V5.60c                   parm                    b1stxt
V5.60c                   goto      end_spØrr
V5.60c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutiner for sjekking av input
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Orderetype
      *
     c     sjekk_otyp    begsr
      *
     c                   eval      b_feil = *off
     c                   eval      votyl1_otyp = b1otyp
     c     votyl1_key    chain     votyl1r                            31
     c                   if        *in31 = *on
     c                   eval      b_feil = *on
     c                   goto      end_otyp
     c                   endif
     c                   if        vaosys <> 1
     c                   eval      *in37 = *on
     c                   eval      b_feil = *on
     c                   endif
      *
     c     end_otyp      endsr
      *
      * Leverandør
      *
     c     sjekk_ldor    begsr
      *
     c                   eval      b1lnav = *blank
     c                   eval      b_feil = *off
     c                   if        b1ldor <> 0
     c                   eval      rlevl1_levr = b1ldor
     c     rlevl1_key    chain     rlevl1r                            32
     c                   if        *in32 = *on
     c                   eval      b_feil = *on
     c                   goto      end_ldor
     c                   endif
6.30 c                   if        rlsprk = 'J' or
6.30 c                             rlpass = 'J'
6.30 c                   eval      *in40 = *on
6.11 c                   eval      b_feil = *on
6.11 c                   goto      end_ldor
6.11 c                   endif
      *
     c                   eval      b1lnav = rlnavn
     c                   endif
      *
     c     end_ldor      endsr
      *
      * Lager
      *
     c     sjekk_lage    begsr
      *
     c                   eval      b1ltxt = *blank
     c                   eval      b_feil = *off
     c                   call      'FÅ720R'
     c                   parm                    w_firm
     c                   parm                    b1lage
     c                   parm                    b1ltxt
     c                   parm                    w_adrs
     c                   parm                    w_ponr
     c                   parm                    w_pste
     c                   parm                    w_avde
     c                   parm                    b_feil
      *
     c                   if        b_feil = *on
     c                   eval      *in33 = *on
     c                   endif
      *
     c     end_lage      endsr
      *
      * Avdeling
      *
     c     sjekk_avde    begsr
      *
     c                   eval      b_feil = *off
      *
     c                   if        b1avde <> 0
     c                   call      'RS707R'
     c                   parm                    w_stat
     c                   parm                    b1avde
     c                   parm                    b1atxt
      *
     c                   if        w_stat <> *blank
     c                   eval      *in38 = *on
     c                   eval      b_feil = *on
     c                   endif
     c                   endif
      *
     c     end_avde      endsr
V5.60 *
V5.60 * Selger
V5.60 *
V5.60c     sjekk_selg    begsr
V5.60 *
V5.60c                   eval      b_feil = *off
V5.60 *
V5.60c**                 if        b1selg <> 0
V5.60c                   call      'RS709R'
V5.60c                   parm                    w_stat
V5.60c                   parm                    b1selg
V5.60c                   parm                    b1stxt
V5.60 *
V5.60c                   if        w_stat <> *blank
V5.60c                   eval      *in39 = *on
V5.60c                   eval      b_feil = *on
V5.60c                   endif
V5.60c**                 endif
V5.60 *
V5.60c     end_selg      endsr
      *
      * Dato
      *
     c     sjekk_dato    begsr
      *
     c                   eval      b_feil = *off
     c                   if        b1bdat <> 0
     c     *dmy          test(d)                 b1bdat                 34
     c                   if        *in34 = *on
     c                   eval      b_feil = *on
     c                   goto      end_dato
     c                   endif
     c                   endif
      *
     c                   if        b1ldat <> 0
     c     *dmy          test(d)                 b1ldat                 36
     c                   if        *in36 = *on
     c                   eval      b_feil = *on
     c                   goto      end_dato
     c                   endif
     c                   endif
      *
     c                   if        b1bdat <> 0 and
     c                             b1ldat <> 0
     c     *dmy          move      b1bdat        w_bdat
     c     *dmy          move      b1ldat        w_ldat
     c                   if        w_ldat < w_bdat
     c                   eval      *in35 = *on
     c                   eval      b_feil = *on
     c                   goto      end_dato
     c                   endif
     c                   endif
      *
     c     end_dato      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på ordre-type
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
6.10  *
6.10  * Key for oppslag på lbrukerregister
6.10  *
6.10 c     fusrl1_key    klist
6.10 c                   kfld                    w_firm
6.10 c                   kfld                    fusrl1_user
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
6.10 c                   eval      fusrl1_user = l_user
6.10 c     fusrl1_key    chain     fusrl1
6.10 c                   if        not %found
6.10 c                   eval      fbselg = *zero
6.10 c                   endif
      *
     c                   endsr
