     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON
      * Program . . . . : RK712R
      * Beskrivelse . . : Overføring til Entra-format
      *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       040805 AMD  Nytt program
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fraa9l1    if   e           k disk    rename(raa9pfr:raa9l1r)
     frefal1    if   e           k disk    rename(refapfr:refal1r)
     frekul1    if   e           k disk    rename(rekupfr:rekul1r)
     fref2pf    uf a e             disk
     frek2pf    uf a e             disk
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable
      *
     d w_sted          s             25
     d w_klie          s              3  0
      *
      * Definering av variabler i nøkler
      *
     d w_firm          s                   like(rnfirm)
      *
      * Datastruktur for fakturalinjer
      *
     d                 ds
     d d_frec_1                     256
     d  d_klie_1                      3  0 overlay(d_frec_1:1)
     d  d_kund_1                     11  0 overlay(d_frec_1:4)
     d  d_fakt_1                     11  0 overlay(d_frec_1:15)
     d  d_dato_1                      8  0 overlay(d_frec_1:26)
     d  d_forf_1                      8  0 overlay(d_frec_1:34)
     d  d_fbel_1                     11  2 overlay(d_frec_1:42)
     d  d_valk_1                      3    overlay(d_frec_1:53)
     d  d_bilk_1                      2  0 overlay(d_frec_1:56)
     d  d_fref_1                     11  0 overlay(d_frec_1:58)
      *
      * Datastruktur for kundetabell
      *
     d                 ds
     d d_krec_2                     256
     d  d_kund_2                     11  0 overlay(d_krec_2:1)
     d  d_knav_2                     30    overlay(d_krec_2:12)
     d  d_adr1_2                     30    overlay(d_krec_2:42)
     d  d_adr2_2                     30    overlay(d_krec_2:72)
     d  d_ponr_2                      4  0 overlay(d_krec_2:102)
     d  d_byby_2                     26    overlay(d_krec_2:106)
     d  d_land_2                      3    overlay(d_krec_2:132)
     d  d_tlff_2                     13    overlay(d_krec_2:135)
     d  d_sprk_2                      3    overlay(d_krec_2:148)
     d  d_ftak_2                     15  0 overlay(d_krec_2:151)
     d  d_valu_2                      3    overlay(d_krec_2:166)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Lese Kunde-transaksjoner
      *
     c                   read      refal1
     c                   dow       not %eof
      *
      * Oppdater faktura-tabell
      *
     c                   exsr      xopd_fakt
     c                   read      refal1
      *
     c                   enddo
      *
      * Lese Kunde-register
      *
     c                   read      rekul1
     c                   dow       not %eof
      *
      * Oppdater kunde-tabell
      *
     c                   exsr      xopd_kund
     c                   read      rekul1
      *
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatering av fil for faktura-poster
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xopd_fakt     begsr
      *
      * Fakturaposter
      *
     c                   eval      d_klie_1 = w_klie
     c                   eval      d_kund_1 = rnkund
     c                   eval      d_fakt_1 = rnbiln
     c                   eval      d_valk_1 = 'NOK'
     c                   eval      d_fref_1 = 0
      *
      * Snu fortegn på beløp
      * dersom negativt
      *
     c                   if        rnbelØ   < 0
     c                   eval      d_fbel_1 = rnbelØ * -1
     c                   eval      d_bilk_1 = 22
     c                   else
     c                   eval      d_fbel_1 = rnbelØ
     c                   eval      d_bilk_1 = 21
     c                   endif
      *
      * Legge ut dato til 8 numerisk
      *
     c                   z-add     0             d_dato_1
     c                   if        rnbdat <> *loval
     c     *ymd          move      rnbdat        d_dato_1
     c                   endif
      *
     c                   z-add     0             d_forf_1
     c                   if        rnfdat <> *loval
     c     *ymd          move      rnfdat        d_forf_1
     c                   endif
      *
     c                   eval      refrec   = d_frec_1
      *
     c                   write     ref2pfr
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatering av fil for kunde-tabell
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xopd_kund     begsr
      *
      * Kunderegister
      *
     c                   eval      d_kund_2 = rkkund
     c                   eval      d_knav_2 = rknavn
     c                   eval      d_adr1_2 = rkgate
     c                   eval      d_adr2_2 = rksted
     c                   eval      d_ponr_2 = rkponr
     c                   eval      d_byby_2 = *blank
     c                   eval      d_land_2 = 'N  '
     c                   eval      d_tlff_2 = rktlfn
     c                   eval      d_sprk_2 = *blank
     c                   eval      d_ftak_2 = rkftnr
     c                   eval      d_valu_2 = *blank
      *
     c                   eval      rekrec   = d_krec_2
      *
     c                   write     rek2pfr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for statustabell factoring
      *
     c     raa9l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for kundetransaksjoner
      *
     c     refal1_key    klist
     c                   kfld                    w_firm
      *
      * Key for kunderegister
      *
     c     rekul1_key    klist
     c                   kfld                    w_firm
      *
      * Legge inn firmanummer
      *
     c                   eval      w_firm = l_firm
      *
      * Hent inn klientnummer
      *
     c     raa9l1_key    chain     raa9l1r
     c                   if        %found
     c                   move      ra9kli        w_klie
     c                   endif
      *
     c                   endsr
      *
