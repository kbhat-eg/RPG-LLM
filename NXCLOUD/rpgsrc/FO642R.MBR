     h datedit(*dmy.) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO642R
      * Beskrivelse . . : Utskrift av kjøre-liste
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * R5.21 200803 AMD  Lagt om til AP600R i stedet for AP100R
7.xx  * 7.00  290316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     ffo642d    cf   e             workstn
      *
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     fra07pf    if   e           k disk    rename(ra07pfr:ra07pfr)
     fra25lr    if   e           k disk    rename(ra25pfr:ra25lrr)
      *
     d w_firm          s              3  0
     d w_atxt          s             20
     d w_navn          s             30
      *
     d ra07pf_firm     s                   like(ragfir)
     d ra07pf_gkod     s                   like(ragkod)
     d ra25lr_firm     s                   like(rayfir)
     d ra25lr_yrut     s                   like(rayrut)
      *
     d                 ds
      *                                                      Filemember
     d ldambr                        10
     d  ldabok                        1    overlay(ldambr)
     d  ldafir                        3  0 overlay(ldambr:2)
     d  ldalst                        6  0 overlay(ldambr:5)
      *
     d                 ds
      *                                                      Parameter-inn
     d ldprec                        64
     d  ldotyp                        2    overlay(ldprec)
     d  ldfdat                       10d   datfmt(*iso)
     d                                     overlay(ldprec:03)
     d  ldtdat                       10d   datfmt(*iso)
     d                                     overlay(ldprec:13)
     d  ldfavd                        4  0 overlay(ldprec:23)
     d  ldtavd                        4  0 overlay(ldprec:27)
     d  ldfkjo                        2  0 overlay(ldprec:31)
     d  ldtkjo                        2  0 overlay(ldprec:33)
     d  ldvari                        1  0 overlay(ldprec:35)
      *
      * Definering av Local Data Area
      *
     d lda            uds
     d l_ruti                800    809
     d l_chgv                844    844
     d l_sfir                944    946  0
      *
     d otyfir          s                   like(vaofir)
     d otytyp          s                   like(vaotyp)
     d wcnumm          s              6  0
     d wcqnnn          s              1
5.21 d w_in03          s              1
5.21 d w_tvbt          s              1
5.21 d w_btch          s              1
     d wwfdat          s                   like(ldfdat)
     d wwtdat          s                   like(ldtdat)
     d wwvalg          s              1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandle bilde for registrering av parametre                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     b1tag         tag
      *
      *  Nullstiller variable som skal vises i skjermbildet
      *
     c                   eval      b1otyp = *blank
     c                   eval      b1fdat = *zero
     c                   eval      b1tdat = *zero
     c                   eval      b1favd = *zero
     c                   eval      b1tavd = *hival
     c                   eval      b1fkjo = *zero
     c                   eval      b1tkjo = *hival
      *
     c     b1tagb        tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc
     c                   goto      xslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b1tagb
     c                   endsl
      *
      *  Sjekk   Finnes ordretype i ORDRE-TYPE-REGISTER
      *
     c                   if        b1otyp <> *blank
     c                   move      b1otyp        otytyp
     c     otykey        chain     votyl1r                            90
     c                   if        *in90 = *on
     c                   move      *on           *in31
     c                   goto      b1tagb
     c                   endif
     c                   endif
      *
      *  Sjekk   Er avdeling gyldig
      *
     c                   if        b1favd > *zeros
     c                   eval      ra07pf_firm = w_firm
     c                   eval      ra07pf_gkod = b1favd
     c     ra07pf_key    chain     ra07pfr                            90
     c                   if        *in90 = *on
     c                   eval      *in32 = *on
     c                   goto      b1tagb
     c                   endif
     c                   endif
      *
     c                   if        b1tavd < *hival
     c                   eval      ra07pf_firm = w_firm
     c                   eval      ra07pf_gkod = b1tavd
     c     ra07pf_key    chain     ra07pfr                            90
     c                   if        *in90 = *on
     c                   eval      *in33 = *on
     c                   goto      b1tagb
     c                   endif
     c                   endif
      *
      *  Sjekk   Er kjørerute gyldig
      *
     c                   if        b1fkjo > *zeros
     c                   eval      ra25lr_firm = w_firm
     c                   eval      ra25lr_yrut = b1fkjo
     c     ra25lr_key    chain     ra25lrr                            90
     c                   if        *in90 = *on
     c                   eval      *in34 = *on
     c                   goto      b1tagb
     c                   endif
     c                   endif
      *
     c                   if        b1tkjo < *hival
     c                   eval      ra25lr_firm = w_firm
     c                   eval      ra25lr_yrut = b1tkjo
     c     ra25lr_key    chain     ra25lrr                            90
     c                   if        *in90 = *on
     c                   eval      *in35 = *on
     c                   goto      b1tagb
     c                   endif
     c                   endif
      *
      * Sjekke om dato fra/til er gyldige
      *
     c                   if        b1fdat = *zero                                        .....
     c                   move      udate         b1fdat                                      .
     c                   endif                                                           .....
      *
     c     *dmy          test(d)                 b1fdat                 36
     c                   if        *in36 = *on                                           .....
     c                   goto      b1tagb                                                    .
     c                   endif                                                           .....
      *
     c                   if        b1tdat = *zero                                        .....
     c                   move      udate         b1tdat                                      .
     c                   endif                                                           .....
      *
     c     *dmy          test(d)                 b1tdat                 37
     c                   if        *in37 = *on                                           .....
     c                   goto      b1tagb                                                    .
     c                   endif                                                           .....
      *
      *  Sjekk   Er dato fra større enn dato til
      *
     c                   eval      wwfdat = *loval
     c                   eval      wwtdat = *loval
      *
     c                   if        b1fdat <> 0
     c     *dmy          move      b1fdat        wwfdat
     c                   endif
      *
     c                   if        b1tdat <> 0
     c     *dmy          move      b1tdat        wwtdat
     c                   endif
      *
     c                   if        wwfdat > wwtdat
     c                   move      *on           *in38
     c                   goto      b1tagb
     c                   endif
      *
      *  Legger inntastede opplysninger i Hjelpe-felter for Parameter
      *
     c                   move      b1otyp        ldotyp
     c                   move      wwfdat        ldfdat
     c                   move      wwtdat        ldtdat
     c                   move      b1favd        ldfavd
     c                   move      b1tavd        ldtavd
     c                   move      b1fkjo        ldfkjo
     c                   move      b1tkjo        ldtkjo
     c                   move      b1vari        ldvari
      *
      *  Dersom dato til=0 skal t.o.m. alle datoer velges
      *
     c                   if        b1tdat = *zero
     c                   move      *hival        ldtdat
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Inntasting er ferdig !  Oppstart av utvalg/utskriftsprogrammer *
      *  Kaller opp neste 'CL'-program                                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *  Legg inn verdi for bare henting av opplysninger
      *
     c                   eval      l_chgv = *blank                               KODE VISE BILDE
     c                   eval      l_ruti = 'FO646'                              RUTINE
     c                   out       *dtaara
      *
      *  Kaller opp program før utskrift
      *
5.21 c                   call      'AP600R'
5.21 c                   parm                    l_ruti
5.21 c                   parm                    l_chgv
5.21 c                   parm                    w_tvbt
5.21 c                   parm                    w_in03
5.21 c                   parm                    w_btch
      *
      *  F4=Legge jobb på jobbkø
      *
     c                   if        *inkd = *on
     c                   move      '1'           wwvalg
     c                   endif
      *
      *  Henter siste nummer for File-member
      *
     c                   call      'FA920R'
     c                   parm                    wcqnnn
     c                   parm                    wcnumm
      *
     c                   move      'K'           ldabok
     c                   move      wcnumm        ldalst
      *
      *  Kaller opp subprogram
      *
     c                   call      'FO643C'
     c                   parm                    ldprec
     c                   parm                    ldambr
     c                   parm                    wwvalg
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av spørringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Ordretype
      *
     c                   if        w_afld = 'B1OTYP'
     c                   call      'VA550R'
     c                   parm                    w_firm
     c                   parm                    b1otyp
     c                   goto      end_spØrr
     c                   endif
      *
      * Avdeling
      *
     c                   if        w_afld = 'B1FAVD'
     c                   call      'RA507R'
     c                   parm                    w_firm
     c                   parm                    b1favd
     c                   parm                    w_atxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'B1TAVD'
     c                   call      'RA507R'
     c                   parm                    w_firm
     c                   parm                    b1tavd
     c                   parm                    w_atxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Kjørerute
      *
     c                   if        w_afld = 'B1FKJO'
     c                   call      'RA525R'
     c                   parm                    w_firm
     c                   parm                    b1fkjo
     c                   parm                    w_atxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'B1TKJO'
     c                   call      'RA525R'
     c                   parm                    w_firm
     c                   parm                    b1tkjo
     c                   parm                    w_atxt
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     *inzsr        begsr
      *
      *  Definisjon av local data area for output
      *
     c     *dtaara       define    *lda          lda
      *
5.21 c                   eval      w_in03 = *blank
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     c                   eval      wwvalg = *zero
     c                   eval      wcqnnn = *zero
     c                   eval      wcnumm = *zero
      *
      *  Redefinering av felter
      *
      *
      *  Redefinering av nøkler
      *
      *
      *  Key for file : ORDRE-TYPE-REGISTER
      *
     c     otykey        klist
     c                   kfld                    otyfir
     c                   kfld                    otytyp
      *
      *  Key for file : Avdeling
      *
     c     ra07pf_key    klist
     c                   kfld                    ra07pf_firm
     c                   kfld                    ra07pf_gkod
      *
      *  Key for file : Kjørerute
      *
     c     ra25lr_key    klist
     c                   kfld                    ra25lr_firm
     c                   kfld                    ra25lr_yrut
      *
      *  Legge inn firmanummer i nøkklene
      *
     c                   move      l_sfir        otyfir
     c                   move      l_sfir        ldafir
      *
     c                   eval      w_firm      = l_sfir
     c                   eval      ra07pf_firm = l_sfir
     c                   eval      ra25lr_firm = l_sfir
      *
     c                   endsr
