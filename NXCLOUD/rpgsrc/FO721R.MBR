      /TITLE  ASOFAK: Hvem som holder ordre klargjøres i parametere
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO721R
      * Beskrivelse . . : Endre infotyp på ordrehode
      *                   hvis infotype er gyldig
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * R7.00 221119 BKV  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohelu    uf   e           k disk    rename(fohepfr:fohelur)
     fvinfl1    if   e           k disk    rename(vinfpfr:vinfl1r)
      *
      * Definering av parametere
      *
     d p_numm          s                   like(fonumm)
     d p_suff          s                   like(fosuff)
     d p_ityp          s                   like(foityp)
      *
      * Definering av local data area
      *
     d                uds
     d l_user                        10
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d w_firm          s                   like(fofirm)
     d fohelu_numm     s                   like(fonumm)
     d fohelu_suff     s                   like(fosuff)
     d vinfl1_ityp     s                   like(vaityp)
      *
      * Definering av variable
      *
     d w_koda          s                   like(fokoda)
     d w_koti          s                   like(fokoti)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Legge inn opplysninger som skal oppdatere ordrehode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Hente inn dagens dato og
      * klokkeslett
      *
     c                   time                    w_koda
     c                   time                    w_koti
         //
         // Sjekk at infotyp finnes
         //
         vinfl1_ityp = p_ityp;
         chain vinfl1_key vinfl1r;
         if %found(vinfl1);
           //
           // Oppdater ORDRE-HODE-REGISTER
           //
           chain fohelu_key fohelur;
           if %found(fohelu);
             foityp = p_ityp;
     c                   time                    foedat
     c                   time                    foetim
             foeusr = l_user;
             update fohelur;
           endif;
         endif;
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Tar inn parameter fra forrige program
      *
     c     *entry        plist
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_ityp
      *
      * Key for file : ORDRE-HODE-REGISTER
      *
     c     fohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelu_numm
     c                   kfld                    fohelu_suff
      *
      * Key for oppslag på ordre-info-type
      *
     c     vinfl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vinfl1_ityp
      *
      * Legger inn firma ordrenummer og suffix i nøkklene
      *
     c                   eval      w_firm = l_firm
     c                   eval      fohelu_numm = p_numm
     c                   eval      fohelu_suff = p_suff
      *
     c                   endsr
