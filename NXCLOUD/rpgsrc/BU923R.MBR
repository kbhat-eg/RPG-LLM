     h option(*nodebugio) datedit(*dmy) decedit(',')
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSHOP
      * Program . . . . : BU923R
      * Beskrivelse . . : Butikk, henter kundeinformasjon
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       250505 HKO  Nytt program
      *       240805 AMD  Endret distrikt fra 2.0 til 4.0
5.60  *       250408 AMD  Beløpsgrense for almenningsrabatt ble bare
      *                   hentet fra kunde. Nå sjekkes også almenning
6.20  * 6.20  140611 bsa  Memosaldo flyttet ut på eget register
6.21  * 6.20  070213 ask  Setter akk. alm. kjøp til 0 ved negativ saldo
6.NU  * R6.30 170614 nho  Programmet gjennomgått mtp nummerutvidelser.
7.01  * MGR   151216 sst  Ber flytende Memosaldo m/ordre pr ant. dager
7.01  *       170117 sst  Hente parameter meosaldo fra AFPSPF
8.01  * K000  231122 sst  Flyttet ant memo-dager og utv.kreditgrense til  UTVIDET_MEMOSALDO (nx)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
6.20 frkmel1    if   e           k disk    rename(rkmepfr:rkmel1r)
     fra11l1    if   e           k disk    rename(ra11pfr:ra11l1r)
     fra06l1    if   e           k disk    rename(ra06pfr:ra06l1r)
     frksal1    if   e           k disk    rename(rksapfr:rksal1r)
     frmknl2    if   e           k disk    rename(rmknpfr:rmknl2r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
8.01 f*afpslr    if   e           k disk    rename(afpspfr:afpslrr)
      *
      * Definering av parametre
      *
     d p_firm          s              3
     d p_kund          s              6  0
     d p_kinf          s            550
     d p_ktxt          s             20
     d p_rtxt          s             20
     d p_spa1          s             30
     d p_spa2          s             30
     d p_spa3          s             30
     d p_alme          s              4  0
     d p_proj          s              1  0
      *
      * Definering av datastrukturer
      *
      * Datastruktur - kundeinformasjon
      *
     d                 ds
6.NU d d_kinf                       552
     d  d_navn                       35    overlay(d_kinf:1)
     d  d_adr1                       35    overlay(d_kinf:36)
     d  d_adr2                       35    overlay(d_kinf:71)
     d  d_ponr                       10    overlay(d_kinf:106)
     d  d_sted                       35    overlay(d_kinf:116)
     d  d_kprs                       35    overlay(d_kinf:151)
     d  d_tlfn                       15    overlay(d_kinf:186)
     d  d_mobn                       15    overlay(d_kinf:201)
     d  d_tlfx                       15    overlay(d_kinf:216)
     d  d_mail                       50    overlay(d_kinf:231)
     d  d_orgn                        9  0 overlay(d_kinf:281)
     d  d_eanl                       13  0 overlay(d_kinf:290)
6.NU d  d_faku                        8  0 overlay(d_kinf:303)
6.NU d  d_koku                        6  0 overlay(d_kinf:311)
6.NU d  d_avde                        4  0 overlay(d_kinf:317)
6.NU d  d_lagr                        2  0 overlay(d_kinf:321)
6.NU d  d_selg                        4  0 overlay(d_kinf:323)
6.NU d  d_dist                        4  0 overlay(d_kinf:327)
6.NU d  d_kjor                        2  0 overlay(d_kinf:331)
6.NU d  d_kjnr                        5  0 overlay(d_kinf:333)
6.NU d  d_kkat                        4  0 overlay(d_kinf:338)
6.NU d  d_rkat                        3  0 overlay(d_kinf:342)
6.NU d  d_orab                        6    overlay(d_kinf:345)
6.NU d  d_betb                        2  0 overlay(d_kinf:351)
6.NU d  d_betm                        1  0 overlay(d_kinf:353)
6.NU d  d_land                        2    overlay(d_kinf:354)
6.NU d  d_valu                        3    overlay(d_kinf:356)
6.NU d  d_kmva                        1  0 overlay(d_kinf:359)
6.NU d  d_kfgb                        1  0 overlay(d_kinf:360)
6.NU d  d_sekv                        2  0 overlay(d_kinf:361)
6.NU d  d_rekv                        1  0 overlay(d_kinf:363)
6.NU d  d_kres                        1  0 overlay(d_kinf:364)
6.NU d  d_grns                        5  0 overlay(d_kinf:365)
6.NU d  d_sald                       12    overlay(d_kinf:370)
6.NU d  d_mems                       12    overlay(d_kinf:382)
6.NU d  d_odat                         d   overlay(d_kinf:394) datfmt(*iso)
6.NU d  d_edat                         d   overlay(d_kinf:404) datfmt(*iso)
6.NU d  d_etim                         t   overlay(d_kinf:414) timfmt(*iso)
6.NU d  d_eusr                       10    overlay(d_kinf:422)
6.NU d  d_prpa                        1  0 overlay(d_kinf:432)
6.NU d  d_kjal                       11  2 overlay(d_kinf:433)
6.NU d  d_gral                        9  0 overlay(d_kinf:444)
6.NU d  d_dumm                        1    overlay(d_kinf:552) inz('*')
      *
      * Definering av variabler i nøkler
      *
     d rkunl1_kund     s                   like(rkkund)
     d ra11l1_kkod     s                   like(rakkod)
     d ra06l1_fkod     s                   like(rafkod)
     d rksal1_saku     s                   like(rksaku)
     d rmknl2_xknr     s                   like(rmxknr)
     d fkprl1_kund     s                   like(flkund)
8.01 d*afpslr_ufil     s                   like(l_filg)
8.01 d*afpslr_uide     s                   like(afuide)
      *
      * Definering av variable
      *
     d b_proj          s              1    inz(*off)
      *
     d w_firm          s              3  0
     d w_date          s               d   datfmt(*iso)
5.60 d w_gral          s                   like(rkgral)
7.01  *w_utvgrns = utvidelse av memosaldo(Mestergruppen)
7.01 d w_utvgrns       s              2  0
7.01  *w_memodagr = antall dager ordre skal med i memosaldo
7.01 d w_memodagr      s              3  0
7.01 d w_3x            s              3
7.01 d p_fir           s              3
7.01 d p_kun           s              6
      *
7.01 d                uds
7.01 d  l_filg               931    933
      *
7.01 d u_kmem          s              1    inz(*off)                            Kredit og memodager
      *
7.01   dcl-s co402_verdi1  char(1);
7.01   dcl-s co402_verdi2  char(2048);
7.01   DCL-PR CO402R EXTPGM('CO402R');
7.01     p_filgrp            char(3)     const;
7.01     p_firm              packed(3:0) const;
7.01     p_lager             packed(2:0) const;
7.01     p_nokkel            char(50)    const;
7.01     p_verdi1            char(1);
7.01     p_verdi2            char(2048);
7.01   END-PR;
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Utleder firma fra parameter
      *
     c                   move      p_firm        w_firm
      *
      * Henter current dato
      *
     c                   time                    w_date
      *
      * Avbryter dersom kunde ikke finnes
      *
     c                   eval      rkunl1_kund = p_kund
     c     rkunl1_key    chain     rkunl1
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
7.01  *
7.01  * Beregne memosaldo pr dato, Mestergruppen
7.01 c                   if        u_kmem = *on
7.01 c                   if        w_memodagr > 0
7.01 c                   move      w_firm        p_fir
7.01 c                   move      rkunl1_kund   p_kun
7.01 c                   call      'FO763R'
7.01 c                   parm                    p_fir
7.01 c                   parm                    p_kun
7.01 c                   endif
7.01 c                   endif
6.20  * Henter inn informasjon om memosaldo
6.20 c     rkunl1_key    chain     rkmel1
      *
      * Henter beskrivelse av kundekategori
      *
     c                   if        rkkatg <> 0
     c                   eval      ra11l1_kkod = rkkatg
     c     ra11l1_key    chain     ra11l1
     c                   endif
      *
      * Henter beskrivelse av rabattkategori
      *
     c                   if        rkrabk <> 0
     c                   eval      ra06l1_fkod = rkrabk
     c     ra06l1_key    chain     ra06l1
     c                   endif
      *
      * Henter spesialavtale
      *
     c                   eval      rksal1_saku = rkkund
     c     rksal1_key    chain     rksal1
      *
      * Henter almenning
      *
     c                   eval      rmknl2_xknr = rkkund
     c     rmknl2_key    chain     rmknl2
5.60  *
5.60  * Sjekk om beløpsgrense er lagt på almenning
5.60  * dersom den ikke ligger på kunde
5.60  *
5.60 c                   eval      w_gral = 0
5.60 c                   if        rkgral = 0
5.60 c                   call      'FO106R'
5.60 c                   parm                    rkkund
5.60 c                   parm                    w_gral
5.60 c                   endif
      *
      * Sjekker om kunde har aktive kundeprosjekt
      *
     c                   eval      fkprl1_kund = rkkund
     c     fkprl1_key    setll     fkprl1
     c     fkprl1_key    reade     fkprl1
     c                   dow       not %eof
     c                   if        flfdat = *loval and
     c                             fltdat = *loval or
     c                             flfdat <= w_date and
     c                             fltdat >= w_date
     c                   eval      b_proj = *on
     c                   leave
     c                   endif
     c     fkprl1_key    reade     fkprl1
     c                   enddo
      *
      * Skriver til datastruktur
      *
     c                   eval      d_navn = rknavn
     c                   eval      d_adr1 = rkgate
     c                   eval      d_adr2 = rkadr2
     c                   eval      d_ponr = *blank
     c                   eval      d_sted = rksted
     c                   eval      d_kprs = rkpers
     c                   eval      d_tlfn = rktlfn
     c                   eval      d_mobn = rkmobn
     c                   eval      d_tlfx = rktlfx
     c                   eval      d_mail = rkemal
     c                   eval      d_orgn = rkftnr
     c                   eval      d_eanl = rkeank
     c                   eval      d_faku = rkfaku
     c                   eval      d_koku = rkkoku
     c                   eval      d_avde = rkavde
     c                   eval      d_lagr = rklagr
     c                   eval      d_selg = rkslgr
     c                   eval      d_dist = rkdist
     c                   eval      d_kjor = rkkjØr
     c                   eval      d_kjnr = rkkjnr
     c                   eval      d_kkat = rkkatg
     c                   eval      d_rkat = rkrabk
     c                   eval      d_orab = %editc(rkorab:'4')
     c                   eval      d_orab = %xlate(' ':'0':d_orab)
     c                   eval      d_betb = rkbetb
     c                   eval      d_betm = rkbetm
     c                   eval      d_land = rkland
     c                   eval      d_valu = rkvalk
     c                   eval      d_kmva = rkmkod
     c                   eval      d_kfgb = rkfagb
     c                   eval      d_sekv = rksekv
     c                   eval      d_rekv = rkrekv
     c                   eval      d_kres = rkkres
     c                   eval      d_grns = rkgrns
     c                   eval      d_sald = %editc(rksald:'4')
     c                   eval      d_sald = %xlate(' ':'0':d_sald)
     c                   eval      d_mems = %editc(rkmems:'4')
     c                   eval      d_mems = %xlate(' ':'0':d_mems)
     c                   eval      d_odat = rkedat
     c                   eval      d_edat = rkedat
     c                   eval      d_etim = rketim
     c                   eval      d_eusr = rkesig
     c                   if        rkprpa = 1
     c                   eval      d_prpa = 0
     c                   else
     c                   eval      d_prpa = 1
     c                   endif
5.60 c                   eval      d_kjal = rkkjal + rkmema
5.61 c                   if        d_kjal < 0
5.61 c                   eval      d_kjal = 0
5.61 c                   endif
     c                   eval      d_gral = rkgral
5.60  *
5.60  * Dersom grense ikke er lagt
5.60  * på kunde, skal eventuell
5.60  * grense på almenning benyttes
5.60  *
5.60 c                   if        rkgral = 0
5.60 c                   eval      d_gral = w_gral
5.60 c                   endif
      *
     c                   movel     rkponr        d_ponr
      *
      * Skriver til parametre
      *
     c                   eval      p_kinf = d_kinf
      *
     c                   eval      p_ktxt = raktxt
     c                   eval      p_rtxt = raftxt
     c                   eval      p_spa1 = rksat1
     c                   eval      p_spa2 = rksat2
     c                   eval      p_spa3 = rksat3
     c                   eval      p_alme = rmxalm
     c                   eval      p_proj = 0
      *
     c                   if        b_proj = *on
     c                   eval      p_proj = 1
     c                   endif
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameter
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kund
     c                   parm                    p_kinf
     c                   parm                    p_ktxt
     c                   parm                    p_rtxt
     c                   parm                    p_spa1
     c                   parm                    p_spa2
     c                   parm                    p_spa3
     c                   parm                    p_alme
     c                   parm                    p_proj
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på kundekategori
      *
     c     ra11l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra11l1_kkod
      *
      * Key for oppslag på rabattkategori
      *
     c     ra06l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra06l1_fkod
      *
      * Key for oppslag på kunde-spesialavtale
      *
     c     rksal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rksal1_saku
      *
      * Key for oppslag på eiendomsknytning
      *
     c     rmknl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rmknl2_xknr
      *
      * Key for oppslag på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
7.01  *
7.01  * Key for propertiesfil
7.01  *
8.01 c*    afpslr_key    klist
8.01 c*                  kfld                    afpslr_ufil
8.01 c*                  kfld                    afpslr_uide
7.01  *
7.01  * Utvidet memosaldo
8.01  *   Hent inn ant dager fram ordre skal med i memosaldo
7.01  *   Hent inn % utvidelse av kreditgrense
7.13  *
8.01 c                   eval      w_memodagr = 999
8.01 c                   eval      w_utvgrns  = 0
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'UTVIDET_MEMOSALDO':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_kmem = *on;
8.01     if  %subst(co402_verdi2:1:3) <> *blank;
8.01       w_memodagr = %dec(%subst(co402_verdi2:1:3):3:0);
8.01     endif;
8.01     if  %subst(co402_verdi2:133:2) <> *blank;
8.01       w_utvgrns = %dec(%subst(co402_verdi2:133:2):2:0);
8.01     endif;
7.01   endif;
7.01  *
7.01  *   Hent inn % utvidelse av kreditgrense
8.01 c*                  eval      afpslr_ufil = l_filg
8.01 c*                  eval      afpslr_uide = 'KRGRNSUTV'
8.01 c*    afpslr_key    chain     afpslrr
8.01 c*                  if        %found(afpslr)
8.01 c*                  movel     afudss        w_utvgrns
8.01 c*                  endif
7.01  *
7.01  *   Hent inn ant dager fram ordre skal med i memosaldo
8.01 c*                  eval      afpslr_uide = 'MEMODAGER'
8.01 c*    afpslr_key    chain     afpslrr
8.01 c*                  if        %found(afpslr)
8.01 c*                  movel     afudss        w_3x
8.01 c*                  move      w_3x          w_memodagr
8.01 c*                  endif
      *
     c                   endsr
