      /TITLE  Notatblokk - renummerering
     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK980R                                        *
      * Beskrivelse . . : Notatblokk - renummering                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
T5.20 * R5.20     110604  Nytt program.                             HFL
T5.30 * R5.30     200904  Endret filnavn på inputfil.               HFL
T5.40 * R5.40     150605  Leser RKLWLu i stedet for RKLWPF.         HFL
6.10  * V6.10     210909  Lagt inn sporingsfelter                   BSA
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frklwlu    if   e           k disk    rename(rklwpfr:rklwlur)
     f                                     prefix(w:1)
     frklalu    uf a e           k disk    rename(rklapfr:rklalur)
      *
      * LDA
     d                uds
      *
     d l_wsid                901    910                                         Bruker
     d l_user                911    920                                         Bruker
     d l_firm                944    946  0                                      Firma
     d l_fnav                951    980                                         Navn
      *
     d w_nylin         s                   like(rxlinj)
     d w_systs         s                   like(rxsyst)
     d w_konts         s                   like(rxkont)
      *
      * Les notatblokk fra kopifil
      *
E5.40c                   read      rklwlu
     c                   dou       %eof
     c                   exsr      renumm                                                    .
E5.40c                   read      rklwlu
     c                   enddo                                                           .....
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
      *
      /eject
      ****************************************************************
      *   SUBRUTINE FOR Å RENUMMERERE NOTATBLOKK                     *
      ****************************************************************
     c     renumm        begsr
      *
      * Brudd på kode/konto?
      *
     c                   if        wxsyst <> w_systs or
     c                             wxkont <> w_konts
     c                   eval      w_nylin = *zeros
     c                   endif                                                           .....
      *
     c                   eval      w_nylin = w_nylin + 10
     c                   eval      w_systs  = wxsyst
     c                   eval      w_konts  = wxkont
      *
     c                   eval      rxfirm  = wxfirm
     c                   eval      rxsyst  = wxsyst
     c                   eval      rxpros  = wxpros
     c                   eval      rxakti  = wxakti
     c                   eval      rxkont  = wxkont
     c                   eval      rxspes  = wxspes
     c                   eval      rxavde  = wxavde
     c                   eval      rxlinj  = w_nylin
     c                   eval      rxtgrp  = wxtgrp
     c                   eval      rxtext  = wxtext
T5.40c                   eval      rxtist  = wxtist
6.10 c                   time                    rxodat
6.10 c                   time                    rxotim
6.10 c                   eval      rxousr = l_user
6.10 c                   time                    rxedat
6.10 c                   time                    rxetim
6.10 c                   eval      rxeusr = l_user
     c                   write     rklalur                                                   .
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   endsr
