      /TITLE  Oppslag mot bruker-register
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : AA100R
      * Beskrivelse . . : Syst. Adm. Oppslag mot bruker for
      *                              oppdatering av LDA v/AA724R
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       260309 AMD  Nytt program
      *
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer
      *
      *       LR = Avslutt program
      *    60-69 = Fileindikatorer chain etc.
      *    70-79 = Arbeidsindikatorer i sub-rutiner
      *    80-89 = Arbeidsindikatorer ordinære
      *    90-99 = Benyttes ved std. sub-rutiner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fausrl1    if   e           k disk
     f                                     rename(ausrpfr:ausrl1r)
      *
      * Definering av Local data area
      *
     d                uds
     d dsuser                911    920
     d dssfil                931    933
     d dssbib                934    943
     d dssfir                944    946  0
     d dssavd                947    950  0
     d dsfnav                951    980
     d dsbfil                981    981
     d dsbfir                982    982
     d dsbavd                983    983
     d dslevl                996    997  0
     d dslagr               1001   1002  0
     d dsmilj               1003   1005
      *
      * Definering av parametre
      *
     d p_user          s                   like(abuser)
      *
      * Definering av variabler i nøkler
      *
     d ausrl1_user     s                   like(abuser)
      *
      * Definering av variable
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent inn bruker-record
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      ausrl1_user = p_user
     c     ausrl1_key    chain     ausrl1r
      *
     c                   if        %found
      *
     c                   if        dsuser = *blank
     c                   eval      dsuser = abuser
     c                   endif
      *
     c                   eval      dssfil = absfil
     c                   eval      dssbib = absbib
     c                   eval      dssfir = absfir
     c                   eval      dssavd = absavd
     c                   eval      dsfnav = abfnav
     c                   eval      dsbfil = abbfil
     c                   eval      dsbfir = abbfir
     c                   eval      dsbavd = abbavd
     c                   eval      dslevl = ablevl
     c                   eval      dslagr = ablagr
     c                   eval      dsmilj = abmilj
     c                   else
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_user
      *
      * Key for file nummerisk
      *
     c     ausrl1_key    klist
     c                   kfld                    ausrl1_user
      *
     c                   endsr
      *
