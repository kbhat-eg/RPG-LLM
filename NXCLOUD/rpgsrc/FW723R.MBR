      /title  ASOFAK Lev.vare, henter vareregister - Norske Røtter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FW723R
      * Beskrivelse . . : Danning av leverandør vareregister, Norske Røtter
      *
      *                   Innlesing via MAL-regneark !
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       270301 HKO  Nytt program
      *       020501 HKO  Tilpasset nytt regneark
      * 6.10  230609 BSA  Oppdatert med sporingsinformasjon
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fflvwpf    if   e           k disk
     fflvapf    o  a e             disk
      *
6.10  * Local data area
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av datastruktur
      *
      *  Recordbeskrivelse - Norske røtter
      *
     d                 ds
     d d_inpu                       143
     d  d_vare                       15    overlay(d_inpu:1)                    Varenr
     d  d_tek1                       45    overlay(d_inpu:16)                   Varetekst-1
     d  d_tek2                       35    overlay(d_inpu:61)                   Varetekst-2
     d  d_tek3                       10    overlay(d_inpu:96)                   Varetekst-3
     d  d_eann                       13  0 overlay(d_inpu:106)                  Ean-nummer
     d  d_enhe                        5    overlay(d_inpu:119)                  Enhet
     d  d_inpr                       10    overlay(d_inpu:124)                  Innkjøpspris
     d   d_inpr_kr                    7  0 overlay(d_inpr:1)                     - kroner
     d   d_inpr_Øre                   2  2 overlay(d_inpr:9)                     - øre
     d  d_saim                       10    overlay(d_inpu:134)                  Salgspris
     d   d_saim_kr                    7  0 overlay(d_saim:1)                     - kroner
     d   d_saim_Øre                   2  2 overlay(d_saim:9)                     - øre
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_ldor          s              6  0
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_udat          s               d   datfmt(*iso)
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
     d w_vtxt          s             90
      *
      * Definering av konstanter
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser input-fil, og legger data ut på leverandør vareregister
      *
     c                   read      flvwpf                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      d_inpu = fwirad
      *
      * Oversetter tekster til ascii og store bokstaver
      *
     c                   eval      d_tek1 = %trim(d_tek1)
     c                   eval      d_tek2 = %trim(d_tek2)
     c                   eval      d_tek3 = %trim(d_tek3)
      *
     c     X'46':'Æ'     xlate     d_tek1        d_tek1
     c     X'A0':'æ'     xlate     d_tek1        d_tek1
     c     X'77':'Ø'     xlate     d_tek1        d_tek1
     c     X'90':'ø'     xlate     d_tek1        d_tek1
     c     X'2A':'Å'     xlate     d_tek1        d_tek1
     c     X'EF':'å'     xlate     d_tek1        d_tek1
      *
     c     X'46':'Æ'     xlate     d_tek2        d_tek2
     c     X'A0':'æ'     xlate     d_tek2        d_tek2
     c     X'77':'Ø'     xlate     d_tek2        d_tek2
     c     X'90':'ø'     xlate     d_tek2        d_tek2
     c     X'2A':'Å'     xlate     d_tek2        d_tek2
     c     X'EF':'å'     xlate     d_tek2        d_tek2
      *
     c     X'46':'Æ'     xlate     d_tek3        d_tek3
     c     X'A0':'æ'     xlate     d_tek3        d_tek3
     c     X'77':'Ø'     xlate     d_tek3        d_tek3
     c     X'90':'ø'     xlate     d_tek3        d_tek3
     c     X'2A':'Å'     xlate     d_tek3        d_tek3
     c     X'EF':'å'     xlate     d_tek3        d_tek3
      *
     c     Lo:Up         xlate     d_tek1        d_tek1
     c     Lo:Up         xlate     d_tek2        d_tek2
     c     Lo:Up         xlate     d_tek3        d_tek3
      *
      * Skriver til leverandør vareregister
      *
     c                   eval      fwfirm = p_firm
     c                   eval      fwldor = p_ldor
     c                   eval      fwekod = *blank
     c                   eval      fwtek1 = *blank
     c                   eval      fwtek2 = *blank
     c                   eval      fwlvar = d_vare
     c                   eval      fweann = d_eann
     c                   eval      fwnobb = 0
     c                   eval      fwdato = *loval
     c                   eval      fwenhe = d_enhe
     c                   eval      fwinpr = d_inpr_kr + d_inpr_Øre
     c                   eval      fwsapr = (d_saim_kr + d_saim_Øre) * w_mvaf
     c                   eval      fwbmen = 0
     c                   eval      fwhkod = 1
      *
     c                   eval      fwline = fwline + 1
      *
      * Konkatinerer og splitter varetekst
      *
     c                   eval      w_vtxt = %trim(d_tek1) + ' ' +
     c                                      %trim(d_tek2) + ' ' +
     c                                      %trim(d_tek3)
      *
     c                   eval      fwtek1 = %subst(w_vtxt:1:35)
     c                   eval      fwtek2 = %subst(w_vtxt:36:35)
      *
      * Bestillingsmengde
      *
     c***                if        d_antp <> 0 and
     c***                          d_antk <> 0
     c***                eval      fwbmen = d_antk / d_antp
     c***                endif
      *
6.10 c                   time                    fwodat
6.10 c                   time                    fwotim
6.10 c                   eval      fwousr = l_user
6.10 c                   time                    fwedat
6.10 c                   time                    fwetim
6.10 c                   eval      fweusr = l_user
     c                   write     flvapfr
      *
     c                   read      flvwpf                                 90
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
      * Henter dagens dato
      *
     c                   time                    w_udat
      *
      * Henter mva-faktor
      *
     c                   call      'FÅ705R'
     c                   parm                    w_firm
     c                   parm                    w_udat
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
      *
     c                   endsr
