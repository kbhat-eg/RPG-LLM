     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FX842R
      * Beskrivelse . . : Leser alle priser pr en leverandør, hvis prisen
      *                   ikke er hovedleverandør på varen slettes den
      *                   Sjekker også lager og denne leverandør ligger
      *                   på et lager - fjerner i så fall denne
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       130113 bof  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvprl3    if   e           k disk    rename(vvprpfr:vvprl3r)
     fvvprlu    uf   e           k disk    rename(vvprpfr:vvprlur)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvlaglu    uf   e           k disk    rename(vlagpfr:vlaglur)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vpfirm)
     d p_ldor          s                   like(vpldor)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vvfirm)
     d w_ldor          s                   like(vpldor)
      *
      * Definering av nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vvprl3_ldor     s                   like(vpldor)
     d vvprlu_vare     s                   like(vpvare)
     d vvprlu_ldor     s                   like(vpldor)
     d vvprlu_prgr     s                   like(vpprgr)
     d vvprlu_enhe     s                   like(vpenhe)
     d vvprlu_lety     s                   like(vplety)
     d vvprlu_pdat     s                   like(vppdat)
     d vlaglu_vare     s                   like(vlvare)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser alle priser pr. levernadør
      *
     c                   eval      vvprl3_ldor = w_ldor
     c     vvprl3_key    setll     vvprl3
     c     vvprl3_key    reade     vvprl3
     c                   dow       not %eof(vvprl3)
      *
     c                   eval      vvarl1_vare = vpvare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found(vvarl1)  and
     c                             vpldor <> vvldor
      * en underleverandør pris , kan slettes
      *
     c                   eval      vvprlu_vare = vpvare
     c                   eval      vvprlu_ldor = vpldor
     c                   eval      vvprlu_prgr = vpprgr
     c                   eval      vvprlu_enhe = vpenhe
     c                   eval      vvprlu_lety = vplety
     c                   eval      vvprlu_pdat = vppdat
     c     vvprlu_key    chain     vvprlu
     c                   if        %found(vvprlu)
     c                   delete    vvprlur
     c                   exsr      sjekk_lager
     c                   endif
     c                   endif
      *
     c                   read      vvprl3
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker om varen har denne leverandør på noen av sine lager, fjerner
      * det
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_lager   begsr
     c                   eval      vlaglu_vare = vpvare
     c     vlaglu_key    setll     vlaglu
     c     vlaglu_key    reade     vlaglu
     c                   dow       not %eof(vlaglu)
     c                   if        vlbldo = w_ldor
     c                   eval      vlbldo = 0
     c                   time                    vledat
     c                   time                    vletim
     c                   eval      vleusr = l_user
     c                   update    vlaglur
     c                   endif
     c     vlaglu_key    reade     vlaglu
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ldor
      *
      * Nøkker for les og oppdatering av lager
      *
     c     vlaglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglu_vare
      *
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les av vare-pris pr. leverandør
      *
     c     vvprl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprl3_ldor
      *
      * Key for oppdatering av vare-pris
      *
     c     vvprlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprlu_vare
     c                   kfld                    vvprlu_ldor
     c                   kfld                    vvprlu_prgr
     c                   kfld                    vvprlu_enhe
     c                   kfld                    vvprlu_lety
     c                   kfld                    vvprlu_pdat
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_ldor = p_ldor
      *
     c                   endsr
