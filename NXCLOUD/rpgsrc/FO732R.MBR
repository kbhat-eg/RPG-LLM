     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO732R
      * Beskrivelse . . : Oppdatering av lager utfra endringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * R3.11 220300 AMD  Leveringstype er tatt med i datastruktur hlrec
      *                   I tillegg legges det ut referanse på 10 posisjoner
5.01  * R5.01 241002 AMD  Lagt om rutiner for oppdatering av lager
5.31  * R5.31 100104 ASK  Lagt inn endringer for lager på linjenivå.
      *                   Foreløpig blir bare varelinjer som ikke er
      *                   overstyrte endret ved endring på hodenivå.
      *                   (NB! Endring 5.31 er fjernet pga ny løsning
      *                    merket 5.40 (AMD))
5.40  * R5.40 270405 AMD  Lagt inn oppdatering av lager på linjenivå
      *                   der hvor lager var lik gammel kode på hode
      *                   eller alle linjer dersom det var valgt.
5.41  *  5.41 100806 AMD  Justeringer av lageroppdatering dersom det
      *                   var avvik mellom lager på hode og linje ved
      *                   skifting til nytt lager (se dok 2006_08_10)
5.70  *  5.70 081208 bof  Utvidet med prisgruppe - hvis pval = 0
5.70  *                   så endres prisene i hht. til valgt lager
      *  6.10  220909 ASK  Nye parametre til VL001R
6.11  *  6.10  110610 bof  Justert i utvidelse av parm VL001R
6.nu  * R6.30  030614 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.30  * 6.30  040615 bkv  Ordre økt fra 6 til 8
6.31  * 6.30  301018 bof  Utvidet med trelast-sortiment
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffstsl1    if   e           k disk    rename(fstspfr:fstsl1r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
5.40 ffodtlu    uf   e           k disk    rename(fodtpfr:fodtlur)
      *
      * Definering av parametere
      *
     d p_firm          s                   like(fofirm)
6.nu d p_numm          s                   like(fonumm)
     d p_suff          s                   like(fosuff)
     d p_foty          s                   like(footyp)
     d p_flag          s                   like(folage)
5.40 d p_valg          s              1
5.70 d p_pval          s              1
      *
      * Definering av datastruktur
      *
     d                 ds
6.nu d hlrec                        128
     d  hlvare                       15    overlay(hlrec)
     d  hllage                        2  0 overlay(hlrec:16)
     d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
     d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
     d  hlotyp                        2    overlay(hlrec:36)
     d  hlltyp                        2    overlay(hlrec:38)
     d  hlanta                       11  3 overlay(hlrec:40)
     d  hlkopr                        9  2 overlay(hlrec:51)
     d  hlrefr                       20    overlay(hlrec:60)
     d  hlsyst                        1    overlay(hlrec:80)
     d  hlaltk                        2    overlay(hlrec:81)
     d  hlrest                        1    overlay(hlrec:83)
     d  hltype                        1    overlay(hlrec:84)
     d  hlenhe                        3    overlay(hlrec:85)
     d  hlinlr                        1    overlay(hlrec:88)
     d  hllety                        1    overlay(hlrec:89)
6.nu d  hlonum                        8  0 overlay(hlrec:90)
6.nu d  hlolin                        4  0 overlay(hlrec:98)
      *
      * Definering av variable i nøkler
      *
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
5.40 d fodtlu_numm     s                   like(fdnumm)
5.40 d fodtlu_suff     s                   like(fdsuff)
5.40 d fodtlu_line     s                   like(fdline)
      *
      * Definering av variable
      *
     d w_firm          s                   like(fofirm)
     d w_tlag          s                   like(folage)
     d w_toty          s                   like(footyp)
     d w_lage          s                   like(folage)
     d w_otyp          s                   like(footyp)
6.nu d w_lrec          s            128
     d w_stat          s              1
5.01 d w_oakk          s              1  0
5.40 d w_updt          s              1  0
5.70 d w_enor          s              1
6.30 d w_numa          s              8
5.02 d w_lina          s              4
      *
6.31 d w_udat          s               d   datfmt(*iso)
6.31 d w_ldor          s                   like(fdldor)
6.31 d w_vtyp          s              1
6.31 d w_lv_nobb       s                   like(fdnobb)
6.31 d w_lv_modn       s              8  0
6.31 d w_lv_ldor       s                   like(fdldor)
6.31 d w_lv_lvar       s                   like(fdlvar)
6.31 d w_east          s              1
6.31 d w_eann          s                   like(fdeann)
      *
6.31 d b_inlr          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Lesing ordrehode    ORDRE-HODE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     fohel1_key    chain     fohel1r                            92
      *
     c                   eval      w_toty = footyp
     c                   eval      w_tlag = folage
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Lesing av ordre     ORDRE-LINJE-REGISTER
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     fodtl1_key    setll     fodtl1
     c     fodtl1_key    reade     fodtl1                                 90
      *
     c                   dow       *in90 = *off
      *
5.40  * Finn ut om linjer skal oppdateres
5.40  *
5.40 c                   eval      w_updt = 0
5.40 c                   if        p_valg <> '1'
5.40 c                   if        p_flag = fdlage
5.40 c                   eval      w_updt = 1
5.40 c                   endif
5.40 c                   endif
5.40  *
5.40 c                   if        p_valg =  '1'
5.40 c                   eval      w_updt = 1
5.40 c                   endif
      *
5.40 c                   if        w_updt = 1
      *
      * Kaller opp subrutine for oppdatering av lager
      *
     c                   if        faalag <> *zero
5.41 c                   if        fdlage <> w_tlag
     c                   eval      w_otyp =  p_foty
5.41 c                   eval      w_lage =  fdlage
     c                   mult      -1            fdanta
     c                   exsr      opplag
5.41 c                   endif
     c                   endif
      *
      * Kaller opp subrutine for oppdatering av lager
      *
     c                   if        faalag <> *zero
5.41 c                   if        fdlage <> w_tlag
     c                   eval      w_otyp = w_toty
     c                   eval      w_lage = w_tlag
     c                   mult      -1            fdanta
     c                   exsr      opplag
5.41 c                   endif
     c                   endif
5.40  *
5.40  * Oppdaterer linje med riktig lagernummer
5.40  *
5.40 c                   eval      fodtlu_numm = fdnumm
5.40 c                   eval      fodtlu_suff = fdsuff
5.40 c                   eval      fodtlu_line = fdline
5.40  *
5.40 c     fodtlu_key    chain     fodtlur
5.40 c                   if        %found
5.40 c                   eval      fdlage = w_tlag
6.31 c                   exsr      finn_ldor
6.31 c                   if        w_ldor <> 0
6.31 c                   eval      fdldor = w_ldor
6.31 c                   endif
6.31 c                   if        w_eann <> 0
6.31 c                   eval      fdeann = w_Eann
6.31 c                   endif
6.31 c                   if        w_lv_nobb <> 0
6.31 c                   eval      fdnobb = w_lv_nobb
6.31 c                   eval      fdlvar = w_lv_lvar
6.31 c                   endif
5.40 c                   update    fodtlur
5.40 c                   endif
      *
5.40 c                   endif
      *
     c     fodtl1_key    reade     fodtl1                                 90
     c                   enddo
5.70  *
5.70  * Her skal pris endres hvis valg er 0
5.70  *
5.70 c                   if        p_pval =  '0'
5.70 c                   eval      w_enor =  '1'
5.70 c                   call      'FO730R  '
5.70 c                   parm                    w_enor
5.70 c                   parm                    fofirm
5.70 c                   parm                    fonumm
5.70 c                   parm                    fosuff
5.70 c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdatering av Lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opplag        begsr
5.01  *
5.01  * Dersom behandlingskode er 2 eller 3,
5.01  * og beholdning ikke er oppdatert,
5.01  * skal heller ingen oppdatering skje her.
5.01  *
5.01 c                   call      'VA750R'
5.01 c                   parm                    w_firm
5.01 c                   parm                    w_otyp
5.01 c                   parm                    w_oakk
5.01  *
5.01 c                   if        w_oakk = 2 or
5.01 c                             w_oakk = 3
5.01 c                   if        foolag = 0
5.01 c                   leavesr
5.01 c                   endif
5.01 c                   endif
      *
      * Legger over verdier i overførings-felter
      *
     c                   eval      hlvare = fdvare
     c                   eval      hllage = w_lage
     c                   eval      hlenhe = fdenh1
     c                   move      *loval        hldato
     c                   move      *loval        hltime
     c                   eval      hlotyp = w_otyp
     c                   eval      hlltyp = fdltyp
      *
     c                   z-add     fdanta        hlanta
     c                   z-add     fdkopr        hlkopr
     c                   eval      hlonum = fdnumm
     c                   eval      hlolin = fdline
     c                   eval      hlsyst = 'F'
     c                   eval      hlaltk = *blank
     c                   eval      hlrest = *blank
     c                   move      fdnumm        w_numa
     c                   move      fdline        w_lina
     c                   eval      hlrefr = 'O:' + w_numa +
     c                                      ' L:' + w_lina
3.11 c                   eval      hllety = %editc(fdlety:'X')
      *
      * Kaller opp lageroppdaterings-program
      *
     c                   eval      w_lrec = hlrec
      *
     c                   call      'VL001R'
     c                   parm                    w_stat
     c                   parm                    w_lrec
      *
5.01 c     end_oppl      endsr
      *
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  * Subrutine for å hente leverandør, eann, evt. logi.vare-info
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *
6.31 c     finn_ldor     begsr
6.31 c
6.31 c                   call      'VL710R'
6.31 c                   parm                    w_firm
6.31 c                   parm                    fdvare
6.31 c                   parm                    fdlage
6.31 c                   parm                    w_vtyp
6.31 c                   parm                    w_udat
6.31 c                   parm                    w_ldor
6.31 c                   parm                    b_inlr
6.31 c                   parm                    w_lv_nobb
6.31 c                   parm                    w_lv_modn
6.31 c                   parm                    w_lv_ldor
6.31 c                   parm                    w_lv_lvar
6.31  *
6.31 c                   call      'VE713R'
6.31 c                   parm                    w_firm
6.31 c                   parm                    w_ldor
6.31 c                   parm                    fdvare
6.31 c                   parm                    fdenh1
6.31 c                   parm                    w_eann
6.31 c                   parm                    w_east
6.31 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for file : OFAK-KODE-REGISTER
      *
     c     fstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for file : ORDRE-HODE-REGISTER
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for file : ORDRE-LINJE-REGISTER
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
5.40  *
5.40  * Key for file : ORDRE-LINJE-REGISTER UPDATE
5.40  *
5.40 c     fodtlu_key    klist
5.40 c                   kfld                    w_firm
5.40 c                   kfld                    fodtlu_numm
5.40 c                   kfld                    fodtlu_suff
5.40 c                   kfld                    fodtlu_line
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Tar inn parameter fra forrige program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *entry        plist
     c                   parm                    p_firm
6.nu c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_foty
     c                   parm                    p_flag
5.40 c                   parm                    p_valg
5.70 c                   parm                    p_pval
      *
      * Legger inn type for oppdatering av lager  R=Registrering
      *
5.01 c                   eval      hltype = ' '
      *
      * Legger inn firmanummer i nøkklene
      *
5.01 c                   eval      w_firm = p_firm
      *
      * Legger inn ordrenummer og suffix inn i nøkklene
      *
     c                   eval      fohel1_numm = p_numm
     c                   eval      fohel1_suff = p_suff
      *
     c                   eval      fodtl1_numm = p_numm
     c                   eval      fodtl1_suff = p_suff
      *
      * Hent opplysninger fra OFAK-KODE-REGISTER
      *
     c     fstsl1_key    chain     fstsl1r                            90
      *
     c                   endsr
