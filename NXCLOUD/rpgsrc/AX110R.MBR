     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASPGPL
      * Program . . . . : AX110R
      * Beskrivelse . . : XML oppdatering nobb, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       140111 bof  Nytt program
6.11  *       140313 bof  Utvidet aktiv for ny løsning
6.20  *       150714 bof  Utvidet med en del felter
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     faxmll1    if   e           k disk    rename(axmlpfr:axmll1r)
     faxmllr    if   e           k disk    rename(axmlpfr:axmllrr)
     faxmllu    uf a e           k disk    rename(axmlpfr:axmllur)
      *
     fAX110d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d axmll1_mkun     s                   like(axmkun)
     d axmllr_mkun     s                   like(axmkun)
     d axmllu_mkun     s                   like(axmkun)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d s_mkun          s                   like(axmkun)
6.20 d s_mpsv          s                   like(axmpsv)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2mkun <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      axmll1_mkun = b1mkun
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Kundenr (asp)
      *
     c                   if        b2mkun <> 0
     c                   eval      w_seqe = *blank
     c                   eval      axmll1_mkun = b2mkun
     c     axmll1_key    setll     axmll1
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2mkun = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1mkun = b1mkun
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1mkun = b1mkun
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1mkun = b1mkun
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1mkun = b1mkun
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1mkun = b2mkun
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c1taga
     c                   endif
      *
     c                   if        c1mkun = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Validering
      *
     c***                if        c1mkun = 0
     c***                eval      *in31  = *on
     c***                goto      c1taga
     c***                endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      axmllr_mkun = c1mkun
     c     axmllr_key    chain     axmllr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1mkun = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2mkun = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      axmllr_mkun = c1mkun
     c     axmllr_key    chain     axmllr
      *
     c                   if        %found
6.11 c                   eval      c2makt = axmakt
6.11 c                   eval      c2meus = axmeus
     c                   if        axmsda <> *loval
     c     *dmy          move      axmsda        c2msda
     c                   else
     c                   eval      c2msda = 0
     c                   endif
     c                   eval      c2mba1 = %subst(axmban:1:50)
     c                   eval      c2mba2 = %subst(axmban:51:50)
     c                   if        axmoda <> *loval
     c     *dmy          move      axmoda        c2moda
     c                   else
     c                   eval      c2moda = 0
     c                   endif
     c                   if        axmeda <> *loval
     c     *dmy          move      axmeda        c2meda
     c                   else
     c                   eval      c2meda = 0
     c                   endif
6.20 c                   eval      c2mver = axmver
6.20 c                   eval      c2mfgr = axmfgr
6.20 c                   movel     axmfho        c2mfho
6.20 c                   eval      c2mdi1 = %subst(axmfdi:1:63)
6.20 c                   eval      c2mdi2 = %subst(axmfdi:64:63)
6.20 c                   eval      c2mfl1 = %subst(axmxfl:1:63)
6.20 c                   eval      c2mfl2 = %subst(axmxfl:64:63)
6.20 c                   eval      c2musr = axmusr
6.20 c                   eval      c2mpsv = axmpsv
     c                   else
     c                   eval      c2msda = 0
6.11 c                   eval      c2makt = 0
     c                   eval      c2mba1 = *blank
     c                   eval      c2mba2 = *blank
     c                   eval      c2meus = *blank
     c                   eval      c2moda = 0
     c                   eval      c2meda = 0
6.20 c                   eval      c2mver = 0
6.20 c                   eval      c2mfgr = *blank
6.20 c                   eval      c2mfho = *blank
6.20 c                   eval      c2mdi1 = *blank
6.20 c                   eval      c2mdi2 = *blank
6.20 c                   eval      c2mfl1 = *blank
6.20 c                   eval      c2mfl2 = *blank
6.20 c                   eval      c2musr = *blank
6.20 c                   eval      c2mpsv = *blank
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1mkun = k1mkun
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
6.20  *
6.20 c                   eval      s_mpsv = c2mpsv
6.20 c                   if        c2mpsv <> *blank
6.20 c                   eval      c2mpsv = '********************'
6.20 c                   else
6.20 c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2msda = 0
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
     c     *dmy          test(d)                 c2msda                 32
     c                   if        *in32 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   if        c2mba1 = *blank and
     c                             c2mba2 = *blank
     c                   eval      *in33 = *on
     c                   goto      c2taga
     c                   endif
6.20  *  hvis versjon 2, så er det krav til mange felter
6.20 c                   if        c2mver = 2
6.20  *
6.20 c                   if        c2mfgr = *Blank
6.20 c                   eval      *in34 = *on
6.20 c                   goto      c2taga
6.20 c                   endif
6.20  *
6.20 c                   if        c2mfho = *Blank
6.20 c                   eval      *in35 = *on
6.20 c                   goto      c2taga
6.20 c                   endif
6.20  *
6.20 c                   if        c2mdi1 = *Blank
6.20 c                   eval      *in36 = *on
6.20 c                   goto      c2taga
6.20 c                   endif
6.20  *
6.20 c                   if        c2musr = *Blank
6.20 c                   eval      *in37 = *on
6.20 c                   goto      c2taga
6.20 c                   endif
6.20  *
6.20 c                   if        c2mpsv = *Blank
6.20 c                   eval      *in38 = *on
6.20 c                   goto      c2taga
6.20 c                   endif
6.20 c                   endif
6.20  *
6.20 c                   if        %scan('*':c2mpsv) > 0
6.20 c                   eval      c2mpsv = s_mpsv
6.20 c                   endif
      *
      * Oppdatering
      *
     c                   clear                   axmllur
      *
     c                   eval      axmllu_mkun = c1mkun
     c     axmllu_key    chain     axmllur
      *
6.11 c                   eval      axmakt = c2makt
     c     *dmy          move      c2msda        axmsda
     c                   eval      axmban = c2mba1 + c2mba2
6.20 c                   eval      axmver = c2mver
6.20 c                   eval      axmfgr = c2mfgr
6.20 c                   movel     c2mfho        axmfho
6.20 c                   eval      axmfdi = c2mdi1 +
6.20 c                                      c2mdi2
6.20 c                   eval      axmxfl = c2mfl1 +
6.20 c                                      c2mfl2
6.20 c                   eval      axmusr = c2musr
6.20 c                   eval      axmpsv = c2mpsv
      *
     c                   time                    axmeda
     c                   time                    axmeti
     c                   eval      axmeus = l_user
      *
     c                   if        %found
     c                   update    axmllur
     c                   else
     c                   eval      axmkun = c1mkun
     c                   time                    axmoda
     c                   time                    axmoti
     c                   write     axmllur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   if        axmsda <> *loval
     c     *dmy          move      axmsda        b1msda
     c                   else
     c                   eval      b1msda = 0
     c                   endif
     c                   movel     axmban        b1mban
6.11 c                   eval      b1makt = axmakt
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      axmllr_mkun = d1mkun
     c     axmllr_key    chain     axmllr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      axmllu_mkun = d1mkun
     c     axmllu_key    chain     axmllur
      *
     c                   delete    axmllur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_mkun = k1mkun
      *
      * Fyller kopierings-bildet
      *
     c                   eval      axmllr_mkun = k1mkun
     c     axmllr_key    chain     axmllr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Sjekk om utfylt
      *
     c                   if        k1mkun = 0
     c                   eval      *in31 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      axmllr_mkun = k1mkun
     c     axmllr_key    chain     axmllr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1mkun = s_mkun
     c                   exsr      xc2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      axmll1
      *
     c                   if        %eof
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      axmll1_mkun = axmkun
      *
     c                   eval      b1valg = 0
     c                   eval      b1mkun = axmkun
6.11 c                   eval      b1makt = axmakt
     c                   if        axmsda <> *loval
     c     *dmy          move      axmsda        b1msda
     c                   else
     c                   eval      b1msda = 0
     c                   endif
     c                   movel     axmban        b1mban
6.20 c                   select
6.20 c                   when      axmver = 0
6.20 c                   eval      b1mver = *blank
6.20 c                   when      axmver = 1
6.20 c                   eval      b1mver = '2.6'
6.20 c                   when      axmver = 2
6.20 c                   eval      b1mver = '8.0'
6.20 c                   other
6.20 c                   eval      b1mver = *blank
6.20 c                   endsl
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     axmll1_key    setgt     axmll1
     c                   readp     axmll1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     axmll1
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      axmll1_mkun = axmkun
      *
     c                   enddo
      *
     c                   if        %eof
     c     axmll1_key    setll     axmll1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på rabattgruppe
      *
     c     axmll1_key    klist
     c                   kfld                    axmll1_mkun
      *
      * Key for les på rabattgruppe
      *
     c     axmllr_key    klist
     c                   kfld                    axmllr_mkun
      *
      * Key for oppdatering av rabattgruppe
      *
     c     axmllu_key    klist
     c                   kfld                    axmllu_mkun
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      axmll1_mkun = 0
     c     axmll1_key    setll     axmll1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
