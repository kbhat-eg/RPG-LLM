     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK651R                                              *
      * Beskrivelse . . : Betalingsvarsel - utplukk                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      * R5.30     180604  Kontroll mot forfall og bilag purret etter     KOB  *
      * R5.30     140705  Rentekode 3 skal ikke behandles                HFL  *
T5.40 * R5.40     110106  Ny parameter: Forfalte/alle.                   HFL  *
T5.40 *                   Dersom valgt A=alle poster og det er funnet
T5.40 *                   minst en forfalt post, leses reskontroposter
T5.40 *                   en gang til.
T5.40 *                   Da skal alle åpne poster som ikke har forfalt
T5.40 *                   også tas med.
      * R5.60     200906  Fjernet test på rentekode 3  - brukes i forb.  KOB  *
      *                   med kreditrente hos Mestergruppen              KOB  *
      * R5.60     171008  Lagt ut egen timestamp på arbeidsfil           KOB  *
      * R5.60     280409  Fjernet test på rnrest > 0                     KOB  *
6.10  * V6.10     230909  Lagt inn sporingsfelter                        BSA
6.11  * V6.10     070510  Endret sporing                                 BSA
6.12  * V6.10     160211  Rentekode 3 skal ikke purres
      *                   (Obs! Mestergruppen)                                *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frkunlu    uf   e           k disk    rename(rkunpfr:rkunlur)
     frktrl1    if   e           k disk    rename(rktrpfr:rktrl1r)
T5.40frktrl9    if   e           k disk    rename(rktrpfr:rktrl9r)
     frkwbpf    uf a e           k disk    rename(rkwbpfr:rkwbpfr)
      *
     d                uds
     d  pfirm                300    302  0
     d  pnavn                303    332
     d  puser                333    342
     d  pkdat                343    348  0
     d  praar                349    352  0
     d  prper                353    354  0
     d  pavdf                355    358  0
     d  pavdt                359    362  0
     d  pkatf                363    366  0
     d  pkatt                367    370  0
     d  pself                371    374  0
     d  pselt                375    378  0
     d  pknrf                379    384  0
     d  pknrt                385    390  0
     d  kun                  391    450  0 dim(10)
     d  pknr1                391    396  0
     d  pknr2                397    402  0
     d  pknr3                403    408  0
     d  pknr4                409    414  0
     d  pknr5                415    420  0
     d  pknr6                421    426  0
     d  pknr7                427    432  0
     d  pknr8                433    438  0
     d  pknr9                439    444  0
     d  pknr0                445    450  0
     d  pfdat                451    456  0
     d  plope                457    459  0
     d  ppkod                460    460
     d  ppros                461    463  1
     d  pgrns                464    465  0
     d  pgbyr                466    466
     d  pmini                467    468  0
     d  pbskr                469    469
     d  pegrn                470    470
     d  pajor                471    471
     d  ptext                472    472  0
     d  ppunr                473    473  0
     d  ppbeh                474    474
     d  palfa                475    475
     d  pdfgg                476    481  0
T5.40d  ppfor                482    482
     d  pparam               300    490
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d w_fdat          s               d   datfmt(*iso)
     d w_kdat          s               d   datfmt(*iso)
     d w_forf          s               d   datfmt(*iso)
     d w_rest          s                   like(rnrest)
     d w_firm          s                   like(rkfirm)
      *
     d                 ds
     d w_dato                         6  0                                      Hele utvekslingsfelt
     d  w_dag                         2  0 overlay(w_dato)                      Status
     d  w_mnd                         2  0 overlay(w_dato:3)                    Firma
     d  w_aar                         2  0 overlay(w_dato:5)                    Firma
      *
     d w_udat          s              6  0
      *
     d n               s              2  0
     d wkun            s              7  0
T5.40d wfkund          s                   like(rkkund)
     d rktrl1_kund     s                   like(rkkund)
T5.40d rktrl9_kund     s                   like(rkkund)
     d rkunl1_kund     s                   like(rkkund)
      *
      * Definering av variable for timestamp
      *
     d w_tmst          s               z
     d w_tmst_20       s             20
     d w_tell          s              6  0
     d w_tell_alf      s              6
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Utplukk av kunder til inkassovarsel                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c                   eval      w_firm = l_firm
      *
     c     rkunlu_key    setll     rkunlu                                 61
     c     rkunlu_key    reade     rkunlu                                 61
      *
     c                   dow       *in61 = *off
      *
      * Kunden skal ikke behandles
      *
     c                   if        rkrenk = 2 or
T5.30c***                          rkrenk = 3
6.12 c                             rkrenk = 3
     c                   goto      nykund
     c                   endif
      *
      * Sjekk om kunden er oppgitt i tabell
      *
     c                   eval      n = 1
     c                   xfoot     kun           wkun
      *
     c                   if        wkun <> 0
     c                   dou       n > 10
     c                   if        rkkund = kun(n)
     c                   goto      kunmed
     c                   endif
     c                   eval      n = n + 1
     c                   enddo
T3.30c                   goto      nykund
     c                   endif
      *
     c                   if        rkkund < pknrf or
     c                             rkkund > pknrt
     c                   goto      nykund
     c                   endif
      *
     c     kunmed        tag
      *
     c                   if        rksald <= 0
     c                   goto      nykund
     c                   endif
      *
     c                   if        rkkatg < pkatf or
     c                             rkkatg > pkatt
     c                   goto      nykund
     c                   endif
      *
     c                   if        rkslgr < pself or
     c                             rkslgr > pselt
     c                   goto      nykund
     c                   endif
      *
T5.40c                   eval      wfkund = *zero
     c                   exsr      beh_trans
      *
T5.40 * Dersom valgt A=alle poster og det er funnet minst en forfalt
T5.40 * post, leses reskontroposter en gang til.
T5.40 * Da skal alle åpne poster som ikke har forfalt, også tas med.
      *
T5.40c                   if        ppfor  = 'A' and
T5.40c                             wfkund <> *zero
T5.40c                   exsr      beh_trans2
T5.40c                   endif
      *
     c                   if        w_rest < 0
     c                   eval      rkpneg = 'N'
     c                   else
     c                   eval      rkpneg = ' '
     c                   endif
      *
6.10 c*                  time                    rkedat
6.10 c*                  time                    rketim
6.10 c*                  eval      rkesig = l_user
6.11 c                   time                    rk2dpt
6.11 c                   eval      rkepgm = 'RK651R    '
     c                   update    rkunlur
      *
     c     nykund        tag
      *
     c                   eval      w_rest = 0
      *
     c     rkunlu_key    reade     rkunlu                                 61
      *
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
      *
     c                   exsr      slett_trans
      *
     c                   eval      *inlr = *on
     c                   return
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Utplukk av kundeposter til purring                              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     beh_trans     begsr
      *
     c                   eval      w_rest = 0
      *
     c                   eval      rktrl1_kund = rkkund
      *
     c     rktrl1_key    setll     rktrl1                                 62
     c     rktrl1_key    reade     rktrl1                                 62
      *
     c                   dow       *in62 = *off
      *
      * Sjekk avdeling
      *
     c                   if        rnavde = *zeros
     c                   eval      rnavde = rkavde
     c                   endif
      *
     c                   if        pavdf <> *zeros and
     c                             rnavde < pavdf
     c                   goto      neste
     c                   endif
      *
     c                   if        pavdt <> *zeros and
     c                             rnavde > pavdt
     c                   goto      neste
     c                   endif
      *
      * Sendt til inkasso tidligere
      *
     c                   if        rninko = 'I' or
     c                             rninko = 'J'
     c                   goto      neste
     c                   endif
      *
      * Reklamasjon
      *
     c                   if        rnbilk = 89  or
     c                             rnrekl = 'J'
     c                   goto      neste
     c                   endif
      *
      * Restbeløp = 0
      *
     c                   if        rnrest = 0
     c                   goto      neste
     c                   endif
      *
      * Sjekk antall purringer
      *
     c                   select
     c                   when      ppbeh = '<' and
     c                             rnantp >= ppunr
     c                   goto      neste
      *
     c                   when      ppbeh = '=' and
     c                             ppunr <> 0 and
     c                             rnantp <> ppunr
     c                   goto      neste
      *
     c                   when      ppbeh = '>' and
     c                             rnantp <= ppunr
     c                   goto      neste
     c                   endsl
      *
      * Skal posten purres ?
      *
S5.60c***                if        rnrest > 0
      *
      * Sjekk forfallsdato på posteringen +
      * løpedager mot kjøredato
      *
     c     rnfdat        adddur    plope : *d    w_forf
      *
     c                   if        w_forf >= w_kdat
     c                   goto      neste
     c                   endif
      *
      * Sjekk dato for siste purredato fra parameter mot
      * siste purredato på postering
      *
     c                   if        rnpdat > w_fdat
     c                   goto      neste
     c                   endif
      *
S5.60c***                endif
      *
     c                   if        palfa = 'J'
     c                   eval      rwkalf = rkalfa
     c                   else
     c                   eval      rwkalf = *blank
     c                   endif
      *
      * Teller/Timestamp
      *
     c                   eval      w_tell = w_tell + 1
     c                   move      w_tell        w_tell_alf
     c                   time                    rwtist
     c     *iso0         move      rwtist        w_tmst_20
     c                   move      w_tell_alf    w_tmst_20
     c     *iso0         move      w_tmst_20     rwtist
      *
     c                   write     rkwbpfr
      *
T5.40 * Denne kunde har forfalt post
      *
T5.40c                   eval      wfkund = rnkund
      *
     c                   eval      w_rest = w_rest + rnrest
      *
     c     neste         tag
      *
     c     rktrl1_key    reade     rktrl1                                 62
     c                   enddo
      *
     c                   endsr
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
T5.40 * Utplukk av kundeposter til purring 2. gang (valg A=alle)        *
T5.40 * Subrutinen er lik BEH_TRANS bortsett fra linjer merket 5.30     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
T5.40c     beh_trans2    begsr
      *
S5.40c*                  eval      w_rest = 0
      *
E5.40c                   eval      rktrl9_kund = wfkund
      *
E5.40c     rktrl9_key    setll     rktrl9                                 62
E5.40c     rktrl9_key    reade     rktrl9                                 62
      *
     c                   dow       *in62 = *off
      *
      * Sjekk avdeling
      *
     c                   if        rnavde = *zeros
     c                   eval      rnavde = rkavde
     c                   endif
      *
     c                   if        pavdf <> *zeros and
     c                             rnavde < pavdf
E5.40c                   goto      neste2
     c                   endif
      *
     c                   if        pavdt <> *zeros and
     c                             rnavde > pavdt
E5.40c                   goto      neste2
     c                   endif
      *
      * Sendt til inkasso tidligere
      *
     c                   if        rninko = 'I' or
     c                             rninko = 'J'
E5.40c                   goto      neste2
     c                   endif
      *
      * Reklamasjon
      *
     c                   if        rnbilk = 89  or
     c                             rnrekl = 'J'
E5.40c                   goto      neste2
     c                   endif
      *
      * Restbeløp = 0
      *
     c                   if        rnrest = 0
E5.40c                   goto      neste2
     c                   endif
      *
      * Sjekk antall purringer
      *
     c                   select
     c                   when      ppbeh = '<' and
     c                             rnantp >= ppunr
E5.40c                   goto      neste2
      *
     c                   when      ppbeh = '=' and
     c                             ppunr <> 0 and
     c                             rnantp <> ppunr
E5.40c                   goto      neste2
      *
     c                   when      ppbeh = '>' and
     c                             rnantp <= ppunr
E5.40c                   goto      neste2
     c                   endsl
      *
      * Skal posten purres ?
      *
S5.60c***                if        rnrest > 0
      *
      * Sjekk forfallsdato på posteringen +
      * løpedager mot kjøredato
      *
     c     rnfdat        adddur    plope : *d    w_forf
      *
T5.40 * Tar med åpne, ennå ikke forfalte poster
T5.40 * Åpne, forfalte poster ble lagt ut i BEH_TRANS
      *
E5.40c                   if        w_forf <  w_kdat
E5.40c                   goto      neste2
     c                   endif
      *
      * Sjekk dato for siste purredato fra parameter mot
      * siste purredato på postering
      *
     c                   if        rnpdat > w_fdat
E5.40c                   goto      neste2
     c                   endif
      *
S5.60c***                endif
      *
     c                   if        palfa = 'J'
     c                   eval      rwkalf = rkalfa
     c                   else
     c                   eval      rwkalf = *blank
     c                   endif
      *
     c                   write     rkwbpfr
      *
T5.40 * Denne kunde har forfalt post
      *
T5.40c                   eval      wfkund = rnkund
      *
     c                   eval      w_rest = w_rest + rnrest
      *
E5.40c     neste2        tag
      *
E5.40c     rktrl9_key    reade     rktrl9                                 62
     c                   enddo
      *
     c                   endsr
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Slett poster som gir negativ saldo                              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     slett_trans   begsr
      *
     c     rkwbpf_key    setll     rkwbpf                                 63
     c     rkwbpf_key    reade     rkwbpf                                 63
      *
     c                   dow       *in63 = *off
      *
      * Sjekk kunde
      *
     c                   eval      rkunl1_kund = rnkund
     c     rkunl1_key    chain     rkunl1                             64
      *
     c                   if        rkpneg = 'N'
     c                   delete    rkwbpfr
     c                   endif
      *
     c     rkwbpf_key    reade     rkwbpf                                 63
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c     *dmy          move      pfdat         w_fdat
     c     *dmy          move      pkdat         w_kdat
      *
      * Key for lesing av kunderegister
      *
     c     rkunlu_key    klist
     c                   kfld                    w_firm
      *
      * Key for lesing av kunderegister
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for lesing av kundeposteringer
      *
     c     rktrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rktrl1_kund
      *
T5.40 * Key for lesing av kundeposteringer 2. gang
      *
T5.40c     rktrl9_key    klist
T5.40c                   kfld                    w_firm
T5.40c                   kfld                    rktrl9_kund
      *
      * Key for lesing av utplukkede posteringer
      *
     c     rkwbpf_key    klist
     c                   kfld                    w_firm
      *
     c                   endsr
