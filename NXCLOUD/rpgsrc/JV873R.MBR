     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JV873R
      * Beskrivelse . . : Sjekker om varen finnes på master nivå
      *                   Hvis finnes i vvarpf legg i sortiment
      *                   Hvis ikke finnes i vvarpf frigi vare
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       011107 bof  Nytt program
6.11  * 6.10  130709 bof  Utivdet eannr til 14 og brukt std. oppslag
6.20  * 6.20  071110 bof  Utivdet med lager på sortiment, satt til 0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * master
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      * til filgr
     fvsdtlu    uf a e           k disk    rename(vsdtpfr:vsdtlur)
     fjvscl1    if   e           k disk    rename(jvscpfr:jvscl1r)
     fjvsclu    uf   e           k disk    rename(jvscpfr:jvsclur)
     fjvdtlu    uf   e           k disk    rename(jvdtpfr:jvdtlur)
      *
      * Definering av parametre
      *
     d p_scan          s             26
     d vsdtlu_vsor     s                   like(vdvsor)
6.20 d vsdtlu_lage     s                   like(vdlage)
     d vsdtlu_ldor     s                   like(vdldor)
     d vsdtlu_ogrp     s                   like(vdogrp)
     d vsdtlu_hgrp     s                   like(vdhgrp)
     d vsdtlu_ugrp     s                   like(vdugrp)
     d vsdtlu_modn     s                   like(vdmodn)
     d vsdtlu_vare     s                   like(vdvare)
      *
     d jvdtlu_dvar     s                   like(jvdvar)
     d vvarl1_vare     s                   like(vvvare)
     d jvsclu_cean     s                   like(jscean)
     d jvsclu_csca     s                   like(jscsca)
      *
     d w_tfir          s                   like(vvfirm)
     d w_mfir          s                   like(vvfirm)
     d w_vsor          s                   like(vdvsor)
6.11 d w_eava          s                   like(vvvare)
6.11 d w_eaen          s                   like(vvenhe)
6.11 d w_east          s              1
6.11 d w_firm          s              3  0
      *
     d p_tfir          s                   like(vvfirm)
     d p_mfir          s                   like(vvfirm)
     d p_vsor          s                   like(vdvsor)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
5.54 d l_firm                944    946  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
     c                   read      jvscl1
      *
     c                   dow       not %eof(jvscl1)
      *
6.11 c                   call      'VE710R  '
6.11 c                   parm                    w_firm
6.11 c                   parm                    jscean
6.11 c                   parm                    w_eava
6.11 c                   parm                    w_eaen
6.11 c                   parm                    w_east
6.11 c                   if        w_east = '1'
     c                   exsr      frigi
     c                   else
     c                   exsr      upd_sort
     c                   endif
      *
     c                   read      jvscl1
     c                   enddo
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å frigi varer i vareadministrasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     frigi         begsr
      *
     c                   eval      jvdtlu_dvar = w_eava
     c     jvdtlu_key    chain     jvdtlu
      *
     c                   if        %found(jvdtlu)
     c                   eval      jvdhko = 0
     c                   update    jvdtlur
     c                   endif
      *
      * status settes til 1 i scannereg.
     c                   eval      jvsclu_cean = jscean
     c                   eval      jvsclu_csca = jscsca
     c     jvsclu_key    chain     jvsclu
     c                   if        %found(jvsclu)
     c                   eval      jscsts = 1
     c                   update    jvsclur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatere sortiment i EVR
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     upd_sort      begsr
      *
      * leser vareregister i master
     c                   eval      vvarl1_vare = w_eava
     c     vvarl1_key    chain     vvarl1
     c
      * Oppdaterer sortiment i master
      *
     c                   clear                   vsdtlur
      *
     c                   eval      vsdtlu_vsor = w_vsor
6.20 c                   eval      vsdtlu_lage = 0
     c                   eval      vsdtlu_ldor = vvldor
     c                   eval      vsdtlu_ogrp = vvogrp
     c                   eval      vsdtlu_hgrp = vvhgrp
     c                   eval      vsdtlu_ugrp = vvugrp
     c                   eval      vsdtlu_modn = vvnmod
     c                   eval      vsdtlu_vare = vvvare
     c     vsdtlu_key    chain     vsdtlur
      *
     c                   eval      vdeusr = l_user
     c                   time                    vdedat
     c                   time                    vdetim
      *
     c                   if        not %found(vsdtlu)
     c                   eval      vdfirm = w_mfir
     c                   eval      vdvsor = w_vsor
6.20 c                   eval      vdlage = 0
     c                   eval      vdldor = vvldor
     c                   eval      vdogrp = vvogrp
     c                   eval      vdhgrp = vvhgrp
     c                   eval      vdugrp = vvugrp
     c                   eval      vdmodn = vvnmod
     c                   eval      vdvare = vvvare
     c                   time                    vdodat
     c                   write     vsdtlur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_mfir
     c                   kfld                    vvarl1_vare
      *
      * Key for frigi varer
      *
     c     jvdtlu_key    klist
     c                   kfld                    w_tfir
     c                   kfld                    jvdtlu_dvar
      *
      * Key for oppslag på scannereg
      *
     c     jvsclu_key    klist
     c                   kfld                    jvsclu_cean
     c                   kfld                    jvsclu_csca
      *
      * Key for oppdatering av varesortiment-linje-register
      *
     c     vsdtlu_key    klist
     c                   kfld                    w_mfir
     c                   kfld                    vsdtlu_vsor
6.20 c                   kfld                    vsdtlu_lage
     c                   kfld                    vsdtlu_ldor
     c                   kfld                    vsdtlu_ogrp
     c                   kfld                    vsdtlu_hgrp
     c                   kfld                    vsdtlu_ugrp
     c                   kfld                    vsdtlu_modn
     c                   kfld                    vsdtlu_vare
      *
     C     *ENTRY        PLIST
     C                   PARM                    p_mfir
     C                   PARM                    p_tfir
     C                   PARM                    p_vsor
      *
      *master fiigr. firm og sortiemnt
     c                   eval      w_mfir = p_mfir
     c                   eval      w_vsor = p_vsor
      *
      *til filgr. firm
     c                   eval      w_tfir = p_tfir
      *
     c                   endsr
