     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSHOP
      * Program . . . . : BU725R
      * Beskrivelse . . : Butikk, eksport av vareinformasjon
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       220304 HKO  Nytt program
      *       020805 HKO  Endret les mot vare-enhet for å finne salgsenheter
5.70  * PRGR  121208 bof  Utvidet med prisgruppe
6.11  * 6.11  140709 bof  Utvidet med ean-nr fra vare-enhet-leverandør
6.12  *       190410 bof  utvidet med kasse for å hente riktig lager
6.20  * 6.20  190511 bof  Henter utg. fra lager - byttet program
6.21  * 6.20  190511 bof  Utvidet varepris med leverandør
6.30  * 6.30  170918 bkv  Utvidet med trelast sortiment
8.01  *PRG2   080622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvsol1    if   e           k disk    rename(vvsopfr:vvsol1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
     fvvenl2    if   e           k disk    rename(vvenpfr:vvenl2r)
6.11 fvvepl1    if   e           k disk    rename(vveppfr:vvepl1r)
     fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
     fvvprlr    if   e           k disk    rename(vvprpfr:vvprlrr)
     fveanl1    if   e           k disk    rename(veanpfr:veanl1r)
     fveanl2    if   e           k disk    rename(veanpfr:veanl2r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fra26l1    if   e           k disk    rename(ra26pfr:ra26l1r)
     fvmvalr    if   e           k disk    rename(vmvapfr:vmvalrr)
      *
     fbovast    uf a e             disk    rename(bovast:bovastr)
     fbovai1    if   e           k disk    rename(bovast:bovai1r)
     fbovest    uf a e             disk    rename(bovest:bovestr)
     fbovpst    uf a e             disk    rename(bovpst:bovpstr)
     fboeast    uf a e             disk    rename(boeast:boeastr)
6.12 fbkasi1    if   e           k disk    rename(bkasst:bkasi1r)
6.30 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vvfirm)
     d p_dato          s               d   datfmt(*iso)
     d p_time          s               t   timfmt(*iso)
6.12 d p_kass          s                   like(bkkass)
      *
      * Definering av variabler i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vvsol1_svar     s                   like(vvsvar)
     d vvenl2_vare     s                   like(vevare)
     d vvenl2_saen     s                   like(vesaen)
6.11 d vvepl1_pvar     s                   like(vepvar)
6.11 d vvepl1_penh     s                   like(vepenh)
6.11 d vvepl1_pldo     s                   like(vepldo)
     d vvprlr_vare     s                   like(vpvare)
6.21 d vvprlr_ldor     s                   like(vpldor)
5.70 d vvprlr_prgr     s                   like(vpprgr)
     d vvprlr_enhe     s                   like(vpenhe)
     d veanl2_vare     s                   like(vxvare)
     d rlevl1_levr     s                   like(rllevr)
     d ra26l1_zsak     s                   like(razsak)
     d bovai1_vare     s                   like(vare)
     d vmvalr_mkod     s                   like(vamkod)
6.12 d bkasi1_kass     s                   like(bkkass)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vvfirm)
      *
8.01 d w_prgr          s              2
5.70 d w_lage          s              2  0
5.70 d w_vtyp          s                   like(vvvtyp)
6.21 d w_ldor          s                   like(vvldor)
6.30 d w_lv_nobb       s                   like(vvlnob)
6.30 d w_lv_modn       s                   like(vvlmod)
6.30 d w_lv_lldo       s                   like(vvlnle)
6.30 d w_lv_llva       s                   like(vvllva)
      *
     d w_cdat          s               d   datfmt(*iso)
6.20 d w_utda          s               d   datfmt(*iso)
      *
     d s_lety          s                   like(vplety)
     d s_pdat          s                   like(vppdat)
      *
5.70 d b_pris          s              1    inz(*off)
6.20 d b_inlr          s              1    inz(*off)
      *
5.70 d                uds
5.70 d l_user                911    920
5.70 d l_sfir                944    946  0
5.70 d l_savd                947    950  0
5.70 d l_lagr               1001   1002  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter current-dato
      *
     c                   time                    w_cdat
      *
      * Initierer filer
      *
     c                   clear                   bovastr
     c                   clear                   bovestr
     c                   clear                   bovpstr
     c                   clear                   boeastr
      *
     c                   eval      firm = w_firm
      *
      * Leser alle varer
      *
     c     vvarl1_key2   setll     vvarl1
     c     vvarl1_key2   reade     vvarl1
     c                   dow       %eof = *off
      *
     c                   select
     c                   when      p_dato = *loval
     c                   exsr      vareinfo
     c                   when      p_dato <> *loval
     c                   if        p_dato < vvedat or
     c                             p_dato = vvedat and
     c                             p_time <= vvetim
     c                   exsr      vareinfo
     c                   endif
     c                   endsl
      *
     c     vvarl1_key2   reade     vvarl1
     c                   enddo
      *
      * Dersom endringsdato/endringstidspunkt er oppgitt letes det også
      * etter endrede enheter, priser eller ean-nr
      *
     c                   if        p_dato = *loval
     c                   goto      avslutt
     c                   endif
      *
      * Endrede enheter
      *
     c     vvenl1_key    setll     vvenl1
     c     vvenl1_key    reade     vvenl1
     c                   dow       %eof = *off
      *
     c                   if        p_dato < veedat or
     c                             p_dato = veedat and
     c                             p_time <= veetim
     c                   eval      bovai1_vare = vevare
     c     bovai1_key    chain     bovai1
     c                   if        not %found
     c                   eval      vvarl1_vare = vevare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   exsr      vareinfo
     c                   endif
     c                   endif
     c                   endif

     c     vvenl1_key    reade     vvenl1
     c                   enddo
      *
      * Endrede priser
      *
     c     vvprl1_key    setll     vvprl1
     c     vvprl1_key    reade     vvprl1
     c                   dow       %eof = *off
      *
     c                   if        p_dato < vpedat or
     c                             p_dato = vpedat and
     c                             p_time <= vpetim
     c                   eval      bovai1_vare = vpvare
     c     bovai1_key    chain     bovai1
     c                   if        not %found
     c                   eval      vvarl1_vare = vpvare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   exsr      vareinfo
     c                   endif
     c                   endif
     c                   endif

     c     vvprl1_key    reade     vvprl1
     c                   enddo
      *
      * Endrede ean-nr
      *
     c     veanl1_key    setll     veanl1
     c     veanl1_key    reade     veanl1
     c                   dow       %eof = *off
      *
     c                   if        p_dato < vxedat or
     c                             p_dato = vxedat and
     c                             p_time <= vxetim
     c                   eval      bovai1_vare = vxvare
     c     bovai1_key    chain     bovai1
     c                   if        not %found
     c                   eval      vvarl1_vare = vxvare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   exsr      vareinfo
     c                   endif
     c                   endif
     c                   endif

     c     veanl1_key    reade     veanl1
     c                   enddo
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av komplett vareinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     vareinfo      begsr
      *
      * henter varetype
      *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    w_lage
5.70 c                   parm                    w_vtyp
6.20 c                   parm                    w_utda
6.20 c                   parm                    w_ldor
6.20 c                   parm                    b_inlr
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
      *
     c                   eval      vare = vvvare
     c                   eval      ogrp = vvogrp
     c                   eval      hgrp = vvhgrp
     c                   eval      ugrp = vvugrp
     c                   eval      tek1 = vvtek1
     c                   eval      tek2 = vvtek2
6.30 c                   if        w_lv_nobb > 0
6.30 c                   eval      nobb = w_lv_nobb
6.30 c                   eval      modn = w_lv_modn
     c                   eval      lvar = w_lv_llva
6.30 c                   else
     c                   eval      nobb = vvnobb
     c                   eval      modn = vvnmod
     c                   eval      lvar = vvlvar
6.30 c                   endif
     c                   eval      ldor = vvldor
     c                   eval      lnav = *blank
     c                   eval      delt = vvnlev
     c                   eval      avar = vvavar
     c                   eval      ahgr = vvahgr
     c                   eval      augr = vvaugr
     c                   eval      kpri = vvkpri
5.70 c                   eval      vtyp = w_vtyp
     c                   eval      enhe = vvenhe
     c                   eval      enhs = vvenhs
     c                   eval      enhl = vvenhl
     c                   eval      pran = *blank
     c                   eval      crdo = vvcrdo
      * Hente reell momskode
     c                   eval      vmvalr_mkod = vvkmva
     c     vmvalr_key    chain     vmvalr
     c                   if        %found
     c                   eval      kmva = vamkof
     c                   else
     c                   eval      kmva = *blank
     c                   endif
      *
     c                   eval      omrn = vvomrn
6.20 c                   eval      udat = w_utda
     c                   eval      nuda = vvnuda
     c                   eval      stek = *blank
     c                   eval      odat = vvodat
     c                   eval      edat = vvedat
     c                   eval      etim = vvetim
     c                   eval      eusr = vveusr
      *
      * Leverandørnavn
      *
     c                   if        vvldor <> 0
     c                   eval      rlevl1_levr = vvldor
     c     rlevl1_key    chain     rlevl1
     c                   if        %found
     c                   eval      lnav = rlnavn
     c                   endif
     c                   endif
      *
      * Produktansvarlig
      *
     c                   if        vvpran <> *blank
     c                   eval      ra26l1_zsak = vvpran
     c     ra26l1_key    chain     ra26l1
     c                   if        %found
     c                   eval      pran = raztxt
     c                   endif
     c                   endif
      *
      * Søketekst
      *
     c                   eval      vvsol1_svar = vvvare
     c     vvsol1_key    chain     vvsol1
     c                   if        %found
     c                   eval      stek = %trim(vvstek)
     c                   endif
      *
      * Skriver til fil
      *
     c                   write     bovastr
      *
      * Tilhørende enheter og priser
      *
     c                   exsr      enheter
      *
      * Tilhørende EAN-nr
      *
     c                   exsr      ean
      *
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av enheter og priser for vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     enheter       begsr
      *
     c                   eval      vvenl2_vare = vvvare
     c                   eval      vvenl2_saen = 1
     c     vvenl2_key    setll     vvenl2
     c     vvenl2_key2   reade     vvenl2
     c                   dow       %eof = *off
      *
     c                   eval      vare = vevare
     c                   eval      enhe = veenhe
     c                   eval      linj = vesaen
     c                   eval      omre = veomre
     c                   eval      bred = 0
     c                   eval      leng = 0
     c                   eval      hoyd = 0
     c                   eval      vekt = 0
     c                   eval      volu = 0
     c                   eval      saen = vesaen
     c                   eval      inen = veinen
     c                   eval      pben = vepben
     c                   eval      odat = veedat
     c                   eval      edat = veedat
     c                   eval      etim = veetim
     c                   eval      eusr = veeusr
      *
     c                   write     bovestr
      *
      * Tilhørende priser
6.21  *
6.21 c                   eval      b_pris = *off
6.21 c                   if        w_prgr <> *blank
6.21 c                   eval      vvprlr_prgr = w_prgr
6.21  *  med prisgruppe
6.21  * 1. leverandør for lageret
6.21 c                   eval      vvprlr_ldor = w_ldor
6.21 c                   exsr      priser
6.21 c                   if        b_pris = *off  and
6.21 c                             w_ldor <> vvldor
6.21  * 2. hovedleverandør
6.21 c                   eval      vvprlr_ldor = vvldor
6.21 c                   exsr      priser
6.21 c                   if        b_pris = *off
6.21  * siste mulighet...
6.21 c                   eval      vvprlr_ldor = 0
6.21 c                   exsr      priser
6.21 c                   endif
6.21 c                   endif
6.21 c                   endif
6.21 c                   if        b_pris = *off
6.21 c                   eval      vvprlr_prgr = *blank
6.21  *  uten  prisgruppe
6.21  * 1. leverandør for lageret
6.21 c                   eval      vvprlr_ldor = w_ldor
6.21 c                   exsr      priser
6.21 c                   if        b_pris = *off  and
6.21 c                             w_ldor <> vvldor
6.21  * 2. hovedleverandør
6.21 c                   eval      vvprlr_ldor = vvldor
6.21 c                   exsr      priser
6.21 c                   if        b_pris = *off
6.21  * siste mulighet...
6.21 c                   eval      vvprlr_ldor = 0
6.21 c                   exsr      priser
6.21 c                   endif
6.21 c                   endif
6.21 c                   endif
      *
     c     vvenl2_key2   reade     vvenl2
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av priser for vare og enhet
      * Henter gjeldene og framtidige priser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     priser        begsr
      *
     c                   eval      s_lety = -9
     c                   eval      s_pdat = *loval
      *
     c                   eval      vvprlr_vare = vvvare
     c                   eval      vvprlr_enhe = veenhe
     c     vvprlr_key    setll     vvprlr
     c     vvprlr_key    reade     vvprlr
     c                   dow       %eof = *off
      *
     c                   if        vplety <> s_lety or
     c                             s_pdat > w_cdat
     c
     c                   eval      vare = vpvare
     c                   eval      enhe = vpenhe
      *                  eval      ldor = vpldor
     c                   eval      lety = vplety
     c                   eval      pdat = vppdat
     c                   eval      sapr = vpsapr
     c                   eval      kopr = vpkopr
     c                   eval      inpr = vpinpr
     c                   eval      bupr = vpbupr
     c                   eval      nopr = 0
     c                   eval      odat = vpedat
     c                   eval      edat = vpedat
     c                   eval      etim = vpetim
     c                   eval      eusr = vpeusr
      *
     c                   write     bovpstr
      *
5.70 c                   eval      b_pris = *on
      *
     c                   endif
      *
     c                   eval      s_lety = vplety
     c                   eval      s_pdat = vppdat
      *
     c     vvprlr_key    reade     vvprlr
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av EAN-nr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ean           begsr
6.11 c                   eval      vvenl2_vare = vvvare
6.11 c                   eval      vvenl2_saen = 1
6.11 c     vvenl2_key    setll     vvenl2
6.11 c     vvenl2_key2   reade     vvenl2
6.11 c                   dow       %eof = *off
6.11  *
6.11 c                   eval      vvepl1_pvar = vevare
6.11 c                   eval      vvepl1_penh = veenhe
6.11 c                   eval      vvepl1_pldo = vvldor
6.11 c     vvepl1_key    chain     vvepl1
6.11 c                   if        %found(vvepl1) and
6.11 c                             vepgti <> 0
6.11 c                   eval      eann = vepgti
6.11 c                   eval      vare = vevare
6.11 c                   eval      enhe = *blank
     c                   time                    time
6.11 c                   eval      edat = veedat
6.11 c                   eval      etim = veetim
6.11 c                   eval      eusr = veeusr
6.11  *
6.11 c                   write     boeastr
6.11  *
6.11 c                   endif
6.11  *
6.11 c     vvenl1_key    reade     vvenl1
6.11 c                   enddo
      *
      *
     c                   eval      veanl2_vare = vvvare
     c     veanl2_key    setll     veanl2
     c     veanl2_key    reade     veanl2
     c                   dow       %eof = *off
      *
     c                   eval      eann = vxeann
     c                   eval      vare = vxvare
     c                   eval      enhe = *blank
     c                   eval      time = vxtime
     c                   eval      edat = vxedat
     c                   eval      etim = vxetim
     c                   eval      eusr = vxeusr
      *
     c                   write     boeastr
      *
     c     veanl2_key    reade     veanl2
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameter
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_dato
     c                   parm                    p_time
6.12 c                   parm                    p_kass
      *
      * Key for oppslag/les på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
     c     vvarl1_key2   klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på vare-søketekst
      *
     c     vvsol1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvsol1_svar
      *
      * Key for les på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
      *
     c     vvenl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
     c                   kfld                    vvenl2_saen
      *
     c     vvenl2_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl2_vare
6.11  *
6.11  * Key for oppslag på vare enhet pr. leverandør
6.11  *
6.11 c     vvepl1_key    klist
6.11 c                   kfld                    w_firm
6.11 c                   kfld                    vvepl1_pvar
6.11 c                   kfld                    vvepl1_penh
6.11 c                   kfld                    vvepl1_pldo
      *
      * Key for les på vare-pris
      *
     c     vvprl1_key    klist
     c                   kfld                    w_firm
      *
     c     vvprlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprlr_vare
6.21 c                   kfld                    vvprlr_ldor
     c                   kfld                    vvprlr_prgr
     c                   kfld                    vvprlr_enhe
      *
      * Key for les på EAN
      *
     c     veanl1_key    klist
     c                   kfld                    w_firm
      *
     c     veanl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    veanl2_vare
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på produktansvarlig
      *
     c     ra26l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra26l1_zsak
      *
      * Key for oppslag på overføring vare
      *
     c     bovai1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    bovai1_vare
      *
      *
      *
     c     vmvalr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vmvalr_mkod
6.12  *
6.12  * Key for oppslag på kasse-register
6.12  *
6.12 c     bkasi1_key    klist
6.12 c                   kfld                    w_firm
6.12 c                   kfld                    bkasi1_kass
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
6.12 c                   eval      bkasi1_kass = p_kass
6.12 c     bkasi1_key    chain     bkasi1
6.12 c                   eval      w_lage = bklage
5.70  *
5.70  * henter prisgruppe
5.70  *
5.70 c                   call      'VL711R   '
5.70 c                   parm                    w_prgr
      *
     c                   endsr
