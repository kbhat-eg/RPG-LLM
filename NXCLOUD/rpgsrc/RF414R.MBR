     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RF414R                                              *
      * Beskrivelse . . : Resultatrapport, kopiering                          *
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       210104 KOB  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
     frrf0l1    if   e           k disk    rename(rrf0pfr:rrf0l1r)
     frrf0l2    if   e           k disk    rename(rrf0pfr:rrf0l2r)
     frrf5l1    if   e           k disk    rename(rrf5pfr:rrf5l1r)
     frrf8l1    if   e           k disk    rename(rrf8pfr:rrf8l1r)
     frrf0lu    uf a e           k disk    rename(rrf0pfr:rrf0lur)
     frrf5lu    uf a e           k disk    rename(rrf5pfr:rrf5lur)
     frrf8lu    uf a e           k disk    rename(rrf8pfr:rrf8lur)
      *
     frf414d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d afirl1_firm     s                   like(aaffir)
      *
     d rrf0l1_0fir     s                   like(rr0fir)
     d rrf0l1_0rap     s                   like(rr0rap)
      *
     d rrf0l2_0fir     s                   like(rr0fir)
     d rrf0l2_0txt     s                   like(rr0txt)
      *
     d rrf0lu_0fir     s                   like(rr0fir)
     d rrf0lu_0rap     s                   like(rr0rap)
      *
     d rrf5l1_5fir     s                   like(rr5fir)
     d rrf5l1_5rap     s                   like(rr5rap)
      *
     d rrf5lu_5fir     s                   like(rr5fir)
     d rrf5lu_5rap     s                   like(rr5rap)
     d rrf5lu_5lin     s                   like(rr5lin)
      *
     d rrf8l1_8fir     s                   like(rr0fir)
     d rrf8l1_8rap     s                   like(rr8rap)
     d rrf8lu_8fir     s                   like(rr8fir)
      *
     d rrf8lu_8rap     s                   like(rr8rap)
     d rrf8lu_8lin     s                   like(rr8lin)
     d rrf8lu_8lØp     s                   like(rr8lØp)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_ffir          s              3  0
     d w_tfir          s              3  0
     d w_0rap          s                   like(rr0rap)
     d w_in03          s              1  0
      *
     d s_0rap          s                   like(rr0rap)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(12)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Siden som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      rrf0l2_0txt = b10txt
     c     rrf0l2_key    setll     rrf0l2
     c                   other
     c                   eval      rrf0l1_0rap = b10rap
     c     rrf0l1_key    setll     rrf0l1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Rapport
      *
     c                   if        b20rap <> 0
     c                   eval      w_seqe = *blank
     c                   eval      rrf0l1_0rap = b20rap
     c     rrf0l1_key    setll     rrf0l1
     c                   goto      end_pos
     c                   endif
      *
      * Beskrivelse
      *
     c                   if        b20txt <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      rrf0l2_0txt = b20txt
     c     rrf0l2_key    setll     rrf0l2
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b20rap = 0
     c                   eval      b20txt = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      s_0rap = b10rap
     c                   exsr      kopier
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å kopiere
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kopier        begsr
      *
      * Kopier raporthode
      *
     c                   eval      rrf0l1_0fir = w_ffir
     c                   eval      rrf0l1_0rap = s_0rap
     c     rrf0l1_key    setll     rrf0l1r                                60
     c     rrf0l1_key    reade     rrf0l1r                                60
      *
     c                   dow       *in60 = *off                                 EOF
      *
     c                   eval      rrf0lu_0fir = w_tfir
     c                   eval      rrf0lu_0rap = s_0rap
     c     rrf0lu_key    chain     rrf0lur                            63
      *
     c                   if        *in63 = *on
     c                   eval      rr0fir = w_tfir
     c                   write     rrf0lur
     c                   endif
      *
     c     rrf0l1_key    reade     rrf0l1r                                60
      *
     c                   enddo
      *
      * Kopier raportlinjer
      *
     c                   eval      rrf5l1_5fir = w_ffir
     c                   eval      rrf5l1_5rap = s_0rap
     c     rrf5l1_key    setll     rrf5l1r                                61
     c     rrf5l1_key    reade     rrf5l1r                                61
      *
     c                   dow       *in61 = *off                                 EOF
      *
     c                   eval      rrf5lu_5fir = w_tfir
     c                   eval      rrf5lu_5rap = s_0rap
     c                   eval      rrf5lu_5lin = rr5lin
     c     rrf5lu_key    chain     rrf5lur                            63
      *
     c                   if        *in63 = *on
     c                   eval      rr5fir = w_tfir
     c                   write     rrf5lur
     c                   endif
      *
     c     rrf5l1_key    reade     rrf5l1r                                61
      *
     c                   enddo
      *
      * Kopier fra-til begrep
      *
     c                   eval      rrf8l1_8fir = w_ffir
     c                   eval      rrf8l1_8rap = s_0rap
     c     rrf8l1_key    setll     rrf8l1r                                62
     c     rrf8l1_key    reade     rrf8l1r                                62
      *
     c                   dow       *in62 = *off                                 EOF
      *
     c                   eval      rrf8lu_8fir = w_tfir
     c                   eval      rrf8lu_8rap = s_0rap
     c                   eval      rrf8lu_8lin = rr8lin
     c                   eval      rrf8lu_8lØp = rr8lØp
     c     rrf8lu_key    chain     rrf8lur                            63
      *
     c                   if        *in63 = *on
     c                   eval      rr8fir = w_tfir
     c                   write     rrf8lur
     c                   endif
      *
     c     rrf8l1_key    reade     rrf8l1r                                62
      *
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      rrf0l2
     c                   other
     c                   read      rrf0l1
     c                   endsl
      *
     c                   if        %eof or
     c                             rr0fir <> w_ffir
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      rrf0l2_0txt = rr0txt
     c                   other
     c                   eval      rrf0l1_0rap = rr0rap
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b10rap = rr0rap
     c                   eval      b10txt = rr0txt
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   select
     c                   when      w_seqe = 'B' and
     c                             %eof(rrf0l2)
     c     rrf0l2_key    setgt     rrf0l2
     c                   readp     rrf0l2
     c                   when      w_seqe = *blank and
     c                             %eof(rrf0l1)
     c     rrf0l1_key    setgt     rrf0l1
     c                   readp     rrf0l1
     c                   endsl
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     rrf0l2
     c                   other
     c                   readp     rrf0l1
     c                   endsl
      *
     c                   if        %eof or
     c                             rr0fir <> w_ffir
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      rrf0l2_0txt = rr0txt
     c                   other
     c                   eval      rrf0l1_0rap = rr0rap
     c                   endsl
      *
     c                   enddo
      *
     c                   select
     c                   when      w_seqe = 'B' and
     c                             %eof(rrf0l2)
     c     rrf0l2_key    setll     rrf0l2
     c                   when      w_seqe = *blank and
     c                             %eof(rrf0l1)
     c     rrf0l1_key    setll     rrf0l1
     c                   endsl
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    w_in03
     c                   parm                    w_tfir
      *
      * Key for FIRMA
      *
     c     afirl1_key    klist
     c                   kfld                    afirl1_firm
      *
      * Key for posisjonering på rapport
      *
     c     rrf0l1_key    klist
     c                   kfld                    rrf0l1_0fir
     c                   kfld                    rrf0l1_0rap
      *
      * Key for posisjonering på beskrivelse
      *
     c     rrf0l2_key    klist
     c                   kfld                    rrf0l2_0fir
     c                   kfld                    rrf0l2_0txt
      *
      * Key for oppdatering av rapport
      *
     c     rrf0lu_key    klist
     c                   kfld                    rrf0lu_0fir
     c                   kfld                    rrf0lu_0rap
      *
      * Key for les på rapportlinje
      *
     c     rrf5l1_key    klist
     c                   kfld                    rrf5l1_5fir
     c                   kfld                    rrf5l1_5rap
      *
      * Key for oppdatering av rapportlinje
      *
     c     rrf5lu_key    klist
     c                   kfld                    rrf5lu_5fir
     c                   kfld                    rrf5lu_5rap
     c                   kfld                    rrf5lu_5lin
      *
      * Key for les på fra-til begrep
      *
     c     rrf8l1_key    klist
     c                   kfld                    rrf8l1_8fir
     c                   kfld                    rrf8l1_8rap
      *
      * Key for oppdatering av fra-til begrep
      *
     c     rrf8lu_key    klist
     c                   kfld                    rrf8lu_8fir
     c                   kfld                    rrf8lu_8rap
     c                   kfld                    rrf8lu_8lin
     c                   kfld                    rrf8lu_8lØp
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_ffir = l_firm
     c                   move      l_firm        a1ffir
     c                   move      l_fnav        a1fnav
      *
     c                   eval      *in22 = *on
      *
      * Henter til firma
      *
     c                   eval      afirl1_firm = w_tfir
     c     afirl1_key    chain     afirl1
      *
     c                   eval      a1tfir = w_tfir
     c                   if        %found
     c                   move      aafnav        a1tnav
     c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rrf0l2_0fir = w_ffir
      *
     c                   eval      rrf0l1_0fir = w_ffir
     c                   eval      rrf0l1_0rap = 0
     c     rrf0l1_key    setll     rrf0l1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
