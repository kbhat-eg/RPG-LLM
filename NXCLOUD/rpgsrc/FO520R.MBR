     H/TITLE  ASOFAK: Vise vare-opplysninger
     H DATEDIT(*DMY)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO520R
      * Beskrivelse . . : Vise vare-opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
5.31  *       240205 AMD  Fjernet test på siste dato fra lagerakk.reg.
5.31  *                   (hva nå den har vært benyttet til ?)
7.xx  * 7.00  260116 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.00  * 7.00  060320 bkv  Takler 132 bilder
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     FFO520D    CF   E             WORKSTN
      *
     FVVARL1    IF   E           K DISK
     F                                     RENAME(VVARPFR:VVARL1R)
      *
      *
     D                UDS
     D  LDUSER               911    920
     D  DSSFIR               944    946  0
     D  l_fnav               951    980
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandling av varenummer                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *  Forklaring til statuskoder
      *
      *       WRETUR  Benyttes ved retur fra ett annet program
      *       WDIREK  Benyttes ved direkteoppkall med varenummer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  B1 Dersom varenummer er med i parameterliste                   *
      *     hoppes bilde B1 over.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     b1var0        ifne      *zero
     c                   movel     b1var0        b1var1
     c                   eval      wdirek = '1'
     c                   goto      b1tag0
     c                   endif
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  B1 Behandling av vareregister                                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     B1TAG         TAG
      *
      *  Nullstiller/Blanker felter som skal benyttes
      *
     C     B1TAGA        TAG
      *
     C                   MOVE      *OFF          *IN41
     C                   MOVE      *BLANK        B1VAR1
      *
     C                   MOVE      *BLANK        B2TEK1
      *
      *  Send Alt
      *
     C     B1TAGB        TAG
      ***                  MOVE *ZERO     B2VALG
     C                   MOVE      *BLANK        WRETUR
      *
     C                   EXFMT     B1BLD
     C*                  MOVEL     B1VAR1        B1VAR0
      *
      *  F3=Avslutt program
      *
     C     *INKC         IFEQ      *ON
     C                   GOTO      XSLUTT
     C                   END
      *
      *  F4=Spørring i vare-register
      *
     C     *INKD         IFEQ      *ON
     C                   CALL      'VV500R'
     C                   PARM                    VARFIR
     C                   PARM                    VARNUM
     C                   PARM                    WPTEK1
     C                   MOVEL     VARNUM        B1VAR0
     C                   END
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Sjekker opplysninger om varen                                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     B1TAG0        TAG
      *
      *  Sjekk, Finnes vare i VARE-REGISTER
      *
     C                   MOVEL     B1VAR0        VARNUM
     C     VARKEY        CHAIN     VVARL1R                            90
     C     *IN90         IFEQ      *ON
     C                   MOVE      *ON           *IN31
     C                   MOVE      *OFF          *IN41
     C                   GOTO      B1TAGB
     C                   END
      *
     C                   MOVE      *ON           *IN41
     C                   MOVE      VVTEK1        B2TEK1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  B2 Behandling av spørring                                      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     B2TAG         TAG
      *
      *  Nullstiller/Blanker felter som skal benyttes
      *
     C     B2TAGA        TAG
      *
      *  Send Alt
      *
     C     B2TAGB        TAG
      ***                  MOVE *ZERO     B2VALG
     C                   MOVE      *BLANK        WRETUR
      *
     C                   EXFMT     B2BLD
      *
      *  F3=Gå tilbake
      *
     C     *INKC         IFEQ      *ON
     c     wdirek        ifeq      '1'
     c                   goto      xslutt
     c                   else
     c                   move      *zero         b1var0
     c                   goto      b1tagb
     C                   endif
     C                   END
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Sjekker hvilket valg som er benyttet                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *  Kaller opp SUB-program for å vise    (ORDRE-LINJER)
      *
     C     B2VALG        IFEQ      01
     C                   MOVE      '0'           WRETUR
     C                   CALL      'VA556R'
     C                   PARM                    WRETUR
     C                   PARM                    VARFIR
     C                   PARM                    OTYNUM
      *
     C     WRETUR        IFEQ      *BLANK
     C                   CALL      'FO514R'
     C                   PARM                    WRETUR
     C                   PARM                    OTYNUM
     C                   PARM                    VARFIR
     C                   PARM                    VARNUM
     C                   PARM                    B2TEK1
     C                   GOTO      B2TAGB
     C                   ENDIF
     C                   ENDIF
      *
      *  Kaller opp SUB-program for å vise    (BESTILLINGS-LINJER)
      *
     C     B2VALG        IFEQ      02
5.31 C***> WPDATO        IFNE      *loval
     C                   MOVE      '1'           WRETUR
     C                   CALL      'VA556R'
     C                   PARM                    WRETUR
     C                   PARM                    VARFIR
     C                   PARM                    OTYNUM
      *
     C     WRETUR        IFEQ      *BLANK
     C                   CALL      'LO520R'
     C                   PARM                    WRETUR
     C                   PARM                    OTYNUM
     C                   PARM                    VARFIR
     C                   PARM                    VARNUM
     C                   PARM                    B2TEK1
     C                   GOTO      B2TAGB
     C                   ENDIF
5.31 C***>               ENDIF
     C                   ENDIF
      *
      *  Kaller opp SUB-program for å vise   LAGERBEHOLDNING
      *
      *
     C     B2VALG        IFEQ      03
     C                   CALL      'LL502R'
     C                   PARM                    VARFIR
     C                   PARM                    VARNUM
     C                   GOTO      B2TAGB
     C                   ENDIF
      *
      *  Gå tilbake til start
      *
     C                   MOVE      *OFF          *IN41
     C                   GOTO      B2TAGB
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSLUTT        TAG
      *
     C                   SETON                                        LR
     C                   RETURN
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *INZSR        BEGSR
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     C                   MOVE      *BLANK        WRETUR            1
     C                   MOVE      *BLANK        WDIREK            1
      *
     C                   MOVE      *ZERO         SUBDAT            8 0
     C                   MOVE      *ZERO         SUBDMY            6 0
     C                   MOVE      *ZERO         SUBDAM            4 0
     C                   MOVE      *ZERO         SUBDAG            2 0
     C                   MOVE      *ZERO         SUBMND            2 0
     C                   MOVE      *ZERO         SUBAAR            2 0
     C                   MOVE      *ZERO         SUBARB            2 0
      *
     C                   MOVE      *ZERO         LAGNUM            2 0
     C                   MOVE      *BLANK        OTYNUM            2
      *
      *  Redefinering av felter
      *
     C     *LIKE         DEFINE    VVTEK1        WPTEK1
     C     *LIKE         DEFINE    VVODAT        WPDATO
      *
      *  Redefinering av nøkler
      *
     C     *LIKE         DEFINE    VVFIRM        VARFIR
     C     *LIKE         DEFINE    VVVARE        VARNUM
      *
      *  Key for file : KUNDE-REGISTER
      *
     C     VARKEY        KLIST
     C                   KFLD                    VARFIR
     C                   KFLD                    VARNUM
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    WPEROR            1
     C                   PARM                    WPFIRM            3 0
     C                   PARM                    WPVARE           15
      *
      *  Legger inn firmanummer i nøkklene
      *
     C                   MOVE      DSSFIR        VARFIR
      *
      *  Legger inn varenummer dersom utfylt
      *
     C                   MOVEL     WPVARE        B1VAR0
      *
      *  Legger inn valg 3 som standard
      *
     C                   Z-ADD     3             B2VALG
      *
      *  Henter dato for siste oppdatering av Lagerinformsjons-dato-register
      *  I CL-programmet FLSTSC settes kode 'F' i WPEROR dersom program
      *  i ASLAGR ikke ble funnet, d.v.s ASLAGR var ikke i biblioteks-
      *  lista.
     C                   CALL      'FLSTSC'
     C                   PARM                    WPEROR
     C                   PARM                    WPDATO
      *
     C                   ENDSR
