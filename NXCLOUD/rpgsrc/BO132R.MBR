     h datedit(*dmy)
     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSHOP
      * Program . . . . : BO132R
      * Beskrivelse . . : Varsler om feil under dagsoppgjør i batch
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * R8.00 090924 bsa  Nytt program
8.01  * R8.00 231024 bsa  Endret ordlyd i mail
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Definering av datastrukturer
      *
      *
      * Definering av local data area
      *
     d lda            uds
     d l_prtf                800    809
     d l_chgv                844    844
     d l_user                911    920
     d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variable i nøkler
      *
     d w_firm          s                   like(l_firm)
      *
      * Definering av variable
      *
     d w_info_mail     s            100
     d w_send          s             50
      *
     d p_rec1          s             50    inz(' ')
     d p_rec2          s             50    inz(' ')
     d p_rec3          s             50    inz(' ')
     d p_send          s             50    inz(' ')
     d p_subj          s            100    inz(' ')
     d p_txt1          s            100    inz(' ')
     d p_txt2          s            100    inz(' ')
     d p_txt3          s            100    inz(' ')
     d p_txt4          s            100    inz(' ')
     d p_avsl          s            100    inz(' ')
     d p_wkod          s              2  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekk kode og send mail
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   if        %trim(w_info_mail) <> *blanks and
     c                             %trim(w_send) <> *blanks
      *
      * Forutsetter at vi har verdier i både mottaker og avsender
      *
     c                   eval      p_subj = 'Automatisk kjøring av dagsoppgjør'
     c                   eval      p_txt1 = *blank
     c                   eval      p_txt2 = *blank
     c                   eval      p_txt3 = *blank
     c                   eval      p_txt4 = *blank
     c                   eval      p_rec1 = %trim(w_info_mail)
     c                   eval      p_rec2 = *blank
     c                   eval      p_rec3 = *blank
     c                   eval      p_send = w_send
     c                   eval      p_avsl = 'Sendt fra automatisk oppdaterings-
     c                                      rutine.'
      * Parameter 1 - jobb ligger igjen
     c                   if        p_wkod = 1
     c                   eval      p_txt1 = 'Kan ikke kjøres da forrige dagsopp+
     c                                      gjør ser ut til å ha stoppet'
     c                   endif
      * Parameter 2 - ingen poster til oppdatering
     c                   if        p_wkod = 2
     c                   eval      p_txt1 = 'Ingen poster til oppdatering i reg+
     c                                      nskap'
     c                   endif
      * Parameter 3 - økonomioppdatering, rutinen er i bruk
     c                   if        p_wkod = 3
     c                   eval      p_txt1 = 'Økonomioppdatering, rutinen er bru+
     c                                      k. Dagsoppgjør avsluttes'
     c                   endif
      * Parameter 4 - Feil i tansaksjoner
     c                   if        p_wkod = 4
8.01 c                   eval      p_txt1 = 'Dagsoppgjør er kjørt. Feil i trans+
8.01 c                                      aksjoner. Sjekk feilliste. Økonomio+
8.01 c                                      ppdatering avsluttes'
     c                   endif
      * Parameter 5 - Ingen poster
     c                   if        p_wkod = 5
     c                   eval      p_txt1 = 'Økonomioppdatering, ingen transer +
     c                                      til oppdatering. Dagsoppgjør avslut+
     c                                      tes'
     c                   endif
      * Parameter 9 - tilgangsprobleer
     c                   if        p_wkod = 9
     c                   eval      p_txt1 = 'Ser ikke ut til at du har tilgang +
     c                                      eller nødvendige parametre er ikke +
     c                                      satt opp i skjerm/kasse/bruker.'
     c                   endif
      * Parameter 99 - Jpobben er kjørt, regnskap er oppdatert
     c                   if        p_wkod = 99
     c                   eval      p_txt1 = 'Dagsoppgjør er kjørt, og postering+
     c                                      er er oppdatert i Nexstep'
     c                   endif
      *
      * Kall program som lager xml og sender mail via proa
      *
     c                   call      'AF726R'
     c                   parm                    p_rec1
     c                   parm                    p_rec2
     c                   parm                    p_rec3
     c                   parm                    p_send
     c                   parm                    p_subj
     c                   parm                    p_txt1
     c                   parm                    p_txt2
     c                   parm                    p_txt3
     c                   parm                    p_txt4
     c                   parm                    p_avsl
      *
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_wkod
      *
      * Definisjon av local data area for output
      *
     c     *dtaara       define    *lda          lda
      *
      /Free
      *
       exec sql
        select afudss
        into   :w_info_mail
        from   afpspf
        where  afufil = :l_filg and
        afuide = 'DAGSO_INFO';

       exec sql
        select afudss
        into   :w_send
        from   afpspf
        where  afufil = :l_filg and
        afuide = 'NOMAIL';

      /End-Free
      *
     c                   endsr
