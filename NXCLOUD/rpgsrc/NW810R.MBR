     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : NW810R
      * Beskrivelse . . : WAP, API - hente kundeprosjekt
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       291003 HKO  Nytt program
      *       180304 HKO  Tilpasset nye feltlenger
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fapospf    if   e           k disk    rename(apospfr:apospfr)
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_avde          s              4  0
     d p_uniq          s             15
     d p_dtqi          s             10
     d p_dtqo          s             10
     d p_bibl          s             10
     d p_feil          s              1
     d p_inpl          s              5p 0
     d p_outl          s              5p 0
     d p_wait          s              5p 0
     d p_keyo          s              2
     d p_keyl          s              3p 0
     d p_sndl          s              3p 0
     d p_sndi          s             36
      *
      * Definering av datastrukturer
      *
      * Datakø - inn
      *
     d                 ds
     d d_inpu                         9
     d  d_kund                        9  0 overlay(d_inpu:1)
      *
      * Datakø - ut
      *
     d                 ds
     d d_outp                       152
     d  d_kpro                       12    overlay(d_outp: 1)
     d  d_navn                       35    overlay(d_outp:13)
     d  d_adr1                       35    overlay(d_outp:48)
     d  d_adr2                       35    overlay(d_outp:83)
     d  d_padr                       35    overlay(d_outp:118)
      *
      * Definering av variabler i nøkler
      *
     d fkprl1_kund     s                   like(flkund)
     d apospf_ponr     s                   like(apponr)
      *
      * Definering av variable
      *
     d w_udat          s               d   datfmt(*iso)
      *
     d w_firm          s                   like(flfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser og behandler datakø (input)
      *
     c                   dou       d_inpu = *blank
      *
     c                   eval      d_inpu = *blank
      *
     c                   call      'QRCVDTAQ'
     c                   parm                    p_dtqi
     c                   parm                    p_bibl
     c                   parm                    p_inpl
     c                   parm                    d_inpu
     c                   parm                    p_wait
     c                   parm                    p_keyo
     c                   parm                    p_keyl
     c                   parm                    p_uniq
     c                   parm                    p_sndl
     c                   parm                    p_sndi
      *
     c                   if        d_inpu <> *blank
     c                   exsr      behandling
     c                   endif
      *
     c                   enddo
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av forespørsel
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   if        d_kund = 0
     c                   eval      p_feil = *on
     c                   leavesr
     c                   endif
      *
      * Initierer output datakø
      *
     c                   eval      d_outp = *blank
      *
      * Leser kundeprosjekt og returnerer aktive
      *
     c                   eval      fkprl1_kund = d_kund
     c     fkprl1_key    setll     fkprl1
     c     fkprl1_key    reade     fkprl1
     c                   dow       not %eof
      *
     c                   if        fltdat <> *loval and
     c                             fltdat < w_udat
     c                   goto      les_neste
     c                   endif
      *
     c                   movel     flkpro        d_kpro
     c***                eval      d_kpro = flkpro
     c                   eval      d_navn = flnavn
     c                   eval      d_adr1 = fladr1
     c                   eval      d_adr2 = fladr2
     c                   eval      d_padr = *blank
      *
     c                   if        flponr <> 0
     c                   eval      apospf_ponr = flponr
     c     apospf_key    chain     apospf
     c                   if        %found
     c                   eval      d_padr = %editc(flponr:'4') + ' ' +
     c                                      %trim(apsted)
     c                   endif
     c                   endif
      *
      * Skriver til output-datakø
      *
     c                   call      'QSNDDTAQ'
     c                   parm                    p_dtqo
     c                   parm                    p_bibl
     c                   parm                    p_outl
     c                   parm                    d_outp
     c                   parm                    p_keyl
     c                   parm                    p_uniq
      *
     c     les_neste     tag
      *
     c     fkprl1_key    reade     fkprl1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      p_feil = *on
     c                   goto      avslutt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_avde
     c                   parm                    p_uniq
     c                   parm                    p_dtqi
     c                   parm                    p_dtqo
     c                   parm                    p_bibl
     c                   parm                    p_feil
     c                   parm                    p_inpl
     c                   parm                    p_outl
     c                   parm                    p_wait
     c                   parm                    p_keyo
     c                   parm                    p_keyl
     c                   parm                    p_sndl
     c                   parm                    p_sndi
      *
      * Key for les på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
      *
      * Key for oppslag på poststed
      *
     c     apospf_key    klist
     c                   kfld                    apospf_ponr
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
      * Henter dagens dato
      *
     c                   time                    w_udat
      *
     c                   endsr
