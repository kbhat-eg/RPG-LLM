     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . :
      * Beskrivelse . . : Konvertering av NOBB deltakernr-knytning til resk.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       010220 HKO  Nytt program
7.01  *K00    150620 bof  Sjekker om lev. finnes før man tar update
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjlevl3    if   e           k disk    rename(jlevpfr:jlevl3r)
     frlevlu    uf   e           k disk    rename(rlevpfr:rlevlur)
      *
      * Definering av variable i nøkler
      *
     d jlevl3_enum     s                   like(jlenum)
     d rlevlu_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser knytninger i VA, og konverterer disse til leverandørreskontro
      *
     c                   eval      jlevl3_enum = *hival
     c     jlevl3_key    setgt     jlevl3
     c                   readp     jlevl3
     c                   dow       not %eof
      *
     c                   if        jlenum <> 0
      *
     c                   eval      rlevlu_levr = jlenum
     c     rlevlu_key    chain     rlevlur
7.01 c                   if        %found(rlevlu)
     c                   if        rlnobb = 0
     c                   eval      rlnobb = jlldor
     c                   update    rlevlur
     c                   else
     c                   unlock    rlevlu
     c                   endif
7.01 c                   endif
      *
     c                   endif
      *
     c                   readp     jlevl3
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for les på leverandør (VA)
      *
     c     jlevl3_key    klist
     c                   kfld                    jlevl3_enum
      *
      * Key for oppdatering av reskontro-leverandør
      *
     c     rlevlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevlu_levr
      *
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
