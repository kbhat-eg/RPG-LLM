     H/TITLE  ASSRVL Spørrevindu m/vedlikehold
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * System  . . . . : ASOKON                                        *
      * Program . . . . : RP604R                                        *
      * Beskrivelse . . : Spørrevindu m/vedlikehold mot rprrPF          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      *                   Nytt program April-2013                       *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KC = F3  = Exit                                           *
      *       KE = F5  = Forny                                          *
      *       KL = F12 = Annuller                                       *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       10 = Roll                                                 *
      *       12 = Sfldsp                                               *
      *       13 = Sfldspctl                                            *
      *       14 = Sflclr                                               *
      *       15 = Sflend                                               *
      *       16 = Readc 16 on, no more records                         *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     Frprrl1    IF   E           K DISK
     F                                     RENAME(rprrpfr:rprrl1r)
     Frprrl2    IF   E           K DISK
     F                                     RENAME(rprrpfr:rprrl2r)
     Frprrlu    UF A E           K DISK
     F                                     RENAME(rprrpfr:rprrlur)
     Frp604d    CF   E             WORKSTN
     F                                     SFILE(B1WSF:SRRN01)
      *
     D MLD             S             11    DIM(4) CTDATA PERRCD(1)
      *
     d rprrl1_fir      s                   like(rphfir)
     d rprrl1_rap      s                   like(rphrap)
     d rprrl2_fir      s                   like(rphfir)
     d rprrl2_txt      s                   like(rphtxt)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d pafirm          s              3
     d wwrap           s              4
ad   d w_tilgko        s             10
ad   d w_rutine        s             10
ad   d w_adv           s              3
ad   d w_status        s             10
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av felter                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      *     nn - er 01 for subfile 01,
      *             02 for subfile 02,
      *             03 for subfile 03, o.s.v.
      * SRRNnn - Relativt recordnummer i subfile
      * SPGEnn - Sidestørrelse i subfile (page)
      * SFRNnn - Første recordnummer på siden
      * SSRNnn - Siste  recordnummer på siden
      * SVRNnn - Vis den side som inneholder denne record.
      *
      * Forklaring på spesielle felter
      * som benyttes ved windows.
      * - - - - - - - - - - - - - - - - -
      *
      * WACTLI - Actual line, angir startlinje for vindu.
      * WACTPO - Actual posision, angir startposisjon for vindu.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * B1WFL  - B1 Subfile - vindu for valg av post
      * B2WCT  - B2 Subfile - vindu control record
      * B2CMD  - B2 Vindu for CMD-taster
      * C2WIN  - C2 Vindu for endre vise opprette
      * D1WIN  - D1 Vindu for slette (Delete)
      * ASUME  -    Record for å styre at vindu legges over
      *             eksisterende data.
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * B2WCT Behandle åpningsbildet med subfile                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Send Alt
      *
     C     B2TAGA        TAG
     C                   Z-ADD     4             WACTLI
     C                   Z-ADD     26            WACTPO
     C                   WRITE     B2CMD
      *
      * Bare data
      *
     C     B2TAGB        TAG
     C                   EXSR      XDSP01
      *
      * Avslutt program
      *
     C     *INKC         CABEQ     *ON           XSLUTT
      *
      * Forny
      *
     C     *INKE         IFEQ      *ON
     C     SFRN01        IFNE      *ZERO
     C     SFRN01        CHAIN     B1WSF                              60
     C                   END
     C                   MOVE      b1rap         wwrap
     C                   MOVEL     b1rapx        wwrapx
      *                                                                 .
     C     WSEQEN        IFEQ      ' '
     C     rprrl1_key    SETLL     rprrl1
     C                   ELSE
     C     rprrl2_key    SETLL     rprrl2
     C                   END
      *                                                                 .
     C                   EXSR      XCLR01
     C                   EXSR      XCRT01
     C                   GOTO      B2TAGB
     C                   ENDIF
      *
      * Ny/endre rapport
      *
     c                   if        *inkf = *on
      *  Sjekk om tilgang
ad   c                   eval      w_tilgko = l_user
ad   c                   eval      w_rutine = 'DEFPRRAPP'
ad   c                   eval      w_status = *blank
ad   c                   call      'AD005R'
ad   c                   parm                    w_tilgko
ad   c                   parm                    w_rutine
ad   c                   parm                    w_adv
ad   c                   parm                    w_status
ad   c                   if        w_status = 'Sperret'
ad   c                   goto      b2tagb
ad   c                   endif
     c                   call      'RP200R'
     c                   if        sfrn01 <> *zero
     c     sfrn01        chain     b1wsf
     c                   endif
      *
     c                   if        wseqen = *blank
     c     rprrl1_key    setll     rprrl1
     c                   else
     c     rprrl2_key    setll     rprrl2
     c                   endif
      *
     c                   exsr      xclr01
     c                   exsr      xcrt01
     c                   goto      b2taga
     c                   endif
      *
      * Roll
      *
     C     *IN10         IFEQ      *ON
     C                   EXSR      XCRT01
     C                   GOTO      B2TAGB
     C                   ENDIF
      *
      * Posisjoner
      * startverdi
      * på konto
      *
     C     b2rap         IFNE      *ZERO
     C                   MOVE      ' '           WSEQEN
     C                   MOVE      b2rap         rprrl1_rap
     C     rprrl1_key    SETLL     rprrl1
     C                   EXSR      XCLR01
     C                   EXSR      XCRT01
     C                   MOVE      *ZERO         b2rap
     C                   GOTO      B2TAGB
     C                   ENDIF
      *
      * Posisjoner
      * startverdi
      * på alfafelt
      *
     C     b2rapx        IFNE      *BLANK
     C                   MOVE      'A'           WSEQEN
     C                   MOVE      *ZERO         wwrap
     C                   MOVEL     b2rapx        rprrl2_txt
     C     rprrl2_key    SETLL     rprrl2
     C                   EXSR      XCLR01
     C                   EXSR      XCRT01
     C                   MOVE      *ZERO         b2rap
     C                   MOVE      *BLANK        b2rapx
     C                   GOTO      B2TAGB
     C                   ENDIF
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Behandling av subfile                                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Sjekk om valg
      * er foretatt i
      * subfile
      *
     C                   DO        SSRN01
     C                   READC     B1WSF                                  16
     C     *IN16         IFEQ      *ON
     C                   LEAVE
     C                   ENDIF
      *                                                                 .
      * Hente post                                                      .
      *                                                                 .
     C     B1VALG        IFEQ      '1'
     C                   MOVE      b1rap         wwrap
     C                   MOVEl     b1rapx        parapx
     C                   GOTO      XSLUTT
     C                   ENDIF
      *                                                                 .
      * Vise post                                                       .
      *                                                                 .
     C                   ENDDO
      *
     C                   GOTO      B2TAGA
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XSLUTT        TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Tøm subfile                                                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XCLR01        BEGSR
     C                   MOVE      *ON           *IN14
     C                   WRITE     B2WCT
     C                   MOVE      *OFF          *IN14
      *
     C                   MOVE      *OFF          *IN15
     C                   Z-ADD     0             SRRN01
     C                   Z-ADD     0             SFRN01
     C                   Z-ADD     0             SSRN01
     C                   Z-ADD     0             SPGE01
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Fyll opp subfile                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -  - -*
      *
     C     XCRT01        BEGSR
     C                   move      *off          *in15
     C                   ADD       7             SPGE01
     C                   Z-ADD     SSRN01        SRRN01
      *
     C     SRRN01        DOUEQ     SPGE01
      *                                                                 .
     C     WSEQEN        IFEQ      ' '
     C                   READ      rprrl1                                 15
     C                   ELSE
     C                   READ      rprrl2                                 15
     C                   END
      *
     C                   if        *in15 = *off and
     C                             rphfir <> wwfirm
     C                   move      *on           *in15
     C                   endif
      *
     C     *IN15         CABEQ     '1'           XEND01
     c                   if        rphlin = *zero
     C                   ADD       1             SRRN01
     C                   MOVE      rphrap        b1rap
     C                   MOVEL     rphtxt        b1rapx
     C                   WRITE     B1WSF
     c                   endif
     C                   ENDDO
      *
     C     XEND01        TAG
      *
     C     SRRN01        IFGT      SSRN01
     C     SSRN01        ADD       1             SFRN01
     C                   ENDIF
      *
     C                   Z-ADD     SRRN01        SSRN01
     C                   Z-ADD     SFRN01        SVRN01
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Display subfile                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XDSP01        BEGSR
      *
     C     SRRN01        IFNE      0
     C                   MOVE      *ON           *IN12
     C                   ENDIF
      *
     C                   MOVE      *ON           *IN13
     C                   EXFMT     B2WCT
     C                   MOVE      *OFF          *IN12
     C                   MOVE      *OFF          *IN13
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     *INZSR        BEGSR
      *
     C                   Z-ADD     0             SPGE01            3 0
     C                   Z-ADD     0             SRRN01            4 0
     C                   Z-ADD     0             WACTLI            2 0
     C                   Z-ADD     0             WACTPO            3 0
     C                   MOVE      *BLANK        WSEQEN            1
      *
     C     *LIKE         DEFINE    SRRN01        SFRN01
     C     *LIKE         DEFINE    SRRN01        SSRN01
     C     *LIKE         DEFINE    rphfir        WWFIRM
     C     *LIKE         DEFINE    rphtxt        wwrapx
     C     *LIKE         DEFINE    rphrap        parap
     C     *LIKE         DEFINE    rphtxt        parapx
      *
      * Parameterliste
      *
     C     *ENTRY        PLIST
     C                   PARM                    PAFIRM
     C                   PARM                    wwrap
     C                   PARM                    parapx
      *
     C                   MOVE      PAFIRM        WWFIRM
      *
      * Key for file nummerisk
      *
     C     rprrl1_key    KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    rprrl1_rap
      *
      * Key for file alfabetisk
      *
     C     rprrl2_key    KLIST
     C                   KFLD                    WWFIRM
     C                   KFLD                    rprrl2_txt
      *
     c                   eval      rprrl1_rap = *zero
     c                   eval      rprrl2_txt = *blank
     C     WSEQEN        IFEQ      'A'
     C     rprrl2_key    SETLL     rprrl2
     C                   ELSE
     C     rprrl1_key    SETLL     rprrl1
     C                   END
      *
     C                   EXSR      XCRT01
      *
     C                   ENDSR
      *
**
  Ny post                  001
  Endring                  002
  Vise                     003
* Slettet *                004
