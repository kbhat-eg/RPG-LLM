     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLOGI
      * Program . . . . : MM740R
      * Beskrivelse . . : Leser vareregister
      *                   Oppdaterer lager med lagerverdier
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       280105 bø   Nytt program
      *       050607 BØ   Lagt inn test om sperre for oppd. prognose
      *       210607 BØ   Innkjøpsparm.oppd. uten des. til lager untatt
      *                   spesielle enhet. Hardkodet nå - bør inn i reg.5.60
5.60  *       050408 bø   Lagerenhet fra vare-register
5.61  *       250309 bø   Lagt på endringsdato og tidspunkt
      * 6.10  260609 BSA  Oppdatert med sporingsinformasjon
6.20  * 6.20  070211 nho   Byttet ut fil mpspxx med vlagxx
6.21  * 6.20  310812 bof  Nytt felt, endringsdato for faste data som vises
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fmprwl1    if   e           k disk    rename(mpropfr:mprol1r)
6.20 fvlaglr    if   e           k disk    rename(vlagpfr:vlaglrr)
     fvlaglu    uf a e           k disk    rename(vlagpfr:vlaglur)
      *
      *
     d vlaglu_vare     s                   like(vlvare)
     d vlaglu_lage     s                   like(vllage)
     d mprwla_vare     s                   like(movare)
     d mprwl2_vsor     s                   like(movsor)
     d mprwl3_ogrp     s                   like(moogrp)
     d mprwl3_hgrp     s                   like(mohgrp)
     d mprwl3_ugrp     s                   like(mougrp)
     d mprwlx_ogrp     s                   like(moogrp)
     d mprwlx_hgrp     s                   like(mohgrp)
     d mprwly_ogrp     s                   like(moogrp)
     d mprwlz_vare     s                   like(vvvare)
6.20 d vlaglr_vare     s                   like(vlvare)
6.20 d vlaglr_lage     s                   like(vllage)
5.60 d vvarl1_vare     s                   like(vvvare)
      *
     d w_firm          s                   like(vvfirm)
     d p_pvsor         s                   like(movsor)
     d p_pogrp         s                   like(moogrp)
     d p_phgrp         s                   like(mohgrp)
     d p_pugrp         s                   like(mougrp)
     d p_pldor         s              6  0
     d p_pvare         s                   like(movare)
     d p_list          s            128
     d w_vsor          s                   like(movsor)
     d w_ogrp          s                   like(moogrp)
     d w_hgrp          s                   like(mohgrp)
     d w_ugrp          s                   like(mougrp)
     d w_ldor          s              6  0
     d w_vark          s                   like(movare)
     d w_udat          s               d   datfmt(*iso)
     d w_b90           s              9  0
     d w_b91           s              9  1
      *
      * Local Data Area
      *
     d                uds
      *
6.10 d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
      *
      * Definering av variable
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
readec     mprwl1_key    setll     mprwl1
readec     mprwl1_key    reade     mprwl1
readec                   dow       not %eof(mprwl1)
      *
      * sjekker om det finnes sperre på oppdatering
      *
6.20 c                   eval      vlaglr_vare = movare
6.20 c                   eval      vlaglr_lage = molage
6.20 c     vlaglr_key    chain     vlaglr
6.20 c                   if        %found(vlaglr)
6.20 c                   if        vlstda <> *loval and
6.20 c                             vlstda > w_udat
     c                   goto      neste
     c                   endif
5.61 c                   endif
      *
     c                   exsr      upd_lager
      *
     c     neste         tag
readec     mprwl1_key    reade     mprwl1
readec                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * --------------------------------------------------------------
      *  behandler lest vare / lager
      * --------------------------------------------------------------
      *
     c     upd_lager     begsr
      *
5.60 c                   eval      vvarl1_vare = movare
5.60 c     vvarl1_key    chain     vvarl1
      *
     c                   clear                   vlaglur
     c                   eval      vlaglu_vare = movare
     c                   eval      vlaglu_lage = molage
     c     vlaglu_key    chain     vlaglu
5.60 c                   if        vvenhl <> 'LM ' and
5.60 c                             vvenhl <> 'PAL' and
5.60 c                             vvenhl <> 'M  ' and
5.60 c                             vvenhl <> 'LTR' and
5.60 c                             vvenhl <> 'KG ' and
5.60 c                             vvenhl <> 'M2 ' and
5.60 c                             vvenhl <> 'M3 '
      * ingen desimal
     c                   eval(h)   w_b90  = mobpun
     c                   eval      vlbpun = w_b90
     c                   eval(h)   w_b90  = mobmen
     c                   eval      vlbmen = w_b90
     c                   eval(h)   w_b90  = momini
     c                   eval      vlmini = w_b90
     c                   else
      * 1 desimal
     c                   eval(h)   w_b91  = mobpun
     c                   eval      vlbpun = w_b91
     c                   eval(h)   w_b91  = mobmen
     c                   eval      vlbmen = w_b91
     c                   eval(h)   w_b91  = momini
     c                   eval      vlmini = w_b91
     c                   endif
6.10 c                   time                    vledat
6.10 c                   time                    vletim
6.10 c                   eval      vleusr = l_user
6.21 c                   time                    vlfeda
6.21 c                   time                    vlfeti
6.21 c                   eval      vlfeus = l_user
     c                   if        %found(vlaglu)
     c                   update    vlaglur
     c                   else
     c                   eval      vlfirm = mofirm
     c                   eval      vlvare = movare
     c                   eval      vllage = molage
6.10 c                   time                    vlodat
6.10 c                   time                    vlotim
6.10 c                   eval      vlousr = l_user
     c                   write     vlaglur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
5.60  *
5.60  *  Key for les av vare
5.60  *
5.60 c     vvarl1_key    klist
5.60 c                   kfld                    w_firm
5.60 c                   kfld                    vvarl1_vare
      *
      *  Key for les av innkjøpsparameter
      *
     c     mprwl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vsor
     c                   kfld                    w_ogrp
     c                   kfld                    w_hgrp
     c                   kfld                    w_ugrp
     c                   kfld                    w_ldor
     c                   kfld                    w_vark
      *
      * Key for les av lager
      *
     c     vlaglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglu_vare
     c                   kfld                    vlaglu_lage
      *
      *  Key for les av sperre for oppdatering fra prognose
      *
6.20 c     vlaglr_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    vlaglr_vare
6.20 c                   kfld                    vlaglr_lage
      *
     c     *entry        plist
     c                   parm                    p_pvsor
     c                   parm                    p_pogrp
     c                   parm                    p_phgrp
     c                   parm                    p_pugrp
     c                   parm                    p_pldor
     c                   parm                    p_pvare
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_vsor = p_pvsor
     c                   eval      w_ogrp = p_pogrp
     c                   eval      w_hgrp = p_phgrp
     c                   eval      w_ugrp = p_pugrp
     c                   eval      w_ldor = p_pldor
     c                   eval      w_vark = p_pvare
     c                   time                    w_udat
      *
     c                   endsr
