     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VV106R
      * Beskrivelse . . : Vare, vedlikehold - prisjustering etter basisenhet
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       050100 HKO  Nytt program
5.70  * PRGR  241008 bof  Utvidet med prisgruppe
6.20  *       010411 bof  Utvidet med prisgiver,default vvldor
7.xx  * 7.00  040216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.20 fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
      *
     fvv106d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vpfirm)
     d p_vare          s                   like(vpvare)
     d p_prgr          s                   like(vpprgr)
     d p_enhe          s                   like(vpenhe)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
     d b_inlr          s              1    inz(*off)
6.20 d b_ldor          s              1    inz(*off)
      *
     d w_udat          s               d   datfmt(*iso)
     d w_spro          s              7  4
     d w_kpro          s              7  4
     d w_ipro          s              7  4
     d w_bpro          s              7  4
     d w_dekn          s              5  2
      *
     d w_firm          s                   like(vpfirm)
     d w_lety          s                   like(vplety)
6.20 d w_vare          s                   like(vvvare)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.20 c     vvarl1_key    chain     vvarl1
      *
      * Initierer skjermbildet
      *
     c                   eval      w_lety = 0
     c                   eval      b_inlr = *off
     c                   time                    w_udat
6.20 c                   eval      b1ldor = vvldor
      *
     c                   call      'VP701R'
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_prgr
     c                   parm                    p_enhe
     c                   parm                    w_lety
     c                   parm                    w_udat
     c                   parm                    b_inlr
     c                   parm                    b1sagm
     c                   parm                    b1bugm
     c                   parm                    b1kogm
     c                   parm                    b1ingm
      *
     c                   eval      b1enhe = p_enhe
      *
     c                   eval      b1sapr = b1sagm
     c                   eval      b1kopr = b1kogm
     c                   eval      b1inpr = b1ingm
     c                   eval      b1bupr = b1bugm
      *
      * Behandler bildet
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   goto      avslutt
     c                   endif
      *
      * Sjekker input-felter
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
      * Finner prosentvise endring av pris, og kaller program for
      * justering av priser
      *
     c                   eval(h)   w_spro = (b1sapr - b1sagm) * 100 / b1sagm
      *
     c                   if        b1kopr <> b1kogm
     c                   eval(h)   w_kpro = (b1kopr - b1kogm) * 100 / b1kogm
     c                   endif
      *
     c                   if        b1inpr <> b1ingm
     c                   eval(h)   w_ipro = (b1inpr - b1ingm) * 100 / b1ingm
     c                   endif
      *
     c                   if        b1bupr <> b1bugm
     c                   eval(h)   w_bpro = (b1bupr - b1bugm) * 100 / b1bugm
     c                   endif
      *
     c                   eval      b_inlr = *off
      *
     c                   call      'VP800R'
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    w_udat
     c                   parm                    w_spro
     c                   parm                    w_kpro
     c                   parm                    w_ipro
     c                   parm                    w_bpro
     c                   parm                    w_dekn
     c                   parm                    b_inlr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
6.20  *
6.20  * leverandør
6.20  *
6.20 c                   eval      b_ldor = *off
6.20 c     vvprl1_key    setll     vvprl1
6.20 c     vvprl1_key    reade     vvprl1
6.20 c                   dow       not %eof(vvprl1)
6.20 c                   if        vpldor = b1ldor
6.20 c                   eval      b_ldor = *on
6.20 c                   endif
6.20 c     vvprl1_key    reade     vvprl1
6.20 c                   enddo
6.20 c                   if        b_ldor = *off
6.20 c                   eval      b_feil = *on
6.20 c                   eval      *in37 = *on
6.20 c                   goto      end_sjekk                                                 .
6.20 c                   endif
      *
      * Minst en av prisene må endres
      *
     c                   if        b1sapr = b1sagm and
     c                             b1kopr = b1kogm and
     c                             b1inpr = b1ingm and
     c                             b1bupr = b1bugm
     c                   eval      *in31 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Salgspris
      *
     c                   if        b1sapr = 0
     c                   eval      *in32 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   if        b1sagm = 0
     c                   eval      *in33 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Kostpris
      *
     c                   if        b1kopr = 0 and
     c                             b1kogm <> 0 or
     c                             b1kopr <> 0 and
     c                             b1kogm = 0
     c                   eval      *in34 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Innkjøpspris
      *
     c                   if        b1inpr = 0 and
     c                             b1ingm <> 0 or
     c                             b1inpr <> 0 and
     c                             b1ingm = 0
     c                   eval      *in35 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Butikkpris
      *
     c                   if        b1bupr = 0 and
     c                             b1bugm <> 0 or
     c                             b1bupr <> 0 and
     c                             b1bugm = 0
     c                   eval      *in36 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
6.20  *
6.20  * Key for oppslag på vare
6.20  *
6.20 c     vvarl1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    w_vare
6.20  *
6.20  * Key for oppslag på vare-pris
6.20  *
6.20 c     vvprl1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    w_vare
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_prgr
     c                   parm                    p_enhe
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
6.20 c                   eval      w_vare = p_vare
      *
      *
     c                   endsr
