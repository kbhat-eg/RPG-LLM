     H/TITLE  Utskrift av Ordre-status-liste
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : FO693R
      * Beskrivelse . . : Utskrift av Ordre-status-liste
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
6.20  *       060214 nho  Feil utlegg av ordreverdi
6.nu  *       230614 bof  Gjennomgått mtp. nummerutvidelse
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     Frkunl1    IF   E           K DISK    RENAME(RKUNPFR:rkunl1r)
     FFSTSL1    IF   E           K DISK    RENAME(FSTSPFR:FSTSL1R)
     FFOHEL1    IF   E           K DISK    RENAME(FOHEPFR:FOHEL1R)
      *
     FFO693P    O    E             PRINTER
      *
     D                 DS
      *                                                      Parameter-inn
     D  LDPREC                 1     64
     D  LDVAL1                 1      1  0
      *
      * Hente inn firma fra local
      *
     D                UDS
     D  DSFIRM               944    946  0
     D  l_fnav               951    980
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av formater                                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede formater
      * - - - - - - - - - -
      *
      * A1HDR  - Heading A1 på rapport
      * D1DET  - Detail  D1 på rapport
      * T1TOT  - Total   T1 på rapport
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A1HDR Behandle heading A1                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      * Skrive heading
      *
     C                   WRITE     A1HDR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * D1DET Behandle file                                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Lese file
      *
     C     HEDKEY        SETLL     FOHEL1
     C                   READ      FOHEL1                                 15
      *
     C     *IN15         DOWEQ     '0'
      *
      * Hent opplysninger fra KUNDE-REGISTER
      *
     C                   MOVE      FOKUND        KUNNUM
     C     KUNKEY        CHAIN     rkunl1r                            64
     C                   MOVE      RKNAVN        D1NAVN
     C   64              MOVE      *BLANK        D1NAVN
      *
      * Snu dato
      *
     c                   eval      d1bdat = 0
     c                   eval      d1ldat = 0
      *
     c                   if        fobdat <> *loval
     c     *dmy          move      fobdat        d1bdat
     c                   endif
      *
     c                   if        foldat <> *loval
     c     *dmy          move      foldat        d1ldat
     c                   endif
      *
6.20 c     fotots        sub       fototr        w_tots
6.20  *
      * Behandle record
      *
     C                   ADD       1             T1POST
     C                   WRITE     D1DET                                30
      *
      * Er det overflow ?
      *
     C     *IN30         IFEQ      *ON
     C                   WRITE     A1HDR
     C                   ENDIF
      *
     C                   READ      FOHEL1                                 15
     C                   ENDDO
      *
     C     XEND01        TAG
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * T1TOT Behandle total T1                                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      * Skrive total
      *
     C                   WRITE     T1TOT                                30
     C                   GOTO      XSLUTT
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSLUTT        TAG
      *
     C                   SETON                                        LR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     *INZSR        BEGSR
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
      *  Redefinering av felter
      *
      *  Redefinering av nøkler
      *
6.20 C     *LIKE         DEFINE    FOTOTS        W_TOTS
     C     *LIKE         DEFINE    FAAFIR        STSFIR
      *
     C     *LIKE         DEFINE    RKFIRM        KUNFIR
     C     *LIKE         DEFINE    RKKUND        KUNNUM
      *
     C     *LIKE         DEFINE    FOFIRM        HEDFIR
     C     *LIKE         DEFINE    FONUMM        HEDNUM
     C     *LIKE         DEFINE    FOSUFF        HEDSUF
      *
      * Key for kunde-register
      *
     C     KUNKEY        KLIST
     C                   KFLD                    KUNFIR
     C                   KFLD                    KUNNUM
      *
      * Key for statusregister
      *
     C     STSKEY        KLIST
     C                   KFLD                    STSFIR
      *
      * Key for ORDRE-HODE-REGISTER
      *
     C     HEDKEY        KLIST
     C                   KFLD                    HEDFIR
     C                   KFLD                    HEDNUM
     C                   KFLD                    HEDSUF
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    WWPARM           64
      *
     C                   MOVE      WWPARM        LDPREC
      *
     C                   MOVEL     l_fnav        A1FNAV
      * Legge inn key
      *
     C                   MOVE      DSFIRM        A1FIRM
     C                   MOVE      DSFIRM        KUNFIR
     C                   MOVE      DSFIRM        STSFIR
     C                   MOVE      DSFIRM        HEDFIR
      *
      * Hent opplysninger fra OFAK-STATUS-KODER
      *
     C     STSKEY        CHAIN     FSTSL1R                            69
      *
     C                   ENDSR
