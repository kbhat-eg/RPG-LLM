     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FX810R
      * Beskrivelse . . : Oppdatering av alfafeltet fra kunderegister
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       290403 AMD  Nytt program
      * 6.10  240609 BSA  Oppdatert med sporingsinformasjon
6.nu  * 6.30  240614 bof  Gjennomgått mtp. nummerutvidelse
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohelu    uf a e           k disk    rename(fohepfr:fohelur)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
      *
      * Definering av local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d fohelu_firm     s                   like(fofirm)
     d fohelu_numm     s                   like(fonumm)
     d fohelu_suff     s                   like(fosuff)
     d rkunl1_firm     s                   like(rkfirm)
     d rkunl1_kund     s                   like(rkkund)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Leser igjennom hele ordreregister
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     fohelu_key    setll     fohelu
     c                   read      fohelur
      *
     c                   dow       not %eof
      *
      * Oppdaterer ordrehode
      *
     c                   eval      rkunl1_firm = fofirm
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1r
     c                   if        %found
     c                   move      *blank        fokuna
     c                   movel     rkalfa        fokuna
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   update    fohelur
     c                   endif
      *
     c     les_hode      tag
     c                   read      fohelur
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppdatering av ORDRE-HODE
      *
     c     fohelu_key    klist
     c                   kfld                    fohelu_firm
     c                   kfld                    fohelu_numm
     c                   kfld                    fohelu_suff
      *
      * Key for lesing av kunderegister
      *
     c     rkunl1_key    klist
     c                   kfld                    rkunl1_firm
     c                   kfld                    rkunl1_kund
      *
      * Legger inn startverdier
      *
     c                   eval      fohelu_firm = 0
     c                   eval      fohelu_numm = 0
     c                   eval      fohelu_suff = 0
      *
     c                   endsr
