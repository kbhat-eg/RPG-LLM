     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASSHOP
      * Program . . . . : BU721R
      * Beskrivelse . . : Butikk, eksport av modul fritekst
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       220304 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjmtxl1    if   e           k disk    rename(jmtxpfr:jmtxl1r)
     fvvarlb    if   e           k disk    rename(vvarpfr:vvarlbr)
      *
     fbomfst    uf a e           k disk    rename(bomfst:bomfstr)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vvfirm)
      *
      * Definering av variable i nøkler
      *
     d jmtxl1_tmod     s                   like(jutmod)
     d vvarlb_nmod     s                   like(vvnmod)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vvfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer fil
      *
     c                   clear                   bomfstr
      *
     c                   eval      firm = w_firm
      *
      * Behandler ModulFritekst
      *
     c                   eval      jmtxl1_tmod = 0
     c     jmtxl1_key    setll     jmtxl1
     c                   read      jmtxl1
     c                   dow       %eof = *off
      *
     c                   eval      vvarlb_nmod = jutmod
     c     vvarlb_key    chain     vvarlb
     c                   if        %found
      *
     c                   eval      modn = jutmod
     c                   eval      line = jutlin
     c                   eval      text = juttxt
     c                   eval      eusr = *blank
      *
     c                   time                    odat
     c                   time                    edat
     c                   time                    etim
      *
     c                   write     bomfstr
      *
     c                   endif
      *
     c                   read      jmtxl1
     c                   enddo
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameter
      *
     c     *entry        plist
     c                   parm                    p_firm
      *
      * Key for les på ModulFritekst
      *
     c     jmtxl1_key    klist
     c                   kfld                    jmtxl1_tmod
      *
      * Key for oppslag på vare
      *
     c     vvarlb_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlb_nmod
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
