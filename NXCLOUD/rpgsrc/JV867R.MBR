     h option(*nodebugio) datedit(*dmy)

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JV867R
      * Beskrivelse . . : Oppdater fra NOBB, varegruppe
      *
      *                   Oppdaterer varegrupper NOBB (XML-format)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       040414 BKV
6.30  * R6.30 170316 BKV  Lagt inn UNSPSCnr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvogrlu    uf a e           k disk    rename(vogrpfr:vogrlur)
     f                                     infsr(FileErr)
     fvhgrlu    uf a e           k disk    rename(vhgrpfr:vhgrlur)
     f                                     infsr(FileErr)
     fvugrlu    uf a e           k disk    rename(vugrpfr:vugrlur)
     f                                     infsr(FileErr)
     fjstsl1    if   e           k disk    rename(jstspfr:jstsl1r)
     f                                     infsr(FileErr)
      * Brukes som parametre til jobblogging
     d p_jbid          s             41
     d p_jsts          s              2  0
     d p_jmld          s            200
      *
      * Definering av variable i nøkler
      *
     d vogrlu_oogr     s                   like(vgoogr)
     d vhgrlu_hogr     s                   like(vghogr)
     d vhgrlu_hhgr     s                   like(vghhgr)
     d vugrlu_uogr     s                   like(vguogr)
     d vugrlu_uhgr     s                   like(vguhgr)
     d vugrlu_uugr     s                   like(vguugr)
      *
      * Definering av Local Data Area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
     d w_firm          s              3  0
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_stat          s              1
     d p_orec          s            100
     d p_hrec          s            100
     d p_urec          s            100
     d p_job_log       s             50
     d p_inlr          s              1
      *
      *
      *  Dataelementer overgruppe
      *
     d                 ds
     d d_orec                       100
     d  d_ogrp                        2    overlay(d_orec:1)
     d   d_ogrp_num                   2  0 overlay(d_ogrp:1)
     d  d_obes                       35    overlay(d_orec:4)
     d  d_crea                       10    overlay(d_orec:45)
     d   d_crea_iso                    d   overlay(d_crea:1) datfmt(*iso)
     d  d_upda                       10    overlay(d_orec:55)
     d   d_upda_iso                    d   overlay(d_upda:1) datfmt(*iso)
      *
      *  Dataelementer hovedgruppe
      *
     d                 ds
     d d_hrec                       100
     d  d_hgrp                        2    overlay(d_hrec:1)
     d   d_hgrp_num                   2  0 overlay(d_hgrp:1)
     d  d_hbes                       35    overlay(d_hrec:4)
      *
      *  Dataelementer undergruppe
      *
     d                 ds
     d d_urec                       100
     d  d_ugrp                        3    overlay(d_urec:1)
     d   d_ugrp_num                   3  0 overlay(d_ugrp:1)
     d  d_ubes                       35    overlay(d_urec:5)
     d  d_ups1                        3    overlay(d_urec:40)
     d  d_ups2                        3    overlay(d_urec:43)
6.30 d  d_ufri                       10    overlay(d_urec:46)
      *
     d                 ds
     d d_job_log                     50
     d  w_ovgnt                       5    overlay(d_job_log:2)
     d  w_ovgn                        5  0 overlay(w_ovgnt:1)
     d  w_hvgnt                       5    overlay(d_job_log:7)
     d  w_hvgn                        5  0 overlay(w_hvgnt:1)
     d  w_uvgnt                       5    overlay(d_job_log:12)
     d  w_uvgn                        5  0 overlay(w_uvgnt:1)
     d  w_ovgut                       5    overlay(d_job_log:17)
     d  w_ovgu                        5  0 overlay(w_ovgut:1)
     d  w_hvgut                       5    overlay(d_job_log:22)
     d  w_hvgu                        5  0 overlay(w_hvgut:1)
     d  w_uvgut                       5    overlay(d_job_log:27)
     d  w_uvgu                        5  0 overlay(w_uvgut:1)
     d  w_ovglt                       5    overlay(d_job_log:32)
     d  w_ovgl                        5  0 overlay(w_ovglt:1)
     d  w_hvglt                       5    overlay(d_job_log:37)
     d  w_hvgl                        5  0 overlay(w_hvglt:1)
     d  w_uvglt                       5    overlay(d_job_log:42)
     d  w_uvgl                        5  0 overlay(w_uvglt:1)
      * Definering av variable
      *
     d b_inlr          s              1
     d b_vfel          s              1    inz(*off)
      *
      * Datastruktur for informasjon ved exceptions
      *
     d PgmError       sds
     d  proc_name        *proc
     d  pgm_status       *STATUS
     d  prv_status            16     20s 0
     d  line_numm             21     28
     d  routine          *ROUTINE
     d  parms            *PARMS
     d  excp_type             40     42
     d  excp_numm             43     46
     d  pgm_lib               81     90
     d  excp_data             91    170
     d  excp_id              171    174
     d  date                 191    198
     d  year                 199    200s 0
     d  last_file            201    208
     d  file_info            209    243
     d  job_name             244    253
     d  job_user             254    263
     d  job_numb             264    269s 0

      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

     c                   eval      d_job_log = p_job_log
     c                   if        p_inlr = *on
     c                   eval      d_orec = p_orec
     c                   eval      d_hrec = p_hrec
     c                   eval      d_urec = p_urec

     c                   if        d_ugrp_num <> *zero
     c                   exsr      skr_ugrp
     c                   elseif    d_hgrp_num <> *zero
     c                   exsr      skr_hgrp
     c                   else
     c                   exsr      skr_ogrp
     c                   endif
      *
     c                   endif

      * retunerer jobb status
     c                   eval      p_job_log = d_job_log

      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     avslutt       tag

     c                   if        p_inlr <> *on
     c                   eval      *inlr = *on
     c                   endif
     c                   return

      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Error rutine for file errors
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     FileErr       begsr
      *
     c                   eval      p_stat = '1'
     c                   eval      p_jsts = 50
     c                   eval      p_jmld = 'Fil ' + %trim(last_file) +
     c                             ' feil. Info=' + %trim(excp_data) +
     c                             '. Job=' + %trim(job_name) + '/' +
     c                             %trim(job_user) + '/' +
     c                             %char(job_numb)
      *
     c                   call      'AB705R'
     c                   parm                    p_jbid
     c                   parm                    p_jsts
     c                   parm                    p_jmld
      *
     c                   goto      avslutt
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer overgruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_ogrp      begsr
      *

      *
     c                   eval      vogrlu_oogr = d_ogrp_num
     c     vogrlu_key    chain     vogrlur
      *
     c                   if        %found
     c                   if        vgotxt <> d_obes
     c                   eval      vgotxt = d_obes
     c                   eval      vgoeus = l_user
     c                   time                    vgoeda
     c                   time                    vgoeti
     c                   eval      w_ovgu = w_ovgu + 1
     c                   update    vogrlur
     c                   endif
     c                   else
     c                   clear                   vogrlur
     c                   eval      vgofir = w_firm
     c                   eval      vgoogr = d_ogrp_num
     c                   eval      vgotxt = d_obes
      *
     c                   eval      vgoous = l_user
     c                   time                    vgooda
     c                   time                    vgooti
     c                   eval      vgoeus = l_user
     c                   time                    vgoeda
     c                   time                    vgoeti
      *
     c                   eval      w_ovgn = w_ovgn + 1
     c                   write     vogrlur
     c                   endif
     c                   eval      w_ovgl = w_ovgl + 1
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer hovedgruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_hgrp      begsr
      *
     c                   eval      vhgrlu_hogr = d_ogrp_num
     c                   eval      vhgrlu_hhgr = d_hgrp_num
     c     vhgrlu_key    chain     vhgrlur
      *
     c                   if        %found
     c                   if        vghtxt <> d_hbes
     c                   eval      vghtxt = d_hbes
     c                   eval      vgheus = l_user
     c                   time                    vgheda
     c                   time                    vgheti
     c                   eval      w_hvgu = w_hvgu + 1
     c                   update    vhgrlur
     c                   endif
     c                   else
     c                   clear                   vhgrlur
     c                   eval      vghfir      = w_firm
     c                   eval      vghogr      = d_ogrp_num
     c                   eval      vghhgr      = d_hgrp_num
     c                   eval      vghtxt      = d_hbes
      *
     c                   eval      vgheus = l_user
     c                   time                    vgheda
     c                   time                    vgheti
     c                   eval      vghous = l_user
     c                   time                    vghoda
     c                   time                    vghoti
      *
     c                   eval      w_hvgn = w_hvgn + 1
     c                   write     vhgrlur
     c                   endif
     c                   eval      w_hvgl = w_hvgl + 1
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Oppdaterer undergruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_ugrp      begsr
      *
     c                   eval      vugrlu_uogr = d_ogrp_num
     c                   eval      vugrlu_uhgr = d_hgrp_num
     c                   eval      vugrlu_uugr = d_ugrp_num
     c     vugrlu_key    chain     vugrlur
      *
     c                   if        %found
     c                   if        vgutxt <> d_ubes or
     c                             (vguep1 <> d_ups1 and d_ups1 <> *blank) or
6.30 c                             (vguep2 <> d_ups2 and d_ups2 <> *blank) or
6.30 c                             (vgufri <> d_ufri and d_ufri <> *blank)
     c                   eval      vgutxt = d_ubes
     c                   eval      vguep1 = d_ups1
     c                   eval      vguep2 = d_ups2
6.30 c                   eval      vgufri = d_ufri
     c                   eval      vgueus = l_user
     c                   time                    vgueda
     c                   time                    vgueti
     c                   eval      w_uvgu = w_uvgu + 1
     c                   update    vugrlur
     c                   endif
     c                   else
     c                   clear                   vugrlur
      *
     c                   eval      vgufir = w_firm
     c                   eval      vguogr = d_ogrp_num
     c                   eval      vguhgr = d_hgrp_num
     c                   eval      vguugr = d_ugrp_num
     c                   eval      vgutxt = d_ubes
     c                   eval      vguep1 = d_ups1
     c                   eval      vguep2 = d_ups2
6.30 c                   eval      vgufri = d_ufri
      *
     c                   eval      vguous = l_user
     c                   time                    vguoda
     c                   time                    vguoti
     c                   eval      vgueus = l_user
     c                   time                    vgueda
     c                   time                    vgueti
      *
     c                   eval      w_uvgn = w_uvgn + 1
     c                   write     vugrlur
     c                   endif
     c                   eval      w_uvgl = w_uvgl + 1
      *
     c                   endsr


      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_orec
     c                   parm                    p_hrec
     c                   parm                    p_urec
     c                   parm                    p_job_log
     c                   parm                    p_inlr
      *
      * Key for oppdatering av overgruppe
      *
     c     vogrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrlu_oogr
      *
      * Key for oppdatering av hovedgruppe
      *
     c     vhgrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrlu_hogr
     c                   kfld                    vhgrlu_hhgr
      *
      * Key for oppdatering av undergruppe
      *
     c     vugrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrlu_uogr
     c                   kfld                    vugrlu_uhgr
     c                   kfld                    vugrlu_uugr
      *
      * Key for oppslag på status
      *
     c     jstsl1_key    klist
     c                   kfld                    w_firm
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = l_firm
      *
     c     jstsl1_key    chain     jstsl1
      *
     c                   endsr

