     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : NN700R
      * Beskrivelse . . : Nettbutikk, eksport av kundeinformasjon
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       140502 HKO  Nytt program
6.20  * 6.20  210813 bsa  Rekompilert grunnet endret ANBLPF
6.31  * 6.30  080219 bof  Rekompilert grunnet endret ANBLPF
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fapospf    if   e           k disk    rename(apospfr:apospfr)
     fanbll3    if   e           k disk    rename(anblpfr:anbll3r)
     fanbhl1    if   e           k disk    rename(anbhpfr:anbhl1r)
      *
     fnetkund   uf a e           k disk
     fnetkrapp  o  a e             disk    rename(netkrapp:netkrappr)
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_fgr                 931    933
      *
      * Definering av datastrukturer
      *
      * Recordbeskrivelse - meldingshode
      *
     d                 ds
     d d_unh                         41
     d  d_unhrcd                      3    overlay(d_unh: 1) inz('UNH')
     d  d_unhfill1                    1    overlay(d_unh: 4) inz(';')
     d  d_unhrefno                   26    overlay(d_unh: 5)
     d  d_unhfill2                    1    overlay(d_unh:31) inz(';')
     d  d_unhtypeid                   6    overlay(d_unh:32) inz('IMLIMP')
     d  d_unhfill3                    1    overlay(d_unh:38) inz(';')
     d  d_unhversion                  3    overlay(d_unh:39) inz('1.0')
      *
      * Recordbeskrivelse - meldingsstart
      *
     d                 ds
     d d_bgm                         16
     d  d_bgmrcd                      3    overlay(d_bgm: 1) inz('BGM')
     d  d_bgmfill1                    1    overlay(d_bgm: 4) inz(';')
     d  d_bgmetypekod                 9    overlay(d_bgm: 5) inz('IMLCUSIMP')
     d  d_bgmfill2                    2    overlay(d_bgm:14) inz(';;')
     d  d_bgmfunction                 1    overlay(d_bgm:16) inz('1')
      *
      * Recordbeskrivelse - organisasjon
      *
     d                 ds
     d d_org                        126
     d  d_orgrcd                      3    overlay(d_org: 1) inz('ORG')
     d  d_orgfill1                    1    overlay(d_org: 4) inz(';')
     d  d_orgid                      30    overlay(d_org: 5)
     d  d_orgfill2                    1    overlay(d_org:35) inz(';')
     d  d_orgstoreid                 30    overlay(d_org:36)
     d  d_orgfill3                    1    overlay(d_org:66) inz(';')
     d  d_orgname                    60    overlay(d_org:67)
      *
      * Recordbeskrivelse - kunde
      *
     d                 ds
     d d_cus                        509
     d  d_cusrcd                      3    overlay(d_cus:  1) inz('CUS')
     d  d_cusfill1                    1    overlay(d_cus:  4) inz(';')
     d  d_cusnum                      6  0 overlay(d_cus:  5)
     d  d_cusfill2                    1    overlay(d_cus: 11) inz(';')
     d  d_cusorg                      9  0 overlay(d_cus: 12)
     d  d_cusfill3                    1    overlay(d_cus: 21) inz(';')
     d  d_cusnam                     35    overlay(d_cus: 22)
     d  d_cusfill4                    2    overlay(d_cus: 57) inz(';;')
     d  d_cusad1                     35    overlay(d_cus: 59)
     d  d_cusfill5                    1    overlay(d_cus: 94) inz(';')
     d  d_cusad2                     35    overlay(d_cus: 95)
     d  d_cusfill6                    1    overlay(d_cus:130) inz(';')
     d  d_cuspon                      4  0 overlay(d_cus:131)
     d  d_cusfill7                    1    overlay(d_cus:135) inz(';')
     d  d_cuspad                     35    overlay(d_cus:136)
     d  d_cusfill8                    1    overlay(d_cus:171) inz(';')
     d  d_cuscty                      2    overlay(d_cus:172)
     d  d_cusfill9                    1    overlay(d_cus:174) inz(';')
     d  d_cusmai                    100    overlay(d_cus:175)
     d  d_cusfill10                   1    overlay(d_cus:275) inz(';')
     d  d_custlf                     15    overlay(d_cus:276)
     d  d_cusfill11                   1    overlay(d_cus:291) inz(';')
     d  d_cusmob                     15    overlay(d_cus:292)
     d  d_cusfill12                   1    overlay(d_cus:307) inz(';')
     d  d_cusfax                     15    overlay(d_cus:308)
     d  d_cusfill13                   1    overlay(d_cus:323) inz(';')
     d  d_cuscre                      1  0 overlay(d_cus:324)
     d**d_cusfill14                   1    overlay(d_cus:325) inz(';')
     d**d_cuscop                     35    overlay(d_cus:326)
     d**d_cusfill15                   1    overlay(d_cus:361) inz(';')
     d**d_cuscml                    100    overlay(d_cus:362)
     d**d_cusfill16                   1    overlay(d_cus:462) inz(';')
     d**d_cusctl                     15    overlay(d_cus:463)
     d**d_cusfill17                   1    overlay(d_cus:478) inz(';')
     d**d_cuscmo                     15    overlay(d_cus:479)
     d**d_cusfill18                   1    overlay(d_cus:494) inz(';')
     d**d_cuscfx                     15    overlay(d_cus:495)
      *
      * Recordbeskrivelse - kundeprosjekt
      *
     d                 ds
     d d_cpr                        351
     d  d_cprrcd                      3    overlay(d_cpr:  1) inz('CPR')
     d  d_cprfill1                    1    overlay(d_cpr:  4) inz(';')
     d  d_cprcun                      6  0 overlay(d_cpr:  5)
     d  d_cprfill2                    1    overlay(d_cpr: 11) inz(';')
     d  d_cprprn                      6  0 overlay(d_cpr: 12)
     d  d_cprfill3                    1    overlay(d_cpr: 18) inz(';')
     d  d_cprnam                     35    overlay(d_cpr: 19)
     d  d_cprfill4                    1    overlay(d_cpr: 54) inz(';')
     d  d_cprad1                     35    overlay(d_cpr: 55)
     d  d_cprfill5                    1    overlay(d_cpr: 90) inz(';')
     d  d_cprad2                     35    overlay(d_cpr: 91)
     d  d_cprfill6                    1    overlay(d_cpr:126) inz(';')
     d  d_cprpon                      4  0 overlay(d_cpr:127)
     d  d_cprfill7                    1    overlay(d_cpr:131) inz(';')
     d  d_cprpad                     35    overlay(d_cpr:132)
     d  d_cprfill8                    1    overlay(d_cpr:167) inz(';')
     d  d_cprcop                     35    overlay(d_cpr:168)
     d  d_cprfill9                    1    overlay(d_cpr:203) inz(';')
     d  d_cprcml                    100    overlay(d_cpr:204)
     d  d_cprfill10                   1    overlay(d_cpr:304) inz(';')
     d  d_cprctl                     15    overlay(d_cpr:305)
     d  d_cprfill11                   1    overlay(d_cpr:320) inz(';')
     d  d_cprcmo                     15    overlay(d_cpr:321)
     d  d_cprfill12                   1    overlay(d_cpr:336) inz(';')
     d  d_cprcfx                     15    overlay(d_cpr:337)
      *
      * Recordbeskrivelse - meldingshale
      *
     d                 ds
     d d_unt                         43
     d  d_untrcd                      3    overlay(d_unt: 1) inz('UNT')
     d  d_untfill1                    1    overlay(d_unt: 4) inz(';')
     d  d_untnosegmen                12  0 overlay(d_unt: 5)
     d  d_untfill2                    1    overlay(d_unt:17) inz(';')
     d  d_untrefno                   26    overlay(d_unt:18)
      *
      * Definering av variabler i nøkler
      *
     d fkprl1_kund     s                   like(flkund)
     d apospf_ponr     s                   like(apponr)
     d anbll3_fgr      s                   like(aolfgr)
     d anbll3_fir      s                   like(aolfir)
      *
      * Definering av variable
      *
     d w_tmst          s               z
     d w_tell          s             12  0
     d w_ponr          s              4
      *
     d w_firm          s                   like(rkfirm)
     d w_dato          s                   like(flfdat)
      *
      * Definering av parametre
      *
     d p_stat          s              1
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      p_stat = *blank
      *
      * Henter data om nettbutikk og eier. Avslutter hvis ingen finnes.
      *
     c                   eval      anbll3_fgr = l_fgr
     c                   eval      anbll3_fir = l_firm
     c     anbll3_key    chain     anbll3r
     c                   if        not %found
     c                   eval      p_stat = 'E'
     c                   goto      avslutt
     c                   endif
     c     aoleie        chain     anbhl1r
     c                   if        not %found
     c                   eval      p_stat = 'E'
     c                   goto      avslutt
     c                   endif
      *
      * Meldingshode
      *
     c                   time                    w_tmst
     c                   time                    w_dato
     c                   move      w_tmst        d_unhrefno
      *
     c                   eval      data = d_unh
     c                   write     netkundr
      *
      * Meldingsstart
      *
     c                   eval      data = d_bgm
     c                   write     netkundr
      *
      * Organisasjon
      *
     c                   eval      d_orgid = aoheie
     c                   eval      d_orgstoreid = aolbut
     c                   eval      d_orgname = aohnav
      *
     c                   eval      data = d_org
     c                   write     netkundr
      *
      * Kunde / kundeprosjekt
      *
     c     rkunl1_key    setll     rkunl1
     c     rkunl1_key    reade     rkunl1
     c                   dow       %eof = *off
      *
     c                   if        rkebiz = 1 and
     c                             rkemal <> *blank
      *
     c                   if        rkftnr = 0
     c                   eval      SrcDta = %trim(rknavn) +
     c                             ' mangler ' +
     c                             'org-nr. Kunden opprettes ' +
     c                             'som privatkunde.'
     c                   write     netkrappr
     c                   eval      p_stat = 'R'
     c                   endif
      *
     c                   if        %subst(rksted:6:25) = *blank
     c                   eval      SrcDta = %trim(rknavn) +
     c                             ' mangler ' +
     c                             'postadresse, og utelates'
     c                   write     netkrappr
     c                   eval      p_stat = 'R'
     c     rkunl1_key    reade     rkunl1
     c                   iter
     c                   endif
      *
     c                   eval      w_tell = w_tell + 1
      *
     c                   eval      d_cusnum = rkkund
     c                   eval      d_cusorg = rkftnr
     c                   eval      d_cusnam = rknavn
     c                   eval      d_cusad1 = rkgate
     c                   eval      d_cusad2 = rkadr2
     c                   eval      d_cuspon = rkponr
     c                   eval      d_cuspad = %subst(rksted:6:25)
     c                   eval      d_cuscty = 'NO'
     c                   eval      d_cusmai = rkemal
     c                   eval      d_cuscre = 0
     c*                  eval      d_cuscre = rkkres
      *
     c                   if        rkland <> *blank
     c                   eval      d_cuscty = rkland
     c                   endif
      *
     c                   eval      data = d_cus
     c                   write     netkundr
      *
     c                   eval      fkprl1_kund = rkkund
     c     fkprl1_key    setll     fkprl1
     c     fkprl1_key    reade     fkprl1
     c                   dow       %eof = *off
      *
     c                   if        fltdat >= w_dato
     c                   eval      w_tell = w_tell + 1
      *
     c                   eval      d_cprcun = flkund
     c                   eval      d_cprprn = flkpro
     c                   eval      d_cprnam = flnavn
     c                   eval      d_cprad1 = fladr1
     c                   eval      d_cprad2 = fladr2
     c                   eval      d_cprpon = flponr
     c                   eval      d_cprpad = *blank
     c                   eval      d_cprcop = flkprs
     c                   eval      d_cprcml = flmail
     c                   eval      d_cprctl = fltlfa
     c                   eval      d_cprcmo = fltlfm
     c                   eval      d_cprcfx = *blank
      *
     c                   if        flponr <> 0
     c                   eval      apospf_ponr = flponr
     c     apospf_key    chain     apospf
     c                   if        %found
     c                   eval      d_cprpad = apsted
     c                   endif
     c                   endif
      *
     c                   eval      data = d_cpr
     c                   write     netkundr
     c                   endif
      *
     c     fkprl1_key    reade     fkprl1
     c                   enddo
      *
     c                   endif
     c                   if        rkemal = *blank and
     c                             rkebiz = 1
     c                   eval      SrcDta = %trim(rknavn) +
     c                             ' mangler ' +
     c                             'E-post adresse, og utelates'
     c                   write     netkrappr
     c                   eval      p_stat = 'R'
     c                   endif
      *
     c     rkunl1_key    reade     rkunl1
     c                   enddo
      *
      * Meldingshale
      *
     c                   eval      d_untnosegmen = w_tell
     c                   eval      d_untrefno = d_unhrefno
      *
     c                   eval      data = d_unt
     c                   write     netkundr
      *
      *
      * Avslutt
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_stat
      *
      * Key for les på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
      *
      * Key for oppslag på poststed-register
      *
     c     apospf_key    klist
     c                   kfld                    apospf_ponr
      *
      * Key for oppslag på nettbutikk
      *
     c     anbll3_key    klist
     c                   kfld                    anbll3_fgr
     c                   kfld                    anbll3_fir
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
