     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON
      * Program . . . . : RR635R
      * Beskrivelse . . : Automatisk bygging av resultatrapporter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *
T400  * V4.00     150402  Endret spes/bærer fra 99 til 9999.              HFL *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frhovl1    if   e           k disk    rename(rhovpfr:rhovl1r)
     fra19l1    if   e           k disk    rename(ra19pfr:ra19l1r)
     fra20l1    if   e           k disk    rename(ra20pfr:ra20l1r)
     frrf0l1    uf a e           k disk    rename(rrf0pfr:rrf0l1r)
     frrf5l2    uf a e           k disk    rename(rrf5pfr:rrf5l2r)
     frrf8l1    uf a e           k disk    rename(rrf8pfr:rrf8l1r)
      *
     d rhovl1_firm     s                   like(rhfirm)
      *
     d ra19l1_sfir     s                   like(rasfir)
      *
     d ra20l1_tfir     s                   like(ratfir)
      *
     d rrf0l1_firm     s                   like(rr0fir)
     d rrf0l1_rapp     s                   like(rr0rap)
      *
     d rrf5l2_firm     s                   like(rr5fir)
     d rrf5l2_rapp     s                   like(rr5rap)
      *
     d rrf8l1_firm     s                   like(rr8fir)
     d rrf8l1_rapp     s                   like(rr8rap)
     d rrf8l1_linj     s                   like(rr8lin)
      *
      * Definering av LDA
      *
     d                uds
     D l_wsid                901    910
     D l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     D w_timestamp     s             12  0
     D w_date          s              6  0
      *
      * Datastruktur for parameterliste - inn
      *
     d p_list          s             64
      *
     d                 ds
     d d_list                        64
     d  d_firm                        3  0 overlay(d_list)
     d  d_rapp                        2  0 overlay(d_list:4)
     d  d_type                        1  0 overlay(d_list:6)
     d  d_linj                        1  0 overlay(d_list:7)
      *
     d w_klasse        s              2  0
      *
      * Leser konto hvis rapport på kontonivå
      *
     c                   if        d_linj = 0
      *
     c                   eval      rhovl1_firm = l_firm
      *
     c     rhovl1_key    setll     rhovl1r                                90
     c     rhovl1_key    reade     rhovl1r                                90
      *
     c                   dow       *in90 = *off
      *
      * Bare hovedkonti legges ut
      *
     c                   if        rhspes = *zero and
     c                             rhavde = *zero
      *
      * Balanserapport
      *
     c                   if        d_type = 0 and
     c                             rhkont < 3000
      *
     c                   exsr      linje_konto
      *
     c                   endif
      *
      * Resultatrapport
      *
     c                   if        d_type = 1 and
     c                             rhkont > 2999
      *
     c                   exsr      linje_konto
      *
     c                   endif
      *
     c                   endif
      *
     c     rhovl1_key    reade     rhovl1r                                90
      *
     c                   enddo
      *
     c                   endif
      *
     c/eject
      *
      * Leser kontoklasse
      *
     c                   eval      ra19l1_sfir = l_firm
      *
     c     ra19l1_key    setll     ra19l1r                                91
     c     ra19l1_key    reade     ra19l1r                                91
      *
     c                   dow       *in91 = *off
      *
      * Balanserapport
      *
     c                   if        d_type = 0 and
     c                             raskod < 3
      *
     c                   exsr      linje19
      *
     c                   endif
      *
      * Resultatrapport
      *
     c                   if        d_type = 1 and
     c                             raskod > 2
      *
     c                   exsr      linje19
      *
     c                   endif
      *
     c     ra19l1_key    reade     ra19l1r                                91
      *
     c                   enddo
      *
     c/eject
      *
      * Leser kontogruppe
      *
     c                   eval      ra20l1_tfir = l_firm
      *
     c     ra20l1_key    setll     ra20l1r                                92
     c     ra20l1_key    reade     ra20l1r                                92
      *
     c                   dow       *in92 = *off
      *
      * Legg ut rapport på kontonivå
      *
     c                   if        d_linj = 0
      *
      * Balanserapport
      *
     c                   if        d_type = 0 and
     c                             ratkod < 30
      *
     c                   exsr      linje20
      *
     c                   endif
      *
      * Resultatrapport
      *
     c                   if        d_type = 1 and
     c                             ratkod > 29
      *
     c                   exsr      linje20
      *
     c                   endif
      *
     c                   else
      *
      * Legg ut rapport på gruppenivå
      *
      *
      * Balanserapport
      *
     c                   if        d_type = 0 and
     c                             ratkod < 30
      *
     c                   exsr      linje_gruppe
      *
     c                   endif
      *
      * Resultatrapport
      *
     c                   if        d_type = 1 and
     c                             ratkod > 29
      *
     c                   exsr      linje_gruppe
      *
     c                   endif
      *
     c                   endif
      *
     c     ra20l1_key    reade     ra20l1r                                92
      *
     c                   enddo
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
      * Legg ut avlutningslinjer
      *
      *
      * Resultatrapport
      *
     c                   if        d_type = 1
      *
     c                   eval      rr5fir = d_firm
     c                   eval      rr5rap = d_rapp
     c                   eval      rr5lin = 99998
     c                   eval      rr5txt = 'Driftsutgifter'
     c                   eval      rr5kod = 'L'
     c                   eval      rr5sln = *zero
     c                   eval      rr5nul = *blank
     c                   eval      rr5abl = *blank
     c                   eval      rr5ber = *blank
      *
     c                   write     rrf5l2r
      *
     c                   eval      rr8fir = d_firm
     c                   eval      rr8rap = d_rapp
     c                   eval      rr8lin = 99998
     c                   eval      rr8lØp = 001
     c                   eval      rr8fkl = *zero
     c                   eval      rr8fkt = *zero
     c                   eval      rr8fsp = *zero
     c                   eval      rr8fav = *zero
     c                   eval      rr8tkl = *zero
     c                   eval      rr8tkt = *zero
     c                   eval      rr8tsp = *zero
     c                   eval      rr8tav = *zero
     c                   eval      rr8klf = 'L'
     c                   eval      rr8lif = 49997
     c                   eval      rr8mot = *blank
     c                   eval      rr8pro = *zeros
      *
     c                   write     rrf8l1r
      *
     c                   eval      rr8lØp = 002
     c                   eval      rr8lif = 59997
     c                   write     rrf8l1r
      *
     c                   eval      rr8lØp = 003
     c                   eval      rr8lif = 69997
     c                   write     rrf8l1r
      *
     c                   eval      rr8lØp = 004
     c                   eval      rr8lif = 79997
     c                   write     rrf8l1r
      *
     c                   eval      rr8lØp = 005
     c                   eval      rr8lif = 89997
     c                   write     rrf8l1r
      *
      * Driftsresultat
      *
     c                   eval      rr5fir = d_firm
     c                   eval      rr5rap = d_rapp
     c                   eval      rr5lin = 99999
     c                   eval      rr5txt = 'Driftsresultat'
     c                   eval      rr5kod = 'L'
     c                   eval      rr5sln = *zero
     c                   eval      rr5nul = *blank
     c                   eval      rr5abl = *blank
     c                   eval      rr5ber = *blank
      *
     c                   write     rrf5l2r
      *
     c                   eval      rr8fir = d_firm
     c                   eval      rr8rap = d_rapp
     c                   eval      rr8lin = 99999
     c                   eval      rr8lØp = 001
     c                   eval      rr8fkl = *zero
     c                   eval      rr8fkt = *zero
     c                   eval      rr8fsp = *zero
     c                   eval      rr8fav = *zero
     c                   eval      rr8tkl = *zero
     c                   eval      rr8tkt = *zero
     c                   eval      rr8tsp = *zero
     c                   eval      rr8tav = *zero
     c                   eval      rr8klf = 'L'
     c                   eval      rr8lif = 39997
     c                   eval      rr8mot = ' '
     c                   eval      rr8pro = *zeros
      *
     c                   write     rrf8l1r
      *
     c                   eval      rr8lØp = 002
     c                   eval      rr8lif = 99998
     c                   eval      rr8mot = 'J'
     c                   write     rrf8l1r
      *
     c                   endif
      *
      * Balanserapport
      *
     c                   if        d_type = 0
      *
     c                   eval      rr5fir = d_firm
     c                   eval      rr5rap = d_rapp
     c                   eval      rr5lin = 99998
     c                   eval      rr5txt = 'Driftsresultat'
     c                   eval      rr5kod = 'K'
     c                   eval      rr5sln = *zero
     c                   eval      rr5nul = *blank
     c                   eval      rr5abl = *blank
     c                   eval      rr5ber = *blank
      *
     c                   write     rrf5l2r
      *
     c                   eval      rr8fir = d_firm
     c                   eval      rr8rap = d_rapp
     c                   eval      rr8lin = 99998
     c                   eval      rr8lØp = 001
     c                   eval      rr8fkl = *zero
     c                   eval      rr8fkt = 3000
     c                   eval      rr8fsp = *zero
     c                   eval      rr8fav = *zero
     c                   eval      rr8tkl = *zero
     c                   eval      rr8tkt = 9999
E400 c                   eval      rr8tsp = 9999
     c                   eval      rr8tav = *zero
     c                   eval      rr8klf = *blank
     c                   eval      rr8lif = *zero
     c                   eval      rr8mot = 'J'
     c                   eval      rr8pro = *zeros
      *
     c                   write     rrf8l1r
      *
     c                   eval      rr5fir = d_firm
     c                   eval      rr5rap = d_rapp
     c                   eval      rr5lin = 99999
     c                   eval      rr5txt = 'Avstemming    '
     c                   eval      rr5kod = 'L'
     c                   eval      rr5sln = *zero
     c                   eval      rr5nul = *blank
     c                   eval      rr5abl = *blank
     c                   eval      rr5ber = *blank
      *
     c                   write     rrf5l2r
      *
     c                   eval      rr8fir = d_firm
     c                   eval      rr8rap = d_rapp
     c                   eval      rr8lin = 99999
     c                   eval      rr8lØp = 001
     c                   eval      rr8fkl = *zero
     c                   eval      rr8fkt = *zero
     c                   eval      rr8fsp = *zero
     c                   eval      rr8fav = *zero
     c                   eval      rr8tkl = *zero
     c                   eval      rr8tkt = *zero
     c                   eval      rr8tsp = *zero
     c                   eval      rr8tav = *zero
     c                   eval      rr8klf = 'L'
     c                   eval      rr8lif = 19997
     c                   eval      rr8mot = *blank
     c                   eval      rr8pro = *zeros
      *
     c                   write     rrf8l1r
      *
     c                   eval      rr8lØp = 002
     c                   eval      rr8lif = 29997
     c                   eval      rr8mot = 'J'
     c                   write     rrf8l1r
      *
     c                   endif
      *
     c                   eval      *inlr = *on
     c                   return
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for linjer på kontonivå
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     linje_konto   begsr
      *
     c                   eval      rr5fir = d_firm
     c                   eval      rr5rap = d_rapp
     c                   eval      rr5lin = rhkont * 10
     c                   eval      rr5txt = rhtext
     c                   eval      rr5kod = 'K'
     c                   eval      rr5sln = *zero
     c                   eval      rr5nul = *blank
     c                   eval      rr5abl = *blank
     c                   eval      rr5ber = *blank
      *
     c                   write     rrf5l2r
      *
     c                   eval      rr8fir = d_firm
     c                   eval      rr8rap = d_rapp
     c                   eval      rr8lin = rhkont * 10
     c                   eval      rr8lØp = 001
     c                   eval      rr8fkl = *zero
     c                   eval      rr8fkt = rhkont
     c                   eval      rr8fav = *zero
     c                   eval      rr8tkl = *zero
     c                   eval      rr8tkt = rhkont
      *
     c                   if        rhspes = *zero
     c                   eval      rr8fsp = *zero
E400 c                   eval      rr8tsp = 9999
     c                   else
     c                   eval      rr8fsp = rhspes
     c                   eval      rr8tsp = rhspes
     c                   endif
      *
     c                   eval      rr8tav = *zero
     c                   eval      rr8klf = 'K'
     c                   eval      rr8lif = *zero
      *
     c                   movel     rhkont        w_klasse
      *
     c                   if        w_klasse = 02 or
     c                             w_klasse = 03
     c                   eval      rr8mot = 'J'
     c                   else
     c                   eval      rr8mot = *blank
     c                   endif
      *
     c                   eval      rr8pro = *zeros
      *
     c                   write     rrf8l1r
      *
     c                   endsr
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for linjer på kontogruppenivå
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     linje_gruppe  begsr
      *
     c                   eval      rr5fir = d_firm
     c                   eval      rr5rap = d_rapp
     c                   eval      rr5lin = (ratkod * 1000)
     c                   eval      rr5txt = rattxt
     c                   eval      rr5kod = 'K'
     c                   eval      rr5sln = *zero
     c                   eval      rr5nul = *blank
     c                   eval      rr5abl = *blank
     c                   eval      rr5ber = *blank
      *
     c                   write     rrf5l2r
      *
     c                   eval      rr8fir = d_firm
     c                   eval      rr8rap = d_rapp
     c                   eval      rr8lin = (ratkod * 1000)
     c                   eval      rr8lØp = 001
     c                   eval      rr8fkl = *zero
     c                   eval      rr8fkt = (ratkod * 100)
     c                   eval      rr8fsp = *zero
     c                   eval      rr8fav = *zero
     c                   eval      rr8tkl = *zero
     c                   eval      rr8tkt = (ratkod * 100) + 99
E400 c                   eval      rr8tsp = 9999
     c                   eval      rr8tav = *zero
     c                   eval      rr8klf = 'K'
     c                   eval      rr8lif = *zero
      *
     c                   movel     ratkod        w_klasse
     c                   if        w_klasse >= 20 and
     c                             w_klasse <= 39
     c                   eval      rr8mot = 'J'
     c                   else
     c                   eval      rr8mot = *blank
     c                   endif
      *
     c                   eval      rr8pro = *zeros
      *
     c                   write     rrf8l1r
      *
     c                   endsr
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for linjer - Kontoklasse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     linje19       begsr
      *
      * Tekstlinje
      *
     c                   eval      rr5fir = d_firm
     c                   eval      rr5rap = d_rapp
     c                   eval      rr5lin = (raskod * 10000) - 2
     c                   eval      rr5txt = rastxt
     c                   eval      rr5kod = 'T'
     c                   eval      rr5sln = *zero
     c                   eval      rr5nul = *blank
     c                   eval      rr5abl = *blank
     c                   eval      rr5ber = *blank
      *
     c                   write     rrf5l2r
      *
      * Sumlinje
      *
     c                   eval      rr5fir = d_firm
     c                   eval      rr5rap = d_rapp
     c                   eval      rr5lin = (raskod * 10000) + 9997
     c                   eval      rr5txt = rastxt
     c                   eval      rr5kod = 'S'
     c                   eval      rr5sln = *zero
     c                   eval      rr5nul = *blank
     c                   eval      rr5abl = '3'
     c                   eval      rr5ber = *blank
      *
     c                   write     rrf5l2r
      *
     c                   endsr
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for linjer - Kontogruppe
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     linje20       begsr
      *
      * Tekstlinje
      *
     c                   eval      rr5fir = d_firm
     c                   eval      rr5rap = d_rapp
     c                   eval      rr5lin = (ratkod * 1000) - 1
     c                   eval      rr5txt = rattxt
     c                   eval      rr5kod = 'T'
     c                   eval      rr5sln = *zero
     c                   eval      rr5nul = *blank
     c                   eval      rr5abl = *blank
     c                   eval      rr5ber = *blank
      *
     c                   write     rrf5l2r
      *
      * Sumlinje
      *
     c                   eval      rr5fir = d_firm
     c                   eval      rr5rap = d_rapp
     c                   eval      rr5lin = (ratkod * 1000) + 996
     c                   eval      rr5txt = rattxt
     c                   eval      rr5kod = 'B'
     c                   eval      rr5sln = *zero
     c                   eval      rr5nul = *blank
     c                   eval      rr5abl = '1'
     c                   eval      rr5ber = *blank
      *
     c                   write     rrf5l2r
      *
     c                   endsr
      *
     c/eject
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Paramtre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
     c                   eval      d_list = p_list
      *
      * Generer rapport-hode (RR0)
      *
     c                   eval      rr0fir = d_firm
     c                   eval      rr0rap = d_rapp
      *
      * Rapporttype
      *
     c                   if        d_type = 0
     c                   eval      rr0txt = 'Balanserapport'
     c                   else
     c                   eval      rr0txt = 'Resultatrapport'
     c                   endif
      *
     c                   eval      rr0bfj = 'B'
      *
     c                   write     rrf0l1r
      *
     C                   time                    w_timestamp
     C                   move      w_timestamp   w_date
      *
      * Key for kontoplanregister
      *
     c     rhovl1_key    klist
     c                   kfld                    rhovl1_firm
      *
      * Key for kontogruppe
      *
     c     ra19l1_key    klist
     c                   kfld                    ra19l1_sfir
      *
      * Key for kontoklasse
      *
     c     ra20l1_key    klist
     c                   kfld                    ra20l1_tfir
      *
      * Key for rapportregister 0
      *
     c     rrf0l1_key    klist
     c                   kfld                    rrf0l1_firm
      *
      * Key for rapportregister 5
      *
     c     rrf5l2_key    klist
     c                   kfld                    rrf5l2_firm
     c                   kfld                    rrf5l2_rapp
      *
      * Key for rapportregister 8
      *
     c     rrf8l1_key    klist
     c                   kfld                    rrf8l1_firm
     c                   kfld                    rrf8l1_rapp
     c                   kfld                    rrf8l1_linj
      *
     c                   endsr
