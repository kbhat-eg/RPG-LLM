     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VP702R
      * Beskrivelse . . : Vare-pris, henter priser for enhet III
      *
      *                   Input:  Firma, vare, prisgr, enhet, leveringstype,
      *                           dato og LR-flagg
      *                   Output: Salgspris, kostpris, innkjøpspris,
      *                           butikkpris, prisdato og omregningsfaktor
      *                           volum i forhold til basisenhet
      *
      *                   LR-flagg må være lik '1' for at *inLR ikke skal
      *                   settes
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       300999 HKO  Nytt program
5.70  * PRGR  231008 BOF  Utvidet med prisgruppe
6.20  *       220311 BOF  Utvidet med prisgiver på vare-pris
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvprl1    if   e           k disk    rename(vvprpfr:vvprl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvvenl1    if   e           k disk    rename(vvenpfr:vvenl1r)
      *
      * Definering av pamametre
      *
     d p_firm          s                   like(vpfirm)
     d p_vare          s                   like(vpvare)
5.70 d p_prgr          s                   like(vpprgr)
     d p_enhe          s                   like(vpenhe)
     d p_lety          s                   like(vplety)
     d p_dato          s                   like(vppdat)
     d p_sapr          s                   like(vpsapr)
     d p_bupr          s                   like(vpbupr)
     d p_kopr          s                   like(vpkopr)
     d p_inpr          s                   like(vpinpr)
     d p_pdat          s                   like(vppdat)
     d p_omre          s                   like(veomre)
      *
     d p_inlr          s              1
      *
      * Definering av variable i nøkler
      *
     d vvprl1_vare     s                   like(vpvare)
6.20 d vvprl1_ldor     s                   like(vpldor)
5.70 d vvprl1_prgr     s                   like(vpprgr)
     d vvprl1_enhe     s                   like(vpenhe)
     d vvprl1_lety     s                   like(vplety)
     d vvarl1_vare     s                   like(vvvare)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d vvenl1_vare     s                   like(vevare)
     d vvenl1_enhe     s                   like(veenhe)
      *
      * Definering av variable
      *
     d b_pris          s              1    inz(*off)
6.20 d b_inlr          s              1    inz(*off)
      *
     d w_firm          s                   like(vpfirm)
     d w_dato          s                   like(vppdat)
     d w_omre          s                   like(veomre)
6.20 d w_ldor          s                   like(vvldor)
6.20 d w_lage          s              2  0
      *
6.20 d                uds
6.20 d l_lage               1001   1002  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
6.20 c                   eval      w_lage = l_lage
6.20  *
6.20  * Henter leverandør for vare og lager
6.20  *
6.20 c                   call      'VL721R  '
6.20 c                   parm                    w_firm
6.20 c                   parm                    p_vare
6.20 c                   parm                    w_lage
6.20 c                   parm                    w_ldor
6.20 c                   parm                    b_inlr
      *
      * Initierer dato
      *
     c                   eval      w_dato = p_dato
      *
     c                   if        w_dato = *loval
     c                   time                    w_dato
     c                   endif
      *
      * Initierer output-parametre
      *
     c                   eval      p_sapr = 0
     c                   eval      p_bupr = 0
     c                   eval      p_kopr = 0
     c                   eval      p_inpr = 0
     c                   eval      p_pdat = *loval
     c                   eval      p_omre = 0
      *
      * Henter pris for vare, leveringstype og enhet
      *
6.20  * 1. leverandør for lageret
6.20 c                   eval      vvprl1_ldor = w_ldor
6.20 c                   exsr      hent_pris
6.20 c                   if        b_pris = *off  and
6.20 c                             w_ldor <> vvldor
6.20  * 2. hovedleverandør
6.20 c                   eval      vvprl1_ldor = vvldor
6.20 c                   exsr      hent_pris
6.20 c                   if        b_pris = *off
6.20  * siste mulighet...
6.20 c                   eval      vvprl1_ldor = 0
6.20 c                   exsr      hent_pris
6.20 c                   endif
6.20 c                   endif
      *
      * Avbryter dersom pris ikke ble funnet
      *
     c                   if        b_pris = *off
     c                   goto      avslutt
     c                   endif
      *
      * Henter vareinformasjon
      *
     c                   clear                   vvarl1r
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1                             90
      *
      * Dersom kostpris er 0 hentes kostpris på bakgrunn av DG-% på vare
      * eller undergruppe
      *
     c                   if        p_kopr = 0
      *
     c                   if        vvdekn = 0
     c                   eval      vugrl1_uogr = vvogrp
     c                   eval      vugrl1_uhgr = vvhgrp
     c                   eval      vugrl1_uugr = vvugrp
     c     vugrl1_key    chain     vugrl1r                            90
     c                   if        *in90 = *off
     c                   eval      vvdekn = vgudek
     c                   endif
     c                   endif
      *
     c                   eval(h)   p_kopr = p_sapr * vvdekn / 100
     c                   if        p_kopr <> 0
     c                   eval      p_kopr = p_sapr - p_kopr
     c                   endif
      *
     c                   endif
      *
      * Innkjøpspris settes lik kostpris dersom innkjøpspris er 0
      *
     c                   if        p_inpr = 0
     c                   eval      p_inpr = p_kopr
     c                   endif
      *
      * Henter omregningsfaktor volum for enhet i forhold til basisenhet
      *
     c                   eval      vvenl1_vare = p_vare
     c                   eval      vvenl1_enhe = p_enhe
     c     vvenl1_key    chain     vvenl1                             90
     c                   if        *in90 = *off and
     c                             veomre <> 0
     c                   eval      w_omre = veomre
     c                   eval      vvenl1_vare = p_vare
     c                   eval      vvenl1_enhe = vvenhe
     c     vvenl1_key    chain     vvenl1                             90
     c                   if        *in90 = *off and
     c                             veomre <> 0
     c                   eval(h)   p_omre = w_omre / veomre
     c                   endif
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   if        p_inlr <> *on
     c                   eval      *inlr = *on
     c                   endif
      *
     c                   return
      *----------------------------------------------------------------------------
6.20  * Henter priser med evt. valgt prisgruppe, evt. uten
6.20  **** koden er flyttet for å få en mer fleksibel kode ***
6.20  *----------------------------------------------------------------------------
6.20  *
6.20 c     hent_pris     begsr
6.20  *
6.20 c                   eval      b_pris = *off
6.20  *
6.20 c                   eval      vvprl1_vare = p_vare
6.20 c                   eval      vvprl1_prgr = p_prgr
6.20 c                   eval      vvprl1_enhe = p_enhe
6.20 c                   eval      vvprl1_lety = p_lety
6.20 c     vvprl1_key    setll     vvprl1
6.20 c     vvprl1_key    reade     vvprl1                                 90
6.20 c                   dow       *in90 = *off
6.20  *
6.20 c                   eval      b_pris = *on
6.20 c                   eval      p_sapr = vpsapr
6.20 c                   eval      p_bupr = vpbupr
6.20 c                   eval      p_kopr = vpkopr
6.20 c                   eval      p_inpr = vpinpr
6.20 c                   eval      p_pdat = vppdat
6.20  *
6.20 c                   if        w_dato >= vppdat
6.20 c                   leave
6.20 c                   endif
6.20  *
6.20 c     vvprl1_key    reade     vvprl1                                 90
6.20 c                   enddo
6.20  *
6.20  * Dersom pris ikke finnes for oppgitt leveringstype, hentes pris fra
6.20  * leveringstype 0
6.20  *
6.20 c                   if        b_pris = *off and
6.20 c                             p_lety <> 0
6.20  *
6.20 c                   eval      vvprl1_vare = p_vare
6.20 c                   eval      vvprl1_prgr = p_prgr
6.20 c                   eval      vvprl1_enhe = p_enhe
6.20 c                   eval      vvprl1_lety = 0
6.20 c     vvprl1_key    setll     vvprl1
6.20 c     vvprl1_key    reade     vvprl1                                 90
6.20 c                   dow       *in90 = *off
6.20  *
6.20 c                   eval      b_pris = *on
6.20 c                   eval      p_sapr = vpsapr
6.20 c                   eval      p_bupr = vpbupr
6.20 c                   eval      p_kopr = vpkopr
6.20 c                   eval      p_inpr = vpinpr
6.20 c                   eval      p_pdat = vppdat
6.20  *
6.20 c                   if        w_dato >= vppdat
6.20 c                   leave
6.20 c                   endif
6.20  *
6.20 c     vvprl1_key    reade     vvprl1                                 90
6.20 c                   enddo
6.20  *
6.20 c                   endif
6.20  *
6.20  * Dersom pris ikke finnes for oppgitt prisgruppe, hentes pris fra
6.20  * prisgruppe = blank
6.20  *
6.20 c                   if        b_pris = *off and
6.20 c                             p_prgr <> *blank
6.20 c                   eval      vvprl1_vare = p_vare
6.20 c                   eval      vvprl1_prgr = *blank
6.20 c                   eval      vvprl1_enhe = p_enhe
6.20 c                   eval      vvprl1_lety = p_lety
6.20 c     vvprl1_key    setll     vvprl1
6.20 c     vvprl1_key    reade     vvprl1                                 90
6.20 c                   dow       *in90 = *off
6.20  *
6.20 c                   eval      b_pris = *on
6.20 c                   eval      p_sapr = vpsapr
6.20 c                   eval      p_bupr = vpbupr
6.20 c                   eval      p_kopr = vpkopr
6.20 c                   eval      p_inpr = vpinpr
6.20 c                   eval      p_pdat = vppdat
6.20  *
6.20 c                   if        w_dato >= vppdat
6.20 c                   leave
6.20 c                   endif
6.20  *
6.20 c     vvprl1_key    reade     vvprl1                                 90
6.20 c                   enddo
6.20  *
6.20  * Dersom pris ikke finnes for oppgitt leveringstype, hentes pris fra
6.20  * leveringstype 0
6.20  *
6.20 c                   if        b_pris = *off and
6.20 c                             p_lety <> 0
6.20  *
6.20 c                   eval      vvprl1_vare = p_vare
6.20 c                   eval      vvprl1_prgr = *blank
6.20 c                   eval      vvprl1_enhe = p_enhe
6.20 c                   eval      vvprl1_lety = 0
6.20 c     vvprl1_key    setll     vvprl1
6.20 c     vvprl1_key    reade     vvprl1                                 90
6.20 c                   dow       *in90 = *off
6.20  *
6.20 c                   eval      b_pris = *on
6.20 c                   eval      p_sapr = vpsapr
6.20 c                   eval      p_bupr = vpbupr
6.20 c                   eval      p_kopr = vpkopr
6.20 c                   eval      p_inpr = vpinpr
6.20 c                   eval      p_pdat = vppdat
6.20  *
6.20 c                   if        w_dato >= vppdat
6.20 c                   leave
6.20 c                   endif
6.20  *
6.20 c     vvprl1_key    reade     vvprl1                                 90
6.20 c                   enddo
6.20  *
6.20 c                   endif
6.20  *
6.20 c                   endif
6.20  *
6.20 c                   endsr
6.20  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
5.70 c                   parm                    p_prgr
     c                   parm                    p_enhe
     c                   parm                    p_lety
     c                   parm                    p_dato
     c                   parm                    p_inlr
     c                   parm                    p_sapr
     c                   parm                    p_bupr
     c                   parm                    p_kopr
     c                   parm                    p_inpr
     c                   parm                    p_pdat
     c                   parm                    p_omre
      *
      * Key for les på vare-pris
      *
     c     vvprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvprl1_vare
6.20 c                   kfld                    vvprl1_ldor
5.70 c                   kfld                    vvprl1_prgr
     c                   kfld                    vvprl1_enhe
     c                   kfld                    vvprl1_lety
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på vare-enhet
      *
     c     vvenl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl1_vare
     c                   kfld                    vvenl1_enhe
      *
     c                   endsr
