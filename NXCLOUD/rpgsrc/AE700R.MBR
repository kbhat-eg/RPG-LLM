      /TITLE  ASPGPL Utkø/Enheter, Leser definert fil
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASPGPL
      * Program . . . . : AE700R
      * Beskrivelse . . : Utkø/Enheter, Leser definert fil
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       070300 AMD  Nytt program
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     faenhl1    if a e           k disk    rename(aenhpfr:aenhl1r)
      *
      * Definering av parametere
      *
      *
      * Definering av variable i nøkler
      *
     d aenhl1_prid     s                   like(aeprid)
      *
      * Definering av variable
      *
     d w_prid          s                   like(aeprid)
     d w_qmsg          s             10
     d w_form          s             10
     d w_tilk          s                   like(aetilk)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser definerte utkøer/enheter
      *
     c                   eval      aenhl1_prid = *blank
     c     aenhl1_key    setll     aenhl1
     c                   read      aenhl1                                 90
      *
     c                   dow       *in90  = *off
      *
     c                   if        aestrt = 1
     c                   exsr      call_pgm
     c                   endif
      *
     c                   read      aenhl1                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Kaller program for start/stop funksjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     call_pgm      begsr
      *
     c                   eval      w_prid = aeprid
     c                   eval      w_tilk = aetilk
      *
     c                   if        aeqmsg = 1
     c                   eval      w_qmsg = '*INQMSG   '
     c                   else
     c                   eval      w_qmsg = '*NOMSG    '
     c                   endif
      *
     c                   if        aeform = *blank
     c                   eval      w_form = '*ALL      '
     c                   else
     c                   eval      w_form = aeform
     c                   endif
      *
      * Kaller program
      *
     c                   call      'AE702C'
     c                   parm                    w_prid
     c                   parm                    w_qmsg
     c                   parm                    w_form
     c                   parm                    w_tilk
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for utkø/enhets-register
      *
     c     aenhl1_key    klist
     c                   kfld                    aenhl1_prid
      *
     c                   endsr
