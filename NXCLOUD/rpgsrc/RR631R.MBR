     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOKON
      * Program . . . . : RR631R
      * Beskrivelse . . : Utlisting av resultatrapporter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
T5.00 * R5.00 240203 HFL  Endret layout (Bærer utvedet til 4 pos.)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frrf0l1    if   e           k disk    rename(rrf0pfr:rrf0l1r)
     frrf5l1    if   e           k disk    rename(rrf5pfr:rrf5l1r)
     frrf8l2    if   e           k disk    rename(rrf8pfr:rrf8l2r)
      *
     frr631p    o    e             printer oflind(*in30)
      *
     d rrf0l1_firm     s                   like(rr0fir)
     d rrf0l1_rapp     s                   like(rr0rap)
      *
     d rrf5l1_firm     s                   like(rr5fir)
     d rrf5l1_rapp     s                   like(rr5rap)
      *
     d rrf8l2_firm     s                   like(rr8fir)
     d rrf8l2_rapp     s                   like(rr8rap)
     d rrf8l2_linj     s                   like(rr8lin)
      *
      * Definering av LDA
      *
     d                uds
     D l_wsid                901    910
     D l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     D w_timestamp     s             12  0
     D w_date          s              6  0
      *
     d p_rapp          s                   like(rr0rap)
      *
      * Leser rapport record 0
      *
     c                   eval      rrf0l1_firm = l_firm
     c                   eval      rrf0l1_rapp = p_rapp
      *
     c     rrf0l1_key    setll     rrf0l1r                                90
     c     rrf0l1_key    reade     rrf0l1r                                90
      *
     c                   dow       *in90 = *off
      *
     c                   if        p_rapp = 00 or
     c                             p_rapp = rr0rap
      *
     c                   eval      a1firm = l_firm
     c                   eval      a1navn = l_fnav
     c                   eval      a1user = l_user
     c                   eval      a1date = w_date
     c                   eval      a1wsid = l_wsid
     c                   eval      a1rapp = rr0rap
     c                   eval      a1text = rr0txt
      *
     c                   exsr      heading
      *
     c                   eval      *in91 = *off
     c                   exsr      linje
     c                   endif
      *
     c     rrf0l1_key    reade     rrf0l1r                                90
      *
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for heading
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     heading       begsr
      *
     c                   write     a1hdr
      *
     c                   endsr
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for linjer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     linje         begsr
      *
      * Leser rapport record 5
      *
     c                   eval      rrf5l1_firm = l_firm
     c                   eval      rrf5l1_rapp = rr0rap
      *
     c     rrf5l1_key    setll     rrf5l1r                                91
     c     rrf5l1_key    reade     rrf5l1r                                91
      *
     c                   dow       *in91 = *off
      *
     c                   eval      b1linj = rr5lin
     c                   eval      b1text = rr5txt
     c                   eval      b1kode = rr5kod
     c                   eval      b1saml = rr5sln
     c                   eval      b1null = rr5nul
     c                   eval      b1antb = rr5abl
      *
     c                   if        rr5kod = 'K' or
     c                             rr5kod = 'L'
     c                   eval      *in31 = *off
     c                   else
     c                   eval      *in31 = *on
     c                   endif
      *
     c                   write     b1lin
      *
     c                   if        *in30 = *on
     c                   exsr      heading
     c                   eval      *in30 = *off
     c                   endif
      *
     c***                eval      *in92 = *off
     c                   exsr      konto
      *
     c     rrf5l1_key    reade     rrf5l1r                                91
     c                   enddo
      *
     c                   endsr
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for konti
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     konto         begsr
      *
      * Leser rapport record 8
      *
     c                   eval      rrf8l2_firm = l_firm
     c                   eval      rrf8l2_rapp = rr5rap
     c                   eval      rrf8l2_linj = rr5lin
      *
     c     rrf8l2_key    setll     rrf8l2r                                92
     c     rrf8l2_key    reade     rrf8l2r                                92
      *
     c                   dow       *in92 = *off
      *
     c                   eval      c1sekv = rr8lØp
     c                   eval      c1konf = rr8fkt
     c                   eval      c1spef = rr8fsp
     c                   eval      c1avdf = rr8fav
     c                   eval      c1kont = rr8tkt
     c                   eval      c1spet = rr8tsp
     c                   eval      c1avdt = rr8tav
     c                   eval      c1koli = rr8klf
     c                   eval      c1flin = rr8lif
     c                   eval      c1mots = rr8mot
     c                   eval      c1pros = rr8pro
      *
     c                   write     c1kon
      *
     c                   if        *in30 = *on
     c                   exsr      heading
     c                   eval      *in30 = *off
     c                   endif
      *
     c     rrf8l2_key    reade     rrf8l2r                                92
     c                   enddo
      *
     c                   endsr
      *
     c/eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Paramtre
      *
     c     *entry        plist
     c                   parm                    p_rapp
      *
     C                   time                    w_timestamp
     C                   move      w_timestamp   w_date
      *
      *
      * Key for rapportregister 0
      *
     c     rrf0l1_key    klist
     c                   kfld                    rrf0l1_firm
      *
      * Key for rapportregister 5
      *
     c     rrf5l1_key    klist
     c                   kfld                    rrf5l1_firm
     c                   kfld                    rrf5l1_rapp
      *
      * Key for rapportregister 8
      *
     c     rrf8l2_key    klist
     c                   kfld                    rrf8l2_firm
     c                   kfld                    rrf8l2_rapp
     c                   kfld                    rrf8l2_linj
      *
     c                   endsr
