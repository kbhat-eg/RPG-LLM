     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VV787R
      * Beskrivelse . . : Vare, sletting i annen filgruppe - styreprogram
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       191199 HKO  Nytt program
      *       100402 HKO  Lagt inn behandling av nye parametre
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     facpll1    if   e           k disk    rename(acplpfr:acpll1r)
      *
      * Datastruktur for parameterliste -inn/-ut
      *
     d                 ds
     d d_list                       260
     d  d_fgrp                        3    overlay(d_list:1)
     d  d_firm                        3  0 overlay(d_list:4)
      *
      * Datastruktur for parameterliste -ut
      *
     d                 ds
     d d_list2                        8
     d  d_fgrt                        3    overlay(d_list2:1)
     d  d_firt                        3  0 overlay(d_list2:4)
      *
      * Definering av parametre
      *
     d p_list          s            260
      *
      * Definering av variable i nøkler
      *
     d acpll1_lfgr     s                   like(aclfgr)
     d acpll1_lfir     s                   like(aclfir)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser filkopiering-linje-register og kaller program for alle
      * filgrupper/firmaer det skal slettes i (kopieres til)
      *
     c                   eval      acpll1_lfgr = d_fgrp
     c                   eval      acpll1_lfir = d_firm
     c     acpll1_key    setll     acpll1
     c     acpll1_key    reade     acpll1                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      d_fgrt = aclfgt
     c                   eval      d_firt = aclfit
      *
     c                   call      'VV788C'
     c                   parm                    d_list
     c                   parm                    d_list2
      *
     c     acpll1_key    reade     acpll1                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   move      *on           *inlr
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for les på filkopiering-linje-register
      *
     c     acpll1_key    klist
     c                   kfld                    acpll1_lfgr
     c                   kfld                    acpll1_lfir
      *
      *
      * Kopiering av parameterliste til datastruktur
      *
     c                   eval      d_list = p_list
      *
     c                   endsr
