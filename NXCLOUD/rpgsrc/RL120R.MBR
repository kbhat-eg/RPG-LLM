     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : NXCLOUD
      * Program . . . . : RL120R
      * Beskrivelse . . : Låste valutakoder pr. leverandør
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       060121 ASK  Nytt program
8.01  * 800   150621 ask  Lagt inn "reade" i bygging av subfile
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fra60i2    if   e           k disk    rename(ra60st:ra60i2r)
     fra60ir    if   e           k disk    rename(ra60st:ra60irr)
     fra60iu    uf a e           k disk    rename(ra60st:ra60iur)
      *
     frl120d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av parametre
      *
     d p_vlev          s                   like(rlvlev)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d ra60i2_vlev     s                   like(rlvlev)
     d ra60ir_vuid     s                   like(rlvuid)
     d ra60iu_vuid     s                   like(rlvuid)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
      *
     d w_firm          s                   like(rlvfir)
     d w_uid           s                   like(rlvuid)
     d w_vfda          s                   like(rlvfda)
     d w_vtda          s                   like(rlvfda)
     d s_vfda          s              6  0
     d s_vtda          s              6  0
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   eval      b_oppr = *on
     c                   exsr      xc2bld
     c                   exsr      forny
     c                   eval      b_oppr = *off
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   eval      ra60i2_vlev = b2ldor
     c     ra60i2_key    setll     ra60i2
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      ra60ir_vuid = b1uid
     c     ra60ir_key    chain     ra60ir
      *
     c                   if        %found and
     c                             b_oppr = *off
     c                   eval      c2vako = rlvval
     c                   eval      c2vatx = b1valt
      *
     c                   if        rlvfda = *loval
     c                   move      *zeros        c2vfda
     c                   else
     c     *dmy          move      rlvfda        c2vfda
     c     *dmy          move      rlvfda        s_vfda
     c                   endif
      *
     c                   if        rlvtda = *loval
     c                   move      *zeros        c2vtda
     c                   else
     c     *dmy          move      rlvtda        c2vtda
     c     *dmy          move      rlvtda        s_vtda
     c                   endif
      *
     c                   eval      c2vkur = rlvkur
     c                   eval      c2eusr = rlveus
     c     *dmy          move      rlveda        c2edat
     c                   else
     c                   eval      c2vako = b2vako
     c                   eval      c2vatx = b2vatx
     c                   eval      c2vtda = 0
     c                   eval      c2vfda = 0
     c                   eval      c2vkur = 0
     c                   eval      c2eusr = *blank
     c                   eval      s_vfda = 0
     c                   eval      s_vtda = 0
     c                   eval      c2edat = 0
     c                   eval      b1uid  = 0
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2vfda = 0 or
     c                             c2vtda = 0
     c                   exsr      xc2msg1
     c                   goto      c2taga
     c                   endif
      *
     c                   if        c2vfda <> s_vfda or
     c                             c2vtda <> s_vtda
      *
      * Sjekk om fra dato går inn annen period
      *
     c                   eval      w_vfda = %date(c2vfda:*dmy)
     c/exec sql
     c+ select rlvuid
     c+ into   :w_uid
     c+ from   ra60st
     c+ where  :w_vfda between rlvfda and rlvtda
     c+         and :p_vlev = rlvlev and :b1uid <> rlvuid
     c/end-exec
     c                   if        sqlcod = 0
     c                   exsr      xc2msg2
     c                   goto      c2taga
     c                   endif
      *
      * Sjekk om til dato går inn annen period
      *
     c                   eval      w_vtda = %date(c2vtda:*dmy)
     c/exec sql
     c+ select rlvuid
     c+ into   :w_uid
     c+ from   ra60st
     c+ where  :w_vtda between rlvfda and rlvtda
     c+         and :p_vlev = rlvlev and :b1uid <> rlvuid
     c/end-exec
     c                   if        sqlcod = 0
     c                   exsr      xc2msg2
     c                   goto      c2taga
     c                   endif
      *
      * Sjekk om periode overlapper en annen periode
      *
     c/exec sql
     c+ select rlvuid
     c+ into   :w_uid
     c+ from   ra60st
     c+ where  rlvfda between :w_vfda and :w_vtda
     c+    and rlvtda between :w_vfda and :w_vtda
     c+    and :p_vlev = rlvlev and :b1uid <> rlvuid
     c/end-exec
     c                   if        sqlcod = 0
     c                   exsr      xc2msg2
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
      * Sjekk om valutakurs er utfylt
      *
     c                   if        c2vkur = 0
     c                   exsr      xc2msg4
     c                   goto      c2taga
     c                   endif
      *
      * Oppdatering
      *
     c                   eval      ra60iu_vuid = b1uid
     c     ra60iu_key    chain     ra60iur
      *
     c                   eval      rlvfda = %date(c2vfda:*dmy)
     c                   eval      rlvtda = %date(c2vtda:*dmy)
     c                   eval      rlvkur = c2vkur                                           .
      *
     c                   time                    rlveti
     c                   time                    rlveda
     c                   eval      rlveus = l_user
      *
     c                   if        %found and
     c                             b_oppr = *off
     c                   update    ra60iur
     c                   else
     c                   eval      rlvfir = w_firm
     c                   eval      rlvlev = p_vlev
     c                   eval      rlvval = b2vako
     c                   write     ra60iur
     c                   endif
      *
     c                   eval      b_forn = *on
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av feilmelding
      * Dato ikke utfylt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2msg1       begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c2msg1
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av feilmelding
      * Dato overlapper annen periode
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2msg2       begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c2msg2
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av feilmelding
      * Valutakurs er ikke utfylt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2msg4       begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c2msg4
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      ra60ir_vuid = b1uid
     c     ra60ir_key    chain     ra60ir
     c                   if        %found
     c                   eval      d1vako = rlvval
     c                   eval      d1vatx = b1valt
      *
     c                   if        rlvfda = *loval
     c                   move      *zeros        d1vfda
     c                   else
     c     *dmy          move      rlvfda        d1vfda
     c                   endif
      *
     c                   if        rlvtda = *loval
     c                   move      *zeros        d1vtda
     c                   else
     c     *dmy          move      rlvtda        d1vtda
     c                   endif
     c                   eval      d1vkur = rlvkur
     c                   endif
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
      * Sletter rad
      *
     c                   eval      ra60iu_vuid = b1uid
     c     ra60iu_key    chain     ra60iu
      *
     c                   if        %found
     c                   delete    ra60iu
     c                   endif
      *
     c                   eval      b_forn = *on
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
8.01 c     ra60i2_key    reade     ra60i2
      *
     c                   if        %eof or
     c                             rlvfir <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1uid  = rlvuid
     c                   eval      b1valk = rlvval
     c                   eval      b1valt = '    '
     c     *dmy          move      rlvfda        b1vfda
     c     *dmy          move      rlvtda        b1vtda
     c                   eval      b1kurs = rlvkur
      *
     c/exec sql
     c+ select rapmna
     c+ into   :b1valt
     c+ from   ra16pf
     c+ where  rapfir = :w_firm and rapkod = :b1valk
     c/end-exec
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     ra60i2_key    setgt     ra60i2
     c                   readp     ra60i2
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     ra60i2
      *
     c                   if        %eof or
     c                             rlvfir <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   enddo
      *
     c                   if        %eof
     c     ra60i2_key    setll     ra60i2
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_vlev
      *
      * Key for posisjonering på leverandør
      *
     c     ra60i2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra60i2_vlev
      *
      * Key for les på valutapost
      *
     c     ra60ir_key    klist
     c                   kfld                    ra60ir_vuid
      *
      * Key for oppdatering av valutapost
      *
     c     ra60iu_key    klist
     c                   kfld                    ra60iu_vuid
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Henter informasjon om leverandør og valuta
      *
     c                   eval      b2ldor = p_vlev
     c/exec sql
     c+ select rlnavn, rlvalk
     c+ into   :b2lnav, :b2vako
     c+ from   rlevpf
     c+ where  rlfirm = :w_firm and rllevr = :p_vlev
     c/end-exec

     c/exec sql
     c+ select rapmna, rapmku
     c+ into   :b2vatx, :b2kurs
     c+ from   ra16pf
     c+ where  rapfir = :w_firm and rapkod = :b2vako
     c/end-exec
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      ra60i2_vlev = p_vlev
     c     ra60i2_key    setll     ra60i2
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
