     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : VT592R
      * Beskrivelse . . : Skal finne 1 tilbudspris, skal velge den som
      *                   ligger nærmest fra dato og tildato
      *
      * Spesialversjon. :  Skal hente innkjøpsperiode og innkjøpspris fra VTILPF
      *                    + tilleggstabell
      * Tilpasset for . :  Spesialitet
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       210217 bof  Nytt program (tatt utg.punkt i JM592R)
6.31  * spes  310519 bof  Hente innkjøpsperiode og innkjøpspris fra VTILPF
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skvdrmindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvtillr    if   e           k disk    rename(vtilpfr:vtillrr)
     fvti2lr    if   e           k disk    rename(vti2pfr:vti2lrr)
6.31 f                                     usropn
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vtfirm)
     d p_vare          s                   like(vtvare)
6.20 d p_enhe          s                   like(vtenh1)
6.20 d p_inpr          s                   like(vtinp1)
     d p_inlr          s              1
     d b_funn          s              1    inz(*off)
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
6.31 d l_filg                931    933
      *
      *
      * Definering av variable i nøkler
      *
     d w_firm          s                   like(vtfirm)
6.20 d w_vare          s                   like(vtvare)
6.20 d w_dato          s                   like(vtedat)
6.20 d w_loval         s                   like(vtedat)
6.20 d w_antt          s              4  0
6.20 d w_inpr          s                   like(vtinp1)
6.20 d w_enhe          s                   like(vtenh1)
6.20 d w_vidf          s                   like(vtvidf)
     d w_prgr          s                   like(vtprgr)
     d w_lety          s                   like(vtlety)
     d w_fdat          s                   like(vtfdat)
     d w_ftim          s                   like(vtftim)
     d w_tdat          s                   like(vttdat)
     d w_ttim          s                   like(vtttim)

     d w_vidt          s                   like(vtvidt)
      *
     d vtillr_firm     s                   like(vtfirm)
     d vtillr_ldor     s                   like(vtldor)
     d vtillr_ogrp     s                   like(vtogrp)
     d vtillr_hgrp     s                   like(vthgrp)
     d vtillr_ugrp     s                   like(vtugrp)
     d vtillr_vare     s                   like(vtvare)
     d vtillr_prgr     s                   like(vtprgr)
     d vtillr_lety     s                   like(vtlety)
     d vtillr_fdat     s                   like(vtfdat)
     d vtillr_ftim     s                   like(vtftim)
     d vtillr_tdat     s                   like(vttdat)
     d vtillr_ttim     s                   like(vtttim)
      *
6.31 d u_631           s              1    inz(*off)                            Endring 7.00
6.31  * Definering av eksterne prg
6.31  *
6.31   dcl-s co402_verdi1  char(1);
6.31   dcl-s co402_verdi2  char(2048);
6.31   DCL-PR CO402R EXTPGM('CO402R');
6.31     p_filgrp            char(3)     const;
6.31     p_firm              packed(3:0) const;
6.31     p_lager             packed(2:0) const;
6.31     p_nokkel            char(50)    const;
6.31     p_verdi1            char(1);
6.31     p_verdi2            char(2048);
6.31   END-PR;
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(10)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skvdrmbilder.
      * - - - - - - - - - - - - - - - - -
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO
6.3q C/End-Exec
      *
6.31 c                   if        u_631 = *on
6.31 c                   open      vti2lr
6.31 c                   endif
6.31 c                   if        u_631 = *off
6.31 c                   goto      avslutt
6.31 c                   endif

     c                   eval      w_firm = p_firm
     c                   eval      w_vare = p_vare
     c                   eval      p_inpr = 0
6.20 c                   eval      w_loval= *loval
     c                   time                    w_dato
     c                   eval      b_funn = *off
     c                   if        p_inlr <> *on
     c                   goto      avslutt
     c                   endif
      *
      * sjekk om vvarn finnes på flere kampanjer
      *
     c                   exsr      finn_inpr
      *
      * Avslutt program
      *
     c     avslutt       tag
6.31 c                   if        u_631 = *on
6.31 c                   close     vti2lr
6.31 c                   endif
      *
     c                   if        p_inlr <> *on
     c                   eval      *inlr = *on
     c                   endif
     c                   return
      *
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  * Subrutine for å finne innkjøpspris
6.31  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.31  *
6.31 c     finn_inpr     begsr
6.31  *
     c                   time                    w_dato
     c                   eval      w_loval = *loval
     c                   eval      w_vidf  = *loval
     c                   eval      w_inpr = 0
6.31  *
6.31 c/exec sql
6.31 c+                  declare kamp2 cursor for
     c+                  select vtfirm,
     c+                         vtvare,
     c+                         vtprgr,
     c+                         vtlety,
     c+                         vtfdat,
     c+                         vtftim,
     c+                         vttdat,
     c+                         vtttim,
     c+                         vtvidf,
     c+                         vtvidt
6.31 c+                  from   vti2pf
6.31 c+                  where  vtfirm = :p_firm and
6.31 c+                         vtvare = :p_vare and
6.31 c+                         vtvidf <> :w_loval and
6.31 c+                         vtvidt <> :w_loval and
6.31 c+                         vtvidf <= :w_dato  and
6.31 c+                         vtvidt >= :w_dato
6.31 c+                  for read only
6.31 c/end-exec
6.31  *
6.31 c/exec sql
6.31 c+ open kamp2
6.31 c/end-exec
      *
      * Fyller opp subfile
      *
     c                   dou       *in15 = *on
6.31  *
6.31 c/exec sql
     c+                  fetch next
     c+                  from  kamp2
     c+                  into  :vtfirm,
     c+                        :vtvare,
     c+                        :vtprgr,
6.32 c+                        :vtlety,
6.32 c+                        :vtfdat,
6.32 c+                        :vtftim,
6.32 c+                        :vttdat,
6.32 c+                        :vtttim,
6.32 c+                        :vtvidf,
6.32 c+                        :vtvidt
6.31 c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   if        *in15
     c                   leave
     c                   endif
      *
     c                   if        vtvidf > w_vidf
     c                   eval      w_firm = vtfirm
     c                   eval      w_vare = vtvare
     c                   eval      w_prgr = vtprgr
     c                   eval      w_lety = vtlety
     c                   eval      w_fdat = vtfdat
     c                   eval      w_ftim = vtftim
     c                   eval      w_tdat = vttdat
     c                   eval      w_ttim = vtttim
     c                   eval      w_vidf = vtvidf
     c                   eval      w_vidt = vtvidt
     c                   eval      b_funn = *on
     c                   endif
      *
     c                   enddo
      *
6.31 c/exec sql
6.31 c+ close kamp2
6.31 c/end-exec
6.31  *
     c                   if        b_funn  = *off
     c                   leavesr
     c                   endif
      * les tilbudsregister
     c                   eval      vtillr_firm = w_firm
     c                   eval      vtillr_ldor = 0
     c                   eval      vtillr_ogrp = 0
     c                   eval      vtillr_hgrp = 0
     c                   eval      vtillr_ugrp = 0
     c                   eval      vtillr_vare = w_vare
     c                   eval      vtillr_prgr = w_prgr
     c                   eval      vtillr_lety = w_lety
     c                   eval      vtillr_fdat = w_fdat
     c                   eval      vtillr_ftim = w_ftim
     c                   eval      vtillr_tdat = w_tdat
     c                   eval      vtillr_ttim = w_ttim
     c     vtillr_key    chain     vtillr
     c                   if        %found(vtillr)
     c                   select
     c                   when      p_enhe = vtenh1
     c                   eval      p_inpr = vtinp1
     c                   when      p_enhe = vtenh2
     c                   eval      p_inpr = vtinp2
     c                   when      p_enhe = vtenh3
     c                   eval      p_inpr = vtinp3
     c                   endsl
     c                   endif
6.31 c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
6.20 c                   parm                    p_vare
6.20 c                   parm                    p_enhe
     c                   parm                    p_inpr
     c                   parm                    p_inlr
      *
      * Key for oppslag på tilbudsregister
      *
     c     vtillr_key    klist
     c                   kfld                    vtillr_firm
     c                   kfld                    vtillr_ogrp
     c                   kfld                    vtillr_hgrp
     c                   kfld                    vtillr_ugrp
     c                   kfld                    vtillr_ldor
     c                   kfld                    vtillr_vare
     c                   kfld                    vtillr_prgr
     c                   kfld                    vtillr_lety
     c                   kfld                    vtillr_fdat
     c                   kfld                    vtillr_ftim
     c                   kfld                    vtillr_tdat
     c                   kfld                    vtillr_ttim

      *
      * Henter parametre
      *
      *
6.31  *
6.31  * Endring 6.31
6.31  *
6.31   CallP CO402R(l_filg:w_firm:0:'TILB_INNKJ_PERIOD':
6.31                    co402_verdi1:co402_verdi2);
6.31   if co402_verdi1 = '1';
6.31     u_631 = *on;
6.31   endif;
      *
     c                   endsr
