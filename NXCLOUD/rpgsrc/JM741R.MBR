     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASVADM
      * Program . . . . : JM740R
      * Beskrivelse . . : Kampanje-vare, oppdater fra NOBB
      *
7.01  * Spesialversjon. : Utvidet med prisgr. på VA kampanje
7.01  * Tilpasset for . : Mestergruppen
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       290408 bof  Nytt program
6.30  *       090714 bkv  Håndterer oppdatering (var slett og insert)
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fpcintpf   if   e           k disk
      *
     fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
     fjkahlu    uf a e           k disk    rename(jkahpfr:jkahlur)
     fjkavlu    uf a e           k disk    rename(jkavpfr:jkavlur)
      *
      * Definering av Local Data Area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Datastrukturer
      *
      * Definering av Norgros kampanje-recordformat
      *
     d                 ds
     d d_nrec                       256
     d  d_n_htyp                      2    overlay(d_nrec:1)   inz('1,')
     d  d_n_nobb                      8  0 overlay(d_nrec:3)
     d  d_n_fil1                      1    overlay(d_nrec:11)   inz(',')
     d  d_n_modn                      8    overlay(d_nrec:12)
     d  d_n_fil2                      1    overlay(d_nrec:20)  inz(',')
     d  d_n_vgrp                      7    overlay(d_nrec:21)
     d  d_n_fil3                      2    overlay(d_nrec:28)  inz(',"')
     d  d_n_tek1                     35    overlay(d_nrec:30)  inz(',')
     d  d_n_fil4                      3    overlay(d_nrec:65)  inz('","')
     d  d_n_tek2                     35    overlay(d_nrec:68)  inz(',')
      *
     d                 ds
     d d_vrec                       256
     d  d_v_vtyp                      2    overlay(d_vrec:1)   inz('3,')
     d  d_v_fil1                     19    overlay(d_vrec:3)
     d  d_v_vldo                      6  0 overlay(d_vrec:22)
     d  d_v_fil2                     94    overlay(d_vrec:28)
     d  d_v_vkai                      9  2 overlay(d_vrec:122)
     d  d_v_fil3                      1    overlay(d_vrec:131)
     d  d_v_ifda                      8  0 overlay(d_vrec:132)
     d  d_v_fil4                      1    overlay(d_vrec:140)
     d  d_v_itda                      8  0 overlay(d_vrec:141)
     d  d_v_fil5                      1    overlay(d_vrec:149)
     d  d_v_kpri                      9  2 overlay(d_vrec:150)
     d  d_v_fil6                      1    overlay(d_vrec:159)
     d  d_v_kfda                      8  0 overlay(d_vrec:160)
     d  d_v_fil7                      1    overlay(d_vrec:168)
     d  d_v_ktda                      8  0 overlay(d_vrec:169)
     d  d_v_fil8                      1    overlay(d_vrec:177)
     d  d_v_vkaa                      9  2 overlay(d_vrec:178)
     d  d_v_fil9                      1    overlay(d_vrec:187)
     d  d_v_efda                      8  0 overlay(d_vrec:188)
     d  d_v_fil10                     1    overlay(d_vrec:196)
     d  d_v_etda                      8  0 overlay(d_vrec:197)
      *
      * Definering av variable i nøkler
      *
     d jkahlu_kamp     s                   like(jmkamp)
     d jkavlu_vkam     s                   like(jmvkam)
7.01 d jkavlu_vprg     s                   like(jmvprg)
6.32 d jkavlu_vvar     s                   like(jmvvar)
     d jvarl3_nobb     s                   like(jvnobb)
      *
      * Definering av variable
      *
     d w_rect          s              1
     d w_dato_6        s              6  0
     d w_fdat          s              6  0
     d w_tdat          s              6  0
      *
     d w_firm          s                   like(jmfirm)
      *
     d b_nobb          s              1    inz(*off)
     d b_peri          s              1    inz(*off)
6.30 d b_ny            s              1    inz(*off)
      *
     d p_kamp          s                   like(jmkamp)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   exsr      hode
      * Leser kampanjefil
      *
     c                   read      pcintpf
     c                   dow       not %eof
      *
     c                   eval      w_rect = %subst(pcint:1:1)
      *
     c                   select
     c                   when      w_rect = '1'
     c                   exsr      nobb
     c                   when      w_rect = '3'
     c                   if        b_nobb = *on
     c                   exsr      varelinje
     c                   endif
     c                   eval      b_nobb = *off
     c                   endsl
      *
     c                   read      pcintpf
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av hoderecord
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode          begsr
      *
      * Sletter eventuell eksisterende kampanje på varenivå
      *
     c                   eval      jkahlu_kamp = p_kamp
     c     jkahlu_key    chain     jkahlur
     c                   if        %found
6.30 c                   eval      b_ny = *on
6.30 c                   eval      jmeusr = l_user
6.30 c                   eval      jmodat = *loval
6.30 c                   time                    jmedat
6.30 c                   time                    jmetim
      *
6.30 c                   update    jkahlur

      * hvis kampanje finnes fra før så slettes detaljene
6.30 c*                  eval      jkavlu_vkam = p_kamp
6.30 c*    jkavlu_key    setll     jkavlu
6.30 c*    jkavlu_key    reade     jkavlur
6.30 c*                  dow       not %eof
6.30 c*                  delete    jkavlur
6.30 c*    jkavlu_key    reade     jkavlur
6.30 c*                  enddo
6.30 c                   else
      *
      * Hvis kampanje ikke finnes fra før, så opprettes heading
      *
     c                   clear                   jkahlur
      *
     c                   eval      jmfirm = w_firm
     c                   eval      jmkamp = p_kamp
     c                   eval      jmbesk = 'Automatisk opprettet'
     c                   eval      jmansv = *blank
     c                   eval      jmodat = *loval
     c                   eval      jmeusr = l_user
      *
     c                   eval      jmfdat = *loval
     c                   eval      jmtdat = *loval
      *
6.30 c                   time                    jmooda
     c                   time                    jmedat
     c                   time                    jmetim
      *
     c                   write     jkahlur
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av varelinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     nobb          begsr
      *
     c                   eval      d_nrec = pcint
      *
     c                   eval      jvarl3_nobb = d_n_nobb
     c     jvarl3_key    chain     jvarl3
     c                   if        not %found
     c                   movel     d_n_nobb      jvvare
     c                   endif
      *
     c                   eval      b_nobb = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av varelinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     varelinje     begsr
      *
     c                   eval      d_vrec = pcint
      *
     c                   if        b_peri = *off
     c                   exsr      upd_kamp
     c                   endif
      *
      * Finner varenr på bakgrunn av nobbnr
6.30 c                   eval      jkavlu_vkam = p_kamp
7.01 c                   eval      jkavlu_vprg = *blank
6.30 c                   eval      jkavlu_vvar = jvvare
6.30 c     jkavlu_key    chain     jkavlur
6.30 c                   if        %found
6.30  * oppdaterer kun hvis ikke låst
6.30 c                   if        jmvlas = 0
6.30 c                   exsr      opd_vare
6.30 c                   endif
6.30 c                   else
6.30 c                   exsr      skr_vare
6.30 c                   endif

     c                   endsr


6.30  *
6.30  * Skriver til kampanje-vare
6.30  *
6.30 c     skr_vare      begsr
6.30  *
6.30 c                   clear                   jkavlur
      *
     c                   eval      jmvfir = w_firm
     c                   eval      jmvkam = p_kamp
7.01 c                   eval      jmvprg = *Blank
     c                   eval      jmvvar = jvvare
     c                   eval      jmvldo = d_v_vldo
     c                   eval      jmvkai = d_v_vkai
     c                   eval      jmvkpr = 0
     c                   eval      jmvkaa = d_v_vkaa
     c                   eval      jmvkau = 0
     c                   eval      jmvans = *blank
     c                   eval      jmveus = l_user
6.30 c                   eval      jmvlas = 0
6.30  *
6.30 c                   time                    jmvoda
6.30 c                   time                    jmveda
     c                   time                    jmveti
      *
     c                   write     jkavlur
      *
     c                   endsr
6.30  *
6.30  * Oppdater kampanje-vare
6.30  *
6.30 c     opd_vare      begsr
6.30  *
6.30  *
6.30 c                   eval      jmvfir = w_firm
6.30 c                   eval      jmvkam = p_kamp
6.30 c                   eval      jmvvar = jvvare
6.30 c                   eval      jmvldo = d_v_vldo
6.30 c                   eval      jmvkai = d_v_vkai
6.30 c                   eval      jmvkpr = 0
6.30 c                   eval      jmvkaa = d_v_vkaa
6.30 c                   eval      jmvkau = 0
6.30 c                   eval      jmvans = *blank
6.30 c                   eval      jmveus = l_user
6.30 c                   eval      jmvlas = 0
      *
6.32 c                   time                    jmvoda
6.30 c                   time                    jmveda
6.30 c                   time                    jmveti
      *
6.30 c                   update    jkavlur
      *
6.30 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å oppdatere kampanje hode med fra - til dato
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     upd_kamp      begsr
      *
     c                   move      d_v_kfda      w_dato_6
     c     *ymd          test(d)                 w_dato_6               33
     c                   if        *in33 = *on
     c                   leavesr
     c                   endif
     c                   eval      w_fdat = w_dato_6
     c                   move      d_v_ktda      w_dato_6
     c     *ymd          test(d)                 w_dato_6               33
     c                   if        *in33 = *on
     c                   leavesr
     c                   endif
     c                   eval      w_tdat = w_dato_6
      *
     c                   eval      jkahlu_kamp = p_kamp
     c     jkahlu_key    chain     jkahlur
     c                   if        %found(jkahlu)
     c     *ymd          move      w_fdat        jmfdat
     c     *ymd          move      w_tdat        jmtdat
     c                   update    jkahlur
     c                   eval      b_peri = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å initiere
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *
     c     *entry        plist
     c                   parm                    p_kamp
      *
      * Key for oppslag på vare
      *
     c     jvarl3_key    klist
     c                   kfld                    jvarl3_nobb
      *
      * Key for oppdatering av kampanje-hode
      *
     c     jkahlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jkahlu_kamp
      *
      * Key for sletting av kampanje-vare
      *
     c     jkavlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jkavlu_vkam
7.01 c                   kfld                    jkavlu_vprg
6.30 c                   kfld                    jkavlu_vvar
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
