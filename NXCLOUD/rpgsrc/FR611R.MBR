     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FR611R
      * Beskrivelse . . : Rabattmatrise, kundebrev - utplukk
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       130599 HKO  Nytt program
      *       200300 HKO  Lagt inn leverandør som nivå i rabattmatrisen
      *       180204 HKO  Erstattet rabattgruppe med varegruppe og modul
      *       130504 HKO  Lagt inn valg av sortiment
5.70  * PRGR  221008 bof  Utvidet med prisgruppe                       l
      * 6.10  250609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.11  191010 AMD  Prisgruppe manglet ved oppkall av FR612R
6.20  * 6.20  041110 bof  utvidet nøkkel vsdtpf/vshepf
6.21  * 6.20  211015 bof  Skal kun skrive ut lagervarer
6.31  * 6.30  300316 bof  Utvidet med prisgruppe i nøkkel
6.32  * 6.30  170918 bkv  Utvidet med trelast sortiment
7.01  * 7.00  251119 bsa  Skal hente prisgruppe fra kunde
8.01  *PRG2   100622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffpril1    if   e           k disk    rename(fpripfr:fpril1r)
     ffrabl1    if   e           k disk    rename(frabpfr:frabl1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     ffkprl1    if   e           k disk    rename(fkprpfr:fkprl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvsdtl1    if   e           k disk    rename(vsdtpfr:vsdtl1r)
     fvsdtl6    if   e           k disk    rename(vsdtpfr:vsdtl6r)
     ffprwlu    uf a e           k disk    rename(fprwpfr:fprwlur)
     ffrawlu    uf a e           k disk    rename(frawpfr:frawlur)
6.32 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
      * Datastruktur
      *
6.10  * Defineringa av LDA
6.10  *
6.10 d                uds
6.10 d l_user                911    920
6.20 d l_lage               1001   1002  0
      *
      *
      * Datastruktur for parameterliste -inn
      *
     d                 ds
     d d_list                       256
     d  d_firm                        3  0 overlay(d_list)
     d  d_rkat                        3  0 overlay(d_list:4)
     d  d_kund                        6  0 overlay(d_list:7)
     d  d_kpro                        6  0 overlay(d_list:13)
     d  d_vsor                       10    overlay(d_list:19)
     d  d_rdat                       10d   overlay(d_list:29) datfmt(*iso)
     d  d_txt1                       60    overlay(d_list:39)
     d  d_txt2                       60    overlay(d_list:99)
     d  d_txt3                       60    overlay(d_list:159)
     d  d_valg                        1  0 overlay(d_list:219)
8.01 d  d_prgr                        2    overlay(d_list:220)
      *
      * Definering av parametre
      *
     d p_parm          s            256
      *
      * Definering av variable i nøkler
      *
     d rkunl1_kund     s                   like(rkkund)
     d fkprl1_kund     s                   like(flkund)
     d fkprl1_kpro     s                   like(flkpro)
     d vvarl1_vare     s                   like(vvvare)
     d vsdtl1_vsor     s                   like(vdvsor)
6.20 d vsdtl1_lage     s                   like(vdlage)
     d vsdtl1_ldor     s                   like(vdldor)
     d vsdtl1_ogrp     s                   like(vdogrp)
     d vsdtl1_hgrp     s                   like(vdhgrp)
     d vsdtl1_ugrp     s                   like(vdugrp)
     d vsdtl1_modn     s                   like(vdmodn)
     d vsdtl1_vare     s                   like(vdvare)
     d vsdtl6_vsor     s                   like(vdvsor)
6.20 d vsdtl6_lage     s                   like(vdlage)
     d vsdtl6_vare     s                   like(vdvare)
      *
      * Definering av variable
      *
     d b_sort          s              1    inz(*off)
6.21 d b_inlr          s              1    inz(*off)
      *
     d w_valg          s              1  0
      *
     d w_firm          s                   like(frfirm)
     d w_kund          s                   like(frkund)
     d w_kpro          s                   like(frkpro)
     d w_vsor          s                   like(vdvsor)
     d w_ldor          s                   like(vvldor)
6.32 d w_lv_nobb       s                   like(vvlnob)
6.32 d w_lv_modn       s                   like(vvlmod)
6.32 d w_lv_lldo       s                   like(vvlnle)
6.32 d w_lv_llva       s                   like(vvllva)
     d w_ogrp          s                   like(vvogrp)
     d w_hgrp          s                   like(vvhgrp)
     d w_ugrp          s                   like(vvugrp)
     d w_modn          s                   like(vvnmod)
     d w_vare          s                   like(vvvare)
6.21 d w_lage          s                   like(vdlage)
6.21 d w_vtyp          s                   like(vvvtyp)
6.21 d w_udat          s               d   datfmt(*iso)
7.01 d w_pgav          s                   like(rkavde)
7.01 d w_pgla          s                   like(rklagr)
7.01 d w_prgr          s                   like(fpprgr)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser spesialpriser, og kopierer denne over til arbeidsfil
      * sammen med tilleggsopplysninger
      *
     c                   exsr      priser
      *
      * Leser rabattmatrisen, og kopierer denne over til arbeidsfil
      * sammen med tilleggsopplysninger
      *
     c                   exsr      rabatter
      *
      * Skriver kundebrev avhengig av utplukks-parametre
      *
     c                   select
     c                   when      d_rkat <> 0
     c                   exsr      utpl_rkat
     c                   when      d_kund <> 0 and
     c                             d_kpro = 0
     c                   exsr      utpl_kund
     c                   when      d_kund <> 0 and
     c                             d_kpro <> 0
     c                   eval      w_kund = d_kund
     c                   eval      w_kpro = d_kpro
     c                   eval      w_valg = 1
     c                   exsr      kundebrev
     c                   endsl
      *
      * Avslutter program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp arbeidsfil.
      * Alle spesialpriser kopieres til arbeidsfil, og kompletteres med
      * rabattkategori og kundekategori for kunde/kundeprosjekt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     priser        begsr
      *
     c     fpril1_key    setll     fpril1
     c     fpril1_key    reade     fpril1
     c                   dow       not %eof
5.70  *
5.70  * Hvis prisgruppe er utfylt
5.70  *
5.70 c                   if        d_prgr <> *blank and
5.70 c                             fpprgr <> d_prgr
5.70 c                   goto      neste_pris
5.70 c                   endif
      *
      * Sjekker om avtale inngår i valgt sortiment
      *
     c                   if        d_vsor <> *blank
     c                   eval      w_vare = fpvare
     c                   exsr      sortiment
     c                   if        b_sort = *off
     c                   goto      neste_pris
     c                   endif
     c                   endif
6.21  *
6.21 c                   eval      w_vare = fpvare
6.21 c                   eval      w_lage = l_lage
6.21 c                   exsr      hent_vtyp
6.21 c                   if        w_vtyp <> 'L'
5.21 c                   goto      neste_pris
5.21 c                   endif
      *
      * Behandling av kunde/kundeprosjekt
      *
     c                   if        fpkund <> 0 or
     c                             fpkpro <> 0
      *
     c                   eval      fprkat = 0
     c                   eval      fpkkat = 0
      *
      * Henter eventuell rabattkategori fra kundeprosjekt
      *
     c                   if        fpkpro <> 0
     c                   eval      fkprl1_kund = fpkund
     c                   eval      fkprl1_kpro = fpkpro
     c     fkprl1_key    chain     fkprl1
     c                   if        %found and
     c                             florka <> 0
     c                   eval      fprkat = florka
     c                   endif
     c                   endif
      *
      * Henter rabattkategori og kundekategori fra kunde
      *
     c                   eval      rkunl1_kund = fpkund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   if        fprkat = 0
     c                   eval      fprkat = rkrabk
     c                   endif
     c                   eval      fpkkat = rkkatg
     c                   endif
      *
     c                   endif
7.01  *
7.01  * Hvis prisgruppe er utfylt
7.01  *
7.01 c                   if        d_prgr <> *blank and
7.01 c                             fpprgr <> d_prgr
7.01 c                   goto      neste_pris
7.01 c                   endif
      *
      * Henter varetekst
      *
     c                   eval      fptek1 = *blank
      *
     c                   eval      vvarl1_vare = fpvare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      fptek1 = vvtek1
     c                   endif
      *
6.10 c                   time                    fpodat
6.10 c                   time                    fpotim
6.10 c                   eval      fpousr = l_user
6.10 c                   time                    fpedat
6.10 c                   time                    fpetim
6.10 c                   eval      fpeusr = l_user
     c                   write     fprwlur
      *
     c     neste_pris    tag
      *
     c     fpril1_key    reade     fpril1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp arbeidsfil.
      * Hele rabattmatrisen kopieres til arbeidsfil, og kompletteres med
      * rabattkategori og kundekategori for kunde/kundeprosjekt og med
      * varegruppe, leverandør og modul for vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     rabatter      begsr
      *
     c     frabl1_key    setll     frabl1
     c     frabl1_key    reade     frabl1
     c                   dow       not %eof
5.70  *
5.70  * Hvis prisgruppe er utfylt
5.70  *
5.70 c                   if        d_prgr <> *blank and
5.70 c                             frprgr <> d_prgr
5.70 c                   goto      neste_rabatt
5.70 c                   endif
      *
      * Sjekker om avtale inngår i valgt sortiment
      *
     c                   if        d_vsor <> *blank
     c                   eval      w_ogrp = frogrp
     c                   eval      w_hgrp = frhgrp
     c                   eval      w_ugrp = frugrp
     c                   eval      w_ldor = frldor
     c                   eval      w_modn = frmodn
     c                   eval      w_vare = frvare
     c                   exsr      sortiment
     c                   if        b_sort = *off
     c                   goto      neste_rabatt
     c                   endif
     c                   endif
      *
      * Behandling av kunde/kundeprosjekt
      *
     c                   if        frkund <> 0 or
     c                             frkpro <> 0
      *
     c                   eval      frrkat = 0
     c                   eval      frkkat = 0
      *
      * Henter eventuell rabattkategori fra kundeprosjekt
      *
     c                   if        frkpro <> 0
     c                   eval      fkprl1_kund = frkund
     c                   eval      fkprl1_kpro = frkpro
     c     fkprl1_key    chain     fkprl1
     c                   if        %found and
     c                             florka <> 0
     c                   eval      frrkat = florka
     c                   endif
     c                   endif
      *
      * Henter rabattkategori og kundekategori fra kunde
      *
     c                   eval      rkunl1_kund = frkund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found
     c                   if        frrkat = 0
     c                   eval      frrkat = rkrabk
     c                   endif
     c                   eval      frkkat = rkkatg
     c                   endif
7.01  *
7.01  * hvis kunde har lager, så hent prisgruppe herfra
7.01  *
7.01 c                   if        rklagr <> 0
7.01 c                   exsr      hent_prisgr
7.01 c                   eval      d_prgr = w_prgr
7.01 c                   endif
      *
     c                   endif
5.70  *
5.70  * Hvis prisgruppe er utfylt
5.70  *
5.70 c                   if        d_prgr <> *blank and
5.70 c                             frprgr <> d_prgr
5.70 c                   goto      neste_rabatt
5.70 c                   endif
      *
      * Henter varegruppe, leverandør, modul og varetekst fra vare
      *
     c                   eval      frtek1 = *blank
      *
     c                   if        frvare <> *blank
6.21  *
6.21 c                   eval      w_vare = frvare
6.21 c                   eval      w_lage = l_lage
6.21 c                   exsr      hent_vtyp
6.21 c                   if        w_vtyp <> 'L'
5.21 c                   goto      neste_rabatt
5.21 c                   endif
      *
     c                   eval      frogrp = 0
     c                   eval      frhgrp = 0
     c                   eval      frugrp = 0
     c                   eval      frldor = 0
     c                   eval      frmodn = 0
      *
     c                   eval      vvarl1_vare = frvare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      frogrp = vvogrp
     c                   eval      frhgrp = vvhgrp
     c                   eval      frugrp = vvugrp
     c                   eval      frldor = vvldor
6.32 c                   if        w_lv_nobb > 0
6.32 c                   eval      frmodn = w_lv_modn
6.32 c                   else
     c                   eval      frmodn = vvnmod
6.32 c                   endif
     c                   eval      frtek1 = vvtek1
     c                   endif
      *
     c                   endif
      *
6.10 c                   time                    frodat
6.10 c                   time                    frotim
6.10 c                   eval      frousr = l_user
6.10 c                   time                    fredat
6.10 c                   time                    fretim
6.10 c                   eval      freusr = l_user
     c                   write     frawlur
      *
     c     neste_rabatt  tag
      *
     c     frabl1_key    reade     frabl1
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av utplukk på rabattkategori
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utpl_rkat     begsr
      *
      * Leser kunderegister på valgt rabattkategori
      *
     c     rkunl1_key2   setll     rkunl1
     c     rkunl1_key2   reade     rkunl1                                 90
     c                   dow       *in90 = *off
      *
     c                   if        rkrabk = d_rkat
     c                   eval      d_kund = rkkund
     c                   exsr      utpl_kund
     c                   endif
      *
     c     rkunl1_key2   reade     rkunl1                                 90
     c                   enddo
      *
      * Leser kundeprosjekt som er overstyrt mot valgt rabattkategori.
      * Disse behandles under forutsetning av at kunden ikke tilhører valgt
      * rabattkategori, og at prosjektet er aktivt
      *
     c     fkprl1_key3   setll     fkprl1
     c     fkprl1_key3   reade     fkprl1                                 90
     c                   dow       *in90 = *off
      *
     c                   if        florka = d_rkat and
     c                             flokun = 0 and
     c                             flokpr = 0 and
     c                             flfdat <= d_rdat and
     c                             fltdat >= d_rdat
     c                   eval      w_kund = flkund
     c                   eval      w_kpro = flkpro
     c                   eval      w_valg = d_valg
     c                   exsr      kundebrev
     c                   endif
      *
     c     fkprl1_key3   reade     fkprl1                                 90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av utplukk på kunde
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     utpl_kund     begsr
      *
      * Skriver kundebrev for kunde
      *
     c                   eval      w_kund = d_kund
     c                   eval      w_kpro = 0
     c                   eval      w_valg = 1
      *
     c                   exsr      kundebrev
      *
      * Leser kundeprosjekt tilhørende kunde
      * Disse behandles under forutsetning av at prosjektet er aktivt
      *
     c                   eval      fkprl1_kund = d_kund
     c     fkprl1_key2   setll     fkprl1
     c     fkprl1_key2   reade     fkprl1                                 90
     c                   dow       *in90 = *off
      *
     c                   if        flfdat <= d_rdat and
     c                             fltdat >= d_rdat
     c                   eval      w_kund = flkund
     c                   eval      w_kpro = flkpro
     c                   eval      w_valg = d_valg
     c                   exsr      kundebrev
     c                   endif
      *
     c     fkprl1_key2   reade     fkprl1                                 90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke om linje inngår i valgt sortiment
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sortiment     begsr
      *
     c                   eval      b_sort = *off
      *
      * Sjekker om vare finnes, og henter vareinformasjon
      *
     c                   if        w_vare <> *blank
      *
     c                   eval      vvarl1_vare = w_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   leavesr
     c                   endif
      *
6.32 c                   exsr      hent_vtyp
      *
     c                   eval      w_ldor = vvldor
     c                   eval      w_ogrp = vvogrp
     c                   eval      w_hgrp = vvhgrp
     c                   eval      w_ugrp = vvugrp
6.32 c                   if        w_lv_nobb > 0
6.32 c                   eval      w_modn = w_lv_modn
6.32 c                   else
     c                   eval      w_modn = vvnmod
6.32 c                   endif
      *
     c                   endif
      *
      * Sjekker om avtale inngår i sortimentet
      *
     c                   if        w_vare <> *blank
6.20  * først les med lager, så uten
6.20 c                   eval      vsdtl6_lage = l_lage
     c                   eval      vsdtl6_vare = w_vare
     c     vsdtl6_key    chain     vsdtl6
     c                   if        %found
     c                   eval      b_sort = *on
     c                   leavesr
6.20 c                   else
6.20 c                   eval      vsdtl6_lage = 0
6.20 c                   eval      vsdtl6_vare = w_vare
6.20 c     vsdtl6_key    chain     vsdtl6
6.20 c                   if        %found
6.20 c                   eval      b_sort = *on
6.20 c                   leavesr
6.20 c                   endif
     c                   endif
     c                   endif
6.20  * først les med lager, så uten
6.20 c                   eval      vsdtl1_lage = l_lage
     c                   eval      vsdtl1_ldor = w_ldor
     c                   eval      vsdtl1_ogrp = w_ogrp
     c                   eval      vsdtl1_hgrp = w_hgrp
     c                   eval      vsdtl1_ugrp = w_ugrp
     c                   eval      vsdtl1_modn = w_modn
     c                   eval      vsdtl1_vare = *blank
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found
     c                   eval      vsdtl1_modn = 0
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found
     c                   eval      vsdtl1_ugrp = 0
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found
     c                   eval      vsdtl1_hgrp = 0
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found
     c                   eval      vsdtl1_ogrp = 0
     c     vsdtl1_key    chain     vsdtl1
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   if        not %found and
     c                             w_ldor <> 0
     c                   eval      vsdtl1_ldor = 0
     c                   eval      vsdtl1_ogrp = w_ogrp
     c                   eval      vsdtl1_hgrp = w_hgrp
     c                   eval      vsdtl1_ugrp = w_ugrp
     c                   eval      vsdtl1_modn = w_modn
     c                   eval      vsdtl1_vare = *blank
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found
     c                   eval      vsdtl1_modn = 0
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found
     c                   eval      vsdtl1_ugrp = 0
     c     vsdtl1_key    chain     vsdtl1
     c                   if        not %found
     c                   eval      vsdtl1_hgrp = 0
     c     vsdtl1_key    chain     vsdtl1
     c                   endif
     c                   endif
     c                   endif
     c                   endif
      *
     c                   if        not %found
6.20 c                   goto      les_uten
     c                   endif
      *
     c                   eval      b_sort = *on
6.20 c                   leavesr
      *
6.20  * leser uten
6.20 c     les_uten      tag
6.20 c
6.20 c                   eval      vsdtl1_lage = 0
6.20 c                   eval      vsdtl1_ldor = w_ldor
6.20 c                   eval      vsdtl1_ogrp = w_ogrp
6.20 c                   eval      vsdtl1_hgrp = w_hgrp
6.20 c                   eval      vsdtl1_ugrp = w_ugrp
6.20 c                   eval      vsdtl1_modn = w_modn
6.20 c                   eval      vsdtl1_vare = *blank
6.20 c     vsdtl1_key    chain     vsdtl1
6.20 c                   if        not %found
6.20 c                   eval      vsdtl1_modn = 0
6.20 c     vsdtl1_key    chain     vsdtl1
6.20 c                   if        not %found
6.20 c                   eval      vsdtl1_ugrp = 0
6.20 c     vsdtl1_key    chain     vsdtl1
6.20 c                   if        not %found
6.20 c                   eval      vsdtl1_hgrp = 0
6.20 c     vsdtl1_key    chain     vsdtl1
6.20 c                   if        not %found
6.20 c                   eval      vsdtl1_ogrp = 0
6.20 c     vsdtl1_key    chain     vsdtl1
6.20 c                   endif
6.20 c                   endif
6.20 c                   endif
6.20 c                   endif
6.20  *
6.20 c                   if        not %found and
6.20 c                             w_ldor <> 0
6.20 c                   eval      vsdtl1_ldor = 0
6.20 c                   eval      vsdtl1_ogrp = w_ogrp
6.20 c                   eval      vsdtl1_hgrp = w_hgrp
6.20 c                   eval      vsdtl1_ugrp = w_ugrp
6.20 c                   eval      vsdtl1_modn = w_modn
6.20 c                   eval      vsdtl1_vare = *blank
6.20 c     vsdtl1_key    chain     vsdtl1
6.20 c                   if        not %found
6.20 c                   eval      vsdtl1_modn = 0
6.20 c     vsdtl1_key    chain     vsdtl1
6.20 c                   if        not %found
6.20 c                   eval      vsdtl1_ugrp = 0
6.20 c     vsdtl1_key    chain     vsdtl1
6.20 c                   if        not %found
6.20 c                   eval      vsdtl1_hgrp = 0
6.20 c     vsdtl1_key    chain     vsdtl1
6.20 c                   endif
6.20 c                   endif
6.20 c                   endif
6.20 c                   endif
6.20  *
6.20 c                   if        not %found
6.20 c                   leavesr
6.20 c                   endif
6.20  *
6.20 c                   eval      b_sort = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utskrift av kundebrev
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kundebrev     begsr
      *
      * Kaller program for utskrift av kundebrev
      *
     c                   call      'FR612R'
     c                   parm                    w_firm
     c                   parm                    w_kund
     c                   parm                    w_kpro
     c                   parm                    d_rdat
     c                   parm                    d_txt1
     c                   parm                    d_txt2
     c                   parm                    d_txt3
     c                   parm                    w_valg
6.11 c                   parm                    d_prgr
      *
     c                   endsr
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  * Subrutine for å hente varetype ut fra lager / vare
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  *
6.21 c     hent_vtyp     begsr
6.21 c
6.21 c                   call      'VL710R'
6.21 c                   parm                    w_firm
6.21 c                   parm                    w_vare
6.21 c                   parm                    w_lage
6.21 c                   parm                    w_vtyp
6.21 c                   parm                    w_udat
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.3x c                   parm                    w_lv_nobb
6.3x c                   parm                    w_lv_modn
6.3x c                   parm                    w_lv_lldo
6.3x c                   parm                    w_lv_llva
6.21 c                   endsr
6.21  *
      *
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Henter prisgruppe
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01 c     hent_prisgr   begsr
7.01  *
7.01  * Henter prisgruppe
7.01 c                   eval      w_pgla = rklagr
7.01 c                   eval      w_pgav = 0
7.01  *
7.01 c                   call      'VL712R   '
7.01 c                   parm                    w_firm
7.01 c                   parm                    w_pgla
7.01 c                   parm                    w_pgav
7.01 c                   parm                    w_prgr
7.01  *
7.01 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Paramtre
      *
     c     *entry        plist
     c                   parm                    p_parm
      *
      * Key for les på spesialpris
      *
     c     fpril1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les på rabattmatrise
      *
     c     frabl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag og les på kunderegister
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
     c     rkunl1_key2   klist
     c                   kfld                    w_firm
      *
      * Key for oppslag og les på kundeprosjekt
      *
     c     fkprl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
     c                   kfld                    fkprl1_kpro
      *
     c     fkprl1_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    fkprl1_kund
      *
     c     fkprl1_key3   klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les/oppslag på varesortiment-linje
      *
     c     vsdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vsor
6.20 c                   kfld                    vsdtl1_lage
     c                   kfld                    vsdtl1_ldor
     c                   kfld                    vsdtl1_ogrp
     c                   kfld                    vsdtl1_hgrp
     c                   kfld                    vsdtl1_ugrp
     c                   kfld                    vsdtl1_modn
     c                   kfld                    vsdtl1_vare
      *
     c     vsdtl6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vsor
6.20 c                   kfld                    vsdtl6_lage
     c                   kfld                    vsdtl6_vare
      *
      *
      * Flytter parameter til datasturuktur
      *
     c                   eval      d_list = p_parm
      *
     c                   eval      w_firm = d_firm
     c                   eval      w_vsor = d_vsor
      *
     c                   endsr
