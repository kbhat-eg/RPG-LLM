     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FX839R
      * Beskrivelse . . : Korrigering av statistikk fra kontohenvisning
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       050110 ASK  Nytt program
8.01  *PRG2   220622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner - COMP
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsstala    uf   e           k disk    rename(sstapfr:sstalar)
      *
      *
      * Definering av lda
      *
     d                uds
     d l_firm                944    946  0
      *
      * Datastruktur for kontoinformasjon
      *
     d                 ds
5.20 d d_hele                       128
     d  d_kav1                        4  0 overlay(d_hele)
     d  d_kko1                        6  0 overlay(d_hele:5)
4.02 d  d_ksp1                        4  0 overlay(d_hele:11)
4.02 d  d_kav2                        4  0 overlay(d_hele:15)
4.02 d  d_kko2                        6  0 overlay(d_hele:19)
4.02 d  d_ksp2                        4  0 overlay(d_hele:25)
4.02 d  d_kav3                        4  0 overlay(d_hele:29)
4.02 d  d_kko3                        6  0 overlay(d_hele:33)
4.02 d  d_ksp3                        4  0 overlay(d_hele:39)
4.02 d  d_kav4                        4  0 overlay(d_hele:43)
4.02 d  d_kko4                        6  0 overlay(d_hele:47)
4.02 d  d_ksp4                        4  0 overlay(d_hele:53)
4.02 d  d_kav5                        4  0 overlay(d_hele:57)
4.02 d  d_kko5                        6  0 overlay(d_hele:61)
4.02 d  d_ksp5                        4  0 overlay(d_hele:67)
4.02 d  d_kav6                        4  0 overlay(d_hele:71)
4.02 d  d_kko6                        6  0 overlay(d_hele:75)
4.02 d  d_ksp6                        4  0 overlay(d_hele:81)
4.02 d  d_kav7                        4  0 overlay(d_hele:85)
4.02 d  d_kko7                        6  0 overlay(d_hele:89)
4.02 d  d_ksp7                        4  0 overlay(d_hele:95)
4.02 d  d_khnv                        4    overlay(d_hele:99)
5.20 d  d_nivo                        1    overlay(d_hele:103)
      *
      * Definering av variabler i nøkler
      *
     d w_firm          s                   like(sffirm)
     d sstala_paar     s                   like(sfpaar)
      *
      * Definering av variabler
      *
     d w_savd          s                   like(sfsavd)
     d w_skon          s                   like(sfskon)
     d w_retk          s              1
5.20 d w_hele          s            128
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      sstala_paar = 2010
     c     sstala_key    setll     sstalar
      *
     c                   read      sstalar
     c                   dow       not %eof
      *
      * Finner info via kontohenvisning
      *
     c                   if        sfkhev <> *blank
     c                   eval      w_savd = 0
     c                   eval      w_skon = 0
     c                   exsr      finn_khen
     c                   if        sfkhev = d_khnv and
     c                             w_skon <> 0
     c                   eval      sfskon = w_skon
     c                   update    sstalar
     c                   endif
     c                   endif
      *
     c                   read      sstalar
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å finne avdeling fra kontohenvisning
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     finn_khen     begsr
      *
      * Legger ut parametere for konto-henvisnings-program
      *
     c                   eval      w_retk = *blank
     c                   move      '0'           w_hele
      *
      * Kaller program for å finne konto
      *
     c                   call      'VA720R'
     c                   parm                    w_retk
5.20 c                   parm                    sflety
     c                   parm                    sfotyp
     c                   parm                    sfogrp
     c                   parm                    sfhgrp
     c                   parm                    sfugrp
     c                   parm                    sfvare
     c                   parm                    w_hele
      *
     c                   eval      d_hele = w_hele
     c                   eval      w_savd = d_kav1
     c                   if        sfomva = '0'
     c                   eval      w_skon = d_kko2
     c                   else
     c                   eval      w_skon = d_kko1
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for Statistikk-linje
      *
     c     sstala_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sstala_paar
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
