     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VG710R
      * Beskrivelse . . : Henter inn prod. ansv. fra varegruppe
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       211004 AMD  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vgufir)
     d p_ogrp          s                   like(vguogr)
     d p_hgrp          s                   like(vguhgr)
     d p_ugrp          s                   like(vguugr)
     d p_pran          s                   like(vgupra)
      *
      * Definering av variable i nøkler
      *
     d w_firm          s                   like(vgofir)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hente inn produktansvarlig fra riktig nivå
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      p_pran = *blank
      *
      *  Slå opp mot undergrupperegister
      *
     c                   eval      vugrl1_uogr = p_ogrp
     c                   eval      vugrl1_uhgr = p_hgrp
     c                   eval      vugrl1_uugr = p_ugrp
     c     vugrl1_key    chain     vugrl1r
     c                   if        %found
     c     vgupra        ifne      *blank
     c                   eval      p_pran = vgupra
     c                   goto      xtag01
     c                   endif
     c                   endif
      *
      *  Slå opp mot hovedgrupperegister
      *
     c                   eval      vhgrl1_hogr = p_ogrp
     c                   eval      vhgrl1_hhgr = p_hgrp
     c     vhgrl1_key    chain     vhgrl1r
     c                   if        %found
     c     vghpra        ifne      *blank
     c                   eval      p_pran = vghpra
     c                   goto      xtag01
     c                   endif
     c                   endif
      *
      *  Slå opp mot overgrupperegister
      *
     c                   eval      vogrl1_oogr = p_ogrp
     c     vogrl1_key    chain     vogrl1r
     c                   if        %found
     c     vgopra        ifne      *blank
     c                   eval      p_pran = vgopra
     c                   goto      xtag01
     c                   endif
     c                   endif
      *
     c     xtag01        tag
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_ogrp
     c                   parm                    p_hgrp
     c                   parm                    p_ugrp
     c                   parm                    p_pran
      *
      * Key for Overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for Hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for Undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Legger inn firmanummer
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
