     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : EK722R
      * Beskrivelse . . : Kunde bonus, summerer opp og skriver sumskjema
      *
      * Spesialversjon. : Brukes kun på Degernes
      * Tilpasset for . : Degernes
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       050109 ASK  Nytt program
9.01  *       040310 bof  Takle flere bonusgrupper (inntil 99)
9.02  *       280211 bof  Teste ekstra på firma
6.10  * 6.10  110811 bsa  Tar hensyn til valgt bonustype
6.11  * 6.10  020911 bsa  Tar hensyn til om byggende element
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fekboi1    if   e           k disk    rename(ekbost:ekboi1r)
     feppsl1    if   e           k disk    rename(eppspfr:eppsl1r)
      *
     fek722p    o    e             printer
9.01  *
9.01  * Tabeller
9.01  *
9.01 d a_merk          s              4    dim(99)
9.01 d a_besk          s             35    dim(99)
9.01 d a_pros          s              3  1 dim(99)
9.01 d a_bgru          s             11  2 dim(99)
9.01 d a_sbon          s              9  0 dim(99)
      *
      * Definering av parametre
      *
     d p_firm          s              3
     d p_paar          s              4
6.10 d p_tbot          s              2
      *
      * Definering av variabler i nøkler
      *
     d eppsl1_aarr     s                   like(epaarr)
      *
      * Definering av variable
      *
     d w_firm          s                   like(ekfirm)
6.10 d w_tbot          s              2
9.01 d x               s              2  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
9.01  *
9.01  * Fyller tabellene med prosent,bonusgruppe og beskrivelse
9.01  *
9.01 c                   eval      a_merk(*) = *blank
9.01 c                   eval      a_besk(*) = *blank
9.01 c                   eval      a_pros(*) = 0
9.01 c                   eval      a_bgru(*) = 0
9.01 c                   eval      a_sbon(*) = 0
9.01 c                   eval      x = 0
9.01  *
9.01 c                   movel     p_paar        eppsl1_aarr
9.01 c     eppsl1_key    setll     eppsl1
9.01 c     eppsl1_key    reade     eppsl1
9.01 c                   dow       not  %eof(eppsl1)
9.02 c                   if        epfirm = w_firm
9.01 c                   eval      x = x + 1
9.01 c                   eval      a_merk(x) = epmerk
9.01 c                   eval      a_pros(x) = eppros
9.01 c                   eval      a_besk(x) = epbesk
9.02 c                   endif
9.01 c     eppsl1_key    reade     eppsl1
9.01 c                   enddo
      *
      *
      * Leser bonusgrunlag og bonus
      * og akkumulerer pr. varegruppe (merknad)
      *
     c     ekboi1_key    setll     ekboi1
     c     ekboi1_key    reade     ekboi1
     c                   dow       not %eof(ekboi1)
9.02 c                   if        ekfirm = w_firm
6.10  * Gjelder kun valgt bonustype
6.10 c                   if        w_tbot <> *blank and
6.10 c                             w_tbot <> ekboty
6.10 c                   goto      ny_boty
6.10 c                   endif
6.11  *
6.11  * Sjekk om bonuselementet er byggende
6.11 c                   if        ekebyg = 1
6.11 c                   goto      ny_boty
6.11 c                   endif
6.10  *
9.01 c                   eval      x = %lookup(%subst(ekmerk:1:4):a_merk)
9.01 c                   if        x > 0
9.01 c                   eval      a_bgru(x) = a_bgru(x) + ekbgru
9.01 c                   eval      a_sbon(x) = a_sbon(x) + eksbon
9.01 c                   endif
9.02 c                   endif
      *
6.10 c     ny_boty       tag
      *
     c     ekboi1_key    reade     ekboi1
     c                   enddo
      *
      * Summerer og skriver ut sumskjema
      *
9.01 c                   eval      x = 1
9.01 c                   dow       x < 99
9.01 c                   eval      t1subg = t1subg + a_bgru(x)
9.01 c                   eval      t1subn = t1subn + a_sbon(x)
9.01 C                   eval      x = x + 1
9.01 c                   enddo
     c                   eval      a1aarr = p_paar
      *
     c                   write     a1hdr
9.01  *
9.01  * skriver bonusgrupper
9.01  *
9.01 c                   eval      x = 1
9.01 c                   dow       x < 99
9.01  *
9.01 c                   if        a_merk(x) = *blank
9.01 c                   leave
9.01 c                   endif
9.01  *
9.01 c                   eval      d1merk = a_merk(x)
9.01 c                   movel     a_besk(x)     d1besk
9.01 c                   eval      d1bgru = a_bgru(x)
9.01 c                   eval      d1sbon = a_sbon(x)
9.01 c                   eval      d1pros = a_pros(x)
9.01  *
9.01 c                   write     d1det                                40
9.01 c                   if        *in40 = *on
9.01 c                   write     a1hdr
9.01 c                   eval      *in40 = *off
9.01 c                   endif
9.01  *
9.01 C                   eval      x = x + 1
9.01  *
9.01 c                   enddo
9.01  *
9.01 c                   write     t1tot
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_paar
6.10 c                   parm                    p_tbot
      *
      * Key for les på kundebonus
      *
     c     ekboi1_key    klist
     c                   kfld                    w_firm
      *
      * Key for les av bonussatser
      *
     c     eppsl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    eppsl1_aarr
      *
      * Henter parameter
      *
     c                   move      p_firm        w_firm
     c                   move      p_paar        eppsl1_aarr
6.10 c                   eval      w_tbot = p_tbot
      *
     c                   endsr
