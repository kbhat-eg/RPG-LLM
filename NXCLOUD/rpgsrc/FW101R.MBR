      /title  ASOFAK Lev.vare, innlesing av varer - param. 2
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FW101R
      * Beskrivelse . . : Innlesing av varer/priser fra leverandør
      *                   parameterprogram 2
      *
      *                   Velger overstyringer
      *
      *                   Mulige leverandører :
      *                     01 - ASP gammelt standardformat
      *                     02 - ASP nytt standardformat
      *
      *                     04 - Jernia
      *                     05 - Luna
      *                     06 - Arvid Nilsson (Brenna direkte)
      *                     07 - ESAB
      *                     08 - Farveringen
      *                     09 - Farvemiljø Nord
      *                     10 - Christ Engebretsen
      *                     11 - Elektorforbundets standardformat (ELFO)
      *                     12 - Ole Moe Engros
      *                     13 - Coward
      *                     14 - Sandvik
      *                     15 - Felleskjøpet Maskin
      *                     16 - Smith
      *
      *                     20 - Hov+Dokka
      *                     21 - Andvord
      *                     22 - Sørbø
      *                     23 - Norske Røtter
      *                     24 - Emco
      *                     25 - Generelt SDV format (laget for T.M.Bergersen)
5.61  *                     26 - Forus ?
5.61  *                     27 - Generelt Integ.dat  (IFS)
5.61  *                     28 - Generelt Integ.dat  (QDLS)
5.61  *                     29 - Kontrollliste  Integ.dat  (IFS)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       060699 HKO  Nytt program
      *       040100 HKO  Lagt inn Andvord
      *       250200 HKO  Lagt inn Ole Moe Engros
      *       280200 HKO  Lagt inn Coward
      *       100800 HKO  Lagt inn Sørbø
      *       220800 HKO  Lagt inn Sandvik
      *       290800 HKO  Lagt inn Felleskjøpet Maskin
      *       090101 HKO  Lagt inn Smith
      *       230301 HKO  Lagt inn Norske Røtter
      *       191003 HKO  Endret filnavn fra Jernia til Pxxxxxxx.PRI
      *       280408 bof  Lagt inn Gen. SDV format (T.M. Bergersen)
5.61  *       090309 bof  Lagt inn integ.dat format
5.62  *       110810 bof  Lagt inn kontroll-liste for integ.dat format
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
      *
     ffw101d    cf   e             workstn
      *
      * Definering av array
      *
5.62 d a_ldor          s             76    dim(29) ctdata perrcd(1)
      *
      * Definering av Local data area
      *
     d                uds
     d l_fnav                951    980
      *
      * Definering av datastrukturer
      *
      *  Leverandør-array
      *
     d                 ds
     d d_larr                        76
     d  d_lnum                        2  0 overlay(d_larr)
     d  d_lnav                       31    overlay(d_larr:4)
     d  d_file                       12    overlay(d_larr:35)
     d  d_fldr                       20    overlay(d_larr:47)
     d  d_prog                       10    overlay(d_larr:67)
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_valg          s              2  0
      *
      * Definering av variable i nøkler
      *
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(rlfirm)
     d w_lnav          s                   like(rlnavn)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer skjermbildet
      *
     c                   eval      d_larr = a_ldor(p_valg)
      *
     c                   eval      b1valg = p_valg
     c                   eval      b1file = d_file
     c                   eval      b1fldr = d_fldr
      *
      * Viser bildet
      *
     c     b1taga        tag
      *
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b1taga
     c                   endsl
      *
      * Validering av input
      *
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      b1taga
     c                   endif
      *
      * Kaller program for henting av fil fra leverandører
      *
     c                   call      'FW102R'
     c                   parm                    w_firm
     c                   parm                    b1file
     c                   parm                    b1fldr
     c                   parm                    d_prog
     c                   parm                    b1ldor
     c                   parm                    p_valg
     c                   parm                    b_feil
      *
     c                   if        b_feil = *on
     c                   eval      *in35 = *on
     c                   goto      b1taga
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Behandling av spørringer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Leverandør
      *
     c                   if        w_afld = 'B1LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    b1ldor
     c                   parm                    w_lnav
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for sjekking av input-felter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
      * Filnavn
      *
     c                   if        b1file = *blank
     c                   eval      *in31 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Leverandør
      *
     c                   if        b1ldor = 0
     c                   eval      *in32 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c                   eval      rlevl1_levr = b1ldor
     c     rlevl1_key    chain     rlevl1                             91
     c                   if        *in91 = *on
     c                   eval      *in33 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_valg
      *
      * Key for oppslag på leverandør-register
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
      *
** Leverandørerer
01 ASP gammelt standardformat     STANDARD                        FW701R
02 ASP nytt standardformat                                        FW702R
03
04 Jernia                         Pxxxxxxx.PRIPRIS_INN            FW704R
05 Luna                           PRISFIL.NEW PRIS_INN            FW705R
06 Arvid Nilsson                  KONVFILE.TXTPRIS_INN            FW706R
07 ESAB                                                           FW707R
08 Faveringen                     FOxxxxxx.KUNPRIS_INN            FW708R
09 Favemiljø Nord                 PRISMEAN.BOKPRIS_INN            FW709R
10 Christ Engebretsen             PCESOR      PRIS_INN            FW710R
11 ELFO standardformat            VARExxxx    PRIS_INN            FW711R
12 Ole Moe Endros                 Pxxxxxxx.xx PRIS_INN            FW712R
13 Coward                         PRISER.SDF  PRIS_INN            FW713R
14 Sandvik                        SVNET.TXT   PRIS_INN            FW714R
15 Felleskjøpet Maskin            FKM.TXT     PRIS_INN            FW715R
16 Smith                          VARE.TXT    PRIS_INN            FW716R
17
18
19
20 Hov+Dokka                      PRISLnn.xxx PRIS_INN            FW720R
21 Andvord                        ANDVORD.TXT PRIS_INN            FW721R
22 Sørbø                          SØRBØ       PRIS_INN            FW722R
23 Norske Røtter                  ROTTER      PRIS_INN            FW723R
24 Emco                           EMCO.PRN    PRIS_INN            FW724R
25 Gen.SDV Format                 TMRPIS.SDV  PRIS_INN            FW725R
26 Forus                          xxxxxxxxxx  PRIS_INN            FW726R
27 Integ DAT                      PrisFil.dat PRIS_INN            FW727R
28 Integ DAT (QDLS)               V07768      PRIS_INN            FW727R
29 Kontrollliste Integ IFS        PrisFil.dat PRIS_INN            FW729R
