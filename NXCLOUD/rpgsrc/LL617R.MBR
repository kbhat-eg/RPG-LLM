      /TITLE  Utplukk fra lagerregister
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Program . . . . : LL617R
      * Beskrivelse . . : Utplukk til ABC-analyse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.
      * --------- ------  --------------------------------------------------
      *           010200  Nytt program                             KOB
5.60  *       050408 bø   Lagerenhet fra vare-register
5.70  *  PRGR 021208 bof  Utvidet med prisgruppe
6.30  * 6.30  030117 bsa  Sender med lager til prisprogram.
8.01  *PRG2   150622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  BRUK AV INDIKATORER
      *
      *       LR = Avslutt program
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer
      *    70-79 = Arbeidsindikatorer i sub-rutiner
      *    80-89 = Arbeidsindikatorer ordinære
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvlagl1    if   e           k disk    rename(vlagpfr:vlaglur)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     flwa1lu    o    e             disk    rename(lwa1pfr:lwa1lur)
      *
      *  Parameter-rec
      *
     d                 ds
     d d_prec                       128
     d  d_fvar                        8  0 overlay(d_prec)
     d  d_tvar                        8  0 overlay(d_prec:9)
     d  d_flag                        2  0 overlay(d_prec:17)
     d  d_tlag                        2  0 overlay(d_prec:19)
     d  d_fogr                        2  0 overlay(d_prec:21)
     d  d_togr                        2  0 overlay(d_prec:23)
     d  d_fhgr                        2  0 overlay(d_prec:25)
     d  d_thgr                        2  0 overlay(d_prec:27)
     d  d_fugr                        3  0 overlay(d_prec:29)
     d  d_tugr                        3  0 overlay(d_prec:32)
     d  d_levn                        6  0 overlay(d_prec:35)
     d  d_sakb                        4    overlay(d_prec:41)
     d  d_sort                        1  0 overlay(d_prec:45)
      *
      * Key på arbeidsfil
      *
     d                 ds
     d d_mkey                        65
     d  d_mk01                        8  0 overlay(d_mkey)
     d  d_mk02                        8  0 overlay(d_mkey:9)
     d  d_mk03                        8  0 overlay(d_mkey:17)
     d  d_mk04                        8  0 overlay(d_mkey:25)
     d  d_mk05                        8  0 overlay(d_mkey:33)
      *
      * Datastrukturer for testing på varegrupper
      *
     d                 ds
     d f_vgrp                         7
     d  f_ogrp                        2  0 overlay(f_vgrp)
     d  f_hgrp                        2  0 overlay(f_vgrp:3)
     d  f_ugrp                        3  0 overlay(f_vgrp:5)
      *
     d                 ds
     d t_vgrp                         7
     d  t_ogrp                        2  0 overlay(t_vgrp)
     d  t_hgrp                        2  0 overlay(t_vgrp:3)
     d  t_ugrp                        3  0 overlay(t_vgrp:5)
      *
     d                 ds
     d v_vgrp                         7
     d  v_ogrp                        2  0 overlay(v_vgrp)
     d  v_hgrp                        2  0 overlay(v_vgrp:3)
     d  v_ugrp                        3  0 overlay(v_vgrp:5)
      *
     d p_firm          s                   like(vlfirm)
     d p_vare          s                   like(vlvare)
     d p_lety          s              1  0
     d p_dato          s                   like(vltrda)
     d p_enhe          s              3
     d p_sapr          s              9  2
     d p_gmpr          s              9  2
     d p_kopr          s              9  2
     d p_inpr          s              9  2
6.30 d p_lage          s              2  0
      *
      * Local Data Area
      *
     d                uds
      *
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d vlagl1_vare     s                   like(vlvare)
     d vlagl1_lage     s                   like(vllage)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vogrl1_oogr     s                   like(vgoogr)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vvfirm)
     d w_parm          s            128
     d w_vare          s              8  0
      *
     d w_dato          s               d   datfmt(*iso)
     d w_kpny          s              9  2
     d w_spny          s              9  2
5.70 d w_avde          s              4  0
8.01 d w_prgr          s              2
      *
      * Definering av midlertidige felt
      *
     d vgopra          s                   like(vgupra)
     d vghpra          s                   like(vgupra)
     d vgolag          s                   like(vgulag)
     d vghlag          s                   like(vgulag)
      *
      *
      *---------------------------------------------------------------
      *  Behandle lagerrecords
      *---------------------------------------------------------------
      *
     c     les_lager     tag
      *
     c                   read      vlagl1                                 90
     c                   if        *in90 = *on
     c                   goto      end_pgm
     c                   endif
     c                   movel     vlvare        w_vare
      *
      * Sjekk riktig firma
      *
     c                   if        vlfirm <> l_firm
     c                   goto      les_lager
     c                   endif
      *
      * Sjekk varenr. fra - til
      *
     c                   if        w_vare < d_fvar
     c                             or w_vare > d_tvar
     c                   goto      les_lager
     c                   endif
      *
      * Sjekk lagernr. fra - til
      *
     c                   if        vllage < d_flag
     c                             or vllage > d_tlag
     c                   goto      les_lager
     c                   endif
      *
      * Sjekk om varenr. i registeret
      *
     c                   eval      vvarl1_vare = vlvare
     c     vvarl1_key    chain     vvarl1                             91
     c                   if        *in91 = *on
     c                   goto      les_lager
     c                   endif
      *
      * Sjekk om varegruppe fra - til
      *
     c                   eval      v_ogrp = vvogrp
     c                   eval      v_hgrp = vvhgrp
     c                   eval      v_ugrp = vvugrp
      *
     c                   if        v_vgrp < f_vgrp
     c                             or v_vgrp > t_vgrp
     c                   goto      les_lager
     c                   endif
      *
      * Sjekk om undergruppe skal lagerstyres
      *
     c                   eval      vugrl1_uogr = vvogrp
     c                   eval      vugrl1_uhgr = vvhgrp
     c                   eval      vugrl1_uugr = vvugrp
     c     vugrl1_key    chain     vugrl1                             92
     c                   if        *in92 = *off and
     c                             vgulag = 1
     c                   goto      les_lager
     c                   endif
      *
      * Sjekk om hovedgruppe skal lagerstyres
      *
     c                   eval      vhgrl1_hogr = vvogrp
     c                   eval      vhgrl1_hhgr = vvhgrp
     c     vhgrl1_key    chain     vhgrl1                             92
     c                   if        *in92 = *off and
     c                             vghlag = 1
     c                   goto      les_lager
     c                   endif
      *
      * Sjekk om overgruppe skal lagerstyres
      *
     c                   eval      vogrl1_oogr = vvogrp
     c     vogrl1_key    chain     vogrl1                             92
     c                   if        *in92 = *off and
     c                             vgolag = 1
     c                   goto      les_lager
     c                   endif
      *
      * Sjekk om riktig leverandør - hvis leverandør er valgt
      *
     c                   if        d_levn <> *zero
     c                             and d_levn <> vvldor
     c                   goto      les_lager
     c                   endif
      *
      * Sjekk om riktig saksbehandler - hvis saksbehandler er valgt
      *
     c                   if        d_sakb <> *blank and
     c                             d_sakb <> vgopra and
     c                             d_sakb <> vghpra and
     c                             d_sakb <> vgupra
     c                   goto      les_lager
     c                   endif
      *
      * Sjekk om lagervare
      *
5.70 c                   if        vlvtyp <> 'L'
     c                   goto      les_lager
     c                   endif
      *
      * Bygg opp sorteringsbegrep
      *
     c                   eval      d_mkey = *blank
      *
     c                   select
     c                   when      d_sort = 1
     c                   eval      d_mk01 = w_vare
     c                   eval      d_mk02 = vllage
     c                   when      d_sort = 2
     c                   eval      d_mk01 = vvogrp
     c                   when      d_sort = 3
     c                   eval      d_mk01 = vvogrp
     c                   eval      d_mk02 = vvhgrp
     c                   when      d_sort = 4
     c                   eval      d_mk01 = vvogrp
     c                   eval      d_mk02 = vvhgrp
     c                   eval      d_mk03 = vvugrp
     c                   endsl
     c                   eval      mlakey = d_mkey
      *
      * Finn riktige priser
      *
     c                   eval      p_vare = vlvare
5.60 c                   eval      p_enhe = vvenhl
     c                   eval      p_lety = 0
     c                   eval      p_dato = w_dato
5.70 c                   exsr      hent_prisgr
6.30 c                   eval      p_lage = vllage
      *
     c                   call      'VP730R'
     c                   parm                    w_firm
     c                   parm                    p_vare
5.70 c                   parm                    w_prgr
     c                   parm                    p_lety
     c                   parm                    p_dato
     c                   parm                    p_enhe
     c                   parm                    p_sapr
     c                   parm                    p_gmpr
     c                   parm                    p_kopr
     c                   parm                    p_inpr
6.30 c                   parm                    p_lage
      *
     c                   eval      mlenhl = p_enhe
     c                   eval      mlspri = p_sapr
     c                   eval      mlkpri = p_kopr
     c                   eval      mlipri = p_inpr
      *
      * Beregn lagerverdi
      *
     c                   eval(h)   mlomst = mlspri * vlsalg
     c                   eval(h)   mlkost = mlkpri * vlsalg
     c                   eval(h)   mldekt = (mlspri - mlkpri) * vlsalg
     c                   eval(h)   mlinkt = mlipri * vlbehl
     c                   eval(h)   mllagv = mlkpri * vlbehl
      *
      * Flytt til arbeidsfil
      *
     c                   eval      mlfirm = vlfirm
     c                   movel     vlvare        mlvare
     c                   eval      mllage = vllage
     c                   eval      mlplas = vlplas
     c                   eval      mlpakk = vlpakk
     c                   eval      mloord = vloord
     c                   eval      mlores = vlores
     c                   eval      mlbbes = vlbbes
     c                   eval      mlbbes = vlbbes
     c                   eval      mlbres = vlbres
     c                   eval      mlinng = vlinng
     c                   eval      mlutta = vlutta
     c                   eval      mljust = vljust
     c                   eval      mltell = vltell
     c                   eval      mlsalg = vlsalg
     c                   eval      mlretu = vlretu
     c                   eval      mlb101 = vlb101
     c                   eval      mlbehl = vlbehl
     c                   eval      mlbeve = vlbeve
     c                   eval      mlkpny = vlkpny
     c                   eval      mlmini = vlmini
     c                   eval      mlmaks = vlmaks
     c                   eval      mlltid = vlltid
     c                   eval      mlbpun = vlbpun
     c                   eval      mlbmen = vlbmen
     c                   eval      mlsikl = vlsikl
     c                   eval      mltrda = vltrda
     c                   eval      mlopda = vlopda
     c                   eval      mlsdat = vlsdat
     c                   eval      mlogrp = vvogrp
     c                   eval      mlhgrp = vvhgrp
     c                   eval      mlugrp = vvugrp
      *
     c                   write     lwa1lur
      *
     c                   goto      les_lager
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     end_pgm       tag
      *
     c                   eval      *inlr = *on
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5 70  * Subrutine for å hente prisgruppe
5.70  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.70  *
5.70 c     hent_prisgr   begsr
5.70  *
5.70 c                   eval      w_avde = 0
5.70 c                   call      'VL712R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vllage
5.70 c                   parm                    w_avde
5.70 c                   parm                    w_prgr
5.70  *
5.70 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key les av varer
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Key for les av lager
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlagl1_vare
     c                   kfld                    vlagl1_lage
      *
      * Key for les av overgruppe/hovedgruppe/undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for laes av overgruppe/hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for les av overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    w_parm
     c                   eval      d_prec = w_parm
      *
      * Legge inn firmanummer
      *
     c                   move      l_firm        w_firm
      *
      * Bygg opp varegruppe fra
      *
     c                   eval      f_ogrp = d_fogr
     c                   eval      f_hgrp = d_fhgr
     c                   eval      f_ugrp = d_fugr
      *
      * Bygg opp varegruppe til
      *
     c                   eval      t_ogrp = d_togr
     c                   eval      t_hgrp = d_thgr
     c                   eval      t_ugrp = d_tugr
      *
      * Sett startpunkt for les
      *
     c                   movel     d_fvar        vlagl1_vare
     c                   move      d_flag        vlagl1_lage
     c     vlagl1_key    setll     vlagl1
      *
     c                   endsr
