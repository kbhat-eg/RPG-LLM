     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FU114R
      * Beskrivelse . . : Sletting av tilbud
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
5.01  * R5.01 241002 AMD  Lagt om rutiner for oppdatering av lager
      *       281105 HKO  Fjernet test på utgår-kode, og tester kun på
      *                   passert utgår-dato
6.nu  * R6.30 240614 bof  Gjennomgått mtp. nummerutvidelse
7.01  * R6.30 100317 sst  Logge massesletting av tilbud
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     FFOHEL1    IF   E           K DISK    RENAME(FOHEPFR:FOHEL1R)
      *
     D                 DS
      *                                                      Parameter-inn
     D  d_list                 1     64
     D  d_otyp                 1      2
     D  d_dato                 3     12d   datfmt(*iso)
5.01  *
5.01  * Definering av variable
5.01  *
5.01 d w_opdt          s              1
7.01 d w_aktp          s             15
7.01 d w_slko          s              2  0
7.01 d w_kkko          s              2  0
7.01 d w_sgru          s             60
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *               :  Local Data Area                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     D                UDS
     D  l_firm               944    946  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandle utplukk, Setter inn riktige parameterverdier          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c                   MOVE      p_list        d_list
      *
     c                   MOVE      *ZERO         HEDNUM
     c                   MOVE      *ZERO         HEDSUF
      *
     c     HEDKEY        SETLL     FOHEL1R
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Leser poster fra register for utplukks-test                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     START         TAG
      *
     c                   MOVE      *OFF          *IN90
     c                   READ      FOHEL1R                                90
     c     *IN90         CABEQ     *ON           SLUTT
      *
      *  Sjekk   Er ordretype lik valgt,           Hvis nei les neste post
      *
     c                   if        fonumm = 90000012
     c                   eval      fonumm = fonumm
     c                   endif
     c     FOOTYP        CABNE     d_otyp        START
      *
      *  Sjekk   Er Dato innenfor grensene,        Hvis nei les neste post
      *
     c                   if        fotdat = *loval or
     c                             fotdat > d_dato
     c                   goto      start
     c                   endif
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Sletter TILBUD i ORDRE-REGISTER                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Kaller opp subprogrammet for sletting av ordre
      *
     c                   MOVE      'TILB'        WTYPED
     c                   CALL      'FO410R'
     c                   PARM                    FOFIRM
     c                   PARM                    FONUMM
     c                   PARM                    FOSUFF
     c                   PARM                    WTYPED
     c                   PARM                    WPKOM1
     c                   PARM                    WPKOM2
5.01 c                   parm                    w_opdt
7.01  *
7.01  *  Kaller loggprogram for logging av sletting av tiblud
7.01  *
7.01 c                   if        footyp = 'T '
7.01 c                   eval      w_aktp = 'SLETTET'
7.01 c                   eval      w_sgru = *blank
7.01 c                   eval      w_slko = *zero
7.01 c                   eval      w_kkko = *zero
7.01 c                   eval      w_sgru = 'Masse sletting pr. ' +
7.01 c                                      %char(d_dato)
7.01  *
7.01 c                   call      'XL700R'
7.01 c                   parm                    fonumm
7.01 c                   parm                    w_aktp
7.01 c                   parm                    w_slko
7.01 c                   parm                    w_kkko
7.01 c                   parm                    w_sgru
7.01 c                   endif
      *
      *  Velg neste Ordre
      *
     c                   GOTO      START
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     SLUTT         TAG
      *
     c                   SETON                                        LR
     c                   RETURN
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     CSR   *INZSR        BEGSR
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     c                   MOVE      *BLANK        WTYPED            4
      *
     c                   MOVE      *BLANK        WPKOM1           30
     c                   MOVE      *BLANK        WPKOM2           30
      *
      *  Redefinering av nøkler
      *
     c     *LIKE         DEFINE    FOFIRM        HEDFIR
     c     *LIKE         DEFINE    FONUMM        HEDNUM
     c     *LIKE         DEFINE    FOSUFF        HEDSUF
      *
      *  Key for file : ORDRE-HODE-REGISTER
      *
     c     HEDKEY        KLIST
     c                   KFLD                    HEDFIR
     c                   KFLD                    HEDNUM
     c                   KFLD                    HEDSUF
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *ENTRY        PLIST
     c                   PARM                    p_list           64
      *
      *  Legge inn firmanummer i nøkklene
      *
     c                   MOVE      l_firm        HEDFIR
      *
     CSR                 ENDSR
