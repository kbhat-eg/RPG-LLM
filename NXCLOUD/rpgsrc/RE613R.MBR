     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
      *                                                                       *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RE613R                                              *
      * Beskrivelse . . : Saldobalanse til Maestro - utplukk                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *
     frhovl1    if   e           k disk    rename(rhovpfr:rhovl1r)
     frhsal1    if   e           k disk    rename(rhsapfr:rhsal1r)
     fre06pf    o  a e             disk
      *
     d sal             s             15  2 dim(14)                              Saldo
     d saÅ             s             15  2 dim(14)                              Saldo i år
     d saf             s             15  2 dim(14)                              Saldo i fjor
     d sab             s             15  2 dim(14)                              Saldo budsjett
      *
      * Layout til Maestro
      *
     d                 ds
     d dsulda                        80                                         Hele u
     d  dskont                        6  0 overlay(dsulda)                      Konto
     d  dstext                       30    overlay(dsulda:7)                    Konton
     d  dstxt2                       10    overlay(dsulda:37)                   Konton
     d  dskrÅr                       14    overlay(dsulda:47)                   Kroner
     d  dspkÅr                        1    overlay(dsulda:61)                   Desima
     d  dsØrÅr                        2  0 overlay(dsulda:62)                   Øre i
     d  dskrfj                       14    overlay(dsulda:64)                   Kroner
     d  dspkfj                        1    overlay(dsulda:78)                   Desima
     d  dsØrfj                        2  0 overlay(dsulda:79)                   Øre i
      *
     d                uds
     d pfirm                 300    302  0                                      Firma
     d pnavn                         30                                         Navn
     d puser                         10                                         BruerI
     d pdato                          6  0                                      Pr. da
     d praar                          4  0                                      Pr. år
     d pperf                          2  0                                      Period
     d ppert                          2  0                                      Period
     d pavdf                          4  0                                      Avd. f
     d pavdt                          4  0                                      Avd. t
     d prapp                 471    472  0                                      Rappor
     d psumm                          1                                         Avd.su
     d psums                          1                                         Avd.su
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    970
      *
     d wfavde          s                   like(rhavde)
     d wfkont          s                   like(rhkont)
     d wfspes          s                   like(rhspes)
     d whtext          s                   like(rhtext)
     d wdamnd          s              4  0
     d wfibel          s             14  2
     d wrbpÅr          s                   like(wrfpÅr)
     d wrbtÅr          s                   like(wrfpÅr)
     d wrfpÅr          s             16  2
     d wrfpfj          s                   like(wrfpÅr)
     d wrftÅr          s                   like(wrfpÅr)
     d wrftfj          s                   like(wrfpÅr)
     d w_raar          s                   like(riraar)
     d w_firm          s                   like(rhfirm)
     d w_funn          s              1
     d x               s              3  0
      *
     d rhovl1_kont     s                   like(rhkont)
     d rhsal1_kont     s                   like(rhkont)
      *
      *  Saldo-register - Kontonr.rekkefølge
      *
     irhsal1r       01
      *
     i              risa01                      sal(01)
     i              risa02                      sal(02)
     i              risa03                      sal(03)
     i              risa04                      sal(04)
     i              risa05                      sal(05)
     i              risa06                      sal(06)
     i              risa07                      sal(07)
     i              risa08                      sal(08)
     i              risa09                      sal(09)
     i              risa10                      sal(10)
     i              risa11                      sal(11)
     i              risa12                      sal(12)
     i              risa13                      sal(13)
     i              risa00                      sal(14)
      *
     c/eject
      *
      * Les kontplan
      *
     c     rhovl1_key    setll     rhovl1r                                60
     c     rhovl1_key    reade     rhovl1r                                60
      *
     c                   dow       *in60 = *off
      *
      * Konto behandles bare en gang
      *
     c                   if        rhkont = wfkont
     c                   goto      neste_kto
     c                   endif
      *
     c                   eval      wfkont = rhkont
      *
      * Nullstill tabeller
      *
     c                   eval      saÅ = 0
     c                   eval      saf = 0
     c                   eval      sab = 0
      *
     c                   eval      w_funn = ' '
      *
      * Les saldomatrise
      *
     c                   eval      wfspes = *zeros
     c                   eval      wfavde = *zeros
      *
     c                   eval      rhsal1_kont = rhkont
     c     rhsal1_key    setll     rhsal1r                                61
     c     rhsal1_key    reade     rhsal1r                                61
      *
     c                   dow       *in61 = *off
      *
     c                   if        psums = 'J' and                              Pr. kto+spes?
     c                             rispes > rhspes                               legg ut
     c                   exsr      summer                                         pr. spes
     c                   eval      w_funn = ' '
     c                   endif
      *
     c                   if        psumm = 'J' and                              Pr. avdeling?
     c                             riavde > rhavde                               legg ut
     c                   exsr      summer                                         pr. avd.
     c                   eval      w_funn = ' '
     c                   endif
      *
     c                   if        riavde < pavdf or                            Pr. avdeling?
     c                             riavde > pavdt
     c                   eval      wfspes = rispes
     c                   eval      wfavde = riavde
     c                   goto      neste_sal
     c                   endif
      *
      * Saldo i år
      *
     c                   if        riraar = praar and
     c                             ritype = 1
     c                   eval      w_funn = '1'
     c                   add       sal           saÅ
     c                   endif
      *
      * Saldo i fjor
      *
     c                   if        riraar = w_raar and
     c                             ritype = 1
     c                   eval      w_funn = '1'
     c                   add       sal           saf
     c                   endif
      *
      * Budsjett i år
      *
     c                   if        riraar = praar and
     c                             ritype = 3
     c                   eval      w_funn = '1'
     c                   add       sal           sab
     c                   endif
      *
     c                   eval      wfspes = rispes
     c                   eval      wfavde = riavde
      *
     c     neste_sal     tag
      *
     c     rhsal1_key    reade     rhsal1r                                61
     c                   enddo
      *
     c     neste_kto     tag
      *
      * Legg ut post hvis saldorecord funnet
      * på denne konto før eof
      *
     c                   if        *in61 and                                    Pr. kto+spes?
     c                             w_funn = '1'                                 Pr. kto+spes?
     c                   exsr      summer                                         pr. spes
     c                   endif
      *
     c     rhovl1_key    reade     rhovl1r                                60
     c                   enddo
      *
     c     slutt         tag
      *
     c                   seton                                        lr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for summering                                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     summer        begsr
      *
      * Saldo i år
      *
     c                   z-add     saÅ(14)       wrftÅr
     c                   z-add     ppert         x
     c                   z-add     saÅ(x)        wrfpÅr
     c                   z-add     1             x
      *
     c                   dou       x > ppert
     c                   add       saÅ(x)        wrftÅr
     c                   add       1             x
     c                   enddo
      *
      * Budsjett i år
      *
     c                   z-add     sab(14)       wrbtÅr
     c                   z-add     ppert         x
     c                   z-add     sab(x)        wrbpÅr
     c                   z-add     1             x
      *
     c                   dou       x > ppert
     c                   add       sab(x)        wrbtÅr
     c                   add       1             x
     c                   enddo
      *
      * Saldo i fjor
      *
     c                   z-add     saf(14)       wrftfj
     c                   z-add     ppert         x
     c                   z-add     saf(x)        wrfpfj
     c                   z-add     1             x
      *
     c                   dou       x > ppert
     c                   add       saf(x)        wrftfj
     c                   add       1             x
     c                   enddo
      *
     c                   if        psums = 'J'
     c                   eval      rhspes = *zeros
     c                   else
     c                   eval      rhspes = wfspes
     c                   endif
      *
     c                   if        psumm = 'J'
     c                   eval      rhavde = *zeros
     c                   else
     c                   eval      rhavde = wfavde
     c                   endif
      *
     c                   if        psums = 'J'
     c                   eval      rhspes = wfspes
     c                   endif
      *
     c                   if        psumm = 'J'
     c                   eval      rhavde = wfavde
     c                   endif
      *
     c                   z-add     rhkont        dskont
     c                   eval      dstext = rhtext
     c                   movel     rhtxt2        dstxt2
     c                   movel     wrftÅr        dskrÅr
     c                   move      wrftÅr        dsØrÅr
      *
B001 c                   if        wrftÅr < *zeros
 001 c                   movel     '-'           dskrÅr
 001 c                   z-sub     dsØrÅr        dsØrÅr
X001 c                   else
 001 c                   movel     '+'           dskrÅr
E001 c                   endif
      *
     c                   movel     wrftfj        dskrfj
     c                   move      wrftfj        dsØrfj
      *
B001 c                   if        wrftfj < *zeros
 001 c                   movel     '-'           dskrfj
 001 c                   z-sub     dsØrfj        dsØrfj
X001 c                   else
 001 c                   movel     '+'           dskrfj
E001 c                   endif
      *
     c                   eval      rpcmax = dsulda
      *
      * Legg ut record
      *
     c                   if        wrfpÅr <> *zeros or                          Ikke skriv
     c                             wrftÅr <> *zeros or                           post dersom
     c                             wrbpÅr <> *zeros or                            alle beløp
     c                             wrbtÅr <> *zeros or                             er null.
     c                             wrfpfj <> *zeros or
     c                             wrftfj <> *zeros
     c                   write     re06pfr                                       SKRIV POST
     c                   endif
      *
     c                   if        psums = 'J'
     c                   move      rispes        rhspes
     c                   endif
      *
     c                   if        psumm = 'J'
     c                   eval      rhavde = riavde
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   eval      x = 0
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_raar = praar - 1
      *
     c                   eval      wrfpÅr = *zeros
     c                   eval      wrftÅr = *zeros
     c                   eval      wrbpÅr = *zeros
     c                   eval      wrbtÅr = *zeros
     c                   eval      wrfpfj = *zeros
     c                   eval      wrftfj = *zeros
     c                   eval      wfkont = *zeros
      *
      * Key for konto
      *
     c     rhovl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for saldomatrise
      *
     c     rhsal1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhsal1_kont
      *
     c                   endsr
