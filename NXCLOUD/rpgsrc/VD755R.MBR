     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VD755R
      * Beskrivelse . . : Varesortiment-linje, oppretter sortiment - filkopi
      *
      * NB! NB! NB! Takler ikke kopiering til firma forskjellig fra 000
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       150402 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvshel1    if   e           k disk    rename(vshepfr:vshel1r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vcfirm)
     d p_fgrt          s              3
     d p_vsor          s                   like(vcvsor)
      *
      * Definering av variabler i nøkler
      *
     d vshel1_vsor     s                   like(vcvsor)
      *
      * Definering av datastruktur - parameterliste ut
      *
     d                 ds
     d d_list                        48
     d  d_firm                        3  0 overlay(d_list:1)
     d  d_vsor                       10    overlay(d_list:4)
     d  d_besk                       35    overlay(d_list:14)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(vcfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter beskrivelse fra eventuelt eksisterende sortiment
      *
     c                   eval      vshel1_vsor = p_vsor
     c     vshel1_key    chain     vshel1
     c                   if        %found
     c                   eval      d_besk = vcbesk
     c                   endif
      *
      * Kaller oppdateringsprogram
      *
     c                   eval      d_firm = w_firm
     c                   eval      d_vsor = p_vsor
      *
     c                   if        d_besk = *blank
     c                   eval      d_besk = 'Automatisk opprettet'
     c                   endif
      *
     c                   call      'VD751C'
     c                   parm                    d_list
     c                   parm                    p_fgrt
     c                   parm                    b_feil
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_fgrt
     c                   parm                    p_vsor
      *
      * Key for oppslag på sortiment-hode
      *
     c     vshel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vshel1_vsor
      *
      *
      * Henter firma fra parameter
      *
     c                   move      p_firm        w_firm
      *
     c                   endsr
