     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
6.20  * Program . . . . : FX906R
      * Beskrivelse . . : Sjekke om det finnes varer som ikke har innkjøpsenhet
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       131212 bof  Nytt program
6.nu  * R6.30 030614 bsa  Programmet gjennomgått mtp nummerutvidelser.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvvenl4    if   e           k disk    rename(vvenpfr:vvenl4r)
     flwexpf    uf a e           k disk    rename(lexcpfr:lwexpfr)
      *
      * Definering av pamametre
      *
      *
      * Definering av variable i nøkler
      *
     d vvarl1_vare     s                   like(vvvare)
     d rlevl1_levr     s                   like(rllevr)
     d vvenl4_vare     s                   like(vevare)
     d vvenl4_inen     s                   like(veinen)
      *
      * Definering av variable
      *
     d x               s              1  0
     d b_funn          s              1    inz(*off)
6.20 d b_inlr          s              1    inz(*off)
      *
     d w_firm          s                   like(vvfirm)
      *
6.20 d                uds
     d l_firm                944    946  0
6.20 d l_lage               1001   1002  0
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser alle varer
      *
     c     vvarl1_key    setll     vvarl1
     c     vvarl1_key    reade     vvarl1
     c                   dow       not %eof(vvarl1)
      *
      * Leser innkjøpsenheter på vare
      *
     c                   eval      b_funn = *off
      *
     c                   eval      vvenl4_vare = vvvare
     c     vvenl4_key2   setll     vvenl4
     c     vvenl4_key2   reade     vvenl4
     c                   dow       not %eof(vvenl4)
     c                   if        veinen <> 0
     c                   eval      b_funn = *on
     c                   goto      neste_vare
     c                   endif
      *
     c     vvenl4_key2   reade     vvenl4
     c                   enddo
      *
     c     neste_vare    tag
      *
     c                   if        b_funn = *off
     c                   exsr      skriv
     c                   endif
      *
     c     vvarl1_key    reade     vvarl1
     c                   enddo
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
6.21  *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å skrive på regneark
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv         begsr
      *
     c                   eval      rlevl1_levr = VVldor
     c     rlevl1_key    chain     rlevl1
      *
     c                   eval      exclin = *blank
     c                   eval      EXCLIN =  %trim(vvvare)       + X'05' +
     c                                     %trim(vvtek1)     + X'05' +
     c                                     %trim(vvenhe)     + X'05' +
     c                                     %char(vvldor)     + X'05' +
     c                                     %trim(rlnavn)     + X'05'
     c                   write     lwexpfr
6.21  *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     vvenl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl4_vare
     c                   kfld                    vvenl4_inen
      *
     c     vvenl4_key2   klist
     c                   kfld                    w_firm
     c                   kfld                    vvenl4_vare
      *
      * Key for oppslag på vare-register
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      exclin = *blank
     c                   eval      EXCLIN = 'Varenr'         + X'05' +
     c                                     'Varetekst'       + X'05' +
     c                                     'Basisenhet'      + X'05' +
     c                                     'Leverandør'      + X'05' +
     c                                     'Lev.navn'        + X'05'
     c                   write     lwexpfr
6.21  *
      *
     c                   endsr
