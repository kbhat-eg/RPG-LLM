     H/TITLE  ASOFAK: Oppretter standardverdier
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FA900R
      * Beskrivelse . . : Oppretter standardverdier
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * 6.10  250609 BSA  Oppdatert med sporingsinformasjon
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     FAFORLU    UF A E           K DISK    RENAME(AFORPFR:AFORLUR)
     FANUMLU    UF A E           K DISK    RENAME(ANUMPFR:ANUMLUR)
      *
     FFSTSLU    UF A E           K DISK    RENAME(FSTSPFR:FSTSLUR)
     FVLTYLU    UF A E           K DISK    RENAME(VLTYPFR:VLTYLUR)
     FVOTYLU    UF A E           K DISK    RENAME(VOTYPFR:VOTYLUR)
      *
     D RUT1            S             80    DIM(32) CTDATA PERRCD(1)              Rutiner-1
     D RUT2            S             80    DIM(32) CTDATA PERRCD(1)              Rutiner-2
     D NUM1            S             80    DIM(4) CTDATA PERRCD(1)               Nummer-serier
      *
     D STS1            S             80    DIM(1) CTDATA PERRCD(1)               Ofak-koder-1
     D STS2            S             80    DIM(1) CTDATA PERRCD(1)               Ofak-koder-2
     D LTY1            S             80    DIM(3) CTDATA PERRCD(1)               Linje-typer
     D OTY1            S             80    DIM(10) CTDATA PERRCD(1)              Ordre-typer
      *
     D                 DS
     D  A2REC1                 1     80
     D  A2RUTI                 1     10
     D  A2PROG                11     20
     D  A2TXT1                21     60
     D  A2FORM                61     70
     D  A2OUTQ                71     80
      *
     D                 DS
     D  A2REC2                 1     80
     D  A2ACPI                 1      2  0
     D  A2ALIN                 3      5  0
     D  A2OFLW                 6      8  0
     D  A2AKOP                 9     11  0
     D  A2HLDQ                12     12
     D  A2PRTY                13     13
     D  A2INST                14     14  0
     D  A2PRFI                15     15  0
     D  A2BGIR                16     16  0
     D* A2PGIR                17     17  0
     D* A2JOUR                18     18  0
     D  A2MVAK                19     19  0
     D  A2OCRK                20     20  0
      *
     D                 DS
     D  A3REC1                 1     80
     D  A3FELL                 1      6
     D  A3TYPE                 7      8
     D  A3FAST                 9     18
     D  A3TXT1                19     48
     D  A3WRAP                49     49
     D  A3ÅRNU                50     50
     D  A3AUTO                51     51
     D  A3NFOM                52     57  0
     D  A3NTOM                58     63  0
     D  A3SIST                64     69  0
     D  A3TXT2                70     80
      *
      *
     D                 DS
     D  F0REC1                 1     80
     D  F0AORK                 1      6
     D  F0AFAK                 7     12
     D  F0ADEL                13     18
     D  F0ALSM                19     24
     D  F0ABND                25     25
     D  F0AOTY                26     27
     D  F0ALTY                28     29
     D  F0ATTY                30     31
     D  F0AMVA                32     32
     D  F0ALAG                35     35
     D  F0AKUN                36     41
     D  F0AKNR                42     47
     D  F0AVNR                48     62
     D  F0ABIL                63     64
     D  F0ADES                65     65
     D  F0AGRN                66     71
     D  F0ASAK                72     72
     D  F0ASA1                73     73
     D  F0ASA2                74     74
      *
     D                 DS
     D  F0REC2                 1     80
     D  F0AKHV                 1      4
     D  F0AOT1                 5      6
     D  F0AOT2                 7      8
     D  F0AOT3                 9     10
     D  F0AOT4                11     12
     D  F0AOT5                13     14
     D  F0AOT6                15     16
     D  F0AOT7                17     18
     D  F0ARK1                19     19  0
     D  F0ARK2                20     20  0
     D  F0ARK3                21     21  0
     D  F0ARK4                22     22  0
     D  F0ARK5                23     23  0
     D  F0ARK6                24     24  0
     D  F0ARK7                25     25  0
     D  F0ARK8                26     26  0
      *
     D                 DS
     D  F1REC1                 1     80
     D  F1LTYP                 1      2
     D  F1LTXT                 3     32
     D  F1LKTX                33     33  0
     D  F1LPBB                34     34  0
     D  F1LLAG                35     35  0
     D  F1LØKO                36     36  0
     D  F1LSTA                37     37  0
     D  F1LKRE                38     38
      *
      *
     D                 DS
     D  F2REC1                 1     80
     D  F2OTYP                 1      2
     D  F2OTXT                 3     32
     D  F2OSYS                33     33  0
     D  F2OAKK                34     34  0
     D  F2OBOO                35     35  0
     D  F2OSKA                36     36  0
     D  F2OSAM                37     37  0
     D  F2OLAG                38     38  0
     D  F2OØKO                39     39  0
     D  F2OSTA                40     40  0
     D  F2OPRO                41     41  0
     D  F2OBUT                42     42
     D  F2OKRE                43     43
     D  F2OBIL                44     45
     D  F2ONXT                46     47
     D  F2ORUT                48     57
     D  F2ORUB                58     67
      *
     D                UDS
6.10 d l_user                911    920
     D  DSSFIR               944    946  0
     D  L_FNAV               951    980
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Oppretter standard RUTINER                                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C                   MOVE      *ZERO         I
     C     I             DOUEQ     32
     C                   ADD       1             I
     C                   MOVE      RUT1(I)       A2REC1
     C                   MOVE      RUT2(I)       A2REC2
      *
     C                   MOVE      A2RUTI        RUTNUM
     C     RUTKEY        CHAIN     AFORLUR                            90
      *
     C                   MOVE      A2TXT1        AFTXT1
     C                   MOVE      *BLANK        AFTXT2
     C                   MOVE      *BLANK        AFMLD1
     C                   MOVE      *BLANK        AFMLD2
     C                   MOVE      *BLANK        AFMLD3
     C                   MOVE      A2FORM        AFFORM
     C                   MOVE      A2OUTQ        AFOUTQ
     C                   MOVE      A2ACPI        AFACPI
     C                   MOVE      A2ALIN        AFALIN
     C                   MOVE      A2OFLW        AFOFLW
     C                   MOVE      A2AKOP        AFAKOP
     C                   MOVE      A2HLDQ        AFHLDQ
     C                   MOVE      A2PRTY        AFPRTY
     C                   MOVE      A2INST        AFINST
     C                   MOVE      A2PRFI        AFPRFI
     C                   MOVE      A2BGIR        AFBGIR
     C***                MOVE      A2PGIR        AFPGIR
     C***                MOVE      A2JOUR        AFJOUR
     C                   MOVE      A2MVAK        AFMVAK
     C                   MOVE      A2OCRK        AFOCRK
     C                   MOVE      A2PROG        AFPROG
      *
     C     WPVALG        IFEQ      '1'
     C     *IN90         IFEQ      *OFF
     C                   UPDATE    AFORLUR
     C                   END
     C                   END
      *
     C     *IN90         IFEQ      *ON
     C                   MOVE      RUTNUM        AFRUTI
     C                   WRITE     AFORLUR
     C                   END
      *
     C                   END
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Oppretter standard NUMMER-SERIER                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C                   MOVE      *ZERO         I
     C     I             DOUEQ     4
     C                   ADD       1             I
     C                   MOVE      NUM1(I)       A3REC1
      *
     C                   MOVE      A3FELL        NUMFEL
     C                   MOVE      A3TYPE        NUMTYP
     C                   MOVE      A3FAST        NUMFAS
     C     NUMKEY        CHAIN     ANUMLUR                            90
      *
     C                   MOVE      A3TXT1        ANTXT1
     C                   MOVEL     A3TXT2        ANTXT2
     C                   MOVE      A3WRAP        ANWRAP
     C                   MOVE      A3ÅRNU        ANÅRNU
     C                   MOVE      A3AUTO        ANAUTO
     C   90              MOVE      A3NFOM        ANNFOM
     C   90              MOVE      A3NTOM        ANNTOM
     C   90              MOVE      A3SIST        ANSIST
      *
     C     WPVALG        IFEQ      '1'
     C     *IN90         IFEQ      *OFF
     C                   UPDATE    ANUMLUR
     C                   END
     C                   END
      *
     C     *IN90         IFEQ      *ON
     C                   MOVE      NUMFIR        ANFIRM
     C                   MOVE      NUMFEL        ANFELL
     C                   MOVE      NUMTYP        ANTYPE
     C                   MOVE      NUMFAS        ANFAST
     C                   WRITE     ANUMLUR
     C                   END
      *
     C                   END
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Oppretter standard LINJETYPER                                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C                   MOVE      *ZERO         I
     C     I             DOUEQ     3
     C                   ADD       1             I
     C                   MOVE      LTY1(I)       F1REC1
      *
     C                   MOVE      F1LTYP        LTYTYP
     C     LTYKEY        CHAIN     VLTYLUR                            90
      *
     C                   MOVE      F1LTXT        VALTXT
     C                   MOVE      F1LKTX        VALKTX
     C                   MOVE      F1LPBB        VALPBB
     C                   MOVE      F1LLAG        VALLAG
     C                   MOVE      F1LØKO        VALOKO
     C                   MOVE      F1LSTA        VALSTA
     C                   MOVE      F1LKRE        VALKRE
     C                   MOVE      *BLANK        VALO01
     C                   MOVE      *BLANK        VALO02
     C                   MOVE      *BLANK        VALO03
     C                   MOVE      *BLANK        VALO04
      *
     C     WPVALG        IFEQ      '1'
     C     *IN90         IFEQ      *OFF
6.10 c                   time                    valeda
6.10 c                   time                    valeti
6.10 c                   eval      valeus = l_user
     C                   UPDATE    VLTYLUR
     C                   END
     C                   END
      *
     C     *IN90         IFEQ      *ON
     C                   MOVE      LTYFIR        VALFIR
     C                   MOVE      LTYTYP        VALTYP
6.10 c                   time                    valoda
6.10 c                   time                    valoti
6.10 c                   eval      valous = l_user
6.10 c                   time                    valeda
6.10 c                   time                    valeti
6.10 c                   eval      valeus = l_user
     C                   WRITE     VLTYLUR
     C                   END
      *
     C                   END
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Oppretter standard ORDRETYPER                                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C                   MOVE      *ZERO         I
     C     I             DOUEQ     10
     C                   ADD       1             I
     C                   MOVE      OTY1(I)       F2REC1
      *
     C                   MOVE      F2OTYP        OTYTYP
     C     OTYKEY        CHAIN     VOTYLUR                            90
      *
     C                   MOVE      F2OTXT        VAOTXT
     C                   MOVE      F2OSYS        VAOSYS
     C                   MOVE      F2OAKK        VAOAKK
     C                   MOVE      F2OBOO        VAOBOO
     C                   MOVE      F2OSKA        VAOSKA
     C                   MOVE      F2OSAM        VAOSAM
     C                   MOVE      F2OLAG        VAOLAG
     C                   MOVE      F2OØKO        VAOOKO
     C                   MOVE      F2OSTA        VAOSTA
     C                   MOVE      F2OPRO        VAOPRO
     C                   MOVE      F2OBUT        VAOBUT
     C                   MOVE      F2OKRE        VAOKRE
     C                   MOVE      F2OBIL        VAOBIL
     C                   MOVE      F2ONXT        VAONXT
     C                   MOVE      F2ORUT        VAORUT
     C                   MOVE      F2ORUB        VAORUB
      *
     C     WPVALG        IFEQ      '1'
     C     *IN90         IFEQ      *OFF
6.10 c                   time                    vaoeda
6.10 c                   time                    vaoeti
6.10 c                   eval      vaoeus = l_user
     C                   UPDATE    VOTYLUR
     C                   END
     C                   END
      *
     C     *IN90         IFEQ      *ON
     C                   MOVE      OTYFIR        VAOFIR
     C                   MOVE      OTYTYP        VAOTYP
6.10 c                   time                    vaooda
6.10 c                   time                    vaooti
6.10 c                   eval      vaoous = l_user
6.10 c                   time                    vaoeda
6.10 c                   time                    vaoeti
6.10 c                   eval      vaoeus = l_user
     C                   WRITE     VOTYLUR
     C                   END
      *
     C                   END
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Oppretter standard OFAK-KODER                                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C                   MOVE      STS1(01)      F0REC1
     C                   MOVE      STS2(01)      F0REC2
      *
     C     STSKEY        CHAIN     FSTSLUR                            90
      *
     C                   MOVE      F0AORK        FAAORK
     C                   MOVE      F0AFAK        FAAFAK
     C                   MOVE      F0ADEL        FAADEL
     C                   MOVE      F0ALSM        FAALSM
     C                   MOVE      F0ABND        FAABND
     C                   MOVE      F0AOTY        FAAOTY
     C                   MOVE      F0ALTY        FAALTY
     C                   MOVE      F0ATTY        FAATTY
     C                   MOVE      F0AMVA        FAAMVA
     C                   MOVE      F0ALAG        FAALAG
      *
     C                   MOVE      F0AVNR        FAAVNR
     C                   MOVE      F0ABIL        FAABIL
     C                   MOVE      F0ADES        FAADES
     C                   MOVE      F0AGRN        FAAGRN
     C                   MOVE      F0ASAK        FAASAK
     C                   MOVE      F0ASA1        FAASA1
     C                   MOVE      F0ASA2        FAASA2
      *
     C                   MOVE      F0AKHV        FAAKHV
     C                   MOVE      F0AOT1        FAAOT1
     C                   MOVE      F0AOT2        FAAOT2
     C                   MOVE      F0AOT3        FAAOT3
     C                   MOVE      F0AOT4        FAAOT4
     C                   MOVE      F0AOT5        FAAOT5
     C                   MOVE      F0AOT6        FAAOT6
     C                   MOVE      F0AOT7        FAAOT7
     C                   MOVE      F0ARK1        FAARK1
     C                   MOVE      F0ARK2        FAARK2
     C                   MOVE      F0ARK3        FAARK3
     C                   MOVE      F0ARK4        FAARK4
     C                   MOVE      F0ARK5        FAARK5
     C                   MOVE      F0ARK6        FAARK6
     C                   MOVE      F0ARK7        FAARK7
     C                   MOVE      F0ARK8        FAARK8
      *
     C                   MOVE      WPKUND        FAAKUN
     C                   MOVE      WPKUND        FAAKNR
      *
     C     WPVALG        IFEQ      '1'
     C     *IN90         IFEQ      *OFF
6.10 c                   time                    faaeda
6.10 c                   time                    faaeti
6.10 c                   eval      faaeus = l_user
     C                   UPDATE    FSTSLUR
     C                   END
     C                   END
      *
     C     *IN90         IFEQ      *ON
     C                   MOVE      STSFIR        FAAFIR
6.10 c                   time                    faaoda
6.10 c                   time                    faaoti
6.10 c                   eval      faaous = l_user
6.10 c                   time                    faaeda
6.10 c                   time                    faaeti
6.10 c                   eval      faaeus = l_user
     C                   WRITE     FSTSLUR
     C                   END
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSLUTT        TAG
      *
     C                   SETON                                        LR
     C                   RETURN
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     CSR   *INZSR        BEGSR
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     C                   MOVE      *ZERO         I                 2 0
      *
      *  Redefinering av parameterfeltene
      *
     C     *LIKE         DEFINE    FAAKUN        WPKUND
      *
      *  Redefinering av nøkler
      *
     C     *LIKE         DEFINE    AFRUTI        RUTNUM
      *
     C     *LIKE         DEFINE    ANFIRM        NUMFIR
     C     *LIKE         DEFINE    ANFELL        NUMFEL
     C     *LIKE         DEFINE    ANTYPE        NUMTYP
     C     *LIKE         DEFINE    ANFAST        NUMFAS
      *
     C     *LIKE         DEFINE    FAAFIR        STSFIR
      *
     C     *LIKE         DEFINE    VALFIR        LTYFIR
     C     *LIKE         DEFINE    VALTYP        LTYTYP
      *
     C     *LIKE         DEFINE    VAOFIR        OTYFIR
     C     *LIKE         DEFINE    VAOTYP        OTYTYP
      *
      *  Key for file : FORMULAR-RUTINE-REGISTER
      *
     C     RUTKEY        KLIST
     C                   KFLD                    RUTNUM
      *
      *  Key for file : NUMMER-REGISTER
      *
     C     NUMKEY        KLIST
     C                   KFLD                    NUMFIR
     C                   KFLD                    NUMFEL
     C                   KFLD                    NUMTYP
     C                   KFLD                    NUMFAS
      *
      *  Key for file : OFAK-KODE-REGISTER
      *
     C     STSKEY        KLIST
     C                   KFLD                    STSFIR
      *
      *  Key for file : LINJETYPE-REGISTER
      *
     C     LTYKEY        KLIST
     C                   KFLD                    LTYFIR
     C                   KFLD                    LTYTYP
      *
      *  Key for file : ORDRETYPE-REGISTER
      *
     C     OTYKEY        KLIST
     C                   KFLD                    OTYFIR
     C                   KFLD                    OTYTYP
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    WPVALG            1
     C                   PARM                    WPKUND
      *
      *  Legger inn firmanummer i nøkklene
      *
     C                   MOVE      DSSFIR        NUMFIR
     C                   MOVE      DSSFIR        STSFIR
     C                   MOVE      DSSFIR        LTYFIR
     C                   MOVE      DSSFIR        OTYFIR
      *
     CSR                 ENDSR
      *
**     R U T I N E R  (1)
FTILB     FO910C    Tilbud                                  TILB      *JOB
FOBEK     FO920C    Ordrebekreftelse                        OBEK      *JOB
FPLUKK    FO925C    Plukkliste                              PLUK      *JOB
FPAKK     FO930C    Pakkseddel                              PAKK      *JOB
FPAKKNEG  FO930C    Pakkseddel Retur                        PAKK      *JOB
FFAKT     FO940C    Faktura                                 FAKT      *JOB
FKRED     FO940C    Kreditnota                              FAKT      *JOB
FBEST     FO980C    Bestilling                              BEST      *JOB
FO950     FO950R    Frittstående bankgiro                   BANK      *JOB
FR612               Rabattmatrise, Kundebrev                1XA4      *JOB
FR602               Rabattmatrise, Matrise                  0001      *JOB
FR604               Rabattmatrise, Kryssreferanse           0001      *JOB
VG601               Varegruppeplan                          1XA4      *JOB
VP601               Prisbok                                 1XA4      *JOB
VP612               Prisbok til kunder                      1XA4      *JOB
FH601               Navneetiketter fra kunde distr.liste    ETIK      *JOB
FP213     FP213R    Pris  - etikett       - standard        PRIS      *JOB
FP214     FP214R    Hylle - etikett  75mm - standard        HYLLE     *JOB
FP215     FP215R    Hylle - etikett 100mm - standard        HYLLE100  *JOB
FP216     FP216R    Reol  - etikett       - standard        REOL      *JOB
FP226     FP226R    Pris  - etikett       - standard        PRIS      *JOB
FP236     FP236R    Pris  - etikett       - standard        PRIS      *JOB
VV603               Vare - vare-/prisinformasjon            0001      *JOB
VV605               Vare - konto-opplysninger               0001      *JOB
VV611               Vare - dekning pr. varegruppe           0001      *JOB
VV621               Vare - scanning                         1XA4      *JOB
VT601               Tilbudspriser                           1XA4      *JOB
FW601               Varer fra leverandør                    0001      *JOB
FW621               Oppretting av nye varer fra leverandør  0001      *JOB
FW626               Oppdatering av varer fra leverandør     0001      *JOB
FP601               Spesialpriser                           0001      *JOB
VV606               Vare - lager-opplysninger               0001      *JOB
**     R U T I N E R  (2)
15072068001000000000
15072068001000000000
15072068001000000000
15072068001000000000
15072068001000000000
10072068001000010101
10072068001000010100
15072068001000010100
10024024001000010100
15070058001000000000
15048045001000000000
15048045001000000000
15070065001000000000
15070066001000000000
15070066001000000000
10072072001000000000
10072072001000000000
10072072001000000000
10072072001000000000
10072072001000000000
10072072001000000000
10072072001000000000
15048045001000000000
15048045001000000000
15048045001000000000
10072068001000000000
12070065001000000000
15048045001000000000
15048045001000000000
15048045001000000000
15048045001000000000
15048045001000000000
**     N U M M E R - K O D E R
ORDRE             Ordrenummer, fellesserie      10A000000999999000000Ordre/fakt.
FBILAG            Bilagsnummer,fellesserie      10A000000999999000000Ordre/fakt.
FSLETT            Slettenummer,fellesserie      10A000000999999000000Ordre/fakt.
FLISTE            Listenummer, fellesserie      10A000000999999000000Ordre/fakt.
**     O F A K   S T A T U S   K O D E R  (1)
ORDRE FBILAGFSLETTFLISTENO   TX0  0000000000000               002000000000
**     O F A K   S T A T U S   K O D E R  (2)
OFAKT O P F K SPOR11110000
**     L I N J E - T Y P E R
  Vanlig varelinje              00111
- Negativ varelinje             00111-
TXTekstlinje                    10000
**     O R D R E - T Y P E R
T Tilbud                        0000000000 00O FTILB
SPPrisordre                     0000000001 00P FTILB
SÅÅrsavtale                     0000000001 00P FTILB
OROrdre, Rekvisisjon            0000000000-00  FBEST
O Ordrebekreftelse              0100010000 00P FOBEK
PLPlukkliste                    0100010000 00P FPLUKK    BPLUKK
P Pakkseddel                    0200010001 00F FPAKK     BPAKK
PNPakkseddel, negativ           0200010001-00K FPAKKNEG  BPAKKNEG
F Faktura                       0300111100 01  FFAKT     BFAKT
K Kreditnota                    0300011100-03  FKRED     BKRED
