      /TITLE  ASLAGR:  Håndterminalrutiner
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : HK701R
      * Beskrivelse . . : Kundeinfo til håndterminal, produsere fil METO
      *
      *                   Produsere register for ekstern scanning av
      *                   kunderegister.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       180500 ASK  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fhbkupf    o  a e           k disk    rename(hbkupfr:hbkupfr)
      *
      * Kundeinformasjon til fil
      *
     d                 ds
     d d_krec                        37
     d  d_kund                        6  0 overlay(d_krec)
     d  d_navn                       30    overlay(d_krec:7)
     d  d_kode                        1    overlay(d_krec:37)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(rkfirm)
     d p_kat1          s              2
     d p_kat2          s              2
     d p_kat3          s              2
     d p_kat4          s              2
     d p_kat5          s              2
      *
      * Definering av variabler i nøkler
      *
     d rkunl1_kund     s                   like(rkkund)
      *
      * Definering av variable
      *
     d w_firm          s                   like(rkfirm)
     d w_kata          s              2
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Tester om noen kategori er valgt
      *
     c                   if        p_kat1 = *blank and
     c                             p_kat2 = *blank and
     c                             p_kat3 = *blank and
     c                             p_kat4 = *blank and
     c                             p_kat5 = *blank
     c                   goto      end_pgm
     c                   endif
      *
      * Leser kunder og tester om valgt kategori
      *
     c                   eval      rkunl1_kund = 0
     c     rkunl1_key    setll     rkunl1
     c                   read      rkunl1                                 90
     c                   dow       *in90 = *off
      *
     c                   move      rkkatg        w_kata
     c                   if        p_kat1 = 'AL' or
     c                             w_kata = p_kat1 or
     c                             w_kata = p_kat2 or
     c                             w_kata = p_kat3 or
     c                             w_kata = p_kat4 or
     c                             w_kata = p_kat5
     c                   exsr      behandling
     c                   endif
      *
     c                   read      rkunl1                                 90
     c                   enddo
      *
      * Avslutt
      *
     c     end_pgm       tag
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av vare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
      * Oppretter rad i eksternt scanne-register
      *
     c                   clear                   hbkupfr
      *
     c                   eval      d_kund = rkkund
     c                   eval      d_navn = rknavn
     c                   eval      d_kode = '*'
     c                   eval      hkinfo = d_krec
     c                   write     hbkupfr
      *
     c     end_behandl   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kat1
     c                   parm                    p_kat2
     c                   parm                    p_kat3
     c                   parm                    p_kat4
     c                   parm                    p_kat5
      *
      * Key for les på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
