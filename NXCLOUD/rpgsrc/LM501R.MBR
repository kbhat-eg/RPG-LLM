     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : LM501R
      * Beskrivelse . . : Logg-fil, hent tilbake siste loggkode
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
6.20  * 6.20  070312 bsa  Nytt program
6.nu  * 6.30  110614 bof  Sjekket mtp. nummerutvidelse
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fllogl3    if   e           k disk    rename(llogpfr:llogl3r)
     flloglr    if   e           k disk    rename(llogpfr:lloglrr)
     fllokl1    if   e           k disk    rename(llokpfr:llokl1r)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(lmfirm)
     d p_numm          s                   like(lmnumm)
     d p_suff          s                   like(lmsuff)
     d s_logk          s                   like(lmkode)
     d p_logk          s                   like(lmkode)
     d p_lbes          s                   like(lntext)
      *
      * Definering av variabler i nøkler
      *
     d llogl3_numm     s                   like(lmnumm)
     d llogll_numm     s                   like(lmnumm)
     d llogl3_date     s                   like(lmdate)
     d llogl3_time     s                   like(lmtime)
     d lloglr_numm     s                   like(lmnumm)
     d lloglr_suff     s                   like(lmsuff)
     d lloglr_kode     s                   like(lmkode)
     d lloglr_date     s                   like(lmdate)
     d lloglr_time     s                   like(lmtime)
     d llokl1_type     s                   like(lntype)
      *
      * Definering av variable
      *
     d w_firm          s                   like(lmfirm)
      *
      * Definering av konstanter
      *
      *----------------------------------------------------
      * Start lesing av loggkoder                         *
      *----------------------------------------------------
      *
     c                   eval      llogl3_date = *loval
     c                   eval      llogl3_time = *loval
     c     llogl3_key    setll     llogl3
     c     llogl3_kel    reade     llogl3
     c                   dow       not %eof(llogl3)
      *
     c                   eval      s_logk = lmkode
     c                   eval      p_logk = lmkode
      *
     c     llogl3_kel    reade     llogl3
     c                   enddo
      *
      * Hent inn tekst hvis kode er funnet
      *
     c                   eval      p_lbes = *blank
     c                   if        s_logk <> *blanks
     c                   eval      llokl1_type = s_logk
     c     llokl1_key    chain     llokl1r
     c                   if        %found
     c                   eval      p_lbes = lntext
     c                   endif
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_logk
     c                   parm                    p_lbes
      *
      * Key for posisjonering på dato
      *
     c     llogl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    llogl3_numm
     c                   kfld                    llogl3_date
     c                   kfld                    llogl3_time
      *
      * Key for les
      *
     c     llogl3_kel    klist
     c                   kfld                    w_firm
     c                   kfld                    llogll_numm
      *
      * Key for lesing av logg-detaljer
      *
     c     lloglr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lloglr_numm
     c                   kfld                    lloglr_suff
     c                   kfld                    lloglr_kode
     c                   kfld                    lloglr_date
     c                   kfld                    lloglr_time
      *
      * Key for lesing av kodefil
      *
     c     llokl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    llokl1_type
      *
      * Henter parametre, og initierer skjermbildet
      *
     c                   eval      w_firm = p_firm
     c                   eval      llogl3_numm = p_numm
     c                   eval      llogll_numm = p_numm
      *
     c                   eval      p_logk = *blanks
     c                   eval      p_lbes = *blanks
      *
     c                   endsr
