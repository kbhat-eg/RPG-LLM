      /TITLE  ASLAGR: Lagertelling
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  System. . . . . : ASLAGR
      *  Program . . . . : LE620R
      *  Beskrivelse . . : Utskrift av diversevarer, telling
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Spesialversjon. :
      *  Tilpasset for . :
      *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign
      * --------- ------  -------------------------------------------------
7.xx  * 7.00 190516  bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
8.01  * 800  161024  ask  Lagt inn mulighet for å kalle opp pgm med batchid
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fle620d    cf   e             workstn
      *
      *  Definering av datastrukturer
      *
     d                 ds
      *                                                      Filemember
     d d_lmbr                        10
     d  d_lbok                        1    overlay(d_lmbr)
     d  d_firm                        3  0 overlay(d_lmbr:2)
     d  d_llst                        6  0 overlay(d_lmbr:5)
      *
     d                 ds
      *                                                      Parameter-inn
     d d_prec                       128
     d  d_batc                       10    overlay(d_prec)
     d  d_fogr                        2  0 overlay(d_prec:11)
     d  d_togr                        2  0 overlay(d_prec:13)
     d  d_fhgr                        2  0 overlay(d_prec:15)
     d  d_thgr                        2  0 overlay(d_prec:17)
     d  d_fugr                        3  0 overlay(d_prec:19)
     d  d_tugr                        3  0 overlay(d_prec:22)
     d  d_fvar                       15    overlay(d_prec:25)
     d  d_tvar                       15    overlay(d_prec:40)
     d  d_flag                        2  0 overlay(d_prec:55)
     d  d_tlag                        2  0 overlay(d_prec:57)
     d  d_sort                        1  0 overlay(d_prec:59)
      *
      *  Local Data Area
      *
8.01 d lda            uds
     d l_firm                944    946  0
     d l_fnav                951    980
8.01 d l_batc               1006   1015
      *
      *  Definering av variable
      *
     d w_parm          s            128
      *
     d w_batc          s             10
     d w_numm          s              6  0
     d w_qnnn          s              1
     d w_retu          s              1
     d w_valg          s              1
     d w_firm          s              3  0
     d w_lage          s              2  0
     d w_in03          s              1
     d w_vare          s             15
     d w_vtxt          s             35
     d w_otxt          s             35
     d w_htxt          s             35
     d w_utxt          s             35
     d w_latx          s             30
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  A1 Kaller opp subprogram for inntasting av batchnummer
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     a1tag         tag
      *
      *  Kaller opp subprogrammet
      *
8.01 c                   if        l_batc <> ' '
8.01 c                   eval      w_batc = l_batc
8.01 c                   else
     c                   eval      w_retu = 'L'
     c                   call      'LC500R'
     c                   parm                    w_retu
     c                   parm                    w_batc
      *
     c                   if        w_retu <> *blank
     c                   goto      slutt
     c                   endif
8.01 c                   endif
      *
     c                   eval      d_batc = w_batc
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Behandle bilde for registrering av parametre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     b1tag         tag
      *
      *  Nullstiller variable som skal vises i skjermbildet
      *
     c                   eval      b1fogr = *zero
     c                   eval      b1togr = *zero
     c                   eval      b1fhgr = *zero
     c                   eval      b1thgr = *zero
     c                   eval      b1fugr = *zero
     c                   eval      b1tugr = *zero
     c                   eval      b1fvar = *blank
     c                   eval      b1tvar = *blank
     c                   eval      b1flag = *zero
     c                   eval      b1tlag = 99
      *
     c     b1taga        tag
      *
     c     b1tagb        tag
     c                   exfmt     b1bld
      *
      *  F3=Gå tilbake  (Avslutt)
      *
     c                   if        *inkc = *on
     c                   eval      w_in03 = '1'
     c                   goto      slutt
     c                   endif
      *
      *  F4=Spørring
      *
     c                   if        *inkd
     c                   if        wactre = 'B1BLD'
      *
      *  Spørring i vareregister
      *
     c                   if        wactfe = 'B1FVAR'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   movel     w_vare        b1fvar
     c                   goto      b1tagb
     c                   endif
      *
     c                   if        wactfe = 'B1TVAR'
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   movel     w_vare        b1tvar
     c                   goto      b1tagb
     c                   endif
      *
      *  Spørring i lagerregister
      *
     c                   if        wactfe = 'B1FLAG'
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    w_lage
     c                   parm                    w_latx
     c                   move      w_lage        b1flag
     c                   goto      b1tagb
     c                   endif
      *
     c                   if        wactfe = 'B1TLAG'
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    w_lage
     c                   parm                    w_latx
     c                   move      w_lage        b1tlag
     c                   goto      b1tagb
     c                   endif
      *
      *  Spørring i overgrupperegister
      *
     c                   if        wactfe = 'B1FOGR'
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b1fogr
     c                   parm                    w_otxt
     c                   goto      b1tagb
     c                   endif
      *
     c                   if        wactfe = 'B1TOGR'
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b1togr
     c                   parm                    w_otxt
     c                   goto      b1tagb
     c                   endif
      *
      *  Spørring i hovedgrupperegister
      *
     c                   if        wactfe = 'B1FHGR'
 001 C                   call      'VG511R'
 001 C                   parm                    w_firm
 001 C                   parm                    b1fogr
 001 C                   parm                    b1fhgr
 001 C                   parm                    w_htxt
     c                   goto      b1tagb
     c                   endif
      *
     c                   if        wactfe = 'B1THGR'
 001 C                   call      'VG511R'
 001 C                   parm                    w_firm
 001 C                   parm                    b1togr
 001 C                   parm                    b1thgr
 001 C                   parm                    w_htxt
     c                   goto      b1tagb
     c                   endif
      *
      *  Spørring i undergrupperegister
      *
     c                   if        wactfe = 'B1FUGR'
 001 C                   call      'VG512R'
 001 C                   parm                    w_firm
 001 C                   parm                    b1fogr
 001 C                   parm                    b1fhgr
 001 C                   parm                    b1fugr
 001 C                   parm                    w_utxt
     c                   goto      b1tagb
     c                   endif
      *
     c                   if        wactfe = 'B1TUGR'
 001 C                   call      'VG512R'
 001 C                   parm                    w_firm
 001 C                   parm                    b1togr
 001 C                   parm                    b1thgr
 001 C                   parm                    b1tugr
 001 C                   parm                    w_utxt
     c                   goto      b1tagb
     c                   endif
      *
     c                   endif
      *
     c                   endif
      *
      *  Legger inntastede opplysninger i Hjelpe-felter for Parameter
      *
     c                   eval      d_fogr = b1fogr
     c                   eval      d_togr = b1togr
     c                   eval      d_fhgr = b1fhgr
     c                   eval      d_thgr = b1thgr
     c                   eval      d_fugr = b1fugr
     c                   eval      d_tugr = b1tugr
     c                   eval      d_fvar = b1fvar
     c                   eval      d_tvar = b1tvar
     c                   eval      d_flag = b1flag
     c                   eval      d_tlag = b1tlag
      *
      *  Dersom overgruppe til=0 skal t.o.m. alle overgrupper velges
      *
     c                   if        d_togr = *zero
     c                   eval      d_togr = 99
     c                   endif
      *
      *  Dersom hovedgruppe til=0 skal t.o.m. alle hovedgrupper velges
      *
     c                   if        d_thgr = *zero
     c                   eval      d_thgr = 99
     c                   endif
      *
      *  Dersom undergruppe til=0 skal t.o.m. alle undergrupper velges
      *
     c                   if        d_tugr = *zero
     c                   eval      d_tugr = 999
     c                   endif
      *
      *  Dersom varenummer til=0 skal t.o.m. alle varenummer velges
      *
     c                   if        b1tvar = *blank
     c                   eval      d_tvar = '99999999'
     c                   eval      d_tvar = '99999999'
     c                   endif
      *
     c                   eval      d_sort = b1sort
      *
     c                   eval      w_parm = d_prec
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
8.01  *
8.01  * Definisjon av local data area for output
8.01  *
8.01 c     *dtaara       define    *lda          lda
      *
     c                   eval      w_batc = *blank
     c                   eval      w_retu = *blank
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    w_in03
     c                   parm                    w_parm
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     c                   eval      w_valg = *zero
     c                   eval      w_qnnn = *zero
     c                   eval      w_numm = *zero
     c                   eval      b1sort = 1
      *
     c                   eval      d_firm = l_firm
     c                   eval      w_firm = l_firm
      *
     c                   endsr
