      /TITLE
     h option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO706r
      * Beskrivelse . . : Finner totaler pr. plukket ordre med og uten mva.
      *                   ut fra ordrelinjer
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       270417 bof  Nytt program
8.01  *PRG2   210622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     flwpll1    if   e           k disk    rename(lwplpfr:lwpll1r)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
      *
6.10  * Definering av Local data area
6.10  *
6.10 d                uds
6.10 d l_user                911    920
6.12  *
6.12  * Definering av array's
6.12  *
6.12 d mko             s              1    dim(9)
6.12 d msa             s              5  2 dim(9)
6.12 d mgr             s             11  2 dim(9)
6.12 d mbe             s             11  2 dim(9)
6.12 d mog             s             11  2 dim(9)
6.12 d mor             s             11  2 dim(9)
      *
      * Definering av parametere
      *
     d p_firm          s                   like(fdfirm)
     d p_numm          s                   like(fdnumm)
     d p_suff          s                   like(fdsuff)
     d p_sumi          s                   like(fototx)
     d p_sumu          s                   like(fototx)
      *
      * Definering av variable i nøkler
      *
     d vltyl1_ltyp     s                   like(valtyp)
     d fodtl1_numm     s                   like(fdnumm)
     d fodtl1_suff     s                   like(fdsuff)
     d lwpll1_numm     s                   like(mknumm)
     d lwpll1_suff     s                   like(mksuff)
     d vvarl1_vare     s                   like(vvvare)
      *
      * Definering av variable
      *
6.23 d m               s              2  0
6.23 d w_mvas          s              6  2
6.23 d w_mvat          s              5  4
6.23 d w_mvaf          s             10  9
6.23 d w_bpro          s              5  2
6.23 d w_mvtx          s             30
6.12 d w_mvak          s              1
6.12 d w_stat          s              1
      *
     d w_firm          s                   like(fofirm)
     d w_toti          s                   like(fototr)
     d w_mvab          s                   like(fototr)
     d w_npri          s                   like(fototr)
     d w_pris          s                   like(fototr)
     d w_tnet          s                   like(fototr)
     d w_xmbe          s                   like(fototr)
     d w_dato          s                   like(foedat)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c                   eval      w_tnet = 0
     c                   eval      p_sumi = 0
     c                   eval      p_sumu = 0
      *
      * Lese ordrelinjer og summer opp
      *
     c                   eval      lwpll1_numm = p_numm
     c                   eval      lwpll1_suff = p_suff
     c     lwpll1_key    setll     lwpll1
     c     lwpll1_key    reade     lwpll1                                 90
      *
     c                   dow       not %eof(lwpll1)
      * les linjen
     c                   eval      fodtl1_numm = mknumm
     c                   eval      fodtl1_suff = mksuff
     c     fodtl1_key    chain     fodtl1
     c                   if        %found(fodtl1)
      *
      * Hente linjetype
      *
     c                   eval      vltyl1_ltyp = fdltyp
     c     vltyl1_key    chain     vltyl1r                            91
      *
      * Summer opp dersom det ikke er tekstlinjer
      *
     c                   if        valktx = 0
      *
     c                   eval      vvarl1_vare = fdvare
     c     vvarl1_key    chain     vvarl1
      *
     c                   eval      w_npri = fdsums - fdsumr
     c                   eval      w_pris = w_npri / fdanta
     c                   eval      w_tnet = w_tnet + (w_pris * mkantp)
      *
5.50 c                   if        fdomva <> *blank
5.50 c                   eval      w_stat = *blank
5.50 c                   eval      w_mvak = fdomva
5.50 c                   time                    w_dato
5.50  *
5.50 c                   call      'RS205R'
5.50 c                   parm                    w_stat
5.50 c                   parm                    w_mvak
5.50 c                   parm                    w_mvtx
5.50 c                   parm                    w_dato
5.50 c                   parm                    w_mvas
5.50 c                   parm                    w_mvat
5.50 c                   parm                    w_mvaf
5.50 c                   parm                    w_bpro
5.50  *
5.50 c                   if        w_mvas > 0
5.50 c                   eval(h)   w_mvab = w_mvab + (w_npri * w_mvas / 100)
6.12 c                   exsr      xarray
5.50 c                   endif
5.50 c                   endif
     c                   endif
     c                   endif
     c     lwpll1_key    reade     lwpll1                                 90
      *
     c                   enddo
      *
     c                   eval      p_sumu = w_tnet
6.12  *
6.12  * Beregner mva for de forskjellige satser
6.12  *
6.12 c                   eval      mbe = (mgr - mor) * msa / 100
6.12 c                   xfoot     mbe           w_xmbe
6.12 c                   eval      p_sumi = w_tnet + w_xmbe
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      *inlr = *on
     c                   return
6.12  *
6.12  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.12  * Subrutine for array-håndtering av mva
6.12  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.12  *
6.12 c     xarray        begsr
6.12  *
6.12 c     1             do        9             m
6.12 c                   if        mko(m) = w_mvak or
6.12 c                             mko(m) = ' '
6.12 c                   eval      mko(m) = w_mvak
6.12 c                   eval      msa(m) = w_mvas
6.12 c                   eval      mgr(m) = mgr(m) + w_npri
6.12  *
6.12 c                   if        forabp > 0
6.12 c                   if        vvkord = 0
6.12 c                   eval      mog(m) = mog(m) + w_npri
6.12 c                   eval      mor(m) = mog(m) * forabp / 100
6.12 c                   endif
6.12 c                   endif
6.12  *
6.12 c                   leave
6.12 c                   endif
6.12 c                   enddo
6.12  *
6.12 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_firm
6.nu c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_sumi
     c                   parm                    p_sumu
      *
      * Key for oppslag på linjetype
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
      * Key for oppslag på ordrelinje
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fodtl1_numm
     c                   kfld                    fodtl1_suff
6.40  *
6.40  * Key for oppslag på plukkfilen
6.40  *
6.40 c     lwpll1_key    klist
6.40 c                   kfld                    w_firm
6.40 c                   kfld                    lwpll1_numm
6.40 c                   kfld                    lwpll1_suff
      *
      * Key for oppslag på vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
      * Legg inn firmanummer
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
