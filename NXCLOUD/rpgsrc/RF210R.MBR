     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RF210R
      * Beskrivelse . . : Kontaktpersoner, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       300505 KOB  Nytt program
      * V5.40 230306 KOB  Lagt inn test på personnummer <> 0 ved kontroll
      * V5.50 080806 HFL  Bruker nye logiske filer for å behandle
      *                   personer med både kunde- og leverandørnr.
6.10  * R6.10 230909 BSA  Lagt inn sporingsfelter
6.11  * R6.11 170611 NHO  Feil nøkkel ved chain av leverandør
6.12  * R6.12 160413 AMD  Flyttet kodelinjer da variabler ble ødelagt
6.12  *                   av lesing av tabell. Kontaktperson havnet da
6.12  *                   på feil kunde / leverandør + justert litt
7.xx  * 7.00  240216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  240216 bsa  Returnerer feil opprettelse av ny kontaktperson.
Q.02  * 7.00  280319 bsa  Quick wins - mulighet til å kopiere personer
7.02  *       260619 ASK  Lagt inn kobling til tilgangsregister for kontaktpersoner
7.03  *       260619 bsa  Sjekker mot gyldig intervall ved oprpettelse
      *
8.01  * K0000 170921 bsa  Mulighet for søking på rolle.
8.02  * 800   081221 ask  Snur test på intervall kontroll
8.03  * 800   160223 ask  Endret skjermstørrelse
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frukplr    if   e           k disk    rename(rukppfr:rukplrr)
E5.50frukpl6    if   e           k disk    rename(rukppfr:rukpl6r)
T5.50frukpl7    if   e           k disk    rename(rukppfr:rukpl7r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frukplu    uf a e           k disk    rename(rukppfr:rukplur)
     frukpl3    if   e           k disk    rename(rukppfr:rukpl3r)
T5.20f                                     prefix(x:1)
     frukpl4    if   e           k disk    rename(rukppfr:rukpl4r)
T5.20f                                     prefix(x:1)
Q.02 frukpl1    if   e           k disk    rename(rukppfr:rukpl1r)
      *
     frf210d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(rufirm)
     d p_kund          s                   like(rukund)
     d p_levr          s                   like(rulevr)
      *
      * Definering av local data area
      *
     d                uds
     d l_user                911    920
7.02 d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d rukplr_kund     s                   like(rukund)
     d rukplr_levr     s                   like(rulevr)
     d rukplr_kpnr     s                   like(rukpnr)
E5.50d rukpl6_kund     s                   like(rukund)
E5.50d rukpl6_kpnr     s                   like(rukpnr)
T5.50d rukpl7_levr     s                   like(rulevr)
T5.50d rukpl7_kpnr     s                   like(rukpnr)
     d rukplu_kund     s                   like(rukund)
     d rukplu_levr     s                   like(rulevr)
     d rukplu_kpnr     s                   like(rukpnr)
     d rukpl3_alfa     s                   like(rualfa)
     d rukpl4_penr     s                   like(rupenr)
     d rkunl1_kund     s                   like(rkkund)
     d rlevl1_levr     s                   like(rllevr)
Q.02 d rukpl1_kund     s                   like(rukund)
Q.02 d rukpl1_levr     s                   like(rulevr)
Q.02 d rukpl1_kpnr     s                   like(rukpnr)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
     d w_knav          s                   like(rknavn)
     d w_kftp          s              2  0
     d w_ktpn          s             25
      *
     d w_seqe          s              1
     d w_retk          s              1
     d w_4pnr          s              6  0
     d b_valg_ok       s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_oppr          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
      *
     d w_firm          s                   like(rufirm)
     d w_kund          s                   like(rukund)
     d w_levr          s                   like(rulevr)
T5.50d s_kund          s                   like(rukund)
T5.50d s_levr          s                   like(rulevr)
     d w_kpnr          s                   like(rukpnr)
     d w_alfa          s                   like(rualfa)
     d w_kort          s             16
     d w_ddat          s               d   datfmt(*iso)
     d w_adr1          s             30
     d w_adr2          s             30
     d w_sted          s             30
     d w_kprs          s             30
     d w_tlfa          s             15
     d w_tlfp          s             15
     d w_tlfm          s             15
     d w_mail          s             50
     d w_orka          s              3  0
     d w_okun          s              6  0
     d w_okpr          s              6  0
     d w_opda          s               d
     d w_proj          s              6  0
     d w_posi          s              2  0
     d w_modu          s             10
     d w_disp          s              1
     d w_stat          s              1
8.01 d w_obes          s             20
      *
Q.02 d s_kpnr          s                   like(rukpnr)
      *
       dcl-s co402_verdi1  char(1);
       dcl-s co402_verdi2  char(2048);
       DCL-PR CO402R EXTPGM('CO402R');
         p_filgrp            char(3)     const;
         p_firm              packed(3:0) const;
         p_lager             packed(2:0) const;
         p_nokkel            char(50)    const;
         p_verdi1            char(1);
         p_verdi2            char(2048);
       END-PR;
7.02 d u_702           s              1    inz(*off)
7.03 d u_703           s              1    inz(*off)

      *
      * Definering av konstanter
      *
     d Lo              c                   'abcdefghijklmnopqrstuvwxyzæøå'
     d Up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅ'
      *
     d c_sfil          s              2  0 inz(13)
     d c_digi          c                   '0123456789 '
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      bc2bld
     c                   exsr      xc1bld
7.01 c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   if        b_valg_ok = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
T5.50c                   select
T5.50c                   when      w_kund <> *zero
E5.50c                   eval      rukpl6_kpnr = b1kpnr
E5.50c     rukpl6_key    setll     rukpl6
T5.50c                   when      w_levr <> *zero
T5.50c                   eval      rukpl7_kpnr = b1kpnr
E5.50c     rukpl7_key    setll     rukpl7
T5.50c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      b_forn = *off
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2 or
     c                             b1valg = 5
     c                   eval      c2kpnr = b1kpnr
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
Q.02  *
Q.02  * Kopier
Q.02  *
Q.02 c                   if        b1valg = 3
Q.02 c                   eval      k1kpnr = b1kpnr
Q.02 c                   exsr      xk1win
Q.02 c                   eval      b1valg = 0
Q.02 c**                 update    b1sfl
Q.02 c                   iter
Q.02 c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1kpnr = b1kpnr
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Skriv kundekort
      *
     c                   if        b1valg = 6
     c                   call      'RF632C'
     c                   parm                    b1kpnr
      *
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
7.02  * Legg opp tilganger
7.02  *
7.02 c                   if        u_702 = *on
7.02 c                   if        b1valg = 7
7.02  *
7.02 c                   call      'RF228R'
7.02 c                   parm                    w_firm
7.02 c                   parm                    w_kund
7.02 c                   parm                    b1kpnr
7.02  *
7.02 c                   eval      b1valg = 0
7.02 c                   update    b1sfl
7.02 c                   iter
7.02 c                   endif
7.02 c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
      * Fyller opp subfile
      *
     c                   dou       w_srrn = w_spge
      *
T5.50c                   select
T5.50c                   when      w_kund <> *zero
E5.50c                   read      rukpl6
T5.50c                   when      w_levr <> *zero
T5.50c                   read      rukpl7
T5.50c                   endsl
      *
     c                   if        %eof
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
E5.50c                   if        rufirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
T5.50c                   select
T5.50c                   when      w_kund <> *zero
T5.50c                   if        rukund <> w_kund
T5.50c                   eval      *in15 = *on
T5.50c                   leave
T5.50c                   endif
      *
T5.50c                   when      w_levr <> *zero
T5.50c                   if        rulevr <> w_levr
T5.50c                   eval      *in15 = *on
T5.50c                   leave
T5.50c                   endif
T5.50c                   endsl
      *
      * Skriver til subfile
      *
     c                   eval      w_srrn = w_srrn + 1
      *
T5.50c                   select
T5.50c                   when      w_kund <> *zero
E5.50c                   eval      rukpl6_kpnr = rukpnr
T5.50c                   when      w_levr <> *zero
T5.50c                   eval      rukpl7_kpnr = rukpnr
T5.50c                   endsl
      *
     c                   eval      b1valg = 0
      *
     c                   eval      b1kpnr = rukpnr
     c                   eval      b1enav = ruenav
     c                   eval      b1fnav = rufnav
     c                   eval      b1krol = rukrol
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
      * Hent sist brukte kontaktpersonnummer
      *
     c                   eval      w_retk = ' '
     c                   call      'RS003R'
     c                   parm                    w_retk
     c                   parm                    w_firm
     c                   parm                    w_4pnr
      *
     c                   if        w_retk = *blank
     c                   eval      c1kpnr = w_4pnr
     c                   else
     c                   eval      *in37 = *on                                  Pos.cur.
     c                   goto      c1taga
     c                   endif
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Reduser sist brukte kontaktpersonnummer
      *
     c                   select
     c                   when      *inkc or *inkl or
     c                             c1kpnr <> w_4pnr
     c                   eval      w_retk = 'S'
     c                   call      'RS003R'
     c                   parm                    w_retk
     c                   parm                    w_firm
     c                   parm                    w_4pnr
      *
     c                   endsl
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
      * Sender opp informasjonsbilde dersom kontaktperson finnes fra før
      *
     c                   eval      rukplr_kpnr = c1kpnr
     c     rukplr_key    chain     rukplr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
7.03 c                   else
7.03  *
7.03  * Det kan ikke opprettes kontaktpersoner i serien reservet for XL Nett
7.03  *
7.03 c                   if        U_703 = *on
8.02 c                   if        c1kpnr < 900000
8.02 c                   eval      *in32 = *on
7.03 c                   goto      c1taga
7.03 c                   endif
7.03 c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c2kpnr = c1kpnr
     c                   exsr      xc2bld
      *
     c                   eval      c1kpnr = 0
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle skjermbilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      w_kpnr = c2kpnr
      *
T5.50c                   select
T5.50c                   when      w_kund <> *zero
E5.50c                   eval      rukpl6_kund = w_kund
E5.50c                   eval      rukpl6_kpnr = c2kpnr
E5.50c     rukpl6_key    chain     rukpl6
      *
T5.50c                   when      w_levr <> *zero
T5.50c                   eval      rukpl7_levr = w_levr
6.11 c                   eval      rukpl7_kpnr = c2kpnr
T5.50c     rukpl7_key    chain     rukpl7
T5.50c                   endsl
      *
E5.50c                   if        %found
      *
     c                   eval      c2enav = ruenav
     c                   eval      c2fnav = rufnav
      *
     c                   eval      c2penr = rupenr
     c                   eval      c2krol = rukrol
     c                   eval      c2ktlf = ruktlf
     c                   eval      c2kfax = rukfax
     c                   eval      c2ptlf = ruptlf
     c                   eval      c2pfax = rupfax
     c                   eval      c2mtlf = rumtlf
     c                   eval      c2stat = rustat
     c                   eval      c2mail = rumail
     c                   eval      c2eusr = rueusr
      *
     c                   if        ruedat <> *loval
     c     *dmy          move      ruedat        c2edat
     c                   endif
      *
     c                   else
      *
     c                   eval      c2enav = *blank
     c                   eval      c2fnav = *blank
     c                   eval      c2penr = *zero
     c                   eval      c2krol = *blank
     c                   eval      c2ktlf = *blank
     c                   eval      c2kfax = *blank
     c                   eval      c2ptlf = *blank
     c                   eval      c2pfax = *blank
     c                   eval      c2mtlf = *blank
     c                   eval      c2stat = *blank
     c                   eval      c2mail = *blank
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = *zero
      *
     c                   endif
      *
Q.02 c                   if        %trim(k1enav) <> *blanks or
Q.02 c                             %trim(k1fnav) <> *blanks
Q.02 c                   eval      c2enav = k1enav
Q.02 c                   eval      c2fnav = k1fnav
Q.02 c                   eval      k1fnav = *blanks
Q.02 c                   eval      k1enav = *blanks
Q.02 c                   endif
      *
      * Dersom 'vise' settes protect-indikatorer
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
      * Behandler bildet
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
      *
      * Spørring på personroller
      *
     c                   if        w_afld = 'C2KROL'
     c                   call      'RA536R'
     c                   parm                    w_firm
     c                   parm                    c2krol
     c                   parm                    w_obes
     c                   endif
      *
     c                   goto      c2tagb
     c                   endif
      *
      * Kort
      *
     c                   if        *inke
      *
     c                   if        w_kund <> 0
      *
     c                   call      'RF220R'
5.31 c                   parm                    w_firm
5.31 c                   parm                    w_kund
5.31 c                   parm                    w_kpnr
      *
     c                   endif
      *
     c                   goto      c2tagb
     c                   endif
      *
      * Validering
      *
     c                   if        c2enav = *blank
     c                   eval      *in31  = *on
     c                   goto      c2tagb
     c                   endif
      *
     c                   if        c2fnav = *blank
     c                   eval      *in32  = *on
     c                   goto      c2tagb
     c                   endif
6.12  * Tar vare på kunde- og leverandørnr. for oppslag
6.12 c                   eval      s_kund = w_kund
6.12 c                   eval      s_levr = w_levr
      *
      * Sjekk om kontaktperson med samme navn og personr. finnes fra før
      *
     c                   eval      w_alfa = %trimr(c2enav) + ' ' +
     c                                      %trim(c2fnav)
     c     Lo:Up         xlate     w_alfa        w_alfa
      *
     c                   eval      rukpl3_alfa = w_alfa
     c     rukpl3_key    setll     rukpl3                                 60
     c     rukpl3_key    reade     rukpl3                                 60
      *
     c                   dow       *in60 = *off
     c                   if        xualfa = w_alfa and
     c                             xukpnr <> c2kpnr
     c                   exsr      xv2msg
     c                   if        b_anul = *on
     c                   goto      end_c2bld
     c                   endif
     c                   endif
     c     rukpl3_key    reade     rukpl3                                 60
     c                   enddo
      *
      * Sjekk om kontaktperson med samme personnummer finnes fra før
      *
     c                   if        c2penr <> 0
     c                   eval      rukpl4_penr = c2penr
     c     rukpl4_key    setll     rukpl4                                 60
     c     rukpl4_key    reade     rukpl4                                 60
      *
     c                   dow       *in60 = *off
     c                   if        xupenr = c2penr and
     c                             xukpnr <> c2kpnr
     c                   exsr      xv2msg
     c                   if        b_anul = *on
     c                   goto      end_c2bld
     c                   endif
     c                   endif
     c     rukpl4_key    reade     rukpl4                                 60
     c                   enddo
     c                   endif
      *
      * Oppdaterer tabell
      *
     c                   clear                   rukplur
      *
E5.50c                   eval      rukplu_kund = s_kund
E5.50c                   eval      rukplu_levr = s_levr
     c                   eval      rukplu_kpnr = c2kpnr
     c     rukplu_key    chain     rukplur
      *
     c                   eval      ruenav = c2enav
     c                   eval      rufnav = c2fnav
      *
     c                   eval      rualfa = %trimr(ruenav) + ' ' +
     c                                      %trim(rufnav)
     c     Lo:Up         xlate     rualfa        rualfa
      *
     c                   eval      rupenr = c2penr
     c                   eval      rukrol = c2krol
     c                   eval      ruktlf = c2ktlf
     c                   eval      rukfax = c2kfax
     c                   eval      ruptlf = c2ptlf
     c                   eval      rupfax = c2pfax
     c                   eval      rumtlf = c2mtlf
     c                   eval      rustat = c2stat
     c                   eval      rumail = c2mail
     c                   eval      rumail = c2mail
     c                   eval      rueusr = l_user
     c                   time                    ruetim
     c                   time                    ruedat
      *
     c                   if        %found(rukplu)
     c                   update    rukplur
     c                   else
     c                   eval      rufirm = w_firm
     c                   eval      rukund = w_kund
     c                   eval      rulevr = w_levr
     c                   eval      rukpnr = c2kpnr
6.10 c                   eval      ruousr = l_user
6.10 c                   time                    ruotim
6.10 c                   time                    ruodat
     c                   write     rukplur
     c                   eval      b1kpnr = c2kpnr
     c                   exsr      forny
     c                   endif
      *
     c                   eval      b_forn = *on
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xv2msg        begsr
      *
     c                   eval      b_anul = *off
      *
      * Hent kundenavn
      *
     c                   if        rukund > 0
     c                   eval      rkunl1_kund = xukund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found(rkunl1)
     c                   eval      v2kpnr = xukpnr
     c                   eval      v2kund = xukund
     c                   eval      v2ktxt = rknavn
     c                   eval      *in71 = *on
     c                   endif
     c                   endif
      *
      * Hent leverandørnavn
      *
     c                   if        rulevr > 0
     c                   eval      rlevl1_levr = xulevr
     c     rlevl1_key    chain     rlevl1
     c                   if        %found(rlevl1)
     c                   eval      v2kpnr = xukpnr
     c                   eval      v2levr = xulevr
     c                   eval      v2ltxt = rlnavn
     c                   eval      *in72 = *on
     c                   endif
     c                   endif
      *
     c                   exfmt     v2msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_v2msg
     c                   endif
      *
     c     end_v2msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å init. av skjermbilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bc2bld        begsr
      *
     c                   eval      c2enav = *blank
     c                   eval      c2fnav = *blank
     c                   eval      c2penr = *zero
     c                   eval      c2krol = *blank
     c                   eval      c2ktlf = *blank
     c                   eval      c2kfax = *blank
     c                   eval      c2ptlf = *blank
     c                   eval      c2pfax = *blank
     c                   eval      c2mtlf = *blank
     c                   eval      c2stat = *blank
     c                   eval      c2mail = *blank
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = *zero
     c                   eval      c2kpnr = *zero
     c                   eval      c2mail = *blank
     c                   eval      c2eusr = *blank
     c                   eval      c2edat = *zero
      *
     c                   endsr
      *
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
T5.50c                   select
T5.50c                   when      w_kund <> *zero
E5.50c     rukpl6_key    setgt     rukpl6
E5.50c                   readp     rukpl6
T5.50c                   when      w_levr <> *zero
T5.50c     rukpl7_key    setgt     rukpl7
T5.50c                   readp     rukpl7
T5.50c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
T5.50c                   select
T5.50c                   when      w_kund <> *zero
E5.50c                   readp     rukpl6
T5.50c                   when      w_levr <> *zero
T5.50c                   readp     rukpl7
T5.50c                   endsl
      *
     c                   if        %eof or
     c                             rufirm <> w_firm or
     c                             rukund <> w_kund or
     c                             rulevr <> w_levr
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
T5.50c                   select
T5.50c                   when      w_kund <> *zero
E5.50c                   eval      rukpl6_kpnr = rukpnr
T5.50c                   when      w_levr <> *zero
T5.50c                   eval      rukpl7_kpnr = rukpnr
T5.50c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
T5.50c                   select
T5.50c                   when      w_kund <> *zero
E5.50c     rukpl6_key    setll     rukpl6
T5.50c                   when      w_levr <> *zero
T5.50c     rukpl7_key    setll     rukpl7
T5.50c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_bck       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
Q.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
Q.02  * Subrutine for å behandle kopierings-bildet (K1WIN)
Q.02  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
Q.02  *
Q.02 c     xk1win        begsr
Q.02  *
Q.02  * Tar vare på nøkkel til rad det skal kopieres fra
Q.02  *
Q.02 c                   eval      s_kpnr = k1kpnr
Q.02 c                   eval      k1enav = *blanks
Q.02 c                   eval      k1fnav = *blanks
Q.02  *
Q.02  * Fyller kopierings-bildet
Q.02  *
Q.02 c                   eval      rukpl1_kund = b2kund
Q.02 c                   eval      rukpl1_levr = 0
Q.02 c                   eval      rukpl1_kpnr = k1kpnr
Q.02 c     rukpl1_key    chain     rukpl1
Q.02  *
Q.02 c                   if        %found
Q.02 c                   eval      k1enav = ruenav
Q.02 c                   eval      k1fnav = rufnav
Q.02 c                   endif
Q.02  *
Q.02 c     k1taga        tag
Q.02  *
Q.02 c                   exfmt     k1win
Q.02  *
Q.02 c                   if        *inkc or *inkl
Q.02 c                   goto      end_k1win
Q.02 c                   endif
Q.02  *
Q.02  * Sjekk om utfylt
Q.02  *
Q.02 c                   if        k1enav = *blanks
Q.02 c                   eval      *in31 = *on
Q.02 c                   goto      k1taga
Q.02 c                   endif
Q.02  *
Q.02  * Sjekk om utfylt
Q.02  *
Q.02 c                   if        k1fnav = *blanks
Q.02 c                   eval      *in31 = *on
Q.02 c                   goto      k1taga
Q.02 c                   endif
Q.02  *
Q.02  * Hent sist brukte kontaktnr
Q.02  *
Q.02 c                   eval      w_retk = ' '
Q.02 c                   call      'RS003R'
Q.02 c                   parm                    w_retk
Q.02 c                   parm                    w_firm
Q.02 c                   parm                    s_kpnr
Q.02  *
Q.02 c                   if        w_retk = *blank
Q.02 c                   eval      c1kpnr = s_kpnr
Q.02 c                   eval      c2kpnr = s_kpnr
Q.02 c                   endif
Q.02  *
Q.02  * Kaller vedlikeholdsprogram
Q.02  *
Q.02 c                   exsr      xc2bld
Q.02  *
Q.02 c                   eval      b_forn = *on
Q.02  *
Q.02 c     end_k1win     endsr
Q.02  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
T5.50c                   select
T5.50c                   when      w_kund <> *zero
E5.50c                   eval      rukpl6_kund = w_kund
E5.50c                   eval      rukpl6_kpnr = d1kpnr
E5.50c     rukpl6_key    chain     rukpl6
      *
T5.50c                   when      w_levr <> *zero
T5.50c                   eval      rukpl7_levr = w_levr
T5.50c                   eval      rukpl7_kpnr = d1kpnr
T5.50c     rukpl7_key    chain     rukpl7
T5.50c                   endsl
      *
     c                   eval      d1enav = ruenav
     c                   eval      d1fnav = rufnav
     c                   eval      d1kpnr = rukpnr
      *
T5.50c                   eval      *in75 = *off
T5.50c                   if        rukund <> *zero and
T5.50c                             rulevr <> *zero
T5.50c                   eval      *in75 = *on
T5.50c                   endif
     c                   exfmt     d1win
      *
E5.50c                   if        *inkc or *inkl or *in75
     c                   goto      end_d1win
     c                   endif
      *
      * Sletter rad
      *
E5.50c                   eval      rukplu_kund = rukund
E5.50c                   eval      rukplu_levr = rulevr
     c                   eval      rukplu_kpnr = d1kpnr
     c     rukplu_key    chain     rukplur
      *
     c                   delete    rukplur
      *
     c                   eval      b_forn = *on
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_kund
     c                   parm                    p_levr
      *
      * Key for oppslag på kontakperson-register
      *
     c     rukplr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rukplr_kund
     c                   kfld                    rukplr_levr
     c                   kfld                    rukplr_kpnr
      *
      * Key for les av kontakperson-register - alfa
      *
     c     rukpl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rukpl3_alfa
      *
      * Key for les av kontakperson-register - alfa
      *
     c     rukpl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rukpl4_penr
      *
E5.50 * Key for posisjonering kunder
      *
E5.50c     rukpl6_key    klist
     c                   kfld                    w_firm
E5.50c                   kfld                    rukpl6_kund
E5.50c                   kfld                    rukpl6_kpnr
      *
T5.50 * Key for posisjonering leverandører
      *
T5.50c     rukpl7_key    klist
T5.50c                   kfld                    w_firm
T5.50c                   kfld                    rukpl7_levr
T5.50c                   kfld                    rukpl7_kpnr
      *
      * Key for oppdat. av kontaktperson-register
      *
     c     rukplu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rukplu_kund
     c                   kfld                    rukplu_levr
     c                   kfld                    rukplu_kpnr
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
Q.02  *
Q.02  * Key for oppdat. av kontaktperson-register
Q.02  *
Q.02 c     rukpl1_key    klist
Q.02 c                   kfld                    w_firm
Q.02 c                   kfld                    rukpl1_kund
Q.02 c                   kfld                    rukpl1_levr
Q.02 c                   kfld                    rukpl1_kpnr
      *
      * Henter parameter, og initierer skjermbildet
      *
     c                   time                    w_ddat
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_kund = p_kund
     c                   eval      w_levr = p_levr
      *
7.02  *
7.02  * Endring 7.02
7.02  *
7.02   CallP CO402R(l_filg:w_firm:0:'RF210R_702':
7.02                    co402_verdi1:co402_verdi2);
7.02   if co402_verdi1 = '1';
7.02     u_702 = *on;
7.02   endif;
7.03  *
7.03  * Endring 7.03
7.03  *
7.03   CallP CO402R(l_filg:w_firm:0:'RF210R_703':
7.03                    co402_verdi1:co402_verdi2);
7.03   if co402_verdi1 = '1';
7.03     u_703 = *on;
7.03   endif;
      *
     c                   eval      *in22 = *on
      *
      * Styring av tekst på skjermbilder
      *
     c                   if        w_kund > 0
     c                   eval      *in70 = *on
     c                   else
     c                   eval      *in70 = *off
     c                   endif
      *
      * Hent kundenavn
      *
     c                   if        p_kund > 0
     c                   eval      rkunl1_kund = w_kund
     c     rkunl1_key    chain     rkunl1
     c                   if        %found(rkunl1)
     c                   eval      b2kund = w_kund
     c                   eval      b2navn = rknavn
     c                   eval      c2kund = w_kund
     c                   eval      c2knav = rknavn
     c                   endif
     c                   endif
      *
      * Hent leverandørnavn
      *
     c                   if        w_levr > 0
     c                   eval      rlevl1_levr = w_levr
     c     rlevl1_key    chain     rlevl1
     c                   if        %found(rlevl1)
     c                   eval      b2kund = w_levr
     c                   eval      b2navn = rlnavn
     c                   eval      c2kund = w_levr
     c                   eval      c2knav = rlnavn
     c                   endif
     c                   endif
      *
      * Posisjonerer databasen, og fyller opp subfile
      * Kunde
      *
T5.50c                   if        w_kund <> *zero
E5.50c                   eval      rukpl6_kund = w_kund
E5.50c     rukpl6_key    setll     rukpl6
T5.50c                   else
      * Leverandør
     c                   eval      rukpl7_levr = w_levr
T5.50c     rukpl7_key    setll     rukpl7
T5.50c                   endif
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
