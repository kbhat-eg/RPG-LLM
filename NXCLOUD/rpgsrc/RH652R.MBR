     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RH652R                                              *
      * Beskrivelse . . : Hovedbok - revisjonsjournal - utskrift              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
6.10  * V6.10     210909  Lagt inn sporingsfelter                        BSA  *
      *                                                                       *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frhw2pf    ip   e           k disk
     frhtrlu    uf   e           k disk    rename(rhtrpfr:rhtrlur)
     frkunlr    if   e           k disk    rename(rkunpfr:rkunlrr)
     frlevlr    if   e           k disk    rename(rlevpfr:rlevlrr)
     fra05l1    if   e           k disk    rename(ra05pfr:ra05l1r)
      *
     frh652p    o    e             printer oflind(*in30)
      *
     d                uds
     d rhpkda                300    305  0
     d rhprÅr                         4  0
     d rhpper                         2  0
     d rhpblk                         1
     d rhpskr                         1
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d antbil          s              6  0
     d antpos          s              6  0
     d debet           s             11  2
     d diff            s             11  2
     d ingmva          s             11  2
     d kredit          s             11  2
     d lrdeb           s             11  2
     d lrdif           s             11  2
     d lrkre           s             11  2
     d utgmva          s             11  2
     d wbaar           s              2  0
6.NU d wbilnr          s              8  0
6.NU d*wbtext          s             15
6.NU d wbtext          s             13
     d wptext          s             19
     d wraar           s              2  0
     d xmvak           s              1  0
      *
     d w_firm          s                   like(rbfirm)
     d w_biln_g        s                   like(rbbiln)
     d p_kont          s                   like(rbkont)
     d p_spes          s                   like(rbspes)
     d p_avde          s                   like(rbavde)
     d p_stat          s              1
     d p_text          s             30
      *
     d rkunlr_kund     s                   like(rkkund)
     d rlevlr_levr     s                   like(rllevr)
     d ra05l1_ekod     s                   like(raekod)
      *
     c     dummy         tag
      /eject
      *
      * Behandling av transaksjoner
      *
     c                   if        *in30 = *on
     c                   write     head1
     c                   eval      *in30 = *off
     c                   endif
      *
     c                   if        rbmvab = *zeros
     c                   eval      *in80 = *on
     c                   else
     c                   eval      *in80 = *off
     c                   endif
      *
     c                   if        rbinvb = *zeros
     c                   eval      *in81 = *on
     c                   else
     c                   eval      *in81 = *off
     c                   endif
      *
     c                   if        rbbiln <> w_biln_g
     c                   eval      antbil = antbil + 1
     c                   eval      debet = 0
     c                   eval      kredit = 0
     c                   eval      diff = 0
      *
     c                   if        *in06 = *on
     c                   eval      wbilnr = wbilnr + 1
     c                   if        wbilnr <> rbbiln
     c                   exsr      beregn
     c                   write     brudd
     c                   endif
     c                   endif
      *
     c                   if        *in06 = *off
     c                   move      *on           *in06
     c                   endif
      *
     c     *dmy          move      rbbdat        wbdato
      *
     c                   eval      w_biln_g = rbbiln
     c                   endif
      *
     c                   eval      antpos = antpos + 1
      *
     c                   movel     rbtext        wbtext
      *
      * Sjekk om samme periode
      *
     c                   if        rhpper <> rbrper
     c                   move      *on           *in15
     c                   move      rbraar        wraar
     c                   else
     c                   move      *off          *in15
     c                   endif
      *
     c                   eval      wptext = *blank
      /eject
      *
      * Hovedboksposteringer
      *
     c                   if        rbstat = 'H'
      *
      * Sjekk om debet/kredit
      *
     c                   if        rbneto < 0
     c                   move      *on           *in13
     c                   eval      kredit = kredit + rbneto
     c                   eval      diff = diff + rbneto
     c                   else
     c                   move      *off          *in13
     c                   eval      debet = debet + rbneto
     c                   eval      diff = diff + rbneto
     c                   endif
      *
      * Sjekk hvilken behandling
      * MVA-koden har
      *
     c                   if        rbmvak = ' '
     c                   move      '0'           rbmvak
     c                   endif
      *
     c                   move      rbmvak        xmvak
      *
      * Hent behandlingskode
      * fra koderegisteret
      *
     c                   move      rbmvak        raekod
     c     ra05l1_key    chain     ra05l1                             61         MVA-
      *
     c                   if        *in61 = *off                                 Kode finnes
     c                   move      raebmk        xmvak
     c                   endif
      *
     c                   if        xmvak = 3
     c                   move      *on           *in82
     c                   move      rbmvab        utgmva
     c                   else
     c                   move      *off          *in82
     c                   move      rbmvab        ingmva
     c                   endif
      *
      * Hent kontotekst
      *
     c                   move      rbkon6        p_kont
     c                   move      rbspes        p_spes
     c                   move      rbavde        p_avde
     c                   call      'RS740R'                                      kontoplan-
     c                   parm                    p_stat
     c                   parm                    p_kont
     c                   parm                    p_spes
     c                   parm                    p_avde
     c                   parm                    p_text
      *
     c                   if        p_stat <> 'N'                                Kode
     c                   movel     p_text        wptext                          finnes
     c                   endif
      *
     c                   endif
      /eject
      *
      * Kundepostering
      * hent navn
      *
     c                   if        rbstat = 'K'
     c                   move      rbkon6        rkunlr_kund
     c     rkunlr_key    chain     rkunlr                             60
     c                   if        *in60 = *off
     c                   movel     rknavn        wptext
     c                   endif
     c                   endif
      *
      * Leverandørpostering
      * hent navn
      *
     c                   if        rbstat = 'L'
     c                   move      rbkon6        rlevlr_levr
     c     rlevlr_key    chain     rlevlr                             60
     c                   if        *in60 = *off
     c                   movel     rlnavn        wptext
     c                   endif
     c                   endif
      /eject
      *
      * Skriv linje
      *
     c                   write     linje
      *
      * Oppdater hovedboksposteringer som har vært med på liste
      *
     c                   if        rbstat = 'H'
     c                   move      rbkon6        rbkont
     c     rhtrlu_key    chain     rhtrlur                            60
     c                   if        *in60 = *off
     c                   move      '1'           rbrmrk
6.10 c                   time                    rbedat
6.10 c                   time                    rbetim
6.10 c                   eval      rbeusr = l_user
     c                   update    rhtrlur
     c                   endif
     c                   endif
      *
     clr                 write     total
      *
      /eject
      *
      * Beregninger på totaltid
      *
     c     beregn        begsr
      *
     c                   move      rbbiln        wbilnr
      *
      * Test om differanse pr. bilag
      *
     c                   if        diff <> 0
     c                   move      *on           *in12
     c                   else
     c                   move      *off          *in12
     c                   endif
      *
      * Beløpskontroll pr. bilag?
      *
     c                   if        rhpblk = ' '
     c                   move      *off          *in12
     c                   endif
      *
     c                   write     diflin
      *
      * Summer til totaler for firma
      *
     c                   eval      lrdeb = lrdeb + debet
     c                   eval      lrkre = lrkre + kredit
     c                   eval      lrdif = lrdif + diff
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   eval      w_firm = l_firm
      *
     c                   write     head1
      *
      * Key for posteringer - hovedbok
      *
     c     rhtrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rbraar
     c                   kfld                    rbkont
     c                   kfld                    rbspes
     c                   kfld                    rbavde
     c                   kfld                    rbrper
     c                   kfld                    rbbdat
     c                   kfld                    rbtist
      *
      * Key for kunderegister
      *
     c     rkunlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunlr_kund
      *
      * Key for leverandørregister
      *
     c     rlevlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevlr_levr
      *
      * Key for mva-koder
      *
     c     ra05l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra05l1_ekod
      *
     c                   endsr
