     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RL514R
      * Beskrivelse . . : Leverandørposteringer, spørring samlegiro
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       030304 KOB  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frltrl5    if   e           k disk    rename(rltrpfr:rltrl5r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frltrl2    if   e           k disk    rename(rltrpfr:rltrl2r)
      *
     frl514d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d rltrl5_samb     s                   like(rosamb)
     d rlevl1_levr     s                   like(rolevr)
     d rltrl2_levr     s                   like(rolevr)
     d rltrl2_bdat     s                   like(robdat)
     d rltrl2_tist     s                   like(rotist)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_valg_ok       s              1    inz(*off)
      *
     d p_stat          s              1
     d p_gkod          s                   like(rlavde)
     d p_gtxt          s             20
     d p_levr          s                   like(rllevr)
      *
     d w_firm          s                   like(rofirm)
     d w_samb          s                   like(rosamb)                                      .
     d w_levr          s                   like(rolevr)                                      .
     d w_raar          s                   like(roraar)                                      .
     d gml_samb        s                   like(rosamb)
     d gml_levr        s                   like(rolevr)
     d w_fors          s              1  0
     d w_post          s              1  0
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * d_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2samb <> 0 or
     c                             b2raar <> w_raar
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        d_fcrn <> 0
     c     d_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      rltrl5_samb = b1samb
     c     rltrl5_key    setll     rltrl5
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi, regnskapsår utfyllt
      *
     c                   if        b2raar <> *zero or                                    .....
     c                             b2raar <> w_raar                                          .
     c                   eval      w_raar = b2raar                                           .
      *
     c                   endif                                                           .....
      *
     c                   eval      w_seqe = *blank
     c                   eval      w_samb = b2samb
     c                   eval      w_levr = 0
     c                   eval      rltrl5_samb = b2samb
     c     rltrl5_key    setll     rltrl5
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2samb = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = d_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vis
      *
     c                   if        b1valg = 8
     c                   exsr      xc2win                                                  . .
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   eval      w_fors = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      rltrl5
      *
     c                   if        %eof or
     c                             rofirm <> w_firm
     c                   if        w_post > 0
     c                   exsr      skr_subfile                                         ..... .
     c                   endif
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   if        b2raar <> *zero and                                 ..... .
     c                             b2raar <> roraar
     c                   iter                                                              . .
     c                   endif                                                         ..... .
      *
     c                   if        p_levr <> 0 and                                     ..... .
     c                             p_levr <> rolevr
     c                   iter                                                              . .
     c                   endif                                                         ..... .
      *
     c                   if        w_fors = 0
     c                   eval      gml_levr = rolevr                                   ..... .
     c                   eval      gml_samb = rosamb
     c                   eval      w_fors = 1
     c                   endif                                                         ..... .
      *
     c                   if        rolevr <> gml_levr or                               ..... .
     c                             rosamb <> gml_samb
     c                   exsr      skr_subfile                                         ..... .
     c                   endif                                                         ..... .
      *
     c                   eval      b1valg = 0
     c                   eval      b1bdat = robdat
     c                   eval      b1tist = rotist
     c                   eval      b1samb = rosamb
     c     *dmy          move      robdat        b1bida
     c                   eval      b1belØ = b1belØ + robelØ
     c                   eval      b1levr = rolevr
     c                   eval      w_post = 1
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å skrive til subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skr_subfile   begsr
      *
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      rltrl5_samb = gml_samb
      *
      * Hent leverandør
      *
     c                   eval      rlevl1_levr = gml_levr                                    .
     c     rlevl1_key    chain     rlevl1                             61                     .
     c                   if        *in61 = *off                                              .
     c                   eval      b1navn = rlnavn                                           .
     c                   endif                                                               .
      *
     c                   write     b1sfl
     c                   eval      b1belØ = 0
     c                   eval      w_post = 0
      *
     c                   eval      gml_levr = rolevr                                   ..... .
     c                   eval      gml_samb = rosamb
      *
     c     end_skrfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     rltrl5_key    setgt     rltrl5
     c                   readp     rltrl5
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     rltrl5
      *
     c                   if        %eof or
     c                             rofirm <> w_firm
     c                   leave
     c                   endif
      *                                                                 .
     c                   if        b2raar <> *zero and                                 ..... .
     c                             b2raar <> roraar
     c                   iter                                                              . .
     c                   endif                                                         ..... .
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      rltrl5_samb = rosamb
      *
     c                   enddo
      *
     c                   if        %eof
     c     rltrl5_key    setll     rltrl5
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * C2WIN Behandle bilde for vis                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xc2win        begsr
      *
     c                   call      'RL515R'
     c                   parm                    b1levr
     c                   parm                    b1samb
     c                   parm                    w_raar
      *
     c                   endsr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_levr
      *
      * Key for posisjonering på samlegiro
      *
     c     rltrl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rltrl5_samb
      *
      * Key for leverandørregister
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for lesing av postering
      *
     c     rltrl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rltrl2_levr
     c                   kfld                    rltrl2_bdat
     c                   kfld                    rltrl2_tist
      *
      * Henter parametre, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_raar = *year
     c                   eval      b2raar = *year
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rltrl5_samb = 0
     c     rltrl5_key    setll     rltrl5
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
