     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RK634R                                        *
      * Beskrivelse . . : Utskrift av mantall                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       170304 KOB  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frmknl1    if   e           k disk    rename(rmknpfr:rmknl1r)
     frmeil1    if   e           k disk    rename(rmeipfr:rmeil1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fralml1    if   e           k disk    rename(ralmpfr:ralml1r)
     frk634p    o    e             printer oflind(*in30)
      *
      * Definering av variable i nøkler
      *
     d rmknl1_xalm     s                   like(rmxalm)
     d rmeil1_xalm     s                   like(rmxalm)
     d rmeil1_xgnr     s                   like(rmxgnr)
     d rmeil1_xbnr     s                   like(rmxbnr)
     d rkunl1_kund     s                   like(rkkund)
     d ralml1_alme     s                   like(rmalme)
      *
     d                 ds
      *                                                      Parameter-inn
     d ldprec                        64
     d  ldalme                        4  0 overlay(ldprec)
     d  ldpaar                        4  0 overlay(ldprec:5)
     d  ldfdat                       10d   datfmt(*iso)
     d                                     overlay(ldprec:09)
     d  ldtdat                       10d   datfmt(*iso)
     d                                     overlay(ldprec:19)
     d  ldvari                        1  0 overlay(ldprec:35)
      *
      * Definering av Local data area
      *
     d                uds
      *
     d l_wsid                901    910
     d l_user                911    920
     d l_file                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d wwparm          s             64
     d w_firm          s                   like(rkfirm)
     d w_kund_gml      s                   like(rkkund)
     d w_funnet        s              1  0
      *
      * Les eiendomsknytningsregister
      *
     c                   eval      rmknl1_xalm = ldalme
     c     rmknl1_key    setll     rmknl1                                                    .
     c     rmknl1_key    reade     rmknl1                                 90
      *
     c                   dow       *in90 = *off
      *
     c                   if        w_kund_gml = rmxknr
     c                   goto      neste_knr
     c                   endif
      *
     c                   eval      w_funnet = 0
      *
     c                   eval      rkunl1_kund = rmxknr
     c     rkunl1_key    chain     rkunl1
      *
     c                   if        %found
     c                   eval      d1kund = rmxknr
     c                   eval      d1ena1 = rknavn
     c                   else
     c                   eval      d1ena1 = *blank
     c                   endif
      *
     c                   exsr      les_eien
      *
     c     neste_knr     tag
      *
     c                   eval      w_kund_gml = rmxknr
      *
     c     rmknl1_key    reade     rmknl1                                 90
     c                   enddo
      *
     c     slutt         tag
      *
     c                   eval      *inlr = *on
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine som leser eiendomsregisteret                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     les_eien      begsr
      *
     c                   eval      rmeil1_xalm = ldalme
     c                   eval      rmeil1_xgnr = rmxgnr
     c                   eval      rmeil1_xbnr = rmxbnr
     c     rmeil1_key    setll     rmeil1                                                    .
     c     rmeil1_key    reade     rmeil1                                 90
      *
     c                   dow       *in90 = *off
      *
     c                   eval      d1enav = rmenav
     c                   eval      d1ena2 = rmena2
     c                   eval      d1egnr = rmxgnr
     c                   eval      d1ebnr = rmxbnr
      *
     c                   write(e)  d1det
      *
      * Sjekker om overflow
      *
     c                   if        *in30 = *on                                               .
     c                   write(e)  a1hdr                                                     .
     c                   eval      *in30 = *off                                              .
     c                   endif                                                           .....
      *
     c     rmeil1_key    reade     rmeil1                                 90
     c                   enddo
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    wwparm
      *
     c                   move      wwparm        ldprec
      *
      * Key for eiendomsknytningsregister
      *
     c     rmknl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rmknl1_xalm
      *
      * Key for eiendomsregister
      *
     c     rmeil1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rmeil1_xalm
     c                   kfld                    rmeil1_xgnr
     c                   kfld                    rmeil1_xbnr
      *
      * Key for kunderegister
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for almenningsregister
      *
     c     ralml1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ralml1_alme
      *
     c                   eval      w_firm = l_firm
      *
      * Slår opp almenning
      *
     c                   eval      ralml1_alme = ldalme
     c     ralml1_key    chain     ralml1r
      *
     c                   if        %found                                                    .
     c                   eval      h1alme = rmnavn
     c                   endif
      *
      * Skrive rapporthode
      *
     c                   write(e)  a1hdr
      *
     c                   endsr
      *
