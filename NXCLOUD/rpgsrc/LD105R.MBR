     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LD105R
      * Beskrivelse . . : Bestilling-linje, vedlikehold av varelinje
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       191102 HKO  Nytt program
5.61  * R5.61 290908 AMD  Programmet feilet når det ble lagt på et
      *                   prosjektnummer (økonomi) på varelinja.
      *                   Parameterlista var ikke riktig.
5.70  * PRGR  231008 sst  Prisgruppe
      * 6.10  170609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  130709 bof  Utvidet eannr til 14 og brukt std. oppslag
      * 6.10  220909 ASK  Nye parametre til VL001R
6.12  * 6.10  051009 BSA  Henter inn tekstlinje fra språkregsiter hvis finnes
6.13  * 6.10  081009 BSA  Regner om pris til gjeldende valuta.
6.14  * 6.10  190410 bof  Hvis det er endr.av best.så skal man få
      *                   lov til å endre selv om utgatt dato er utfylt
6.15  * 6.10  280710 bsa  Ingen lageroppdatering hvis antall, enhet og lager
6.15  *                   er urørt.
6.16  * 6.10  270511 bof  Takler alt for høye antall i beregning nettosum
6.17  * 6.10  090611 bof  Justert på referanse
6.20  *  6.20 231110 NHO  Proj er nå alfa
6.21  * R6.20 120410 bof  Hvis innkjøpspris endres oppdat.ordre
6.22  * R6.20 151210 bof  Kalkulerer ordre etter at kostpris er endret
6.23  * R6.20 160211 nho  Vis av nytt bilde fra VA
6.24  * 6.20  190511 bø   Utvidet vl710r med div. info - utg.dato testes mot
6.24  *                   std.program
6.25  * 6.20  010611 bsa  Hvis utgår dato og kampanje, fjernes utgårdato på
6.25  *                   denne linja, hvis det overstyres.
6.26  * 6.20  101012 bof  Tar ned linjens leveringsbet. til kalkulasjon
6.27  * 6.20  110112 bsa  Kampanje må med ved kalkulering av innkjøp/kostpriser
6.28  * 6.20  110112 bof  nye parametre til jv145r
6.29  * 6.20  170912 bsa  Henter GTIN fra annet program.(Også vi VA) (NEB)
6.30  * 6.30  200214 bof  Hvis ikke kamp.best, sjekk om vare finnes på
6.30  *                   en eller annen kampanje i VA, henter innk.pris
6.nu  * 6.30  050614 bof  Utvidelser mtp. nummerutvidelse
6.31  * 6.30  080814 bof  Viser erstattet av nobbnr.
6.32  * 6.30  150914 bsa  Legger ut mvakoden på linjenivå.
6.33  * 6.30  221014 nho  Leverandør skal styre mvakoden.
6.34  * 6.30  040615 bkv  Bestilling økt fra 6 til 8
6.35  * 6.30  100615 bkv  Henter kostpris fra kampanje
      *                   Endret slik at finner tilbud først med kampanje,
      *                   så innenfor dato, hvis ikke på kampanje
6.36  * 6.30  080915 nho  Lagt inn kommandotast for visning av NOBB info
6.37  * 6.30  100516 bof  Utvidet med best.lev.+ llager til VP755R
6.39  * 6.30  231116 bsa  Setter mvakode basert på avgiftstype. Ny kode
6.39  *                   for å skille import av varer på mva rapport
6.3a  * 6.30  110517 bof  Henter GTIN ut fra aktuell enhet.
6.3b  * 6.30  170918 bkv  Utvidet med trelast sortiment
6.3c  * 6.30  121018 bof  justert, bruker leverandør fra bestilling.
7.xx  * 7.00  030316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
6.3d  * 6.30  270519 bof  Henter innkj.pris fra evr kampanje. (BM tilp.)
      *
7.01  * 6.30  240817 bsa  Ved utfylling av bekreftet leveringsdato,
7.01  *                   oppdateres tilhørende ordre.
7.02  *       130919 bkv  Korrigerer vising av desimal tall
7.03  * 7.00  130121 ask  Lagt inn mulighet for låst valutakurs på leverandør
      *                   Bygger på funksjonalitet fra 6.13
7.04  * 7.00  250221 bsa  Kan ikke dele på 0, må sette defaultverdi uavhengig
7.04  * 7.00              av valutakode.
7.05  * 7.00  070720 bsa  Hvis leveringstype er utfylt på tidligere linjer, skal
7.05  *                   dette foreslås på neste.
7.07  * 7.00  180820 bof  Utvidet parm til JM591R med prisgruppe
      *
8.01  * 8.00  030821 bsa  Oppdaterer GTIN nummer på bestillingslinje.
8.04  * 8.00  121022 bsa  Kan bli feil GTIN nummer hvis leverandøren ikke
8.04  * 8.00              er prisgiver/leverandør på varen.
8.05  * 8.00  310123 bsa  Hvis leveringsdato ikke fylles inn, hent den fra
8.05  * 8.00              hodet før lagring.
8.06  *PRG2   140622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
     flusrl1    if   e           k disk    rename(lusrpfr:lusrl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvltyl1    if   e           k disk    rename(vltypfr:vltyl1r)
     fvenhl1    if   e           k disk    rename(venhpfr:venhl1r)
     flohel1    if   e           k disk    rename(lohepfr:lohel1r)
     flodtl1    if   e           k disk    rename(lodtpfr:lodtl1r)
     flodtlu    uf a e           k disk    rename(lodtpfr:lodtlur)
6.12 fvsprl1    if   e           k disk    rename(vsprpfr:vsprl1r)
6.12 fra12l1    if   e           k disk    rename(ra12pfr:ra12l1r)
6.12 frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
6.12 fra16l1    if   e           k disk    rename(ra16pfr:ra16l1r)
6.21 ffodtlu    uf   e           k disk    rename(fodtpfr:fodtlur)
6.31 fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
6.32 fvmval1    if   e           k disk    rename(vmvapfr:vmval1r)
6.3b fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
7.06 flodtlr    if   e           k disk    rename(lodtpfr:lodtlrr)
8.05 fllogl1    if   e           k disk    rename(llogpfr:llogl1r)
      *
     fld105d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_kode          s              1
     d p_firm          s                   like(ldfirm)
     d p_numm          s                   like(ldnumm)
     d p_suff          s                   like(ldsuff)
     d p_line          s                   like(ldline)
     d p_ltyp          s                   like(ldltyp)
     d p_lety          s                   like(ldlety)
     d p_vare          s                   like(ldvare)
     d p_anta          s                   like(ldanta)
     d p_enhe          s                   like(ldenh1)
     d p_kost          s              1
6.21 d p_orde          s              1
6.27 d p_kamp          s              5
6.36 d p_nobb          s              8
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
7.00 d l_filg                931    933
      *
      * Definering av datastrukturer
      *
      * Lageroppdatering
      *
     d                 ds
6.nu d hlrec                        128
     d  hlvare                       15    overlay(hlrec)
     d  hllage                        2  0 overlay(hlrec:16)
     d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
     d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
     d  hlotyp                        2    overlay(hlrec:36)
     d  hlltyp                        2    overlay(hlrec:38)
     d  hlanta                       11  3 overlay(hlrec:40)
     d  hlkopr                        9  2 overlay(hlrec:51)
     d  hlrefr                       20    overlay(hlrec:60)
     d  hlsyst                        1    overlay(hlrec:80)
     d  hlaltk                        2    overlay(hlrec:81)
     d  hlrest                        1    overlay(hlrec:83)
     d  hltype                        1    overlay(hlrec:84)
     d  hlenhe                        3    overlay(hlrec:85)
     d  hlinlr                        1    overlay(hlrec:88)
     d  hllety                        1    overlay(hlrec:89)
6.nu d  hlonum                        8  0 overlay(hlrec:90)
6.nu d  hlolin                        4  0 overlay(hlrec:98)
      *
      * Definering av variabler i nøkler
      *
     d lusrl1_user     s                   like(lbuser)
     d vvarl1_vare     s                   like(vvvare)
     d vltyl1_ltyp     s                   like(valtyp)
     d venhl1_eenh     s                   like(vaeenh)
6.12 d vsprl1_tvar     s                   like(vvtvar)
6.12 d vsprl1_tspr     s                   like(vvtspr)
6.12 d ra12l1_lkod     s                   like(ralkod)
6.12 d rlevl1_levr     s                   like(rllevr)
6.21 d fodtlu_numm     s                   like(fdnumm)
6.21 d fodtlu_suff     s                   like(fdsuff)
6.21 d fodtlu_line     s                   like(fdline)
6.31 d jvarl1_vare     s                   like(jvvare)
6.32 d vmval1_mkod     s                   like(vamkod)
7.06 d lodtlr_line     s                   like(ldline)
8.05 d llogl1_kode     s                   like(lmkode)
      *
      * Definering av variable
      *
     d b_endr          s              1    inz(*off)
6.25 d b_endr_udat     s              1    inz(*off)
     d b_forn          s              1    inz(*off)
6.15 d b_lage          s              1    inz(*off)
      *
     d w_mvap          s              6  2
     d w_mvat          s              5  4
     d w_mvaf          s             10  9
     d w_omr2          s             12  6
     d w_omr3          s             12  6
     d w_stat          s              1
     d w_sald          s             11  2
     d w_retk          s              1
     d w_kont          s              5  0
     d w_spes          s              4  0
     d w_txt1          s             30
     d w_txt2          s             30
5.61 d w_utxt          s             30
     d w_ldat          s               d   datfmt(*iso)
     d w_ddat          s               d   datfmt(*iso)
5.27 d w_kamp          s              5
6.30 d w_enhe          s                   like(vvenhe)
6.30 d w_vvar          s                   like(ldvare)
6.30 d w_vldo          s                   like(loldor)
6.30 d w_vkai          s              9  2
6.35 d w_vkpr          s              9  2
7.06 d w_lety          s                   like(ldlety)
      *
     d w_firm          s                   like(vvfirm)
     d w_numm          s                   like(lonumm)
     d w_suff          s                   like(losuff)
     d w_line          s                   like(ldline)
      *
     d w_raba          s                   like(ldsumi)
     d w_sumr          s                   like(ldsumi)
     d w_sumi          s                   like(ldsumi)
     d w_enh1          s                   like(ldenh1)
     d w_inp1          s                   like(ldipny)
     d w_kop1          s                   like(ldkpny)
     d w_enh2          s                   like(ldenh1)
     d w_inp2          s                   like(ldipny)
     d w_kop2          s                   like(ldkpny)
     d w_enh3          s                   like(ldenh1)
     d w_inp3          s                   like(ldipny)
     d w_kop3          s                   like(ldkpny)
     d w_inpr          s                   like(ldipny)
     d w_kopr          s                   like(ldkpny)
     d w_rab1          s                   like(ldrab1)
5.70 d w_vtyp          s                   like(vvvtyp)
5.70 d w_avde          s              4  0
8.06 d w_prgr          s              2
6.11 d w_east          s              1
6.11 d w_eann          s             14  0
7.03 d w_kurs          s             10  6
6.34 d w_numa          s              8
6.17 d w_lina          s              4
6.12 d sptek1          s                   like(vvtte1)
6.12 d sptek2          s                   like(vvtte2)
6.24 d w_udat          s                   like(vvudat)
6.24 d w_ldor          s                   like(vvldor)
6.24 d b_inlr          s              1    inz(*off)
6.3b d w_lv_nobb       s                   like(vvlnob)
6.3b d w_lv_modn       s                   like(vvlmod)
6.3b d w_lv_lldo       s                   like(vvlnle)
6.3b d w_lv_llva       s                   like(vvllva)
      *
     d s_ant1          s                   like(ldanta)
     d s_ant2          s                   like(ldanta)
     d s_ant3          s                   like(ldanta)
     d s_anta          s                   like(ldanta)
     d s_enhe          s                   like(ldenh1)
     d s_inpr          s                   like(ldipny)
     d s_rab1          s                   like(ldrab1)
     d s_rab2          s                   like(ldrab2)
     d s_kplj          s                   like(ldkplj)
6.15 d s_lage          s                   like(ldlage)
6.21 d s_ldat          s                   like(ldldat)
      *
7.00   dcl-s co402_verdi1  char(1);
7.00   dcl-s co402_verdi2  char(2048);
7.00   DCL-PR CO402R EXTPGM('CO402R');
7.00     p_filgrp            char(3)     const;
7.00     p_firm              packed(3:0) const;
7.00     p_lager             packed(2:0) const;
7.00     p_nokkel            char(50)    const;
7.00     p_verdi1            char(1);
7.00     p_verdi2            char(2048);
7.00   END-PR;
7.00 d u_701           s              1    inz(*off)
7.03 d u_s703          s              1    inz(*off)
7.04 d u_hbdl          s              1    inz(*off)
6.3d d u_63d           s              1    inz(*off)
      *
      * Definering av konstanter
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåäö'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÄÖ'
      *
6.3c C/Exec SQL
6.3c C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
6.3c C/End-Exec
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter vareinformasjon
      *
     c                   eval      vvarl1_vare = p_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        not %found
     c                   goto      endring
     c                   endif
      *
6.12  *
6.12  * Hente inn leverandørregister
6.12  *
6.12 c                   eval      sptek1 = *blank
6.12 c                   eval      sptek2 = *blank
6.12 c                   eval      rlevl1_levr = loldor
6.12 c     rlevl1_key    chain     rlevl1r
6.12 c                   if        %found
6.12  *
6.12  * Sjekk om vi skal hente inn språk fra tillegsregister
6.12  *
6.12 c                   if        rlland = '  ' or
6.12 c                             rlland = 'NO'
6.12 c                   else
6.12  * Vi må sjekke via landkoden hvilken språkkode som benyttes
6.12 c                   eval      ra12l1_lkod = rlland
6.12 c     ra12l1_key    chain     ra12l1r
6.12 c                   if        %found and
6.12 c                             ralspr > 1
6.12  * Finnes språktekst
6.11 c                   eval      vsprl1_tvar = p_vare
6.11 c                   eval      vsprl1_tspr = ralspr
6.11 c     vsprl1_key    chain     vsprl1r
6.11 c                   if        %found
6.11 c                   eval      sptek1 = vvtte1
6.11 c                   eval      sptek2 = vvtte2
6.12 c                   endif
6.12 c                   endif
6.12 c                   endif
6.12 c                   endif
6.11  *
6.11  * Kaller program for henting av ean-nr
8.04  * Flyttes
6.11  *
6.29 c**                 call      'VE711R'
8.04 c*                  call      'VE713R'
8.04 c*                  parm                    w_firm
8.04 c*                  parm                    loldor
8.04 c*                  parm                    p_vare
8.04 c*                  parm                    p_enhe
8.04 c*                  parm                    w_eann
8.04 c*                  parm                    w_east
7.03  *
7.03  * Henter låst valutakurs - finnes den ikke, hent løpende kurs
7.03  *
7.04 c                   eval      w_kurs = 1
7.03 c                   if        lovalk <> '   ' and
7.03 c                             lovalk <> 'NOK'
7.03 c/exec sql
7.03 c+ select rlvkur
7.03 c+ into   :w_kurs
7.03 c+ from   ra60st
7.03 c+ where  :lobdat >= rlvfda and :lobdat <= rlvtda and :loldor = rlvlev
7.03 c/end-exec
7.03 c                   if        sqlcod = 100
7.03 c/exec sql
7.03 c+ select rapmku
7.03 c+ into   :w_kurs
7.03 c+ from   ra16pf
7.03 c+ where  rapfir = :w_firm and rapkod = :lovalk
7.03 c/end-exec
7.03 c                   endif
7.03 c                   if        sqlcod = 100
7.03 c                   eval      w_kurs = 1
7.03 c                   endif
7.03 c                   endif
      *
      * Kaller program for henting av pris-informasjon på tre enheter
      *
6.27 C                   eval      w_kamp = p_kamp
     c                   call      'VP755R'
     c                   parm                    w_firm
     c                   parm                    vvvare
5.70 c                   parm                    w_prgr
7.05 c*                  parm                    p_lety
7.05 c                   parm                    w_lety
     c                   parm                    lobdat
     c                   parm                    w_enh1
     c                   parm                    w_kop1
     c                   parm                    w_inp1
     c                   parm                    w_enh2
     c                   parm                    w_kop2
     c                   parm                    w_inp2
     c                   parm                    w_omr2
     c                   parm                    w_enh3
     c                   parm                    w_kop3
     c                   parm                    w_inp3
     c                   parm                    w_omr3
6.27 c                   parm                    w_kamp
6.37 c                   parm                    loldor
6.37 c                   parm                    lolage
6.30  *
6.30  * hvis ikke spes kampanje er valgt, så sjekkes kampanjefilen
6.30  * og henter evt. innkjøpspris
6.30  *
6.30 c*                  if        p_kamp = *blank
6.30  *
6.30 c                   eval      w_vkai = 0
6.35 c                   eval      w_vkpr = 0
6.30 c                   eval      w_vvar = vvvare
6.30 c                   eval      w_vldo = loldor
6.30 c                   eval      w_enhe = *blank
6.35 c                   eval      w_kamp = p_kamp
6.3d c                   if        u_63d = *on
6.3d c                   eval      w_enhe = w_enh1
6.3d c                   eval      b_inlr = *on
6.3d c                   call      'VT592R  '
6.3d c                   parm                    w_firm
6.3d c                   parm                    w_vvar
6.3d c                   parm                    w_enhe
6.3d c                   parm                    w_vkai
6.3d c                   parm                    b_inlr
6.3d c                   eval      b_inlr = *off
6.3d c                   else
6.30 c                   call      'JM591R  '
6.30 c                   parm                    w_firm
6.30 c                   parm                    w_vvar
6.30 c                   parm                    w_vldo
6.30 c                   parm                    w_vkai
6.35 c                   parm                    w_vkpr
6.30 c                   parm                    w_enhe
6.35 c                   parm                    w_kamp
7.07 c                   parm                    w_prgr
6.3d c                   endif
6.30  *
6.30 c                   if        w_vkai <> 0 and
6.30 c                             w_enhe <> *blank
6.30 c                   if        w_enhe = w_enh1 or
6.30 c                             w_enhe = w_enh2 or
6.30 c                             w_enhe = w_enh3
6.35 c                   if        w_vkpr = 0
6.35 c                   eval      w_vkpr = w_vkai
6.35 c                   endif
6.30 c                   exsr      just_enhe
6.30 c                   endif
6.30 c                   endif
6.35 c*                  endif
6.30  *
6.13  *
6.13  * Hvis valutfakurs tilsier dette, omregner vi prisen med en gang
6.13  *
7.03 c                   if        w_kurs <> 1
7.03 c                   eval(h)   w_inp1 = w_inp1 / w_kurs
7.03 c                   eval(h)   w_inp2 = w_inp2 / w_kurs
7.03 c                   eval(h)   w_inp3 = w_inp3 / w_kurs
6.13 c                   endif
5.70  *
5.70  * Kaller program for henting av varetype
5.70  *
5.70 c                   call      'VL710R'
5.70 c                   parm                    p_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    lolage
5.70 c                   parm                    w_vtyp
6.24 c                   parm                    w_udat
6.24 c                   parm                    w_ldor
6.24 c                   parm                    b_inlr
6.3b c                   parm                    w_lv_nobb
6.3b c                   parm                    w_lv_modn
6.3b c                   parm                    w_lv_lldo
6.3b c                   parm                    w_lv_llva
      *
6.3c  * sjekker om logistikk-vare med best.leverandør
6.3c c/exec sql
6.3c c+ select vvlnob,
6.3c c+        vvllva,
6.3c c+        vvlmod
6.3c c+ into   :w_lv_nobb,
6.3c c+        :w_lv_llva,
6.3c c+        :w_lv_modn
6.3c c+ from   vvlepf
6.3c c+ where  vvlvnr = :vvvare and
6.3c c+        vvlldo = :loldor
6.3c c+ fetch first 1 rows only
6.3c c/end-exec
      *
      * Initierer bestilling-linje
      *
     c                   clear                   lodtlur
      *
     c     endring       tag
      *
     c     lodtl1_key    chain     lodtl1
     c                   if        %found
     c                   eval      b_endr = *on
     c                   else
     c                   eval      b_endr = *off
     c                   exsr      ny_linje
     c                   endif
      *
      * Viser registreringbildet
      *
     c     tc1win        tag
      *
     c                   exsr      xc1win
     c                   if        *inkc or *inkl
     c                   goto      avslutt
     c                   endif
      *
      * Gjør oppslag mot bestilling-linje for oppdatering
      *
     c     lodtlu_key    chain     lodtlur
      *
      * Henter verdier fra skjermbildet
      *
     c                   eval      ldanta = c1anta
     c                   eval      ldenh1 = c1enhe
     c                   eval      ldipny = c1inpr
     c                   eval      ldrab1 = c1rab1
     c                   eval      ldrab2 = c1rab2
     c                   eval      ldkplj = c1kplj
     c                   eval      ldkpny = c1kopr
     c                   eval      ldtek1 = c1tek1
     c                   eval      ldtek2 = c1tek2
     c                   eval      ldproj = c1proj
     c                   eval      ldakti = c1akti
     c                   eval      ldldat = *loval
6.32 c                   eval      ldomva = c1omva
7.03 c                   eval      ldlety = c1lety
8.01 c                   eval      ldeann = c1eann
8.05  *
8.05  * Hent leveringsdato fra bestillingshodet hvis ikke bekreftet.
8.05  *
8.05 c                   eval      llogl1_kode = '4'
8.05 c     llogl1_key    chain     llogl1
8.05 c                   if        not %found
8.05  * Hent leveringsdato fra bestillingshodet hvis leveringsdato er "blank".
8.05 c                   if        ldldat = *loval
8.05 c                   eval      ldldat = loldat
8.05 c                   if        lomoda <> *loval
8.05 c                   eval      ldldat = lomoda
8.05 c                   endif
8.05 c                   endif
8.05 c                   endif
      *
     c                   if        c1ldat <> 0
     c     *dmy          move      c1ldat        ldldat
     c                   endif
      *
      * Beregning av totaler
      *
     c                   eval      w_sumi = ldipny * ldanta
     c                   eval      w_sumr = 0
      *
      * Rabatt
      *
     c                   if        vvkord = 0
     c                   eval(h)   w_raba = (w_sumi * lorabp) / 100
     c                   eval      w_sumr = w_raba
     c                   endif
      *
     c                   if        ldrab1 <> 0
     c                   eval(h)   w_raba = (w_sumi - w_sumr) * ldrab1 / 100
     c                   eval      w_sumr = w_sumr + w_raba
     c                   endif
      *
     c                   if        ldrab2 <> 0
     c                   eval(h)   w_raba = (w_sumi - w_sumr) * ldrab2 / 100
     c                   eval      w_sumr = w_sumr + w_raba
     c                   endif
      *
      * Legger inn totaler
      *
     c                   eval      ldsumi = w_sumi
     c                   eval      ldsumr = w_sumr
     c                   eval      ldsumk = ldkpny * ldanta
      *
      * Dersom kredit-linjetype må summene korrigeres
      *
     c                   eval      vltyl1_ltyp = ldltyp
     c     vltyl1_key    chain     vltyl1
     c                   if        valkre = '-'
     c                   eval      ldsumi = ldsumi * -1
     c                   eval      ldsumr = ldsumr * -1
     c                   eval      ldsumk = ldsumk * -1
     c                   endif
      *
      * Legger inn faste opplysninger fra vare og bestilling
      *
     c                   eval      ldotyp = lootyp
     c                   eval      ldlage = lolage
      *
     c                   eval      ldldor = loldor
      *
     c                   eval      ldavde = 0
     c                   eval      ldkont = 0
     c                   eval      ldspes = 0
      *
      * Overstyrer ikke informasjon fra bestillingslinje hvis skaffevare
      *
     c                   if        ldlvre = 0
6.3b c                   if        w_lv_nobb > 0
6.3b c                   eval      ldnobb = w_lv_nobb
6.3b c                   eval      ldlvar = w_lv_llva
6.3b c                   else
     c                   eval      ldnobb = vvnobb
     c                   eval      ldlvar = vvlvar
6.3b c                   endif
     c                   eval      ldldor = vvldor
     c                   eval      ldogrp = vvogrp
     c                   eval      ldhgrp = vvhgrp
     c                   eval      ldugrp = vvugrp
     c                   endif
      *
      * Oversetter varetekst til store bokstaver dersom dette er valgt
      *
     c                   if        laahoy = 1
     c     lo:up         xlate     ldtek1        ldtek1
     c                   endif
      *
      * Oppdaterer bestilling-linje
      *
     c                   if        %found(lodtlu)
6.10 c                   time                    ldedat
6.10 c                   time                    ldetim
6.10 c                   eval      ldeusr = l_user
     c                   update    lodtlur
     c                   else
     c                   eval      ldfirm = w_firm
     c                   eval      ldnumm = w_numm
     c                   eval      ldsuff = w_suff
     c                   eval      ldline = w_line
6.10 c                   time                    ldodat
6.10 c                   time                    ldotim
6.10 c                   eval      ldousr = l_user
6.10 c                   time                    ldedat
6.10 c                   time                    ldetim
6.10 c                   eval      ldeusr = l_user
     c                   write     lodtlur
     c                   endif
6.21  *
6.21  * Hvis endring av kostpris eller lev.dato så oppdateres ordre
6.21  * 06.01.12 BOF fjernet dette fordi det erstattes av "matching"
6.21  *
6.21 c*                  if        ldornu <> 0
6.21 c*                  if        (s_inpr <> 0 and
6.21 c*                            s_inpr <> ldipny)
6.21 c*                  exsr      oppdat_ordre
6.21 c*                  endif
6.21 c*                  endif
7.01 c                   if        u_701 = *on
7.01 c                   if        ldornu <> 0    and
7.01 c                             ldldat <> *loval
7.01 c*                  if        (s_inpr <> 0 and
7.01 c*                            s_inpr <> ldipny)
7.01 c                   exsr      oppdat_ordre
7.01 c                   endif
7.01 c                   endif
      *
      * Oppdatering av lager
      *
     c                   exsr      oppdat_lager
      *
      * Legger inn kode som sier at minst en linje er registrert, og
      * returnerer om kostpris skal vises eller ikke
      *
     c                   eval      p_kode = *on
     c                   eval      p_kost = *in86
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initierer ny bestilling-linje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     ny_linje      begsr
      *
      * Fyller opp bestilling-linje
      *
E    c                   eval      ldltyp = p_ltyp
     c                   eval      ldvare = vvvare
     c                   eval      ldanta = p_anta
     c                   eval      ldenh1 = p_enhe
6.12 c                   if        sptek1 <> *blank
6.12 c                   eval      ldtek1 = vvtte1
6.12 c                   eval      ldtek2 = vvtte2
6.12 c                   else
     c                   eval      ldtek1 = vvtek1
     c                   eval      ldtek2 = vvtek2
6.12 c                   endif
7.05 c*                  eval      ldlety = p_lety
7.05 c                   eval      ldlety = w_lety
      *
     c                   if        p_enhe = *blank
     c                   eval      ldenh1 = w_enh1
     c                   endif
      *
      * Henter inn priser og rabatter
      *
     c                   eval      c1enhe = ldenh1
8.01 c                   eval      ldeann = w_eann
      *
     c                   exsr      hent_pris
      *
     c                   eval      ldipny = c1inpr
     c                   eval      ldkpny = c1kopr
     c                   eval      ldkplj = laalik
     c                   eval      ldrab1 = 0
     c                   eval      ldrab2 = 0
6.32  * Vi må sette mvakode på linja
6.32 c                   eval      ldomva = '1'
6.32 c                   if        lomkod = 1
6.32 c                   eval      ldomva = '0'
6.33 c                   goto      endtes
6.32 c                   endif
6.39  * Ny mva kode håndtering
6.39 c                   if        lomkod = 2
6.39 c                   eval      vmval1_mkod = vvkmva
6.39 c     vmval1_key    chain     vmval1
6.39 c                   if        %found
6.39 c                   eval      ldomva = vamkui
6.39 c                   endif
6.39 c                   goto      endtes
6.39 c                   endif
6.32  * Sjekk evnt overstyring fra varen
6.32 c                   eval      vmval1_mkod = vvkmva
6.32 c     vmval1_key    chain     vmval1
6.32 c                   if        %found
6.32 c                   eval      ldomva = vamkla
6.32 c                   else
6.32 c                   select
6.32 c                   when      vvkmva = 1
6.32 c                   eval      ldomva = '0'
6.32 c                   when      vvkmva = 2
6.32 c                   eval      ldomva = 'I'
6.32 c                   when      vvkmva = 3
6.32 c                   eval      ldomva = 'K'
6.32 c                   endsl
6.32 c                   endif
      *
6.33 c     endtes        tag
     c     end_ny        endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * C1 Behandle bilde for registrering av varelinje med antall
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1win        begsr
      *
      * Initierer skjermbildet
      *
     c                   eval      *in81 = w_enh1 = *blank
      *
     c                   eval      *in82 = w_enh2 = *blank
     c                   eval      *in83 = w_enh3 = *blank
      *
     c                   eval      c1anta = ldanta
     c                   eval      c1enhe = ldenh1
     c                   eval      c1inpr = ldipny
     c                   eval      c1rab1 = ldrab1
     c                   eval      c1rab2 = ldrab2
     c                   eval      c1lety = ldlety
     c                   eval      c1kplj = ldkplj
     c                   eval      c1kopr = ldkpny
     c                   eval      c1ltyp = ldltyp
6.12 c*                  if        sptek1 <> *blank
6.12 c*                  eval      c1tek1 = sptek1
6.12 c*                  eval      c1tek2 = sptek2
6.12 c*                  else
     c                   eval      c1tek1 = ldtek1
     c                   eval      c1tek2 = ldtek2
6.12 c*                  endif
     c                   eval      c1enh1 = w_enh1
     c                   eval      c1enh2 = w_enh2
     c                   eval      c1enh3 = w_enh3
     c                   eval      c1vare = ldvare
6.3b c                   if        w_lv_nobb > 0
6.3b c                   eval      c1nobb = w_lv_nobb
6.3b c                   eval      c1nmod = w_lv_modn
6.3b c                   else
     c                   eval      c1nobb = vvnobb
     c                   eval      c1nmod = vvnmod
6.3b c                   endif
8.01 c**                 eval      c1eann = w_eann
8.01 c                   eval      c1eann = ldeann
5.70 c                   eval      c1vtyp = w_vtyp
     c                   eval      c1inim = 0
     c                   eval      c1ldat = 0
     c                   eval      c1proj = ldproj
     c                   eval      c1akti = ldakti
     c                   eval      c1ant1 = 0
     c                   eval      c1ant2 = 0
     c                   eval      c1ant3 = 0
6.32 c                   eval      c1omva = ldomva
      *
     c                   if        ldlvre = 0
6.3b c                   if        w_lv_nobb > 0
6.3b c                   eval      c1nobb = w_lv_nobb
6.3b c                   eval      c1nmod = w_lv_modn
6.3b c                   else
     c                   eval      c1nobb = vvnobb
     c                   eval      c1nmod = vvnmod
6.3b c                   endif
     c                   eval      c1eann = w_eann
5.70 c                   eval      c1vtyp = w_vtyp
     c                   eval      c1crdo = vvcrdo
6.24 c                   if        w_udat <> *loval
6.24 c     *dmy          move      w_udat        c1udat
     c                   endif
     c                   else
     c                   eval      c1nobb = ldnobb
     c                   endif
6.31  * henter info fra VA
6.31 c                   eval      jvarl1_vare = ldvare
6.31 c     jvarl1_key    chain     jvarl1
6.31 c                   if        %found(jvarl1)
6.31 c                   eval      c1enob = jvenob
6.31 c                   eval      c1nmod = jvmodn
6.31 c                   else
6.31 c                   eval      c1enob = 0
6.31 c                   endif
      *
      *  Sjekker mot utgått dato
      *
6.24 c                   if        w_udat <> *loval and
6.14 c                             b_endr  = *off
     c                   eval      *in42 = *on
     c                   endif
      *
     c                   if        ldldat <> *loval
     c     *dmy          move      ldldat        c1ldat
     c                   endif
      *
      * Kaller subrutine for å vise antall i andre enheter
      *
     c                   exsr      kalkulator
      *
     c                   if        w_enh1 = *blank
     c                   eval      c1enh1 = ldenh1
     c                   eval      c1ant1 = ldanta
     c                   endif
      *
      * Henter lagerbeholdning
      *
     c                   exsr      lagerbeholdn
      *
      * Tar vare på eksisterende verdier
      *
     c                   eval      s_ant1 = c1ant1
     c                   eval      s_ant2 = c1ant2
     c                   eval      s_ant3 = c1ant3
     c                   eval      s_anta = c1anta
     c                   eval      s_enhe = c1enhe
     c                   eval      s_inpr = c1inpr
     c                   eval      s_kplj = c1kplj
6.21 c                   eval      s_ldat = ldldat
      *
      * Viser bildet
      *
     c     c1tagb        tag
6.3a  *
6.3a  *henter riktig gtin
8.04  * Henter GTIN nummer på ny måte
6.3a  *
8.04 c*                  call      'VE713R'
8.04 c*                  parm                    w_firm
8.04 c*                  parm                    loldor
8.04 c*                  parm                    p_vare
8.04 c*                  parm                    c1enhe
8.04 c*                  parm                    c1eann
8.04 c*                  parm                    w_east
6.3a  *
8.04 c                   call      'VE723R'
8.04 c                   parm                    w_firm
8.04 c                   parm                    loldor
8.04 c                   parm                    lolage
8.04 c                   parm                    p_vare
8.04 c                   parm                    c1enhe
8.04 c                   parm                    c1eann
8.04 c                   parm                    w_east
      *
     c                   eval      s_rab1 = c1rab1
     c                   eval      s_rab2 = c1rab2
      *
     c                   eval      c1ntpr = c1inpr
     c                   if        c1rab1 > 0
     c                   eval(h)   c1ntpr = c1ntpr - (c1ntpr * c1rab1 / 100)
     c                   endif
     c                   if        c1rab2 > 0
     c                   eval(h)   c1ntpr = c1ntpr - (c1ntpr * c1rab2 / 100)
     c                   endif
      *
     c                   eval      c1ntim = c1ntpr * w_mvat
      *
6.16 c                   if        (c1ntpr*c1anta) < 999999999,99
     c                   eval      c1ntsu = c1ntpr * c1anta
     c                   eval      c1nisu = c1ntsu * w_mvat
6.16 c                   else
6.16 c                   eval      *in49 = *on
6.16 c                   eval      c1ntsu = 999999999,99
6.16 c                   eval      c1nisu = 999999999,99
6.16 c                   endif
      *
     c                   exfmt     c1win
      *
      *
     c                   eval      b_forn = *off
      *
      * F2=Manuell beregning av kostpris
      *
     c                   if        *inkb = *on
     c                   call      'FO524R'
     c                   parm                    w_kopr
     c                   if        w_kopr <> 0
     c                   eval      c1kopr = w_kopr
     c                   endif
     c                   goto      c1tagb
     c                   endif
      *
      * F3=Gå tilbake
      *
     c                   if        *inkc or *inkl
     c                   goto      end_c1win
     c                   endif
      *
      * F4=Spørring
      *
     c                   if        *inkd  = *on
      *
     c                   if        w_afld = 'C1PROJ'
5.61 c                   eval      w_kont = 0
5.61 c                   eval      w_spes = 0
     c                   call      'RP500R'
     c                   parm                    w_firm
     c                   parm                    c1proj
5.61 c                   parm                    c1akti
5.61 c                   parm                    w_kont
5.61 c                   parm                    w_spes
     c                   parm                    w_txt1
     c                   goto      c1tagb
     c                   endif
      *
     c                   endif
      *
      * F8=Produktinformasjon
      *
     c                   if        *inkh = *on
     c                   call      'FO704C'
     c                   parm                    w_firm
     c                   parm                    ldvare
     c                   goto      c1tagb
     c                   endif
      *
      * F9=Lagerinformasjon
      *
     c                   if        *inki = *on
     c                   call      'LL502R'
     c                   parm                    w_firm
     c                   parm                    ldvare
     c                   goto      c1tagb
     c                   endif
      *
      *
6.23  * F10=VA Informasjon
6.23 c                   if        *inkj = *on
6.23 c                   call      'JV145R'
6.23 c                   parm                    w_firm
6.23 c                   parm                    ldvare
6.28 c                   parm                    w_prgr
6.28 c                   parm                    ldlety
6.28 c                   parm                    lobdat
6.28 c                   parm                    loldor
6.23 c                   goto      c1tagb
6.23 c                   endif
6.23  *
6.36  *
6.36  * F18=NOBB informasjon
6.36  *
6.36 c                   if        *inks = *on
6.36 c                   exsr      vis_nobb
6.36 c                   goto      c1tagb
6.36 c                   endif
      *
      * F23=Skifte mellom skjul/vis kostpris (dersom tillatt på bruker)
      *
     c                   if        *inkx = *on and
     c                             lbko01 = 1
     c                   if        *in86 = *off
     c                   eval      *in86 = *on
     c                   else
     c                   eval      *in86 = *off
     c                   endif
     c                   goto      c1tagb
     c                   endif
      *
      * Enhet må oppgis dersom varen ikke har enhet
      *
     c                   if        w_enh1 = *blank
      *
     c                   if        c1enh1 = *blank
     c                   eval      *in31 = *on
     c                   goto      c1tagb
     c                   endif
      *
     c                   eval      venhl1_eenh = c1enh1
     c     venhl1_key    chain     venhl1
     c                   if        not %found
     c                   eval      *in32 = *on
     c                   goto      c1tagb
     c                   endif
      *
     c                   endif
      *
      * Sjekker og behandler antall
      *
     c                   if        c1ant1 = 0 and
     c                             c1ant2 = 0 and
     c                             c1ant3 = 0 and
     c                             c1anta = 0
     c                   eval      *in33 = *on
     c                   goto      c1tagb
     c                   endif
      *
     c                   select
     c                   when      c1ant1 <> s_ant1 and
     c                             c1ant1 <> 0
     c                   eval      s_ant1 = c1ant1
     c                   eval      c1anta = c1ant1
     c                   eval      c1enhe = c1enh1
6.15 c                   eval      b_lage = *on
     c                   when      c1ant2 <> s_ant2 and
     c                             c1ant2 <> 0
     c                   eval      s_ant2 = c1ant2
     c                   eval      c1anta = c1ant2
     c                   eval      c1enhe = c1enh2
6.15 c                   eval      b_lage = *on
     c                   when      c1ant3 <> s_ant3 and
     c                             c1ant3 <> 0
     c                   eval      s_ant3 = c1ant3
     c                   eval      c1anta = c1ant3
     c                   eval      c1enhe = c1enh3
6.15 c                   eval      b_lage = *on
     c                   endsl
      *
      * Sjekk varetekst
      *
     c                   if        c1tek1 = *blank
     c                   eval      *in35 = *on
     c                   goto      c1tagb
     c                   endif
      *
      * Sjekk leveringsdato
      *
     c                   if        c1ldat <> 0
     c     *dmy          test(d)                 c1ldat                 40
     c                   if        *in40 = *on
     c                   goto      c1tagb
     c                   endif
     c                   endif
      *
      * Sjekk om leveringsdato er større enn bestillingsdato
      *
     c                   if        c1ldat <> 0
     c     *dmy          move      c1ldat        w_ldat
     c                   if        w_ldat < lobdat
     c                   eval      *in41 = *on
     c                   goto      c1tagb
     c                   endif
     c                   endif
      *
      * Sjekk utgår dato
      *
6.24 c                   if        w_udat <> *loval and
6.24 c                             w_udat <= w_ddat and
6.14 c                             b_endr = *off
     c                   eval      *in43 = *on
6.25 c                   if        lokamp <> *blank
6.25 c                   eval      *in43 = *off
6.25 c                   exsr      xc2msg
6.25 c                   if        b_endr_udat = *off
6.25  * Overstyring har ikke skjedd, type settes tilbake,ellers oppdatering
6.25 c                   eval      *in43 = *on
6.25 c                   goto      c1tagb
6.25 c                   else
6.25 c                   eval      c1udat = 0
6.25 c                   eval      w_udat = *loval
6.25 c                   endif
6.25 c                   else
     c                   goto      c1tagb
6.25 c                   endif
     c                   endif
      *
      * Sjekk prosjekt
      *
6.20 c**                 if        c1proj <> 0 and
6.20 c                   if        c1proj <> *blank and
     c                             c1proj <> ldproj
     c                   eval      w_kont = 0
     c                   eval      w_spes = 0
     c                   call      'RS770R'
     c                   parm                    w_retk
     c                   parm                    c1proj
     c                   parm                    c1akti
     c                   parm                    w_kont
     c                   parm                    w_spes
     c                   parm                    w_txt1
     c                   parm                    w_txt2
     c                   if        w_retk <> *blank
     c                   eval      *in45 = *on
     c                   goto      c1tagb
     c                   endif
     c                   endif
      *
      * Sjekk aktivitet
      *
     c                   if        c1akti <> *blank and
     c                             c1akti <> ldakti
     c                   call      'RS721R'
     c                   parm                    w_retk
     c                   parm                    c1akti
     c                   parm                    w_txt1
     c                   if        w_retk <> *blank
     c                   eval      *in46 = *on
     c                   goto      c1tagb
     c                   endif
     c                   endif
      *
      * Kaller subrutine for å vise antall i andre enheter
      *
     c                   if        w_enh2 <> *blank
     c                   exsr      kalkulator
     c                   eval      s_ant1 = c1ant1
     c                   eval      s_ant2 = c1ant2
     c                   eval      s_ant3 = c1ant3
     c                   endif
      *
      * Henter ny lagerbeholdning dersom enhet er endret
      *
     c                   if        c1enhe <> s_enhe
     c                   exsr      lagerbeholdn
     c                   endif
      *
      * Dersom enhet eller leveringstype er endret, og pris ikke er
      * overstyrt, kalkuleres ny pris og rabatt
      *
     c                   if        c1enhe <> s_enhe
      *
     c                   eval      s_enhe = c1enhe
      *
     c                   if        c1inpr = s_inpr or
     c                             c1inpr = 0
      *
      * Henter priser og rabatter
      *
     c                   exsr      hent_pris
      *
      * Oppdater ny pris
      *
     c                   eval      s_anta = c1anta
     c                   eval      s_inpr = c1inpr
     c                   eval      s_rab1 = c1rab1
     c                   eval      s_rab2 = c1rab2
      *
     c                   eval      b_forn = *on
      *
     c                   endif
     c                   endif
      *
      * Behandling av innkjøpspris inkl.mva.
      *
     c                   if        c1inim <> 0
     c                   eval(h)   c1inpr = c1inim * w_mvaf
     c                   eval      c1inim = 0
     c                   goto      c1tagb
     c                   endif
      *
      * Viser bildet på nytt dersom dette er angitt
      *
     c                   if        b_forn = *on
     c                   goto      c1tagb
     c                   endif
      *
      * Viser bildet på nytt dersom varen har flere enheter, antall er
      * endret og bildet ikke allerede er vist
      *
     c                   if        w_enh2 <> *blank and
     c                             c1anta <> s_anta
     c                   eval      s_anta = c1anta
     c                   goto      c1tagb
     c                   endif
      *
      * Sjekk pris
      *
     c                   if        c1inpr <= 0,01
     c                   eval      *in39 = *on
     c                   goto      c1tagb
     c                   endif
7.04  *
7.04  * Sjekk at leveringstype = 0 eller 1
7.04  *
7.04 c                   if            c1lety =  0
7.04 c                             or  c1lety =  7
7.04 c                             or  c1lety =  1
7.04 c                             or (c1lety = 9
7.04 c                             and u_hbdl = *on)
7.04 c**                           and lbko10 = 1)
7.04  *                            Da er det ok
7.04 c                   else
7.04 c                   eval      *in44 = *on
7.04 c                   goto      c1tagb
7.04 c                   endif
      *
      * Gir advarsel desom kostpris oppdateringskode er endret
      *
     c                   if        c1kplj <> s_kplj
     c                   eval      s_kplj = c1kplj
     c                   exfmt     c1msg
     c                   if        *inkc or *inkl
     c                   goto      c1tagb
     c                   endif
     c                   endif
      *
      * Korrigerer vekk fra lager dersom linjen er endret
      *
     c                   if        b_endr = *on
     c                   eval      ldanta = ldanta * -1
     c                   exsr      oppdat_lager
     c                   endif
      *
     c     end_c1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter opplysninger om varen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_pris     begsr
      *
     c                   select
     c                   when      c1enhe = w_enh1
     c                   eval      c1inpr = w_inp1
     c                   eval      c1kopr = w_kop1
     c                   when      c1enhe = w_enh2
     c                   eval      c1inpr = w_inp2
     c                   eval      c1kopr = w_kop2
     c                   when      c1enhe = w_enh3
     c                   eval      c1inpr = w_inp3
     c                   eval      c1kopr = w_kop3
     c                   endsl
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Kalkulator for omregning mellom enheter
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     kalkulator    begsr
      *
     c                   if        c1enhe <> *blank
      *
     c                   eval      c1ant1 = 0
     c                   eval      c1ant2 = 0
     c                   eval      c1ant3 = 0
      *
     c                   select
     c                   when      c1enhe = w_enh1
     c                   eval      c1ant1 = c1anta
     c                   if        w_omr2 <> 0
     c                   eval(h)   c1ant2 = c1anta / w_omr2
     c                   endif
     c                   if        w_omr3 <> 0
     c                   eval(h)   c1ant3 = c1anta / w_omr3
     c                   endif
     c                   when      c1enhe = w_enh2
     c                   eval(h)   c1ant1 = c1anta * w_omr2
     c                   eval      c1ant2 = c1anta
     c                   if        w_omr3 <> 0
     c                   eval(h)   c1ant3 = c1ant1 / w_omr3
     c                   endif
     c                   when      c1enhe = w_enh3
     c                   eval(h)   c1ant1 = c1anta * w_omr3
     c                   if        w_omr2 <> 0
     c                   eval(h)   c1ant2 = c1ant1 / w_omr2
     c                   endif
     c                   eval      c1ant3 = c1anta
     c                   endsl
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter beholdingstall dersom lagervare og lager kjøres
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     lagerbeholdn  begsr
      *
     c                   eval      *in87  = *off
     c                   eval      c1behl = 0
     c                   eval      c1bbes = 0
     c                   eval      c1pakk = 0
     c                   eval      c1disp = 0
      *
     c                   if        laalag = 1 and
5.70 c                             w_vtyp = 'L'
     c                   call      'VL700R'
     c                   parm                    p_firm
     c                   parm                    ldvare
     c                   parm                    lolage
     c                   parm                    c1enhe
     c                   parm                    c1behl
     c                   parm                    c1bbes
     c                   parm                    c1pakk
     c                   parm                    c1disp
     c                   eval      *in87 = *on
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdat_lager  begsr
      *
     c                   if        laalag = 1 and
5.70 c                             w_vtyp = 'L'
     c                   else
     c                   goto      end_lager
     c                   endif
6.15  *
6.15  * Ingen lageroppdatering dersom dette er endring og
6.15  * antall, enhet og lager er urørt
6.15  *
6.15 c                   if        b_endr = *on    and
6.15 c                             b_lage = *off   and
6.15 c                             c1anta = s_anta and
6.15 c                             c1ant1 = s_ant1 and
6.15 c                             c1ant2 = s_ant2 and
6.15 c                             c1ant3 = s_ant3 and
6.15 c                             c1enhe = s_enhe
6.15 c                   leavesr
6.15 c                   endif
      *
      * Legger over verdier i overførings-felter
      *
     c                   eval      hlvare = ldvare
     c                   eval      hllage = lolage
     c                   eval      hlenhe = ldenh1
     c                   eval      hldato = *loval
     c                   eval      hltime = *loval
     c                   eval      hlotyp = lootyp
     c                   eval      hlltyp = ldltyp
      *
     c                   eval      hlanta = ldanta
     c                   eval      hlkopr = ldkpny
6.17 c                   move      ldnumm        w_numa
6.17 c                   move      ldline        w_lina
6.17 c                   eval      hlrefr = 'B:' + w_numa +
6.17 c                                      ' L:' + w_lina
     c                   eval      hlonum = ldnumm
     c                   eval      hlolin = ldline
      *
     c                   eval      hlsyst = 'L'
     c                   eval      hlaltk = *blank
     c                   eval      hlrest = *blank
3.12 c                   move      ldlety        hllety
      *
      * Kaller lageroppdaterings-program
      *
     c                   eval      w_stat = *blank
      *
     c                   call      'VL001R'
     c                   parm                    w_stat
     c                   parm                    hlrec
      *
     c     end_lager     endsr
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  * Subrutine for oppdatering av ordre
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  *
6.21 c     oppdat_ordre  begsr
6.21  *
6.21 c                   eval      fodtlu_numm = ldornu
6.21 c                   eval      fodtlu_suff = ldorsu
6.21 c                   eval      fodtlu_line = ldorli
6.21 c     fodtlu_key    chain     fodtlu
7.01 c                   if        u_701 = *on
7.01 c                   if        %found(fodtlu)   and
7.01 c                             fdldat <> ldldat
7.01 c                   eval      fdldat = ldldat
7.01 c                   update    fodtlur
7.01 c                   endif
7.01 c                   else
6.21 c                   if        %found(fodtlu)
6.21 c                   eval      p_orde = '1'
6.21 c                   eval      fdkopr = ldipny
6.22 c                   eval      fdsumk = fdkopr * fdanta
6.21 c                   update    fodtlur
6.21 c                   endif
6.22 c                   call      'FO700R '
6.22 c                   parm                    w_firm
6.22 c                   parm                    ldornu
6.22 c                   parm                    ldorsu
7.01 c                   endif
6.21 c                   endsr
      *
      *
6.25  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.25  * Subrutine for å bekrefte overstyring av utgår dato
6.25  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.25  *
6.25 c     xc2msg        begsr
6.25  *
6.25 c                   eval      b_endr_udat = *off
6.25  *
6.25 c     c2tagm        tag
6.25  *
6.25 c                   exfmt     c2msg
6.25  *
6.25 c                   select
6.25 c                   when      *inkc or *inkl
6.25 c                   leavesr
6.25 c                   when      *inkg
6.25 c                   eval      b_endr_udat = *on
6.25 c                   leavesr
6.25 c                   endsl
6.25  *
6.25 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * ERROR-rutine
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *pssr         begsr
      *
     c                   eval      *in49 = *on
     c                   goto      tc1win
      *
     c                   endsr
6.30  *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  * Subrutine for justere kampanjepris hentet fra VA til riktig enhet
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  *
6.30 c     just_enhe     begsr
6.30  *
6.30 c                   select
6.30 c                   when      w_enh1 = w_enhe
6.30 c                   eval      w_inp1 = w_vkai
6.35 c                   eval      w_kop1 = w_vkpr
6.30 c                   eval      w_inp2 = w_inp1 * w_omr2 / 1
6.30 c                   eval      w_inp3 = w_inp1 * w_omr3 / 1
6.35 c                   eval      w_kop2 = w_kop1 * w_omr2 / 1
6.35 c                   eval      w_kop3 = w_kop1 * w_omr3 / 1
6.30 c                   when      w_enh2 = w_enhe
6.30 c                   eval      w_inp2 = w_vkai
6.35 c                   eval      w_kop2 = w_vkpr
6.30 c                   eval      w_inp1 = w_inp2 * 1/ w_omr2
6.30 c                   eval      w_inp3 = w_inp2 * w_omr3 / w_omr2
6.35 c                   eval      w_kop1 = w_kop2 * 1/ w_omr2
6.35 c                   eval      w_kop3 = w_kop2 * w_omr3 / 1
6.30 c                   when      w_enh3 = w_enhe
6.30 c                   eval      w_inp3 = w_vkai
6.35 c                   eval      w_kop3 = w_vkpr
6.30 c                   eval      w_inp1 = w_inp3 * 1/ w_omr3
6.30 c                   eval      w_inp2 = w_inp3 * w_omr2 / w_omr3
6.35 c                   eval      w_kop1 = w_kop3 * 1/ w_omr3
6.35 c                   eval      w_kop2 = w_kop3 * w_omr2 / w_omr3
6.30 c                   endsl
6.30  *
6.30 c                   endsr
      *
6.36  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.36  * Vis NOBB informasjon for varen. Samme prinsipp som preview av utskrifter
6.36  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.36  *
6.36 c     vis_nobb      begsr
6.36 c
6.36 c                   eval      p_nobb = %char(c1nobb)
6.36 c                   call      'AP633R'
6.36 c                   parm                    p_nobb
6.36  *
6.36 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_kode
     c                   parm                    p_firm
     c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_line
     c                   parm                    p_ltyp
     c                   parm                    p_lety
     c                   parm                    p_vare
     c                   parm                    p_anta
     c                   parm                    p_enhe
     c                   parm                    p_kost
6.21 c                   parm                    p_orde
6.27 c                   parm                    p_kamp
      *
      * Key for oppslag på status
      *
     c     lstsl1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppslag på bruker
      *
     c     lusrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lusrl1_user
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      * Key for oppslag på linjetype
      *
     c     vltyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltyl1_ltyp
      *
      * Key for oppslag på enhet
      *
     c     venhl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    venhl1_eenh
      *
      * Key for oppslag på bestilling-hode
      *
     c     lohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
      *
      * Key for oppdatering av bestilling-linje
      *
     c     lodtlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
     c                   kfld                    w_line
      *
      * Key for oppslag på bestilling-linje
      *
     c     lodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
     c                   kfld                    w_line
7.06  *
7.06  * Key for oppdatering av bestilling-linje
7.06  *
7.06 c     lodtlr_key    klist
7.06 c                   kfld                    w_firm
7.06 c                   kfld                    w_numm
7.06 c                   kfld                    w_suff
7.06 c                   kfld                    lodtlr_line
7.06  *
7.06  * Key for oppdatering av bestilling-linje
7.06  *
7.06 c     lodtlr_key2   klist
7.06 c                   kfld                    w_firm
7.06 c                   kfld                    w_numm
7.06 c                   kfld                    w_suff
6.12  *
6.12  * Key for oppslag på språkregister
6.12  *
6.12 c     vsprl1_key    klist
6.12 c                   kfld                    w_firm
6.12 c                   kfld                    vsprl1_tvar
6.12 c                   kfld                    vsprl1_tspr
6.12  *
6.12  * Key for oppslag på landkode
6.11  *
6.12 c     ra12l1_key    klist
6.12 c                   kfld                    w_firm
6.12 c                   kfld                    ra12l1_lkod
6.12  *
6.12  * Key for leverandør-register
6.12  *
6.12 C     rlevl1_key    klist
6.12 C                   kfld                    w_firm
6.12 C                   kfld                    rlevl1_levr
6.21  *
6.21  * Key for oppdat av ordre-linje
6.21  *
6.21 c     fodtlu_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    fodtlu_numm
6.21 c                   kfld                    fodtlu_suff
6.21 c                   kfld                    fodtlu_line
6.30  *
6.30  * Key for oppslag på vare i VA
6.30  *
6.30 c     jvarl1_key    klist
6.30 c                   kfld                    jvarl1_vare
6.30  *
6.32  *
6.32  * Key for oppdat av mva kode
6.32  *
6.32 c     vmval1_key    klist
6.32 c                   kfld                    w_firm
6.32 c                   kfld                    vmval1_mkod
8.05  *
8.05  *  Key for file : Logg
8.05  *
8.05 c     llogl1_key    klist
8.05 c                   kfld                    w_firm
8.05 c                   kfld                    w_numm
8.05 c                   kfld                    w_suff
8.05 c                   kfld                    llogl1_kode
      *
      * Henter parametre
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_numm = p_numm
     c                   eval      w_suff = p_suff
     c                   eval      w_line = p_line
6.21 c                   eval      p_orde = '0'
7.01  *
7.01  * Endring 7.01
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'LD105R_701':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_701 = *on;
7.01   endif;
7.03  *
7.03  * Endring 7.03
7.03  *
7.03   CallP CO402R(l_filg:w_firm:0:'LD105R_703':
7.03                    co402_verdi1:co402_verdi2);
7.03   if co402_verdi1 = '1';
7.03     u_s703 = *on;
7.03   endif;
6.3d  *
6.3d  * Endring 6.3d
6.3d  *
6.3d   CallP CO402R(l_filg:w_firm:0:'TILB_INNKJ_PERIOD':
6.3d                    co402_verdi1:co402_verdi2);
6.3d   if co402_verdi1 = '1';
6.3d     u_63d = *on;
6.3d     *in50 = *on;
6.3d   endif;
7.04  *
7.04  * Brukes egen kontering på dir.lev mot prosjekt
7.04  *
7.04   CallP CO402R(l_filg:w_firm:0:'KTO_INNKJ_PROSJ':
7.04                    co402_verdi1:co402_verdi2);
7.04   if co402_verdi1 = '1';
7.04     u_hbdl = *on;
7.04   endif;
      *
      * Legger inn type for oppdatering av lager (R=Registrering)
      *
     c                   eval      hltype = 'R'
      *
      * Hent opplysninger fra koderegister
      *
     c     lstsl1_key    chain     lstsl1r
      *
      * Henter opplysninger fra bruker
      *
     c                   eval      lusrl1_user = l_user
     c     lusrl1_key    chain     lusrl1
      *
      * Sett indikator som styrer kostpris i henhold til parameter
      *
     c                   eval      *in86 = p_kost
7.03  * Hvis bruker kan kjøre inng.faktura, kan man også få sette
7.03  * lev.type = 1
7.03  *
7.03 c                   if        u_s703 = *on
7.03 c                   if        lbko10 = 1
7.03 c                   eval      *in50  = *on
7.03 c                   endif
7.03 c                   endif
      *
      * Hent opplysninger fra bestilling-hode
      *
     c     lohel1_key    chain     lohel1r
      *
      * Henter mva-sats
      *
     c                   call      'FÅ705R'
     c                   parm                    p_firm
     c                   parm                    lobdat
     c                   parm                    w_mvap
     c                   parm                    w_mvat
     c                   parm                    w_mvaf
5.70  *
5.70  * Hent prisgruppe
5.70  *
5.70 c                   eval      w_avde = 0
5.70 c                   call      'VL712R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    lolage
5.70 c                   parm                    w_avde
5.70 c                   parm                    w_prgr
      *
      * Initierer skjermbildet
      *
     c                   time                    w_ddat
     c                   eval      c1vare = p_vare
7.06  *
7.06  * Sjekk om det ligger linjer på bestillingen
7.06  *
7.06 c                   eval      lodtlr_line = 0
7.06 c     lodtlr_key    setll     lodtlr
7.06 c     lodtlr_key2   reade     lodtlr
7.06 c                   dow       not %eof
7.06 c                   if        ldlety <> 0
7.06 c                   eval      w_lety = ldlety
7.06 c                   endif
7.06 c     lodtlr_key2   reade     lodtlr
7.06 c                   enddo
7.06  *
7.06  * Hvis leveringstype er <> 0 i parameterlista, benyttes denne. Hvis ikke hentes
7.06  * leveringstypen her.
7.06  *
7.06 c                   if        p_lety <> 0
7.06 c                   eval      w_lety = p_lety
7.06 c                   endif
7.06  *
      *
     c                   endsr
