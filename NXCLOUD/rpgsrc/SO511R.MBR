      /TITLE  ASOFAK Faktura, vise fakturalinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : SO511R
      * Beskrivelse . . : Faktura, vise fakturalinje
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       220699 HKO  Nytt program
5.51  *       040107 AMD  Lagt inn visning av leveringstype
      *       200208 KOB  Lagt inn visning av konti for salg, innkjøp og kost
6.nu  * R6.30 270514 bsa  Programmet gjennomgått mtp nummerutvidelser.
6.30  * R6.30 060116 bsa  Endret rekkefølge i skjermbilde på rabattfelter.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fsohel1    if   e           k disk    rename(sohepfr:sohel1r)
     fsodtl1    if   e           k disk    rename(sodtpfr:sodtl1r)
     fvkhvl1    if   e           k disk    rename(vkhvpfr:vkhvl1r)
      *
     fso511d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_firm          s                   like(sdfirm)
     d p_aarr          s                   like(sdaarr)
6.nu d p_binr          s                   like(sdbinr)
6.nu d p_numm          s                   like(sdnumm)
     d p_suff          s                   like(sdsuff)
     d p_ktxt          s                   like(sdktxt)
     d p_line          s                   like(sdline)
      *
      * Definering av Local data area
      *
     d                uds
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d sohel1_aarr     s                   like(soaarr)
     d sohel1_binr     s                   like(sobinr)
     d sohel1_numm     s                   like(sonumm)
     d sohel1_suff     s                   like(sosuff)
     d sodtl1_aarr     s                   like(sdaarr)
     d sodtl1_binr     s                   like(sdbinr)
     d sodtl1_numm     s                   like(sdnumm)
     d sodtl1_suff     s                   like(sdsuff)
     d sodtl1_ktxt     s                   like(sdktxt)
     d sodtl1_line     s                   like(sdline)
     d vkhvl1_kkod     s                   like(vakkod)
     d vkhvl1_klty     s                   like(vaklty)
     d vkhvl1_koty     s                   like(vakoty)
      *
      * Definering av variable
      *
     d w_firm          s                   like(sdfirm)
     d w_raba          s                   like(sdrab1)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B1 Behandle bildet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Initierer skjermbildet
      *
     c                   eval      sohel1_aarr = p_aarr
     c                   eval      sohel1_binr = p_binr
     c                   eval      sohel1_numm = p_numm
     c                   eval      sohel1_suff = p_suff
     c     sohel1_key    chain     sohel1                             90
     c                   if        *in90 = *on
     c                   goto      avslutt
     c                   endif
      *
     c                   eval      sodtl1_aarr = p_aarr
     c                   eval      sodtl1_binr = p_binr
     c                   eval      sodtl1_numm = p_numm
     c                   eval      sodtl1_suff = p_suff
     c                   eval      sodtl1_ktxt = p_ktxt
     c                   eval      sodtl1_line = p_line
     c     sodtl1_key    chain     sodtl1                             90
     c                   if        *in90 = *on
     c                   goto      avslutt
     c                   endif
      *
     c     *dmy          move      sofkda        b1fkda
      *
     c                   eval      b1binr = sdbinr
     c                   eval      b1numm = sdnumm
     c                   eval      b1otyp = sdotyp
     c                   eval      b1ltyp = sdltyp
5.51 c                   eval      b1lety = sdlety
     c                   eval      b1vare = sdvare
     c                   eval      b1tek1 = sdtek1
     c                   eval      b1tek2 = sdtek2
     c                   eval      b1anta = sdanta
     c                   eval      b1enh1 = sdenh1
     c                   eval      b1kpri = sdkpri
     c                   eval      b1kopr = sdkopr
     c                   eval      b1sapr = sdsapr
     c                   eval      b1kopr = sdkopr
     c                   eval      b1rkr1 = sdrkr1
     c                   eval      b1rkr2 = sdrkr2
     c                   eval      b1rkro = sdrkro
     c                   eval      b1rab1 = sdrab1
     c                   eval      b1rab2 = sdrab2
      *
     c                   eval      b1salg = sdsalg - sdrkr1 - sdrkr2 -
     c                                      sdrkro
      *
     c                   eval      b1avde = sdavde
     c                   eval      b1kont = sdkont
     c                   eval      b1spes = sdspes
     c                   eval      b1proj = sdproj
     c                   eval      b1akti = sdakti
     c                   eval      b1khev = sdkhev
      *
     c                   exsr      hent_kto_henv
      *
     c                   eval      b1kav3 = vakav3
     c                   eval      b1kko3 = vakko3
     c                   eval      b1ksp3 = vaksp3
      *
     c                   eval      b1kav4 = vakav4
     c                   eval      b1kko4 = vakko4
     c                   eval      b1ksp4 = vaksp4
      *
     c                   eval      b1kav8 = vakav5
     c                   eval      b1kko8 = vakko5
     c                   eval      b1ksp8 = vaksp5
      *
      * Behandler bildet
      *
     c     b1taga        tag
     c                   exfmt     b1bld
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   endsl
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hent kontohenvisningsinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_kto_henv begsr
      *
     c                   eval      vkhvl1_kkod = sdkhev
     c                   eval      vkhvl1_klty = sdlety
     c                   eval      vkhvl1_koty = sdotyp
     c     vkhvl1_key    chain     vkhvl1
      *
     c                   if        not%found
     c                   eval      vkhvl1_koty = ' '
     c     vkhvl1_key    chain     vkhvl1
      *
     c                   if        not%found
     c                   eval      vkhvl1_klty = 0
     c     vkhvl1_key    chain     vkhvl1
     c                   endif
      *
     c                   if        not%found
     c                   eval      vkhvl1_kkod = 'STD '
     c                   eval      b1khev = 'STD '
     c     vkhvl1_key    chain     vkhvl1
     c                   endif
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_aarr
6.nu c                   parm                    p_binr
6.nu c                   parm                    p_numm
     c                   parm                    p_suff
     c                   parm                    p_ktxt
     c                   parm                    p_line
      *
      * Key for oppslag på faktura-hode-register
      *
     c     sohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sohel1_aarr
     c                   kfld                    sohel1_binr
     c                   kfld                    sohel1_numm
     c                   kfld                    sohel1_suff
      *
      * Key for oppslag på faktura-detalj-register
      *
     c     sodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    sodtl1_aarr
     c                   kfld                    sodtl1_binr
     c                   kfld                    sodtl1_numm
     c                   kfld                    sodtl1_suff
     c                   kfld                    sodtl1_ktxt
     c                   kfld                    sodtl1_line
      *
      * Key for oppslag på kontohenvisningsregisteret
      *
     c     vkhvl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vkhvl1_kkod
     c                   kfld                    vkhvl1_klty
     c                   kfld                    vkhvl1_koty
      *
      *
      * Henter firmakode fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
