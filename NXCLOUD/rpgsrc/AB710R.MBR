     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASPGPL
      * Program . . . . : AB710R
      * Beskrivelse . . : Oppdaterer transaksjonsfile mednormal/unormal
      *                   avslutning. Hvis statusmeldinger mottatt har
      *                   alvorsgrad høyere enn 10, blir denne status
      *                   liggende igjen for videre 'action'.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       130809 BSA  Nytt program
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fajlsl1    uf   e           k disk    rename(ajlspfr:ajlsl1r)
     fajltlu    uf a e           k disk    rename(ajltpfr:ajltlur)
      *
      * Definering av Local data area
      *
     d                uds
     d l_firm                944    946  0
     d l_fgr                 931    933
     d l_user                911    920
      *
      * Definering av datastrukturer
     d                 ds
     d d_lnr                          5  0
      *
      * Definering av variabler i nøkler
      *
     d ajlsl1_skey     s                   like(ajskey)
      *
     d ajltlu_jbid     s                   like(ajtjid)
     d ajltlu_lin      s                   like(ajtlin)
      *
      * Definering av variable
      *
     d w_firm          s                   like(ajsfir)
     d w_tstp          s               z
     d w_melding       s                   like(ajttxt)
      *
      * Definering av parametre
     d p_jnav          s                   like(ajsnav)
     d p_jkey          s                   like(ajskey)
      *
      * Definering av tabell
     d a_meld          s            100    dim(2) ctdata perrcd(1)
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hvis jobbid er blank, avslutter vi uten mer logging
     c                   if        p_jkey = *blanks
     c                   eval      *inlr = *on
     c                   return
     c                   endif
      *
      * Vi avslutter med å oppdatere statusrecord
     c                   eval      ajlsl1_skey = p_jkey
     c     ajlsl1_key    chain     ajlsl1r
      *
     c                   if        %found
     c                   if        ajssta > 10
     c                   eval      w_melding = a_meld(2)
     c                   else
     c                   eval      w_melding = a_meld(1)
     c                   eval      ajssok = w_tstp
     c                   eval      ajssta = 0
     c                   endif
     c                   eval      ajsrun = 0
     c                   time                    ajseda
     c                   time                    ajseti
     c                   eval      ajseus = l_user
     c                   update    ajlsl1r
     c                   else
     c                   eval      *inlr = *on
     c                   return
     c                   endif
      *
      * Skriv jobblogg - transaksjon. Finner siste brukte linjenummer
      *
     c                   eval      ajltlu_jbid = p_jkey
     c                   eval      ajltlu_lin  = 99999
     c*
     c     ajltlu_key    setgt     ajltlur
     c                   readp     ajltlur
      *
      * Vi må sjekke at vi har riktig informasjon
     c                   if        ajtfir = w_firm and
     c                             ajtjid = p_jkey
     c                   eval      d_lnr = ajtlin
      * Så skriver vi ny record
     c                   clear                   ajltlur
     c                   eval      ajtfir = w_firm
     c                   eval      ajtjid = ajltlu_jbid
     c                   eval      ajtlin = d_lnr + 1
     c                   eval      ajttxt = w_melding
     c                   eval      ajtsta = ajssta
     c                   time                    ajtoda
     c                   time                    ajtoti
     c                   eval      ajtous = l_user
     c                   write     ajltlur
     c                   endif
      *
      * Avslutt
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c**                 parm                    p_jnav
     c                   parm                    p_jkey
      *
      * Key for oppdatering av jobblogging - status
      *
     c     ajlsl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ajlsl1_skey
      *
      * Key for oppdatering av jobblogging - transaksjoner
      *
     c     ajltlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ajltlu_jbid
     c                   kfld                    ajltlu_lin
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      * Setter timestampvalue for jobben
     c                   time                    w_tstp
      *
     c                   endsr
**
Jobben ble avsluttet på normal måte
Jobben er avsluttet, men det har oppstått feil underveis. Sjekk ut loggfila.
