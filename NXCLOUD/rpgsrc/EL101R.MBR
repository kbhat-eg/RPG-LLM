     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : EL101R
      * Beskrivelse . . : Leverandør bonus, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       271005 HKO  Nytt program
      * 6.10  220609 BSA  Oppdatert med sporingsinformasjon
      *
8.01  * 8.xx  080422 bsa  Tilpasset cloud
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     felboir    if   e           k disk    rename(elbost:elboirr)
     felboiu    uf a e           k disk    rename(elbost:elboiur)
     felboi1    if   e           k disk    rename(elbost:elboi1r)
     felboi2    if   e           k disk    rename(elbost:elboi2r)
     felboi3    if   e           k disk    rename(elbost:elboi3r)
     felboi4    if   e           k disk    rename(elbost:elboi4r)
     felboi5    if   e           k disk    rename(elbost:elboi5r)
     felboi6    if   e           k disk    rename(elbost:elboi6r)
     felboi7    if   e           k disk    rename(elbost:elboi7r)
     felboi8    if   e           k disk    rename(elbost:elboi8r)
     felboi9    if   e           k disk    rename(elbost:elboi9r)
     felbei1    if   e           k disk    rename(elbest:elbei1r)
     felbeiu    uf a e           k disk    rename(elbest:elbeiur)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvvarlb    if   e           k disk    rename(vvarpfr:vvarlbr)
     fvenhl1    if   e           k disk    rename(venhpfr:venhl1r)
     fvltpl1    if   e           k disk    rename(vltppfr:vltpl1r)
     fvbotl1    if   e           k disk    rename(vbotpfr:vbotl1r)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
      *
     fel101d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(elfirm)
     d p_blev          s                   like(elblev)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variable i nøkler
      *
     d elboir_ldor     s                   like(elldor)
     d elboir_ogrp     s                   like(elogrp)
     d elboir_hgrp     s                   like(elhgrp)
     d elboir_ugrp     s                   like(elugrp)
     d elboir_modn     s                   like(elmodn)
     d elboir_vare     s                   like(elvare)
     d elboir_enhe     s                   like(elenhe)
     d elboir_lety     s                   like(ellety)
     d elboir_boty     s                   like(elboty)
     d elboir_fdat     s                   like(elfdat)
     d elboir_tdat     s                   like(eltdat)
     d elboiu_ldor     s                   like(elldor)
     d elboiu_ogrp     s                   like(elogrp)
     d elboiu_hgrp     s                   like(elhgrp)
     d elboiu_ugrp     s                   like(elugrp)
     d elboiu_modn     s                   like(elmodn)
     d elboiu_vare     s                   like(elvare)
     d elboiu_enhe     s                   like(elenhe)
     d elboiu_lety     s                   like(ellety)
     d elboiu_boty     s                   like(elboty)
     d elboiu_fdat     s                   like(elfdat)
     d elboiu_tdat     s                   like(eltdat)
     d elboi1_ldor     s                   like(elldor)
     d elboi1_ogrp     s                   like(elogrp)
     d elboi1_hgrp     s                   like(elhgrp)
     d elboi1_ugrp     s                   like(elugrp)
     d elboi1_modn     s                   like(elmodn)
     d elboi1_vare     s                   like(elvare)
     d elboi1_enhe     s                   like(elenhe)
     d elboi1_lety     s                   like(ellety)
     d elboi1_boty     s                   like(elboty)
     d elboi1_fdat     s                   like(elfdat)
     d elboi1_tdat     s                   like(eltdat)
     d elboi2_ogrp     s                   like(elogrp)
     d elboi2_hgrp     s                   like(elhgrp)
     d elboi2_ugrp     s                   like(elugrp)
     d elboi3_hgrp     s                   like(elhgrp)
     d elboi3_ugrp     s                   like(elugrp)
     d elboi4_ugrp     s                   like(elugrp)
     d elboi5_modn     s                   like(elmodn)
     d elboi6_vare     s                   like(elvare)
     d elboi6_enhe     s                   like(elenhe)
     d elboi6_lety     s                   like(ellety)
     d elboi7_enhe     s                   like(elenhe)
     d elboi8_lety     s                   like(ellety)
     d elboi9_boty     s                   like(elboty)
     d elbei1_eldo     s                   like(eleldo)
     d elbei1_eogr     s                   like(eleogr)
     d elbei1_ehgr     s                   like(elehgr)
     d elbei1_eugr     s                   like(eleugr)
     d elbei1_emod     s                   like(elemod)
     d elbei1_evar     s                   like(elevar)
     d elbei1_eenh     s                   like(eleenh)
     d elbei1_elet     s                   like(elelet)
     d elbei1_ebot     s                   like(elebot)
     d elbei1_efda     s                   like(elefda)
     d elbei1_etda     s                   like(eletda)
     d rlevl1_levr     s                   like(rllevr)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d vvarl1_vare     s                   like(vvvare)
     d vvarlb_nmod     s                   like(vvnmod)
     d venhl1_enhe     s                   like(vaeenh)
     d vltpl1_lety     s                   like(vylety)
     d vbotl1_tbot     s                   like(vatbot)
     d jvarl1_vare     s                   like(jvvare)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_oppr          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_fdat_iso      s               d   datfmt(*iso)
     d w_tdat_iso      s               d   datfmt(*iso)
      *
     d w_fdat          s              6  0
     d w_tdat          s              6  0
     d w_mtxt          s             35
      *
     d w_firm          s                   like(elfirm)
     d w_blev          s                   like(elblev)
     d w_ldor          s                   like(elldor)
     d w_ogrp          s                   like(elogrp)
     d w_hgrp          s                   like(elhgrp)
     d w_ugrp          s                   like(elugrp)
     d w_modn          s                   like(elmodn)
     d w_vare          s                   like(elvare)
     d w_enhe          s                   like(elenhe)
     d w_lety          s                   like(ellety)
     d w_boty          s                   like(elboty)
     d w_lnav          s                   like(rlnavn)
     d w_gtxt          s                   like(vgutxt)
     d w_vtxt          s                   like(vvtek1)
     d w_etxt          s                   like(vaetxt)
     d w_ltxt          s                   like(vybesk)
     d w_btxt          s                   like(vatbes)
      *
     d s_ldor          s                   like(elldor)
     d s_ogrp          s                   like(elogrp)
     d s_hgrp          s                   like(elhgrp)
     d s_ugrp          s                   like(elugrp)
     d s_modn          s                   like(elmodn)
     d s_vare          s                   like(elvare)
     d s_enhe          s                   like(elenhe)
     d s_lety          s                   like(ellety)
     d s_boty          s                   like(elboty)
     d s_fdat          s              6  0
     d s_tdat          s              6  0
      *
      * Definering av konstanter
      *
8.01 d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile                                  DSP SFL
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2tagb
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2ldor <> 0 or
     c                             b2ogrp <> 0 or
     c                             b2hgrp <> 0 or
     c                             b2ugrp <> 0 or
     c                             b2modn <> 0 or
     c                             b2vare <> *blank or
     c                             b2enhe <> *blank or
     c                             b2lety <> 0 or
     c                             b2boty <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'O'
     c                   eval      elboi2_ogrp = b1ogrp
     c                   eval      elboi2_hgrp = b1hgrp
     c                   eval      elboi2_ugrp = b1ugrp
     c     elboi2_key    setll     elboi2
     c                   when      w_seqe = 'H'
     c                   eval      elboi3_hgrp = b1hgrp
     c                   eval      elboi3_ugrp = b1ugrp
     c     elboi3_key    setll     elboi3
     c                   when      w_seqe = 'U'
     c                   eval      elboi4_ugrp = b1ugrp
     c     elboi4_key    setll     elboi4
     c                   when      w_seqe = 'M'
     c                   eval      elboi5_modn = b1modn
     c     elboi5_key    setll     elboi5
     c                   when      w_seqe = 'V'
     c                   eval      elboi6_vare = b1vare
     c                   eval      elboi6_enhe = b1enhe
     c                   eval      elboi6_lety = b1lety
     c     elboi6_key    setll     elboi6
     c                   when      w_seqe = 'E'
     c                   eval      elboi7_enhe = b1enhe
     c     elboi7_key    setll     elboi7
     c                   when      w_seqe = 'T'
     c                   eval      elboi8_lety = b1lety
     c     elboi8_key    setll     elboi8
     c                   when      w_seqe = 'B'
     c                   eval      elboi9_boty = b1boty
     c     elboi9_key    setll     elboi9
     c                   other
     c                   eval      elboi1_ldor = b1ldor
     c                   eval      elboi1_ogrp = b1ogrp
     c                   eval      elboi1_hgrp = b1hgrp
     c                   eval      elboi1_ugrp = b1ugrp
     c                   eval      elboi1_modn = b1modn
     c                   eval      elboi1_vare = b1vare
     c                   eval      elboi1_enhe = b1enhe
     c                   eval      elboi1_lety = b1lety
     c                   eval      elboi1_boty = b1boty
     c                   eval      elboi1_fdat = *loval
     c                   eval      elboi1_tdat = *loval
     c                   if        b1fdat <> 0
     c                   eval      elboi1_fdat = %date(b1fdat:*dmy)
     c                   endif
     c                   if        b1tdat <> 0
     c                   eval      elboi1_tdat = %date(b1tdat:*dmy)
     c                   endif
     c     elboi1_key    setll     elboi1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på leverandør
      *
     c                   if        b2ldor <> 0
     c                   eval      w_seqe = *blank
     c                   eval      elboi1_ldor = b2ldor
     c                   eval      elboi1_ogrp = b2ogrp
     c                   eval      elboi1_hgrp = b2hgrp
     c                   eval      elboi1_ugrp = b2ugrp
     c                   eval      elboi1_modn = b2modn
     c                   eval      elboi1_vare = b2vare
     c                   eval      elboi1_enhe = b2enhe
     c                   eval      elboi1_lety = b2lety
     c                   eval      elboi1_boty = b2boty
     c                   eval      elboi1_fdat = *loval
     c                   eval      elboi1_tdat = *loval
     c     elboi1_key    setll     elboi1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på overgruppe
      *
     c                   if        b2ogrp <> 0
     c                   eval      w_seqe = 'O'
     c                   eval      elboi2_ogrp = b2ogrp
     c                   eval      elboi2_hgrp = b2hgrp
     c                   eval      elboi2_ugrp = b2ugrp
     c     elboi2_key    setll     elboi2
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på hovedgruppe
      *
     c                   if        b2hgrp <> 0
     c                   eval      w_seqe = 'H'
     c                   eval      elboi3_hgrp = b2hgrp
     c                   eval      elboi3_ugrp = b2ugrp
     c     elboi3_key    setll     elboi3
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på undergruppe
      *
     c                   if        b2ugrp <> 0
     c                   eval      w_seqe = 'U'
     c                   eval      elboi4_ugrp = b2ugrp
     c     elboi4_key    setll     elboi4
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på modulnr
      *
     c                   if        b2modn <> 0
     c                   eval      w_seqe = 'M'
     c                   eval      elboi5_modn = b2modn
     c     elboi5_key    setll     elboi5
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på vare
      *
     c                   if        b2vare <> *blank
     c                   eval      w_seqe = 'V'
     c                   eval      elboi6_vare = b2vare
     c                   eval      elboi6_enhe = b2enhe
     c                   eval      elboi6_lety = b2lety
     c     elboi6_key    setll     elboi6
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på enhet
      *
     c                   if        b2enhe <> *blank
     c                   eval      w_seqe = 'E'
     c                   eval      elboi7_enhe = b2enhe
     c     elboi7_key    setll     elboi7
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på leveringstype
      *
     c                   if        b2lety <> 0
     c                   eval      w_seqe = 'T'
     c                   eval      elboi8_lety = b2lety
     c     elboi8_key    setll     elboi8
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på bonustype
      *
     c                   if        b2boty <> *blank
     c                   eval      w_seqe = 'B'
     c                   eval      elboi9_boty = b2boty
     c     elboi9_key    setll     elboi9
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2ldor = 0
     c                   eval      b2ogrp = 0
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   eval      b2modn = 0
     c                   eval      b2vare = *blank
     c                   eval      b2enhe = *blank
     c                   eval      b2lety = 0
     c                   eval      b2boty = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1ldor = b1ldor
     c                   eval      c1ogrp = b1ogrp
     c                   eval      c1hgrp = b1hgrp
     c                   eval      c1ugrp = b1ugrp
     c                   eval      c1modn = b1modn
     c                   eval      c1vare = b1vare
     c                   eval      c1enhe = b1enhe
     c                   eval      c1lety = b1lety
     c                   eval      c1boty = b1boty
     c                   eval      c1fdat = b1fdat
     c                   eval      c1tdat = b1tdat
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1ldor = b1ldor
     c                   eval      k1ogrp = b1ogrp
     c                   eval      k1hgrp = b1hgrp
     c                   eval      k1ugrp = b1ugrp
     c                   eval      k1modn = b1modn
     c                   eval      k1vare = b1vare
     c                   eval      k1enhe = b1enhe
     c                   eval      k1lety = b1lety
     c                   eval      k1boty = b1boty
     c                   eval      k1fdat = b1fdat
     c                   eval      k1tdat = b1tdat
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1ldor = b1ldor
     c                   eval      d1ogrp = b1ogrp
     c                   eval      d1hgrp = b1hgrp
     c                   eval      d1ugrp = b1ugrp
     c                   eval      d1modn = b1modn
     c                   eval      d1vare = b1vare
     c                   eval      d1enhe = b1enhe
     c                   eval      d1lety = b1lety
     c                   eval      d1boty = b1boty
     c                   eval      d1fdat = b1fdat
     c                   eval      d1tdat = b1tdat
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1ldor = b1ldor
     c                   eval      c1ogrp = b1ogrp
     c                   eval      c1hgrp = b1hgrp
     c                   eval      c1ugrp = b1ugrp
     c                   eval      c1modn = b1modn
     c                   eval      c1vare = b1vare
     c                   eval      c1enhe = b1enhe
     c                   eval      c1lety = b1lety
     c                   eval      c1boty = b1boty
     c                   eval      c1fdat = b1fdat
     c                   eval      c1tdat = b1tdat
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Bonuselementer
      *
     c                   if        b1valg = 7
     c                   eval      w_vare = b1vare
     c                   if        b1fdat <> 0
     c                   eval      w_fdat_iso = %date(b1fdat:*dmy)
     c                   else
     c                   eval      w_fdat_iso = *loval
     c                   endif
     c                   if        b1tdat <> 0
     c                   eval      w_tdat_iso = %date(b1tdat:*dmy)
     c                   else
     c                   eval      w_tdat_iso = *loval
     c                   endif
     c                   call      'EL110R'
     c                   parm                    w_firm
     c                   parm                    w_blev
     c                   parm                    b1ldor
     c                   parm                    b1ogrp
     c                   parm                    b1hgrp
     c                   parm                    b1ugrp
     c                   parm                    b1modn
     c                   parm                    w_vare
     c                   parm                    b1enhe
     c                   parm                    b1lety
     c                   parm                    b1boty
     c                   parm                    w_fdat_iso
     c                   parm                    w_tdat_iso
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1ldor = 0
     c                   eval      c1ogrp = 0
     c                   eval      c1hgrp = 0
     c                   eval      c1ugrp = 0
     c                   eval      c1modn = 0
     c                   eval      c1vare = *blank
     c                   eval      c1enhe = *blank
     c                   eval      c1lety = 0
     c                   eval      c1boty = *blank
     c                   eval      c1fdat = 0
     c                   eval      c1tdat = 0
      *
     c                   exsr      hent_info
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      c1taga
     c                   endsl
      *
      * Sjekker input
      *
     c                   eval      w_ldor = c1ldor
     c                   eval      w_ogrp = c1ogrp
     c                   eval      w_hgrp = c1hgrp
     c                   eval      w_ugrp = c1ugrp
     c                   eval      w_modn = c1modn
     c                   eval      w_vare = c1vare
     c                   eval      w_enhe = c1enhe
     c                   eval      w_lety = c1lety
     c                   eval      w_boty = c1boty
     c                   eval      w_fdat = c1fdat
     c                   eval      w_tdat = c1tdat
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      c1taga
     c                   endif
      *
      * Sender opp informasjonsbildet dersom rad finnes fra før
      *
     c                   eval      elboir_ldor = c1ldor
     c                   eval      elboir_ogrp = c1ogrp
     c                   eval      elboir_hgrp = c1hgrp
     c                   eval      elboir_ugrp = c1ugrp
     c                   eval      elboir_modn = c1modn
     c                   eval      elboir_vare = c1vare
     c                   eval      elboir_enhe = c1enhe
     c                   eval      elboir_lety = c1lety
     c                   eval      elboir_boty = c1boty
     c                   eval      elboir_fdat = *loval
     c                   eval      elboir_tdat = *loval
     c                   if        c1fdat <> 0
     c                   eval      elboir_fdat = %date(c1fdat:*dmy)
     c                   endif
     c                   if        c1tdat <> 0
     c                   eval      elboir_tdat = %date(c1tdat:*dmy)
     c                   endif
     c     elboir_key    chain     elboir
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1ldor = 0
     c                   eval      c1ogrp = 0
     c                   eval      c1hgrp = 0
     c                   eval      c1ugrp = 0
     c                   eval      c1vare = *blank
     c                   eval      c1modn = 0
     c                   eval      c1enhe = *blank
     c                   eval      c1lety = 0
     c                   eval      c1boty = *blank
     c                   eval      c1fdat = 0
     c                   eval      c1tdat = 0
      *
     c                   eval      c1lnav = *blank
     c                   eval      c1gtxt = *blank
     c                   eval      c1mtxt = *blank
     c                   eval      c1vtxt = *blank
     c                   eval      c1etxt = *blank
     c                   eval      c1ltxt = *blank
     c                   eval      c1btxt = *blank
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   eval      b_oppr = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * C2BLD Behandle bilde for ny - endring - vise
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      elboir_ldor = c1ldor
     c                   eval      elboir_ogrp = c1ogrp
     c                   eval      elboir_hgrp = c1hgrp
     c                   eval      elboir_ugrp = c1ugrp
     c                   eval      elboir_modn = c1modn
     c                   eval      elboir_vare = c1vare
     c                   eval      elboir_enhe = c1enhe
     c                   eval      elboir_lety = c1lety
     c                   eval      elboir_boty = c1boty
     c                   eval      elboir_fdat = *loval
     c                   eval      elboir_tdat = *loval
     c                   if        c1fdat <> 0
     c                   eval      elboir_fdat = %date(c1fdat:*dmy)
     c                   endif
     c                   if        c1tdat <> 0
     c                   eval      elboir_tdat = %date(c1tdat:*dmy)
     c                   endif
     c     elboir_key    chain     elboir
      *
     c                   if        %found
     c                   eval      c2pbgr = elpbgr
     c                   eval      c2merk = elmerk
     c                   eval      c2eusr = eleusr
     c     *dmy          move      elodat        c2odat
     c     *dmy          move      eledat        c2edat
     c                   eval      c2bgru = elbgru
     c                   eval      c2sbon = elsbon
     c                   eval      c2bdat = 0
     c                   eval      c2btim = 0
     c                   if        elbdat <> *loval
     c     *dmy          move      elbdat        c2bdat
     c                   endif
     c                   if        elbtim <> *loval
     c     *hms          move      elbtim        c2btim
     c                   endif
     c                   else
     c                   eval      c2pbgr = 0
     c                   eval      c2merk = *blank
     c                   eval      c2odat = 0
     c                   eval      c2edat = 0
     c                   eval      c2eusr = *blank
     c                   eval      c2bgru = 0
     c                   eval      c2sbon = 0
     c                   eval      c2bdat = 0
     c                   eval      c2btim = 0
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1ldor = k1ldor
     c                   eval      c1ogrp = k1ogrp
     c                   eval      c1hgrp = k1hgrp
     c                   eval      c1ugrp = k1ugrp
     c                   eval      c1modn = k1modn
     c                   eval      c1vare = k1vare
     c                   eval      c1enhe = k1enhe
     c                   eval      c1lety = k1lety
     c                   eval      c1boty = k1boty
     c                   eval      c1fdat = k1fdat
     c                   eval      c1tdat = k1tdat
     c                   eval      c2bgru = 0
     c                   eval      c2sbon = 0
     c                   eval      c2bdat = 0
     c                   eval      c2btim = 0
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
      * Henter beskrivelse av nivå
      *
     c                   exsr      hent_info
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Validering av input
      *
      * Oppdatering av leverandørbonus
      *
     c                   clear                   elboiur
      *
     c                   eval      elboiu_ldor = c1ldor
     c                   eval      elboiu_ogrp = c1ogrp
     c                   eval      elboiu_hgrp = c1hgrp
     c                   eval      elboiu_ugrp = c1ugrp
     c                   eval      elboiu_modn = c1modn
     c                   eval      elboiu_vare = c1vare
     c                   eval      elboiu_enhe = c1enhe
     c                   eval      elboiu_lety = c1lety
     c                   eval      elboiu_boty = c1boty
     c                   eval      elboiu_fdat = *loval
     c                   eval      elboiu_tdat = *loval
     c                   if        c1fdat <> 0
     c                   eval      elboiu_fdat = %date(c1fdat:*dmy)
     c                   endif
     c                   if        c1tdat <> 0
     c                   eval      elboiu_tdat = %date(c1tdat:*dmy)
     c                   endif
     c     elboiu_key    chain     elboiur
      *
     c                   eval      elpbgr = c2pbgr
     c                   eval      elmerk = c2merk
     c                   eval      eleusr = l_user
      *
     c                   time                    eletim
     c                   time                    eledat
      *
     c                   if        %found(elboiu)
     c                   update    elboiur
     c                   else
     c                   eval      elfirm = w_firm
     c                   eval      elblev = w_blev
     c                   eval      elldor = c1ldor
     c                   eval      elogrp = c1ogrp
     c                   eval      elhgrp = c1hgrp
     c                   eval      elugrp = c1ugrp
     c                   eval      elmodn = c1modn
     c                   eval      elvare = c1vare
     c                   eval      elenhe = c1enhe
     c                   eval      ellety = c1lety
     c                   eval      elboty = c1boty
     c                   eval      elfdat = *loval
     c                   eval      eltdat = *loval
     c                   if        c1fdat <> 0
     c                   eval      elfdat = %date(c1fdat:*dmy)
     c                   endif
     c                   if        c1tdat <> 0
     c                   eval      eltdat = %date(c1tdat:*dmy)
     c                   endif
     c                   time                    elodat
     c                   time                    elotim
6.10 c                   eval      elousr = l_user
     c                   write     elboiur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1merk = elmerk
     c                   endif
      *
      * Kopierer rabattelementer
      *
     c                   if        b1valg = 3
     c                   eval      elbei1_eldo = s_ldor
     c                   eval      elbei1_eogr = s_ogrp
     c                   eval      elbei1_ehgr = s_hgrp
     c                   eval      elbei1_eugr = s_ugrp
     c                   eval      elbei1_emod = s_modn
     c                   eval      elbei1_evar = s_vare
     c                   eval      elbei1_eenh = s_enhe
     c                   eval      elbei1_elet = s_lety
     c                   eval      elbei1_ebot = s_boty
     c                   eval      elbei1_efda = *loval
     c                   eval      elbei1_etda = *loval
     c                   if        s_fdat <> 0
     c                   eval      elbei1_efda = %date(s_fdat:*dmy)
     c                   endif
     c                   if        s_tdat <> 0
     c                   eval      elbei1_etda = %date(s_tdat:*dmy)
     c                   endif
     c     elbei1_key    setll     elbei1
     c     elbei1_key    reade     elbei1
     c                   dow       not %eof
     c                   eval      eleldo = elldor
     c                   eval      eleogr = elogrp
     c                   eval      elehgr = elhgrp
     c                   eval      eleugr = elugrp
     c                   eval      elemod = elmodn
     c                   eval      elevar = elvare
     c                   eval      eleenh = elenhe
     c                   eval      elelet = ellety
     c                   eval      elebot = elboty
     c                   eval      elefda = elfdat
     c                   eval      eletda = eltdat
     c                   eval      elesbo = 0
     c                   eval      elebda = *loval
     c                   eval      elebti = *loval
     c                   eval      eleeus = l_user
     c                   time                    eleoda
     c                   time                    eleoti
6.10 c                   eval      eleous = l_user
     c                   time                    eleeda
     c                   time                    eleeti
     c                   write     elbeiur
     c     elbei1_key    reade     elbei1
     c                   enddo
     c                   endif
      *
      * Kaller bonuselement-bildet
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   call      'EL110R'
     c                   parm                    w_firm
     c                   parm                    w_blev
     c                   parm                    elldor
     c                   parm                    elogrp
     c                   parm                    elhgrp
     c                   parm                    elugrp
     c                   parm                    elmodn
     c                   parm                    elvare
     c                   parm                    elenhe
     c                   parm                    ellety
     c                   parm                    elboty
     c                   parm                    elfdat
     c                   parm                    eltdat
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      elboir_ldor = d1ldor
     c                   eval      elboir_ogrp = d1ogrp
     c                   eval      elboir_hgrp = d1hgrp
     c                   eval      elboir_ugrp = d1ugrp
     c                   eval      elboir_modn = d1modn
     c                   eval      elboir_vare = d1vare
     c                   eval      elboir_enhe = d1enhe
     c                   eval      elboir_lety = d1lety
     c                   eval      elboir_boty = d1boty
     c                   eval      elboir_fdat = *loval
     c                   eval      elboir_tdat = *loval
     c                   if        d1fdat <> 0
     c                   eval      elboir_fdat = %date(d1fdat:*dmy)
     c                   endif
     c                   if        d1tdat <> 0
     c                   eval      elboir_tdat = %date(d1tdat:*dmy)
     c                   endif
     c     elboir_key    chain     elboir
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter bonus-nivå
      *
     c                   eval      elboiu_ldor = d1ldor
     c                   eval      elboiu_ogrp = d1ogrp
     c                   eval      elboiu_hgrp = d1hgrp
     c                   eval      elboiu_ugrp = d1ugrp
     c                   eval      elboiu_modn = d1modn
     c                   eval      elboiu_vare = d1vare
     c                   eval      elboiu_enhe = d1enhe
     c                   eval      elboiu_lety = d1lety
     c                   eval      elboiu_boty = d1boty
     c                   eval      elboiu_fdat = *loval
     c                   eval      elboiu_tdat = *loval
     c                   if        d1fdat <> 0
     c                   eval      elboiu_fdat = %date(d1fdat:*dmy)
     c                   endif
     c                   if        d1tdat <> 0
     c                   eval      elboiu_tdat = %date(d1tdat:*dmy)
     c                   endif
     c     elboiu_key    chain     elboiur
     c                   delete    elboiur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_ldor = k1ldor
     c                   eval      s_ogrp = k1ogrp
     c                   eval      s_hgrp = k1hgrp
     c                   eval      s_ugrp = k1ugrp
     c                   eval      s_modn = k1modn
     c                   eval      s_vare = k1vare
     c                   eval      s_enhe = k1enhe
     c                   eval      s_lety = k1lety
     c                   eval      s_boty = k1boty
     c                   eval      s_fdat = k1fdat
     c                   eval      s_tdat = k1tdat
      *
      * Fyller kopierings-bildet
      *
     c                   eval      elboir_ldor = k1ldor
     c                   eval      elboir_ogrp = k1ogrp
     c                   eval      elboir_hgrp = k1hgrp
     c                   eval      elboir_ugrp = k1ugrp
     c                   eval      elboir_modn = k1modn
     c                   eval      elboir_vare = k1vare
     c                   eval      elboir_enhe = k1enhe
     c                   eval      elboir_lety = k1lety
     c                   eval      elboir_boty = k1boty
     c                   eval      elboir_fdat = *loval
     c                   eval      elboir_tdat = *loval
     c                   if        k1fdat <> 0
     c                   eval      elboir_fdat = %date(k1fdat:*dmy)
     c                   endif
     c                   if        k1tdat <> 0
     c                   eval      elboir_tdat = %date(k1tdat:*dmy)
     c                   endif
      *
     c     elboir_key    chain     elboir
      *
     c                   if        elfdat <> *loval
     c     *dmy          move      elfdat        k1fda2
     c                   else
     c                   eval      k1fda2 = 0
     c                   endif
      *
     c                   if        eltdat <> *loval
     c     *dmy          move      eltdat        k1tda2
     c                   else
     c                   eval      k1tda2 = 0
     c                   endif
      *
     c     k1tagb        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   leavesr
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd = *on
     c                   exsr      spØrring
     c                   goto      k1tagb
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      elboir_ldor = k1ldor
     c                   eval      elboir_ogrp = k1ogrp
     c                   eval      elboir_hgrp = k1hgrp
     c                   eval      elboir_ugrp = k1ugrp
     c                   eval      elboir_modn = k1modn
     c                   eval      elboir_vare = k1vare
     c                   eval      elboir_enhe = k1enhe
     c                   eval      elboir_lety = k1lety
     c                   eval      elboir_boty = k1boty
     c                   eval      elboir_fdat = *loval
     c                   eval      elboir_tdat = *loval
     c                   if        k1fdat <> 0
     c                   eval      elboir_fdat = %date(k1fdat:*dmy)
     c                   endif
     c                   if        k1tdat <> 0
     c                   eval      elboir_tdat = %date(k1tdat:*dmy)
     c                   endif
     c     elboir_key    chain     elboir
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Sjekker input
      *
     c                   eval      w_ldor = k1ldor
     c                   eval      w_ogrp = k1ogrp
     c                   eval      w_hgrp = k1hgrp
     c                   eval      w_ugrp = k1ugrp
     c                   eval      w_modn = k1modn
     c                   eval      w_vare = k1vare
     c                   eval      w_enhe = k1enhe
     c                   eval      w_lety = k1lety
     c                   eval      w_boty = k1boty
     c                   eval      w_fdat = k1fdat
     c                   eval      w_tdat = k1tdat
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1ldor = s_ldor
     c                   eval      c1ogrp = s_ogrp
     c                   eval      c1hgrp = s_hgrp
     c                   eval      c1ugrp = s_ugrp
     c                   eval      c1modn = s_modn
     c                   eval      c1vare = s_vare
     c                   eval      c1enhe = s_enhe
     c                   eval      c1lety = s_lety
     c                   eval      c1boty = s_boty
     c                   eval      c1fdat = s_fdat
     c                   eval      c1tdat = s_tdat
      *
     c                   exsr      xc2bld
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Fyll opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'O'
     c                   read      elboi2
     c                   when      w_seqe = 'H'
     c                   read      elboi3
     c                   when      w_seqe = 'U'
     c                   read      elboi4
     c                   when      w_seqe = 'M'
     c                   read      elboi5
     c                   when      w_seqe = 'V'
     c                   read      elboi6
     c                   when      w_seqe = 'E'
     c                   read      elboi7
     c                   when      w_seqe = 'T'
     c                   read      elboi8
     c                   when      w_seqe = 'B'
     c                   read      elboi9
     c                   other
     c                   read      elboi1
     c                   endsl
      *
     c                   if        %eof or
     c                             elfirm <> w_firm or
     c                             elblev <> w_blev
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'O'
     c                   eval      elboi2_ogrp = elogrp
     c                   eval      elboi2_hgrp = elhgrp
     c                   eval      elboi2_ugrp = elugrp
     c                   when      w_seqe = 'H'
     c                   eval      elboi3_hgrp = elhgrp
     c                   eval      elboi3_ugrp = elugrp
     c                   when      w_seqe = 'U'
     c                   eval      elboi4_ugrp = elugrp
     c                   when      w_seqe = 'M'
     c                   eval      elboi5_modn = elmodn
     c                   when      w_seqe = 'V'
     c                   eval      elboi6_vare = elvare
     c                   eval      elboi6_enhe = elenhe
     c                   eval      elboi6_lety = ellety
     c                   when      w_seqe = 'E'
     c                   eval      elboi7_enhe = elenhe
     c                   when      w_seqe = 'T'
     c                   eval      elboi8_lety = ellety
     c                   when      w_seqe = 'B'
     c                   eval      elboi9_boty = elboty
     c                   other
     c                   eval      elboi1_ldor = elldor
     c                   eval      elboi1_ogrp = elogrp
     c                   eval      elboi1_hgrp = elhgrp
     c                   eval      elboi1_ugrp = elugrp
     c                   eval      elboi1_modn = elmodn
     c                   eval      elboi1_vare = elvare
     c                   eval      elboi1_enhe = elenhe
     c                   eval      elboi1_lety = ellety
     c                   eval      elboi1_boty = elboty
     c                   eval      elboi1_fdat = elfdat
     c                   eval      elboi1_tdat = eltdat
     c                   endsl
      *
     c                   eval      b1ldor = elldor
     c                   eval      b1ogrp = elogrp
     c                   eval      b1hgrp = elhgrp
     c                   eval      b1ugrp = elugrp
     c                   eval      b1modn = elmodn
     c                   eval      b1vare = elvare
     c                   eval      b1enhe = elenhe
     c                   eval      b1lety = ellety
     c                   eval      b1boty = elboty
     c                   eval      b1fdat = 0
     c                   eval      b1tdat = 0
     c                   eval      b1merk = elmerk
      *
     c                   if        elfdat <> *loval
     c     *dmy          move      elfdat        b1fdat
     c                   endif
     c                   if        eltdat <> *loval
     c     *dmy          move      eltdat        b1tdat
     c                   endif
      *
     c                   write     b1sfl
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'O'
     c     elboi2_key    setgt     elboi2
     c                   readp     elboi2
     c                   when      w_seqe = 'H'
     c     elboi3_key    setgt     elboi3
     c                   readp     elboi3
     c                   when      w_seqe = 'U'
     c     elboi4_key    setgt     elboi4
     c                   readp     elboi4
     c                   when      w_seqe = 'M'
     c     elboi5_key    setgt     elboi5
     c                   readp     elboi5
     c                   when      w_seqe = 'V'
     c     elboi6_key    setgt     elboi6
     c                   readp     elboi6
     c                   when      w_seqe = 'E'
     c     elboi7_key    setgt     elboi7
     c                   readp     elboi7
     c                   when      w_seqe = 'T'
     c     elboi8_key    setgt     elboi8
     c                   readp     elboi8
     c                   when      w_seqe = 'B'
     c     elboi9_key    setgt     elboi9
     c                   readp     elboi9
     c                   other
     c     elboi1_key    setgt     elboi1
     c                   readp     elboi1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'O'
     c                   readp     elboi2
     c                   when      w_seqe = 'H'
     c                   readp     elboi3
     c                   when      w_seqe = 'U'
     c                   readp     elboi4
     c                   when      w_seqe = 'M'
     c                   readp     elboi5
     c                   when      w_seqe = 'V'
     c                   readp     elboi6
     c                   when      w_seqe = 'E'
     c                   readp     elboi7
     c                   when      w_seqe = 'T'
     c                   readp     elboi8
     c                   when      w_seqe = 'B'
     c                   readp     elboi9
     c                   other
     c                   readp     elboi1
     c                   endsl
      *
     c                   if        %eof or
     c                             elfirm <> w_firm or
     c                             elblev <> w_blev
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'O'
     c                   eval      elboi2_ogrp = elogrp
     c                   eval      elboi2_hgrp = elhgrp
     c                   eval      elboi2_ugrp = elugrp
     c                   when      w_seqe = 'H'
     c                   eval      elboi3_hgrp = elhgrp
     c                   eval      elboi3_ugrp = elugrp
     c                   when      w_seqe = 'U'
     c                   eval      elboi4_ugrp = elugrp
     c                   when      w_seqe = 'M'
     c                   eval      elboi5_modn = elmodn
     c                   when      w_seqe = 'V'
     c                   eval      elboi6_vare = elvare
     c                   eval      elboi6_enhe = elenhe
     c                   eval      elboi6_lety = ellety
     c                   when      w_seqe = 'E'
     c                   eval      elboi7_enhe = elenhe
     c                   when      w_seqe = 'T'
     c                   eval      elboi8_lety = ellety
     c                   when      w_seqe = 'B'
     c                   eval      elboi9_boty = elboty
     c                   other
     c                   eval      elboi1_ldor = elldor
     c                   eval      elboi1_ogrp = elogrp
     c                   eval      elboi1_hgrp = elhgrp
     c                   eval      elboi1_ugrp = elugrp
     c                   eval      elboi1_modn = elmodn
     c                   eval      elboi1_vare = elvare
     c                   eval      elboi1_enhe = elenhe
     c                   eval      elboi1_lety = ellety
     c                   eval      elboi1_boty = elboty
     c                   eval      elboi1_fdat = elfdat
     c                   eval      elboi1_tdat = eltdat
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'O'
     c     elboi2_key    setll     elboi2
     c                   when      w_seqe = 'H'
     c     elboi3_key    setll     elboi3
     c                   when      w_seqe = 'U'
     c     elboi4_key    setll     elboi4
     c                   when      w_seqe = 'M'
     c     elboi5_key    setll     elboi5
     c                   when      w_seqe = 'V'
     c     elboi6_key    setll     elboi6
     c                   when      w_seqe = 'E'
     c     elboi7_key    setll     elboi7
     c                   when      w_seqe = 'T'
     c     elboi8_key    setll     elboi8
     c                   when      w_seqe = 'B'
     c     elboi9_key    setll     elboi9
     c                   other
     c     elboi1_key    setll     elboi1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Leverandør
      *
     c                   if        w_afld = 'B2LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    b2ldor
     c                   parm                    w_lnav
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    c1ldor
     c                   parm                    c1lnav
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    k1ldor
     c                   parm                    w_lnav
     c                   leavesr
     c                   endif
      *
      * Overgruppe
      *
     c                   if        w_afld = 'B2OGRP'
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    w_gtxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1OGRP'
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    c1ogrp
     c                   parm                    c1gtxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1OGRP'
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    k1ogrp
     c                   parm                    w_gtxt
     c                   leavesr
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_afld = 'B2HGRP'
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    w_gtxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1HGRP'
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    c1ogrp
     c                   parm                    c1hgrp
     c                   parm                    c1gtxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1HGRP'
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    k1ogrp
     c                   parm                    k1hgrp
     c                   parm                    w_gtxt
     c                   leavesr
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_afld = 'B2UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    b2ugrp
     c                   parm                    w_gtxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    c1ogrp
     c                   parm                    c1hgrp
     c                   parm                    c1ugrp
     c                   parm                    c1gtxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    k1ogrp
     c                   parm                    k1hgrp
     c                   parm                    k1ugrp
     c                   parm                    w_gtxt
     c                   leavesr
     c                   endif
      *
      * Modulnr
      *
     c                   if        w_afld = 'B2MODN'
     c                   call      'FO707C'
     c                   parm                    w_firm
     c                   parm                    b2modn
     c                   parm                    w_mtxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1MODN'
     c                   call      'FO707C'
     c                   parm                    w_firm
     c                   parm                    c1modn
     c                   parm                    c1mtxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1MODN'
     c                   call      'FO707C'
     c                   parm                    w_firm
     c                   parm                    k1modn
     c                   parm                    w_mtxt
     c                   leavesr
     c                   endif
      *
      * Varenummer
      *
     c                   if        w_afld = 'B2VARE'
     c                   eval      w_vare = b2vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   eval      b2vare = w_vare
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1VARE'
     c                   eval      w_vare = c1vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    c1vtxt
     c                   eval      c1vare = w_vare
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1VARE'
     c                   eval      w_vare = k1vare
     c                   call      'VV500R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    w_vtxt
     c                   eval      k1vare = w_vare
     c                   leavesr
     c                   endif
      *
      * Enhet
      *
     c                   if        w_afld = 'B2ENHE'
     c                   if        b2vare <> *blank
     c                   eval      w_vare = b2vare
     c                   call      'VE550R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    b2enhe
     c                   parm                    w_etxt
     c                   else
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    b2enhe
     c                   parm                    w_etxt
     c                   endif
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1ENHE'
     c                   if        c1vare <> *blank
     c                   eval      w_vare = c1vare
     c                   call      'VE550R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    c1enhe
     c                   parm                    c1etxt
     c                   else
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    c1enhe
     c                   parm                    c1etxt
     c                   endif
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1ENHE'
     c                   if        k1vare <> *blank
     c                   eval      w_vare = k1vare
     c                   call      'VE550R'
     c                   parm                    w_firm
     c                   parm                    w_vare
     c                   parm                    k1enhe
     c                   parm                    w_etxt
     c                   else
     c                   call      'VA510R'
     c                   parm                    w_firm
     c                   parm                    k1enhe
     c                   parm                    w_etxt
     c                   endif
     c                   leavesr
     c                   endif
      *
      * Leveringstype
      *
     c                   if        w_afld = 'B2LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    b2lety
     c                   parm                    w_ltxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    c1lety
     c                   parm                    c1ltxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1LETY'
     c                   call      'VY500R'
     c                   parm                    w_firm
     c                   parm                    k1lety
     c                   parm                    w_ltxt
     c                   leavesr
     c                   endif
      *
      * Bonustype
      *
     c                   if        w_afld = 'B2BOTY'
     c                   call      'VA565R'
     c                   parm                    w_firm
     c                   parm                    b2boty
     c                   parm                    w_btxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'C1BOTY'
     c                   call      'VA565R'
     c                   parm                    w_firm
     c                   parm                    c1boty
     c                   parm                    c1btxt
     c                   leavesr
     c                   endif
      *
     c                   if        w_afld = 'K1BOTY'
     c                   call      'VA565R'
     c                   parm                    w_firm
     c                   parm                    k1boty
     c                   parm                    w_btxt
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker inntastede verdier, og henter beskrivelser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
     c                   eval      w_lnav = *blank
     c                   eval      w_gtxt = *blank
     c                   eval      w_mtxt = *blank
     c                   eval      w_vtxt = *blank
     c                   eval      w_etxt = *blank
     c                   eval      w_ltxt = *blank
     c                   eval      w_btxt = *blank
      *
      * Bonus-nivå
      *
     c                   if        w_boty = *blank
     c                   eval      *in31 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        w_modn <> 0
     c                   if        w_ldor <> 0 or
     c                             w_ogrp <> 0 or
     c                             w_vare <> *blank
     c                   eval      *in34 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
     c                   if        w_vare <> *blank
     c                   if        w_ldor <> 0 or
     c                             w_ogrp <> 0 or
     c                             w_modn <> 0
     c                   eval      *in35 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   endif
      *
      * Leverandør
      *
     c                   if        w_ldor <> 0
     c                   eval      rlevl1_levr = w_ldor
     c     rlevl1_key    chain     rlevl1                             40
     c                   if        *in40 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_lnav = rlnavn
     c                   endif
      *
      * Overgruppe
      *
     c                   if        w_ogrp <> 0 and
     c                             w_hgrp = 0
     c                   eval      vogrl1_oogr = w_ogrp
     c     vogrl1_key    chain     vogrl1                             41
     c                   if        *in41 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_gtxt = vgotxt
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_hgrp <> 0 and
     c                             w_ugrp = 0
     c                   eval      vhgrl1_hogr = w_ogrp
     c                   eval      vhgrl1_hhgr = w_hgrp
     c     vhgrl1_key    chain     vhgrl1                             42
     c                   if        *in42 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_gtxt = vghtxt
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_ugrp <> 0
     c                   eval      vugrl1_uogr = w_ogrp
     c                   eval      vugrl1_uhgr = w_hgrp
     c                   eval      vugrl1_uugr = w_ugrp
     c     vugrl1_key    chain     vugrl1                             43
     c                   if        *in43 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_gtxt = vgutxt
     c                   endif
      *
      * Modul
      *
     c                   if        w_modn <> 0
     c                   eval      vvarlb_nmod = w_modn
     c     vvarlb_key    chain     vvarlb                             45
     c                   if        *in45 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   call      'FO714C'
     c                   parm                    w_firm
     c                   parm                    w_modn
     c                   parm                    w_mtxt
     c                   endif
      *
      * Varenummer
      *
     c                   if        w_vare <> *blank
     c                   eval      vvarl1_vare = w_vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      w_vtxt = vvtek1
     c                   else
     c                   eval      jvarl1_vare = w_vare
     c     jvarl1_key    chain     jvarl1
     c                   if        not %found
     c                   eval      *in46 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_vtxt = jvtek1
     c                   endif
     c                   endif
      *
      * Enhet
      *
     c                   if        w_enhe <> *blank
     c                   eval      venhl1_enhe = w_enhe
     c     venhl1_key    chain     venhl1                             47
     c                   if        *in47 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_etxt = vaetxt
     c                   endif
      *
      * Leveringstype
      *
     c                   if        w_lety <> 0
     c                   eval      vltpl1_lety = w_lety
     c     vltpl1_key    chain     vltpl1                             48
     c                   if        *in48 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_ltxt = vybesk
     c                   endif
      *
      * Bonustype
      *
     c                   eval      vbotl1_tbot = w_boty
     c     vbotl1_key    chain     vbotl1                             49
     c                   if        *in49 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_btxt = vatbes
      *
      * Dato
      *
     c                   eval      w_fdat_iso = *loval
     c                   eval      w_tdat_iso = *loval
      *
     c                   if        w_fdat <> 0
     c     *dmy          test(d)                 w_fdat                 50
     c                   if        *in50 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_fdat_iso = %date(w_fdat:*dmy)
     c                   endif
      *
     c                   if        w_tdat <> 0
     c     *dmy          test(d)                 w_tdat                 51
     c                   if        *in51 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
     c                   eval      w_tdat_iso = %date(w_tdat:*dmy)
     c                   endif
      *
     c                   if        w_fdat_iso > w_tdat_iso
     c                   eval      *in52 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   if        w_fdat <> 0 and
     c                             w_tdat = 0 or
     c                             w_fdat = 0 and
     c                             w_tdat <> 0
     c                   eval      *in53 = *on
     c                   eval      b_feil = *on
     c                   leavesr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter beskrivelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_info     begsr
      *
     c                   eval      c1lnav = *blank
     c                   eval      c1gtxt = *blank
     c                   eval      c1mtxt = *blank
     c                   eval      c1vtxt = *blank
     c                   eval      c1etxt = *blank
     c                   eval      c1ltxt = *blank
     c                   eval      c1btxt = *blank
      *
      * Leverandør
      *
     c                   if        c1ldor <> 0
     c                   eval      rlevl1_levr = c1ldor
     c     rlevl1_key    chain     rlevl1
     c                   if        %found
     c                   eval      c1lnav = rlnavn
     c                   endif
     c                   endif
      *
      * Overgruppe
      *
     c                   if        c1ogrp <> 0 and
     c                             c1hgrp = 0
     c                   eval      vogrl1_oogr = c1ogrp
     c     vogrl1_key    chain     vogrl1
     c                   if        %found
     c                   eval      c1gtxt = vgotxt
     c                   endif
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        c1hgrp <> 0 and
     c                             c1ugrp = 0
     c                   eval      vhgrl1_hogr = c1ogrp
     c                   eval      vhgrl1_hhgr = c1hgrp
     c     vhgrl1_key    chain     vhgrl1
     c                   if        %found
     c                   eval      c1gtxt = vghtxt
     c                   endif
     c                   endif
      *
      * Undergruppe
      *
     c                   if        c1ugrp <> 0
     c                   eval      vugrl1_uogr = c1ogrp
     c                   eval      vugrl1_uhgr = c1hgrp
     c                   eval      vugrl1_uugr = c1ugrp
     c     vugrl1_key    chain     vugrl1
     c                   if        %found
     c                   eval      c1gtxt = vgutxt
     c                   endif
     c                   endif
      *
      * Modul
      *
     c                   if        c1modn <> 0
     c                   call      'FO714C'
     c                   parm                    w_firm
     c                   parm                    c1modn
     c                   parm                    c1mtxt
     c                   endif
      *
      * Varenummer
      *
     c                   if        c1vare <> *blank
     c                   eval      vvarl1_vare = c1vare
     c     vvarl1_key    chain     vvarl1
     c                   if        %found
     c                   eval      c1vtxt = vvtek1
     c                   else
     c                   eval      jvarl1_vare = c1vare
     c     jvarl1_key    chain     jvarl1
     c                   if        %found
     c                   eval      c1vtxt = jvtek1
     c                   endif
     c                   endif
     c                   endif
      *
      * Enhet
      *
     c                   if        c1enhe <> *blank
     c                   eval      venhl1_enhe = c1enhe
     c     venhl1_key    chain     venhl1
     c                   if        %found
     c                   eval      c1etxt = vaetxt
     c                   endif
     c                   endif
      *
      * Leveringstype
      *
     c                   if        c1lety <> 0
     c                   eval      vltpl1_lety = c1lety
     c     vltpl1_key    chain     vltpl1
     c                   if        %found
     c                   eval      c1ltxt = vybesk
     c                   endif
     c                   endif
      *
      * Bonustype
      *
     c                   if        c1boty <> *blank
     c                   eval      vbotl1_tbot = c1boty
     c     vbotl1_key    chain     vbotl1
     c                   if        %found
     c                   eval      c1btxt = vatbes
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_blev
      *
      * Key for oppslag på leverandørbonus
      *
     c     elboir_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_blev
     c                   kfld                    elboir_ldor
     c                   kfld                    elboir_ogrp
     c                   kfld                    elboir_hgrp
     c                   kfld                    elboir_ugrp
     c                   kfld                    elboir_modn
     c                   kfld                    elboir_vare
     c                   kfld                    elboir_enhe
     c                   kfld                    elboir_lety
     c                   kfld                    elboir_boty
     c                   kfld                    elboir_fdat
     c                   kfld                    elboir_tdat
      *
      * Key for oppdatering av leverandørbonus
      *
     c     elboiu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_blev
     c                   kfld                    elboiu_ldor
     c                   kfld                    elboiu_ogrp
     c                   kfld                    elboiu_hgrp
     c                   kfld                    elboiu_ugrp
     c                   kfld                    elboiu_modn
     c                   kfld                    elboiu_vare
     c                   kfld                    elboiu_enhe
     c                   kfld                    elboiu_lety
     c                   kfld                    elboiu_boty
     c                   kfld                    elboiu_fdat
     c                   kfld                    elboiu_tdat
      *
      * Key for posisjonering på leverandør
      *
     c     elboi1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_blev
     c                   kfld                    elboi1_ldor
     c                   kfld                    elboi1_ogrp
     c                   kfld                    elboi1_hgrp
     c                   kfld                    elboi1_ugrp
     c                   kfld                    elboi1_modn
     c                   kfld                    elboi1_vare
     c                   kfld                    elboi1_enhe
     c                   kfld                    elboi1_lety
     c                   kfld                    elboi1_boty
     c                   kfld                    elboi1_fdat
     c                   kfld                    elboi1_tdat
      *
      * Key for posisjonering på overgruppe
      *
     c     elboi2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_blev
     c                   kfld                    elboi2_ogrp
     c                   kfld                    elboi2_hgrp
     c                   kfld                    elboi2_ugrp
      *
      * Key for posisjonering på hovedgruppe
      *
     c     elboi3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_blev
     c                   kfld                    elboi3_hgrp
     c                   kfld                    elboi3_ugrp
      *
      * Key for posisjonering på undergruppe
      *
     c     elboi4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_blev
     c                   kfld                    elboi4_ugrp
      *
      * Key for posisjonering på modulnr
      *
     c     elboi5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_blev
     c                   kfld                    elboi5_modn
      *
      * Key for posisjonering på varenummer
      *
     c     elboi6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_blev
     c                   kfld                    elboi6_vare
     c                   kfld                    elboi6_enhe
     c                   kfld                    elboi6_lety
      *
      * Key for posisjonering på enhet
      *
     c     elboi7_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_blev
     c                   kfld                    elboi7_enhe
      *
      * Key for posisjonering på leveringstype
      *
     c     elboi8_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_blev
     c                   kfld                    elboi8_lety
      *
      * Key for posisjonering på bonustype
      *
     c     elboi9_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_blev
     c                   kfld                    elboi9_boty
      *
      * Key for les på leverandørbonus-elment
      *
     c     elbei1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_blev
     c                   kfld                    elbei1_eldo
     c                   kfld                    elbei1_eogr
     c                   kfld                    elbei1_ehgr
     c                   kfld                    elbei1_eugr
     c                   kfld                    elbei1_emod
     c                   kfld                    elbei1_evar
     c                   kfld                    elbei1_eenh
     c                   kfld                    elbei1_elet
     c                   kfld                    elbei1_ebot
     c                   kfld                    elbei1_efda
     c                   kfld                    elbei1_etda
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarl1_vare
      *
     c     vvarlb_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlb_nmod
      *
      * Key for oppslag på enhet
      *
     c     venhl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    venhl1_enhe
      *
      * Key for oppslag på leveringstype
      *
     c     vltpl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vltpl1_lety
      *
      * Key for oppslag på bonustype
      *
     c     vbotl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vbotl1_tbot
      *
      * Key for oppslag på vare (VA)
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      *
      * Henter firma og bonusleverandør fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_blev = p_blev
      *
      * Initierer skjermbilde
      *
     c                   eval      b2blev = w_blev
     c                   eval      b2blna = *blank
      *
     c                   eval      rlevl1_levr = w_blev
     c     rlevl1_key    chain     rlevl1
     c                   if        %found
     c                   eval      b2blna = rlnavn
     c                   endif
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c     elboi1_key    setll     elboi1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
