     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  System. . . . . : ASLAGR                                       *
      *  Program . . . . : LO736R                                       *
      *  Beskrivelse . . : Plukke bestillinger / Vise bestillinger      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  Spesialversjon. :                                              *
      *  Tilpasset for . :                                              *
      *                                                                 *
      *  Referanse  Dato   Endringsbeskrivelse                     Sign.*
      *  --------- ------  ---------------------------------------------*
      *  V5.60     230308  Lagt ut Nobbnr. hvis utfylt alternativt  KOB *
      *                    leverandørens varenr                         *
      *  V5.60     230308  Lagt ut Varetype i skjermbildet          KOB *
5.70  *  PRGR      021208  Utvidet med prisgruppe                   bof
      *                                                                 *
      *  6.10      180609  Oppdatert med sporingsinformasjon        BSA
6.11  *  6.10       181109 Fjernet F1 i D1 bilde, skal være som F1  BOF
6.12  *  6.10      200910  Posisjonering av bekrefte sletting skaperBsa
6.12  *                    problemer for jacada. Kun endring i      Bsa
6.12  *                    skjermfila. Kun for kompilering          Bsa
6.20  *  6.20      310311  Brukerkontroll fra IP til IF             nho
6.21  *  6.20      190511  Utvidet vl710r med div. info             bof
6.22  *  6.20      210611  Bruker kode LBKO10 på IP til IF          bof
6.23  *  6.20      200913  Frislipper recodr, skaper lock situasjon bsa
6.23  *                    senere i rutina. (LO114R)                bsa
6.nu  *  6.20      300514  Gjennomgått mtp. nummerutvidelse         bof
6.30  * 6.30  170918 bkv  Utvidet med trelast sortiment
      *                                                                 *
7.01  * K000  270121 bsa  hvis intern-Otyp,  ikke sjekk på bruker
7.01  * K000              Spesial fra Gipling
      *                                                                 *
8.01  * K000  280823 bsa  Problemer med scroll
8.02  * K000  280923 bsa  Viser ikke desimaler hvis antallet er < 0
      *                                                                 *
9.xx  * K000  271124 bsa  Ligger her pga utvidet VOT1PF, Fjernes etter frislipp
9.xx  * K000              av faktura som blir kreditnota og motsatt.
      *                                                                 *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     FVOTYL1    IF   E           K DISK    RENAME(VOTYPFR:VOTYL1R)
      *
     FLOHELU    UF   E           K DISK    RENAME(LOHEPFR:LOHELUR)
     FLOHEL1    IF   E           K DISK    RENAME(LOHEPFR:LOHEL1R)
     FLODTL1    IF   E           K DISK    RENAME(LODTPFR:LODTL1R)
      *
     FLPLOLU    UF A E           K DISK    RENAME(LPLOPFR:LPLOLUR)
     FLPLOL1    IF   E           K DISK    RENAME(LPLOPFR:LPLOL1R)
     FLPLOLO    IF   E           K DISK    RENAME(LPLOPFR:LPLOLOR)
     FLPLLLU    UF A E           K DISK    RENAME(LPLLPFR:LPLLLUR)
     FLPLLL1    IF   E           K DISK    RENAME(LPLLPFR:LPLLL1R)
     flstsl1    if   e           k disk    rename(lstspfr:lstsl1r)
6.20 flusrl1    if   e           k disk    rename(lusrpfr:lusrl1r)
6.30 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
7.01 fvot1l1    if   e           k disk    rename(vot1pfr:vot1l1r)
      *
     FLO736D    CF   E             WORKSTN SFILE(B1SFL:SRRN01)
     F                                     SFILE(D1SFL:SRRN02)
      *
      *
      * Definering av variable i parametre
      *
     d p_firm          s              3  0
     d p_kode          s              2  0
     d p_text          s             20
     d p_adrs          s             30
     d p_ponr          s              4  0
     d p_pste          s             30
     d p_avde          s              4  0
     d p_feil          s              1
      *
      * Definering av variabler i nøkler
      *
     d lohel1_numm     s                   like(lonumm)
     d lohel1_suff     s                   like(losuff)
     d lplolu_numm     s                   like(lkponr)
     d lplolu_suff     s                   like(lkpsuf)
6.20 d lusrl1_user     s                   like(lbuser)
6.20 d lusrl1_firm     s                   like(lbfirm)
7.01 d vot1l1_otyp     s                   like(vaotyp)
      *
      * Definering av øvrige variable
      *
     d w_on01          s              2
     d w_on02          s              2
     d w_on03          s              2
     d w_on04          s              2
     d w_on05          s              2
     d w_on06          s              2
     d w_datf          s                   like(lofkda)
     d w_datt          s                   like(lofkda)
6.21 d w_utda          s                   like(loedat)
6.21 d w_ldor          s                   like(loldor)
6.21 d b_inlr          s              1    inz(*off)
6.30 d w_lv_nobb       s                   like(vvlnob)
6.30 d w_lv_modn       s                   like(vvlmod)
6.30 d w_lv_lldo       s                   like(vvlnle)
6.30 d w_lv_llva       s                   like(vvllva)
      *
      *
     D                UDS
     D  l_wsid               901    910
     D  l_user               911    920
     D  l_firm               944    946  0
     D  l_fnav               951    980
     D  l_syst               999    999
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Velg om det skal plukkes eller vises                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     WPNUMM        CABEQ     *ZERO         B2TAGA
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  A1 Behandler plukking av bestillingsnummer                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     A1TAGA        TAG
      *
      *  Hent opplysninger fra BESTILLING-HODE-REGISTER
      *
     C                   MOVE      WPNUMM        HEDNUM
     C                   MOVE      WPSUFF        HEDSUF
     C     HEDKEY        CHAIN     LOHEL1R                            90
      *
      *  Hent opplysninger fra ORDRETYPE-REGISTER
      *
     C                   MOVE      LOOTYP        OTYTYP
     C     OTYKEY        CHAIN     VOTYL1R                            90
7.01  * gjør oppslag i side-register av ordretype - sys inn i 6.30
7.01  *
7.01 c                   move      lootyp        otytyp
7.01 c     otykey        chain     vot1l1r                            90
      *
      * Lagrer informasjon for "neste ordretype" sjekk
      *
     c                   eval      w_on01 = vaon01
     c                   eval      w_on02 = vaon02
     c                   eval      w_on03 = vaon03
     c                   eval      w_on04 = vaon04
     c                   eval      w_on05 = vaon05
     c                   eval      w_on06 = vaon06
      *
      *  Legger inn opplysningene i skjermbildefeltene
      *
     C                   MOVE      LONUMM        A1NUMM
     C                   MOVE      LOSUFF        A1SUFF
     C                   MOVE      LOOTYP        A1FOTY
     C                   MOVE      LOLAGE        A1LAGE
     C                   MOVE      LOLENA        A1LENA
     C                   MOVE      VAOTXT        A1FTXT
      *
     C                   MOVE      VAONXT        A1OTYP
     C                   MOVE      *ZERO         A1VLIN
     C                   MOVE      *ZERO         A1TILL
     C                   MOVE      *ZERO         A1PTXT
      *
      *  Lager skal bare vises i utplukksbilde
      *  dersom akkumulatortype er = 1
      *
     C                   MOVE      *OFF          *IN49
     C     VAOAKK        IFEQ      1
     C                   MOVE      *ON           *IN49
     C                   ENDIF
      *
      *  Hent, Tekst for lager
      *
     C                   EXSR      SBLAGR
      *
      *  Send Alt
      *
     C                   WRITE     A1BLD
      *
      *  Bare data
      *
     C     A1TAGB        TAG
     C                   EXFMT     A1BLD
      *
      *  F3=Avslutt program
      *
     C     *INKC         IFEQ      *ON
     C                   MOVE      '9'           WPPLOR
     C                   GOTO      XSLUTT
     C                   END
      *
      *  F4=Spørring ordretype
      *
     C     *INKD         IFEQ      *ON
     C     WACTRE        IFEQ      'A1BLD'
     C     WACTFE        IFEQ      'A1OTYP'
     C                   CALL      'VA550R'
     C                   PARM                    HEDFIR
     C                   PARM                    A1OTYP
     C                   GOTO      A1TAGB
     C                   END
     C                   END
     C                   END
      *
      *  F4=Spørring lager
      *
     C     l_syst        IFEQ      'A'
     C     *INKD         IFEQ      *ON
     C     WACTRE        IFEQ      'A1BLD'
     C     WACTFE        IFEQ      'A1LAGE'
     C                   CALL      'RA510R'
     C                   PARM                    p_firm
     C                   PARM                    p_kode
     C                   PARM                    p_text
     C                   MOVE      p_kode        A1LAGE
     C                   MOVE      p_text        A1LATX
     C                   GOTO      A1TAGB
     C                   END
     C                   END
     C                   END
     C                   END
      *
      *  Sjekk,  Finnes ordretype i ORDRETYPE-REGISTER, er den godkjent?
      *
     C                   MOVE      A1OTYP        OTYTYP
     C     OTYKEY        CHAIN     VOTYL1R                            90
     C     *IN90         IFEQ      *ON
     C                   MOVE      *ON           *IN32
     C                   GOTO      A1TAGB
     C                   END
      *
     c                   if        vaosys <> 1
     c                   eval      *in31 = *on
     c                   goto      a1tagb
     c                   endif
      *
7.01  * hvis intern-ordretype, skal det ikke sjekkes på bruker
7.01  *
7.01 c                   if        va1int <> 1
6.20  * Sjekk om det skal testes på lov til utplukk
6.20  *
6.20 c     lusrl1_key    chain     lusrl1
6.20 c                   if        %found
6.20 c                   if        lbko10 = 0 and
6.20 c                             vaoakk > 2
6.20 c                   eval      *in35 = *on
6.20 c                   goto      a1tagb
6.20 c                   endif
6.20 c                   endif
6.20 c
6.20  *
7.01 c                   endif
      *
     c                   if        laatyk = 1
     c                   if        a1otyp <> w_on01 and
     c                             a1otyp <> w_on02 and
     c                             a1otyp <> w_on03 and
     c                             a1otyp <> w_on04 and
     c                             a1otyp <> w_on05 and
     c                             a1otyp <> w_on06
     c                   eval      *in33 = *on
     c                   goto      a1tagb
     c                   endif
     c                   endif
      *
      *  Sjekk,  Finnes lager i KODE-REGISTER
      *
     C                   exsr      sblagr
     C                   if        p_feil = *on
     C                   eval      *in34 = *on
     C                   goto      a1tagb
     C                   endif
6.20  *
      *
      *  Dersom noen valg er endret, sendt bilde pånytt
      *
     C     A1LAGE        IFNE      LOLAGE
     C                   MOVE      A1LAGE        LOLAGE
     C                   GOTO      A1TAGB
     C                   END
      *
      *  Oppdater BESTILLING-HODE-REGISTER, Bestillingen er til behandling
      *
     C                   CALL      'LO709R'
     C                   PARM                    HEDNUM
     C                   PARM                    HEDSUF
      *
     C     HEDKEY        CHAIN     LOHELUR                            90
     C     *IN90         IFEQ      *OFF
     C                   MOVE      3             LOKODE
6.10 c                   time                    loedat
6.10 c                   time                    loetim
6.10 c                   eval      loeusr = l_user
     C                   UPDATE    LOHELUR
     C                   END
      *
      *
      *  Oppdater UTPLUKK-PARAM-REGISTER med ny bestilling
      *
     C                   MOVE      WPNUMM        PLONUM
     C                   MOVE      WPSUFF        PLOSUF
     C     PLOKEY        CHAIN     LPLOLUR                            90
     C     *IN90         IFEQ      *ON
     C                   MOVE      PLOFIR        LKPFIR
     C                   MOVE      PLONUM        LKPONR
     C                   MOVE      PLOSUF        LKPSUF
     C                   MOVE      A1OTYP        LKPOTY
     C                   MOVE      A1LAGE        LKPLAG
     C                   MOVE      l_wsid        LKPWSD
     C                   MOVE      l_user        LKPUSR
     C                   MOVE      *ZERO         LKPALT
     C                   MOVE      *ZERO         LKPDIR
     C                   MOVE      A1VLIN        LKPRES
     C                   MOVE      A1PTXT        LKPTXT
     C                   MOVE      *ZERO         LKPKOL
     C                   MOVE      *ZERO         LKPNET
     C                   MOVE      *ZERO         LKPBRU
     C                   MOVE      *BLANK        LKPLMT
     C                   MOVE      *ZERO         LKPFPR
     C                   MOVE      *BLANK        LKPFTE
     C                   MOVE      *ZERO         LKPEPR
     C                   MOVE      *BLANK        LKPETE
     C                   WRITE     LPLOLUR
     C                   ELSE
     C                   UPDATE    LPLOLUR
     C                   END
      *
      *  Subrutinen for resting av varelinjer skal hentes opp
      *
     C     A1VLIN        IFEQ      '1'
     C     A1VLIN        OREQ      '2'
     C                   EXSR      XD1BLD
     C                   END
      *
      *  Subrutinen for tilleggsopplysninger av varelinjer skal hentes opp
      *
     C     A1TILL        IFEQ      '1'
     C                   EXSR      XB7WIN
     C                   END
      *
      *  Kaller opp utskrift-program for å sette inn alternativ
      *  og om direktevalg skal benyttes
      *
     C                   Z-ADD     1             WWVALG
      *
     C                   CALL      'LO600R'
     C                   PARM                    WPIPOR
     C                   PARM                    PLOFIR
     C                   PARM                    PLONUM
     C                   PARM                    PLOSUF
     C                   PARM                    WWVALG
      *
     C                   MOVE      WPIPOR        WPPLOR
      *
     C                   GOTO      XSLUTT
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  B2 Behandler åpningsbildet samt funksjonstaster                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Send Alt
      *
     C     B2TAGA        TAG
     C                   WRITE     B2CMD
      *
      *  Bare data
      *
     C     B2TAGB        TAG
     C                   EXSR      XDSP01
      *
      *  F1=Vise bla-alternativer
      *
     C     *INKA         IFEQ      *ON
     C                   EXSR      XH1WIN
     C                   ENDIF
      *
      *  F3=Avslutt program
      *
     C     *INKC         IFEQ      *ON
     C                   MOVE      '9'           WPPLOR
     C                   GOTO      XSLUTT
     C                   END
      *
      *  F5=Forny
      *
     C     *INKE         IFEQ      *ON
     C                   EXSR      XLSP01
     C                   GOTO      B2TAGB
     C                   ENDIF
      *
      *  F7=Fakt.utplukk (dato)
      *
     C                   if        *inkg = *on
     c                   exsr      xf7bld
     C                   goto      b2tagb
     C                   end
      *
      *  F15=Starter utskrift
      *
     C     *INKP         IFEQ      *ON
     C                   CALL      'LO604R'
     C                   PARM                    WPFTOR
     C                   PARM                    WPFIRM
     C                   PARM                    WXNUMM
     C                   PARM                    WXSUFF
     C                   PARM                    WXOTYP
     C                   PARM                    WXPDIR
     C                   PARM                    WWOUTQ
     C                   GOTO      B2TAGB
     C                   END
      *
      *  Roll
      *
     C     *IN10         IFEQ      *ON
     C                   EXSR      XCRT01
     C                   GOTO      B2TAGB
     C                   ENDIF
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandling av startverdier for start f.o.m.                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Posisjoner
      *  startverdi
      *  på bestillingsnummer, men rutinen blar bakfra
      *
     C     B2NUMM        IFNE      *ZERO
     C     WSEQEN        IFEQ      'B'
     C                   MOVE      B2NUMM        PLONUM
     C                   MOVE      99            PLOSUF
     C     PLOKEY        SETLL     LPLOL1
     C                   EXSR      XCLR01
     C                   EXSR      XCRT01
     C                   MOVE      *ZERO         B2NUMM
     C                   GOTO      B2TAGB
     C                   END
     C                   END
      *  Posisjoner
      *  startverdi
      *  på ordretype + bestillingsnummer
      *
     C     B2NUMM        IFNE      *ZERO
     C     WSEQEN        IFEQ      'O'
     C                   MOVE      B2NUMM        POTNUM
     C                   MOVE      *ZERO         POTSUF
     C     POTKEY        SETLL     LPLOLO
     C                   EXSR      XCLR01
     C                   EXSR      XCRT01
     C                   MOVE      *ZERO         B2NUMM
     C                   GOTO      B2TAGB
     C                   END
     C                   END
      *
      *
      *  Posisjoner
      *  startverdi
      *  på bestillingsnummer
      *
     C     B2NUMM        IFNE      *ZERO
     C                   MOVE      ' '           WSEQEN
     C                   MOVE      B2NUMM        PLONUM
     C                   MOVE      *ZERO         PLOSUF
     C     PLOKEY        SETLL     LPLOL1
     C                   EXSR      XCLR01
     C                   EXSR      XCRT01
     C                   MOVE      *ZERO         B2NUMM
     C                   GOTO      B2TAGB
     C                   END
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandling av subfile, Foretar valgene                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Sjekk om valg
      *  er foretatt
      *
     C                   DO        SSRN01
     C                   READC     B1SFL                                  16
     C     *IN16         IFEQ      *ON
     C                   LEAVE
     C                   ENDIF
      *
      *  Ved valg 2 eller 7, Sjekk om dette er en utskrift
      *  Hvis dette er en utskrift skal den ikke kunne vedlikeholdes
      *
     C     B1VALG        IFEQ      '2 '
     C     B1VALG        OREQ      '7 '
      *
      *  Hent opplysninger fra BESTILLING-HODE-REGISTER
      *
     C                   MOVE      B1NUMM        HEDNUM
     C                   MOVE      B1SUFF        HEDSUF
     C     HEDKEY        CHAIN     LOHEL1R                            90
      *
     C     LOKODE        IFNE      3
     C                   MOVE      *BLANK        B1VALG
     C                   UPDATE    B1SFL
     C                   ITER
     C                   END
      *
     C                   END
      *
      *  Registrering/Endring av resting
      *
     C     B1VALG        IFEQ      '2 '
     C                   MOVE      B1NUMM        HEDNUM
     C                   MOVE      B1SUFF        HEDSUF
     C                   EXSR      XD1BLD
     C                   ENDIF
      *
      *  Slette utplukk/Fjerne merke for at bestilling er til behandling
      *
     C     B1VALG        IFEQ      '4 '
     C                   MOVE      B1NUMM        PLONUM
     C                   MOVE      B1SUFF        PLOSUF
     C                   EXSR      XB4WIN
     C                   ITER
     C                   ENDIF
      *
      *  Registre/Endre tilleggsopplysninger
      *
     C     B1VALG        IFEQ      '7 '
     C                   MOVE      B1NUMM        PLONUM
     C                   MOVE      B1SUFF        PLOSUF
     C                   EXSR      XB7WIN
     C                   ITER
     C                   ENDIF
      *
     C                   ENDDO
      *
      *  Nullstiller/blanker feltet for start f.o.m.
      *
     C                   MOVE      *ZERO         B2NUMM
      *
     C                   GOTO      B2TAGA
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutt program                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XSLUTT        TAG
      *
     C                   SETON                                        LR
     C                   RETURN
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  B4 Behandle bilde for sletting av utvalg                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XB4WIN        BEGSR
      *
     C                   MOVE      PLONUM        B4NUMM
      *
     C                   WRITE     B4BLD
     C                   EXFMT     B4BLD
      *
      *  F3=Avslutt
      *
     C     *INKC         CABEQ     *ON           B4TAG1
      *
      *  Kaller opp subprogram for å slette utvalg og blanke merking
      *
     C                   CALL      'LO402R'
     C                   PARM                    WPIPOR
     C                   PARM                    PLOFIR
     C                   PARM                    PLONUM
     C                   PARM                    PLOSUF
      *
      *  Oppdater subfile
      *
     C                   MOVE      *BLANK        B1WSID
     C                   MOVEL     '*Slettet'    B1WSID
      *
     C     B4TAG1        TAG
      *
     C                   MOVE      *BLANK        B1VALG
     C                   UPDATE    B1SFL
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  B7 Behandle bilde for innlegging av tilleggsopplysninger       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XB7WIN        BEGSR
      *
      *  Henter inn opplysninger fra UTPLUKK-PARAM-REGISTER
      *
     C     PLOKEY        CHAIN     LPLOL1                             90
     C     *IN90         IFEQ      *OFF
     C                   MOVE      LKPONR        B7NUMM
     C                   MOVE      LKPKOL        B7KOLI
     C                   MOVE      LKPNET        B7NETT
     C                   MOVE      LKPBRU        B7BRUT
     C                   MOVE      LKPLMT        B7LMTX
     C                   MOVE      LKPFPR        B7FRPR
     C                   MOVE      LKPFTE        B7FRTE
     C                   MOVE      LKPEPR        B7EMPR
     C                   MOVE      LKPETE        B7EMTE
     C                   END
      *
     C                   WRITE     B7BLD
     C                   EXFMT     B7BLD
      *
      *  F3=Avslutt
      *
     C     *INKC         CABEQ     *ON           XB7SLT
      *
      *  Oppdater UTPLUKK-PARAM-REGISTER
      *
     C     PLOKEY        CHAIN     LPLOLUR                            90
     C     *IN90         IFEQ      *OFF
     C                   MOVE      B7KOLI        LKPKOL
     C                   MOVE      B7NETT        LKPNET
     C                   MOVE      B7BRUT        LKPBRU
     C                   MOVE      B7LMTX        LKPLMT
     C                   MOVE      B7FRPR        LKPFPR
     C                   MOVE      B7FRTE        LKPFTE
     C                   MOVE      B7EMPR        LKPEPR
     C                   MOVE      B7EMTE        LKPETE
     C                   UPDATE    LPLOLUR
     C                   END
      *
      *  Oppdater subfile
      *
     C     XB7SLT        TAG
      *
     C     B1VALG        IFNE      *BLANK
     C                   MOVE      *BLANK        B1VALG
     C                   UPDATE    B1SFL
     C                   END
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  D1 Behandle bilde for PLUKKING AV LINJER                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XD1BLD        BEGSR
      *
6.11 C                   MOVE      *ON           *IN21
      *
      *  Legger inn bestillingsnummer og suffix i nøkklene
      *
     C                   MOVE      HEDNUM        LINNUM
     C                   MOVE      HEDSUF        LINSUF
     C                   MOVE      HEDNUM        PLLNUM
     C                   MOVE      HEDSUF        PLLSUF
      *
      *  Hent opplysninger fra BESTILLING-HODE-REGISTER
      *
     C     HEDKEY        CHAIN     LOHEL1R                            90
     C                   MOVE      LONUMM        D2NUMM
     C                   MOVE      LOSUFF        D2SUFF
     C                   MOVE      LOLDOR        D2LDOR
     C                   MOVE      LOLENA        D2LENA
      *
      *  Hent opplysninger fra BESTILLING-LINJE-REGISTER
      *
     C                   MOVE      *ZERO         LINLIN
     C                   MOVE      *ZERO         PLLLIN
     C     LINKEY        SETLL     LODTL1
     C                   EXSR      XCLR02
     C                   EXSR      XCRT02
      *
      *  Send Alt
      *
     C     D2TAGA        TAG
     C                   WRITE     D2CMD
      *
      *  Bare data
      *
     C     D2TAGB        TAG
     C                   EXSR      XDSP02
      *
      *  F3=Avslutt
      *
     C     *INKC         CABEQ     *ON           XD1SLT
      *
      *  F5=Forny
      *
     C     *INKE         IFEQ      *ON
     C                   EXSR      XLSP02
     C                   GOTO      D2TAGB
     C                   ENDIF
      *
      *  Roll
      *
     C     *IN10         IFEQ      *ON
     C                   EXSR      XCRT02
     C                   GOTO      D2TAGB
     C                   ENDIF
      *
8.01 c                   if        *in11 = *on
8.01 c                   exsr      xlsp02
8.01 c                   goto      d2tagb
8.01 c                   endif
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandling av startverdier for start f.o.m.                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Posisjoner
      *  startverdi
      *  på linjenummer
      *
     C     D2LINE        IFNE      *ZERO
     C                   MOVE      D2LINE        LINLIN
     C     LINKEY        SETLL     LODTL1R
     C                   EXSR      XCLR02
     C                   EXSR      XCRT02
     C                   MOVE      *ZERO         D2LINE
     C                   GOTO      D2TAGB
     C                   END
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandling av subfile, Foretar valgene                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Sjekk om valg
      *  er foretatt
      *
     C                   DO        SSRN02
     C                   READC     D1SFL                                  16
     C     *IN16         IFEQ      *ON
     C                   LEAVE
     C                   ENDIF
      *
      *  Sjekk alle linjer
      *
     C                   MOVE      D1LINE        PLLLIN
     C                   EXSR      XD7WIN
      *
     C                   ENDDO
      *
      *  Nullstiller/blanker feltet for start f.o.m.
      *
     C                   MOVE      *ZERO         D2LINE
      *
     C                   EXSR      XLSP02
     C                   GOTO      D2TAGA
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Avslutter behandlingen av utplukk/resting                      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XD1SLT        TAG
      *
      *  Oppdaterer subfile i forrige bilde (B1) før retur
      *
     C     B1VALG        IFNE      *BLANK
     C     SRRN01        CHAIN     B1SFL                              90
     C                   MOVE      *BLANK        B1VALG
     C                   UPDATE    B1SFL
     C                   END
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  D7 Behandle bilde for OPPDATERING AV PLUKK-REGISTER            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XD7WIN        BEGSR
      *
      *  Benytter Z-ADD på antall inn i nytt felt fordi feltene
      *  ikke er like store, benytter kortere feltlengde i bildet
      *
     C     PLLKEY        CHAIN     LPLLLUR                            96
      *
      *  Hverken antall eller kode er tastet, hopp ut, Slett evnt. record
      *
     c                   if        d1kode = '9'
     c                   eval      d1kode = *blank
     c                   endif
     C     D1LEVE        IFEQ      *ZERO
     C     D1KODE        IFEQ      *BLANK
     C     *IN96         IFEQ      *OFF
     C                   DELETE    LPLLLUR
     C                   ENDIF
     C                   GOTO      XD7SLT
     C                   ENDIF
     C                   ENDIF
      *
      *  Legg inn verdier i UTPLUKK-LINJE-REGISTER
      *
     C     D1EVAR        IFEQ      *BLANK
     C     D1LEVE        IFEQ      1
     C                   MOVE      '1'           D1KODE
     C                   END
     C     D1LEVE        IFEQ      3
     C                   MOVE      '3'           D1KODE
     C                   END
     C     D1LEVE        IFEQ      4
     C                   MOVE      '4'           D1KODE
     C                   END
     C                   MOVE      *ZERO         D1LEVE
     C                   END
      *
     C                   Z-ADD     D1LEVE        LKLANT
     C                   MOVE      D1KODE        LKLKOD
      *
     C     *IN96         IFEQ      *OFF
     C                   UPDATE    LPLLLUR
     C                   ELSE
     C                   MOVE      PLLFIR        LKLFIR
     C                   MOVE      PLLNUM        LKLONR
     C                   MOVE      PLLSUF        LKLSUF
     C                   MOVE      PLLLIN        LKLLIN
     C                   WRITE     LPLLLUR
     C                   END
      *
      *  Oppdater subfile
      *
     C     XD7SLT        TAG
      *
      * Setter farge
     c                   eval      *in55 = *off
     c                   eval      *in56 = *off
     c                   eval      *in57 = *off
     c                   eval      *in58 = *off
     c                   eval      *in59 = *on
     c                   select
     c                   when      d1kode = '1'
     c                   eval      *in57 = *on
     c                   eval      *in59 = *off
     c                   when      d1kode = '3'
     c                   eval      *in56 = *on
     c                   eval      *in59 = *off
     c                   when      d1kode = '4'
     c                   eval      *in55 = *on
     c                   eval      *in59 = *off
     c                   when      d1kode = '5'
     c                   eval      *in58 = *on
     c                   eval      *in59 = *off
     c                   endsl
      *
     C                   UPDATE    D1SFL
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  H1 Behandle bilde for bla-alternativer                         *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     XH1WIN        BEGSR
      *
      *  Send Alt
      *
     C                   WRITE     H1BLD
     C                   EXFMT     H1BLD
      *
     C     *INKC         CABEQ     *ON           XH1SLT
      *
      *  Kun en bestemt ordre-type
      *
     C     H1OTYP        IFNE      *BLANK
     C                   MOVE      'O'           WSEQEN
     C                   MOVE      *ZERO         POTNUM
     C                   MOVE      *ZERO         POTSUF
     C                   MOVE      H1OTYP        POTOTY
     C     POTKEY        SETLL     LPLOLO
     C                   EXSR      XCLR01
     C                   EXSR      XCRT01
     C                   MOVE      *ZERO         B2NUMM
     C                   MOVE      *BLANK        H1OTYP
     C                   GOTO      XH1SLT
     C                   END
      *
      *  Ved blaing i bestillings-register skal det blas bakfra
      *
     C     H1VALG        IFEQ      1
     C                   MOVE      'B'           WSEQEN
     C                   MOVE      999999        PLONUM
     C                   MOVE      99            PLOSUF
     C     PLOKEY        SETLL     LPLOL1
     C                   EXSR      XCLR01
     C                   EXSR      XCRT01
     C                   MOVE      *ZERO         B2NUMM
     C                   MOVE      *BLANK        H1VALG
     C                   GOTO      XH1SLT
     C                   ENDIF
      *
      *  Dersom ingen er valgt, men ENTER er trykket
      *  skal startverdi settes til bestillingsnummer lik null
      *
     C                   MOVE      ' '           WSEQEN
     C                   MOVE      *ZERO         PLONUM
     C                   MOVE      *ZERO         PLOSUF
     C     PLOKEY        SETLL     LPLOL1
     C                   EXSR      XCLR01
     C                   EXSR      XCRT01
     C                   MOVE      *ZERO         B2NUMM
      *
     C     XH1SLT        ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Sjekk/Hent tekst for lager                                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     SBLAGR        BEGSR
      *
     C                   eval      p_kode = a1lage
      *
     c                   call      'FÅ720R'
     c                   parm                    p_firm
     c                   parm                    p_kode
     c                   parm                    p_text
     c                   parm                    p_adrs
     c                   parm                    p_ponr
     c                   parm                    p_pste
     c                   parm                    p_avde
     c                   parm                    p_feil
      *
     C                   eval      a1latx = *blank
      *
     C                   if        p_feil = *off
     C                   eval      a1latx = p_text
     C                   endif
      *
     C                   ENDSR
      *
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Fornyer subfilen (dvs. loader subfilen pånytt)                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XLSP01        BEGSR
      *
     C     SFRN01        IFNE      *ZERO
     C     SFRN01        CHAIN     B1SFL                              60
     C                   END
      *
     C                   MOVE      B1NUMM        PLONUM
     C                   MOVE      B1SUFF        PLOSUF
      *
     C     WSEQEN        IFEQ      'B'
     C     PLOKEY        SETLL     LPLOL1
     C                   END
     C     WSEQEN        IFEQ      'O'
     C     POTKEY        SETLL     LPLOLO
     C                   END
     C     WSEQEN        IFEQ      *BLANK
     C     PLOKEY        SETLL     LPLOL1
     C                   END
      *
     C                   EXSR      XCLR01
     C                   EXSR      XCRT01
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Display subfile                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XDSP01        BEGSR
      *
     C     SRRN01        IFNE      0
     C                   MOVE      *ON           *IN12
     C                   ENDIF
      *
     C                   MOVE      *ON           *IN13
     C                   EXFMT     B2CTL
     C                   MOVE      *OFF          *IN12
     C                   MOVE      *OFF          *IN13
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tøm subfile                                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XCLR01        BEGSR
     C                   MOVE      *ON           *IN14
     C                   WRITE     B2CTL
     C                   MOVE      *OFF          *IN14
      *
     C                   MOVE      *OFF          *IN15
     C                   Z-ADD     0             SRRN01
     C                   Z-ADD     0             SFRN01
     C                   Z-ADD     0             SSRN01
     C                   Z-ADD     0             SPGE01
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Fyll opp subfile                                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XCRT01        BEGSR
     C                   ADD       14            SPGE01
     C                   Z-ADD     SSRN01        SRRN01
      *
     C     SRRN01        DOUEQ     SPGE01
      *
     C     WSEQEN        IFEQ      'B'
     C                   READP     LPLOL1                                 15
     C                   END
     C     WSEQEN        IFEQ      'O'
     C                   READ      LPLOLO                                 15
     C     *IN15         IFEQ      *OFF
     C     LKPOTY        IFNE      POTOTY
     C                   MOVE      *ON           *IN15
     C                   END
     C                   END
     C                   END
     C     WSEQEN        IFEQ      *BLANK
     C                   READ      LPLOL1                                 15
     C                   END
      *
     C     *IN15         CABEQ     '1'           XEND01
     C                   ADD       1             SRRN01
     C                   MOVE      LKPONR        B1NUMM
     C                   MOVE      LKPSUF        B1SUFF
     C                   MOVE      LKPOTY        B1OTYP
     C                   MOVE      LKPWSD        B1WSID
     C                   MOVE      LKPUSR        B1USER
     C                   MOVE      LKPALT        B1PALT
     C                   MOVE      LKPTXT        B1PTXT
     C                   WRITE     B1SFL
     C                   ENDDO
      *
     C     XEND01        TAG
      *
     C     SRRN01        IFGT      SSRN01
     C     SSRN01        ADD       1             SFRN01
     C                   ENDIF
      *
     C                   Z-ADD     SRRN01        SSRN01
     C                   Z-ADD     SFRN01        SVRN01
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Fornyer subfilen (dvs. loader subfilen pånytt)                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XLSP02        BEGSR
      *
     C     SFRN02        IFNE      *ZERO
     C     SFRN02        CHAIN     D1SFL                              90
     C                   END
      *
8.01 c                   if        *in11 = *on
8.01 c                   eval      d1line = 0
8.01 c                   endif
     C                   MOVE      D1LINE        LINLIN
      *
     C     LINKEY        SETLL     LODTL1
      *
     C                   EXSR      XCLR02
     C                   EXSR      XCRT02
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Display subfile                                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XDSP02        BEGSR
      *
     C     SRRN02        IFNE      0
     C                   MOVE      *ON           *IN12
     C                   ENDIF
      *
     C                   MOVE      *ON           *IN13
     C                   EXFMT     D2CTL
     C                   MOVE      *OFF          *IN12
     C                   MOVE      *OFF          *IN13
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tøm subfile                                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XCLR02        BEGSR
     C                   MOVE      *ON           *IN14
     C                   WRITE     D2CTL
     C                   MOVE      *OFF          *IN14
      *
     C                   MOVE      *OFF          *IN15
     C                   Z-ADD     0             SRRN02
     C                   Z-ADD     0             SFRN02
     C                   Z-ADD     0             SSRN02
     C                   Z-ADD     0             SPGE02
      *
     C                   ENDSR
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Fyll opp subfile                                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     XCRT02        BEGSR
     C                   ADD       14            SPGE02
     C                   Z-ADD     SSRN02        SRRN02
      *
     C     SRRN02        DOUEQ     SPGE02
      *
     C                   READ      LODTL1                                 15
     C     *IN15         IFEQ      *OFF
     C     LDNUMM        IFNE      LINNUM
     C                   MOVE      *ON           *IN15
     C                   END
     C                   END
     C     *IN15         IFEQ      *OFF
     C     LDSUFF        IFNE      LINSUF
     C                   MOVE      *ON           *IN15
     C                   END
     C                   END
      *
     C     *IN15         CABEQ     '1'           XEND02
      *
     C                   ADD       1             SRRN02
      *
     C                   MOVE      LDLINE        PLLLIN
     C     PLLKEY        CHAIN     LPLLL1                             96
     C  N96              Z-ADD     LKLANT        D1LEVE
     C  N96              MOVE      LKLKOD        D1KODE
     C   96              MOVE      *ZERO         D1LEVE
     C   96              MOVE      *BLANK        D1KODE
      *
     C                   MOVE      LDLINE        D1LINE
     C                   MOVE      LDANTA        D1ANTA
      *
V5.60C                   if        LDNOBB <> 0
V5.60C                   MOVEL     LDNOBB        D1VARE
V5.60C                   else
V5.60C                   MOVEL     LDLVAR        D1VARE
V5.60C                   endif
      *
      * henter varetype
      *
5.70 c                   call      'VL710R'
5.70 c                   parm                    ldfirm
5.70 c                   parm                    ldvare
5.70 c                   parm                    ldlage
5.70 c                   parm                    d1vtyp
6.21 c                   parm                    w_utda
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
      *
     C                   MOVEL     LDVARE        D1EVAR
     C                   MOVEL     LDTEK1        D1TEK1
     C                   MOVE      LDENH1        D1ENH1
      *
      * Setter farge
     c                   eval      *in55 = *off
     c                   eval      *in56 = *off
     c                   eval      *in57 = *off
     c                   eval      *in58 = *off
     c                   eval      *in59 = *on
     c                   select
     c                   when      d1kode = '1'
     c                   eval      *in57 = *on
     c                   eval      *in59 = *off
     c                   when      d1kode = '3'
     c                   eval      *in56 = *on
     c                   eval      *in59 = *off
     c                   when      d1kode = '4'
     c                   eval      *in55 = *on
     c                   eval      *in59 = *off
     c                   when      d1kode = '5'
     c                   eval      *in58 = *on
     c                   eval      *in59 = *off
     c                   endsl
      *
     C                   WRITE     D1SFL
     C                   ENDDO
      *
     C     XEND02        TAG
      *
     C     SRRN02        IFGT      SSRN02
     C     SSRN02        ADD       1             SFRN02
     C                   ENDIF
      *
     C                   Z-ADD     SRRN02        SSRN02
     C                   Z-ADD     SFRN02        SVRN02
      *
     C                   ENDSR
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Faktura-utplukk (fra/til dato)                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xf7bld        begsr
      *
     c                   eval      *in31 = *off
     c                   eval      *in32 = *off
     c                   eval      *in33 = *off
      *
     c     f7taga        tag
     c                   exfmt     f7bld
      *
     c                   if        *inkc = *on or
     c                             *inkl = *on
     c                   goto      end_f7bld
     c                   endif
      *
     c                   if        f7otyp = *blank
     c                   eval      *in31 = *on
     c                   goto      f7taga
     c                   endif
      *
     c                   if        f7fdat = 0
     c                   eval      *in32 = *on
     c                   goto      f7taga
     c                   endif
      *
     c                   if        f7fdat <> 0
     c     *dmy          test(d)                 f7fdat                 32
     c                   if        *in32  = *on
     c                   goto      f7taga
     c                   endif
     c                   endif
      *
     c                   if        f7tdat = 0
     c                   eval      *in33 = *on
     c                   goto      f7taga
     c                   endif
      *
     c                   if        f7tdat <> 0
     c     *dmy          test(d)                 f7tdat                 33
     c                   if        *in33  = *on
     c                   goto      f7taga
     c                   endif
     c                   endif
      *
     c     *dmy          move      f7fdat        w_datf
     c     *dmy          move      f7tdat        w_datt
      *
     c                   eval      lohel1_numm = *zero
     c                   eval      lohel1_suff = *zero
     c     lohel1_key    setll     lohel1
     c                   read      lohel1r
      *
     c                   dow       not %eof
      *
     c                   if        lootyp = f7otyp and
     c                             lofkda >= w_datf and
     c                             lofkda <= w_datt
     c                   exsr      oppd_utpl
     c                   endif
      *
     c                   read      lohel1r
     c                   enddo
      *
     c                   exsr      xlsp01
      *
     c     end_f7bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Oppdater utplukksfil ihht parameter                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     oppd_utpl     begsr
      *
     c                   eval      lplolu_numm = lonumm
     c                   eval      lplolu_suff = losuff
     c     lplolu_key    chain     lplolu
      *
     c                   if        not %found
     c                   eval      lkpfir = wpfirm
     c                   eval      lkponr = lonumm
     c                   eval      lkpsuf = losuff
     c                   eval      lkpoty = lootyp
     c                   eval      lkplag = lolage
     c                   eval      lkpwsd = l_wsid
     c                   eval      lkpusr = l_user
     c                   eval      lkpalt = *zero
     c                   eval      lkpdir = *zero
     c                   eval      lkpres = *zero
     c                   eval      lkptxt = *zero
     c                   eval      lkpkol = *zero
     c                   eval      lkpnet = *zero
     c                   eval      lkpbru = *zero
     c                   eval      lkplmt = *blank
     c                   eval      lkpfpr = *zero
     c                   eval      lkpfte = *blank
     c                   eval      lkpepr = *zero
     c                   eval      lkpete = *blank
     C                   write     lplolur
6.23 C                   else
6.23 C                   unlock    lplolu
     c                   endif
      *
     c     end_oppd      endsr
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     C     *INZSR        BEGSR
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     C                   Z-ADD     0             SPGE01            3 0
     C                   Z-ADD     0             SRRN01            4 0
     C                   MOVE      *BLANK        WSEQEN            1
     C                   MOVE      *BLANK        WPRETK            1
     C                   MOVE      *BLANK        WPPLOR            1
     C                   MOVE      *BLANK        WP110D            1
      *
     C                   Z-ADD     0             SPGE02            3 0
     C                   Z-ADD     0             SRRN02            4 0
      *
     C                   MOVE      *BLANK        WWOUTQ           10
      *
     C                   MOVE      *BLANK        WPIPOR            1
     C                   MOVE      *BLANK        WPFTOR            1
     C                   MOVE      *ZERO         WWVALG            1 0
     C                   MOVE      *ZERO         WXPDIR            1 0
      *
      *  Redefinering av felter
      *
     C     *LIKE         DEFINE    SRRN01        SFRN01
     C     *LIKE         DEFINE    SRRN01        SSRN01
      *
     C     *LIKE         DEFINE    SRRN02        SFRN02
     C     *LIKE         DEFINE    SRRN02        SSRN02
      *
     C     *LIKE         DEFINE    LKPONR        WXNUMM
     C     *LIKE         DEFINE    LKPSUF        WXSUFF
     C     *LIKE         DEFINE    LKPOTY        WXOTYP
      *
      *  Redefinering av parameterfeltene
      *
     C     *LIKE         DEFINE    LOFIRM        WPFIRM
     C     *LIKE         DEFINE    LONUMM        WPNUMM
     C     *LIKE         DEFINE    LOSUFF        WPSUFF
      *
      *  Redefinering av nøkler
      *
     C     *LIKE         DEFINE    VAOFIR        OTYFIR
     C     *LIKE         DEFINE    VAOTYP        OTYTYP
      *
     C     *LIKE         DEFINE    LOFIRM        HEDFIR
     C     *LIKE         DEFINE    LONUMM        HEDNUM
     C     *LIKE         DEFINE    LOSUFF        HEDSUF
      *
     C     *LIKE         DEFINE    LDFIRM        LINFIR
     C     *LIKE         DEFINE    LDNUMM        LINNUM
     C     *LIKE         DEFINE    LDSUFF        LINSUF
     C     *LIKE         DEFINE    LDLINE        LINLIN
      *
     C     *LIKE         DEFINE    LKPFIR        PLOFIR
     C     *LIKE         DEFINE    LKPONR        PLONUM
     C     *LIKE         DEFINE    LKPSUF        PLOSUF
      *
     C     *LIKE         DEFINE    LOFIRM        POTFIR
     C     *LIKE         DEFINE    LOOTYP        POTOTY
     C     *LIKE         DEFINE    LONUMM        POTNUM
     C     *LIKE         DEFINE    LOSUFF        POTSUF
      *
     C     *LIKE         DEFINE    LKLFIR        PLLFIR
     C     *LIKE         DEFINE    LKLONR        PLLNUM
     C     *LIKE         DEFINE    LKLSUF        PLLSUF
     C     *LIKE         DEFINE    LKLLIN        PLLLIN
      *
6.20  *  Key for les av BRUKER BESTILLING/LAGER
6.20  *
6.20 c     lusrl1_key    klist
6.20 c                   kfld                    lusrl1_firm
6.20 c                   kfld                    lusrl1_user
      *
      *  Key for file : ORDRETYPE-REGISTER
      *
     C     OTYKEY        KLIST
     C                   KFLD                    OTYFIR
     C                   KFLD                    OTYTYP
      *
      *  Key for file : BESTILLING-HODE-REGISTER
      *
     C     HEDKEY        KLIST
     C                   KFLD                    HEDFIR
     C                   KFLD                    HEDNUM
     C                   KFLD                    HEDSUF
      *
      *  Key for file : BESTILLING-LINJE-REGISTER
      *
     C     LINKEY        KLIST
     C                   KFLD                    LINFIR
     C                   KFLD                    LINNUM
     C                   KFLD                    LINSUF
     C                   KFLD                    LINLIN
      *
      *  Key for file : UTPLUKK-PARAM-REGISTER
      *
     C     PLOKEY        KLIST
     C                   KFLD                    PLOFIR
     C                   KFLD                    PLONUM
     C                   KFLD                    PLOSUF
      *
      *  Key for file : UTPLUKK-PARAM-ORDRETYPE-REGISTER
      *
     C     POTKEY        KLIST
     C                   KFLD                    POTFIR
     C                   KFLD                    POTOTY
     C                   KFLD                    POTNUM
     C                   KFLD                    POTSUF
      *
      *  Key for file : UTPLUKK-LINJE-REGISTER
      *
     C     PLLKEY        KLIST
     C                   KFLD                    PLLFIR
     C                   KFLD                    PLLNUM
     C                   KFLD                    PLLSUF
     C                   KFLD                    PLLLIN
      *
      * Key for les STATUS
      *
     c     lstsl1_key    klist
     c                   kfld                    wpfirm
      *
      * Key for les av bestillingshode
      *
     c     lohel1_key    klist
     c                   kfld                    wpfirm
     c                   kfld                    lohel1_numm
     c                   kfld                    lohel1_suff
      *
      * Key for les/oppd. av utplukksfil/hode
      *
     c     lplolu_key    klist
     c                   kfld                    wpfirm
     c                   kfld                    lplolu_numm
     c                   kfld                    lplolu_suff
7.01  *
7.01  * Key for les på ordretype - sideregister
7.01  *
7.01 c     vot1l1_key    klist
7.01 c                   kfld                    wpfirm
7.01 c                   kfld                    vot1l1_otyp
      *
     C/EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     C     *ENTRY        PLIST
     C                   PARM                    WPPLOR
     C                   PARM                    WPFIRM
     C                   PARM                    WPNUMM
     C                   PARM                    WPSUFF
      *
      *  Legger inn firma i nøkklene
      *
     C                   MOVE      WPFIRM        OTYFIR
     C                   MOVE      WPFIRM        HEDFIR
     C                   MOVE      WPFIRM        LINFIR
     C                   MOVE      WPFIRM        PLOFIR
     C                   MOVE      WPFIRM        POTFIR
     C                   MOVE      WPFIRM        PLLFIR
     c                   eval      p_firm = wpfirm
6.20 c                   eval      lusrl1_user = l_user
6.20 c                   eval      lusrl1_firm = l_firm
      *
      *  Nullstiller felter som alltid skal være blanke/null
      *
     C                   MOVE      *ZERO         PLONUM
     C                   MOVE      *ZERO         PLOSUF
     C                   MOVE      *ZERO         POTNUM
     C                   MOVE      *ZERO         POTSUF
     C                   MOVE      *ZERO         PLLNUM
     C                   MOVE      *ZERO         PLLSUF
     C                   MOVE      *ZERO         PLLLIN
      *
     C                   MOVE      *ZERO         WXNUMM
     C                   MOVE      *ZERO         WXSUFF
     C                   MOVE      *BLANK        WXOTYP
      *
      * Hent opplysninger fra LAGER-STATUS-KODER
      *
     c     lstsl1_key    chain     lstsl1r                            90
      *
      *  Fyller opp skjermbildet for første visning
      *
     C     PLOKEY        SETLL     LPLOL1
     C                   EXSR      XCRT01
      *
     C                   ENDSR
