     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RH110R
      * Beskrivelse . . : Hovedboksposter, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       260302 KOB  Nytt program
T5.00 * R5.00 050803 HFL  Ikke mulig å endre bilagstekst
T5.40 * R5.40 050805 HFL  Notatark på postnivå.
      * V5.50 100607 KOB  Lagt inn konkatenering av lev.fakturanr og bilagstekst
      *                   og visning av lev.fakturanr i detaljbildet.
      * V5.50 100607 KOB  Fjernet visning av investeringsavgift  - flyttet felt
6.10  * V6.10 210909 BSA  Lagt inn sporingsfelter
6.20  * V6.20 021210 NHO  Prosjekt er nå alfa
7.XX  * V7.00 020120 BKV  Tilpasset Cloud
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frhtrl1    if   e           k disk    rename(rhtrpfr:rhtrl1r)
     frhtrl2    if   e           k disk    rename(rhtrpfr:rhtrl2r)
     frhtrl3    if   e           k disk    rename(rhtrpfr:rhtrl3r)
     frhtrl4    if   e           k disk    rename(rhtrpfr:rhtrl4r)
     frhtrlu    uf a e           k disk    rename(rhtrpfr:rhtrlur)
      *
T5.40frklal2    if   e           k disk    rename(rklapfr:rklal2r)
T5.40frklal3    if   e           k disk    rename(rklapfr:rklal3r)
     frh110d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d rhtrl1_kont     s                   like(rbkont)
     d rhtrl1_avde     s                   like(rbavde)
     d rhtrl1_spes     s                   like(rbspes)
     d rhtrl1_rper     s                   like(rbrper)
     d rhtrl1_bdat     s                   like(rbbdat)
     d rhtrl1_tist     s                   like(rbtist)
      *
     d rhtrlu_kont     s                   like(rbkont)
     d rhtrlu_avde     s                   like(rbavde)
     d rhtrlu_spes     s                   like(rbspes)
     d rhtrlu_rper     s                   like(rbrper)
     d rhtrlu_bdat     s                   like(rbbdat)
     d rhtrlu_tist     s                   like(rbtist)
      *
     d rhtrl2_avde     s                   like(rbavde)
     d rhtrl3_spes     s                   like(rbspes)
     d rhtrl4_biln     s                   like(rbbiln)
      *
T5.40d rklal2_syst     s                   like(rxsyst)
T5.40d rklal2_pros     s                   like(rxpros)
T5.40d rklal2_akti     s                   like(rxakti)
T5.40d rklal2_kont     s                   like(rxkont)
T5.40d rklal2_spes     s                   like(rxspes)
T5.40d rklal2_avde     s                   like(rxavde)
      *
T5.40d rklal3_syst     s                   like(rxsyst)
T5.40d rklal3_pros     s                   like(rxpros)
T5.40d rklal3_akti     s                   like(rxakti)
T5.40d rklal3_kont     s                   like(rxkont)
T5.40d rklal3_spes     s                   like(rxspes)
T5.40d rklal3_avde     s                   like(rxavde)
T5.40d rklal3_tist     s                   like(rxtist)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d sbraar          s                   like(rbraar)
     d sbrper          s                   like(rbrper)
      *
     d w_seqe          s              1
     d w_sude          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(rbfirm)
     d w_raar          s                   like(rbraar)
     d w_rper          s                   like(rbrper)
      *
     d p_stat          s              1
     d p_kont          s                   like(rbkont)
     d p_avde          s                   like(rbavde)
     d p_spes          s                   like(rbspes)
     d p_akod          s                   like(rbbilk)
     d p_tx20          s                   like(rbtext)
      *
     d s_kont          s                   like(rbkont)
     d s_avde          s                   like(rbavde)
     d s_spes          s                   like(rbspes)
      *
T5.40 * Parameter - program RF020R
6.20 i/COPY QCPYLESRC,RF020PAR
T5.40d*                ds
T5.40d*rxulda                       175
T5.40d* rxstat                        1    overlay(rxulda)
T5.40d* rxfirm                        3  0 overlay(rxulda:2)
T5.40d* rxsyst                        1    overlay(rxulda:5)
 6.20d**rxpros                        6  0 overlay(rxulda:6)
 6.20d* rxpros                        6    overlay(rxulda:6)
T5.40d* rxakti                        3    overlay(rxulda:12)
T5.40d* rxf001                        3    overlay(rxulda:15)
T5.40d* rxkont                        6  0 overlay(rxulda:18)
T5.40d* rxspes                        4  0 overlay(rxulda:24)
T5.40d* rxavde                        4  0 overlay(rxulda:28)
T5.40d* rxlinj                        4  0 overlay(rxulda:32)
T5.40d* rxtgrp                        3    overlay(rxulda:36)
T5.40d* rxtext                       78    overlay(rxulda:39)
T5.40d* rxf002                       13    overlay(rxulda:117)
T5.40d* rxvalg                        1  0 overlay(rxulda:130)
T5.40d* rxtist                         z   overlay(rxulda:131)
T5.40d* rxbiln                        6  0 overlay(rxulda:158)
T5.40d* rxrper                        2  0 overlay(rxulda:164)
T5.40d* rxraar                        4  0 overlay(rxulda:166)
T5.40d* rxonr                         6  0 overlay(rxulda:170)
T5.40d rxupar          s                   like(rxulda)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(12)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * d_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C2WIN  - C2 Skjermbilde for endre/vise
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
T5.40 * Finnes notatark på kontoen?
      *
T5.40c                   eval      rklal2_syst = 'H'
6.200c                   eval      rklal2_pros = *blank
T5.40c                   eval      rklal2_akti = *blanks
T5.40c                   eval      rklal2_kont = b1kont
T5.40c                   eval      rklal2_spes = b1spes
T5.40c                   eval      rklal2_avde = b1avde
T5.40c     rklal2_key    setll     rklal2                                 94
      *
T5.40c                   if        *in94 = *on
T5.40c                   eval      C2notb = 'NOTATARK'
T5.40c                   endif
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
T5.40c                   when      *inkb
T5.40c                   move      *loval        rxtist
T5.40c                   eval      rxbiln = *zero
T5.40c                   eval      rxrper = *zero
T5.40c                   eval      rxraar = *zero
T5.40c                   eval      rxonr  = *zero
T5.40c                   exsr      notat
T5.40c                   goto      b2tagb
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Sjekk om år/periode er endret
      *
     c                   if        b2raar <> w_raar or
     c                             b2rper <> w_rper
     c                   eval      w_raar = b2raar
     c                   eval      w_rper = b2rper
     c                   exsr      forny
     c***                exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2kont <> 0 or
     c                             b2avde <> 0 or
     c                             b2spes <> 0 or
     c                             b2biln <> 0
     c                   eval      b2sumi = *zeros
     c                   eval      b2sumu = *zeros
     c                   eval      *in31 = *on
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        d_fcrn <> 0
     c     d_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
      *                                                                 .
     c                   when      w_seqe = ' '
      *                                                                 .
     c                   exsr      beregn_ib_ub
      *                                                                 .
     c                   if        w_sude = 'S'
     c     rhtrl1_key    setll     rhtrl1
     c                   else
     c     rhtrl1_keyh   setll     rhtrl1
     c                   endif
     c*
     c                   when      w_seqe = 'K'
     c                   eval      rhtrl1_kont = b1kont
     c     rhtrl1_key    setll     rhtrl1
     c*
     c                   when      w_seqe = 'A'
     c                   eval      rhtrl2_avde = b1avde
     c     rhtrl2_key    setll     rhtrl2
     c*
     c                   when      w_seqe = 'B'
     c                   eval      rhtrl3_spes = b1spes
     c     rhtrl3_key    setll     rhtrl3
     c*
     c                   when      w_seqe = 'N'
     c                   eval      rhtrl4_biln = b1biln
     c     rhtrl4_key    setll     rhtrl4
     c*
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Konto
      *
     c                   if        b2kont <> 0
     c                   eval      w_seqe = 'K'
     c                   eval      rhtrl1_kont = b2kont
     c     rhtrl1_key    setll     rhtrl1
     c                   goto      end_pos
     c                   endif
      *
      * Avdeling
      *
     c                   if        b2avde <> 0
     c                   eval      w_seqe = 'A'
     c                   eval      rhtrl2_avde = b2avde
     c     rhtrl2_key    setll     rhtrl2
     c                   goto      end_pos
     c                   endif
      *
      * Kostnadsbærer
      *
     c                   if        b2spes <> 0
     c                   eval      w_seqe = 'B'
     c                   eval      rhtrl3_spes = b2spes
     c     rhtrl3_key    setll     rhtrl3
     c                   goto      end_pos
     c                   endif
      *
      * Bilagsnummer
      *
     c                   if        b2biln <> 0
     c                   eval      w_seqe = 'N'
     c                   eval      rhtrl4_biln = b2biln
     c     rhtrl4_key    setll     rhtrl4
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2kont = 0
     c                   eval      b2avde = 0
     c                   eval      b2spes = 0
     c                   eval      b2biln = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = d_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      rhtrl1_kont = b1kont
     c                   eval      rhtrl1_spes = b1spes
     c                   eval      rhtrl1_avde = b1avde
     c                   eval      rhtrl1_rper = b1rper
     c                   eval      rhtrl1_bdat = b1bdat
     c                   eval      rhtrl1_tist = b1tist
     c                   eval      c2nyen = ' Endre'
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      rhtrl1_kont = b1kont
     c                   eval      rhtrl1_spes = b1spes
     c                   eval      rhtrl1_avde = b1avde
     c                   eval      rhtrl1_rper = b1rper
     c                   eval      rhtrl1_bdat = b1bdat
     c                   eval      rhtrl1_tist = b1tist
     c                   eval      c2nyen = ' Vis'
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis sumposter
      *
     c                   if        b1valg = 8
      *
     c                   if        b1autk = '2'
      *
     c                   exsr      hent_text
      *
     c                   call      'RH514R'
     c                   parm                    b1raar
     c                   parm                    b1kont
     c                   parm                    b1spes
     c                   parm                    b1avde
     c                   parm                    rhtext
     c                   parm                    b1biln
     c                   parm                    b1text
     c                   parm                    b1bdat
     c                   parm                    b1neto
      *
     c                   else
      *
     c                   eval      *in32 = *on
     c                   endif
      *
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c     rhtrl1_key1   chain     rhtrl1
      *
     c                   if        %found
      *
      * Hente konto-tekster
      *
     c                   exsr      hent_text
      *
     c     *dmy          move      rbbdat        c2bdat
     c                   eval      c2bilk = rbbilk
     c                   eval      c2text = rbtext
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c                   eval      w_arow = 4
     c                   eval      w_acol = 15
      *
     c     c2taga        tag
      *
T5.40 * Gult bilagsnr. hvis det finnes notatblokk på posten
      *
T5.40c                   eval      rklal3_syst = 'H'
6.20 c                   eval      rklal3_pros = *blank
T5.40c                   eval      rklal3_akti = *blanks
T5.40c                   eval      rklal3_kont = b1kont
T5.40c                   eval      rklal3_spes = b1spes
T5.40c                   eval      rklal3_avde = b1avde
T5.40c                   eval      rklal3_tist = b1tist
T5.40c     rklal3_key    setll     rklal3                                 73
      *
     c                   exfmt     c2win
T5.40c                   eval      *in73 = *off
      *
T5.40 * Notatblokk bilag
T5.40c                   if        *inkb
T5.40c                   eval      rxtist = rbtist
T5.40c                   eval      rxbiln = rbbiln
T5.40c                   eval      rxrper = rbrper
T5.40c                   eval      rxraar = rbraar
T5.40c                   eval      rxonr  = *zero
T5.40c                   exsr      notat
T5.40c                   goto      end_c2bld
T5.40c                   endif
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
      * Call program for bilagskoder
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'C2BILK'
     c                   call      'RA501R'
     c                   parm                    w_firm
     c                   parm                    p_akod
     c                   parm                    p_tx20
      *                                                                 .
     c                   eval      c2bilk = p_akod
     c                   eval      c2text = p_tx20
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c                   eval      p_akod = c2bilk
      *
     c                   call      'RS701R'
     c                   parm                    p_stat
     c                   parm                    p_akod
     c                   parm                    p_tx20
      *                                                                 .
     c                   if        p_stat = 'N'
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   else
     c                   if        c2text = *blank
     c                   eval      c2text = p_tx20
     c                   goto      c2taga
     c                   endif
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   rhtrlur
      *
     c                   eval      rhtrlu_kont = b1kont
     c                   eval      rhtrlu_spes = b1spes
     c                   eval      rhtrlu_avde = b1avde
     c                   eval      rhtrlu_rper = b1rper
     c                   eval      rhtrlu_bdat = b1bdat
     c                   eval      rhtrlu_tist = b1tist
     c     rhtrlu_key    chain     rhtrlur
      *
     c                   eval      rbbilk = c2bilk
     c                   eval      rbtext = c2text
      *
     c                   if        %found
6.10 c                   time                    rbedat
6.10 c                   time                    rbetim
6.10 c                   eval      rbeusr = l_user
     c                   update    rhtrlur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1bilk = rbbilk
     c                   eval      b1text = rbtext
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
      *
      * Les posteringer i kontorekkefølge (Opprinnelig konto)
      *
     c                   when      w_seqe = ' '
      *
     c                   if        w_sude = 'S'
     c     rhtrl1_key    reade     rhtrl1                                 15
     c                   else
     c     rhtrl1_keyh   reade     rhtrl1                                 15
     c                   endif
      *
     c                   when      w_seqe = 'K'
     c     rhtrl1_key    reade     rhtrl1
      *
     c                   when      w_seqe = 'A'
     c     rhtrl2_key    reade     rhtrl2
      *
     c                   when      w_seqe = 'B'
     c     rhtrl3_key    reade     rhtrl3
      *
     c                   when      w_seqe = 'N'
     c     rhtrl4_key    reade     rhtrl4
      *
     c                   endsl
      *
     c                   if        %eof or
     c                             rbfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
      * Hvis periode oppgitt - ta bare med periodens poster
      *
     c                   if        w_rper <> 0 and
     c                             rbrper <> w_rper
     c                   goto      crt_neste
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1kont = rbkont
     c                   eval      b1avde = rbavde
     c                   eval      b1spes = rbspes
     c                   eval      b1biln = rbbiln
     c     *dmy          move      rbbdat        b1bida
T5.00c                   eval      b1bdat = rbbdat
     c                   eval      b1bilk = rbbilk
      *
     c                   if        rblfak <> *blank
     c                   eval      b1text = %trim(rblfak)+'/'+%trim(rbtext)
     c                   else
     c                   eval      b1text = rbtext
     c                   endif
     c                   eval      b1mvak = rbmvak
     c                   eval      b1neto = rbneto
     c                   eval      b1raar = rbraar
     c                   eval      b1rper = rbrper
     c                   eval      b1tist = rbtist
     c                   eval      b1autk = rbautk
      *
T5.40 * Gult bilagsnr. hvis det finnes notatblokk på posten
      *
T5.40c                   eval      rklal3_syst = 'H'
6.20 c                   eval      rklal3_pros = *blank
T5.40c                   eval      rklal3_akti = *blanks
T5.40c                   eval      rklal3_kont = b1kont
T5.40c                   eval      rklal3_spes = b1spes
T5.40c                   eval      rklal3_avde = b1avde
T5.40c                   eval      rklal3_tist = b1tist
T5.40c     rklal3_key    setll     rklal3                                 73
      *
     c                   write     b1sfl
T5.40c                   eval      *in73 = *off
      *
     c     crt_neste     tag
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = ' '
     c                   if        w_sude = 'S'
     c     rhtrl1_key    setgt     rhtrl1
     c                   else
     c     rhtrl1_keyh   setgt     rhtrl1
     c                   endif
      *                                                                 .
     c                   when      w_seqe = 'K'
     c     rhtrl1_key    setgt     rhtrl1
     c                   readpe    rhtrl1
     c                   when      w_seqe = 'A'
     c     rhtrl2_key    setgt     rhtrl2
     c                   readpe    rhtrl2
     c                   when      w_seqe = 'B'
     c     rhtrl3_key    setgt     rhtrl3
     c                   readpe    rhtrl3
     c                   when      w_seqe = 'N'
     c     rhtrl4_key    setgt     rhtrl4
     c                   readpe    rhtrl4
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = ' '
     c                   if        w_sude = 'S'
     c     rhtrl1_key    readpe    rhtrl1
     c                   else
     c     rhtrl1_keyh   readpe    rhtrl1
     c                   endif
     c                   when      w_seqe = 'K'
     c     rhtrl1_key    readpe    rhtrl1
     c                   when      w_seqe = 'A'
     c     rhtrl2_key    readpe    rhtrl2
     c                   when      w_seqe = 'B'
     c     rhtrl3_key    readpe    rhtrl3
     c                   when      w_seqe = 'N'
     c     rhtrl4_key    readpe    rhtrl4
     c                   endsl
      *
     c                   if        %eof or
     c                             rbfirm <> w_firm
     c                   leave
     c                   endif
      *
      * Hvis periode oppgitt - ta bare med periodens poster
      *
     c                   if        w_rper <> 0 and
     c                             rbrper <> w_rper
     c                   goto      bck_neste
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
      *                                                                 .
     c                   when      w_seqe = ' '
     c                   eval      rhtrl1_kont = rbkont
      *                                                                 .
     c                   when      w_seqe = 'K'
     c                   eval      rhtrl1_kont = rbkont
      *                                                                 .
     c                   when      w_seqe = 'A'
     c                   eval      rhtrl2_avde = rbavde
      *                                                                 .
     c                   when      w_seqe = 'B'
     c                   eval      rhtrl3_spes = rbspes
      *                                                                 .
     c                   when      w_seqe = 'N'
     c                   eval      rhtrl4_biln = rbbiln
      *                                                                 .
     c                   endsl
      *
     c     bck_neste     tag
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = ' '
     c                   if        w_sude = 'S'
     c     rhtrl1_key    setll     rhtrl1
     c                   else
     c     rhtrl1_keyh   setll     rhtrl1
     c                   endif
     c                   when      w_seqe = 'K'
     c     rhtrl1_key    setll     rhtrl1
     c                   when      w_seqe = 'A'
     c     rhtrl2_key    setll     rhtrl2
     c                   when      w_seqe = 'B'
     c     rhtrl3_key    setll     rhtrl3
     c                   when      w_seqe = 'N'
     c     rhtrl4_key    setll     rhtrl4
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Hente inn kontonavn fra kontoplanen                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     hent_text     begsr
      *
      * Konto-spes-avd
      *
     c                   call      'RS740R'
     c                   parm                    p_stat
     c                   parm                    b1kont
     c                   parm                    b1spes
     c                   parm                    b1avde
     c                   parm                    rhtext
      *
      * Hent avdeling
      *
     c                   call      'RS707R'
     c                   parm                    p_stat
     c                   parm                    b1avde
     c                   parm                    ragtxt
      *
T5.00c                   if        p_stat = 'N'
T5.00c                   eval      ragtxt = *blank
T5.00c                   endif
      *
      * Hent kostnadsbærer
      *
     c                   call      'RS728R'
     c                   parm                    p_stat
     c                   parm                    b1spes
     c                   parm                    rebtxt
      *
     c                   if        p_stat = 'N'
     c                   eval      rebtxt = *blank
     c                   endif
      *
     c                   endsr
      *
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Beregn inngående og utgående balanse                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     beregn_ib_ub  begsr
      *
      * Les posteringer i kontorekkefølge
      *
     c                   eval      *in31  = *off
     c                   eval      b2sumi = *zeros
     c                   eval      b2sumu = *zeros
      *
     c                   if        w_seqe = ' '
      *
     c                   if        w_sude = 'S'
     c     rhtrl1_key    setll     rhtrl1                                 91
     c     rhtrl1_key    reade     rhtrl1                                 91
     c                   else
     c     rhtrl1_keyh   setll     rhtrl1                                 91
     c     rhtrl1_keyh   reade     rhtrl1                                 91
     c                   endif
      *
     c                   dow       *in91 = *off
      *
      * Beregn IB
      *
     c                   if        sbrper =  *zero
     c                             and rbrper <  01
     c                   add       rbneto        b2sumi
     c                   endif
      *
     c                   if        sbrper <> *zero
     c                             and rbrper <  sbrper
     c                   add       rbneto        b2sumi
     c                   endif
      *
      * Beregn UB
      *
     c                   if        sbrper =  *zero
     c                   add       rbneto        b2sumu
     c                   endif
      *
     c                   if        sbrper <> *zero
     c                             and rbrper <= sbrper
     c                   add       rbneto        b2sumu
     c                   endif
      *
     c                   if        w_sude = 'S'
     c     rhtrl1_key    reade     rhtrl1                                 91
     c                   else
     c     rhtrl1_keyh   reade     rhtrl1                                 91
     c                   endif
      *
     c                   enddo
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
T5.40 * Subrutine for oppkall av notatark
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     notat         begsr
      *
     c                   eval      rxfirm = w_firm
     c                   eval      rxsyst = 'H'
6.20 c                   eval      rxpros = *blank
     c                   eval      rxakti = ' '
     c                   eval      rxkont = rbkont
     c                   eval      rxspes = rbspes
     c                   eval      rxavde = rbavde
     c                   eval      rxlinj = *zeros
     c                   eval      rxtext = *blank
     c                   eval      rxvalg = *blank
     c                   move      rxulda        rxupar
      *
     c                   call      'RF020R'
     c                   parm                    rxupar
      *
     c                   move      rxupar        rxulda
      *
T5.40c                   endsr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    w_sude
     c                   parm                    w_raar
     c                   parm                    w_rper
     c                   parm                    p_kont
     c                   parm                    p_spes
     c                   parm                    p_avde
      *
      * Key for posisjonering på konto
      *
     c     rhtrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_raar
     c                   kfld                    rhtrl1_kont
      *
      * Key for posisjonering på hele konto
      *
     c     rhtrl1_keyh   klist
     c                   kfld                    w_firm
     c                   kfld                    w_raar
     c                   kfld                    rhtrl1_kont
     c                   kfld                    rhtrl1_spes
     c                   kfld                    rhtrl1_avde
      *
      *
      * Key for posisjonering på avdeling
      *
     c     rhtrl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_raar
     c                   kfld                    rhtrl2_avde
      *
      * Key for posisjonering på kostnadsbærer
      *
     c     rhtrl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_raar
     c                   kfld                    rhtrl3_spes
      *
      * Key for startpunkt bilagsnummer
      *
     c     rhtrl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_raar
     c                   kfld                    rhtrl4_biln
      *
      * Key for oppdatering av bilag
      *
     c     rhtrl1_key1   klist
     c                   kfld                    w_firm
     c                   kfld                    w_raar
     c                   kfld                    rhtrl1_kont
     c                   kfld                    rhtrl1_spes
     c                   kfld                    rhtrl1_avde
     c                   kfld                    rhtrl1_rper
     c                   kfld                    rhtrl1_bdat
     c                   kfld                    rhtrl1_tist
      *
      * Key for oppdatering av bilag
      *
     c     rhtrlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_raar
     c                   kfld                    rhtrlu_kont
     c                   kfld                    rhtrlu_spes
     c                   kfld                    rhtrlu_avde
     c                   kfld                    rhtrlu_rper
     c                   kfld                    rhtrlu_bdat
     c                   kfld                    rhtrlu_tist
      *
T5.40 * Key for notatblokk kontonivå
      *
T5.40c     rklal2_key    klist
T5.40c                   kfld                    w_firm
T5.40c                   kfld                    rklal2_syst
T5.40c                   kfld                    rklal2_pros
T5.40c                   kfld                    rklal2_akti
T5.40c                   kfld                    rklal2_kont
      *
T5.40 * Key for notatblokk postnivå
      *
T5.40c     rklal3_key    klist
T5.40c                   kfld                    w_firm
T5.40c                   kfld                    rklal3_syst
T5.40c                   kfld                    rklal3_pros
T5.40c                   kfld                    rklal3_akti
T5.40c                   kfld                    rklal3_kont
T5.40c                   kfld                    rklal3_spes
T5.40c                   kfld                    rklal3_avde
T5.40c                   kfld                    rklal3_tist
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_seqe = ' '
     c                   eval      b2raar = w_raar
     c                   eval      b2rper = w_rper
     c                   eval      sbrper = *zero
      *
     c                   eval      *in22 = *on
     c                   eval      *in31 = *off
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rhtrl1_kont = p_kont
     c                   eval      rhtrl1_spes = p_spes
     c                   eval      rhtrl1_avde = p_avde
      *
     c                   select
      *
     c                   when      w_seqe = ' '
      *
     c                   exsr      beregn_ib_ub
      *
     c                   if        w_sude = 'S'
     c     rhtrl1_key    setll     rhtrl1
     c                   else
     c     rhtrl1_keyh   setll     rhtrl1
     c                   endif
      *
     c                   when      w_seqe = 'K'
     c     rhtrl1_key    setll     rhtrl1
      *
     c                   when      w_seqe = 'A'
     c     rhtrl2_key    setll     rhtrl2
      *
     c                   when      w_seqe = 'B'
     c     rhtrl3_key    setll     rhtrl3
      *
     c                   when      w_seqe = 'N'
     c     rhtrl4_key    setll     rhtrl4
     c                   endsl
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
