     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : RY751R
      * Beskrivelse . . : Leser CHRIST-fil, og oppdaterer innkjøpspris og
      *                   oppretter påslag på varenummernivå
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       131099 HKO  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjrvapf    if   e           k disk
      *
     fjvarl8    if   e           k disk    rename(jvarpfr:jvarl8r)
     fjvdtl1    if   e           k disk    rename(jvdtpfr:jvdtl1r)
     fjvprlu    uf a e           k disk    rename(jvprpfr:jvprlur)
     fjpfalu    uf a e           k disk    rename(jpfapfr:jpfalur)
      *
      * Definering av LDA
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av recordbeskrivelse - Christ Engebretsen
      *
     d                 ds
     d d_inpu                       160
     d  d_pdat                        6  0 overlay(d_inpu:1)                    Dato fra grossist
     d  d_udat                        6  0 overlay(d_inpu:7)                    Utgått dato vare
     d  d_vare                       13    overlay(d_inpu:13)                   Artikkelnr
     d  d_evar                       13    overlay(d_inpu:26)                   Erstanings varenr
     d  d_prod                        5    overlay(d_inpu:39)                   Produsentnr
     d  d_pvar                       13    overlay(d_inpu:44)                   Prod. varenr
     d  d_eann                       13    overlay(d_inpu:57)                   EAN-nr
     d  d_hgrp                        2    overlay(d_inpu:70)                   Hovedgruppe
     d  d_ugrp                        3    overlay(d_inpu:72)                   Undergruppe
     d  d_agrp                        3    overlay(d_inpu:75)                   Artikkelgruppe
     d  d_tek1                       30    overlay(d_inpu:78)                   Benevnelse
     d  d_stor                        8    overlay(d_inpu:108)                  Størrelse
     d  d_enhe                        5    overlay(d_inpu:116)                  Enhet
     d  d_anta                        9  3 overlay(d_inpu:121)                  Antall i pakn.
     d  d_rkod                        4    overlay(d_inpu:130)                  Rabattkode
     d  d_inpr                        9  2 overlay(d_inpu:134)                  Innkjøpspris
     d  d_grpr                        9  2 overlay(d_inpu:143)                  Grunnpris
     d  d_sapr                        9  2 overlay(d_inpu:152)                  Salgspris
      *
      * Definering av variable i nøkler
      *
     d jvarl8_ldor     s                   like(jvldor)
     d jvarl8_lvar     s                   like(jvlvar)
     d jvdtl1_dvar     s                   like(jvdvar)
     d jvprlu_vare     s                   like(jxvare)
      *
      * Definering av variable
      *
     d w_firm          s              3  0
     d w_lvar          s             15
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Leser CHRIST-fil
      *
     c                   read      jrvapf                                 90
     c                   dow       *in90 = *off
      *
     c                   eval      d_inpu = jrvrad
      *
     c                   exsr      oppdater
      *
     c                   read      jrvapf                                 90
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av varerecord
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppdater      begsr
      *
     c                   if        d_inpr = 0
     c                   leavesr
     c                   endif
      *
     c                   eval      w_lvar = %trim('0' + %subst(d_vare:8:6))
      *
     c                   eval      jvarl8_ldor = 009998
     c                   eval      jvarl8_lvar = w_lvar
     c     jvarl8_key    chain     jvarl8                             91
     c                   if        *in91 = *on
     c                   leavesr
     c                   endif
      *
      * Oppdaterer vare-pris
      *
     c                   exsr      oppd_pris
      *
      * Oppretter påslag på frigitte varer med salgspris
      *
     c                   if        d_sapr = 0
     c                   leavesr
     c                   endif
      *
     c                   eval      jvdtl1_dvar = jvvare
     c     jvdtl1_key    chain     jvdtl1                             91
     c                   if        *in91 = *on or
     c                             jvdhko = 1
     c                   leavesr
     c                   endif
      *
     c                   clear                   jpfalur
      *
     c                   eval      jcfirm = w_firm
     c                   eval      jcogrp = jvogrp
     c                   eval      jchgrp = jvhgrp
     c                   eval      jcugrp = jvugrp
     c                   eval      jcldor = jvldor
     c                   eval      jcmodn = jvmodn
     c                   eval      jcvare = jvvare
     c                   eval      jckofa = 1
     c                   eval(h)   jcsafa = d_sapr / d_inpr
      *
     c                   time                    jcodat
     c                   time                    jcedat
     c                   time                    jcetim
      *
     c                   write     jpfalur
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppdatering av vare-pris
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     oppd_pris     begsr
      *
     c                   eval      jvprlu_vare = jvvare
     c     jvprlu_key    chain     jvprlu                             91
      *
     c                   eval      jxvare = jvvare
     c                   eval      jxpris = d_inpr
     c                   eval      jxldor = jvldor
     c                   eval      jxlvar = jvlvar
     c                   eval      jxgdat = *loval
     c                   eval      jxrsts = 1
      *
     c                   if        d_pdat <> 0
     c     *ymd          move      d_pdat        jxgdat
     c                   endif
      *
     c                   if        *in91 = *off
     c                   update    jvprlur
     c                   else
     c                   write     jvprlur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for oppslag på vare m/lev.varenummer
      *
     c     jvarl8_key    klist
     c                   kfld                    jvarl8_ldor
     c                   kfld                    jvarl8_lvar
      *
      * Key for oppslag på vare-detalj
      *
     c     jvdtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    jvdtl1_dvar
      *
      * Key for oppdatering av vare-pris
      *
     c     jvprlu_key    klist
     c                   kfld                    jvprlu_vare
      *
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
     c                   endsr
