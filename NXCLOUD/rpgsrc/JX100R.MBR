     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JX100R
      * Beskrivelse . . : Vare-pris, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       130696 HKO  Nytt program
      *       030197 HKO  Merker rader som opprettes
      *       150900 HKO  Skrevet om til ny standard
      *       310505 HKO  Lagt inn EAN-nr og cross docking
      *       160905 HKO  Oppdaterer søketekst
      *       111107 bof  Utvidet varepris med gammel pris
5.61  *       210109 bof  Sjekker om gyldig fra dato er fylt ut
6.10  *       260809 bof  Henter prisenhet fra vare og ikke pakn.000
6.10  *                   Fjernet ean-nr fra vedl.
6.11  *       120510 bof  Gjeninnført JXEANN, skal utgå etterhvert
6.20  *       310511 bof  Lagt på skrivebeskyttelse på utvalgte felter ihht.
6.20  *                   AVALPF
6.30  *       140714 bof  Lagt på filter, og fjernet felter som utgår
6.30  *                   Lev.varenr ligger nå på Vare-leverandør,eann på pakn.
6.30  *                   Gammel pris, kommer nå som egne poster
6.31  *       220515 bof  Vil at utgåtte skal vises når man kommer inn 1.gang
6.32  *       010916 bof  Hvis man oppretter pris, sjekk om JVLEPF er dannet
6.33  *       281018 bkv  TLS
6.34  *       250119 bkv  Hvis egen pris, mulighet til å legge inn lev.varenr.
7.xx  * 7.00  060616 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  011119 BOF  Hvis vare er utgått på prisgiver, vis i nytt felt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvprlr    if   e           k disk    rename(jvprpfr:jvprlrr)
     fjvprl1    if   e           k disk    rename(jvprpfr:jvprl1r)
     fjvprlu    uf a e           k disk    rename(jvprpfr:jvprlur)
     fjvarl1    if   e           k disk    rename(jvarpfr:jvarl1r)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
6.30 fjvlelr    if   e           k disk    rename(jvlepfr:jvlelrr)
6.20 favall1    if   e           k disk    rename(avalpfr:avall1r)
6.32 fjvlelu    uf a e           k disk    rename(jvlepfr:jvlelur)
      *
     fjx100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_vare          s                   like(jxvare)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d jvprlr_ldor     s                   like(jxldor)
6.30 d jvprlr_gdat     s                   like(jxgdat)
     d jvprlr_rsts     s                   like(jxrsts)
     d jvprl1_ldor     s                   like(jxldor)
     d jvprlu_ldor     s                   like(jxldor)
6.30 d jvprlu_gdat     s                   like(jxgdat)
     d jvprlu_rsts     s                   like(jxrsts)
     d jvarl1_vare     s                   like(jvvare)
     d jlevl1_ldor     s                   like(jlldor)
6.30 d jvlelr_lldo     s                   like(jvlldo)
6.20 d avall1_apgm     s                   like(avapgm)
6.32 d jvlelu_lvnr     s                   like(jvlvnr)
6.32 d jvlelu_lldo     s                   like(jvlldo)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_inlr          s              1    inz(*off)
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
6.30 d b_subf          s              1    inz(*off)
      *
     d w_firm          s              3  0
      *
     d w_vare          s                   like(jxvare)
     d w_ldor          s                   like(jxldor)
     d w_rsts          s                   like(jxrsts)
     d w_navn          s                   like(jlnavn)
6.30 d w_udat          s                   like(jxedat)
7.01 d w_anta          s              4  0
7.01 d w_lova          s                   like(jxedat)
      *
     d s_ldor          s                   like(jxldor)
6.30 d s_gdat          s              6  0
     d s_rsts          s                   like(jxrsts)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(10)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
7.01 C/Exec SQL
7.01 C+  Set Option DatFmt=*ISO
7.01 C/End-Exec
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
6.30 c                   when      *inka
6.30 c                   exsr      xf1win
     c                   exsr      forny
6.30 c                   goto      b2taga
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2ldor <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   eval      jvprl1_ldor = b1ldor
     c     jvprl1_key    setll     jvprl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Leverandør
      *
     c                   if        b2ldor <> 0
     c                   eval      w_seqe = *blank
     c                   eval      jvprl1_ldor = b2ldor
     c     jvprl1_key    setll     jvprl1
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2ldor = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1vare = b2vare
     c                   eval      c1ldor = b1ldor
6.30 c                   eval      c1gdat = b1gdat
     c                   eval      c1rsts = b1rsts
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
7.01 c                   exsr      farge
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1vare = b2vare
     c                   eval      k1ldor = b1ldor
6.30 c                   eval      k1gdat = b1gdat
     c                   eval      k1rsts = b1rsts
     c                   exsr      xk1win
     c                   eval      b1valg = 0
7.01 c                   exsr      farge
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1vare = b2vare
6.30 c                   eval      d1gdat = b1gdat
     c                   eval      d1ldor = b1ldor
     c                   eval      d1rsts = b1rsts
     c                   exsr      xd1win
     c                   eval      b1valg = 0
7.01 c                   exsr      farge
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1vare = b2vare
     c                   eval      c1ldor = b1ldor
6.30 c                   eval      c1gdat = b1gdat
     c                   eval      c1rsts = b1rsts
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
7.01 c                   exsr      farge
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1vare = b2vare
6.30 c     *dmy          move      w_udat        c1gdat
     c                   eval      c1ldor = b2ldor
     c                   eval      c1rsts = 1
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      c1taga
     c                   endsl
      *
     c                   if        c1ldor = 0
     c                   goto      end_c1bld
     c                   endif
6.30  *
6.30  * Sjekk om gyldig dato
6.30  *
6.30 c                   if        c1gdat =  0
6.30 c                   eval      *in32 = *on
6.30 c                   goto      c1taga
6.30 c                   endif
6.30  *
6.30 c                   if        c1gdat <> 0
6.30 c     *dmy          test(d)                 c1gdat                 32
6.30 c                   if        *in32 = *on
6.30 c                   goto      c1taga
6.30 c                   endif
6.30 c                   endif
      *
      *
      * Validering
      *
     c                   eval      w_ldor = c1ldor
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      c1taga
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      jvprlr_ldor = c1ldor
6.30 c     *dmy          move      c1gdat        jvprlr_gdat
     c                   eval      jvprlr_rsts = c1rsts
     c     jvprlr_key    chain     jvprlr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1ldor = 0
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2ldor = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
6.20  * henter evt. oversikt over skrivebeskyttelse for C2BLD
6.20 c                   exsr      skrb_C2
      *
6.34 c                   eval      *in68 = *on
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      jvprlr_ldor = c1ldor
6.30 c     *dmy          move      c1gdat        jvprlr_gdat
     c                   eval      jvprlr_rsts = c1rsts
     c     jvprlr_key    chain     jvprlr
      *
     c                   if        %found
      *
     c                   eval      c2pris = jxpris
     c                   eval      c2tdat = 0
     c                   eval      c2fbel = jxfbel
     c                   eval      c2valu = jxvalu
     c                   eval      c2crdo = jxcrdo
     c                   eval      c2eusr = jxeusr
      *
     c                   if        jxtdat <> *loval
     c     *dmy          move      jxtdat        c2tdat
     c                   endif
      *
     c     *dmy          move      jxodat        c2odat
     c     *dmy          move      jxedat        c2edat
      *
6.34 c                   if        jxrsts = 1
6.34 c                   eval      *in68 = *off
6.34 c                   endif
      *
     c                   else
     c                   eval      c2pris = 0
     c                   eval      c2tdat = 0
     c                   eval      c2fbel = 0
     c                   eval      c2valu = *blank
     c                   eval      c2crdo = 0
     c                   eval      c2odat = 0
     c                   eval      c2edat = 0
     c                   eval      c2eusr = *blank
6.34 c                   eval      *in68 = *off
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1vare = k1vare
     c                   eval      c1ldor = k1ldor
6.30 c                   eval      c1gdat = k1gdat
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
6.30  *
6.30 c                   eval      jvlelr_lldo = c1ldor
6.30 c     jvlelr_key    chain     jvlelr
6.30 c                   if        %found(jvlelr)
6.30 c                   movel     jvllva        c2lvar
6.30 c                   else
6.30 c                   eval      c2lvar = *blank
6.30 c                   endif
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Pris må oppgis
      *
     c                   if        c2pris = 0
     c                   eval      *in31 = *on
     c                   goto      c2tagb
     c                   endif
      *
      * Sjekk om gyldig dato
      *
     c                   if        c2tdat <> 0
     c     *dmy          test(d)                 c2tdat                 33
     c                   if        *in33 = *on
     c                   goto      c2tagb
     c                   endif
     c                   endif
      *
      * Oppdatering
      *
     c                   clear                   jvprlur
      *
     c                   eval      jvprlu_ldor = c1ldor
6.30 c     *dmy          move      c1gdat        jvprlu_gdat
     c                   eval      jvprlu_rsts = c1rsts
     c     jvprlu_key    chain     jvprlur
      *
6.34 c                   if        jxrsts = 1
6.34 c                   eval      jxlvar = c2lvar
6.34 c                   endif
     c                   eval      jxpris = c2pris
6.30 c*                  eval      jxgdat = *loval
     c                   eval      jxtdat = *loval
     c                   eval      jxfbel = c2fbel
     c                   eval      jxvalu = c2valu
     c                   eval      jxcrdo = c2crdo
     c                   eval      jxeusr = l_user
      *
     c                   time                    jxedat
     c                   time                    jxetim
      *
     c                   if        c2tdat <> 0
     c     *dmy          move      c2tdat        jxtdat
     c                   endif
      *
     c                   if        %found(jvprlu)
     c                   update    jvprlur
     c                   else
     c                   eval      jxvare = c1vare
     c                   eval      jxldor = c1ldor
6.30 c                   if        c1gdat <> 0
6.30 c     *dmy          move      c1gdat        jxgdat
6.30 c                   endif
6.34 c                   eval      jxlvar = c2lvar
     c                   eval      jxrsts = 1
     c                   time                    jxodat
     c                   write     jvprlur
      *
6.34 c*                   clear                   jvlelur
6.34 c*                   eval      jvlelu_lvnr = jxvare
6.34 c*                   eval      jvlelu_lldo = jxldor
6.34 c*     jvlelu_key    chain     jvlelu
6.34 c*                   if        not %found(jvlelu)
6.34 c*                   eval      jvlvnr = jxvare
6.34 c*                   eval      jvlldo = jxldor
6.34 c*                   if        b2ldor = jxldor
6.34 c*                   eval      jvlvei = 1
6.34 c*                   endif
6.34 c*                   eval      jvllva = jxlvar
6.34 c*                   time                    jvloda
6.34 c*                   time                    jvloti
6.34 c*                   eval      jvlous = l_user
6.34 c*                   time                    jvleda
6.34 c*                   time                    jvleti
6.34 c*                   eval      jvleus = l_user
6.34 c*                   write     jvlelur
6.34 c*                   endif
     c                   endif
6.34 c                   if        jxrsts = 1
6.34 c                   clear                   jvlelur
6.34 c                   eval      jvlelu_lvnr = jxvare
6.34 c                   eval      jvlelu_lldo = jxldor
6.34 c     jvlelu_key    chain     jvlelu
6.34 c                   if        not %found(jvlelu)
6.34 c                   eval      jvlvnr = jxvare
6.34 c                   eval      jvlldo = jxldor
6.34 c                   if        b2ldor = jxldor
6.34 c                   eval      jvlvei = 1
6.34 c                   endif
6.34 c                   eval      jvllva = jxlvar
6.34 c                   time                    jvloda
6.34 c                   time                    jvloti
6.34 c                   eval      jvlous = l_user
6.34 c                   time                    jvleda
6.34 c                   time                    jvleti
6.34 c                   eval      jvleus = l_user
6.34 c                   write     jvlelur
6.34 c                   else
6.34 c                   if        jxrsts = 1 and jvllva <> jxlvar
6.34 c                   eval      jvllva = jxlvar
6.34 c                   time                    jvleda
6.34 c                   time                    jvleti
6.34 c                   eval      jvleus = l_user
6.34 c                   update    jvlelur
6.34 c                   endif
6.34 c                   endif
6.34 c                   endif
      *
6.33  *
6.33  * Oppdatering av logistikkvare
6.33  *
6.33 c                   call      'JK101R'
6.33 c                   parm                    jxvare
6.33 c
      *
      * Oppdatering av søketekster
      *
     c                   call      'JV790R'
     c                   parm                    w_firm
     c                   parm                    jxvare
     c                   parm                    b_inlr
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1ldor = c1ldor
     c                   eval      b1pris = c2pris
6.30 c                   eval      b1tdat = c2tdat
     c                   eval      b1crdo = c2crdo
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      jvprlr_ldor = d1ldor
6.30 c     *dmy          move      d1gdat        jvprlr_gdat
     c                   eval      jvprlr_rsts = d1rsts
     c     jvprlr_key    chain     jvprlr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      jvprlu_ldor = d1ldor
6.30 c     *dmy          move      d1gdat        jvprlu_gdat
     c                   eval      jvprlu_rsts = d1rsts
     c     jvprlu_key    chain     jvprlur
      *
     c                   delete    jvprlur
      *
     c     end_d1win     endsr
6.30  *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  * Bilder for filter
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  *
6.30 c     xf1win        begsr
6.30  *
6.30 c     f1tagb        tag
6.30  *
6.30 c                   exfmt     f1win
6.30  *
6.30 c                   if        *inkc or *inkl
6.30 c                   leavesr
6.30 c                   endif
6.30  *
6.30  * Spørring
6.30  *
6.30 c                   if        *inkd
6.30  *
6.30 c                   if        w_afld = 'F1LDOR'
6.30 c                   call      'JL500R'
6.30 c                   parm                    f1ldor
6.30 c                   parm                    f1lnav
6.30 c                   goto      f1tagb
6.30 c                   endif
6.30  *
6.30 c                   endif
6.30  *
6.30  * Validering
6.30  *
6.30 c                   if        f1ldor <> 0
6.30 c                   eval      jlevl1_ldor = f1ldor
6.30 c     jlevl1_key    chain     jlevl1
6.30 c                   if        not %found(jlevl1)
6.30 c                   eval      *in33 = *on
6.30 c                   goto      f1tagb
6.30 c                   endif
6.30 c                   eval      f1lnav = jlnavn
6.30 c                   endif
6.30  *
6.30 c*    end_filter    tag
6.30  *
6.30 c*                  eval      *in22 = *off
6.30  *
6.30 c*                  eval      w_seqe = *blank
6.30  *
6.30 c*    jvprl1_key    setll     jvprl1
6.30  *
6.30 c*                  exsr      clr_subfile
6.30 c*                  exsr      crt_subfile
6.30  *
6.30 c                   endsr
6.30  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_ldor = k1ldor
     c                   eval      s_gdat = k1gdat
     c                   eval      s_rsts = k1rsts
      *
      * Fyller kopierings-bildet
      *
     c                   eval      jvprlr_ldor = k1ldor
6.30 c                   if        k1gdat <> 0
6.30 c     *dmy          move      k1gdat        jvprlr_gdat
6.30 c                   else
6.30 c                   eval      jvprlr_gdat = *loval
6.30 c                   endif
     c                   eval      jvprlr_rsts = k1rsts
     c     jvprlr_key    chain     jvprlr
      *
     c     k1tagb        tag
6.30  *
6.30 c                   eval      k2gdat = k1gdat
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd = *on
     c                   exsr      spØrring
     c                   goto      k1tagb
     c                   endif
      *
      * Sjekker input
      *
     c                   eval      w_ldor = k1ldor
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      jvprlr_ldor = k1ldor
6.30 c                   if        k1gdat <> 0
6.30 c     *dmy          move      k1gdat        jvprlr_gdat
6.30 c                   else
6.30 c                   eval      jvprlr_gdat = *loval
6.30 c                   endif
     c                   eval      jvprlr_rsts = 1
     c     jvprlr_key    chain     jvprlr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1ldor = s_ldor
     c                   eval      c1rsts = s_rsts
6.30 c                   eval      c1gdat = s_gdat
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      jvprl1                                 15
      *
     c                   if        *in15
     c                   leave
     c                   endif
      *
     c                   if        jxvare <> w_vare
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
6.30 c                   exsr      chk_subfile
6.30 c                   if        b_subf = *off
6.30 c                   iter
6.30 c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      jvprl1_ldor = jxldor
      *
     c                   eval      b1ldor = jxldor
     c                   eval      b1pris = jxpris
     c                   eval      b1gdat = 0
     c                   eval      b1crdo = jxcrdo
     c                   eval      b1rsts = jxrsts
      *
     c                   if        jxgdat <> *loval
     c     *dmy          move      jxgdat        b1gdat
     c                   endif
6.30  *
6.30 c                   if        jxtdat <> *loval
6.30 c     *dmy          move      jxtdat        b1tdat
6.30 c                   else
6.30 c                   eval      b1tdat = 0
6.30 c                   endif
      *
      * Henter leverandørnavn
      *
     c                   eval      jlevl1_ldor = jxldor
     c     jlevl1_key    chain     jlevl1
     c                   if        %found
     c                   eval      b1lnav = jlnavn
     c                   else
     c                   eval      b1lnav = *blank
     c                   endif
      *
      * Merker rader som er opprettet lokalt
      *
     c                   if        jxrsts = 1
     c                   eval      b1rkod = 'L'
     c                   else
     c                   eval      b1rkod = *blank
     c                   endif
      *
7.01 c                   exsr      farge
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     jvprl1_key    setgt     jvprl1
     c                   readp     jvprl1
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     jvprl1
      *
     c                   if        %eof or
     c                             jxvare <> w_vare
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      jvprl1_ldor = jxldor
      *
     c                   enddo
      *
     c                   if        %eof
     c     jvprl1_key    setll     jvprl1
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
     c                   if        w_afld = 'B2LDOR'
     c                   call      'JL500R'
     c                   parm                    b2ldor
     c                   parm                    w_navn
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1LDOR'
     c                   call      'JL500R'
     c                   parm                    c1ldor
     c                   parm                    w_navn
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1LDOR'
     c                   call      'JL500R'
     c                   parm                    k1ldor
     c                   parm                    w_navn
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker inntastede verdier, og henter beskrivelser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
      * Sjekk nivå
      *
     c                   if        w_ldor = 0
     c                   eval      *in31 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Leverandør
      *
     c                   eval      jlevl1_ldor = w_ldor
     c     jlevl1_key    chain     jlevl1                             35
     c                   if        *in35 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
     c     end_sjekk     endsr
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  * Subrutine for lage oversikt over felter som skal skrivebeskyttes
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     skrb_c2       begsr
6.20 c                   eval      *in60  = *off
6.20 c                   eval      *in61  = *off
6.20 c                   eval      *in62  = *off
6.20 c                   eval      *in63  = *off
6.20 c                   eval      *in64  = *off
6.20 c                   eval      *in65  = *off
6.20 c                   eval      *in66  = *off
6.20 c                   eval      *in67  = *off
6.20  *
6.20 c                   if        b2vare >= '90000000'
6.20 c                   leavesr
6.20 c                   endif
6.20  *
6.20  *
6.20  * Firmastyrt validering av utvalgte felter
6.20  *
6.20  *
6.20 c                   eval      avall1_apgm = 'JX100R'
6.20 c     avall1_key    setll     avall1
6.20 c     avall1_key    reade     avall1
6.20 c                   dow       not %eof(avall1)
6.20 c                   if        avaflt = 'JXPRIS'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in60  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JXGDAT'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in61  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JXTDAT'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in62  = *on
6.20 c                   endif
6.30 c*                  if        avaflt = 'JXGPRI'  and
6.30 c*                            avabes = 1
6.30 c*                  eval      *in63  = *on
6.30 c*                  endif
6.30 c*                  if        avaflt = 'JXLVAR'  and
6.30 c*                            avabes = 1
6.30 c*                  eval      *in64  = *on
6.30 c*                  endif
6.20 c                   if        avaflt = 'JXFBEL'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in65  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JXVALU'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in66  = *on
6.20 c                   endif
6.20 c                   if        avaflt = 'JXCRDO'  and
6.20 c                             avabes = 1
6.20 c                   eval      *in67  = *on
6.20 c                   endif
6.20 c     avall1_key    reade     avall1
6.20 c                   enddo
6.20  *
6.20 c                   endsr
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  * Subrutine for å sjekke om rad skal være med i subfile
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  *
6.30 c     chk_subfile   begsr
6.30  *
6.30 c                   eval      b_subf = *off
6.30  *
6.30  * sjekk om lev.nr er ihht. filter
6.30  *
6.30 c                   if        f1ldor <> 0  and
6.30 c                             f1ldor <> jxldor
6.30 c                   leavesr
6.30 c                   endif
6.30  *
6.30  * sjekk om pris er utgått
6.30  *
6.30 c                   if        f1utgp = 0   and
6.30 c                             jxtdat <> *loval  and
6.30 c                             jxtdat <  w_udat
6.30 c                   leavesr
6.30 c                   endif
6.30  *
6.30 c                   eval      b_subf = *on
6.30  *
6.30 c                   endsr
     c
      *
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Subrutine for å finne evt. farge på linje
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01 c     farge         begsr
7.01  *
7.01  * sjekk om alle prisgivere er utgått på varen
7.01 c                   eval      w_anta = 0
7.01 c                   eval      *in50 = *off
7.01 c/exec sql
7.01 c+ select count(*) into :w_anta
7.01 c+          from jvlepf  where
7.01 c+               jvlvnr = :b2vare and
7.01 c+               jvlldo = :b1ldor and
7.01 c+               jvluda <> :w_lova and
7.01 c+               jvluda <  :w_udat
7.01 c/end-exec
7.01 c                   if        w_anta > 0
7.01 c                   eval      *in50 = *on
7.01 c                   endif
7.01 c                   endsr
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_vare
      *
      * Key for les på vare-pris
      *
     c     jvprl1_key    klist
     c                   kfld                    w_vare
     c                   kfld                    jvprl1_ldor
      *
      * Key for oppslag/les på vare-pris
      *
     c     jvprlr_key    klist
     c                   kfld                    w_vare
     c                   kfld                    jvprlr_ldor
6.30 c                   kfld                    jvprlr_gdat
     c                   kfld                    jvprlr_rsts
      *
      * Key for oppdatering av vare-pris
      *
     c     jvprlu_key    klist
     c                   kfld                    w_vare
     c                   kfld                    jvprlu_ldor
6.30 c                   kfld                    jvprlu_gdat
     c                   kfld                    jvprlu_rsts
      *
      * Key for oppslag på vare
      *
     c     jvarl1_key    klist
     c                   kfld                    jvarl1_vare
      *
      * Key for oppslag på leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
6.30  *
6.30  * Key for les på vare-leverandør
6.30  *
6.30 c     jvlelr_key    klist
6.30 c                   kfld                    w_vare
6.30 c                   kfld                    jvlelr_lldo
6.20  *
6.20  * Key for file : Firmastyrt validering
6.20  *
6.20 c     avall1_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    avall1_apgm
6.32  *
6.32  * Key for oppdatering av vare-leverandør
6.32  *
6.32 c     jvlelu_key    klist
6.32 c                   kfld                    jvlelu_lvnr
6.32 c                   kfld                    jvlelu_lldo
      *
      *
      * Henter vare fra parameter
      *
     c                   eval      w_vare = p_vare
      *
      * Henter firma fra LDA
      *
     c                   eval      w_firm = l_firm
      *
      * Henter vareinformasjon, leverandørnavn or prisenhet
      *
     c                   eval      jvarl1_vare = p_vare
     c     jvarl1_key    chain     jvarl1
     c                   if        %found
     c                   eval      b2vare = jvvare
     c                   eval      b2tek1 = jvtek1
     c                   eval      b2numm = jvldor
     c                   eval      jlevl1_ldor = jvldor
     c     jlevl1_key    chain     jlevl1
     c                   if        %found
     c                   eval      b2lnav = jlnavn
     c                   endif
     c                   endif
      *
6.10 c                   eval      b2enhe = jvpenh
6.30 c                   time                    w_udat
      *
6.31  * Vil at utgåtte priser skal vises default
6.31  *
6.31 c                   eval      f1utgp = 1
7.01 c                   eval      w_lova = *loval
      *
      * Posisjonerer i databasen, og oppretter subfile
      *
     c                   eval      *in22 = *on
      *
     c     jvprl1_key    setll     jvprl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
