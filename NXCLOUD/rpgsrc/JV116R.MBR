     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASVADM
      * Program . . . . : JV116R
      * Beskrivelse . . : Vedlikehold av Oppsalg /Mersalg (BM info)
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       080114 bof  Nytt program
      *       010414 bof  Nytt ønske, vil at alle BM felter skal synes her.
      *       010414 bof  All info skal foreløbig være i vis-modus *in30 brukes
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fjvarl3    if   e           k disk    rename(jvarpfr:jvarl3r)
     fjvoml1    if   e           k disk    rename(jvompfr:jvoml1r)
     fjvomlu    uf a e           k disk    rename(jvompfr:jvomlur)
     fjlevl1    if   e           k disk    rename(jlevpfr:jlevl1r)
     fjkatl1    if   e           k disk    rename(jkatpfr:jkatl1r)
      *
     fJV116d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_onob          s                   like(jvonob)
     d p_valg          s              1  0
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_fnav                951    980
      *
      * Definering av variable i nøkler
      *
     d jvarl3_nobb     s                   like(jvnobb)
     d jvoml1_onob     s                   like(jvonob)
     d jvoml1_otyp     s                   like(jvotyp)
     d jvomlu_onob     s                   like(jvonob)
     d jvomlu_otyp     s                   like(jvotyp)
     d jvarl1_vare     s                   like(jvvare)
     d jkatl1_kate     s                   like(jkkate)
     d jlevl1_ldor     s                   like(jlldor)
      *
      * Definering av variable
      *
     d b_feil          s              1    inz(*off)
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c                   eval      *in30 = *on
      *
     c                   exsr      xc2bld
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for bilde 1
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
      * blanker info
     c                   eval      c2sort = *blank
     c                   eval      c2sldo = 0
     c                   eval      c2slna = *blank
     c                   eval      c2utko = *blank
     c                   eval      c2bran = *blank
     c                   eval      c2knav = *blank
     c                   movel     jvsort        c2sort
     c                   eval      c2sldo = jvsldo
     c                   eval      jlevl1_ldor = jvsldo
     c     jlevl1_key    chain     jlevl1
     c                   if        %found(jlevl1)
     c                   eval      c2slna = jlnavn
     c                   endif
      *
     c                   select
     c                   when      jvutko = '1'
     c                   eval      c2utko = 'Utstilt'
     c                   when      jvutko = '2'
     c                   eval      c2utko = 'Utstilt og lagerført'
     c                   when      jvutko = '0'
     c                   eval      c2utko = *blank
     c                   other
     c                   eval      c2utko = *blank
     c                   endsl
     c
     c                   eval      c2bran = jvbran
     c                   eval      jkatl1_kate = jvkate
     c     jkatl1_key    chain     jkatl1
     c                   if        %found(jkatl1)
     c                   eval      c2knav = jknavn
     c                   endif
     c
      *
      * Oppsalg
     c                   eval      jvoml1_onob = p_onob
     c                   eval      jvoml1_otyp = 'O'
     c     jvoml1_key    chain     jvoml1
     c                   if        %found(jvoml1)
      *
     c                   eval      c2opp1 = %subst(jvotx1:1:50)
     c                   eval      c2opp2 = %subst(jvotx1:51:50)
      *
     c     *dmy          move      jvooda        c2odat
     c     *dmy          move      jvoeda        c2edat
      *
     c                   else
      *
     c                   eval      c2opp1 = *blank
     c                   eval      c2opp2 = *blank
     c                   eval      c2odat = 0
     c                   eval      c2edat = 0
      *
     c                   endif
      *
     c                   eval      jvoml1_onob = p_onob
     c                   eval      jvoml1_otyp = 'M'
     c     jvoml1_key    chain     jvoml1
     c                   if        %found(jvoml1)
      *
     c                   eval      c2mer1 = %subst(jvotx1:1:50)
     c                   eval      c2mer2 = %subst(jvotx1:51:50)
      *
     c                   else
      *
     c                   eval      c2mer1 = *blank
     c                   eval      c2mer2 = *blank
      *
     c                   endif
      *
     c     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc = *on
     c                   goto      avslutt
     c                   endsl
      *
      * Oppdater register
      *
      * Oppsalg
     c                   eval      jvomlu_onob = p_onob
     c                   eval      jvomlu_otyp = 'O'
     c     jvomlu_key    chain     jvomlu
     c                   eval      jvotx1 = c2opp1 + c2opp2
     c                   time                    jvoeda
     c                   time                    jvoeti
     c                   eval      jvoeus = l_user
     c                   if        %found(jvomlu)
     c                   update    jvomlur
     c                   else
     c                   eval      jvonob = p_onob
     c                   eval      jvotyp = 'O'
     c                   time                    jvooda
     c                   time                    jvooti
     c                   eval      jvoous = l_user
     c                   write     jvomlur
     c                   endif
      *
      * Mersalg
     c                   eval      jvomlu_onob = p_onob
     c                   eval      jvomlu_otyp = 'M'
     c     jvomlu_key    chain     jvomlu
     c                   eval      jvotx1 = c2mer1 + c2mer2
     c                   time                    jvoeda
     c                   time                    jvoeti
     c                   eval      jvoeus = l_user
     c                   if        %found(jvomlu)
     c                   update    jvomlur
     c                   else
     c                   eval      jvonob = p_onob
     c                   eval      jvotyp = 'M'
     c                   time                    jvooda
     c                   time                    jvooti
     c                   eval      jvoous = l_user
     c                   write     jvomlur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_onob
     c                   parm                    p_valg
      *
      * Key for oppslag på vare oppsalg /mersalg
      *
     c     jvoml1_key    klist
     c                   kfld                    jvoml1_onob
     c                   kfld                    jvoml1_otyp
      *
      * Key for oppdat  på vare oppsalg / mersalg
      *
     c     jvomlu_key    klist
     c                   kfld                    jvomlu_onob
     c                   kfld                    jvomlu_otyp
      *
      * Key for oppslag på vare
      *
     c     jvarl3_key    klist
     c                   kfld                    jvarl3_nobb
      *
      * Key for oppslag på kategori ansvarlig
      *
     c     jkatl1_key    klist
     c                   kfld                    jkatl1_kate
      *
      * Key for oppslag på VA leverandør
      *
     c     jlevl1_key    klist
     c                   kfld                    jlevl1_ldor
      *
      *
     c                   eval      jvarl3_nobb = p_onob
     c     jvarl3_key    chain     jvarl3
      *
     c                   eval      c2nobb = p_onob
     c                   eval      c2vare = jvvare
     c                   movel     jvtek1        c2tek1
      *
     c                   endsr
