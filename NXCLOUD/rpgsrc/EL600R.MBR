     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * System. . . . . : ASOFAK                                              *
      * Program . . . . : EL600R                                              *
      * Beskrivelse . . : Utskrift av bonusgrunnlag - parameter               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      *K0000      050224  Nytt program                                    BSA *
      *                                                                       *
      *                                                                       *
      *                                                                       *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fvbotl1    if   e           k disk    rename(vbotpfr:vbotl1r)
     fel600d    cf   e             workstn
      *
     d waar            s              4  0
     d wmnd            s              2  0
     d wpakod          s              2  0
     d wpdat1          s              6  0
      *
     d p_kund          s              6  0
     d p_in03          s              1
     d p_stat          s              1
     d p_kode          s              4  0
     d p_tx30          s             30
      *
     d w_tbot          s                   like(vatbot)
     d w_tbes          s                   like(vatbes)
     d w_firm          s              3  0
     d w_pkda          s              6  0
     d wsavli          s              2  0
     d wsavpo          s              3  0
     d var             s             40    dim(2) ctdata perrcd(1)               Varsel m/svarkrav
      *
     d vbotl1_tbot     s                   like(vatbot)
      *
     d                uds
     d paarf                 300    303  0                                      Pr. år
     d paart                 304    307  0                                      Pr. år
     d palt1                 308    308  0                                      Pr. år
     d plnrf                 316    321  0                                      Lev. f.o.m.
     d plnrt                 322    327  0                                      Lev. t.o.m.
     d ptbot                 328    329                                         Bonustype
      *
     d  l_user               911    920
     d  l_firm               944    946  0
      *
      /eject
      *
     c     dummy         tag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av felter                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * A2BLD  - A2 Bilde for valg av parametre
      *
      /space 3
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A2BLD Behandle bilde for valg av utskriftskriteria              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     a2taga        tag
      *
      * Bare data
      *
     c     a2tagb        tag
      *
     c                   exfmt     a2bld
      *
     c                   eval      *in31 = *off
     c                   eval      *in32 = *off
     c                   eval      *in33 = *off
     c                   eval      *in35 = *off
     c                   eval      *in36 = *off
     c                   eval      *in40 = *off
     c                   eval      *in41 = *off
      *
      * Gå ut
      *
     c                   if        *inkc = *on                                  .....
     c                   move      '1'           p_in03                             .
     c                   goto      xslutt                                           .
     c                   endif                                                  .....
      *
      * Spørring
      *
     c                   if        *inkd = *on
     c                   exsr      spr_rut
     c                   goto      a2taga
     c                   endif
      *
      * Kontroll av angitt år
      * Varsel hvis år større enn *year
      *
     c                   if        a2brar > *year                                        .....
     c                   movel     var(1)        m1mld1
     c                   movel     var(2)        m1mld2
     c                   move      'N'           m1svar
     c                   exsr      xm1win
     c                   if        m1svar = 'N'
     c                   move      *on           *in32                          Pos.cur.     .
     c                   goto      a2taga                                                    .
     c                   endif
     c                   endif                                                           .....
      *
      *  Kontroll av leverandørnummer fra - til
      *
     c                   if        a2plnf > a2plnt                                       .....
     c                   eval      *in40 = *on
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      * Sjekke om angitt bonustype er gyldig
      *
     c                   if        a2bonu <> *blank                                      .....
     c                   eval      vbotl1_tbot = a2bonu                                      .
     c     vbotl1_key    chain     vbotl1
     c                   if        not %found
     c                   eval      *in32 = *on
     c                   goto      a2taga
     c                   endif                                                           .....
     c                   endif                                                           .....
      *
      * Sjekke om alternativ for rapport
      *
     c                   if        a2alt1 <> '0' and
     c                             a2alt1 <> '1'
     c                   eval      *in34 = *on
     c                   goto      a2taga
     c                   endif                                                           .....
      *
      * Skriv parameter
      *
     c                   move      a2brar        paarf
     c                   move      a2brar        paart
     c                   move      a2plnf        plnrf
     c                   move      a2plnt        plnrt
     c                   move      a2bonu        ptbot
     c                   move      a2alt1        palt1
      *
      /space 3
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
     c                   eval      *inlr = *on
     c                   return
      /eject
      *----------------------------------------------------------------------
      * Subrutine for forespørsel                                           *
      *----------------------------------------------------------------------
     c     spr_rut       begsr
      *
      * Call leverandørnummer fra
      *
     c                   if        w_afld = 'A2PLNF'
     c                   eval      p_kund = a2plnf
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    p_kund
     c                   parm                    p_tx30
      *
     c                   eval      a2plnf = p_kund
     c                   eval      *in57 = *on
     c                   goto      end_spr_rut
     c                   endif
      *
      * Call leverandørnummer til
      *
     c                   if        w_afld = 'A2PLNT'
     c                   eval      p_kund = a2plnt
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    p_kund
     c                   parm                    p_tx30
      *
     c                   eval      a2plnt = p_kund
     c                   eval      *in58 = *on
     c                   goto      end_spr_rut
     c                   endif
      *
      * Spørre på bonustype
      *
     c                   if        w_afld = 'A2BONU'
     c                   eval      w_tbot = *blank
     c                   eval      w_tbes = *blank
     c                   call      'VA565R'
     c                   parm                    w_firm
     c                   parm                    w_tbot
     c                   parm                    w_tbes
      *
     c                   if        w_tbot <> *blank
     c                   eval      a2bonu = w_tbot
     c                   goto      end_spr_rut
     c                   endif
     c                   endif
      *
     c     end_spr_rut   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * M1WIN Behandle bilde for melding med svar                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xm1win        begsr
      *
     c                   eval      wsavli = 6
     c                   eval      wsavpo = 22
     c                   eval      w_arow = wsavli
     c                   eval      w_acol = wsavpo
      *
     c                   exfmt     m1win
      *
     c                   if        *inkc = *on
     c                   move      'N'           m1svar
     c                   endif
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_in03
      *
     c                   eval      wpakod = *zero
     c                   eval      wpdat1 = 0
      *
      * Key for sjekk av bonustype
      *
     c     vbotl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vbotl1_tbot
      *
      * Legge inn firmanummer og navn fra parameter
      *
     c                   move      l_firm        w_firm
     c                   eval      a2plnf = *zeros
     c                   eval      a2plnt = 999999
      *
     c                   eval      p_in03 = ' '
      *
     c                   endsr
**
Valgt periode er neste år.
Bekreft dersom dette er riktig.
