     h datedit(*dmy) option(*nodebugio)
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RH673R                                              *
      * Beskrivelse . . : Hovedbok - MVA-spesifikasjon - utskrift             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                                     *
      * Tilpasset for . :                                                     *
      *                                                                       *
      * Referanse  Dato   Endringsbeskrivelse                            Sign *
      * --------- ------  ---------------------------------------------- ---- *
      *  V5.40    080307  Nytt program                                   HFL  *
      *  V5.50    231007  Skrev ikke mva-tekst på 1. linje.              HFL  *
      *                                                                       *
      * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
      *                                                                       *
      *  BRUK AV INDIKATORER                                                  *
      *                                                                       *
      *       KA = F1  = Valg av firma                                        *
      *       KC = F3  = Exit                                                 *
      *                                                                       *
      *       LR = Avslutt program                                            *
      *       30 = Protect av felter ved vise                                 *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer                   *
      *    60-69 = Fileindikatorer chain etc.                                 *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                           *
      *    80-89 = Arbeidsindikatorer ordinære                                *
      *    90-99 = Benyttes ved std. sub-rutiner                              *
      *                                                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     frhtrla    if   e           k disk    rename(rhtrpfr:rhtrlar)
     frhovlr    if   e           k disk    rename(rhovpfr:rhovlrr)
     fra05lr    if   e           k disk    rename(ra05pfr:ra05lrr)
     frh673p    o    e             printer oflind(*in30)
      *
     d rhtrla_raar     s                   like(rbraar)
     d rhtrla_mvak     s                   like(rbmvak)
     d rhtrla_kont     s                   like(rbkont)
     d rhtrla_pmva     s                   like(rbpmva)
     d rhtrla_bdat     s                   like(rbbdat)
      *
     d rhovlr_kont     s                   like(rhkont)
     d rhovlr_spes     s                   like(rhspes)
     d rhovlr_avde     s                   like(rhavde)
      *
     d ra05lr_ekod     s                   like(raekod)
      *
     d s_mvak          s                   like(rbmvak)
     d w_firm          s                   like(l_firm)
     d x               s              2  0
     d amva            s              1
      *
      * Array/subarray for akkumulering av beløp pr. mvakode
      *
     d                 ds
     d mvainfo                             dim(99)
     d mvk                            1    overlay(mvainfo : *next)
     d txt                           30    overlay(mvainfo : *next)
     d grl                           13  2 overlay(mvainfo : *next)
     d mva                           11  2 overlay(mvainfo : *next)
      *
     d                 ds
     d w_brudd1                      11
     d   rbkont                       5  0 overlay(w_brudd1)
     d   rbmvak                       1    overlay(w_brudd1:6)
     d   rbpmva                       5  2 overlay(w_brudd1:7)
     d   w_klas                       1  0 overlay(w_brudd1:2)
      *
     d                 ds
     d g_brudd1                      11
     d   g_kont                       5  0 overlay(g_brudd1)
     d   g_mvak                       1    overlay(g_brudd1:6)
     d   g_pmva                       5  2 overlay(g_brudd1:7)
     d   g_klas                       1  0 overlay(g_brudd1:2)
      *
      * Local data area
      *
     d                uds
     d rhpkda                300    305  0
     d rhprÅr                         4  0
     d rhpmva                         1  0
     d rhpmv0                         1
     d rhpsps                         1
      *
     d l_wsid                901    910
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      *
      /eject
     c     dummy         tag
      *
      * Sett starverdi
      *
     c                   eval      rhtrla_raar = rhprÅr
     c     rhtrla_key    setll     rhtrla
     c     rhtrla_key    reade     rhtrla                                 60
      *
     c                   dow       *in60 = *off
      *
      * Sett mva-kode = 0 hvis blank
      *
     c                   if        rbmvak = ' '
     c                   eval      rbmvak = '0'
     c                   endif
      *
      * Brudd på MVA-kode
      *
     c*                  if        rbmvak <> s_mvak and
     c*                            s_mvak <> *blank
E5.50c                   if        rbmvak <> s_mvak
     c                   exsr      bruddmva
     c                   endif
      *
     c                   move      rbmvak        s_mvak
      *
      * Kun posteringer med denne mva-periode
      * blir behandlet videre
      *
     c                   if        rbmvak = '9' and
     c                             rbautk = '2'
     c                   goto      neste
     c                   endif
      *
     c                   if        rbraar  = rhprÅr and
     c                             rbmvap  = rhpmva
     c                   exsr      beh_post
     c                   endif
      *
     c     neste         tag
     c     rhtrla_key    reade     rhtrla                                 60
     c                   enddo
      *
      /space 3
      *
     c     slutt         tag
     c                   exsr      sumd1det
     c                   eval      t1klas = g_klas
     c                   exsr      sumklas
     c                   exsr      sumtot
      *
     c                   exsr      mvatot
      *
     c                   seton                                        lr
      *
      /eject
      *
      * Subrutine for behandling av posteringer
      *
     c     beh_post      begsr
      *
     c                   if        rhpmv0 <> 'J' and
     c                             rbmvak = '0'
     c                   goto      ikke0
     c                   endif
      *
      * Brudd på MVA-kode/konto/%
      *
     c                   if        w_brudd1 <> g_brudd1 and
     c                             g_kont   <> *zeros
     c                   exsr      sumd1det
     c                   endif
      *
      * Brudd på kontoklasse
      *
     c                   if        w_klas   > g_klas and
     c                             g_klas  <> *zero
     c                   eval      t1klas = g_klas
     c                   exsr      sumklas
     c                   endif
      *
     c                   move      rbmvak        g_mvak
     c                   move      rbkont        g_kont
     c                   move      rbpmva        g_pmva
      *
     c                   eval      g_klas = w_klas
      *
      * Bilagsdato
      *
     c     *dmy          move      rbbdat        wbbdat
      *
      * Koder uten grunnlag, f.eks. 0 og 4
      *
     c                   if        rbmvag = *zero
     c                   eval      rbmvag = rbneto
     c                   endif
      *
     c                   eval      w1kont = rbkont
     c                   eval      w1mvak = rbmvak
     c                   eval      w1pmva = rbpmva
      *
      * Sum mva-grunnlag
      *
     c                   eval      w1mvag = w1mvag + rbmvag
     c                   eval      t1mvag = t1mvag + rbmvag
     c                   eval      t2mvag = t2mvag + rbmvag
      *
      * Sum mva-beløp
      *
     c                   eval      w1mvab = w1mvab + rbmvab
     c                   eval      t1mvab = t1mvab + rbmvab
     c                   eval      t2mvab = t2mvab + rbmvab
      *
      * Akkumulerer mva-array
      *
     c                   eval      x = 1
     c     rbmvak        lookup    mvk(x)                                 62
     c                   if        *in62 = *on
     c                   eval      grl(x) = grl(x) + rbmvag
     c                   eval      mva(x) = mva(x) + rbmvab
     c                   endif
      *
     c                   eval      waetxd = waetxt
      *
      * Skriv heading
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   eval      *in30 = *off
     c                   endif
      *
      * Skriv postering
      *
     c                   if        rhpsps = 'J'
     c                   write     d1spes
     c                   endif
      *
     c     ikke0         tag
     c                   endsr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for behandling av brudd på mva-kode                   *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     bruddmva      begsr
      *
     c                   if        rbmvak = ' '
     c                   move      '0'           amva
     c                   else
     c                   move      rbmvak        amva
     c                   endif
      *
      * Hent behandlingskode fra MVA-kode i koderegister
      *
     c                   move      amva          ra05lr_ekod
     c     ra05lr_key    chain     ra05lr
      *
     c                   if        %found
     c                   eval      waetxt = raetxt
     c                   else
     c                   eval      waetxt = 'MVA-tekst ugyldig'
     c                   endif
      *
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for utskrift sum pr. konto
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     sumd1det      begsr
      *
     c                   eval      rhovlr_kont = g_kont
     c                   eval      rhovlr_spes = *zeros
     c                   eval      rhovlr_avde = *zeros
     c     rhovlr_key    chain     rhovlr
     c                   if        %found
     c                   eval      w1text = rhtext
     c                   else
     c                   eval      w1text = 'Kontotekst ugyldig'
     c                   endif
      *
      * Skriv heading
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   eval      *in30 = *off
     c                   endif
      *
     c                   write     d1det
     c                   write     blankl
      *
      * Nullstill beløpsfelt
      *
     c                   eval      w1mvag = *zeros
     c                   eval      w1mvab = *zeros
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for utskrift sum pr. kontoklasse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     sumklas       begsr
      *
      * Skriv heading
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   eval      *in30 = *off
     c                   endif
      *
     c                   write     t1lin
     c                   eval      t1mvag = *zeros
     c                   eval      t1mvab = *zeros
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for utskrift sum totalt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     sumtot        begsr
      *
      * Skriv heading
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   eval      *in30 = *off
     c                   endif
      *
     c                   write     t2lin
     c                   write     blankl
     c                   eval      t2mvag = *zeros
     c                   eval      t2mvab = *zeros
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for utskrift av totalsummer pr. mva-kode              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     mvatot        begsr
      *
      * Skriv heading
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   eval      *in30 = *off
     c                   endif
      *
     c     1             do        98            x
      *
     c                   if        grl(x) <> *zeros
     c                   eval      t3mvak = mvk(x)
     c                   eval      t3etxt = txt(x)
     c                   eval      t3mvag = grl(x)
     c                   eval      t4mvag = t4mvag + grl(x)
     c                   eval      t3mvab = mva(x)
     c                   eval      t4mvab = t4mvab + mva(x)
      *
     c                   write     t3lin
     c                   endif
      *
     c                   enddo
      *
     c                   write     t4lin
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Definisjoner av arbeidsfelt
      *
     c                   eval      page = 0
     c                   eval      g_kont = *zeros
     c                   eval      g_pmva = *zeros
      *
     c                   if        rhpsps = 'J'
     c                   eval      *in63  = *on
     c                   endif
      *
      * Skriv heading
      *
     c                   write     a1hdr
      *
      * Key for posteringsregister - MVA
      *
     c     rhtrla_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhtrla_raar
     c*                  kfld                    rhtrlu_kont
     c*                  kfld                    rhtrlu_mvak
     c*                  kfld                    rhtrlu_pmva
     c*                  kfld                    rhtrlu_bdat
      *
      * Key for kontoplan
      *
     c     rhovlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhovlr_kont
     c                   kfld                    rhovlr_spes
     c                   kfld                    rhovlr_avde
      *
      * Key for oppslag i mva-kode
      *
     c     ra05lr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra05lr_ekod
      *
      * Key for les av mva-kode
      *
     c     ra05lr_les    klist
     c                   kfld                    w_firm
      *
     c                   move      l_firm        w_firm
      *
      * Danner tabell med alle mulige mva-koder
      *
     c                   eval      x = 0
     c     ra05lr_les    setll     ra05lr
     c     ra05lr_les    reade     ra05lr                                 60
      *
     c                   dow       *in60 = *off
     c                   eval      x = x + 1
     c                   eval      mvk(x) = raekod
     c                   eval      txt(x) = raetxt
     c     ra05lr_les    reade     ra05lr                                 60
     c                   enddo
      *
      * Nullstiller numeriske felt i hele arrayen
      *
     c     1             do        98            x
     c                   eval      grl(x) = *zeros
     c                   eval      mva(x) = *zeros
     c                   enddo
      *
     c                   endsr
      *
