     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RL511R
      * kundrivelse . . : Leverandørposteringer, spørring samlegiro
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       060802 KOB  Nytt program
7.xx  * 7.xx  030516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frltrl5    if   e           k disk    rename(rltrpfr:rltrl5r)
     frltrl1    if   e           k disk    rename(rltrpfr:rltrl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frltrl2    if   e           k disk    rename(rltrpfr:rltrl2r)
      *
     frl511d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d rltrl5_samb     s                   like(rosamb)
     d rltrl1_levr     s                   like(rolevr)
     d rlevl1_levr     s                   like(rolevr)
     d rltrl2_levr     s                   like(rolevr)
     d rltrl2_bdat     s                   like(robdat)
     d rltrl2_tist     s                   like(rotist)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_valg_ok       s              1    inz(*off)
      *
     d w_firm          s                   like(rofirm)
     d w_samb          s                   like(rosamb)                                      .
     d w_levr          s                   like(rolevr)                                      .
     d w_raar          s                   like(roraar)                                      .
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(13)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * d_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2samb <> 0 or
     c                             b2levr <> 0 or
     c                             b2raar <> w_raar
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        d_fcrn <> 0
     c     d_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      rltrl1_levr = b1levr
     c     rltrl1_key    setll     rltrl1
     c                   other
     c                   eval      rltrl5_samb = b1samb
     c     rltrl5_key    setll     rltrl5
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      *
      * Posisjoner startverdi, regnskapsår endret
      *
     c                   if        b2raar <> w_raar                                          .
     c                   eval      b2samb = w_samb                                           .
     c                   eval      b2levr = w_levr                                           .
     c                   eval      w_raar = b2raar                                           .
      *
     c                   if        b2samb = 0 and
     c                             b2levr = 0
     c                   eval      b2samb = 1
     c                   endif
      *                                                                 .
     c                   endif                                                           .....
      *
      * Samlegiro
      *
     c                   if        b2samb <> 0
     c                   eval      w_seqe = *blank
     c                   eval      w_samb = b2samb
     c                   eval      w_levr = 0
     c                   eval      rltrl5_samb = b2samb
     c     rltrl5_key    setll     rltrl5
     c                   goto      end_pos
     c                   endif
      *
      * Leverandørnummer
      *
     c                   if        b2levr <> 0
     c                   eval      w_seqe = 'B'
     c                   eval      w_levr = b2levr
     c                   eval      w_samb = 0
     c                   eval      rltrl1_levr = b2levr
     c     rltrl1_key    setll     rltrl1
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2samb = 0
     c                   eval      b2levr = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = d_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   exsr      xc2win                                                  . .
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   read      rltrl1
     c                   other
     c                   read      rltrl5
     c                   endsl
      *
     c                   if        %eof or
     c                             rofirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *                                                                 .
     c                   if        b2raar <> *zero and                                 ..... .
     c                             b2raar <> roraar
     c                   iter                                                              . .
     c                   endif                                                         ..... .
      *
     c                   if        rosamb = *zero                                      ..... .
     c                   iter                                                              . .
     c                   endif                                                         ..... .
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      rltrl1_levr = rolevr
     c                   other
     c                   eval      rltrl5_samb = rosamb
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1bdat = robdat
     c                   eval      b1tist = rotist
     c                   eval      b1samb = rosamb
     c                   eval      b1biln = robiln
     c     *dmy          move      robdat        b1bida
     c                   eval      b1bilk = robilk
     c                   eval      b1text = rotext
     c                   eval      b1belØ = robelØ
     c                   eval      b1levr = rolevr
      *                                                                 .
      * Hent leverandør                                                 .
      *                                                                 .
     c                   eval      rlevl1_levr = rolevr                                      .
     c     rlevl1_key    chain     rlevl1                             61                     .
     c                   if        *in61 = *off                                              .
     c                   eval      b1navn = rlnavn                                           .
     c                   endif                                                               .
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'B'
     c     rltrl1_key    setgt     rltrl1
     c                   readp     rltrl1
     c                   other
     c     rltrl5_key    setgt     rltrl5
     c                   readp     rltrl5
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   readp     rltrl1
     c                   other
     c                   readp     rltrl5
     c                   endsl
      *
     c                   if        %eof or
     c                             rofirm <> w_firm
     c                   leave
     c                   endif
      *                                                                 .
     c                   if        b2raar <> *zero and                                 ..... .
     c                             b2raar <> roraar
     c                   iter                                                              . .
     c                   endif                                                         ..... .
      *
     c                   if        rosamb = *zero                                      ..... .
     c                   iter                                                              . .
     c                   endif                                                         ..... .
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'B'
     c                   eval      rltrl1_levr = rolevr
     c                   other
     c                   eval      rltrl5_samb = rosamb
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'B'
     c     rltrl1_key    setll     rltrl1
     c                   other
     c     rltrl5_key    setll     rltrl5
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * C2WIN Behandle bilde for vis                                    *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xc2win        begsr
      *
     c                   eval      rltrl2_levr = b1levr
     c                   eval      rltrl2_bdat = b1bdat
     c                   eval      rltrl2_tist = b1tist
     c     rltrl2_key    chain     rltrl2                             60
      *
     c                   move      rosamb        c2samb                                      .
     c                   move      robiln        c2biln                                      .
     c                   move      robilk        c2bilk                                      .
     c                   move      rotext        c2text                                      .
     c     *dmy          move      robdat        c2bdat                                      .
     c     *dmy          move      rofdat        c2fdat                                      .
     c                   move      robetb        c2betb                                      .
     c                   move      rorper        c2rper                                      .
     c                   move      roraar        c2raar                                      .
     c                   move      rorekl        c2rekl                                      .
     c                   move      rojour        c2jour                                      .
     c                   move      rorefn        c2refn                                      .
     c                   move      robelØ        c2belØ                                      .
     c                   move      rorest        c2rest                                      .
     c                   move      rovalk        c2valk                                      .
     c                   move      roavde        c2avde                                      .
     c                   move      rovalb        c2valb                                      .
     c                   move      rovalr        c2valr                                      .
     c                   move      roatts        c2atts                                      .
     c                   move      robetk        c2betk                                      .
     c                   move      rorrf1        c2rrf1                                      .
     c                   move      rorrf2        c2rrf2                                      .
     c                   move      rodeck        c2deck                                      .
     c                   move      rokidi        c2kidi                                      .
      *
     c     c2taga        tag
      *
     c                   exfmt     c2win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   goto      c2taga
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   endsr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering på samlefakturanummer
      *
     c     rltrl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rltrl5_samb
      *
      * Key for posisjonering på kundenummer
      *
     c     rltrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rltrl1_levr
      *
      * Key for kunderegister via kundenummer
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for lesing av postering
      *
     c     rltrl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rltrl2_levr
     c                   kfld                    rltrl2_bdat
     c                   kfld                    rltrl2_tist
      *
      * Henter parametre, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_raar = *year
     c                   eval      b2raar = *year
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rltrl5_samb = 0
     c     rltrl5_key    setll     rltrl5
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
