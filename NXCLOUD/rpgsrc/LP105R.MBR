     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASLAGR
      * Program . . . . : LP105R
      * Beskrivelse . . : Leverandør tilleggsdager, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *  6.30 010914 Bsa  Nytt program
7.xx  * 7.00  160316 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     flldglr    if   e           k disk    rename(lldgpfr:lldglrr)
     flldgl1    if   e           k disk    rename(lldgpfr:lldgl1r)
     flldglu    uf a e           k disk    rename(lldgpfr:lldglur)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
      *
     flp105d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av array
      *
     d a_meld          s             11    dim(3) ctdata perrcd(1)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d lldgl1_ldor     s                   like(lpgldo)
     d lldgl1_lage     s                   like(lpglag)
     d lldglr_ldor     s                   like(lpgldo)
     d lldglr_lage     s                   like(lpglag)
     d lldglu_ldor     s                   like(lpgldo)
     d lldglu_lage     s                   like(lpglag)
     d rlevl1_levr     s                   like(rllevr)
      *
      * Definering av variable
      *
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
     d w_spge          s              4  0
     d w_srrn          s              4  0
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(lpgfir)
     d w_ldor          s                   like(lpgldo)
     d w_lage          s              2  0
     d w_lagr          s              2
     d w_lage_alf      s              2
     d w_lnav          s                   like(rlnavn)
     d w_latx          s             20
     d w_adrs          s             30
     d w_ponr          s              4  0
     d w_pste          s             30
     d w_avde          s              4  0
      *
      * Konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * wvisrn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for å opprette ny rad
      * C1MSG  - C1 Meleingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      b2taga
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2valg = 0
     c                   if        b2ldor <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
     c                   endif
      *
      * Behandling av subfile control
      *
     c                   if        b2valg <> 0
     c                   exsr      control
     c                   if        b_feil = *on
     c                   goto      b2tagb
     c                   else
     c                   goto      b2taga
     c                   endif
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c     lldgl1_key    setll     lldgl1
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjonerer på leverandørnummer
      *
     c                   if        b2ldor <> 0
     c                   eval      w_seqe = *blank
     c                   eval      lldgl1_ldor = b2ldor
     c     lldgl1_key    setll     lldgl1
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2ldor = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behanding av subfile-control
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     control       begsr
      *
     c                   eval      b_forn = *off
     c                   eval      b_feil = *off
      *
      * Validering av nøkkel
      *
     c                   if        b2ldor = 0
     c                   eval      b_feil = *on
     c                   eval      *in31  = *on
     c                   goto      end_ctl
     c                   endif
      *
     c                   eval      lldglr_ldor = b2ldor
     c     lldglr_key    chain     lldglr                             90
     C                   if        *in90
     c                   eval      b_feil = *on
     c                   eval      *in31  = *on
     c                   goto      end_ctl
     c                   endif
      *
     c                   eval      w_lage_alf = b2lage
     c                   exsr      hent_lag
     c                   if        b_feil = *on
     c                   eval      *in32  = *on
     c                   goto      end_ctl
     c                   endif
      *
      * Endre
      *
     c                   if        b2valg = 2
     c                   eval      c1ldor = b2ldor
     c                   eval      c1lage = b2lage
     c                   eval      lldglr_ldor = c1ldor
     c                   eval      lldglr_lage = %int(c1lage)
     c                   exsr      xc2bld
     c                   eval      b_forn = *on
     c                   goto      end_ctl
     c                   endif
      *
      * Kopiere
      *
     c                   if        b2valg = 3
     c                   eval      k1ldor = b2ldor
     c                   eval      k1lage = b2lage
     c                   exsr      xk1win
     c                   goto      end_ctl
     c                   endif
      *
      * Slette
      *
     c                   if        b2valg = 4
     c                   eval      d1ldor = b2ldor
     c                   eval      d1lage = b2lage
     c                   exsr      xd1win
     c                   goto      end_ctl
     c                   endif
      *
      * Vise
      *
     C                   if        b2valg = 5
     c                   eval      c1ldor = b2ldor
     c                   eval      c1lage = b2lage
     c                   eval      lldglr_ldor = c1ldor
     c                   eval      lldglr_lage = %int(c1lage)
     c                   exsr      xc2bld
     c                   goto      end_ctl
     c                   endif
      *
     c     end_ctl       tag
      *
     c                   eval      b2valg = 0
     c                   eval      b2ldor = 0
     c                   eval      b2lage = *blank
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
      * Beholder eksisterende posisjonering i subfilen
      *
     c                   if        wcurrn > 0
     c                   eval      wvisrn = wcurrn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl                                  16
      *
     c                   if        *in16
     c                   leave
     c                   endif
      *
     c                   eval      wvisrn = w_srrn
      *
      * Endre post
      *
     c                   if        b1valg = 2
     c                   eval      c1ldor = b1ldor
     c                   eval      c1lage = b1lage
     c                   eval      lldglr_ldor = c1ldor
     c                   if        c1lage <> *blanks
     c                   eval      lldglr_lage = %int(c1lage)
     c                   else
     c                   eval      lldglr_lage = 0
     c                   endif
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopiere post
      *
     c                   if        b1valg = 3
     c                   eval      k1ldor = b1ldor
     c                   eval      k1lage = b1lage
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slette post
      *
     c                   if        b1valg = 4
     c                   eval      d1ldor = b1ldor
     c                   eval      d1lage = b1lage
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise post
      *
     c                   if        b1valg = 5
     c                   eval      c1ldor = b1ldor
     c                   eval      c1lage = b1lage
     c                   eval      lldglr_ldor = c1ldor
     c                   if        c1lage <> *blanks
     c                   eval      lldglr_lage = %int(c1lage)
     c                   else
     c                   eval      lldglr_lage = 0
     c                   endif
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      c1ldor = 0
     c                   eval      c1lnav = *blank
     c                   eval      c1lage = *blank
     c                   eval      c1lagn = *blank
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      c1taga
     c                   endsl
      *
      * Validering
      *
     c                   if        c1ldor = 0
     c                   eval      *in31  = *on
     c                   goto      c1taga
     c                   endif
      *
     c                   eval      c1lnav = *blank
     c                   eval      rlevl1_levr = c1ldor
     c     rlevl1_key    chain     rlevl1                             90
     c                   if        *in90
     c                   eval      *in31 = *on
     c                   goto      c1taga
     c                   endif
     c                   eval      c1lnav = rlnavn
      *
     c                   eval      w_lage_alf = c1lage
     c                   exsr      hent_lag
     c                   if        b_feil = *on
     c                   eval      *in32  = *on
     c                   goto      c1taga
     c                   endif
      *
      * hvis lager er utfylt sett 0 foran
      *
     c                   if        c1lage <> *blank
     c     ' ':'0'       xlate     c1lage        c1lage
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      lldglr_ldor = c1ldor
     c                   if        c1lage <> *blank
     c                   eval      lldglr_lage = %int(c1lage)
     c                   else
     c                   eval      lldglr_lage = 0
     c                   endif
     c     lldglr_key    chain     lldglr                             90
     c                   if        *in90 = *off
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      lldglr_ldor = c1ldor
     c                   if        c1lage <> *blank
     c                   eval      lldglr_lage = %int(c1lage)
     c                   else
     c                   eval      lldglr_lage = 0
     c                   endif
     c                   exsr      xc2bld
      *
     c     end_c1bld     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbilde legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle skjermbilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c     xc2bld_ny     tag
      *
     c                   eval      w_lage_alf = c1lage
     c                   exsr      hent_lag
     c                   eval      c2lagn = w_latx
      *
     c     lldglr_key    chain     lldglr                             90
      *
     c                   if        *in90 = *off
     c                   eval      c2gdag = lpgdag
     c                   eval      c2eusr = lpgeus
     c     *dmy          move      lpgeda        c2edat
     c                   else
     c                   eval      c2gdag = 0
     c                   eval      c2edat = 0
     c                   eval      c2eusr = *blank
     c                   endif
      *
     c                   if        b1valg = 3 or b2valg = 3
     c                   eval      c1ldor = k1ldor
     c                   eval      c1lage = k1lage
     c                   eval      lldglr_ldor = k1ldor
     c                   if        k1lage <> *blank
     c                   eval      lldglr_lage = %int(k1lage)
     c                   else
     c                   eval      lldglr_lage = 0
     c                   endif
     c                   endif
      *
      * Henter beskrivelse av felter
      *
     c                   eval      c1lnav = *blank
      *
     c                   eval      rlevl1_levr = c1ldor
     c     rlevl1_key    chain     rlevl1                             91
     c                   if        *in91 = *off
     c                   eval      c1lnav = rlnavn
     c                   endif
      *
      * Dersom 'vise' settes protect-indikatoer
      *
     c                   if        b1valg = 5 or b2valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     C     c2tagb        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
      *
     c                   endif
      *
     c**                 eval      *in32 = *on
     c**                 goto      c2tagb
      *
      * Oppdaterer tabell
      *
     c     oppdater      tag
     c                   clear                   lldglur
      *
     c                   eval      lldglu_ldor = c1ldor
     c                   if        c1lage <> *blanks
     c                   eval      lldglu_lage = %int(c1lage)
     c                   else
     c                   eval      lldglu_lage = 0
     c                   endif
     c     lldglu_key    chain     lldglur                            90
      *
     c                   eval      lpgdag = c2gdag
     c                   eval      lpgeus = l_user
      *
     c                   time                    lpgeda
     c                   time                    lpgeti
      *
     c                   if        *in90 = *off
     c                   update    lldglur
     c                   else
     c                   eval      lpgfir = w_firm
     c                   eval      lpgldo = c1ldor
     c                   if        c1lage <> *blanks
     c                   eval      lpglag = %int(c1lage)
     c                   else
     c                   eval      lpglag = 0
     c                   endif
     c                   eval      lpgous = l_user
      *
     c                   time                    lpgoda
     c                   time                    lpgoti
     c                   write     lldglur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1ldor = lpgldo
      *
     c                   if        b1valg = 1 or b2valg = 1 or
     c                             b1valg = 3 or b2valg = 3
     c                   eval      b_forn = *on
     c                   endif
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      lldglr_ldor = d1ldor
     c                   if        d1lage <> *blanks
     c                   eval      lldglr_lage = %int(d1lage)
     c                   else
     c                   eval      lldglr_lage = 0
     c                   endif
     c     lldglr_key    chain     lldglr                             90
      *
     c                   eval      rlevl1_levr = d1ldor
     c     rlevl1_key    chain     rlevl1                             90
     c                   eval      d1lnav = rlnavn
      *
     c                   eval      w_lage_alf = d1lage
     c                   exsr      hent_lag
     c                   eval      d1lagn = w_latx
     c                   eval      d1gdag = lpgdag
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
     c                   eval      lldglu_ldor = lldglr_ldor
     c                   eval      lldglu_lage = lldglr_lage
     c     lldglu_key    chain     lldglur                            90
     c                   delete    lldglur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      w_ldor   = k1ldor
     c                   eval      w_lagr   = k1lage
     c                   eval      k2ldor   = k1ldor
     c                   eval      k2lage   = k1lage
     c                   eval      k1ldor   = 0
     c                   eval      k1lage   = *blank
      *
      * Fyller kopierings-bildet
      *
     c                   eval      lldglr_ldor = k2ldor
     c                   if        k2lage <> *blanks
     c                   eval      lldglr_lage = %int(k2lage)
     c                   else
     c                   eval      lldglr_lage = 0
     c                   endif
     c     lldglr_key    chain     lldglr                             90
      *
     c                   eval      rlevl1_levr = k2ldor
     c     rlevl1_key    chain     rlevl1                             90
     c                   eval      k2lnav = rlnavn
      *
     c                   eval      w_lage_alf = k2lage
     c                   exsr      hent_lag
     c                   eval      k2lagn = w_latx
     c                   eval      k1lnav = *blank
     c                   eval      k1lagn = *blank
      *
     c     k1tagb        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
     c                   exsr      spØrring
     c                   goto      k1tagb
     c                   endif
      *
      * Validering
      *
     c                   if        k1ldor = 0
     c                   eval      *in31 = *on
     c                   goto      k1tagb
     c                   endif
      *
     c                   eval      rlevl1_levr = k1ldor
     c     rlevl1_key    chain     rlevl1                             90
     c                   if        *in90
     c                   eval      *in31 = *on
     c                   goto      k1tagb
     c                   endif
      *
     c                   eval      w_lage_alf = k1lage
     c                   exsr      hent_lag
     c                   if        b_feil = *on
     c                   eval      *in32  = *on
     c                   goto      k1tagb
     c                   endif
      *
      * hvis lager er utfylt sett 0 foran
      *
     c                   if        k1lage <> *blank
     c     ' ':'0'       xlate     k1lage        k1lage
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      lldglr_ldor = k1ldor
     c                   if        k1lage <> *blanks
     c                   eval      lldglr_lage = %int(k1lage)
     c                   else
     c                   eval      lldglr_lage = 0
     c                   endif
     c     lldglr_key    chain     lldglr                             90
     c                   if        *in90 = *off
     c                   eval      *in33 = *on
     c                   goto      k1tagb
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1ldor = k1ldor
     c                   eval      c1lage = k1lage
     c                   eval      lldglr_ldor = w_ldor
     c                   if        w_lagr <> *blanks
     c                   eval      lldglr_lage = %int(w_lagr)
     c                   else
     c                   eval      lldglr_lage = 0
     c                   endif
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      lldgl1                                 15
      *
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
     c                   if        lpgfir <> l_firm
     c                   goto      end_crt
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1ldor = lpgldo
     c                   if        lpglag <> 0
     c                   move      lpglag        b1lage
     c                   else
     c                   eval      b1lage = *blanks
     c                   endif
      *
     c                   eval      b1lnav = *blank
     c                   eval      rlevl1_levr = b1ldor
     c     rlevl1_key    chain     rlevl1                             91
     c                   if        *in91 = *off
     c                   eval      b1lnav = rlnavn
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      wvisrn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in12 = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Leverandør
      *
     c                   if        wactfe = 'B2LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    b2ldor
     c                   parm                    w_lnav
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        wactfe = 'C1LDOR'
     c                   eval      c1lnav = *blank
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    c1ldor
     c                   parm                    c1lnav
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        wactfe = 'K1LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    k1ldor
     c                   parm                    k1lnav
     c                   goto      end_spØrr
     c                   endif
      *
      * Lager
      *
     c                   if        wactfe = 'B2LAGE'
     c                   eval      w_lage = 0
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    w_lage
     c                   parm                    c1lagn
     c                   move      w_lage        b2lage
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        wactfe = 'C1LAGE'
     c                   eval      c1lagn = *blank
     c                   eval      w_lage = 0
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    w_lage
     c                   parm                    c1lagn
     c                   move      w_lage        c1lage
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        wactfe = 'K1LAGE'
     c                   eval      k1lagn = *blank
     c                   eval      w_lage = 0
     c                   call      'RA510R'
     c                   parm                    w_firm
     c                   parm                    w_lage
     c                   parm                    k1lagn
     c                   move      w_lage        k1lage
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for henting av lagerinformasjon
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_lag      begsr
      *
     c                   eval      b_feil = *off
      *
     c                   if        w_lage_alf = *blank
     c                   eval      w_latx = 'Alle lager'
     c                   goto      end_lag
     c                   else
     c                   move      w_lage_alf    w_lage
     c                   endif
      *
     c                   call      'FÅ720R'
     c                   parm                    w_firm
     c                   parm                    w_lage
     c                   parm                    w_latx
     c                   parm                    w_adrs
     c                   parm                    w_ponr
     c                   parm                    w_pste
     c                   parm                    w_avde
     c                   parm                    b_feil
      *
     c                   if        b_feil = *on
     c                   eval      w_latx = 'Ugyldig lager'
     c                   endif
      *
     c     end_lag       endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Key for posisjonering tillleggsdager
      *
     c     lldgl1_key    klist
     C                   kfld                    w_firm
     c                   kfld                    lldgl1_ldor
     c                   kfld                    lldgl1_lage
      *
      * Key for oppslag på tilleggsdager
      *
     c     lldglr_key    klist
     C                   kfld                    w_firm
     c                   kfld                    lldglr_ldor
     c                   kfld                    lldglr_lage
      *
      * Key for oppdatering av leverandør distribusjon
      *
     c     lldglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lldglu_ldor
     c                   kfld                    lldglu_lage
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Kopierer firma til firma-variabel
      *
     c                   eval      w_firm = l_firm
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      lldgl1_ldor = 0
     c                   eval      lldgl1_lage = 0
     c     lldgl1_key    setll     lldgl1
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
**
  Ny post                  001
  Endring                  002
  Vise                     003
