     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOKON
      * Program . . . . : RL100R
      * Beskrivelse . . : Leverandører, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       150802 KOB  Nytt program
      * V5.10 100603 KOB  Endret sortering ved alfasøk fra lever. til alfa
T5.10 * V5.20 241003 KOB  Fjernet bruk av spesialtabell for alfafelt          *
T5.30 * v5.30 040205 HFL  Sletter notatblokk.
T5.30 * v5.30 030505 HFL  F11 viser foretaksnr.
T5.40 * V5.40 170605 HFL  Leser RKLAL2 i stedet for RKLALU.
T5.40 * V5.41 270605 KOB  Lagt inn mulighet for å utelukke passive
T5.40 * V5.41 101006 KOB  Lagt inn visning av bankkontonummer
T5.40 * V5.60 210308 KOB  Lagt inn utskrift og visning av arkiv
      * R5.60 191208 KOB  Korrigert posisjonering etter sletting
pdf   * R6.10     041010  Henter fra ASP arkiv. Mulighet for valg mellom BSA  *
pdf   *                   ASP og Multiarkiv gjøres i AP622C. Det finnes  BSA  *
pdf   *                   to varianter av dette programmet.              BSA  *
6.20  * R6.20     070611  Starter arkiv                                  BSA  *
6.21  * R6.20     210711  Legger ut firmanummer i skjultfelt for         bsa  *
6.21  *                   spørring mot ASP-arkiv fra Jacada              bsa  *
6.22  * R6.20     061212  Endret metode for oppslag mot arkiv            bsa  *
6.23  * R6.20     260413  Pros er ikke lenger nummerisk                  nho  *
6.23  * R6.20     051113  Ved slett av lev. sjekkes om det finnes varer  nho  *
      *                   på denne leverandøren i VVARPF
6.24  * R6.20     051113  Ved slett av lev. sjekkes om det finnes varer  nho  *
      *                   på denne leverandøren i VVVRPF
6.25  * R6.20     021213  Ved slett av lev. sjekkes om det finnes varer  nho  *
      *                   på denne leverandøren i VLAGPF
6.3q  *       260515 bof  Datoformat sql
7.xx  * 7.00  240216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  *       130812 bof  Legge inn test på nye leverandører - kun 700-serien
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frlevlr    if   e           k disk    rename(rlevpfr:rlevlrr)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     frlevl2    if   e           k disk    rename(rlevpfr:rlevl2r)
     frlevl3    if   e           k disk    rename(rlevpfr:rlevl3r)
     frlevlu    uf a e           k disk    rename(rlevpfr:rlevlur)
     frlsolu    uf   e           k disk    rename(rlsopfr:rlsolur)
E5.40frklal2    uf   e           k disk    rename(rklapfr:rklal2r)
     frltrl1    if   e           k disk    rename(rltrpfr:rltrl1r)
     fraa1l1    if   e           k disk    rename(raa1pfr:raa1l1r)
     fapospf    if   e           k disk
6.23 fvvarl5    uf   e           k disk    rename(vvarpfr:vvarl5r)
6.24 fvvprl3    if   e           k disk    rename(vvprpfr:vvprl3r)
      *
     frl100d    cf   e             workstn sfile(b1sfl:w_srrn)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d rlevlr_levr     s                   like(rllevr)
     d rlevl1_levr     s                   like(rllevr)
     d rlevl2_alfa     s                   like(rlalfa)
     d rlevl3_ponr     s                   like(rlponr)
     d rlevlu_levr     s                   like(rllevr)
     d rlsolu_slev     s                   like(rlslev)
     d rltrl1_levr     s                   like(rolevr)
     d apospf_ponr     s                   like(apponr)
E5.40d rklal2_syst     s                   like(rxsyst)
E5.40d rklal2_pros     s                   like(rxpros)
E5.40d rklal2_akti     s                   like(rxakti)
E5.40d rklal2_levr     s                   like(rxkont)
6.23 d vvarl5_ldor     s                   like(vvldor)
6.24 d vvprl3_ldor     s                   like(vpldor)
      *
      * Definering av variable
      *
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
6.25 d vlfirm          s              3  0
6.25 d w_kkod          s              6  0
6.25 d vlbldo          s              6  0
      *
     d w_seqe          s              1
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d b_open_sok      s              1    inz(*off)
      *
     d w_udat          s               d   datfmt(*iso)
     d w_valg          s              1  0
     d w_retk          s              1
     d w_4lnr          s              6  0
     d w_ste1          s             35
     d w_ste2          s             35
     d w_ste3          s             35
     d w_ste4          s             35
     d w_ste5          s             35
     d w_posi          s              3  0
     d s_stek          s             35
6.22 d w_type          s             50
6.22 d w_refn          s             50
6.23 d w_mld1          s             50
      *
     d w_firm          s                   like(rlfirm)
     d w_levr          s                   like(rllevr)
     d w_1bek          s                   like(ra1bek)
     d w_1bkk          s                   like(ra1bkk)
      *
pdf  d*w_modu          s             10    inz('MSI     ')
pdf  d w_modu          s             10    inz('PROA    ')
     d w_disp          s              1    inz('1')
     d w_tilg          s              1
      *
      * Definering av parametre MultiArcive
      *
     d w_pgrp          s              2
     d w_aara          s              4
     d w_bila          s              6
     d w_afir          s              3
      *
     d p_stat          s              1
     d p_bkod          s                   like(rlbetm)
     d p_btxt          s             20
     d p_ckod          s                   like(rlbetb)
     d p_ctxt          s             20
     d p_nkod          s                   like(rlkatg)
     d p_ntxt          s             20
      *
     d ww1bek          s                   like(ra1bek)
     d ww1bkk          s                   like(ra1bkk)
      *
      * Definering av konstanter
      *
     d lo              c                   'abcdefghijklmnopqrstuvwxyzæøåÆØÅ'
     d up              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZÆØÅÆØÅ'
     d*uh              c                   'ABCDEFGHIJKLMNOPQRSTUVWXYZ²ÔÖ²ÔÖ'
      *
     d s_levr          s                   like(rllevr)
      *
      * Eksternt program
7.00   dcl-s co402_verdi1  char(1);
7.00   dcl-s co402_verdi2  char(2048);
7.00   DCL-PR CO402R EXTPGM('CO402R');
7.00     p_filgrp            char(3)     const;
7.00     p_firm              packed(3:0) const;
7.00     p_lager             packed(2:0) const;
7.00     p_nokkel            char(50)    const;
7.00     p_verdi1            char(1);
7.00     p_verdi2            char(2048);
7.00   END-PR;
7.02 d u_701           s              1    inz(*off)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(12)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste recordnummer på siden
      * w_curn - Current recordnummer på siden
      * w_virn - Siden som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.3q C/Exec SQL
6.3q C+  Set Option DatFmt=*ISO, DlyPrp=*YES, SRTSEQ=*LANGIDUNQ, LANGID=NOR
6.3q C/End-Exec
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * F5=Vis alle/aktive
      *
     c                   if        *inke
     c                   if        *in55 = *on
     c                   eval      *in55 = *off
     c                   else
     c                   eval      *in55 = *on
     c                   endif
     c                   exsr      forny
     c                   goto      b2tagb
     c                   endif
      *
      * F8=Spørring på posteringer
      *
     c                   if        *inkh = *on
     c                   eval      w_valg = b1valg
     c                   call      'RL110R'
     c                   goto      b2tagb
     c                   endif
      *
      * F9=Gjenta søk
      *
     c                   if        *inki = *on
     c                   eval      *in22 = *on
     c                   eval      b2stek = s_stek
     c                   goto      b2tagb
     c                   endif
      *
      * F11=Bytter på å vise informasjon i subfien
      * 1. gang vises postnr./sted og telefonnr.
      * 2. gang vises gateadresse  og telefaxnr.
T5.30 * 3. gang vises postnr./sted og foretaksnr.
T5.30 * 4. gang vises postnr./sted og bankkontonr.
      *
     c                   if        *inkk = *on
      *
     c                   if        *in80 = *on
     c                   eval      *in80 = *off
T5.30c                   eval      *in81 = *on
     c                   else
T5.30c                   if        *in81 = *on
T5.30c                   eval      *in81 = *off
     c                   eval      *in82 = *on
T5.30c                   else
T5.30c                   if        *in82 = *on
T5.30c                   eval      *in82 = *off
     c                   eval      *in80 = *off
T5.30c                   else
     c                   eval      *in80 = *on
T5.30c                   endif
     c                   endif
     c                   endif
      *
     c                   exsr      forny
     c                   goto      b2taga
     c                   endif
      *
      * Søking
      *
     c                   if        b2stek <> *blank
     c                   exsr      sØk
     c                   goto      b2tagb
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2levr <> 0 or
     c                             b2alfa <> *blank or
     c                             b2ponr <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_sfrn <> 0
     c     w_sfrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'S'
     c                   eval      b_open_sok = *on
     c                   when      w_seqe = 'A'
     c                   eval      rlevl2_alfa = b1alfa
     c     rlevl2_key    setll     rlevl2
     c                   when      w_seqe = 'P'
     c                   eval      rlevl3_ponr = b1ponr
     c     rlevl3_key    setll     rlevl3
     c                   other
     c                   eval      rlevl1_levr = b1levr
     c     rlevl1_key    setll     rlevl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for søk i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sØk           begsr
      *
     c                   eval      w_seqe = 'S'
     c                   eval      b_open_sok = *on
      *
     c                   eval      s_stek = b2stek
      *
      * Utleder søketekster ut av søketekst
      *
     c                   call      'AS700R'
     c                   parm                    b2stek
     c                   parm                    w_ste1
     c                   parm                    w_ste2
     c                   parm                    w_ste3
     c                   parm                    w_ste4
     c                   parm                    w_ste5
      *
      * Blanker subfile, og fyller opp i.h.t. søk
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker søkefelt
      *
     c                   eval      b2stek = *blank
      *
     c                   endsr
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjonerer på leverandør
      *
     c                   if        b2levr <> 0
     c                   eval      w_seqe = *blank
     c                   eval      rlevl1_levr = b2levr
     c     rlevl1_key    setll     rlevl1
     c                   goto      end_pos
     c                   endif
      *
      * Posisjonerer på alfafelt
      *
     c                   if        b2alfa <> *blank
     c                   eval      w_seqe = 'A'
     c                   eval      rlevl2_alfa = b2alfa
     c     rlevl2_key    setll     rlevl2
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på postnummer
      *
     c                   if        b2ponr <> 0
     c                   eval      w_seqe = 'P'
     c                   eval      rlevl3_ponr = b2ponr
     c     rlevl3_key    setll     rlevl3
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2levr = 0
     c                   eval      b2alfa = *blank
     c                   eval      b2ponr = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   if        w_curn > 0
     c                   eval      w_virn = w_curn
     c                   endif
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1levr = b1levr
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1levr = b1levr
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1levr = b1levr
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1levr = b1levr
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Skriv kontoutdrag
      *
     c                   if        b1valg = 6
     c                   eval      *in30  = *on
     c                   exsr      skriv
     c                   eval      b1valg = *zero
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis samlegiro
      *
     c                   if        b1valg = 7
     c                   call      'RL514R'
     c                   parm                    b1levr
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vise posteringer
      *
     c                   if        b1valg = 8
     c                   eval      *in30  = *on
     c                   exsr      posteringer
     c                   eval      b1valg = *zero
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis arkiv
      *
     c                   if        b1valg = 9
      *
      * Kontrollerer gyldig tilgang
      *
     c                   call      'AX700R'
     c                   parm                    w_modu
     c                   parm                    w_disp
     c                   parm                    w_tilg
      *
     c                   if        w_tilg = '0'
      *
6.22 c**                 eval      w_type = 'ARKIV  '
6.22 c                   eval      w_type = 'INNGÅENDE%20FAKTURA'
     c                   move      w_firm        w_afir
     c                   eval      w_pgrp = '50'
     c                   eval      w_aara = '9999'
     c                   eval      w_bila = %char(b1levr)
6.22 c                   eval      w_refn = *blank
      *
pdf  c                   call      'AP622C'
pdf  c                   parm                    w_type
6.22 c                   parm                    w_refn
6.22 c*                  parm                    w_aara
6.22 c*                  parm                    w_bila
6.22 c*                  parm                    w_pgrp
      *
pdf  c**                 call      'AH720C'
pdf  c**                 parm                    w_pgrp
pdp  c**                 parm                    w_afir
pdp  c**                 parm                    w_aara
pdp  c**                 parm                    w_bila
      *
     c                   endif
      *
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
     c/eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
      * Hent sist brukte leverandørnummer
      *
     c                   eval      w_retk = ' '
     c                   call      'RS002R'
     c                   parm                    w_retk
     c                   parm                    w_firm
     c                   parm                    w_4lnr
      *
     c                   if        w_retk = *blank
     c                   eval      c1levr = w_4lnr
     c                   else
     c                   eval      *in37 = *on
     c                   goto      c1taga
     c                   endif
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Reduser sist brukte leverandørnummer
      *
     c                   select
     c                   when      *inkc or *inkl or
     c                             c1levr <> w_4lnr
     c                   eval      w_retk = 'S'
     c                   call      'RS002R'
     c                   parm                    w_retk
     c                   parm                    w_firm
     c                   parm                    w_4lnr
      *
     c                   endsl
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   endsl
      *
7.01  *
7.01  * Sjekk at den er innenfor 700-serien
7.01  *
7.01 c                   if        u_701 = *on
7.01 c                   if        c1levr < 700000 or
7.01 c                             c1levr > 799999
7.01 c                   eval      *in38 = *on
7.01 c                   goto      c1taga
7.01 c                   endif
7.01 c                   endif
      *
      * Sjekk mot grenser
      *
     c                   if        c1levr < ra1hkt
     c                   eval      *in38 = *on
     c                   goto      c1taga
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      rlevlr_levr = c1levr
     c     rlevlr_key    chain     rlevlr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      b1levr = c1levr
     c                   exsr      xc2bld
      *
     c                   eval      c1levr = 0
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2levr = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_levr = b1levr
     c                   eval      w_valg = b1valg
     c                   call      'RL101R'
     c                   parm                    w_levr
     c                   parm                    w_valg
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      rlevlr_levr = w_levr
     c     rlevlr_key    chain     rlevlr                             90
     c                   if        *in90 = *off
     c                   eval      b1navn = rlnavn
kob  c                   eval      b1alfa = rlalfa
     c                   eval      b1sted = rlsted
     c                   eval      b1tlfn = rltlfn
      *
     c                   if        *in80 = *on
     c                   eval      b1sted = rlgate
     c                   eval      b1tlfn = rltlfx
     c                   endif
      *
T5.30c                   if        *in81 = *on
T5.30c                   eval      b1tlfn = *blank
T5.30c                   if        rlftnr <> *zero
T5.30c                   movel     rlftnr        b1tlfn
T5.30c                   endif
T5.30c                   endif
     c                   endif
     c                   endif
      *
     c                   eval      b2levr = *zeros
     c                   eval      b2alfa = *blank
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
     c                   eval      *in31  = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      rlevlr_levr = d1levr
     c     rlevlr_key    chain     rlevlr
      *
     c     d1tagb        tag
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
      * Sjekk om saldo = 0
      *
     c                   if        rlsald <> 0
     c                   eval      *in32 = *on
     c                   goto      d1tagb
     c                   endif
      *
      * Sjekk om det finnes posteringer på leverandør
      *
     c                   eval      rltrl1_levr = d1levr
     c     rltrl1_key    chain     rltrl1                             91
     c                   if        *in91 = *off
     c                   eval      *in34 = *on
     c                   goto      d1tagb
     c                   endif
      *
6.23  * Sjekk om det finnes varer på leverandøren i varereg.
6.23  *
6.23 c                   eval      vvarl5_ldor = d1levr
6.23 c     vvarl5_key    setll     vvarl5
6.23 c     vvarl5_key    reade     vvarl5
6.23 c                   if        vvldor = vvarl5_ldor
6.23 c                   eval      w_mld1 = 'Leverandør kan ikke s+
6.23 c                                       lettes. Finnes i varer+
6.23 c                                       eg.      '
6.23 c                   call      'AA005R'
6.23 c                   parm                    w_mld1
6.23 c                   goto      d1tagb
6.23 c                   endif
6.23  *
6.24  * Sjekk om det leverandøren finnes i vare/pris register
6.24  *
6.24 c                   eval      vvprl3_ldor = d1levr
6.24 c     vvprl3_key    setll     vvprl3
6.24 c     vvprl3_key    reade     vvprl3
6.24 c                   if        vpldor = vvprl3_ldor
6.24 c                   eval      w_mld1 = 'Leverandør kan ikke s+
6.24 c                                       lettes. Finnes i vare/+
6.24 c                                       risreg.  '
6.24 c                   call      'AA005R'
6.24 c                   parm                    w_mld1
6.24 c                   goto      d1tagb
6.24 c                   endif
6.24  *
6.25  * Sjekker om leverandør finnes i VLAGPF
6.25 c                   eval      w_kkod = d1levr
6.25 c                   exsr      les_hist
6.25 c                   if        *in40 = '1'
6.25 c                   eval      w_mld1 = 'Leverandør kan ikke s+
6.25 c                                       lettes. Finnes i Vlagp+
6.25 c                                       f        '
6.25 c                   call      'AA005R'
6.25 c                   parm                    w_mld1
6.25 c                   goto      d1tagb
6.25 c                   eval      *in40 = '0'
6.25 c                   goto      d1tagb
6.25 c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter leverandør
      *
     c                   eval      rlevlu_levr = d1levr
     c     rlevlu_key    chain     rlevlur
      *
     c                   delete    rlevlur
      *
      * Sletter leverandør fra frisøkregister
      *
     c                   eval      rlsolu_slev = d1levr
     c     rlsolu_key    chain     rlsolu                             61
     c*
     c                   if        *in61 = *off
     c                   delete    rlsolur
     c                   endif
      *
T5.30 * Sletter notatblokk
      *
E5.40c                   eval      rklal2_syst = 'L'
6.23 c**                 eval      rklal2_pros = *zeros
6.23 c                   eval      rklal2_pros = *blanks
E5.40c                   eval      rklal2_akti = *blanks
E5.40c                   eval      rklal2_levr = d1levr
E5.40c     rklal2_key    setll     rklal2                                 94
      *
T5.30c                   if        *in94 = *on
E5.40c     rklal2_key    reade     rklal2                                 95
T5.30c                   dow       *in95 = *off
E5.40c                   delete    rklal2r
E5.40c     rklal2_key    reade     rklal2                                 95
T5.30c                   enddo
T5.30c                   endif
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Hent data fra leverandørregister
      *
     c                   eval      rlevlr_levr = k1levr
     c     rlevlr_key    chain     rlevlr
      *
      * Hent sist brukte leverandørnummer
      *
     c                   eval      w_retk = ' '
     c                   call      'RS002R'
     c                   parm                    w_retk
     c                   parm                    w_firm
     c                   parm                    w_4lnr
      *
     c                   if        w_retk = *blank
     c                   eval      k1levr = w_4lnr
     c                   else
     c                   eval      *in37 = *on
     c                   goto      k1taga
     c                   endif
      *
     c                   eval      k1alfa = rlalfa
     c                   eval      k1navn = rlnavn
     c                   eval      k1gate = rlgate
     c                   eval      k1adr2 = rladr2
     c                   eval      k1sted = rlsted
     c                   eval      k1ponr = rlponr
     c                   eval      k1tlfn = rltlfn
     c                   eval      k1tlfx = rltlfx
     c                   eval      k1betb = rlbetb
     c                   eval      k1betm = rlbetm
     c                   eval      k1katg = rlkatg
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
      * Reduser sist brukte leverandørnummer
      *
     c                   if        *inkc or *inkl
     c                   eval      w_retk = 'S'
     c                   call      'RS002R'
     c                   parm                    w_retk
     c                   parm                    w_firm
     c                   parm                    w_4lnr
      *
     c                   goto      end_k1win
     c                   endif
      *
      * Spørring på poststed
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'K1PONR'
     c                   call      'AF010R'
     c                   parm                    k1ponr
     c                   goto      k1taga
     c                   endif
      *
      * Spørring på betalingsbetingelse
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'K1BETB'
     c                   call      'RA503R'
     c                   parm                    w_firm
     c                   parm                    p_ckod
     c                   parm                    p_ctxt
      *
     c                   eval      k1betb = p_ckod
     c                   goto      k1taga
     c                   endif
      *
      * Spørring på betalingsmåte
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'K1BETM'
     c                   call      'RA502R'
     c                   parm                    w_firm
     c                   parm                    p_bkod
     c                   parm                    p_btxt
      *
     c                   eval      k1betm = p_bkod
     c                   goto      k1taga
     c                   endif
      *
      * Spørring på leverandørkategori
      *
     c                   if        *inkd = *on and
     c                             w_afld = 'K1KATG'
     c                   call      'RA514R'
     c                   parm                    w_firm
     c                   parm                    p_nkod
     c                   parm                    p_ntxt
      *
     c                   eval      k1katg = p_nkod
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   if        k1levr <> 0
     c                   eval      rlevlr_levr = k1levr
     c     rlevlr_key    chain     rlevlr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
     c                   endif
      *
      * Kontroll av skjermbildet
      *
     c                   exsr      sjekk_input
      *
     c                   if        b_feil = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kontroller leverandørnummer mot grenseverdier, hvis utfyldt i bilde
      *
     c                   if        k1levr <> 0 and
     c                             k1levr < ra1hrs
     c                   eval      *in37 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Konvertere alfafelt til store bokstaver
      *
     c                   if        k1alfa = *blank
     c                   eval      k1alfa = k1navn
     c                   endif
      *
     c     lo:up         xlate     k1alfa        k1alfa
      *
      * Konvertere navn/adresse til store bokstaver, dersom statuskode=0
      *
     c                   if        w_1bkk = 0
     c     lo:up         xlate     k1navn        k1navn
     c     lo:up         xlate     k1gate        k1gate
     c     lo:up         xlate     k1adr2        k1adr2
     c     lo:up         xlate     k1sted        k1sted
     c                   endif
      *
      * Oppdater file
      *
     c                   eval      rlevlu_levr = k1levr
     c     rlevlu_key    chain     rlevlur                            90
     c                   eval      rllevr = k1levr
     c                   eval      rlalfa = k1alfa
     c                   eval      rlnavn = k1navn
     c                   eval      rlgate = k1gate
     c                   eval      rladr2 = k1adr2
     c                   eval      rlsted = k1sted
     c                   eval      rlponr = k1ponr
     c                   eval      rltlfn = k1tlfn
     c                   eval      rltlfx = k1tlfx
      *
     c                   if        k1betb = 0
     c                   eval      rlbetb = w_1bek
     c                   else
     c                   eval      rlbetb = k1betb
     c                   endif
      *
     c                   eval      rlbetm = k1betm
     c                   eval      rlkatg = k1katg
      *
     c                   eval      rllagr = 0
     c                   eval      rldist = 0
     c                   eval      rlbgir = 0
     c                   eval      rlsald = 0
     c                   eval      rlkjØp = 0
     c                   eval      rlktid = 0
     c                   eval      rlkjØf = 0
     c                   eval      rlvalk = *blank
     c                   eval      rlsprk = *blank
     c                   eval      rlmkod = 0
     c                   eval      rllbet = 0
     c                   eval      rlform = 0
     c                   eval      rlland = *blank
     c                   eval      rlorab = 0
     c                   eval      rlsaif = 0
     c                   eval      rlfrie = *blank
     c                   eval      rlavde = 0
     c                   eval      rlpers = *blank
     c                   eval      rlftnr = 0
     c                   eval      rledik = *blank
     c                   eval      rlkomn = 0
     c                   eval      rlndat = w_udat
     c                   eval      rlnsig = l_user
     c                   eval      rledat = w_udat
     c                   eval      rlesig = l_user
      *
     c                   time                    rlntim
     c                   time                    rletim
      *
     c                   write     rlevlur
      *
     c                   call      'RL750R'
     c                   parm                    w_firm
     c                   parm                    rllevr
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1levr = s_levr
kob  c                   eval      b1levr = k1levr
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
      * Initierer SQL for søk
      *
     c                   if        w_seqe = 'S' and
     c                             b_open_sok = *on
      *
     c/exec sql
     c+                  declare sok cursor for
     c+                  select rlsfir,
     c+                         rlstek,
     c+                         rlslev
     c+                  from   rlsopf
     c+                  where  rlsfir = :w_firm and
     c+                         rlstek like :w_ste1
     c+                  order by rlsfir, rlstek
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok
     c/end-exec
     c/exec sql
     c+                  open sok
     c/end-exec
      *
     c                   endif
      *
      * Fyller opp subfile
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'S'
     c/exec sql
     c+                  fetch next
     c+                  from  sok
     c+                  into  :rlsfir,
     c+                        :rlstek,
     c+                        :rlslev
     c/end-exec
     c                   if        sqlcod = 100 or sqlstt = '02000'
     c                   eval      *in15 = *on
     c                   endif
      *
     c                   when      w_seqe = 'A'
     c                   read      rlevl2                                 15
     c                   when      w_seqe = 'P'
     c                   read      rlevl3                                 15
     c                   other
     c                   read      rlevl1                                 15
     c                   endsl
      *
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
     c                   if        w_seqe = 'S'
      *
     c                   if        w_ste2 <> *blank
      *
     c                   eval      w_posi = %scan(%trim(w_ste2):%trim(rlstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
      *
     c                   if        w_ste3 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste3):%trim(rlstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste4 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste4):%trim(rlstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   if        w_ste5 <> *blank
     c                   eval      w_posi = %scan(%trim(w_ste5):%trim(rlstek))
     c                   if        w_posi = 0
     c                   iter
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c                   eval      rlevl1_levr = rlslev
     c     rlevl1_key    chain     rlevl1                             91
     c                   if        *in91 = *on
     c                   iter
     c                   endif
      *
     c                   endif
      *
     c                   if        rlfirm <> w_firm
     c                   eval      *in15 = *on
     c                   goto      end_crt
     c                   endif
      *
      * Utelat passive leverandører
      *
     c                   if        *in55 = *on and
     c                             rlpass = 'J'
     c                   iter
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1levr = rllevr
     c                   eval      b1navn = rlnavn
     c                   eval      b1sted = rlsted
     c                   eval      b1tlfn = rltlfn
     c                   eval      b1alfa = rlalfa
     c                   eval      b1ponr = rlponr
      *
     c                   if        *in80 = *on
     c                   eval      b1sted = rlgate
     c                   eval      b1tlfn = rltlfx
     c                   endif
      *
      * Legg ut foretaksnummer
      *
T5.30c                   if        *in81 = *on
T5.30c                   eval      b1tlfn = *blank
T5.30c                   if        rlftnr <> *zero
T5.30c                   movel     rlftnr        b1tlfn
T5.30c                   endif
T5.30c                   endif
      *
      * Legg ut bankkontonummer
      *
T5.30c                   if        *in82 = *on
T5.30c                   eval      b1tlfn = *blank
T5.30c                   if        rlbgir <> *zero
T5.30c                   movel     rlbgir        b1tlfn
T5.30c                   endif
T5.30c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   eval      b_open_sok = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   select
     c                   when      w_seqe = 'A' and
     c                             %eof(rlevl2)
     c     rlevl2_key    setgt     rlevl2
     c                   readp     rlevl2
     c                   when      w_seqe = 'P' and
     c                             %eof(rlevl3)
     c     rlevl3_key    setgt     rlevl3
     c                   readp     rlevl3
     c                   when      w_seqe = *blank and
     c                             %eof(rlevl1)
     c     rlevl1_key    setgt     rlevl1
     c                   readp     rlevl1
     c                   endsl
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   readp     rlevl2
     c                   when      w_seqe = 'P'
     c                   readp     rlevl3
     c                   other
     c                   readp     rlevl1
     c                   endsl
      *
     c                   if        %eof or
     c                             rlfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'A'
     c                   eval      rlevl2_alfa = rlalfa
     c                   when      w_seqe = 'P'
     c                   eval      rlevl3_ponr = rlponr
     c                   other
     c                   eval      rlevl1_levr = rllevr
     c                   endsl
      *
     c                   enddo
      *
     c                   select
     c                   when      w_seqe = 'A' and
     c                             %eof(rlevl2)
     c     rlevl2_key    setll     rlevl2
     c                   when      w_seqe = 'P' and
     c                             %eof(rlevl3)
     c     rlevl3_key    setll     rlevl3
     c                   when      w_seqe = *blank and
     c                             %eof(rlevl1)
     c     rlevl1_key    setll     rlevl1
     c                   endsl
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekk av input ved kopiering
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
      * Sjekker postnummer
      *
     c                   if        k1ponr <> 0
     c                   eval      apospf_ponr = k1ponr
     c     apospf_key    chain     apospf                             32
     c                   if        *in32 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      k1sted = *blank
     c                   movel     apponr        k1sted
     c                   move      apsted        k1sted
     c                   endif
      *
      * Sjekke om betalingsbetingelse er gyldig
      *
     c                   if        k1betb = 0
     c                   eval      k1betb = w_1bek
     c                   endif
      *
     c                   eval      p_ckod = k1betb
     c                   call      'RS703R'
     c                   parm                    p_stat
     c                   parm                    p_ckod
     c                   parm                    p_ctxt
      *
     c                   if        p_stat = 'N'
     c                   eval      *in33 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Sjekke om betalingsmåte er gyldig
      *
     c                   if        k1betm <> 0
     c                   eval      p_bkod = k1betm
     c                   call      'RS702R'
     c                   parm                    p_stat
     c                   parm                    p_bkod
     c                   parm                    p_btxt
      *
     c                   if        p_stat = 'N'
     c                   eval      *in34 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
      * Sjekke om leverandørkategori er gyldig
      *
     c                   if        k1katg <> 0
     c                   eval      p_nkod = k1katg
     c                   call      'RS714R'
     c                   parm                    p_stat
     c                   parm                    p_nkod
     c                   parm                    p_ntxt
      *
     c                   if        p_stat = 'N'
     c                   eval      *in35 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   endif
      *
     c     end_sjekk     endsr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Vise posteringer for leverandør
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posteringer   begsr
      *
     c                   call      'RL112R'
     c                   parm                    w_valg
     c                   parm                    b1levr
      *
     c                   eval      b2levr = *zeros
     c                   eval      b2alfa = *blank
     c                   eval      *in31  = *on
     c                   endsr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Kall parameterprogram utskrift av kontoutdrag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     skriv         begsr
      *
     c                   call      'RL643C'
     c                   parm                    b1levr
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.25  ******************************************************
6.25  * Start endring, bruker sql for å danne riktig nøkkel
6.25  ******************************************************
     c     les_hist      begsr
      *
     c/exec sql
     c+                  declare sok_h cursor for
     c+                  select vlfirm,
     c+                         vlbldo
     c+                  from   vlagpf
     c+                     where vlfirm = :w_firm and
     c+                           vlbldo = :w_kkod
      *
     c+                  for read only
     c/end-exec
     c/exec sql
     c+                  close sok_h
     c/end-exec
     c/exec sql
     c+                  open sok_h
     c/end-exec
      *
     c     fetch_vare    tag
     c/exec sql
     c+                  fetch next
     c+                  from  sok_h
     c+                  into  :vlfirm,
     c+                        :vlbldo
     c/end-exec
      *
      *
      * Tester på firmakode/valgt kundekat
     c                   if        vlfirm <> l_firm or
     c                             vlbldo <> w_kkod
     c                   goto      slutt1
     c                   endif
      *
      * Gir feilmelding dersom finnes i registeret
     c                   if        vlfirm = l_firm and
     c                             vlbldo <> w_kkod
     c                   goto      fetch_vare
     c                   else
     c                   eval      *in40 = '1'
     c                   goto      slutt1
     c                   endif
      *
     c     slutt1        tag
      *
     c                   endsr
6.25  * Slutt endring
6.25  ******************************************************
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for oppslag på leverandør
      *
     c     rlevlr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevlr_levr
      *
      * Key for posisjonering på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for posisjonering på alfa
      *
     c     rlevl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl2_alfa
      *
      * Key for posisjonering på postnummer
      *
     c     rlevl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl3_ponr
      *
      * Key for oppdatering av leverandør
      *
     c     rlevlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevlu_levr
      *
      * Key for oppdatering av leverandør-søketekst
      *
     c     rlsolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlsolu_slev
      *
      * Key for oppslag på leverandørposter
      *
     c     rltrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rltrl1_levr
      *
      * Key for oppslag på statuskode 1
      *
     c     raa1l1_key    klist
     c                   kfld                    w_firm
      *
      * Key for postnummer-register
      *
     c     apospf_key    klist
     c                   kfld                    apospf_ponr
      *
T5.30 * Key for notatblokk
      *
E5.40c     rklal2_key    klist
T5.30c                   kfld                    w_firm
E5.40c                   kfld                    rklal2_syst
E5.40c                   kfld                    rklal2_pros
E5.40c                   kfld                    rklal2_akti
E5.40c                   kfld                    rklal2_levr
      *
6.23  * Key for oppslag mot varereg
6.23  *
6.23 c     vvarl5_key    klist
6.23 c                   kfld                    w_firm
6.23 c                   kfld                    vvarl5_ldor
      *
6.24  * Key for les av vare-pris pr. leverandør
6.24  *
6.24 c     vvprl3_key    klist
6.24 c                   kfld                    w_firm
6.24 c                   kfld                    vvprl3_ldor
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
6.21 c                   eval      b2firm = w_firm
7.01  *
7.01  * Endring 7.01
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'RL100R_701':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_701 = *on;
7.01   endif;
      *
      * Henter statuskode 1
      *
     c     raa1l1_key    chain     raa1l1                             90
      *
     c                   if        *in90 = *off
     c                   eval      w_1bek = ra1bek
     c                   eval      w_1bkk = ra1bkk
     c                   else
     c                   eval      w_1bek = 15
     c                   eval      w_1bkk = 0
     c                   endif
      *
      * Henter dagens dato
      *
     c                   time                    w_udat
      *
     c                   eval      *in22 = *on
     c                   eval      *in55 = *off
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rlevl1_levr = 0
     c     rlevl1_key    setll     rlevl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
