     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VD771R
      * Beskrivelse . . : Varesortiment-sjekker om vare finnes i et sortiment
      *                    av type = C
      *
      *                   LR-flagg må være lik '1' for at *inLR ikke skal
      *                   settes
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       091110 bof  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.21 fvsdtl6    if   e           k disk    rename(vsdtpfr:vsdtl6r)
6.21 fvshel3    if   e           k disk    rename(vshepfr:vshel3r)
      *
      * Definering av Local data area *LDA
      *
     d                uds
     d l_fgrp                931    933
     d l_user                911    920
      *
      * Definering av variable i nøkler
      *
6.21 d vshel3_type     s                   like(vctype)
6.21 d vsdtl6_vsor     s                   like(vdvsor)
6.21 d vsdtl6_lage     s                   like(vdlage)
6.21 d vsdtl6_vare     s                   like(vdvare)
      *
6.21 d b_sess          s              1    inz(*off)
6.21 d b_sent          s              1    inz(*off)
      *
      * Definering av variable
      *
     d p_firm          s                   like(vdfirm)
     d p_vare          s                   like(vdvare)
     d p_lage          s                   like(vdlage)
     d p_stat          s              1
     d p_inlr          s              1
      *
     d w_firm          s                   like(vdfirm)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.21 c                   eval      p_stat = *blank
      *
      *leser all sortiment med type = C   - med evt.lager
      *
     c                   eval      vshel3_type = 'C'
     c     vshel3_key    setll     vshel3
     c     vshel3_key    reade     vshel3
     c                   dow       not %eof(vshel3)
      *
     c                   if        vclage = p_lage
6.21 c                   exsr      sjekk_vsor
6.21 c                   if        b_sent = *on
6.21 c                   eval      p_stat = '1'
     c                   goto      avslutt
6.21 c                   endif
6.21 c                   endif
      *
     c     vshel3_key    reade     vshel3
     c                   enddo
      *
      *leser all sortiment med type = C   - evt. uten lager
      *
     c                   eval      vshel3_type = 'C'
     c     vshel3_key    setll     vshel3
     c     vshel3_key    reade     vshel3
     c                   dow       not %eof(vshel3)
      *
     c                   if        vclage = 0
6.21 c                   exsr      sjekk_vsor
6.21 c                   if        b_sent = *on
6.21 c                   eval      p_stat = '1'
     c                   goto      avslutt
6.21 c                   endif
6.21 c                   endif
      *
     c     vshel3_key    reade     vshel3
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   if        p_inlr <> *on
     c                   eval      *inlr = *on
     c                   endif
     c                   return
6.21  *
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  * Subrutine for å sjekke om varen er i sortiment for C - sentrallagervarer
6.21  *                           eller i et sortiment for P - periodestyrt varer
6.21  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.21  *
6.21 c     sjekk_vsor    begsr
6.21  *
6.21 c                   eval      b_sent = *off
6.21 c                   eval      vsdtl6_vsor = vcvsor
6.21 c                   eval      vsdtl6_lage = vclage
6.21 c                   eval      vsdtl6_vare = p_vare
6.21 c     vsdtl6_key    setll     vsdtl6
6.21 c     vsdtl6_key    reade     vsdtl6
6.21 c                   dow       not %eof(vsdtl6)
      *
     c                   eval      b_sent = *on
6.21  *
6.21 c     vsdtl6_key    reade     vsdtl6
6.21 c                   enddo
6.21  *
6.21 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_lage
     c                   parm                    p_vare
     c                   parm                    p_stat
     c                   parm                    p_inlr
6.21  *
6.21  * Key for les av varer fra sortiment
6.21  *
6.21 c     vsdtl6_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    vsdtl6_vsor
6.21 c                   kfld                    vsdtl6_lage
6.21 c                   kfld                    vsdtl6_vare
6.21  *
6.21  * Key for les av soritment hode pr. type
6.21  *
6.21 c     vshel3_key    klist
6.21 c                   kfld                    w_firm
6.21 c                   kfld                    vshel3_type
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
     c                   endsr
