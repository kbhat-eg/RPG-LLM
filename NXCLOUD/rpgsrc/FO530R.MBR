      /TITLE  ASOFAK Faktura, spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : FO530R
      * Beskrivelse . . : Faktura, saldokontroll
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       250414 bsa  Nytt program
6.20  *       200514 bsa  Utvidet med nøkkel på requester
6.32  * 6.30  101014 bsa  Kan ikke frigi ordre hvis det er sendt forespørsel
6.32  *                   om å ta penger.
6.33  * 6.30  301014 bsa  Rollback er uteglemt
6.nu  * 6.30  211114 bsa  Gjennomgang med tanke på utvidelse av nummerserier
6.34  * 6.30  261114 bsa  Utvidet bruk av statuser
6.35  * 6.30  120515 bsa  Setter sperrekode på nytt
6.36  * 6.30  270715 bsa  Viser ikke ordretype "t", negative ordre og negative beløp
6.37  * 6.30  030815 bsa  Skal ikke sende negative beløp til betaling.
6.38  * 6.30  180815 bsa  Skal kun sende 10 posisjoners nøkkel
6.39  * 6.30  201015 bsa  Feil ved kombinasjon kort og lager.
6.3a  * 6.30  101215 Bsa  Utvidet streng for saldosjekk med firma og avdeling
6.3b  * 6.30  171116 Bsa  Skal ikke slippe videre negative ordretyper
6.3c  * 6.30  111217 Bsa  Kan bli øreavvik i journaler ved oppdatering. Ref
6.3c  *                   eJournal sak 94262.
6.3d  * 6.30  170418 Bsa  Bruk riktig lager
6.3e  * 6.30  310518 Bsa  Må beregne mva av hver enkelt varelinje
6.3f  * 6.30  060319 Bsa  Forsøker unngå øredifferanse
7.xx  * 7.00  180216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * 7.00  170220 bkv  Endret F tast, innføre filter funksjon.
7.02  * 7.00  170220 bkv  Sender med KID ved Santander betaling.  (D21)
7.03  * 7.00  130121 bsa  Masngle F5 i skjermbilde
7.04  * 7.00  130421 bsa  Kaller FO730 for å rekalkulere ordretotal. Det kan
7.04  * 7.00              finnes ordre som ikke har riktig total. Det gir feil
7.04  * 7.00              beløp som sendes til belastning.
7.05  * 7.00  130421 bsa  Hvis ordren er "opptatt", skal den ikke vises.
7.06  * 7.00  050523 mst  Sett riktig nøkkel ved lesing på 'F1'
      *
8.01  *PRG2   200622 bof  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
6.3e ffodtlr    if   e           k disk    rename(fodtpfr:fodtlrr)
6.3e fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     ffohelr    if   e           k disk    rename(fohepfr:fohelrr)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffohelu    uf   e           k disk    rename(fohepfr:fohelur)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fruksl1    if   e           k disk    rename(rukspfr:ruksl1r)
     frukrl5    if   e           k disk    rename(rukrpfr:rukrl5r)
     frksjl1    if   e           k disk    rename(rksjpfr:rksjl1r)
     fvotyl1    if   e           k disk    rename(votypfr:votyl1r)
     frksjlu    uf a e           k disk    rename(rksjpfr:rksjlur)
      *
     ffo530d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(fofirm)
     d p_otyp          s                   like(footyp)
     d p_kund          s                   like(fokund)
     d p_numm          s                   like(fonumm)
     d p_suff          s                   like(fosuff)
     d p_fsum          s             13  2
7.02 d p_kidn          s             18
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fkey               369    369
     d  d_srow               370    370I 0
     d  d_scol               371    371I 0
     d  d_fcrn               378    379I 0
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
7.00 d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
6.3e d fodtlr_numm     s                   like(fonumm)
6.3e d fodtlr_suff     s                   like(fosuff)
6.3e d fodtlr_line     s                   like(fdline)
6.3e d vvarl1_vare     s                   like(vvvare)
     d fohelr_numm     s                   like(fonumm)
     d fohelr_suff     s                   like(fosuff)
     d fohelu_numm     s                   like(fonumm)
     d fohelu_suff     s                   like(fosuff)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d rkunl1_kund     s                   like(rkkund)
     d ruksl1_skty     s                   like(ruskty)
     d ruksl1_slag     s                   like(ruslag)
     d rksjl1_jnum     s                   like(rsjnum)
     d rksjl1_jsuf     s                   like(rsjsuf)
     d rksjlu_jnum     s                   like(rsjnum)
     d rksjlu_jsuf     s                   like(rsjsuf)
     d rukrl5_ktyp     s                   like(ruktyp)
     d rukrl5_kkun     s                   like(rukkun)
     d rukrl5_kpro     s                   like(rukpro)
     d votyl1_otyp     s                   like(vaotyp)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_feil          s              1    inz(*off)
     d b_requ          s              1    inz(*off)
     d b_trek          s              1    inz(*off)
      *
     d w_firm          s                   like(fofirm)
      *
      * Definering av parametre MultiArcive
      *
     d w_type          s             10
     d w_disp          s              1    inz('1')
     d w_tilg          s              1
     d w_pgrp          s              2
     d w_afir          s              3
     d w_fsum          s                   like(fotots)
     d w_requ          s           1000
     d w_list          s             64
     d w_osta          s              1
     d w_lpro          s              1
6.38 d w_rkey          s             10
6.20 d w_kode          s              1
6.20 d w_fell          s              6
6.20 d w_rtyp          s              2
6.20 d w_fast          s             10
6.20 d w_ruti          s             10
6.nu d w_rnum          s              8  0
6.3f d w_mvab          s             11  3
6.3f d w_mvag          s             11  3
6.3e d w_rkr1          s             11  2
6.3e d w_rkr2          s             11  2
6.3e d w_rkr3          s             11  2
6.3e d w_rkro          s             11  2
6.3e d w_stat          s              1
6.3e d w_mvak          s              1
6.3e d w_mvtx          s             30
6.3e d w_bpro          s              5  2
6.3e d w_dato          s               d   datfmt(*iso)
6.3e d w_mvap          s              6  2
6.3e d w_mvat          s              5  4
6.3e d w_mvaf          s             10  9
7.01 d w_knav          s                   like(rknavn)
7.01 d w_gtxt          s             30
7.04 d w_enor          s              1
      *
      * Definering av request struktur
      *
     d                 ds
     d d_requ                      1000
     d  d_firm                        3    overlay(d_requ:1)
     d  d_des0                        1    overlay(d_requ:4)
6.nu d  d_ordr                        8    overlay(d_requ:5)
6.nu d  d_des1                        1    overlay(d_requ:13)
6.nu d  d_suff                        2    overlay(d_requ:14)
6.nu d  d_des2                        1    overlay(d_requ:16)
6.nu d  d_belh                       11  0 overlay(d_requ:17)
6.nu d  d_skil                        1    overlay(d_requ:28)
6.nu d  d_beld                        2  2 overlay(d_requ:29)
6.nu d  d_des3                        1    overlay(d_requ:31)
6.nu d  d_kort                       11    overlay(d_requ:32)
7.02 d  d_des4                        1    overlay(d_requ:43)
7.02 d  d_kidn                       18    overlay(d_requ:44)
     d                 ds
     d d_belop                       13  2
     d  d_helt                       13  0 overlay(d_belop:1)
     d  d_desi                        2  2 overlay(d_belop:12)
      *
      * Origo integrasjon
      *
     d                 ds
     d d_list                        64
     d  d_ses0                        1    overlay(d_list:1)
     d  d_selh                       11  0 overlay(d_list:2)
     d  d_sskl                        1    overlay(d_list:13)
     d  d_seld                        2  2 overlay(d_list:14)
     d  d_ses1                        1    overlay(d_list:16)
     d  d_skrt                       11    overlay(d_list:17)
6.3a d  d_ses2                        1    overlay(d_list:28)
6.3a d  d_sfir                        3    overlay(d_list:29)
6.3a d  d_ses3                        1    overlay(d_list:32)
6.3a d  d_avde                        4    overlay(d_list:33)
6.20 d                 ds
6.38 d d_rkey                        10
6.38 d  d_fast                        2    overlay(d_rkey:1)
6.38 d  d_rnum                        8  0 overlay(d_rkey:3)
      * Eksternt program
7.00   dcl-s co402_verdi1  char(1);
7.00   dcl-s co402_verdi2  char(2048);
7.00   DCL-PR CO402R EXTPGM('CO402R');
7.00     p_filgrp            char(3)     const;
7.00     p_firm              packed(3:0) const;
7.00     p_lager             packed(2:0) const;
7.00     p_nokkel            char(50)    const;
7.00     p_verdi1            char(1);
7.00     p_verdi2            char(2048);
7.00   END-PR;
7.02 d u_702           s              1    inz(*off)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(14)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
     c                   exsr      dsp_subfile
      *
      * Behandling av standard funksjonstaster
      *
     c                   select
7.01 c                   when      *inka
7.01 c                   exsr      xf1win
7.01 c                   when      *inkm
7.01 c                   exsr      xb13win
     c                   goto      b2tagb
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
      * Trekk beløp for alle
     c                   when      *inkg
     c                   exsr      trk_alle
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Posisjonering
      *
     c                   if        b2numm <> 0 or
     c                             b2otyp <> *blank or
     c                             b2kuna <> *blank
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
     c                   if        b_requ = *on
     c                   exsr      forny
     c                   eval      b_requ = *off
     c                   endif
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Subrutine for behandling av spørring
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01 c     spØrring      begsr
7.01  *
7.01  * Kunde
7.01  *
7.01 c                   if        w_afld = 'F1KUND'
7.01 c                   call      'RK500R'
7.01 c                   parm                    w_firm
7.01 c                   parm                    f1kund
7.01 c                   parm                    w_knav
7.01 c                   leavesr
7.01 c                   endif
7.01  *
7.01  * Ordretype
7.01  *
7.01 c                   if        w_afld = 'F1OTYP'
7.01 c                   call      'VA550R'
7.01 c                   parm                    w_firm
7.01 c                   parm                    f1otyp
7.01 c                   leavesr
7.01 c                   endif
7.01  *
7.01  * Call program for avdeling
7.01  *
7.01 c                   if        w_afld = 'F1AVDE'
7.01 c                   call      'RA507R'
7.01 c                   parm                    w_firm
7.01 c                   parm                    f1avde
7.01 c                   parm                    w_gtxt
7.01 c                   leavesr
7.01 c                   endif
7.01  *
7.01 c                   endsr
7.01  *
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  * Subrutine for å vise filter
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.01  *
7.01 c     xf1win        begsr
7.01  *
7.01 c                   eval      w_xrow = d_srow
7.01 c                   eval      w_xcol = d_scol
7.01  *
7.01 c     f1taga        tag
7.01  *
7.01 c                   exfmt     f1win
7.01  *
7.01  * Behandling av funksjonstaster
7.01  *
7.01 c                   select
7.01 c                   when      *inkc or *inkl
7.01 c                   leavesr
7.01 c                   when      *inkd
7.01 c                   exsr      spØrring
7.01 c                   goto      f1taga
7.01 c                   endsl
7.01  *
7.01 c                   exsr      clr_subfile
7.01 c                   exsr      crt_subfile
7.01  *
7.01 c                   endsr
7.01  *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å vise statuskoder
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
7.01 c     xb13win       begsr
      *
     c                   eval      w_xrow = d_srow
     c                   eval      w_xcol = d_scol
      *
     c                   exfmt     b13win
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c*                  select
     c*                  when      w_seqe = 'O'
     c*    sohel3_key    setll     sohel3
     c*                  other
     c     fohelr_key    setll     fohelr
     c*                  endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Posisjoner startverdi på ordrenummer
      *
     c                   if        b2numm <> 0
     c                   eval      w_seqe = 'O'
     c                   eval      fohelr_numm = b2numm
     c     fohelr_key    setll     fohelr
     c                   goto      end_pos
     c                   endif
      *
      * Posisjoner startverdi på ordretype
      *
     c*                  if        b2otyp <> *blank
     c*                  eval      w_seqe = 'T'
     c*                  eval      sohel4_otyp = b2otyp
     c*    sohel4_key    setll     sohel4
     c*                  goto      end_pos
     c*                  endif
      *
      * Posisjoner startverdi på fakturadato
      *
     c*                  if        b2fkda <> 0
      *
     c*                  eval      w_seqe = 'D'
     c*                  eval      sohel5_fkda = *loval
      *
     c*    *dmy          test(d)                 b2fkda                 90
     c*                  if        *in90 = *off
     c*    *dmy          move      b2fkda        sohel5_fkda
     c*                  endif
      *
     c*    sohel5_key    setll     sohel5
     c*                  goto      end_pos
      *
     c*                  endif
      *
      * Posisjoner startverdi på alfasøk kunde
      *
     c*                  if        b2kuna <> *blank
     c*                  eval      w_seqe = 'K'
     c*                  eval      sohela_kuna = b2kuna
     c*                  eval      sohela_navn = b2navn
     c*    sohela_key    setll     sohela
     c*                  goto      end_pos
     c*                  endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2numm = 0
     c                   eval      b2otyp = *blank
     c                   eval      b2kuna = *blank
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
     c                   eval      b_requ = *off
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Arbeide med ordre
      *
     c                   if        b1valg = 2
      * Jobb med ordrevedlikehold
     c                   eval      w_lpro = 'L'
     c                   call      'FO500R'
     c                   parm                    b1numm
     c                   parm                    w_lpro
     c                   eval      b1valg = 0
     c                   eval      b_requ = *on
     c***                update    b1sfl
     c                   iter
     c                   endif
6.34  *
6.34  * 5=Vis statusinfo
6.34  *
6.34 c                   if        b1valg = 5
6.34 c                   eval      c1numm = b1numm
6.34 c                   exsr      xc2bld
6.34 c                   eval      b1valg = 0
6.34 c                   update    b1sfl
6.34 c                   iter
6.34 c                   endif
      *
      * Sjekk saldo
      *
     c                   if        b1valg = 6
      * Sjekk om det tilgjengelig saldo
     c                   exsr      sjk_saldo
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Trekk beløp
      *
     c                   if        b1valg = 7
      * opprett statusrecord for trekk
     c                   exsr      opr_trekk
      * Bygg sammen streng som brukes ved request
6.20 c                   if        b_trek = *on
     c                   exsr      opr_request
6.20 c                   endif
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Frifgri ordre - fjerne sperrekode
      *
     c                   if        b1valg = 8
     c                   exsr      rel_ordre
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34  * Subrutine for å behandle bilde for vise (C2BLD)
6.34  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.34  *
6.34 c     xc2bld        begsr
6.34  *
6.34 c                   eval      w_arow = 0
6.34 c                   eval      w_acol = 0
6.34  *
6.34 c                   eval      rksjl1_jnum = b1numm
6.34 c                   eval      rksjl1_jsuf = b1suff
6.34 c     rksjl1_key    chain     rksjl1
6.34  *
6.34 c                   if        %found
6.34 c                   movel     rsjmsg        c2msg1
6.34 c                   eval      c2msg2 = %subst(rsjmsg:71:70)
6.34 c                   eval      c2msg3 = %subst(rsjmsg:141:70)
6.34 c                   eval      c2msg4 = %subst(rsjmsg:211:70)
6.34 c                   eval      c2msg5 = %subst(rsjmsg:281:70)
6.34 c                   eval      c2msg6 = %subst(rsjmsg:351:70)
6.34 c                   eval      c2msg7 = %subst(rsjmsg:421:70)
6.34 c                   eval      c2msg8 = %subst(rsjmsg:491:22)
6.34 c                   else
6.34 c                   eval      c2msg1 = *blank
6.34 c                   eval      c2msg2 = *blank
6.34 c                   eval      c2msg3 = *blank
6.34 c                   eval      c2msg4 = *blank
6.34 c                   eval      c2msg5 = *blank
6.34 c                   eval      c2msg6 = *blank
6.34 c                   eval      c2msg7 = *blank
6.34 c                   eval      c2msg8 = *blank
6.34 c                   endif
6.34  *
6.34 c     c2taga        tag
6.34  *
6.34 c                   exfmt     c2bld
6.34  *
6.34 c                   if        *inkc or *inkl
6.34 c                   goto      end_c2bld
6.34 c                   endif
6.34  *
6.34 c     end_c2bld     tag
6.34  *
6.34 c                   endsr
6.34  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
     c                   eval      fohelr_numm = 0
     c     fohelr_key    setll     fohelr
      *
     c                   dou       w_srrn = w_spge
      *
     c                   read      fohelr                                 15
      *
     c                   if        *in15
     c                   leave
     c                   endif
      *
     c                   if        fofirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
7.05  *
7.05  * Hvis ordren er opptatt, skal den ikke vises.
7.05  *
7.05 c                   if        fokode <> 0
7.05 c                   iter
7.05 c                   endif
      * Vi skal bare kortordre
     c                   if        fopsuf = 0 or
     c                             foityp = *blanks
     c                   iter
     c                   endif
6.39  * Sjekk mot lagerkode på ordre, deretter lager 0
6.39 c                   eval      ruksl1_skty = fopsuf
6.39 c                   eval      ruksl1_slag = folage
6.39 c     ruksl1_key    chain     ruksl1
6.39 c                   if        not %found
      * Sjekk også mot lager 0
     c                   eval      ruksl1_skty = fopsuf
     c                   eval      ruksl1_slag = 0
     c     ruksl1_key    chain     ruksl1
     c                   if        not %found
     c                   iter
     c                   endif
6.39  *
6.39 c                   endif
      * Vi skal bare ha kreditkort
     c                   if        rustyp <> 'K'
     c                   iter
     c                   endif
      * Vi tar ikke med hvis sperrekode på ordre er forskjellig fra
      * den som er oppgitt på kort.
     c                   if        rusity <> foityp
     c                   iter
     c                   endif
6.36  *
6.36  * Sjekk om ordetypen er "gyldig"
6.36  *
6.36 c                   eval      votyl1_otyp = footyp
6.36 c     votyl1_key    chain     votyl1
6.36 c                   if        %found
6.36 c                   if        vaoakk = 0 or
6.36 c                             vaokre = '-'
6.36 c                   iter
6.36 c                   endif
6.36 c                   endif
6.36  *
6.36  * Sjekk om positivt beløp
6.36  *
6.36 c                   if        fotots < 0
6.36 c                   iter
6.36 c                   endif
7.01  *
7.01  * Sjekk på filterverdier
7.01  *
7.01  * Valgt kunde ?
7.01  *
7.01 c                   if        f1kund <> 0      and
7.01 c                             f1kund <> fokund
7.01 c                   iter
7.01 c                   endif
7.01  *
7.01  * Valgt ordretype ?
7.01  *
7.01 c                   if        f1otyp <> *blanks and
7.01 c                             f1otyp <> footyp
7.01 c                   iter
7.01 c                   endif
7.01  *
7.01  * Valgt avdeling ?
7.01  *
7.01 c                   if        f1avde <> 0       and
7.01 c                             f1avde <> foavde
7.01 c                   iter
7.01 c                   endif
7.04  * NB Kun flyttet tildeling av felter
7.04 c                   eval      b1numm = fonumm
7.04 c                   eval      b1suff = fosuff
7.04  *
7.04  * Rekalkuler ordren, slik at vi er sikker på at det er riktig totalbeløp
7.04  *
7.04 c                   eval      w_enor = *blanks
7.04 c                   call      'FO730R'
7.04 c                   parm                    w_enor
7.04 c                   parm                    w_firm
7.04 c                   parm                    b1numm
7.04 c                   parm                    b1suff
7.04  *
7.04  * Så må vi hente inn oppdatert ordre.
7.04  *
7.04 c                   eval      fohel1_numm = b1numm
7.04 c                   eval      fohel1_suff = b1suff
7.04 c     fohel1_key    chain     fohel1
7.04  *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1valg = 0
     c                   eval      b1otyp = footyp
     c                   eval      b1sity = foityp
7.01 c                   eval      b1avde = foavde
      *
     c                   if        fobdat <> *loval
     c     *dmy          move      fobdat        b1bdat
     c                   endif
      *
     c                   eval      b1kuna = *blank
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1                             90
     c                   if        *in90 = *off
     c                   movel     rknavn        b1kuna
     c                   endif
      *
      * Henter fakturasum fra ordren
      *
     c                   eval      w_fsum = fotots - fototr
     c                   if        fomkod = 0
6.3e c**                 eval(h)   w_fsum = w_fsum * 1.25
6.3e  * Må beregne mva for hver enkelt linje
6.3e c                   exsr      kalk_mva
6.3e c                   eval(h)   w_fsum = w_fsum + w_mvab
     c                   endif
      *
     c                   eval      b1fsum = w_fsum
      * Hent inn info fra saldoregister
     c                   eval      b1ksjk = '0'
     c                   eval      rksjl1_jnum = fonumm
     c                   eval      rksjl1_jsuf = fosuff
     c     rksjl1_key    chain     rksjl1
     c                   if        %found
     c                   eval      b1ksjk = rsjsta
     c                   endif
      *
      * Skriver til subfile
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c     fohelr_key    setgt     fohelr
     c                   readp     fohelr
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   readp     fohelr
      *
     c                   if        %eof or
     c                             fofirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   eval      fohelr_numm = fonumm
      *
     c                   enddo
      *
     c                   if        %eof
     c     fohelr_key    setll     fohelr
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av trekktabell
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opr_trekk     begsr
      * Finn info fra ordrenummer
     c                   eval      b_trek = *off
      * Finn info fra ordrenummer
     c                   eval      fohel1_numm = b1numm
     c                   eval      fohel1_suff = b1suff
     c     fohel1_key    chain     fohel1
      * sjekk om ordetypen er "faktura"
     c                   eval      votyl1_otyp = b1otyp
     c     votyl1_key    chain     votyl1
     c                   if        %found     and
     c                             vaoakk > 1 and
     c                             vaoakk < 4
     c                   eval      b_trek = *on
     c                   endif
      *
     c                   if        b_trek = *on
      *
      * Opprett post på trekkoversikten
      *
     c                   eval      rksjlu_jnum = b1numm
     c                   eval      rksjlu_jsuf = b1suff
     c     rksjlu_key    chain     rksjlu
     c                   if        not %found
     c                   clear                   rksjlur
     c                   eval      rsjfir = w_firm
     c                   eval      rsjnum = b1numm
     c                   eval      rsjsuf = b1suff
     c                   eval      rsjkun = fokund
     c                   eval      rsjsum = b1fsum
6.20 c                   eval      rsjsta = 'S'
     c                   time                    rsjeda
     c                   time                    rsjeti
     c                   eval      rsjeus = l_user
     c                   time                    rsjoda
     c                   time                    rsjoti
     c                   eval      rsjous = l_user
     c                   write     rksjlur
     c                   else
6.20  * Hvis den allerede er overført, sender vi ikke ny request
6.20 c                   if        rsjsta = 'S'
6.20 c                   eval      b_trek = *off
6.35 c                   unlock    rksjlu
6.35 c                   goto      end_trekk
6.20 c                   endif
6.24  * Penger er trukket, men endelig oppdatering er ikke ferdig.
6.34 c                   if        rsjsta = 'B'
6.34 c                   eval      b_trek = *off
6.35 c                   unlock    rksjlu
6.35 c                   goto      end_trekk
6.34 c                   endif
6.35 c**                 unlock    rksjlu
6.35 c                   eval      rsjsta = 'S'
6.35 c                   time                    rsjeda
6.35 c                   time                    rsjeti
6.35 c                   eval      rsjeus = l_user
6.35 c                   update    rksjlur
     c                   endif
      *
     c                   else
     c                   eval      b1msg1 = 'Ordren er ikke faktura.'
     c                   eval      b1msg2 = 'Kan ikke ta beløp nå'
     c                   exfmt     b1msg
6.20 c                   eval      b_trek = *off
     c                   endif
      *
6.35 c     end_trekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av requeststreng
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opr_request   begsr
      * Hent beløp som skal sjekkes
     c                   eval      p_fsum = 0
     c                   eval      p_fsum = b1fsum
     c*                  call      'FA925R'
     c*                  parm                    fonumm
     c*                  parm                    fosuff
     c*                  parm                    p_fsum
     c********
      *
     c                   eval      w_requ = *blanks
     c                   eval      d_requ = *blanks
     c                   eval      d_skil = '.'
     c                   eval      d_des0 = ';'
     c                   eval      d_des1 = ';'
     c                   eval      d_des2 = ';'
     c                   eval      d_des3 = ';'
7.02 c                   eval      d_des4 = ';'
      *
     c                   eval      rukrl5_ktyp = fopsuf
     c                   eval      rukrl5_kkun = fokund
     c                   eval      rukrl5_kpro = 0
     c     rukrl5_key    chain     rukrl5
     c                   if        not %found
     c                   eval      rukknr = 0
     c                   endif
      *
     c                   move      w_firm        d_firm
     c                   move      b1numm        d_ordr
     c                   move      b1suff        d_suff
     c**                 eval      d_belh = b1fsum
     c                   eval      d_belh = p_fsum
     c**                 eval      d_belop = b1fsum
     c                   eval      d_belop = p_fsum
     c                   eval      d_beld = d_desi
     c                   move      rukknr        d_kort
7.02  *
7.02  * Hent inn random KID basert på timestamp
7.02  *
7.02 c                   if        u_702 = *on
7.02 c                   eval      p_kidn = *blanks
7.02 c                   call      'FO535R'
7.02 c                   parm                    p_kidn
7.02  *
7.02 c                   eval      d_kidn = p_kidn
7.02 c                   endif
      *
     c                   eval      w_requ = d_requ
      * Kall program som sender request
     c                   call      'AP510C'
     c                   parm                    w_requ
      *
     c                   eval      b_requ = *on
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å trekke alle ordre
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     trk_alle      begsr
      *
     c                   eval      fohelr_numm = 0
     c                   eval      fohelr_suff = 0
     c     fohelr_key    setll     fohelr
     c     w_firm        reade     fohelr
     c                   dow       not %eof
      * Vi skal bare kortordre
     c                   if        fopsuf = 0 or
     c                             foityp = *blanks
     c                   goto      nyles
     c                   endif
6.37  * Vi skal ikke ha med negative beløp
6.37 c                   if        fotots < 0
6.37 c                   goto      nyles
6.37 c                   endif
      * sjekk om ordetypen er "faktura"
     c                   eval      votyl1_otyp = footyp
     c     votyl1_key    chain     votyl1
     c                   if        not %found  or
     c                             vaoakk < 2  or
     c                             vaoakk > 3
     c                   goto      nyles
     c                   endif
6.3b  * Ikke slipp forbi negative ordretyper
6.3b c                   if        vaokre = '-'
6.3b c                   goto      nyles
6.3b c                   endif
6.39  * Sjekk mot lager før lager 0
6.39 c                   eval      ruksl1_skty = fopsuf
6.3d c                   eval      ruksl1_slag = folage
6.39 c     ruksl1_key    chain     ruksl1
6.39 c                   if        not %found
      *
     c                   eval      ruksl1_skty = fopsuf
     c                   eval      ruksl1_slag = 0
     c     ruksl1_key    chain     ruksl1
     c                   if        not %found
     c                   goto      nyles
     c                   endif
6.39  *
6.39 c                   endif
      * Vi skal bare ha kreditkort
     c                   if        rustyp <> 'K'
     c                   goto      nyles
     c                   endif
      * Vi tar ikke med hvis sperrekode på ordre er forskjellig fra
      * den som er oppgitt på kort.
     c                   if        rusity <> foityp
     c                   goto      nyles
     c                   endif
      *
      * Henter fakturasum fra ordren
      *
     c                   eval      w_fsum = fotots - fototr
     c                   if        fomkod = 0
6.3e c*                  eval(h)   w_fsum = w_fsum * 1.25
6.3e  * Må beregne mva for hver enkelt linje
6.3e c                   exsr      kalk_mva
6.3e c                   eval(h)   w_fsum = w_fsum + w_mvab
     c                   endif
      *
     c                   exsr      opr_trk_all
6.20 c                   if        b_trek = *on
     c                   exsr      opr_req_all
6.20 c                   endif
      *
     c     nyles         tag
      *
     c     w_firm        reade     fohelr
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av trekktabell
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opr_trk_all   begsr
      * Opprett post på trekkoversikten
6.20 c                   eval      b_trek = *on
     c                   eval      rksjlu_jnum = fonumm
     c                   eval      rksjlu_jsuf = fosuff
     c     rksjlu_key    chain     rksjlu
     c                   if        not %found
     c                   clear                   rksjlur
     c                   eval      rsjfir = w_firm
     c                   eval      rsjnum = fonumm
     c                   eval      rsjsuf = fosuff
     c                   eval      rsjkun = fokund
     c                   eval      rsjsum = w_fsum
6.20 c                   eval      rsjsta = 'S'
     c                   time                    rsjeda
     c                   time                    rsjeti
     c                   eval      rsjeus = l_user
     c                   time                    rsjoda
     c                   time                    rsjoti
     c                   eval      rsjous = l_user
     c                   write     rksjlur
6.20 c                   else
6.20  * Hvis den allerede er overført, sender vi ikke ny request
6.20 c                   if        rsjsta = 'S'
6.20 c                   eval      b_trek = *off
6.35 c                   unlock    rksjlu
6.35 c                   goto      end_trk_all
6.20 c                   endif
6.34 c                   if        rsjsta = 'B'
6.34 c                   eval      b_trek = *off
6.35 c                   unlock    rksjlu
6.35 c                   goto      end_trk_all
6.34 c                   endif
6.35 c**                 unlock    rksjlu
6.35 c                   eval      rsjsta = 'S'
6.35 c                   time                    rsjeda
6.35 c                   time                    rsjeti
6.35 c                   eval      rsjeus = l_user
6.35 c                   update    rksjlur
     c                   endif
      *
6.35 c     end_trk_all   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for oppretting av requeststreng
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     opr_req_all   begsr
      * Hent beløp som skal sjekkes
     c                   eval      p_fsum = 0
     c                   eval      p_fsum = w_fsum
     c*                  call      'FA925R'
     c*                  parm                    fonumm
     c*                  parm                    fosuff
     c*                  parm                    p_fsum
      *
     c                   eval      w_requ = *blanks
     c                   eval      d_requ = *blanks
     c                   eval      d_skil = '.'
     c                   eval      d_des0 = ';'
     c                   eval      d_des1 = ';'
     c                   eval      d_des2 = ';'
     c                   eval      d_des3 = ';'
7.02 c                   eval      d_des4 = ';'
      *
     c                   eval      rukrl5_ktyp = fopsuf
     c                   eval      rukrl5_kkun = fokund
     c                   eval      rukrl5_kpro = 0
     c     rukrl5_key    chain     rukrl5
     c                   if        not %found
     c                   eval      rukknr = 0
     c                   endif
      *
     c                   move      w_firm        d_firm
     c                   move      fonumm        d_ordr
     c                   move      fosuff        d_suff
     c**                 eval      d_belh = w_fsum
     c                   eval      d_belh = p_fsum
     c**                 eval      d_belop = w_fsum
     c                   eval      d_belop = p_fsum
     c                   eval      d_beld = d_desi
     c                   move      rukknr        d_kort
7.02  *
7.02  * Hent inn random KID basert på timestamp
7.02  *
7.02 c                   if        u_702 = *on
7.02 c                   eval      p_kidn = *blanks
7.02 c                   call      'FO535R'
7.02 c                   parm                    p_kidn
7.02  *
7.02 c                   eval      d_kidn = p_kidn
7.02 c                   endif
      *
     c                   eval      w_requ = d_requ
      * Kall program som sender request
     c                   call      'AP510C'
     c                   parm                    w_requ
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å sjekke saldo
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjk_saldo     begsr
      * Hent beløp som skal sjekkes
     c                   eval      p_fsum = 0
     c                   eval      p_fsum = b1fsum
     c*                  call      'FA925R'
     c*                  parm                    b1numm
     c*                  parm                    b1suff
     c*                  parm                    p_fsum
      *
     c                   eval      b1msg1 = *blanks
     c                   eval      b1msg2 = *blanks
     c                   eval      b1msg3 = *blanks
      * Finn info fra ordrenummer
     c                   eval      fohel1_numm = b1numm
     c                   eval      fohel1_suff = b1suff
     c     fohel1_key    chain     fohel1
      *
     c                   eval      d_list = *blanks
     c                   eval      w_list = *blanks
      *
     c                   eval      rukrl5_ktyp = fopsuf
     c                   eval      rukrl5_kkun = fokund
     c                   eval      rukrl5_kpro = 0
     c     rukrl5_key    chain     rukrl5
     c                   if        not %found
     c                   eval      rukknr = 0
     c                   endif
6.20  * Hent neste ledige nummer
6.20 c                   exsr      hnt_nummer
      *
     c                   move      rukknr        d_skrt
     c                   eval      d_selh = p_fsum
     c                   eval      d_belop = p_fsum
     c                   eval      d_seld = d_desi
6.3a c                   eval      d_sfir = %char(l_firm)
6.3a c                   eval      d_avde = %char(foavde)
     c                   eval      w_osta = *blanks
     c                   eval      d_sskl = '.'
     c                   eval      d_ses0 = ';'
     c                   eval      d_ses1 = ';'
6.3a c                   eval      d_ses2 = ';'
6.3a c                   eval      d_ses3 = ';'
     c                   eval      w_list = d_list
6.20 c                   eval      w_rkey = d_rkey
      *
     c                   call      'AP511C'
6.20 c                   parm                    w_rkey
     c                   parm                    w_list
     c                   parm                    w_osta
      *
     c                   if        w_osta = '1'
     c                   eval      b1msg1 = 'Saldosjekk er OK.'
     c                   endif
      *
     c                   if        w_osta = '2'
     c                   eval      b1msg1 = 'Ikke dekning på kortet,'
     c                   eval      b1msg2 = 'eller kortet er ugyldig'
     c                   endif
      *
     c                   if        w_osta = '3'
     c                   eval      b1msg1 = 'Kortnummer er ugyldig'
     c                   endif
      *
     c                   if        w_osta = 'E'
     c                   eval      b1msg1 = 'Feil under innhenting av'
     c                   eval      b1msg2 = 'saldo.'
     c                   endif
      * Falgg ststus
     c                   exfmt     b1msg
      *
     c                   endsr
      *
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  * Subrutine for etablering av batchnummer
6.20  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.20  *
6.20 c     hnt_nummer    begsr
6.20  *
6.20  * Hnet jobbnummer
6.20  *
6.20 c                   eval      w_kode = '0'
6.20 c                   eval      w_firm = l_firm
6.20 c                   eval      w_fell = 'NXTKLI'
6.20 c                   eval      w_rtyp = *blank
6.20 c                   eval      w_fast = *blank
6.20 c                   eval      w_rnum = *zero
6.20  *
6.20 c                   call      'AS100R'
6.20 c                   parm                    w_kode
6.20 c                   parm                    w_firm
6.20 c                   parm                    w_fell
6.20 c                   parm                    w_rtyp
6.20 c                   parm                    w_fast
6.20 c                   parm                    w_rnum
6.20  *
6.20 c                   eval      d_rnum = w_rnum
6.20  *
6.20 c                   endsr
6.20  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å frigjøre ordren
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     rel_ordre     begsr
6.32  * Sjekk først om ordren er forsøkt trukket.
6.32 c                   if        b1ksjk = 'S'
6.32 c                   exfmt     f15wi5
6.32 c                   goto      end_rel
6.32 c                   endif
      * Finn info fra ordrenummer
     c                   eval      fohelu_numm = b1numm
     c                   eval      fohelu_suff = b1suff
     c     fohelu_key    chain     fohelu
     c                   if        %found
     c                   eval      foityp = *blanks
     c                   update    fohelur
     c                   endif
      *
     c                   eval      b_requ = *on
      *
6.32 c     end_rel       endsr
      *
6.3e  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.3e  * Subrutine for kalkulere mva beløp
6.3e  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.3e  *
6.3e c     kalk_mva      begsr
6.3e  *
6.3e  * Les gjennom ordrelinjene
6.3e  *
6.3e c                   eval      w_mvab = 0
6.3e c                   eval      fodtlr_numm = fonumm
6.3e c                   eval      fodtlr_suff = fosuff
6.3e c                   eval      fodtlr_line = 0
6.3e c     fodtlr_key    setll     fodtlr
6.3e c     fodtlr_key2   reade     fodtlr
6.3e c                   dow       not %eof
6.3e c                   eval      w_mvag = 0
6.3e c                   eval      w_rkr1 = 0
6.3e c                   eval      w_rkr2 = 0
6.3e c                   eval      w_rkr3 = 0
6.3e c                   eval      w_rkro = 0
6.3e c                   eval(h)   w_mvag = fdsapr * fdanta
6.3e  *
6.3e  * Linjerabatter
6.3e  *
6.3e c                   eval(h)   w_rkr1 = (w_mvag * fdrab1) / 100
6.3e c                   eval      w_mvag = w_mvag - w_rkr1
6.3e c                   eval(h)   w_rkr2 = (w_mvag * fdrab2) / 100
6.3e c                   eval      w_mvag = w_mvag - w_rkr2
6.3e c                   eval(h)   w_rkr3 = (w_mvag * fdrab3) / 100
6.3e c                   eval      w_mvag = w_mvag - w_rkr3
6.3e  *
6.3e  * Varelinjens andel av ordrerabatt
6.3e  *
6.3e c                   eval      vvarl1_vare = fdvare
6.3e c     vvarl1_key    chain     vvarl1r
6.3e c                   if        not %found
6.3e c                   eval      vvkord = 0
6.3e c                   endif
6.3e  *
6.3e c                   if        vvkord = 0
6.3e c                   eval(h)   w_rkro = (w_mvag * forabp) / 100
6.3e c                   endif
6.3e c                   eval      w_mvag = w_mvag - w_rkro
6.3e  *
6.3e  * Henter inn prosentsats på mva kode
6.3e  *
6.3e c                   eval      w_mvap = 0
6.3e c                   eval      w_mvat = 0
6.3e c                   eval      w_mvaf = 0
6.3e  *
6.3e c                   if        fdomva <> *blank
6.3e c                   eval      w_stat =  *blank
6.3e c                   eval      w_mvak =  fdomva
6.3e  *
6.3e c                   call      'RS205R'
6.3e c                   parm                    w_stat
6.3e c                   parm                    w_mvak
6.3e c                   parm                    w_mvtx
6.3e c                   parm                    w_dato
6.3e c                   parm                    w_mvap
6.3e c                   parm                    w_mvat
6.3e c                   parm                    w_mvaf
6.3e c                   parm                    w_bpro
6.3e c                   endif
6.3e  *
6.3e  *  Beregning av mva og akkumulerer
6.3e  *
6.3e c                   eval(h)   w_mvab = w_mvab +
6.3e c                                      ((w_mvag * w_mvap) / 100)
6.3e  *
6.3e c     fodtlr_key2   reade     fodtlr
6.3e c                   enddo
6.3e  *
6.3e c                   endsr
6.3e  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c*    *entry        plist
     c*                  parm                    p_firm
     c*                  parm                    p_aarr
     c*                  parm                    p_otyp
     c*                  parm                    p_kund
      *
      * Key for les på ordreregister
      *
     c     fohelr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelr_numm
      *
      * Key for les på fakturaregister
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for oppslag på kunde
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for les på kortregister
      *
     c     ruksl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ruksl1_skty
     c                   kfld                    ruksl1_slag
      *
      * Key for les på saldoregister
      *
     c     rksjl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rksjl1_jnum
     c                   kfld                    rksjl1_jsuf
      *
      * Key for utlegg av poster på saldoregister
      *
     c     rksjlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rksjlu_jnum
     c                   kfld                    rksjlu_jsuf
      *
      * Key for oppslag på kortregister
      *
     c     rukrl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rukrl5_ktyp
     c                   kfld                    rukrl5_kkun
     c                   kfld                    rukrl5_kpro
      *
      * Key for oppslag på ordrtype
      *
     c     votyl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    votyl1_otyp
      *
      * Key for les på fakturaregister
      *
     c     fohelu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohelu_numm
     c                   kfld                    fohelu_suff
6.3e  *
6.3e  * Key for les på ordreregister
6.3e  *
6.3e c     fodtlr_key    klist
6.3e c                   kfld                    w_firm
6.3e c                   kfld                    fodtlr_numm
6.3e c                   kfld                    fodtlr_suff
6.3e c                   kfld                    fodtlr_line
6.3e  *
6.3e  * Key for les på ordreregister
6.3e  *
6.3e c     fodtlr_key2   klist
6.3e c                   kfld                    w_firm
6.3e c                   kfld                    fodtlr_numm
6.3e c                   kfld                    fodtlr_suff
6.3e  *
6.3e  * Key for les på vareregister
6.3e  *
6.3e c     vvarl1_key    klist
6.3e c                   kfld                    w_firm
6.3e c                   kfld                    vvarl1_vare
      *
      * Henter firmakode og år fra parameter
      *
     c                   eval      w_firm = l_firm
6.38 c                   eval      d_fast = 'rq'
7.02  *
7.02  * Endring 7.02
7.02  *
7.02   CallP CO402R(l_filg:w_firm:0:'FO530R_702':
7.02                    co402_verdi1:co402_verdi2);
7.02   if co402_verdi1 = '1';
7.02     u_702 = *on;
7.02   endif;
      *
      * Henter verdi til skjermbilde
      *
     c*                  eval      b2kund = p_kund
      *
      * Fyller subfile
      *
     c                   eval      *in22 = *on
      *
     c                   eval      fohelr_numm = 0
     c     fohelr_key    setll     fohelr
      *
6.3e c                   time                    w_dato
     c                   exsr      crt_subfile
      *
     c                   endsr
