     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : Nexstep
      * Program . . . . : XL700R
      * beskrivelse . . : Logging av tilbudsaktiviteter
      *
      * Spesialversjon. : Tilbudsoppfølging
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       281116 ask  Nytt program
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fxtlolu    uf a e           k disk    rename(xtlopfr:xtlolur)
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     frkunl1    if   e           k disk    rename(rkunpfr:rkunl1r)
     fra09l1    if   e           k disk    rename(ra09pfr:ra09l1r)
      *
      * Definering av parametre
      *
     d p_tiln          s                   like(xltiln)
     d p_aktp          s                   like(xlaktp)
     d p_slet          s                   like(xlslet)
     d p_konk          s                   like(xlkonk)
     d p_sgru          s                   like(xlsgru)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av variabler i nøkler
      *
     d xtlolu_aarr     s                   like(xlaarr)
     d xtlolu_tiln     s                   like(xltiln)
     d fohel1_numm     s                   like(fonumm)
     d fohel1_suff     s                   like(fosuff)
     d rkunl1_kund     s                   like(rkkund)
     d ra09l1_ikod     s                   like(raikod)
      *
      * Definering av variable
      *
     d w_firm          s                   like(xlfirm)
6.30 d w_date          s               d   datfmt(*iso)
6.30 d w_time          s               t   timfmt(*iso)
6.30 d w_dato          s             10
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter inn nødvendig infoprmasjon og logger aktivitet
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Slår opp i ordreregister og finner tilbudsinformasjon
      *
     c                   eval      fohel1_numm = p_tiln
     c                   eval      fohel1_suff = 0
     c     fohel1_key    chain     fohel1r
     c                   if        not %found
     c                   if        p_aktp <> 'SLETTET'
     c                   goto      avslutt
     c                   endif
     c                   endif
      *
      * Slår opp i kunderegister og finner kundenavn
      *
     c                   eval      rkunl1_kund = fokund
     c     rkunl1_key    chain     rkunl1r
     c                   if        not %found
     c                   eval      rknavn =  'Ikke funnet kunde'
     c                   endif
      *
      * Slår opp i koderegister og finner selgernavn
      *
     c                   eval      ra09l1_ikod = foselg
     c     ra09l1_key    chain     ra09l1r
     c                   if        not %found
     c                   eval      raitxt =  'Ikke funnet selger'
     c                   endif
      *
      * Oppdatering av loggfil
      *
     c                   clear                   xtlolur
      *
     c                   eval      xlfirm = w_firm
     c                   eval      xlaarr = *year
     c                   move      fobdat        w_dato
     c                   movel     w_dato        xlaarr
     c                   eval      xltiln = p_tiln
     c                   eval      xlaktp = p_aktp
     c                   eval      xlakda = w_date
     c                   eval      xlakti = w_time
6.10 c                   eval      xlakus = l_user
6.10 c                   eval      xlkunr = fokund
6.10 c                   eval      xlkuna = rknavn
6.10 c                   eval      xlsenr = foselg
6.10 c                   eval      xlsena = raitxt
6.10 c                   eval      xlopda = foddat
6.10 c                   eval      xlutda = fotdat
6.10 c                   eval      xlslet = p_slet
6.10 c                   eval      xlkonk = p_konk
6.10 c                   eval      xlsgru = p_sgru
     c                   write     xtlolur
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_tiln
     c                   parm                    p_aktp
     c                   parm                    p_slet
     c                   parm                    p_konk
     c                   parm                    p_sgru
      *
      * Key for oppdatering av tilbudsaktivitet
      *
     c     xtlolu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    xtlolu_aarr
     c                   kfld                    xtlolu_tiln
      *
      * Key for oppslag på ordre-hode
      *
     c     fohel1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    fohel1_numm
     c                   kfld                    fohel1_suff
      *
      * Key for kunderegister
      *
     c     rkunl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rkunl1_kund
      *
      * Key for oppslag på selgerregister
      *
     c     ra09l1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    ra09l1_ikod
      *
      * Henter verdier fra LDA
      *
     c                   eval      w_firm = l_firm
6.30 c                   time                    w_date
6.30 c                   time                    w_time
      *
     c                   endsr
