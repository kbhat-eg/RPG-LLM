      /title  Overføring av budsjett til økonomi
     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASSTAT
      * Program . . . . : SBOVFR
      * Beskrivelse . . : Overføring av budsjett til økonomi
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      * V5.30 150904 KOB  Endret call av FOHNKR til VA720R
      * V5.30 150904 KOB  Endret fortegn på beløp ved overføring
6.10  * V6.10 210909 BSA  Lagt inn sporingsfelter
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fskbul1    if   e           k disk    rename(skbupfr:skbul1r)
      *
     frhsalu    uf a e           k disk    rename(rhsapfr:rhsalur)
      *
      * Definering av parameter (inn)
      *
     d                 ds
     d wsparm                        64
     d  wsfirm                        3  0 overlay(wsparm:01)
     d  wshhaa                        4  0 overlay(wsparm:04)
     d  wsvers                        2    overlay(wsparm:08)
     d  wsakod                       12    overlay(wsparm:10)
     d  wsaabb                        1    overlay(wsparm:22)
     d  wskode                       12    overlay(wsparm:23)
      *
      * Datastruktur for kontoinformasjon
      *
     d                 ds
     d  d_hele                 1     88
     d  d_kav1                 1      4  0
     d  d_kko1                 5     10  0
     d  d_ksp1                11     12  0
     d  d_kav2                13     16  0
     d  d_kko2                17     22  0
     d  d_ksp2                23     24  0
     d  d_kav3                25     28  0
     d  d_kko3                29     34  0
     d  d_ksp3                35     36  0
     d  d_kav4                37     40  0
     d  d_kko4                41     46  0
     d  d_ksp4                47     48  0
     d  d_kav5                49     52  0
     d  d_kko5                53     58  0
     d  d_ksp5                59     60  0
     d  d_kav6                61     64  0
     d  d_kko6                65     70  0
     d  d_ksp6                71     72  0
     d  d_kav7                73     76  0
     d  d_kko7                77     82  0
     d  d_ksp7                83     84  0
     d  d_khnv                85     88
      *
      * Definering av variable i nøkler
      *
     d skbul1_hhaa     s                   like(sbhhaa)
     d skbul1_vers     s                   like(sbvers)
     d skbul1_kode     s                   like(sbkode)
     d skbul1_aabb     s                   like(sbaabb)
      *
     d skbul1_avde     s                   like(sbavde)
     d skbul1_lage     s                   like(sblage)
     d skbul1_otyp     s                   like(sbotyp)
     d skbul1_land     s                   like(sbland)
     d skbul1_dist     s                   like(sbdist)
     d skbul1_selg     s                   like(sbselg)
     d skbul1_kkat     s                   like(sbkkat)
     d skbul1_kund     s                   like(sbkund)
     d skbul1_ogrp     s                   like(sbogrp)
     d skbul1_hgrp     s                   like(sbhgrp)
     d skbul1_ugrp     s                   like(sbugrp)
     d skbul1_vare     s                   like(sbvare)
      *
      *
     d rhsalu_kont     s                   like(rikont)
     d rhsalu_spes     s                   like(rispes)
     d rhsalu_avde     s                   like(riavde)
     d rhsalu_raar     s                   like(riraar)
     d rhsalu_type     s                   like(ritype)
      *
      * Definering av variable
      *
     d w_firm          s                   like(sbfirm)
      *
      * Definering av Local data area
      *
     d                uds
6.10 d l_user                911    920
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * XINIT  Initisiering av utskriften                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c                   eval      skbul1_avde = *zero
     c                   eval      skbul1_lage = *zero
     c                   eval      skbul1_otyp = *blank
     c                   eval      skbul1_land = *blank
     c                   eval      skbul1_dist = *zero
     c                   eval      skbul1_selg = *zero
     c                   eval      skbul1_kkat = *zero
     c                   eval      skbul1_kund = *zero
     c                   eval      skbul1_ogrp = *zero
     c                   eval      skbul1_hgrp = *zero
     c                   eval      skbul1_ugrp = *zero
     c                   eval      skbul1_vare = *blank
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * XSLETT Slett budjsett i konto-register                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c                   eval      skbul1_kode = wskode
     c     skbul1_key    setll     skbul1r
      *
      * Les ...
      *
     c     xslett        tag
     c                   read      skbul1r                                10
     c     *in10         cabeq     *on           xgener
      *
      * Sjekk, Hopp ut dersom ikke riktig
      *
     c     sbfirm        cabne     w_firm        xgener
     c     sbhhaa        cabne     skbul1_hhaa   xgener
     c     sbvers        cabne     skbul1_vers   xgener
     c     sbkode        cabne     skbul1_kode   xgener
     c     sbaabb        cabne     skbul1_aabb   xgener
      *
      * Har funnet en record som tilfredstiller utvalget
      * Skal nå finne kontoen til denne recorden
      *
     c                   exsr      hntkon
      *
     c                   move      d_kko1        rhsalu_kont
     c                   move      d_ksp1        rhsalu_spes
     c                   move      d_kav1        rhsalu_avde
     c     rhsalu_key    chain     rhsalur                            12
      *
     c     *in12         ifeq      *off
     c                   delete    rhsalur
     c                   endif
      *
      * Returner til nytt les
      *
     c                   goto      xslett
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * XGENER Generering av nye linjer                                *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     xgener        tag
      *
     c                   eval      skbul1_kode = wskode
     c     skbul1_key    setll     skbul1r
      *
     c     xstart        tag
     c                   read      skbul1r                                10
     c     *in10         cabeq     *on           xslutt
      *
      * Sjekk, Hopp ut dersom ikke riktig
      *
     c     sbfirm        cabne     w_firm        xslutt
     c     sbhhaa        cabne     skbul1_hhaa   xslutt
     c     sbvers        cabne     skbul1_vers   xslutt
     c     sbkode        cabne     skbul1_kode   xslutt
     c     sbaabb        cabne     skbul1_aabb   xslutt
      *
      * Har funnet en record som tilfredstiller utvalget
      * Skal nå finne kontoen til denne recorden
      *
     c                   exsr      hntkon
      *
     c                   move      d_kko1        rhsalu_kont
     c                   move      d_ksp1        rhsalu_spes
     c                   move      d_kav1        rhsalu_avde
     c     rhsalu_key    chain     rhsalur                            12
      *
      * Dersom ny, nullstiller felter
      *
     c     *in12         ifeq      *on
     c                   move      *zero         risa01
     c                   move      *zero         risa02
     c                   move      *zero         risa03
     c                   move      *zero         risa04
     c                   move      *zero         risa05
     c                   move      *zero         risa06
     c                   move      *zero         risa07
     c                   move      *zero         risa08
     c                   move      *zero         risa09
     c                   move      *zero         risa10
     c                   move      *zero         risa11
     c                   move      *zero         risa12
     c                   move      *zero         risa13
     c                   move      *zero         risa00
     c                   endif
      *
      * Legger inn verdier fra statistikk-budsjett
      *
     c                   sub       sbbu01        risa01
     c                   sub       sbbu02        risa02
     c                   sub       sbbu03        risa03
     c                   sub       sbbu04        risa04
     c                   sub       sbbu05        risa05
     c                   sub       sbbu06        risa06
     c                   sub       sbbu07        risa07
     c                   sub       sbbu08        risa08
     c                   sub       sbbu09        risa09
     c                   sub       sbbu10        risa10
     c                   sub       sbbu11        risa11
     c                   sub       sbbu12        risa12
      *
6.10 c                   time                    riedat
6.10 c                   time                    rietim
6.10 c                   eval      rieusr = l_user
      *
     c     *in12         ifeq      *off
     c                   update    rhsalur
     c                   else
      *
     c                   move      w_firm        rifirm
     c                   move      rhsalu_kont   rikont
     c                   move      rhsalu_spes   rispes
     c                   move      rhsalu_avde   riavde
     c                   move      rhsalu_raar   riraar
     c                   move      rhsalu_type   ritype
      *
6.10 c                   time                    riodat
6.10 c                   time                    riotim
6.10 c                   eval      riousr = l_user
      *
     c                   write     rhsalur
     c                   endif
      *
      * Returner til nytt les
      *
     c                   goto      xstart
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
      *
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Henter avdeling/konto/spesifikasjon                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     HNTKON        begsr
      *
      *  Legger ut parametere for konto-henvisnings-program
      *
     c                   move      *blank        w_stat
     c                   move      sbogrp        w_ogrp
     c                   move      sbhgrp        w_hgrp
     c                   move      sbugrp        w_ugrp
     c                   move      sbvare        w_vare
     c                   move      *zero         w_hele
      *
      *  Kaller program for å finne konto
      *
     c                   call      'VA720R'
     c                   parm                    w_stat
     c                   parm                    w_ltyp
     c                   parm                    w_otyp
     c                   parm                    w_ogrp
     c                   parm                    w_hgrp
     c                   parm                    w_ugrp
     c                   parm                    w_vare
     c                   parm                    w_hele
     c                   move      w_hele        d_hele
      *
      *  Overstyrer avdeling dersom denne
      *  finnes på ordrehode
      *
     c     sbavde        ifgt      *zero
     c                   z-add     sbavde        d_kav1
     c                   z-add     sbavde        d_kav2
     c                   z-add     sbavde        d_kav3
     c                   z-add     sbavde        d_kav4
     c                   z-add     sbavde        d_kav5
     c                   z-add     sbavde        d_kav6
     c                   z-add     sbavde        d_kav7
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     csr   *inzsr        begsr
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     c                   move      *blank        w_stat            1
     c                   move      *zero         w_ogrp            2 0
     c                   move      *zero         w_hgrp            2 0
     c                   move      *zero         w_ugrp            3 0
     c                   move      *blank        w_vare           15
3.14 c                   move      *zero         w_hele           88
     c                   move      *blank        w_otyp            2
     c                   move      *zero         w_ltyp            1 0
      *
      * Key for saldomatrise via kontonummer - oppdatering
      *
     c     rhsalu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rhsalu_kont
     c                   kfld                    rhsalu_spes
     c                   kfld                    rhsalu_avde
     c                   kfld                    rhsalu_raar
     c                   kfld                    rhsalu_type
      *
      * Key for oppdatering av budsjett-register
      *
     c     skbul1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    skbul1_hhaa
     c                   kfld                    skbul1_vers
     c                   kfld                    skbul1_kode
     c                   kfld                    skbul1_aabb
     c                   kfld                    skbul1_avde
     c                   kfld                    skbul1_lage
     c                   kfld                    skbul1_otyp
     c                   kfld                    skbul1_land
     c                   kfld                    skbul1_dist
     c                   kfld                    skbul1_selg
     c                   kfld                    skbul1_kkat
     c                   kfld                    skbul1_kund
     c                   kfld                    skbul1_ogrp
     c                   kfld                    skbul1_hgrp
     c                   kfld                    skbul1_ugrp
     c                   kfld                    skbul1_vare
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Tar inn parameter fra forrige program                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     *entry        plist
     c                   parm                    dsparm           64
     c                   move      dsparm        wsparm
      *
      *  Legg inn verdier
      *
     c                   eval      w_firm      = wsfirm
     c                   eval      skbul1_hhaa = wshhaa
     c                   eval      skbul1_vers = wsvers
     c                   eval      skbul1_aabb = wsaabb
      *
     c                   eval      rhsalu_raar = wshhaa
     c                   eval      rhsalu_type = 3
      *
     csr                 endsr
