     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VL740R
      * Beskrivelse . . : Bytter varenummer på vare i alle tabeller +
      *                   innkjøpsstatistikk
      *
      *               NB! Dersom det legges inn bytting av varenummer i
      *                   flere tabeller må også jobben for kopiering fra
      *                   filgruppe til filgruppe endres m.h.p. overriding
      *                   av tabeller mot databibliotek !!!
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       220699 HKO  Nytt program
      *       301104 HKO  Parametersatt *inlr
      *       310306 HKO  Utvidet med prognose- og abcxyz-tabeller
      * 6.10  170609 BSA  Oppdatert med sporingsinformasjon
      * 6.20  270716 BKV  Ved sammenslåing, så slettes gammel
      *                   prognose- og abcxyz info
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fltell3    uf   e           k disk    rename(ltelpfr:ltell3r)
     flodtlv    uf   e           k disk    rename(lodtpfr:lodtlvr)
     flfdtl3    uf   e           k disk    rename(lfdtpfr:lfdtl3r)
     flldtl3    uf   e           k disk    rename(lldtpfr:lldtl3r)
     flbdtl3    uf   e           k disk    rename(lbdtpfr:lbdtl3r)
      *
     fhodtl2    uf   e           k disk    rename(hodtpfr:hodtl2r)
      *
     fmabdl9    uf   e           k disk    rename(mabdpfr:mabdl9r)
     fmprolu    uf   e           k disk    rename(mpropfr:mprolur)
     fmmatl5    uf   e           k disk    rename(mmatpfr:mmatl5r)
     fmprulu    uf   e           k disk    rename(mprupfr:mprulur)
     fmltil2    uf   e           k disk    rename(mltipfr:mltil2r)
      *
     fsidtlv    uf   e           k disk    rename(sidtpfr:sidtlvr)
     fsistl2    uf   e           k disk    rename(sistpfr:sistl2r)
      *
6.10  *  Local Data Area
6.10  *
6.10 d                uds
6.10 d l_user                911    920
      *
      * Definering av parametre
      *
     d p_firm          s              3  0
     d p_gvar          s             15
     d p_vare          s             15
     d p_inlr          s              1
6.20 d p_dele          s              1
      *
      * Definering av variable
      *
     d hodtl2_vare     s                   like(hdvare)
      *
     d w_firm          s              3  0
     d w_gvar          s             15
6.20 d w_nvar          s             15
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_gvar = p_gvar
     c                   eval      w_nvar = p_vare
      *
      * Oppdaterer varenummer i lagertelling-telle-register
      *
     c     key01         setll     ltell3
     c     key01         reade     ltell3r
     c                   dow       not %eof
     c                   eval      levare = p_vare
6.10 c                   time                    leedat
6.10 c                   time                    leetim
6.10 c                   eval      leeusr = l_user
     c                   update    ltell3r
     c     key01         reade     ltell3r
     c                   enddo
      *
      * Oppdaterer varenummer i bestilling-linje-register
      *
     c     key01         setll     lodtlv
     c     key01         reade     lodtlvr
     c                   dow       not %eof
     c                   eval      ldvare = p_vare
6.10 c                   time                    ldedat
6.10 c                   time                    ldetim
6.10 c                   eval      ldeusr = l_user
     c                   update    lodtlvr
     c     key01         reade     lodtlvr
     c                   enddo
      *
      * Oppdaterer varenummer i best.forsl-linje-register
      *
     c     key01         setll     lfdtl3
     c     key01         reade     lfdtl3r
     c                   dow       not %eof
     c                   eval      lfvare = p_vare
6.10 c                   time                    lfedat
6.10 c                   time                    lfetim
6.10 c                   eval      lfeusr = l_user
     c                   update    lfdtl3r
     c     key01         reade     lfdtl3r
     c                   enddo
      *
      * Oppdaterer varenummer i lager-trans-linje-register
      *
     c     key01         setll     lldtl3
     c     key01         reade     lldtl3r
     c                   dow       not %eof
     c                   eval      lsvare = p_vare
6.10 c                   time                    lsedat
6.10 c                   time                    lsetim
6.10 c                   eval      lseusr = l_user
     c                   update    lldtl3r
     c     key01         reade     lldtl3r
     c                   enddo
      *
      * Oppdaterer varenummer i lagertelling-bunt-linje-register
      *
     c     key01         setll     lbdtl3
     c     key01         reade     lbdtl3r
     c                   dow       not %eof
     c                   eval      lvvare = p_vare
6.10 c                   time                    lvedat
6.10 c                   time                    lvetim
6.10 c                   eval      lveusr = l_user
     c                   update    lbdtl3r
     c     key01         reade     lbdtl3r
     c                   enddo
      *
      * Oppdaterer varenummer i håndterminal-ordre-linje-register
      *
     c                   movel     w_gvar        hodtl2_vare
     c     hodtl2_key    setll     hodtl2
     c     hodtl2_key    reade     hodtl2r
     c                   dow       not %eof
     c                   movel     p_vare        hdvare
     c                   update    hodtl2r
     c     hodtl2_key    reade     hodtl2r
     c                   enddo
      *
      * Oppdaterer varenummer i prognose- og abcxyz-tabeller
      *
6.20  * Sjekk til-varenummer (ved sammenslåing), sletter evt gammel
6.20  *
6.20 c                   eval      p_dele = *off
6.20 c     key02         setll     mabdl9
6.20 c                   if        %equal(mabdl9)
6.20 c                   eval      p_dele = *on
6.20 c                   endif
     c     key01         setll     mabdl9
     c     key01         reade     mabdl9r
     c                   dow       not %eof
6.20 c                   if        p_dele = *on
6.20 c                   delete    mabdl9r
6.20 c                   else
     c                   eval      mbvare = p_vare
6.10 c                   time                    mbedat
6.10 c                   time                    mbetim
6.10 c                   eval      mbeusr = l_user
     c                   update    mabdl9r
6.20 c                   endif
     c     key01         reade     mabdl9r
     c                   enddo
      *
6.20 c                   eval      p_dele = *off
6.20 c     key02         setll     mprolu
6.20 c                   if        %equal(mprolu)
     c                   eval      p_dele = *on
     c                   endif
     c     key01         setll     mprolu
     c     key01         reade     mprolur
     c                   dow       not %eof
6.20 c                   if        p_dele = *on
6.20 c                   delete    mprolur
6.20 c                   else
     c                   eval      movare = p_vare
6.10 c                   time                    moedat
6.10 c                   time                    moetim
6.10 c                   eval      moeusr = l_user
     c                   update    mprolur
6.20 c                   endif
     c     key01         reade     mprolur
     c                   enddo
      *
6.20 c                   eval      p_dele = *off
6.20 c     key02         setll     mmatl5
6.20 c                   if        %equal(mmatl5)
6.20 c                   eval      p_dele = *on
6.20 c                   endif
     c     key01         setll     mmatl5
     c     key01         reade     mmatl5r
     c                   dow       not %eof
6.20 c                   if        p_dele = *on
6.20 c                   delete    mmatl5r
6.20 c                   else
     c                   eval      mmvare = p_vare
6.10 c                   time                    mmedat
6.10 c                   time                    mmetim
6.10 c                   eval      mmeusr = l_user
     c                   update    mmatl5r
6.20 c                   endif
     c     key01         reade     mmatl5r
     c                   enddo
      *
6.20 c                   eval      p_dele = *off
6.20 c     key02         setll     mprulu
6.20 c                   if        %equal(mprulu)
6.20 c                   eval      p_dele = *on
6.20 c                   endif
     c     key01         setll     mprulu
     c     key01         reade     mprulur
     c                   dow       not %eof
6.20 c                   if        p_dele = *on
6.20 c                   delete    mprulur
6.20 c                   else
     c                   eval      mrvare = p_vare
6.10 c                   time                    mredat
6.10 c                   time                    mretim
6.10 c                   eval      mreusr = l_user
     c                   update    mprulur
6.20 c                   endif
     c     key01         reade     mprulur
     c                   enddo
      *
6.20 c                   eval      p_dele = *off
6.20 c     key02         setll     mltil2
6.20 c                   if        %equal(mltil2)
6.20 c                   eval      p_dele = *on
6.20 c                   endif
     c     key01         setll     mltil2
     c     key01         reade     mltil2r
     c                   dow       not %eof
6.20 c                   if        p_dele = *on
6.20 c                   delete    mltil2r
6.20 c                   else
     c                   eval      mcvare = p_vare
6.10 c                   time                    mcedat
6.10 c                   time                    mcetim
6.10 c                   eval      mceusr = l_user
     c                   update    mltil2r
6.20 c                   endif
     c     key01         reade     mltil2r
     c                   enddo
      *
      * Oppdaterer varenummer i inng.faktura-linje-register
      *
     c     key01         setll     sidtlv
     c     key01         reade     sidtlvr
     c                   dow       not %eof
     c                   eval      slvare = p_vare
6.10 c                   time                    sledat
6.10 c                   time                    sletim
6.10 c                   eval      sleusr = l_user
     c                   update    sidtlvr
     c     key01         reade     sidtlvr
     c                   enddo
      *
      * Oppdaterer varenummer i Innkjøpsstatistikk-register
      *
     c     key01         setll     sistl2
     c     key01         reade     sistl2r
     c                   dow       not %eof
     c                   eval      sivare = p_vare
     c                   update    sistl2r
     c     key01         reade     sistl2r
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   if        p_inlr <> *on
     c                   eval      *inlr = *on
     c                   endif
      *
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_gvar
     c                   parm                    p_vare
     c                   parm                    p_inlr
      *
      * Key for oppslag/les på vare
      *
     c     key01         klist
     c                   kfld                    w_firm
     c                   kfld                    w_gvar
6.20  *
6.20  * Key for oppslag/les på til vare
6.20  *
6.20 c     key02         klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    w_nvar
      *
      * Key for oppdatering av håndterminal-ordre-linje
      *
     c     hodtl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    hodtl2_vare
      *
     c                   endsr
