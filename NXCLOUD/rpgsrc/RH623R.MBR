     h datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * System. . . . . : ASOKON                                              *
      * Program . . . . : RH623R                                        *
      * Beskrivelse . . : Hovedbok - saldo/budsjett - utskrift/parameter*
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      * V5.60      200908 Fjernet paramter for kladdeblokk         KOB  *
7.xx  * 7.xx      140618  Endringer reltart til ny GUI. NB Kan kun gjelde BKV
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KA = F1  = Valg av firma                                  *
      *       KC = F3  = Exit                                           *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *       30 = Protect av felter ved vise                           *
      *    31-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fausrl1    if   e           k disk
     frh623d    cf   e             workstn
      *
     d                uds
     d rhpkda                300    305  0
     d rhprÅr                         4  0
     d rhpper                         2  0
     d rhppef                         2  0
     d rhppet                         2  0
     d rhpavf                         4  0
     d rhpavt                         4  0
     d rhpbØt                         4  0
     d rhpbØf                         4  0
     d rhpktf                         5  0
     d rhpktt                         5  0
     d rhpsrt                         1
     d rhpsml                         1
     d rhpavv                         1
     d rhpavs                         1
     d rhpkgs                         1
     d rhpkks                         1
     d rhptyp                         1
     d rhphel                         1
      *
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d waar            s              4  0
     d wmnd            s              2  0
     d wpakod          s              2  0
     d p_in03          s              1
     d wwaar           s              2  0
     d w_firm          s                   like(l_firm)
     d w_user          s                   like(l_user)
      *
     c     dummy         tag
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av felter                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * A2BLD  - A2 Bilde for valg av parametre
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * A2BLD Behandle bilde for valg av utskriftskriteria              *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     a2taga        tag
      *
      * Bare data
      *
     c     a2tagb        tag
     c                   exfmt     a2bld
      *
      * Gå ut
      *
     c                   if        *inkc = *on                                  .....
     c                   move      '1'           p_in03                             .
     c                   goto      xslutt                                           .
     c                   endif                                                  .....
      *
      *  Kontroll avdeling
      *
     c                   if        a2pavf > a2pavt                                       .....
     c                   eval      *in31 = *on                                               .
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      *  Kontroll kostnadsbærer
      *
     c                   if        a2pbØf > a2pbØt                                       .....
     c                   eval      *in33 = *on                                               .
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      *  Kontroll konto
      *
     c                   if        a2pktf > a2pktt                                       .....
     c                   eval      *in32 = *on                                               .
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
      * Sjekke om kjøredato er gyldig
      *
     c                   if        a2pkda = *zero                                        .....
     c                   move      udate         a2pkda                                      .
     c                   endif                                                           .....
      *
     c                   move      a2pkda        wwaar
      *
     c     *dmy          test(d)                 a2pkda                 33
     c                   if        *in33 = *on                                           .....
     c                   goto      a2taga                                                    .
     c                   endif                                                           .....
      *
     c                   if        a2phel = *blank                                       .....
     c                   move      'H'           a2phel                                      .
     c                   endif                                                           .....
      *
      * Skriv parameter til LDA
      *
     c                   move      a2pkda        rhpkda
     c                   move      a2prÅr        rhprÅr
     c                   move      a2pper        rhpper
     c                   eval      rhppef = *zeros
     c                   eval      rhppet = *zeros
     c                   move      a2pavf        rhpavf
     c                   move      a2pavt        rhpavt
     c                   move      a2pbØf        rhpbØf
     c                   move      a2pbØt        rhpbØt
     c                   move      a2pktf        rhpktf
     c                   move      a2pktt        rhpktt
     c                   move      a2psrt        rhpsrt
     c                   eval      rhpsml = *blank
     c                   eval      rhpavv = *blank
     c                   move      a2pavs        rhpavs
     c                   move      a2pkgs        rhpkgs
     c                   move      a2pkks        rhpkks
     c                   move      a2ptyp        rhptyp
     c                   move      a2phel        rhphel
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /EJECT
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
     c                   eval      wpakod = *zero
      *
      * Legge inn firmanummer og navn fra parameter
      *
     c                   move      l_firm        w_firm
     c                   move      l_user        w_user
     c                   eval      a2pavf = *zeros
     c                   move      9999          a2pavt
     c                   eval      a2pbØf = *zeros
     c                   move      9999          a2pbØt
     c                   eval      a2pktf = *zeros
     c                   move      99999         a2pktt
     c                   move      'R'           a2ptyp
     c                   move      'K'           a2psrt
     c                   move      'N'           a2pavs
     c                   move      'J'           a2pkgs
     c                   move      'J'           a2pkks
     c                   move      'H'           a2phel
      *
     c                   move      udate         a2pkda
      *
      * Beregner periode
      *
     c                   move      *year         waar
      *
     c                   eval      wmnd = *month - 1
      *
     c                   if        wmnd = *zero                                          .....
     c                   movel     12            wmnd                                        .
     c                   eval      waar = waar - 1                                           .
     c                   move      waar          a2prÅr                                      .
     c                   else                                                                .
     c                   move      waar          a2prÅr                                      .
     c                   endif                                                           .....
      *
     c                   move      wmnd          a2pper
      *
      * Hent bruker-record
      *
     c     ausrl1_key    chain     ausrl1                             63
      *
      *  Sjekk om begrenset til avdeling
      *
     c                   if        abbavd = '1'                                          .....
     c                   eval      *in30 = *on                                               .
     c                   move      absavd        a2pavf                                      .
     c                   move      absavd        a2pavt                                      .
     c                   move      'K'           a2psrt                                      .
     c                   endif                                                           .....
      *
     c                   eval      p_in03 = ' '
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_in03
      *
      * Key for bruker-file
      *
     c     ausrl1_key    klist
     c                   kfld                    w_user
      *
     c                   endsr
