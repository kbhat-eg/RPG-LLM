     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VK100R
      * Beskrivelse . . : Varegruppeknytning, vedlikehold
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       040203 HKO  Nytt program
      *       051203 HKO  Test på firma
      * 6.10  290609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  201109 bof  Tar med firma i oppdatering.
7.xx  * 7.00  230216 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Rollup
      *       11 = Rolldown
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-99 = Arbeidsindikatorer
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvgkl1    if   e           k disk    rename(vvgkpfr:vvgkl1r)
     fvvgkl2    if   e           k disk    rename(vvgkpfr:vvgkl2r)
     fvvgkl3    if   e           k disk    rename(vvgkpfr:vvgkl3r)
     fvvgkl4    if   e           k disk    rename(vvgkpfr:vvgkl4r)
     fvvgkl5    if   e           k disk    rename(vvgkpfr:vvgkl5r)
     fvvgkl6    if   e           k disk    rename(vvgkpfr:vvgkl6r)
     fvvgklr    if   e           k disk    rename(vvgkpfr:vvgklrr)
     fvvgklu    uf a e           k disk    rename(vvgkpfr:vvgklur)
     fvogrl1    if   e           k disk    rename(vogrpfr:vogrl1r)
     fvhgrl1    if   e           k disk    rename(vhgrpfr:vhgrl1r)
     fvugrl1    if   e           k disk    rename(vugrpfr:vugrl1r)
     frlevl1    if   e           k disk    rename(rlevpfr:rlevl1r)
     fvaugl1    if   e           k disk    rename(vaugpfr:vaugl1r)
      *
     fvk100d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av variabler i nøkler
      *
     d vvgkl1_ogrp     s                   like(vkogrp)
     d vvgkl1_hgrp     s                   like(vkhgrp)
     d vvgkl1_ugrp     s                   like(vkugrp)
     d vvgkl1_ldor     s                   like(vkldor)
     d vvgkl2_hgrp     s                   like(vkhgrp)
     d vvgkl3_ugrp     s                   like(vkugrp)
     d vvgkl4_ldor     s                   like(vkldor)
     d vvgkl5_ahgr     s                   like(vkahgr)
     d vvgkl5_augr     s                   like(vkaugr)
     d vvgkl6_augr     s                   like(vkaugr)
     d vvgklr_ogrp     s                   like(vkogrp)
     d vvgklr_hgrp     s                   like(vkhgrp)
     d vvgklr_ugrp     s                   like(vkugrp)
     d vvgklr_ldor     s                   like(vkldor)
     d vvgklu_ogrp     s                   like(vkogrp)
     d vvgklu_hgrp     s                   like(vkhgrp)
     d vvgklu_ugrp     s                   like(vkugrp)
     d vvgklu_ldor     s                   like(vkldor)
     d vogrl1_oogr     s                   like(vgoogr)
     d vhgrl1_hogr     s                   like(vghogr)
     d vhgrl1_hhgr     s                   like(vghhgr)
     d vugrl1_uogr     s                   like(vguogr)
     d vugrl1_uhgr     s                   like(vguhgr)
     d vugrl1_uugr     s                   like(vguugr)
     d rlevl1_levr     s                   like(rllevr)
     d vaugl1_uhgr     s                   like(vfuhgr)
     d vaugl1_uugr     s                   like(vfuugr)
      *
      * Definering av variable
      *
     d w_fcrn          s              4  0
     d w_stel          s              4  0
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              2
     d b_oppr          s              1    inz(*off)
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(vgofir)
     d w_ogrp          s                   like(vgoogr)
     d w_hgrp          s                   like(vghhgr)
     d w_ugrp          s                   like(vguugr)
     d w_ldor          s                   like(vkldor)
     d w_otxt          s                   like(vgotxt)
     d w_htxt          s                   like(vghtxt)
     d w_utxt          s                   like(vgutxt)
     d w_lnav          s                   like(rlnavn)
      *
     d s_ogrp          s                   like(vkogrp)
     d s_hgrp          s                   like(vkhgrp)
     d s_ugrp          s                   like(vkugrp)
     d s_ldor          s                   like(vkldor)
      *
      * Definering av konstanter
      *
7.xx d c_sfil          s              2  0 inz(12)
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_fcrn - Første recordnummer på siden
      * w_stel - Subfile-teller
      * w_spge - Sidestørrelse i subfile (page)
      * w_srrn - Relativt recordnummer i subfile
      * w_sfrn - Første recordnummer på siste side
      * w_ssrn - Siste recordnummer på siste side
      * w_curn - Current recordnummer på siden
      * w_virn - Side og cursorplassering som skal vises
      * w_afld - Aktivt feltnavn
      * w_arow - Aktiv rad for cursor
      * w_acol - Aktiv kolonne for cursor
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1BLD  - C1 Skjermbilde for innlegging av nøkkel ved oppretting
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * C2BLD  - C2 Skjermbilde for opprette/endre/vise
      * D1WIN  - D1 Vindu for slette (Delete)
      * K1WIN  - K1 Vindu for kopiere
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *inke
     c                   exsr      forny
     c                   goto      b2tagb
     c                   when      *inkf
     c                   exsr      xc1bld
     c                   goto      b2taga
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in11
     c                   exsr      bck_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Spørring
      *
     c                   if        *inkd
      *
     c                   if        w_afld = 'B2AHGR'
     c                   eval      b2augr = 0
     c                   call      'VF510R'
     c                   parm                    w_firm
     c                   parm                    b2ahgr
     c                   parm                    w_htxt
     c                   goto      b2tagb
     c                   endif
      *
     c                   if        w_afld = 'B2AUGR'
     c                   call      'VF511R'
     c                   parm                    w_firm
     c                   parm                    b2ahgr
     c                   parm                    b2augr
     c                   parm                    w_utxt
     c                   goto      b2tagb
     c                   endif
      *
     c                   exsr      spØrring
     c                   goto      b2tagb
      *
     c                   endif
      *
      * Posisjonering
      *
     c                   if        b2ogrp <> 0 or
     c                             b2hgrp <> 0 or
     c                             b2ugrp <> 0 or
     c                             b2ldor <> 0 or
     c                             b2ahgr <> 0 or
     c                             b2augr <> 0
     c                   exsr      posisjoner
     c                   goto      b2tagb
     c                   endif
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for fornying av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     forny         begsr
      *
     c                   if        w_fcrn <> 0
     c     w_fcrn        chain     b1sfl
     c                   endif
      *
     c                   select
     c                   when      w_seqe = 'H '
     c                   eval      vvgkl2_hgrp = b1hgrp
     c     vvgkl2_key    setll     vvgkl2
     c                   when      w_seqe = 'U '
     c                   eval      vvgkl3_ugrp = b1ugrp
     c     vvgkl3_key    setll     vvgkl3
     c                   when      w_seqe = 'L '
     c                   eval      vvgkl4_ldor = b1ldor
     c     vvgkl4_key    setll     vvgkl4
     c                   when      w_seqe = 'AH'
     c                   eval      vvgkl5_ahgr = b1ahgr
     c                   eval      vvgkl5_augr = b1augr
     c     vvgkl5_key    setll     vvgkl5
     c                   when      w_seqe = 'AU'
     c                   eval      vvgkl6_augr = b1augr
     c     vvgkl6_key    setll     vvgkl6
     c                   other
     c                   eval      vvgkl1_ogrp = b1ogrp
     c                   eval      vvgkl1_hgrp = b1hgrp
     c                   eval      vvgkl1_ugrp = b1ugrp
     c                   eval      vvgkl1_ldor = b1ldor
     c     vvgkl1_key    setll     vvgkl1
     c                   endsl
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c     end_forny     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for posisjonering i subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     posisjoner    begsr
      *
      * Overgruppe
      *
     c                   if        b2ogrp <> 0
     c                   eval      w_seqe = *blank
     c                   eval      vvgkl1_ogrp = b2ogrp
     c                   eval      vvgkl1_hgrp = b2hgrp
     c                   eval      vvgkl1_ugrp = b2ugrp
     c                   eval      vvgkl1_ldor = b2ldor
     c     vvgkl1_key    setll     vvgkl1
     c                   goto      end_pos
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        b2hgrp <> 0
     c                   eval      w_seqe = 'H '
     c                   eval      vvgkl2_hgrp = b2hgrp
     c     vvgkl2_key    setll     vvgkl2
     c                   goto      end_pos
     c                   endif
      *
      * Undergruppe
      *
     c                   if        b2ugrp <> 0
     c                   eval      w_seqe = 'U '
     c                   eval      vvgkl3_ugrp = b2ugrp
     c     vvgkl3_key    setll     vvgkl3
     c                   goto      end_pos
     c                   endif
      *
      * Leverandør
      *
     c                   if        b2ldor <> 0
     c                   eval      w_seqe = 'L '
     c                   eval      vvgkl4_ldor = b2ldor
     c     vvgkl4_key    setll     vvgkl4
     c                   goto      end_pos
     c                   endif
      *
      * Alt. hovedgruppe
      *
     c                   if        b2ahgr <> 0
     c                   eval      w_seqe = 'AH'
     c                   eval      vvgkl5_ahgr = b2ahgr
     c                   eval      vvgkl5_augr = b2augr
     c     vvgkl5_key    setll     vvgkl5
     c                   goto      end_pos
     c                   endif
      *
      * Alt. undergruppe
      *
     c                   if        b2augr <> 0
     c                   eval      w_seqe = 'AU'
     c                   eval      vvgkl6_augr = b2augr
     c     vvgkl6_key    setll     vvgkl6
     c                   goto      end_pos
     c                   endif
      *
     c     end_pos       tag
      *
      * Blanker subfile, og fyller opp i.h.t. posisjonering
      *
     c                   eval      *in22 = *off
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
      * Blanker posisjoneringsfelter
      *
     c                   eval      b2ogrp = 0
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   eval      b2ldor = 0
     c                   eval      b2ahgr = 0
     c                   eval      b2augr = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   eval      b_forn = *off
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = w_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Endre
      *
     c                   if        b1valg = 2
     c                   eval      c1ogrp = b1ogrp
     c                   eval      c1hgrp = b1hgrp
     c                   eval      c1ugrp = b1ugrp
     c                   eval      c1ldor = b1ldor
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Kopier
      *
     c                   if        b1valg = 3
     c                   eval      k1ogrp = b1ogrp
     c                   eval      k1hgrp = b1hgrp
     c                   eval      k1ugrp = b1ugrp
     c                   eval      k1ldor = b1ldor
     c                   exsr      xk1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Slett
      *
     c                   if        b1valg = 4
     c                   eval      d1ogrp = b1ogrp
     c                   eval      d1hgrp = b1hgrp
     c                   eval      d1ugrp = b1ugrp
     c                   eval      d1ldor = b1ldor
     c                   exsr      xd1win
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      c1ogrp = b1ogrp
     c                   eval      c1hgrp = b1hgrp
     c                   eval      c1ugrp = b1ugrp
     c                   eval      c1ldor = b1ldor
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c     end_subfile   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av skjermbilde for ny rad (C1BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1bld        begsr
      *
     c                   eval      b_oppr = *on
     c                   eval      b_forn = *off
      *
     c                   eval      c1ogrp = b2ogrp
     c                   eval      c1hgrp = b2hgrp
     c                   eval      c1ugrp = b2ugrp
     c                   eval      c1ldor = b2ldor
      *
     c                   exsr      hent_info
      *
     c     c1taga        tag
      *
     c                   exfmt     c1bld
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      end_c1bld
     c                   when      *inkd
     c                   exsr      spØrring
     c                   goto      c1taga
     c                   endsl
      *
     c                   if        c1ogrp = 0 and
     c                             c1hgrp = 0 and
     c                             c1ugrp = 0 and
     c                             c1ldor = 0
     c                   goto      end_c1bld
     c                   endif
      *
      * Sjekker input
      *
     c                   eval      w_ogrp = c1ogrp
     c                   eval      w_hgrp = c1hgrp
     c                   eval      w_ugrp = c1ugrp
     c                   eval      w_ldor = c1ldor
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      c1taga
     c                   endif
      *
      * Sender opp informasjonsbilde dersom rad finnes fra før
      *
     c                   eval      vvgklr_ogrp = c1ogrp
     c                   eval      vvgklr_hgrp = c1hgrp
     c                   eval      vvgklr_ugrp = c1ugrp
     c                   eval      vvgklr_ldor = c1ldor
     c     vvgklr_key    chain     vvgklr
     c                   if        %found
     c                   exsr      xc1msg
     c                   if        b_anul = *on
     c                   goto      c1taga
     c                   endif
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   exsr      xc2bld
      *
     c                   eval      c1ogrp = 0
     c                   eval      c1otxt = *blank
     c                   eval      c1hgrp = 0
     c                   eval      c1htxt = *blank
     c                   eval      c1ugrp = 0
     c                   eval      c1utxt = *blank
     c                   eval      c1ldor = 0
     c                   eval      c1lnav = *blank
      *
     c                   goto      c1taga
      *
     c     end_c1bld     tag
      *
     c                   eval      b_oppr = *off
     c                   eval      b2ogrp = 0
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   eval      b2ldor = 0
      *
     c                   if        b_forn = *on
     c                   exsr      forny
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for utlegging av opprettelsesmelding
      * Meldingsbildet legges ut dersom det forsøkes å opprette en rad som
      * finnes fra før.
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc1msg        begsr
      *
     c                   eval      b_anul = *off
      *
     c                   exfmt     c1msg
      *
      * Behandling av funksjonstaster
      *
     c                   if        *inkc or *inkl
     c                   eval      b_anul = *on
     c                   goto      end_c1msg
     c                   endif
      *
     c     end_c1msg     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for ny/endring/vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c                   eval      vvgklr_ogrp = c1ogrp
     c                   eval      vvgklr_hgrp = c1hgrp
     c                   eval      vvgklr_ugrp = c1ugrp
     c                   eval      vvgklr_ldor = c1ldor
     c     vvgklr_key    chain     vvgklr
      *
     c                   if        %found
     c                   eval      c2ahgr = vkahgr
     c                   eval      c2augr = vkaugr
     c                   eval      c2eusr = vkeusr
     c     *dmy          move      vkodat        c2odat
     c     *dmy          move      vkedat        c2edat
     c                   else
     c                   eval      c2ahgr = 0
     c                   eval      c2augr = 0
     c                   eval      c2eusr = *blank
     c                   eval      c2odat = 0
     c                   eval      c2edat = 0
     c                   endif
      *
      * Henter beskrivelse av nivå
      *
     c                   exsr      hent_info
      *
      * Henter beskrivelse av alternativ varegruppe
      *
     c                   eval      c2autx = *blank
      *
     c                   if        c2ahgr <> 0
      *
     c                   eval      vaugl1_uhgr = c2ahgr
     c                   eval      vaugl1_uugr = c2augr
     c     vaugl1_key    chain     vaugl1r
     c                   if        %found
     c                   eval      c2autx = vfutxt
     c                   endif
      *
     c                   endif
      *
     c                   if        b1valg = 3
     c                   eval      c1ogrp = k1ogrp
     c                   eval      c1hgrp = k1hgrp
     c                   eval      c1ugrp = k1ugrp
     c                   eval      c1ldor = k1ldor
     c                   endif
      *
     c                   if        b1valg = 5
     c                   eval      *in30 = *on
     c                   endif
      *
     c     c2taga        tag
      *
     c                   exfmt     c2bld
      *
     c                   if        *inkc or *inkl or *in30
     c                   goto      end_c2bld
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd
      *
     c                   if        w_afld = 'C2AHGR'
     c                   eval      c2augr = 0
     c                   call      'VF510R'
     c                   parm                    w_firm
     c                   parm                    c2ahgr
     c                   parm                    c2autx
     c                   endif
      *
     c                   if        w_afld = 'C2AUGR'
     c                   call      'VF511R'
     c                   parm                    w_firm
     c                   parm                    c2ahgr
     c                   parm                    c2augr
     c                   parm                    c2autx
     c                   endif
      *
     c                   goto      c2taga
     c                   endif
      *
      * Validering av input
      *
     c                   if        c2ahgr = 0 or
     c                             c2augr = 0
     c                   eval      *in31 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   eval      vaugl1_uhgr = c2ahgr
     c                   eval      vaugl1_uugr = c2augr
     c     vaugl1_key    chain     vaugl1
     c                   if        not %found
     c                   eval      *in32 = *on
     c                   goto      c2taga
     c                   endif
      *
     c                   eval      c2autx = vfutxt
      *
      * Oppdatering
      *
     c                   clear                   vvgklur
      *
     c                   eval      vvgklu_ogrp = c1ogrp
     c                   eval      vvgklu_hgrp = c1hgrp
     c                   eval      vvgklu_ugrp = c1ugrp
     c                   eval      vvgklu_ldor = c1ldor
     c     vvgklu_key    chain     vvgklur
      *
     c                   eval      vkahgr = c2ahgr
     c                   eval      vkaugr = c2augr
      *
     c                   time                    vkedat
     c                   time                    vketim
     c                   eval      vkeusr = l_user
      *
     c                   if        %found
     c                   update    vvgklur
     c                   else
6.11 c                   eval      vkfirm = w_firm
     c                   eval      vkogrp = c1ogrp
     c                   eval      vkhgrp = c1hgrp
     c                   eval      vkugrp = c1ugrp
     c                   eval      vkldor = c1ldor
     c                   time                    vkodat
6.10 c                   time                    vkotim
6.10 c                   eval      vkousr = l_user
     c                   write     vvgklur
     c                   endif
      *
      * Oppdater subfile
      *
     c                   if        b1valg = 2
     c                   eval      b1ahgr = vkahgr
     c                   eval      b1augr = vkaugr
     c                   eval      b1autx = c2autx
     c                   endif
      *
     c                   if        b_oppr = *on or
     c                             b1valg = 3
     c                   eval      b_forn = *on
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   eval      *in30  = *off
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av slette-bildet (D1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xd1win        begsr
      *
     c                   eval      vvgklr_ogrp = d1ogrp
     c                   eval      vvgklr_hgrp = d1hgrp
     c                   eval      vvgklr_ugrp = d1ugrp
     c                   eval      vvgklr_ldor = d1ldor
     c     vvgklr_key    chain     vvgklr
      *
     c                   exfmt     d1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_d1win
     c                   endif
      *
     c                   eval      b_forn = *on
      *
      * Sletter rad
      *
     c                   eval      vvgklu_ogrp = d1ogrp
     c                   eval      vvgklu_hgrp = d1hgrp
     c                   eval      vvgklu_ugrp = d1ugrp
     c                   eval      vvgklu_ldor = d1ldor
     c     vvgklu_key    chain     vvgklur
      *
     c                   delete    vvgklur
      *
     c     end_d1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle kopierings-bildet (K1WIN)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xk1win        begsr
      *
      * Tar vare på nøkkel til rad det skal kopieres fra
      *
     c                   eval      s_ogrp = k1ogrp
     c                   eval      s_hgrp = k1hgrp
     c                   eval      s_ugrp = k1ugrp
     c                   eval      s_ldor = k1ldor
      *
      * Fyller kopierings-bildet
      *
     c                   eval      vvgklr_ogrp = k1ogrp
     c                   eval      vvgklr_hgrp = k1hgrp
     c                   eval      vvgklr_ugrp = k1ugrp
     c                   eval      vvgklr_ldor = k1ldor
     c     vvgklr_key    chain     vvgklr
      *
     c     k1taga        tag
      *
     c                   exfmt     k1win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_k1win
     c                   endif
      *
      * Spørring
      *
     c                   if        *inkd = *on
     c                   exsr      spØrring
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker input
      *
     c                   eval      w_ogrp = k1ogrp
     c                   eval      w_hgrp = k1hgrp
     c                   eval      w_ugrp = k1ugrp
     c                   eval      w_ldor = k1ldor
     c                   exsr      sjekk_input
     c                   if        b_feil = *on
     c                   goto      k1taga
     c                   endif
      *
      * Sjekker om rad finnes fra før
      *
     c                   eval      vvgklr_ogrp = k1ogrp
     c                   eval      vvgklr_hgrp = k1hgrp
     c                   eval      vvgklr_ugrp = k1ugrp
     c                   eval      vvgklr_ldor = k1ldor
     c     vvgklr_key    chain     vvgklr
     c                   if        %found
     c                   eval      *in32 = *on
     c                   goto      k1taga
     c                   endif
      *
      * Kaller vedlikeholdsprogram
      *
     c                   eval      c1ogrp = s_ogrp
     c                   eval      c1hgrp = s_hgrp
     c                   eval      c1ugrp = s_ugrp
     c                   eval      c1ldor = s_ldor
      *
     c                   exsr      xc2bld
      *
     c     end_k1win     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c                   select
     c                   when      w_seqe = 'H '
     c                   read      vvgkl2
     c                   when      w_seqe = 'U '
     c                   read      vvgkl3
     c                   when      w_seqe = 'L '
     c                   read      vvgkl4
     c                   when      w_seqe = 'AH'
     c                   read      vvgkl5
     c                   when      w_seqe = 'AU'
     c                   read      vvgkl6
     c                   other
     c                   read      vvgkl1
     c                   endsl
      *
     c                   if        %eof or
     c                             vkfirm <> w_firm
     c                   eval      *in15 = *on
     c                   leave
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   select
     c                   when      w_seqe = 'H '
     c                   eval      vvgkl2_hgrp = vkhgrp
     c                   when      w_seqe = 'U '
     c                   eval      vvgkl3_ugrp = vkugrp
     c                   when      w_seqe = 'L '
     c                   eval      vvgkl4_ldor = vkldor
     c                   when      w_seqe = 'AH'
     c                   eval      vvgkl5_ahgr = vkahgr
     c                   eval      vvgkl5_augr = vkaugr
     c                   when      w_seqe = 'AU'
     c                   eval      vvgkl6_augr = vkaugr
     c                   other
     c                   eval      vvgkl1_ogrp = vkogrp
     c                   eval      vvgkl1_hgrp = vkhgrp
     c                   eval      vvgkl1_ugrp = vkugrp
     c                   eval      vvgkl1_ldor = vkldor
     c                   endsl
      *
     c                   eval      b1valg = 0
     c                   eval      b1ogrp = vkogrp
     c                   eval      b1hgrp = vkhgrp
     c                   eval      b1ugrp = vkugrp
     c                   eval      b1utxt = *blank
     c                   eval      b1ldor = vkldor
     c                   eval      b1lnav = *blank
     c                   eval      b1ahgr = vkahgr
     c                   eval      b1augr = vkaugr
     c                   eval      b1autx = *blank
      *
     c                   if        b1ogrp <> 0
     c                   eval      vogrl1_oogr = b1ogrp
     c     vogrl1_key    chain     vogrl1
     c                   if        %found
     c                   eval      b1utxt = vgotxt
     c                   endif
     c                   endif
      *
     c                   if        b1hgrp <> 0
     c                   eval      vhgrl1_hogr = b1ogrp
     c                   eval      vhgrl1_hhgr = b1hgrp
     c     vhgrl1_key    chain     vhgrl1
     c                   if        %found
     c                   eval      b1utxt = vghtxt
     c                   endif
     c                   endif
      *
     c                   if        b1ugrp <> 0
     c                   eval      vugrl1_uogr = b1ogrp
     c                   eval      vugrl1_uhgr = b1hgrp
     c                   eval      vugrl1_uugr = b1ugrp
     c     vugrl1_key    chain     vugrl1
     c                   if        %found
     c                   eval      b1utxt = vgutxt
     c                   endif
     c                   endif
      *
     c                   if        b1ldor <> 0
     c                   eval      rlevl1_levr = b1ldor
     c     rlevl1_key    chain     rlevl1
     c                   if        %found
     c                   eval      b1lnav = rlnavn
     c                   endif
     c                   endif
      *
     c                   eval      vaugl1_uhgr = b1ahgr
     c                   eval      vaugl1_uugr = b1augr
     c     vaugl1_key    chain     vaugl1r
     c                   if        %found
     c                   eval      b1autx = vfutxt
     c                   endif
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å bla tilbake en side i subfilen
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     bck_subfile   begsr
      *
     c                   if        *in15
     c                   select
     c                   when      w_seqe = 'H '
     c     vvgkl2_key    setgt     vvgkl2
     c                   readp     vvgkl2
     c                   when      w_seqe = 'U '
     c     vvgkl3_key    setgt     vvgkl3
     c                   readp     vvgkl3
     c                   when      w_seqe = 'L '
     c     vvgkl4_key    setgt     vvgkl4
     c                   readp     vvgkl4
     c                   when      w_seqe = 'AH'
     c     vvgkl5_key    setgt     vvgkl5
     c                   readp     vvgkl5
     c                   when      w_seqe = 'AU'
     c     vvgkl6_key    setgt     vvgkl6
     c                   readp     vvgkl6
     c                   other
     c     vvgkl1_key    setgt     vvgkl1
     c                   readp     vvgkl1
     c                   endsl
     c                   endif
      *
      * Leser tilbake en side
      *
     c                   eval      w_stel = 0
      *
     c                   dou       w_stel = w_ssrn + c_sfil
      *
     c                   select
     c                   when      w_seqe = 'H '
     c                   readp     vvgkl2
     c                   when      w_seqe = 'U '
     c                   readp     vvgkl3
     c                   when      w_seqe = 'L '
     c                   readp     vvgkl4
     c                   when      w_seqe = 'AH'
     c                   readp     vvgkl5
     c                   when      w_seqe = 'AU'
     c                   readp     vvgkl6
     c                   other
     c                   readp     vvgkl1
     c                   endsl
      *
     c                   if        %eof or
     c                             vkfirm <> w_firm
     c                   leave
     c                   endif
      *
     c                   eval      w_stel = w_stel + 1
      *
     c                   select
     c                   when      w_seqe = 'H '
     c                   eval      vvgkl2_hgrp = vkhgrp
     c                   when      w_seqe = 'U '
     c                   eval      vvgkl3_ugrp = vkugrp
     c                   when      w_seqe = 'L '
     c                   eval      vvgkl4_ldor = vkldor
     c                   when      w_seqe = 'AH'
     c                   eval      vvgkl5_ahgr = vkahgr
     c                   eval      vvgkl5_augr = vkaugr
     c                   when      w_seqe = 'AU'
     c                   eval      vvgkl6_augr = vkaugr
     c                   other
     c                   eval      vvgkl1_ogrp = vkogrp
     c                   eval      vvgkl1_hgrp = vkhgrp
     c                   eval      vvgkl1_ugrp = vkugrp
     c                   eval      vvgkl1_ldor = vkldor
     c                   endsl
      *
     c                   enddo
      *
     c                   if        %eof
     c                   select
     c                   when      w_seqe = 'H '
     c     vvgkl2_key    setll     vvgkl2
     c                   when      w_seqe = 'U '
     c     vvgkl3_key    setll     vvgkl3
     c                   when      w_seqe = 'L '
     c     vvgkl4_key    setll     vvgkl4
     c                   when      w_seqe = 'AH'
     c     vvgkl5_key    setll     vvgkl5
     c                   when      w_seqe = 'AU'
     c     vvgkl6_key    setll     vvgkl6
     c                   other
     c     vvgkl1_key    setll     vvgkl1
     c                   endsl
     c                   endif
      *
      * Blanker og fyller opp subfile
      *
     c                   exsr      clr_subfile
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
      *
     c                   eval      w_fcrn = d_fcrn
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av spørring
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     spØrring      begsr
      *
      * Overgruppe
      *
     c                   if        w_afld = 'B2OGRP'
     c                   eval      b2hgrp = 0
     c                   eval      b2ugrp = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    w_otxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1OGRP'
     c                   eval      c1hgrp = 0
     c                   eval      c1ugrp = 0
     c                   eval      c1otxt = *blank
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    c1ogrp
     c                   parm                    c1otxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1OGRP'
     c                   eval      k1hgrp = 0
     c                   eval      k1ugrp = 0
     c                   call      'VG510R'
     c                   parm                    w_firm
     c                   parm                    k1ogrp
     c                   parm                    w_otxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_afld = 'B2HGRP'
     c                   eval      b2ugrp = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    w_htxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1HGRP'
     c                   eval      c1ugrp = 0
     c                   eval      c1htxt = *blank
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    c1ogrp
     c                   parm                    c1hgrp
     c                   parm                    c1htxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1HGRP'
     c                   eval      k1ugrp = 0
     c                   call      'VG511R'
     c                   parm                    w_firm
     c                   parm                    k1ogrp
     c                   parm                    k1hgrp
     c                   parm                    w_htxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_afld = 'B2UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    b2ogrp
     c                   parm                    b2hgrp
     c                   parm                    b2ugrp
     c                   parm                    w_utxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1UGRP'
     c                   eval      c1utxt = *blank
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    c1ogrp
     c                   parm                    c1hgrp
     c                   parm                    c1ugrp
     c                   parm                    c1utxt
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1UGRP'
     c                   call      'VG512R'
     c                   parm                    w_firm
     c                   parm                    k1ogrp
     c                   parm                    k1hgrp
     c                   parm                    k1ugrp
     c                   parm                    w_utxt
     c                   goto      end_spØrr
     c                   endif
      *
      * Leverandør
      *
     c                   if        w_afld = 'B2LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    b2ldor
     c                   parm                    w_lnav
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'C1LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    c1ldor
     c                   parm                    c1lnav
     c                   goto      end_spØrr
     c                   endif
      *
     c                   if        w_afld = 'K1LDOR'
     c                   call      'RL500R'
     c                   parm                    w_firm
     c                   parm                    k1ldor
     c                   parm                    w_lnav
     c                   goto      end_spØrr
     c                   endif
      *
     c     end_spØrr     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Sjekker inntastede verdier, og henter beskrivelser
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     sjekk_input   begsr
      *
     c                   eval      b_feil = *off
      *
     c                   eval      w_otxt = *blank
     c                   eval      w_htxt = *blank
     c                   eval      w_utxt = *blank
     c                   eval      w_lnav = *blank
      *
      * Sjekk nivå
      *
     c                   if        w_ogrp = 0
     c                   eval      *in31  = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
      *
      * Overgruppe
      *
     c                   if        w_ogrp <> 0 and
     c                             w_hgrp = 0 and
     c                             w_ugrp = 0
     c                   eval      vogrl1_oogr = w_ogrp
     c     vogrl1_key    chain     vogrl1                             35
     c                   if        *in35 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      w_otxt = vgotxt
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        w_hgrp <> 0 and
     c                             w_ugrp = 0
     c                   eval      vhgrl1_hogr = w_ogrp
     c                   eval      vhgrl1_hhgr = w_hgrp
     c     vhgrl1_key    chain     vhgrl1                             36
     c                   if        *in36 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      w_htxt = vghtxt
     c                   endif
      *
      * Undergruppe
      *
     c                   if        w_ugrp <> 0
     c                   eval      vugrl1_uogr = w_ogrp
     c                   eval      vugrl1_uhgr = w_hgrp
     c                   eval      vugrl1_uugr = w_ugrp
     c     vugrl1_key    chain     vugrl1                             37
     c                   if        *in37 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      w_utxt = vgutxt
     c                   endif
      *
      * Leverandør
      *
     c                   if        w_ldor <> 0
     c                   eval      rlevl1_levr = w_ldor
     c     rlevl1_key    chain     rlevl1                             38
     c                   if        *in38 = *on
     c                   eval      b_feil = *on
     c                   goto      end_sjekk
     c                   endif
     c                   eval      w_lnav = rlnavn
     c                   endif
      *
     c     end_sjekk     endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Henter beskrivelse
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hent_info     begsr
      *
     c                   eval      c1otxt = *blank
     c                   eval      c1htxt = *blank
     c                   eval      c1utxt = *blank
      *
      * Overgruppe
      *
     c                   if        c1ogrp <> 0
     c                   eval      vogrl1_oogr = c1ogrp
     c     vogrl1_key    chain     vogrl1
     c                   if        %found
     c                   eval      c1otxt = vgotxt
     c                   endif
     c                   endif
      *
      * Hovedgruppe
      *
     c                   if        c1hgrp <> 0
     c                   eval      vhgrl1_hogr = c1ogrp
     c                   eval      vhgrl1_hhgr = c1hgrp
     c     vhgrl1_key    chain     vhgrl1
     c                   if        %found
     c                   eval      c1htxt = vghtxt
     c                   endif
     c                   endif
      *
      * Undergruppe
      *
     c                   if        c1ugrp <> 0
     c                   eval      vugrl1_uogr = c1ogrp
     c                   eval      vugrl1_uhgr = c1hgrp
     c                   eval      vugrl1_uugr = c1ugrp
     c     vugrl1_key    chain     vugrl1
     c                   if        %found
     c                   eval      c1utxt = vgutxt
     c                   endif
     c                   endif
      *
      * Leverandør
      *
     c                   if        c1ldor <> 0
     c                   eval      rlevl1_levr = c1ldor
     c     rlevl1_key    chain     rlevl1
     c                   if        %found
     c                   eval      c1lnav = rlnavn
     c                   endif
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
      * Key for posisjonering på overgruppe
      *
     c     vvgkl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvgkl1_ogrp
     c                   kfld                    vvgkl1_hgrp
     c                   kfld                    vvgkl1_ugrp
     c                   kfld                    vvgkl1_ldor
      *
      * Key for posisjonering på hovedgruppe
      *
     c     vvgkl2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvgkl2_hgrp
      *
      * Key for posisjonering på undergruppe
      *
     c     vvgkl3_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvgkl3_ugrp
      *
      * Key for posisjonering på leverandør
      *
     c     vvgkl4_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvgkl4_ldor
      *
      * Key for posisjonering på alternativ hovedgruppe
      *
     c     vvgkl5_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvgkl5_ahgr
     c                   kfld                    vvgkl5_augr
      *
      * Key for posisjonering på alternativ undergruppe
      *
     c     vvgkl6_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvgkl6_augr
      *
      * Key for oppslag på varegruppeknytning
      *
     c     vvgklr_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvgklr_ogrp
     c                   kfld                    vvgklr_hgrp
     c                   kfld                    vvgklr_ugrp
     c                   kfld                    vvgklr_ldor
      *
      * Key for oppdatering av varegruppeknytning
      *
     c     vvgklu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvgklu_ogrp
     c                   kfld                    vvgklu_hgrp
     c                   kfld                    vvgklu_ugrp
     c                   kfld                    vvgklu_ldor
      *
      * Key for oppslag på overgruppe
      *
     c     vogrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vogrl1_oogr
      *
      * Key for oppslag på hovedgruppe
      *
     c     vhgrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vhgrl1_hogr
     c                   kfld                    vhgrl1_hhgr
      *
      * Key for oppslag på undergruppe
      *
     c     vugrl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vugrl1_uogr
     c                   kfld                    vugrl1_uhgr
     c                   kfld                    vugrl1_uugr
      *
      * Key for oppslag på leverandør
      *
     c     rlevl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rlevl1_levr
      *
      * Key for oppslag på alternativ undergruppe
      *
     c     vaugl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vaugl1_uhgr
     c                   kfld                    vaugl1_uugr
      *
      *
      * Henter verdier fra LDA, og initierer skjermbildet
      *
     c                   eval      w_firm = l_firm
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      vvgkl1_ogrp = 0
     c                   eval      vvgkl1_hgrp = 0
     c                   eval      vvgkl1_ugrp = 0
     c                   eval      vvgkl1_ldor = 0
     c     vvgkl1_key    setll     vvgkl1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
