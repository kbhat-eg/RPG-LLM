     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLOGI
      * Program . . . . : MM740R
      * Beskrivelse . . : Leser vareregister
      *                   Oppdaterer lager med lagerverdier
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       280105 bø   Nytt program
      *       020107 BØ   Hvis avvik på best.mengde mer enn 100 % dannes
      *                   avviksreg.
      *       050607 BØ   Lagt inn test om sperre for oppd. prognose
      *       210607 BØ   Innkjøpsparm.oppd. uten des. til lager untatt
      *                   spesielle enhet. Hardkodet nå - bør inn i reg.5.60
5.60  *       310108 BØ   Legger på endr.dato og bruker
5.61  *       050408 bø   Lagerenhet fra vare-register
      * 6.10  260609 BSA  Oppdatert med sporingsinformasjon
6.20  * 6.20  070211 nho   Byttet ut fil mpspxx med vlagxx
6.21  * 6.20  310812 bof  Nytt felt, endringsdato for faste data som vises
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fmprol1    if   e           k disk    rename(mpropfr:mprol1r)
6.20 fvlaglr    if   e           k disk    rename(vlagpfr:vlaglrr)
     fvlaglu    uf a e           k disk    rename(vlagpfr:vlaglur)
     fmpralu    uf a e           k disk    rename(mprapfr:mpralur)
      *
      *
     d vlaglu_vare     s                   like(vlvare)
     d vlaglu_lage     s                   like(vllage)
     d mprola_vare     s                   like(movare)
     d mprol2_vsor     s                   like(movsor)
     d mprol3_ogrp     s                   like(moogrp)
     d mprol3_hgrp     s                   like(mohgrp)
     d mprol3_ugrp     s                   like(mougrp)
     d mprolx_ogrp     s                   like(moogrp)
     d mprolx_hgrp     s                   like(mohgrp)
     d mproly_ogrp     s                   like(moogrp)
     d mprolz_vare     s                   like(vvvare)
     d mpralu_vare     s                   like(mqvare)
     d mpralu_lage     s                   like(mqlage)
6.20 d vlaglr_vare     s                   like(vlvare)
6.20 d vlaglr_lage     s                   like(vllage)
5.61 d vvarl1_vare     s                   like(vvvare)
      *
     d w_firm          s                   like(vvfirm)
     d p_pvsor         s                   like(movsor)
     d p_pogrp         s                   like(moogrp)
     d p_phgrp         s                   like(mohgrp)
     d p_pugrp         s                   like(mougrp)
     d p_pldor         s              6  0
     d p_pvare         s                   like(movare)
     d p_plage         s                   like(molage)
     d p_pcrdo         s              1  0
     d p_list          s            128
     d w_avvik         s              9  1
     d w_udat          s               d   datfmt(*iso)
     d w_b90           s              9  0
     d w_b91           s              9  1
      *
      * Local Data Area
      *
     d                uds
      *
     d l_user                911    920
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
      *
      * Definering av variable
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c                   read      mprol1
     c                   dow       not %eof
      *
      * sjekker om det finnes sperre på oppdatering
      *
6.20 c                   eval      vlaglr_vare = movare
6.20 c                   eval      vlaglr_lage = molage
6.20 c     vlaglr_key    chain     vlaglr
6.20 c                   if        %found(vlaglr)
6.20 c                   if        vlstda <> *loval and
6.20 c                             vlstda > w_udat
     c                   goto      neste
     c                   endif
     c                   endif
      *
     c                   exsr      upd_lager
      *
     c     neste         tag
     c                   read      mprol1
     c                   enddo
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     avslutt       tag
      *
     c                   return
      *
      * --------------------------------------------------------------
      *  behandler lest vare / lager
      * --------------------------------------------------------------
      *
     c     upd_lager     begsr
      *
     c                   exsr      sjekk_avvik
5.61 c                   eval      vvarl1_vare = movare
5.61 c     vvarl1_key    chain     vvarl1
      *
     c                   clear                   vlaglur
     c                   eval      vlaglu_vare = movare
     c                   eval      vlaglu_lage = molage
     c     vlaglu_key    chain     vlaglu
5.61 c                   if        vvenhl <> 'LM ' and
5.61 c                             vvenhl <> 'PAL' and
5.61 c                             vvenhl <> 'M  ' and
5.61 c                             vvenhl <> 'LTR' and
5.61 c                             vvenhl <> 'KG ' and
5.61 c                             vvenhl <> 'M2 ' and
5.61 c                             vvenhl <> 'M3 '
      * ingen desimal
     c                   eval(h)   w_b90  = mobpun
     c                   eval      vlbpun = w_b90
     c                   eval(h)   w_b90  = mobmen
     c                   eval      vlbmen = w_b90
     c                   eval(h)   w_b90  = momini
     c                   eval      vlmini = w_b90
     c                   else
      * 1 desimal
     c                   eval(h)   w_b91  = mobpun
     c                   eval      vlbpun = w_b91
     c                   eval(h)   w_b91  = mobmen
     c                   eval      vlbmen = w_b91
     c                   eval(h)   w_b91  = momini
     c                   eval      vlmini = w_b91
     c                   endif
     c                   time                    vledat
6.10 c                   time                    vletim
     c                   eval      vleusr = l_user
6.21 c                   time                    vlfeda
6.21 c                   time                    vlfeti
6.21 c                   eval      vlfeus = l_user
     c                   if        %found(vlaglu)
     c                   update    vlaglur
     c                   else
     c                   eval      vlfirm = mofirm
     c                   eval      vlvare = movare
     c                   eval      vllage = molage
6.10 c                   time                    vlodat
6.10 c                   time                    vlotim
6.10 c                   eval      vlousr = l_user
     c                   write     vlaglur
     c                   endif
      *
     c                   endsr
      *
      * --------------------------------------------------------------
      *  sjekker om det er større avvik enn 100 %
      * --------------------------------------------------------------
      *
     c     sjekk_avvik   begsr
      *
     c                   if        vlbmen <> 0 and
     c                             mobmen <> 0
     c                   eval(h)   w_avvik = ((mobmen - vlbmen)/mobmen)*100
     c                   if        w_avvik < 0
     c                   eval      w_avvik = w_avvik * -1
     c                   endif
     c                   if        w_avvik >  100,0
     c                   clear                   mpralur
     c                   eval      mpralu_vare = movare
     c                   eval      mpralu_lage = molage
     c     mpralu_key    chain     mpralu
     c                   eval      mqbmeg = vlbmen
     c                   eval      mqbmen = mobmen
     c                   if        %found(mpralu)
     c                   update    mpralur
     c                   else
     c                   eval      mqfirm = mofirm
     c                   eval      mqvare = movare
     c                   eval      mqlage = molage
     c                   write     mpralur
     c                   endif
     c                   endif
      *
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
5.61  *
5.61  *  Key for les av vare
5.61  *
5.61 c     vvarl1_key    klist
5.61 c                   kfld                    w_firm
5.61 c                   kfld                    vvarl1_vare
      *
      *  Key for les av innkjøpsparameter
      *
     c     mprol1_key    klist
     c                   kfld                    w_firm
      *
      * Key for oppdater lager
      *
     c     vlaglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglu_vare
     c                   kfld                    vlaglu_lage
      *
      * Key for oppdater av stort avvik for best.mengde
      *
     c     mpralu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    mpralu_vare
     c                   kfld                    mpralu_lage
      *
      *  Key for les av sperre for oppdatering fra prognose
      *
6.20 c     vlaglr_key    klist
6.20 c                   kfld                    w_firm
6.20 c                   kfld                    vlaglr_vare
6.20 c*                  kfld                    vlaglr_lage
      *
     c                   eval      w_firm = l_firm
     c                   time                    w_udat
      *
     c                   endsr
