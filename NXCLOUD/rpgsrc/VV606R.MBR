     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : VV606R
      * Beskrivelse . . : Vare, utskrift av lager-opplysninger
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       300998 HKO  Nytt program
5.70  * PRGR  241008 bof  Innført prisgruppe /varetupe fra lager
6.21  * 6.20  190511 bø   Utvidet vl710r med div. info
6.30  * 6.30  170918 bkv  Utvidet med trelast sortiment
      *
8.01  *PRG2   150622 bsa  Utvidet prisgruppe fra 1 til 2 posisjoner
8.01  *PRG2               Endringer består kun i kompilering, da alle
8.01  *PRG2               referanser er eksternt definert.
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvawpf    if   e           k disk
      *
     fafirl1    if   e           k disk    rename(afirpfr:afirl1r)
6.30 fvvlel1    if   e           k disk    rename(vvlepfr:vvlel1r)
      *
     fvv606p    o    e             printer
      *
      * Local data area
      *
     d                uds
     d l_wsid                901    910
     d l_user                911    920
5.70 d l_lagr               1001   1002  0
      *
      * Definering av datastruktur
      *
      * Datastruktur for parameterliste -inn
      *
     d                 ds
     d d_list                        80
     d  d_firm                        3  0 overlay(d_list:1)
     d  d_fogr                        2  0 overlay(d_list:4)
     d  d_fhgr                        2  0 overlay(d_list:6)
     d  d_fugr                        3  0 overlay(d_list:8)
     d  d_togr                        2  0 overlay(d_list:11)
     d  d_thgr                        2  0 overlay(d_list:13)
     d  d_tugr                        3  0 overlay(d_list:15)
     d  d_flev                        6  0 overlay(d_list:18)
     d  d_tlev                        6  0 overlay(d_list:24)
     d  d_fvar                       15    overlay(d_list:30)
     d  d_tvar                       15    overlay(d_list:45)
     d  d_dekn                        4  2 overlay(d_list:60)
     d  d_utsk                        1  0 overlay(d_list:64)
     d  d_srt1                        1  0 overlay(d_list:65)
     d  d_srt2                        1  0 overlay(d_list:66)
     d  d_srt3                        1  0 overlay(d_list:67)
     d  d_srt4                        1  0 overlay(d_list:68)
      *
      * Definering av parametre
      *
     d p_list          s             80
      *
      * Definering av variable
      *
     d b_inlr          s              1    inz(*off)
     d w_udat          s               d   datfmt(*iso)
     d w_pdat          s               d   datfmt(*iso)
     d w_lety          s              1  0
     d w_sapr          s              9  2
     d w_bupr          s              9  2
     d w_timestamp     s             12  0
      *
     d w_firm          s                   like(vvfirm)
5.70 d w_lage          s              2  0
5.70 d w_vtyp          s                   like(vvvtyp)
6.21 d w_utda          s                   like(vvudat)
6.21 d w_ldor          s                   like(vvldor)
6.30 d w_lv_nobb       s                   like(vvlnob)
6.30 d w_lv_modn       s                   like(vvlmod)
6.30 d w_lv_lldo       s                   like(vvlnle)
6.30 d w_lv_llva       s                   like(vvllva)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Utskrift av header
      *
     c                   exsr      hode
      *
      * Utskrift av vareregister
      *
     c                   exsr      behandling
      *
      * Avslutt
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering og utskrift av hode-opplysninger
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     hode          begsr
      *
      * Legger inn arb.stasjon og bruker i faste felter
      *
     c                   eval      a1firm = w_firm
     c                   eval      a1user = l_user
     c                   eval      a1wsid = l_wsid
     c                   time                    w_timestamp
     c                   move      w_timestamp   a1date
     c                   movel     w_timestamp   a1time
      *
      * Henter firmaopplysninger
      *
     c     afirl1_key    chain     afirl1r                            90
     c                   eval      a1fnav = aafnav
      *
      * Henter parametre
      *
     c                   eval      a2fogr = d_fogr
     c                   eval      a2fhgr = d_fhgr
     c                   eval      a2fugr = d_fugr
     c                   eval      a2togr = d_togr
     c                   eval      a2thgr = d_thgr
     c                   eval      a2tugr = d_tugr
     c                   eval      a2flev = d_flev
     c                   eval      a2tlev = d_tlev
     c                   eval      a2fvar = d_fvar
     c                   eval      a2tvar = d_tvar
      *
     c                   select
     c                   when      d_srt1 = 1
     c                   eval      a2sort = 'Varegruppe'
     c                   when      d_srt2 = 1
     c                   eval      a2sort = 'Leverandør'
     c                   when      d_srt3 = 1
     c                   eval      a2sort = 'Varetekst'
     c                   when      d_srt4 = 1
     c                   eval      a2sort = 'Varenummer'
     c                   endsl
      *
     c                   select
     c                   when      d_srt1 = 2
     c                   eval      a2sort = %trim(a2sort) + ' - Varegruppe'
     c                   when      d_srt2 = 2
     c                   eval      a2sort = %trim(a2sort) + ' - Leverandør'
     c                   when      d_srt3 = 2
     c                   eval      a2sort = %trim(a2sort) + ' - Varetekst'
     c                   when      d_srt4 = 2
     c                   eval      a2sort = %trim(a2sort) + ' - Varenummer'
     c                   endsl
      *
     c                   select
     c                   when      d_srt1 = 3
     c                   eval      a2sort = %trim(a2sort) + ' - Varegruppe'
     c                   when      d_srt2 = 3
     c                   eval      a2sort = %trim(a2sort) + ' - Leverandør'
     c                   when      d_srt3 = 3
     c                   eval      a2sort = %trim(a2sort) + ' - Varetekst'
     c                   when      d_srt4 = 3
     c                   eval      a2sort = %trim(a2sort) + ' - Varenummer'
     c                   endsl
      *
      * Skriver heading
      *
     c                   write     a1hdr
     c                   write     a2hdr
     c                   write     a3hdr
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Leser arbeidsregsiter, og behandler utskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     behandling    begsr
      *
     c                   read      vvawpf                                 90
     c                   dow       *in90 = *off
      *
      * henter varetype
      *
5.70 c                   call      'VL710R'
5.70 c                   parm                    w_firm
5.70 c                   parm                    vvvare
5.70 c                   parm                    w_lage
5.70 c                   parm                    w_vtyp
6.21 c                   parm                    w_utda
6.21 c                   parm                    w_ldor
6.21 c                   parm                    b_inlr
6.30 c                   parm                    w_lv_nobb
6.30 c                   parm                    w_lv_modn
6.30 c                   parm                    w_lv_lldo
6.30 c                   parm                    w_lv_llva
      *
      * Hopper over skaffevarer, diverse-varer og tjenester
      *
5.70 c                   if        w_vtyp = 'S' or
5.70 c                             w_vtyp = 'D' or
5.70 c                             w_vtyp = 'T'
     c                   goto      neste_vare
     c                   endif
      *
      * Skriver detaljlinje
      *
     c                   exsr      detalj
      *
      * Leser neste rad
      *
     c     neste_vare    tag
      *
     c                   read      vvawpf                                 90
     c                   enddo
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine utskrift av detaljlinje
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     detalj        begsr
      *
     c                   eval      d1ogrp = vvogrp
     c                   eval      d1hgrp = vvhgrp
     c                   eval      d1ugrp = vvugrp
     c                   eval      d1ldor = vvldor
     c                   eval      d1tek1 = vvtek1
     c                   eval      d1vare = vvvare
     c                   eval      d1enhl = vvenhl
     c                   eval      d1enhe = vvenhe
     c                   eval      d1mini = vvmini
     c                   eval      d1maks = vvmaks
     c                   eval      d1sikl = vvsikl
     c                   eval      d1bpun = vvbpun
     c                   eval      d1bmen = vvbmen
     c                   eval      d1ltid = vvltid
     c                   eval      d1plas = vvplas
     c                   eval      d1pdat = 0
      *
      * Henter prisinformasjon
      *
     c                   call      'VP702R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   parm                    vvenhl
     c                   parm                    w_lety
     c                   parm                    w_udat
     c                   parm                    b_inlr
     c                   parm                    w_sapr
     c                   parm                    w_bupr
     c                   parm                    d1kopr
     c                   parm                    d1inpr
     c                   parm                    w_pdat
     c                   parm                    d1omre
      *
     c                   if        w_pdat <> *loval
     c     *dmy          move      w_pdat        d1pdat
     c                   endif
      *
      * Skriv detaljlinje
      *
     c                   write     d1dtl                                30
     c                   exsr      overflow
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av overflow
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     overflow      begsr
      *
     c                   if        *in30 = *on
     c                   write     a1hdr
     c                   write     a3hdr
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_list
      *
      * Key for oppslag på firma
      *
     c     afirl1_key    klist
     c                   kfld                    w_firm
      *
      *
      * Kopierer parameterliste til datastruktur
      *
     c                   eval      d_list = p_list
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = d_firm
      *
5.70 c                   eval      w_lage = l_lagr
      *
      * Henter dagens dato
      *
     c                   time                    w_udat
      *
     c                   endsr
