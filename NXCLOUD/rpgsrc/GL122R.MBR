      /TITLE  ASOKON Arbeid med deklarasjonskoder
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * System  . . . . : ASOKON                                        *
      * Program . . . . : GL122R                                        *
      * Beskrivelse . . : Arbeid med deklarasjonskoder                  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      * Spesialversjon. :                                               *
      * Tilpasset for . :                                               *
      *                                                                 *
      * Referanse  Dato   Endringsbeskrivelse                      Sign.*
      * --------- ------  ----------------------------------------------*
      *           300195  Nytt program januar-1995                 RH   *
7.xx  * 7.00      251116  Endringer reltart til ny GUI. NB Kan kun BSA  *
7.xx  *                   gjelde DDS.
      *                                                                 *
      *                                                                 *
      *                                                                 *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *                                                                 *
      *  BRUK AV INDIKATORER                                            *
      *                                                                 *
      *       KC = F3  = Exit                                           *
      *       KE = F5  = Forny                                          *
      *       KL = F12 = Annuller                                       *
      *                                                                 *
      *       LR = Avslutt program                                      *
      *   SUBFILE-1:                                                    *
      *       10 = Roll                                                 *
      *       12 = Sfldsp                                               *
      *       13 = Sfldspctl                                            *
      *       14 = Sflclr                                               *
      *       15 = Sflend                                               *
      *       16 = Readc 16 on, no more records                         *
      *                                                                 *
      *    30-59 = Varsle m/feilmeldinger/skjermindikatorer             *
      *       40 = Er på dersom dek.kd kun skal VISES.                  *
      *    60-69 = Fileindikatorer chain etc.                           *
      *    70-79 = Arbeidsindikatorer i sub-rutiner                     *
      *    80-89 = Arbeidsindikatorer ordinære                          *
      *    90-99 = Benyttes ved std. sub-rutiner                        *
      *                                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     fgl122d    cf   e             workstn
     f                                     sfile(d1wsf:srrn01)
     fgudkl1    uf a e           k disk
      *
      *
     d txt             s             20    dim(1) ctdata perrcd(1)
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Definering av felter                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      *     nn - er 01 for subfile 01,
      *             02 for subfile 02,
      *             03 for subfile 03, o.s.v.
      * SRRNnn - Relativt recordnummer i subfile
      * SPGEnn - Sidestørrelse i subfile (page)
      * SFRNnn - Første recordnummer på siden
      * SSRNnn - Siste  recordnummer på siden
      * SVRNnn - Vis den side som inneholder denne record.
      *
      * Forklaring på spesielle felter
      * som benyttes ved windows.
      * - - - - - - - - - - - - - - - - -
      *
      * w_arow - Actual line, angir startlinje for vindu.
      * w_acol - Actual posision, angir startposisjon for vindu.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      *
      * A1WIN  - dek.kdopplysninger.
      * S1WIN  - Window.
      * D1WSF  - C1 Subfile window - record
      * D2WCT  - C2 Subfile window - control record - Til oppdatering.
      * D2CMD  - C2 Eget skjermbilde for CMD-taster - Window definisjon
      *
      /eject
      *
     d                uds
     d l_fgrp                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
     d sfrn01          s                   like(srrn01)
     d spge01          s              3  0
     d srrn01          s              4  0
     d ssrn01          s                   like(srrn01)
      *
     d w_arow          s              2  0
     d w_acol          s              3  0
     d w_endr          s              2
     d w_rdec          s              6
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * B2CTL Behandle åpningsbildet med subfile                        *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   eval      w_arow = 4
     c                   eval      w_acol = 4
     c                   write     d2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      xdsp01
      *
      * F3=Avslutt program i fra subfile program.
      *
     c     *inkc         cabeq     *on           xslutt
      *
      * Forny
      *
     c                   if        *inke = *on                                  ...
     c                   exsr      xclr01                                         .
     c                   eval      b2valg = *blanks                               .
     c                   eval      b2rdec = *blanks                               .
     c                   eval      w_rdec = *blanks                               .
     c     key01         setll     gudkl1                             60          .
     c                   exsr      xcrt01                                         .
     c                   goto      b2taga                                         .
     c                   endif                                                  ...
      *
      * Roll
      *
     c                   if        *in10 = *on                                           .....
     c                   exsr      xcrt01                                       CRT SFL      .
     c                   goto      b2tagb                                                    .
     c                   endif                                                           .....
      *
      * Posisjoner
      * startverdi
      *
     c                   if        b2valg = *blank                                       .....
     c                             and b2rdec <> *blanks                                     .
     c                   movel     b2rdec        w_rdec                                      .
     c     key01         setll     gudkl1                                                    .
     c                   exsr      xclr01                                       TØM SFL      .
     c                   exsr      xcrt01                                       CRT SFL      .
     c                   eval      b2rdec = *blanks                                          .
     c                   goto      b2taga                                                    .
     c                   endif                                                           .....
      *
      * Tester på om linjenr. er valgt.
      *
     c                   if        b2valg <> *blank                                      .....
     c                             and b2rdec = *blanks                                      .
     c                   move      *on           *in30                                       .
     c                   goto      b2tagb                                                    .
     c                   endif                                                           .....
      *
      * Slå opp i filen:
      *
     c                   if        b2rdec <> *blanks                            .......
     c                   movel     b2rdec        w_rdec                               .
     c     key01         chain     gudkl1                             60              .
      * Post finnes:                                             .
     c                   if        *in60 = *off                                 ..... .
     c                   unlock    gudkl1                                           . .
     c                   else                                                       . .
      *                                                        . .
      * Post finnes ikke:                                      . .
      *                                                        . .
     c                   if        b2valg = '2'                                 ... . .
     c                             or b2valg = '4'                                . . .
     c                             or b2valg = '5'                                . . .
     c                   move      *on           *in31                            . . .
     c                   goto      b2tagb                                         . . .
     c                   endif                                                  ... . .
     c                   endif                                                  ..... .
     c                   endif                                                  .......
      *
      * Opprette/endre/slette
      *
     c                   if        b2valg = '1'                                 ..........
     c                             or b2valg = '2'                                       .
     c                             or b2valg = '4'                                       .
     c                             or b2valg = '5'                                       .
      *                                                             .
     c                   movel     b2rdec        w_rdec                                  .
      *                                                             .
      * Kontroll på om dek.kd finnes:                               .
      *                                                             .
     c     key01         chain     gudkl1                             60                 .
     c                   if        b2valg = '1'                                 ...      .
     c                             and *in60 = *off                               .      .
     c                   move      *on           *in32                            .      .
     c                   unlock    gudkl1                                         .      .
     c                   goto      b2tagb                                         .      .
     c                   endif                                                  ...      .
      *                                                             .
      * Initierer skjerverdier:                                     .
      *                                                             .
     c                   movel     b2rdec        a1rdec                                      .
     c                   if        *in60 = *off                                 ...      .
     c                   movel     gdrdec        a1rdec                           .      .
     c                   movel     gdrtx1        a1rtx1                           .      .
     c                   movel     gdrtx2        a1rtx2                           .      .
      *                                                      .      .
     c                   else                                                     .      .
     c                   movel     w_rdec        a1rdec                           .      .
     c                   eval      a1rtx1 = *blanks                               .      .
     c                   eval      a1rtx2 = *blanks                               .      .
     c                   endif                                                  ...      .
      *                                                             .
      * Send opp vedlikeholdsbilde:                                 .
      *                                                             .
     c                   exsr      xb2bld                                                .
      *                                                             .
     c                   eval      b2valg = *blanks                                      .
     c                   eval      b2rdec = *blanks                                      .
     c                   endif                                                  ..........
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Behandling av subfile                                           *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      * Sjekk om valg
      * er foretatt i
      * subfile
      *
     c                   do        ssrn01                                                .....
     c                   readc     d1wsf                                  16                 .
      *                                                                 .
     c                   if        *in16 = *on                                  .....        .
     c                   leave                                                      .        .
     c                   endif                                                  .....        .
      *                                                                 .
      * Velge for endring:                                              .
      *                                                                 .
     c                   if        b1valg = '2'                                 .....        .
     c                             or b1valg = '4'                                  .        .
     c                             or b1valg = '5'                                  .        .
      *                                                        .        .
     c                   movel     b1rdec        w_rdec                             .        .
      *                                                        .        .
      * Send opp vedlikeholdsbilde:                            .        .
      *                                                        .        .
     c                   exsr      xb1bld                                           .        .
      *                                                        .        .
     c                   move      srrn01        w_virn                             .        .
     c                   eval      b1valg = *blank                                  .        .
     c                   update    d1wsf                                            .        .
      *                                                        .        .
     c                   iter                                                       .        .
     c                   endif                                                  .....        .
      *                                                                 .
     c                   enddo                                                           .....
      *
      * Initierer subfile på nytt:
      *
     c                   if        w_endr = 'JA'                                ...
     c                   eval      w_endr = *blanks                               .
     c                   eval      w_rdec = *blanks                               .
     c                   move      *off          *in15                            .
     c     key01         setll     gudkl1                                         .
     c                   exsr      xclr01                                         .
     c                   exsr      xcrt01                                         .
     c                   endif                                                  ...
      *
     c                   goto      b2taga
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Avslutt program                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xslutt        tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * B2BLD Behandle bilde for å opprette dek.kd:                     *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xb2bld        begsr
      *
      * Tester på om dek.kd skal endres, vises eller slettes:
      *
     c                   move      *off          *in40
     c                   move      *off          *in41
      *
     c                   if        b2valg = '1'                                 ...
     c                             or b2valg = '2'                                .
     c                   movel     'JA'          w_endr                           .
     c                   endif                                                  ...
      *
     c                   if        b2valg = '4'                                 ...
     c                             or b2valg = '5'                                .
     c                   move      *on           *in40                            .
     c                   endif                                                  ...
      *
     c                   if        b2valg = '4'                                 ...
     c                   move      *on           *in41                            .
     c                   endif                                                  ...
      *
     c     b2tag1        tag
      *
     c                   eval      w_arow = 6
     c                   eval      w_acol = 30
      *
     c     b2tag2        tag
      *
     c                   exfmt     a1win
      *
     c     *inkc         cabeq     *on           xb2end
      *
      * Sletter post:
      *
     c                   if        b2valg = '4'
     c                   delete    gudkpfr
     c                   eval      b1rtxt = *blanks
     c                   movel     txt(1)        b1rtxt
     c                   endif
      *
     c     *in40         cabeq     *on           xb2end
      *
      * Kontroll: (Søker i file med verdier i fra skjermbildet KEY02)
      *
     c     key02         chain     gudkl1                             60
      *
      * Frigjør record:
      *
     c                   if        *in60 = *off                                 ...
     c                   unlock    gudkl1                                         .
     c                   endif                                                  ...
      *
      * Ved Ny dek.kd, eller Endring av dek.kd på
      * eksisterende dek.kd, så må det testes på at dek.kd
      * ikke finnes fra før.  Dersom w_-verdier er ulik A1-verdier,
      * så er det gjort endringer i dek.kd.
      *
     c                   if        *in60 = *off                                 .......
     c                   if        w_rdec <> a1rdec                             ..... .
     c                   if        a1rdec = gdrdec                              ... . .
     c                   move      *on           *in32                            . . .
     c                   goto      b2tag2                                         . . .
     c                   endif                                                  ... . .
     c                   endif                                                  ..... .
     c                   endif                                                  .......
      *
      * OK! oppdaterer.
      *
     c     key01         chain     gudkl1                             60
      *
     c                   movel     a1rdec        gdrdec
     c                   movel     a1rtx1        gdrtx1
     c                   movel     a1rtx2        gdrtx2
      *
     c                   if        *in60 = *on                                  ...
     c                   write     gudkpfr                                        .
     c                   else                                                     .
     c                   update    gudkpfr                                        .
     c                   endif                                                  ...
      *
     c     xb2end        tag
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * B1BLD Behandle bilde for å endre post:                          *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xb1bld        begsr
      *
      * Tester på om dek.kd skal endres, vises eller slettes:
      *
     c                   move      *off          *in40
     c                   move      *off          *in41
      *
     c                   if        b1valg = '2'                                 ...
     c                   move      'JA'          w_endr                           .
     c                   endif                                                  ...
      *
     c                   if        b1valg = '4'                                 ...
     c                             or b1valg = '5'                                .
     c                   move      *on           *in40                            .
     c                   endif                                                  ...
      *
     c                   if        b1valg = '4'                                 ...
     c                   move      *on           *in41                            .
     c                   endif                                                  ...
      *
     c     b1tag1        tag
      *
     c                   movel     b1rdec        a1rdec
     c                   movel     b1rtxt        a1rtx1
     c                   move      b1rtxt        a1rtx2
      *
     c                   eval      w_arow = 6
     c                   eval      w_acol = 30
      *
      * Sender opp bilde for endring/vise:
      *
     c     b1tag2        tag
      *
     c                   exfmt     a1win
      *
      * Kontroll:
      *
     c     *inkc         cabeq     *on           xb1end
      *
      * Sletter post:
      *
     c                   if        b1valg = '4'                                 .....
     c     key01         chain     gudkl1                             60            .
     c                   if        *in60 = *off                                 ... .
     c                   delete    gudkpfr                                        . .
     c                   eval      b1rtxt = *blanks                               . .
     c                   movel     txt(1)        b1rtxt                           . .
     c                   endif                                                  ... .
     c                   endif                                                  .....
      *
     c     *in40         cabeq     *on           xb1end
      *
     c     key02         chain     gudkl1                             60
      *
      * Frigjør record:
      *
     c                   if        *in60 = *off                                 ...
     c                   unlock    gudkl1                                         .
     c                   endif                                                  ...
      *
      * Ved Ny dek.kd, eller Endring av dek.kd. på
      * eksisterende dek.kd, så må det testes på at dek.kd
      * ikke finnes fra før.  Dersom w_-verdier er ulik A1-verdier,
      * så er det gjort endringer i dek.kd.
      *
     c                   if        *in60 = *off                                 .......
     c                   if        w_rdec <> a1rdec                             ..... .
     c                   if        a1rdec = gdrdec                              ... . .
     c                   move      *on           *in32                            . . .
     c                   goto      b1tag2                                         . . .
     c                   endif                                                  ... . .
     c                   endif                                                  ..... .
     c                   endif                                                  .......
      *
      * OK! oppdaterer.
      *
     c     key01         chain     gudkl1                             60
      *
     c                   movel     a1rdec        b1rdec
     c                   movel     a1rtx1        b1rtxt
     c                   move      a1rtx2        b1rtxt
      *
     c                   movel     a1rdec        gdrdec
     c                   movel     a1rtx1        gdrtx1
     c                   movel     a1rtx2        gdrtx2
      *
     c                   if        *in60 = *on                                  ...
     c                   write     gudkpfr                                        .
     c                   else                                                     .
     c                   update    gudkpfr                                        .
     c                   endif                                                  ...
      *
     c     xb1end        tag
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Tøm subfile - 1                                                 *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xclr01        begsr
     c                   move      *on           *in14
     c                   write     d2wct
     c                   move      *off          *in14
      *
     c                   move      *off          *in15
     c                   eval      srrn01 = 0
     c                   eval      sfrn01 = 0
     c                   eval      ssrn01 = 0
     c                   eval      spge01 = 0
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Fyll opp subfile - 1                                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xcrt01        begsr
     c                   eval      spge01 = spge01 + 7                          SFL END
     c                   eval      srrn01 = ssrn01
      *
     c                   dou       srrn01 = spge01                                       .....
      *                                                                 .
     c                   read      gudkl1                                 15                 .
      *                                                                 .
     c     *in15         cabeq     *on           xend01                                      .
     c                   unlock    gudkl1                                                    .
      * Display felter:                                                 .
     c                   eval      srrn01 = srrn01 + 1                                       .
     c                   movel     gdrdec        b1rdec                                      .
     c                   movel     gdrtx1        b1rtxt                                      .
     c                   move      gdrtx2        b1rtxt                                      .
     c                   write     d1wsf                                                     .
     c                   enddo                                                           .....
      *
     c     xend01        tag
      *
     c                   if        srrn01 > ssrn01                                       .....
     c                   eval      sfrn01 = ssrn01 + 1                                       .
     c                   endif                                                           .....
      *
     c                   eval      ssrn01 = srrn01
     c                   eval      w_virn = sfrn01
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Display subfile - 1                                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     xdsp01        begsr
      *
     c                   if        srrn01 <> 0                                           .....
     c                   move      *on           *in12                                       .
     c                   endif                                                           .....
      *
     c                   move      *on           *in13
     c                   exfmt     d2wct
     c                   move      *off          *in12
     c                   move      *off          *in13
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Subrutine for initiering av program                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *inzsr        begsr
      *
      * Initierer hjelpefelt:
      *
     c                   eval      spge01 = 0
     c                   eval      srrn01 = 0
     c                   eval      w_arow = 4
     c                   eval      w_acol = 2
      *
      * Hjelpefelt forteller om subfile skal initieres på nytt:
      *
     c                   eval      w_endr = *blanks
      *
      *
      * Key til bruk på GUDKL1:
      *
     c     key01         klist
     c                   kfld                    w_rdec
      *
      * Key til bruk på GUDKL1:
      *
     c     key02         klist
     c                   kfld                    a1rdec
      *
      * Initierer les for å fylle opp subfile:
      *
     c     key01         setll     gudkl1                             60
      * Fyll opp subfile:
     c                   exsr      xcrt01
      *
     c                   endsr
      *
**
* Slettet *
