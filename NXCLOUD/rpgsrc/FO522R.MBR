     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASOFAK
      * Program . . . . : FO522R
      * Beskrivelse . . : Viser dekningsbidrag
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *              ???  Nytt program
      *       271005 HKO  Tatt med stipulert bonus i kalkulasjonen
      * 6.10  240609 BSA  Oppdatert med sporingsinformasjon
6.NU  * R6.30 230514 bsa  Programmet gjennomgått mtp nummerutvidelser.
      *
7.01  * R7.00 040319 bkv  NOBB frakt
7.02  *  7.00 020317 bof  DG skal beregnes uten bonus
7.03  * K000  301020 bsa  NOBB frakt styres av annen switch
7.04  * K000  021120 bsa  Viser parameter for kostpris=innkjøpspris
7.04  * K000              uavhengig av om switch er satt eller ei.
7.05  * R7.00 190320 bkv  Fix for visning i Newlook INCT0120517
7.06  *K00    030521 sst  Logge omberegning ordre fra F6 DG/Ordre
      *
8.01  *PRG2   090622 BOF  Utvidet prisgruppe fra 1 til 2 posisjoner
      *
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     ffohel1    if   e           k disk    rename(fohepfr:fohel1r)
     ffohelu    uf   e           k disk    rename(fohepfr:fohelur)
     ffodtl1    if   e           k disk    rename(fodtpfr:fodtl1r)
     ffodtlu    uf   e           k disk    rename(fodtpfr:fodtlur)
7.01 ffoh2l1    if   e           k disk    rename(foh2pfr:foh2l1r)
7.01 ffoh2lu    uf a e           k disk    rename(foh2pfr:foh2lur)
7.06 fsloglu    uf a e           k disk    rename(slogpfr:sloglur)
      *
     ffo522d    cf   e             workstn
      *
      * Definering av parametre
      *
     d p_firm          s                   like(fofirm)
     d p_numm          s                   like(fonumm)
     d p_suff          s                   like(fosuff)
      *
      * Definering av Local data area
      *
     d                uds
6.10 d l_user                911    920
7.00 d l_filg                931    933
     d l_fnav                951    980
      *
      * Definering av variable
      *
     d w_alf1          s              1
     d w_retk          s              1
     d w_ftxt          s             20
      *
     d w_firm          s                   like(fofirm)
     d w_numm          s                   like(fonumm)
     d w_suff          s                   like(fosuff)
     d w_nett          s                   like(fotots)
     d w_sumn          s                   like(fdsums)
     d w_sumb          s                   like(fdsums)
7.01 d w_prgr          s                   like(fdprgr)
7.01 d w_lety          s                   like(folety)
7.01 d w_dato          s               d   datfmt(*iso)
7.01 d w_kopr          s              9  2
7.01 d w_inpr          s              9  2
7.01 d w_lenh          s                   like(fdenh1)
7.01 d w_kamp          s                   like(fdkamp)
7.01 d w_inpr2         s                   like(fdinpr)
7.01 d w_kopr2         s                   like(fdkopr)
7.01 d w_sapr2         s                   like(fdsapr)
7.01 d w_bupr2         s                   like(fdsapr)
7.01 d w_hlev          s              1  0
7.01 d b_inlr          s              1    inz(*off)
7.02 d w_totk          s             11  2
7.02 d w_tots          s             11  2
7.02 d w_totr          s             11  2
7.02 d w_dekn          s             11  2
      *
7.01 d u_nobf          s              1    inz(*off)                            fraktkalkulator
7.02 d u_s702          s              1    inz(*off)                            DG uten bonus
7.01  *
7.01  * Definering av eksterne prg
7.01  *
7.00   dcl-s co402_verdi1  char(1);
7.00   dcl-s co402_verdi2  char(2048);
7.00   DCL-PR CO402R EXTPGM('CO402R');
7.00     p_filgrp            char(3)     const;
7.00     p_firm              packed(3:0) const;
7.00     p_lager             packed(2:0) const;
7.00     p_nokkel            char(50)    const;
7.00     p_verdi1            char(1);
7.00     p_verdi2            char(2048);
7.00   END-PR;
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     A1TAGA        TAG
      *
7.01 c                   MOVE      *BLANK        A1SVA2
      *
      * Henter opplysninger fra ordre-hode
      *
     c     fohel1_key    chain     fohel1R
     c                   if        %found
     c                   MOVE      FORKAT        A1RKAT
     c                   MOVE      FORABP        A1RABP
     c                   MOVE      FOPASL        A1PASL
     c                   MOVE      FOPASK        A1PASK
     c                   MOVE      FOPORD        A1PNUM
     c                   Z-ADD     FOTOTS        A1TOTS
     c                   Z-ADD     FOTOTR        A1TOTR
     c                   Z-ADD     FOTOTK        A1TOTK
7.01  * Sjekk om nobb frakt
7.01 c                   if        u_nobf = *on
7.01 c                   eval      fo2nbf = 0
7.01 c     foh2l1_key    chain     foh2l1r
7.01 c                   if        not %found
7.01  * hvis leveringstype direkte, skal alltid ha foh2
7.01 c                   if        folety = 1
7.01 c                   eval      fo2fir = w_firm
7.01 c                   eval      fo2num = w_numm
7.01 c                   eval      fo2suf = w_suff
7.01 c                   eval      fo2nbf = 1
7.01 c                   time                    fo2dat
7.01 c                   time                    fo2tim
7.01 c                   eval      fo2oou = l_user
7.01 c                   time                    fo2eda
7.01 c                   time                    fo2eti
7.01 c                   eval      fo2eus = l_user
7.01 c                   write     foh2lur
7.01 c                   endif
7.01 c                   endif
7.01 c                   if        fo2nbf = 1
7.01 c                   eval      A1SVA2 = '1'
7.01 c                   endif
7.01 c                   endif
     c                   endif
      *
      * Leser alle ordrelinjer og kalkulerer bonus
      *
7.02 c                   eval      w_totk = 0
7.02 c                   eval      w_tots = 0
7.02 c                   eval      w_totr = 0
7.02  *
     c     fodtl1_key    setll     fodtl1
     c     fodtl1_key    reade     fodtl1
     c                   dow       not %eof
     c                   if        fdvare <> *blank
7.02 c                   eval      w_totk = w_totk + fdsumk
7.02 c                   eval      w_tots = w_tots + fdsums
7.02 c                   eval      w_totr = w_totr + fdsumr
7.02  *
     c                   eval      w_sumn = fdsums - fdsumr
     c                   eval      w_sumb = 0
     c                   call      'EK760R'
     c                   parm                    w_firm
     c                   parm                    fokund
     c                   parm                    folkun
     c                   parm                    fokpro
     c                   parm                    forkat
     c                   parm                    fobdat
     c                   parm                    fdvare
     c                   parm                    fdenh1
     c                   parm                    fdlety
     c                   parm                    fdldor
     c                   parm                    w_sumn
     c                   parm                    w_sumb
     c                   endif
     c                   eval      a1totb = a1totb + w_sumb
     c     fodtl1_key    reade     fodtl1
     c                   enddo
      *
      *  Beregner/Nullstiller andre felter som blir benyttet
      *
     c                   MOVE      *BLANK        A1SVAR
     c     A1TOTS        SUB       A1TOTR        A1NETT
     c     A1NETT        SUB       A1TOTK        A1DEKN
     c                   eval      a1dekn = a1dekn - a1totb
      *
7.02 c                   if        u_s702 = *off
     c                   eval      w_nett = a1nett - a1totb
7.02 c                   else
7.02 c                   eval      w_nett = w_tots - w_totr
7.02 c                   eval      w_dekn = w_nett - w_totk
7.02 c                   eval      a1dekn = w_dekn
7.02 c                   endif
7.02  *
      *
     c     w_nett        IFNE      *ZERO
7.02 c                   if        u_s702 = *off
     c     A1DEKN        DIV       w_nett        WWGRAX
7.02 c                   else
7.02 c     w_DEKN        DIV       w_nett        WWGRAX
7.02 c                   endif
     c     WWGRAX        MULT      100           A1GRAD
     c                   END
      *
      *  Send alt
      *
     c     A1TAGB        TAG
     c                   EXFMT     A1BLD
      *
      *  F3=Avslutt program
      *
     c     *INKC         CABEQ     *ON           avslutt
     c     *INKL         CABEQ     *ON           avslutt
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Utførerer diverse tester                                       *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
      *  Sjekk,  Finnes rabattkategori i KODE-REGISTER
      *
     c     A1RKAT        IFNE      FORKAT
     c                   EXSR      SBRKAT
     c     w_retk        IFNE      *BLANK
     c     A1RKAT        IFNE      *ZERO
     c                   MOVE      *ON           *IN31
     c                   GOTO      A1TAGB
     c                   ENDIF
     c                   ENDIF
     c                   ENDIF
      *
      *  Sjekk,  Finnes ordrenummer for pristilbud i ORDRE-HODE-REGISTER
      *
     c     A1PNUM        IFNE      *ZERO
     c     A1PNUM        IFNE      FOPORD
     c                   MOVE      w_numm        WWWNUM
     c                   MOVE      w_suff        WWWSUF
     c                   MOVE      A1PNUM        w_numm
     c                   MOVE      *ZERO         w_suff
     c     fohel1_key    CHAIN     fohel1R                            90
     c                   MOVE      WWWNUM        w_numm
     c                   MOVE      WWWSUF        w_suff
     c     *IN90         IFEQ      *ON
     c                   MOVE      *ON           *IN32
     c                   GOTO      A1TAGB
     c                   END
     c                   END
     c                   END
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Behandling av inntastede verdier                               *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *  Oppdater ORDRE-HODE-REGISTER med nye verdier
      *
     c     fohelu_key    CHAIN     fohelUR                            90
     c     *IN90         IFEQ      *OFF
7.06 c                   eval      elfrav = %editc(forkat:'X') + '\' +
7.06 c                                      %editc(forabp:'X') + '\' +
7.06 c                                      %editc(fopasl:'X')
     c                   MOVE      A1RKAT        FORKAT
     c                   MOVE      A1RABP        FORABP
     c                   MOVE      A1PASL        FOPASL
     c                   MOVE      A1PASK        FOPASK
     c                   MOVE      A1PNUM        FOPORD
6.10 c                   time                    foedat
6.10 c                   time                    foetim
6.10 c                   eval      foeusr = l_user
     c                   UPDATE    fohelUR
     c                   END
      *
      *  Sjekk,  Skal priser/rabatter beregnes pånytt
      *
     c     A1SVAR        IFEQ      '1'
      *
     c                   MOVE      A1SVAR        w_alf1
      *
      *  Kaller opp subprogram for endring av ordre
      *
     c                   CALL      'FO730R'
     c                   PARM                    w_alf1
     c                   PARM                    w_firm
     c                   PARM                    w_numm
     c                   PARM                    w_suff
7.06  *
7.06 c                   exsr      logg_omber
      *
      *  Dersom endring av ordre, skal bildet vises pånytt
      *
     c                   GOTO      A1TAGA
      *
     c                   END
7.01  *
7.01  *  Sjekk,  skal frakt fjernes fra kostpr (nobb frakt)
7.01  *
7.01 c     A1SVA2        IFEQ      '1'
7.04  *
7.04 c                   eval      u_nobf = *on
7.01  *
      * sett fo2nbf  aktiv. hvis ikke finnes, lag record
7.01 c     foh2lu_key    chain     foh2lur
7.01 c                   if        not %found
7.01 c                   eval      fo2fir = w_firm
7.01 c                   eval      fo2num = w_numm
7.01 c                   eval      fo2suf = w_suff
7.01 c                   eval      fo2nbf = 1
7.01 c                   time                    fo2dat
7.01 c                   time                    fo2tim
7.01 c                   eval      fo2oou = l_user
7.01 c                   time                    fo2eda
7.01 c                   time                    fo2eti
7.01 c                   eval      fo2eus = l_user
7.01 c                   write     foh2lur
7.01 c                   else
7.01 c                   eval      fo2nbf = 1
7.01 c                   time                    fo2eda
7.01 c                   time                    fo2eti
7.01 c                   eval      fo2eus = l_user
7.01 c                   update    foh2lur
7.01 c                   endif
7.01  *
7.01  * Leser alle ordrelinjer og setter kostpris lik innkjøpspris
7.01  *
7.01 c     fodtlu_key    setll     fodtlu
7.01 c     fodtlu_key    reade     fodtlu
7.01 c                   dow       not %eof
7.01 c                   if        fdinpr = *zero
7.01 c                   exsr      innpri
7.01 c                   if        w_inpr <> *zero
7.01 c                   eval      fdinpr = w_inpr
7.01 c                   endif
7.01 c                   endif
7.01 c                   if        fdinpr <> *zero
7.01 c                   eval      fdkopr = fdinpr
7.01 c                   time                    fdedat
7.01 c                   time                    fdetim
7.01 c                   eval      fdeusr = l_user
7.01 c                   update    fodtlur
7.01 c                   else
7.01 c                   unlock    fodtlu                                       fant ikke innpris
7.01 c                   endif
7.01 c     fodtlu_key    reade     fodtlur
7.01 c                   enddo
7.01  *
7.01  * kun kalkuere sum på nytt
7.01 c                   EVAL      w_alf1 = ''
7.01  *  Kaller opp subprogram for endring av ordre
7.01  *
7.01 c                   CALL      'FO730R'
7.01 c                   PARM                    w_alf1
7.01 c                   PARM                    w_firm
7.01 c                   PARM                    w_numm
7.01 c                   PARM                    w_suff
7.01  *
7.01 c                   else
7.01 c                   if        u_nobf = *on
7.01  * bruker nobb frakt, og har fjernet status "Bruk nobb frakt"
7.01 c     foh2lu_key    chain     foh2lur
7.01 c                   if        %found
7.01  * hvis foh2 er laget, sett fo2nbr = 0
7.01 c                   eval      fo2nbf = 0
7.01 c                   time                    fo2eda
7.01 c                   time                    fo2eti
7.01 c                   eval      fo2eus = l_user
7.01 c                   update    foh2lur
7.01 c
7.01  * kun kalkuere ordre på nytt (nobb frakt fjernet)
7.01 c                   EVAL      w_alf1 = '1'
7.01  *  Kaller opp subprogram for endring av ordre
7.01  *
7.01 c                   CALL      'FO730R'
7.01 c                   PARM                    w_alf1
7.01 c                   PARM                    w_firm
7.01 c                   PARM                    w_numm
7.01 c                   PARM                    w_suff
7.01 c                   endif
7.01 c                   endif
7.01 c
7.01 c                   END
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Sjekk/Hent tekst for rabattkategori                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
      *
     c     SBRKAT        BEGSR
      *
     c                   call      'RS706R'
     c                   parm                    w_retk
     c                   parm                    a1rkat
     c                   parm                    w_ftxt
      *
     c                   ENDSR
7.01  *
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
7.01  *  Hent innkjøpspris                                              *
7.01  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
7.01  *
7.01 c     INNPRI        BEGSR
7.01  *
7.01 c                   eval      w_lety = folety
7.01 c                   eval      w_prgr = fdprgr
7.01 c                   eval      w_lenh = fdenh1
7.01 c                   eval      w_kamp = fdkamp
7.01  *
7.01 c*                   call      'VP750R'
7.01 c*                   parm                    w_firm
7.01 c*                   parm                    fdvare
7.01 c*                   parm                    w_prgr
7.01 c*                   parm                    w_lety
7.01 c*                   parm                    w_dato
7.01 c*                   parm                    w_lenh
7.01 c*                   parm                    w_kopr
7.01 c*                   parm                    w_inpr
7.01 c*                   parm                    w_kamp
     c
7.00 c                   call      'VP712R'
7.00 c                   parm                    w_firm
7.00 c                   parm                    fdvare
7.00 c                   parm                    fdldor
7.00 c                   parm                    w_prgr
7.00 c                   parm                    w_lenh
7.00 c                   parm                    w_lety
7.00 c                   parm                    w_dato
7.00 c                   parm                    b_inlr
7.00 c                   parm                    w_sapr2
7.00 c                   parm                    w_bupr2
7.00 c                   parm                    w_kopr2
7.00 c                   parm                    w_inpr
7.00 c                   parm                    w_inpr2
7.00 c                   parm                    w_hlev
7.01  *
7.01 c                   ENDSR
      *
7.06  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.06  * Logger omberegning av ordre
7.06  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
7.06  *
7.06 c     logg_omber    begsr
7.06  *
7.06 c                   if        l_user <> *blank
7.06 c                   eval      elfirm = w_firm
7.06 c                   eval      elregi = 'ORDRE'
7.06 c                   eval      elfore = %editc(fonumm:'X')
7.06 c                   eval      elfort = %editc(fosuff:'X')
7.06 c                   eval      elfelt = 'OMBEREGN'
7.06 c                   eval      eluser = l_user
7.06 c                   time                    eldato
7.06 c                   eval      eltilv = %editc(a1rkat:'X') + '\' +
7.06 c                                      %editc(a1rabp:'X') + '\' +
7.06 c                                      %editc(a1pasl:'X')
7.06 c                   time                    elrdat
7.06 c                   time                    elrtim
7.06 c                   eval      elrusr = l_user
7.06 c                   write     sloglur
7.06 c                   endif
7.06  *
7.06 c                   endsr
7.06  *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *  Subrutine for initiering av program                            *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     *INZSR        BEGSR
      *
      * Parametre
      *
     c     *ENTRY        PLIST
     c                   PARM                    p_firm
     c                   PARM                    p_numm
     c                   PARM                    p_suff
      *
      * Key for oppslag på ordre-hode
      *
     c     fohel1_key    KLIST
     c                   KFLD                    w_firm
     c                   KFLD                    w_numm
     c                   KFLD                    w_suff
      *
      * Key for oppdatering av ordre-hode
      *
     c     fohelu_key    KLIST
     c                   KFLD                    w_firm
     c                   KFLD                    w_numm
     c                   KFLD                    w_suff
      *
      * Key for les på ordre-linje
      *
     c     fodtl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_numm
     c                   kfld                    w_suff
7.01  *
7.01  * Key for oppslag på ordre-hode SIDETABELL
7.01  *
7.01 c     foh2l1_key    KLIST
7.01 c                   KFLD                    w_firm
7.01 c                   KFLD                    w_numm
7.01 c                   KFLD                    w_suff
7.01  *
7.01  * Key for oppdatering av ordre-hode SIDETABELL
7.01  *
7.01 c     foh2lu_key    KLIST
7.01 c                   KFLD                    w_firm
7.01 c                   KFLD                    w_numm
7.01 c                   KFLD                    w_suff
7.01  *
7.01  * Key for oppdater ordre-linje
7.01  *
7.01 c     fodtlu_key    klist
7.01 c                   kfld                    w_firm
7.01 c                   kfld                    w_numm
7.01 c                   kfld                    w_suff
7.01 c*                   kfld                    fodtlu_line
      *
      *  Nullstilling av felter som ikke kan redefineres
      *
     c                   MOVE      *ZERO         WWGRAX           14 5
     c                   MOVE      *ZERO         A1GRAD            5 2
      *
      *  Redefinering av felter
      *
     c     *LIKE         DEFINE    w_numm        WWWNUM
     c     *LIKE         DEFINE    w_suff        WWWSUF
      *
      * Henter firma, ordrenr og suffiks fra parametre
      *
     c                   MOVE      p_firm        w_firm
     c                   MOVE      p_numm        w_numm
     c                   MOVE      p_suff        w_suff
      *
7.01 c                   time                    w_dato
      *
7.01  *
7.01  * Test om nobb fraktkalkulator er av eller på
7.01  *
7.01   *in41 = *off;
7.03   CallP CO402R(l_filg:w_firm:0:'AUTOMATISK_NOBB_FRAKTKALKULATOR':
7.01   co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_nobf = *on;
7.04  // *in41 = *on;
7.01   endif;
7.02  *
7.02  * Sjekk om DG skal beregnes uten bonus
7.02  *
7.02   CallP CO402R(l_filg:w_firm:0:'FO522_702':
7.02   co402_verdi1:co402_verdi2);
7.02   if co402_verdi1 = '1';
7.02     u_s702 = *on;
7.01   endif;
      *
     c                   ENDSR
