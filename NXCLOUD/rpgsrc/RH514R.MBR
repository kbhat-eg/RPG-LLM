      /TITLE  ASOKON Spørring på sumdatabase
     H datedit(*dmy) option(*nodebugio)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * System  . . . . : ASOKON
      * Program . . . . : RH514R
      * Beskrivelse . . : Spørring på sumdatabase
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       200500 KOB  Nytt program
      *       311206 KOB  Lagt inn vis av detaljer på sumpost
      *       270306 KOB  Lagt inn vis av leverandørfaktura
      * R5.60 250408 KOB  Lagt inn ny funksjon for å vise arkivpost
pdf   * R6.10     041010  Henter fra ASP arkiv. Mulighet for valg mellom BSA  *
pdf   *                   ASP og Multiarkiv gjøres i AP622C. Det finnes  BSA  *
pdf   *                   to varianter av dette programmet.              BSA  *
6.20  * R6.20     070611  Starter arkivet                                BSA  *
6.21  * R6.20     210711  Legger ut firmanummer i skjultfelt for         bsa  *
6.21  *                   spørring mot ASP-arkiv fra Jacada              bsa  *
6.22  * R6.20     051212  Endret metode for oppslag mot arkiv            bsa  *
6.23  * R6.20     040113  Endret logikk rundt refn                       bsa  *
6.24  * R6.20     270213  Egen kode for diversebilag                     bsa  *
7.xx  * 7.xx  030516 bsa  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
7.01  * R6.20     090414  Ønsker "gammle" måte å slå opp i arkivet på.   bsa  *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      *  Bruk av indikatorer:
      *
      *       LR = Avslutt program
      *       10 = Roll
      *       12 = Sfldsp
      *       13 = Sfldspctl
      *       14 = Sflclr
      *       15 = Sflend
      *       16 = Readc 16 on, no more records
      *       21 = Home-key
      *       22 = Plassering av cursor
      *       30 = Protect av felter ved vise
      *    31-79 = Varsle m/feilmeldinger/skjermindikatorer
      *    80-89 = Arbeidsindikatorer
      *    90-99 = Fileindikatorer chain etc.
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     frzuml1    if   e           k disk    rename(rzumpfr:rzuml1r)
     frzuml2    if   e           k disk    rename(rzumpfr:rzuml2r)
     fraa1l1    if   e           k disk    rename(raa1pfr:raa1l1r)
     frh514d    cf   e             workstn sfile(b1sfl:w_srrn)
     f                                     infds(dspfbk)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
7.01 d l_filg                931    933
     d l_firm                944    946  0
     d l_fnav                951    980
      *
      * Definering av Information Data Structure - display feedback
      *
     d dspfbk          ds
     d  d_fcrn               378    379I 0
      *
      * Definering av parameterverdier
      *
     d p_stat          s              1
     d p_kont          s                   like(rzkont)
     d p_spes          s                   like(rzspes)
     d p_avde          s                   like(rzavde)
     d p_tex1          s             30
     d p_biln          s                   like(rzbiln)
     d p_text          s             20
     d p_bdat          s                   like(rzbdat)
     d p_raar          s                   like(rzraar)
     d p_neto          s                   like(rzbelØ)
     d p_fakt          s                   like(rzbiln)
      *
      * Definering av variabler i nøkler
      *
     d rzuml1_raar     s                   like(rzraar)
     d rzuml1_biln     s                   like(rzbiln)
     d rzuml1_bdat     s                   like(rzbdat)
     d rzuml1_tell     s                   like(rztell)
      *
     d rzuml2_raar     s                   like(rzraar)
     d rzuml2_bilg     s                   like(rzbilg)
     d rzuml2_bdat     s                   like(rzbdat)
     d rzuml2_tell     s                   like(rztell)
      *
      * Definering av variable
      *
     d w_spge          s              4  0
     d w_srrn          s              4  0
     d w_sfrn          s                   like(w_srrn)
     d w_ssrn          s                   like(w_srrn)
      *
     d w_seqe          s              1
     d b_forn          s              1    inz(*off)
     d b_anul          s              1    inz(*off)
     d b_feil          s              1    inz(*off)
      *
     d w_firm          s                   like(rzfirm)
     d w_rper          s              3  0
6.22 d w_type          s             50
      *
      * Definering av parametre MultiArcive
      *
pdf  d*w_modu          s             10    inz('MSA     ')
pdf  d w_modu          s             10    inz('PROA    ')
     d w_disp          s              1    inz('1')
     d w_tilg          s              1
     d w_pgrp          s              2
     d w_aara          s              4
     d w_bila          s              6
     d w_afir          s              3
6.22 d w_refn          s             50
7.00  *
7.00  * Definering av eksterne prg
7.00   dcl-s co402_verdi1  char(1);
7.00   dcl-s co402_verdi2  char(2048);
7.00   DCL-PR CO402R EXTPGM('CO402R');
7.00     p_filgrp            char(3)     const;
7.00     p_firm              packed(3:0) const;
7.00     p_lager             packed(2:0) const;
7.00     p_nokkel            char(50)    const;
7.00     p_verdi1            char(1);
7.00     p_verdi2            char(2048);
7.00   END-PR;
      *
7.01 d u_701           s              1    inz(*off)
      *
      * Definering av konstanter
      *
     d c_sfil          s              2  0 inz(13)
      *
      /eject
      *
      * Forklaring på spesielle felter
      * som benyttes ved sub-files.
      * - - - - - - - - - - - - - - - - -
      * w_srrn - Relativt recordnummer i subfile
      * w_spge - Sidestørrelse i subfile (page)
      * w_sfrn - Første recordnummer på siden
      * w_ssrn - Siste  recordnummer på siden
      * w_virn - Vis den side som inneholder denne record.
      *
      * Benyttede skjermbilder.
      * - - - - - - - - - - - - - - - - -
      * B1SFL  - B1 Subfile - record
      * B2CTL  - B2 Subfile - control record
      * B2CMD  - B2 Eget skjermbilde for CMD-taster
      * C1MSG  - C1 Meldingsbilde for C1BLD
      * S1WIN  - S1 Standardvindu som inneholder definisjon av
      *             størrelse og utseende.  Bilde K1WIN
      *             er referert til dette.
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * B2CTL Behandle åpningsbildet med subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Send Alt
      *
     c     b2taga        tag
      *
     c                   write     b2cmd
      *
      * Bare data
      *
     c     b2tagb        tag
      *
     c                   exsr      dsp_subfile
      *
      * Behandling av funksjonstaster
      *
     c                   select
     c                   when      *inkc or *inkl
     c                   goto      avslutt
     c                   when      *in10
     c                   exsr      crt_subfile
     c                   goto      b2tagb
     c                   when      *in21
     c                   eval      *in22 = *on
     c                   goto      b2taga
     c                   endsl
      *
      * Behandling av subfile
      *
     c                   exsr      subfile
      *
     c                   goto      b2taga
      *
      * Avslutt program
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for behandling av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     subfile       begsr
      *
     c                   if        *in22 = *off
     c                   eval      *in22 = *on
     c                   else
     c                   eval      *in22 = *off
     c                   endif
      *
     c                   eval      w_virn = d_fcrn
      *
      * Leser og behandler subfile
      *
     c                   do        w_ssrn
     c                   readc     b1sfl
      *
     c                   if        %eof
     c                   leave
     c                   endif
      *
     c                   eval      *in22 = *off
     c                   eval      w_virn = w_srrn
      *
      * Vis
      *
     c                   if        b1valg = 5
     c                   eval      rzuml2_raar = b1raar
     c                   eval      rzuml2_bilg = b1bilg
     c                   eval      rzuml2_bdat = b1bida
     c                   eval      rzuml2_tell = b1tell
     c                   exsr      xc2bld
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis bilag
      *
     c                   if        b1valg = 7
      *
     c                   call      'RF512R'
     c                   parm                    b1resn
     c                   parm                    b1bilg
     c                   parm                    b1bida
     c                   parm                    w_rper
     c                   parm                    b1raar
      *
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis faktura
      *
     c                   if        b1valg = 8
      *
     c                   eval      p_raar = b1raar
     c                   eval      p_fakt = b1bilg
      *
      * Sjekk om kunde eller leverandør
      *
     c                   if        b1resn < ra1hrs
      *
     c                   call      'RK110C'
     c                   parm                    w_firm
     c                   parm                    p_raar
     c                   parm                    b1resn
     c                   parm                    p_fakt
      *
     c                   else
      *
     c                   call      'RL110C'
     c                   parm                    w_firm
     c                   parm                    p_raar
     c                   parm                    b1resn
     c                   parm                    p_fakt
      *
     c                   endif
      *
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
      * Vis arkiv
      *
     c                   if        b1valg = 9
      *
      * Kontrollerer gyldig tilgang
      *
     c                   call      'AX700R'
     c                   parm                    w_modu
     c                   parm                    w_disp
     c                   parm                    w_tilg
      *
     c                   if        w_tilg = '0'
      *
6.22 c**                 eval      w_type = 'ARKIV  '
7.01 c                   if        u_701 = *on
7.01 c                   eval      w_type = 'INNGÅENDE%20FAKTURA'
7.01 c                   else
6.24 c                   eval      w_type = 'DIVERSEBILAG'
7.01 c                   endif
     c                   move      w_firm        w_afir
     c                   eval      w_pgrp = '20'
     c                   eval      w_aara = %char(p_raar)
     c                   eval      w_bila = %char(b1bilg)
7.01 c                   if        u_701 = *on
7.01 c                   eval      w_refn = %trim(%char(b1bilg))
7.01 c                   else
6.23 c                   eval      w_refn = %char(p_raar) + %trim(%char(b1bilg))
7.01 c                   endif
      *
pdf  c                   call      'AP622C'
pdf  c                   parm                    w_type
6.22 c                   parm                    w_refn
6.22 c**                 parm                    w_aara
6.22 c**                 parm                    w_bila
6.22 c**                 parm                    w_pgrp
pdf  c**                 call      'AH720C'
pdf  c**                 parm                    w_pgrp
pdf  c**                 parm                    w_afir
pdf  c**                 parm                    w_aara
pdf  c**                 parm                    w_bila
      *
     c                   endif
     c                   eval      b1valg = 0
     c                   update    b1sfl
     c                   iter
     c                   endif
      *
     c                   enddo
      *
     c     end_subfile   endsr
      *
      /eject
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å behandle bilde for vise (C2BLD)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     xc2bld        begsr
      *
     c                   eval      w_arow = 0
     c                   eval      w_acol = 0
      *
     c     rzuml2_key    chain     rzuml2
      *
     c                   if        %found
      *
      * Hente konto-tekster
      *
     c                   exsr      hent_text
      *
     c     *dmy          move      rzbdat        c2bdat
     c                   eval      c2amva = rzamva
     c                   eval      c2pmva = rzpmva
     c                   endif
      *
     c                   eval      w_arow = 4
     c                   eval      w_acol = 15
      *
     c     c2taga        tag
      *
     c                   exfmt     c2win
      *
     c                   if        *inkc or *inkl
     c                   goto      end_c2bld
     c                   endif
      *
     c     end_c2bld     tag
      *
     c                   endsr
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      * Hente inn kontonavn fra kontoplanen                             *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
      *
     c     hent_text     begsr
      *
      * Konto-spes-avd
      *
     c                   call      'RS740R'
     c                   parm                    p_stat
     c                   parm                    b1kont
     c                   parm                    b1spes
     c                   parm                    b1avde
     c                   parm                    rhtext
      *
      * Hent avdeling
      *
T5.01c                   if        b1avde <> 0
     c                   call      'RS707R'
     c                   parm                    p_stat
     c                   parm                    b1avde
     c                   parm                    ragtxt
T5.01c                   else
T5.01c                   eval      ragtxt = *blank
T5.01c                   endif
      *
      * Hent kostnadsbærer
      *
     c                   call      'RS728R'
     c                   parm                    p_stat
     c                   parm                    b1spes
     c                   parm                    rebtxt
      *
     c                   if        p_stat = 'N'
     c                   eval      rebtxt = *blank
     c                   endif
      *
      * Hent reskontro  - kunde
      *
     c                   call      'RS750R'
     c                   parm                    p_stat
     c                   parm                    b1resn
     c                   parm                    c2rnav
      *
     c                   if        p_stat = 'N'
      *
      * Hent reskontro  - leverandør
      *
     c                   call      'RS760R'
     c                   parm                    p_stat
     c                   parm                    b1resn
     c                   parm                    c2rnav
      *
     c                   if        p_stat = 'N'
     c                   eval      c2rnav = *blank
     c                   endif
      *
     c                   endif
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for tømming av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     clr_subfile   begsr
      *
     c                   eval      *in14 = *on
     c                   write     b2ctl
     c                   eval      *in14 = *off
      *
     c                   eval      *in15 = *off
     c                   eval      w_srrn = 0
     c                   eval      w_sfrn = 0
     c                   eval      w_ssrn = 0
     c                   eval      w_spge = 0
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å fylle opp subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     crt_subfile   begsr
      *
     c                   eval      w_spge = w_spge + c_sfil
     c                   eval      w_srrn = w_ssrn
      *
     c                   dou       w_srrn = w_spge
      *
     c     rzuml1_key    reade     rzuml1                                 15
      *
     c                   if        *in15
     c                   goto      end_crt
     c                   endif
      *
     c                   eval      w_srrn = w_srrn + 1
      *
     c                   eval      b1raar = rzraar
     c     *dmy          move      rzbdat        b1bdat
     c                   eval      b1bida = rzbdat
     c                   eval      b1tell = rztell
     c                   eval      b1belo = rzbelØ
     c                   eval      b1bilg = rzbilg
     c                   eval      b1kont = rzkont
     c                   eval      b1spes = rzspes
     c                   eval      b1avde = rzavde
     c                   eval      b1resn = rzresn
      *
     c                   write     b1sfl
      *
     c                   enddo
      *
     c     end_crt       tag
      *
     c                   if        w_srrn > w_ssrn
     c                   eval      w_sfrn = w_ssrn + 1
     c                   endif
      *
     c                   eval      w_ssrn = w_srrn
     c                   eval      w_virn = w_sfrn
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for display av subfile
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     dsp_subfile   begsr
      *
     c                   if        w_srrn <> 0
     c                   eval      *in12 = *on
     c                   else
     c                   write     b2cmd
     c                   endif
      *
     c                   eval      *in13 = *on
     c                   exfmt     b2ctl
      *
     c                   eval      *in12 = *off
     c                   eval      *in13 = *off
     c                   eval      *in22 = *off
      *
     c                   endsr
      *
      /eject
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parameterliste
      *
     c     *entry        plist
     c                   parm                    p_raar
     c                   parm                    p_kont
     c                   parm                    p_spes
     c                   parm                    p_avde
     c                   parm                    p_tex1
     c                   parm                    p_biln
     c                   parm                    p_text
     c                   parm                    p_bdat
     c                   parm                    p_neto
      *
      * Key for posisjonering på bilagsnummer
      *
     c     rzuml1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rzuml1_raar
     c                   kfld                    rzuml1_biln
     c                   kfld                    rzuml1_bdat
      *
      * Key for les av sumpostering
      *
     c     rzuml2_key    klist
     c                   kfld                    w_firm
     c                   kfld                    rzuml2_raar
     c                   kfld                    rzuml2_bilg
     c                   kfld                    rzuml2_bdat
     c                   kfld                    rzuml2_tell
      *
      * Key for statusrecord
      *
     c     raa1l1_key    klist
     c                   kfld                    w_firm
      *
      * Henter firmanummer fra LDA
      *
     c                   eval      w_firm = l_firm
6.21 c                   eval      b2firm = w_firm
7.01  *
7.01  * Endring 7.01
7.01  *
7.01   CallP CO402R(l_filg:w_firm:0:'RK514R_701':
7.01                    co402_verdi1:co402_verdi2);
7.01   if co402_verdi1 = '1';
7.01     u_701 = *on;
7.01   endif;
      *
      * Les status-record
      *
     c     raa1l1_key    chain     raa1l1r
      *
     c                   if        not%found
     c                   endif
      *
      * Legg ut heading i skjermbildet
      *
     c                   eval      b2kont = p_kont
     c                   eval      b2spes = p_spes
     c                   eval      b2avde = p_avde
     c                   eval      b2tex1 = p_tex1
     c                   eval      b2biln = p_biln
     c                   eval      b2text = p_text
     c                   eval      b2belØ = p_neto
      *
     c                   eval      *in22 = *on
      *
      * Posisjonerer databasen, og fyller opp subfile
      *
     c                   eval      rzuml1_raar = p_raar
     c                   eval      rzuml1_biln = p_biln
     c                   eval      rzuml1_bdat = p_bdat
     c     rzuml1_key    setll     rzuml1
      *
     c                   exsr      crt_subfile
      *
     c                   endsr
      *
