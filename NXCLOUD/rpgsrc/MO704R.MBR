     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : MO703R
      * Beskrivelse . . : Overvåking - danner grunndata for type 5
      *                   Lister ut alle med 0 eller neg. beholdning pr.dato
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       050107 bø   Nytt program
      * E01   020408 tsv  Sjekker og legger bare ut hvis vare er aktiv ifg+
      *                   ønsket, retro oppkjøring bare aktiv pr.kjørt dato
5.70  *       260609 bof  Sjekker om varetype er lagervare
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvhisl3    if   e           k disk    rename(vhispfr:vhisl3r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     flwo6lu    uf a e           k disk    rename(lwo6pfr:lwo6lur)
      *
      *
     d vhisl3_vare     s                   like(vhvare)
     d vhisl3_lage     s                   like(vhlage)
     d vhisl3_dato     s                   like(vhdato)
     d vhisl3_time     s                   like(vhtime)
     d lwo6lu_odat     s                   like(mwodat)
     d lwo6lu_osys     s                   like(mwosys)
     d lwo6lu_olag     s                   like(mwolag)
     d lwo6lu_ovnr     s                   like(mwovnr)
     d lwo6lx_odat     s                   like(mwodat)
     d vvarl1_vare     s                   like(vvvare)
      *
     d w_firm          s                   like(vlfirm)
     d w_vare          s                   like(vlvare)
     d w_dato          s                   like(vledat)
     d w_aar           s              4  0
     d p_dato          s                   like(vledat)
     d w_usr           s                   like(vhuser)
     d w_oda           s                   like(vhdato)
     d w_behd          s                   like(vlbehl)
      *
     d b_funn          s              1    inz(*off)
      *
      * Local Data Area
      *
     d                uds
      *
     d l_firm                944    946  0
      *
      * Definering av variable i nøkler
      *
      *
      * Definering av variable
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * sletter først for dato som det skal kjøres for.
      *
     c                   eval      lwo6lx_odat = w_dato
readec     lwo6lx_key    setll     lwo6lu
readec     lwo6lx_key    reade     lwo6lu
readec                   dow       not %eof(lwo6lu)
     c                   delete    lwo6lur
readec     lwo6lx_key    reade     lwo6lu
     c                   enddo
      *
     c                   eval      w_aar = %subdt(w_dato:*years)
      *
     c                   exsr      les_lager
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     c     avslutt       tag
     c                   eval      *inlr = *on
      *
     c                   return
      *
      * --------------------------------------------------------------
      *  Leser ordre - logger ut varer med endret varetekst
      * --------------------------------------------------------------
      *
     c     les_lager     begsr
      *
readec     vlagl1_key    setll     vlagl1
readec     vlagl1_key    reade     vlagl1
readec                   dow       not %eof(vlagl1)
      *
5.70 c                   if        vlvtyp = 'L'
     c                   exsr      finn_behl
      *
     c                   if        b_funn = *on and
     c                             w_behd <= 0
     c                   eval      lwo6lu_odat = w_dato
     c                   eval      lwo6lu_osys = 'L'
     c                   eval      lwo6lu_olag = vllage
     c                   eval      lwo6lu_ovnr = vlvare
     c                   eval      w_usr       = *blank
     c                   eval      w_oda       = w_dato
     c                   eval      vvarl1_vare = vlvare
E01  c*    vvarl1_key    chain     vvarl1
E01  c     vvarl1_key    chain     vvarl1                             88
E01  C                   if        *in88 = *off
     c                   exsr      skriv_logg
E01  c                   endif
     c                   endif
5.70 c                   endif
      *
readec     vlagl1_key    reade     vlagl1
readec                   enddo
      *
     c                   endsr
      *
5.40  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.40  * Subrutine for henting av beholdning pr. en dato (hist.lagerb)
5.40  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
5.40  *
5.40 c     finn_behl     begsr
      *
     c                   eval      b_funn = *off
     c                   if        vlvare = '00387868'
     c                   eval      b_funn = *off
     c                   endif
5.40  *
5.40 c                   eval      vhisl3_vare = vlvare
5.40 c                   eval      vhisl3_lage = vllage
     c                   eval      vhisl3_dato = w_dato
5.40 c                   eval      vhisl3_time = *hival
5.40 c     vhisl3_key    setgt     vhisl3
5.40 c                   readp     vhisl3
5.40 c                   if        not %eof and
5.40 c                             vhvare = vhisl3_vare and
5.40 c                             vhlage = vhisl3_lage
5.40 c                   eval      w_behd = vhbehl
     c                   eval      b_funn = *on
5.40 c                   else
5.40 c                   eval      w_behd = 0
5.40 c                   endif
5.40  *
5.40 c                   endsr
      *
      * --------------------------------------------------------------
      *  Skriver logg ved feil-situasjon type 4
      * --------------------------------------------------------------
      *
     c     skriv_logg    begsr
      *
     c                   clear                   lwo6lur
     c     lwo6lu_key    chain     lwo6lu
     c                   eval      mwousr = vhuser
     c                   eval      mwooda = vhdato
     c                   eval      mwotim = vhtime
     c                   eval      mwobeh = vhbehl
     c                   eval      mwovtx = vvtek1
     c                   eval      mwoogr = vvogrp
     c                   eval      mwohgr = vvhgrp
     c                   eval      mwougr = vvugrp
     c                   if        %found(lwo6lu)
     c                   update    lwo6lur
     c                   else
     c                   eval      mwofir = w_firm
     c                   eval      mwodat = lwo6lu_odat
     c                   eval      mwosys = lwo6lu_osys
     c                   eval      mwovnr = lwo6lu_ovnr
     c                   eval      mwolag = lwo6lu_olag
     c                   write     lwo6lur
     c                   endif
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *  Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      *  Key for les av vare
      *
5.40  *
5.40  * Key for les av lager historikk
5.40  *
5.40 c     vhisl3_key    klist
5.40 c                   kfld                    w_firm
5.40 c                   kfld                    vhisl3_vare
5.40 c                   kfld                    vhisl3_lage
5.40 c                   kfld                    vhisl3_dato
5.40 c                   kfld                    vhisl3_time
      *
      *  Key for les av lager
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
      *
      *  Key for les av vare
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
5.40 c                   kfld                    vvarl1_vare
      *
      * Key for oppdatering av overvåking  6
      *
     c     lwo6lu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwo6lu_odat
     c                   kfld                    lwo6lu_osys
     c                   kfld                    lwo6lu_olag
     c                   kfld                    lwo6lu_ovnr
      *
      * Key for slett av overvåking på den dato man vil kjøre på.
      *
     c     lwo6lx_key    klist
     c                   kfld                    w_firm
     c                   kfld                    lwo6lx_odat
      *
     c     *entry        plist
     c                   parm                    p_dato
      *
     c                   eval      w_firm = l_firm
     c                   eval      w_dato = p_dato
      *
     c                   endsr
