     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System. . . . . : ASLAGR
      * Program . . . . : LL768R
      * Beskrivelse . . : Hvis man kun kjører med et lager (eller
      *                   ingen lagerstyring) så kan man endre
      *                   varetype på varen og det samme skal skje på lager
      *                   Hvis varetype settes til L og lager ikke finnes
      *                   dannes dette.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       021111 bof  Nytt program
6.20  * 6.20  310812 bof  Nytt felt, endringsdato for faste data som vises
6.nu  * 6.30  050614 bof  endring ihht. nummerutvidelse                s
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     fvvarl1    if   e           k disk    rename(vvarpfr:vvarl1r)
     fvlaglu    uf a e           k disk    rename(vlagpfr:vlaglur)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vlfirm)
     d p_vare          s                   like(vlvare)
     d p_lage          s                   like(vllage)
     d p_vtyp          s                   like(vlvtyp)
     d p_inlr          s              1
     d p_akti          s              1
      *
      * Datastruktur for lageroppdatering
      *
     d                 ds
6.nu d hlrec                        128
     d  hlvare                       15    overlay(hlrec)
     d  hllage                        2  0 overlay(hlrec:16)
     d  hldato                         d   overlay(hlrec:18) datfmt(*iso)
     d  hltime                         t   overlay(hlrec:28) timfmt(*iso)
     d  hlotyp                        2    overlay(hlrec:36)
     d  hlltyp                        2    overlay(hlrec:38)
     d  hlanta                       11  3 overlay(hlrec:40)
     d  hlkopr                        9  2 overlay(hlrec:51)
     d  hlrefr                       20    overlay(hlrec:60)
     d  hlsyst                        1    overlay(hlrec:80)
     d  hlaltk                        2    overlay(hlrec:81)
     d  hlrest                        1    overlay(hlrec:83)
     d  hltype                        1    overlay(hlrec:84)
     d  hlenhe                        3    overlay(hlrec:85)
     d  hlinlr                        1    overlay(hlrec:88)
     d  hllety                        1    overlay(hlrec:89)
6.nu d  hlonum                        8  0 overlay(hlrec:90)
6.nu d  hlolin                        4  0 overlay(hlrec:98)
      *
      * Definering av variable i nøkler
      *
     d vlagl1_vare     s                   like(vlvare)
     d vlagl1_lage     s                   like(vllage)
     d vvarl1_vare     s                   like(vvvare)
     d vlaglu_vare     s                   like(vlvare)
     d vlaglu_lage     s                   like(vllage)
      *
      * Definering av variable
      *
     d w_firm          s                   like(vlfirm)
     d w_vare          s                   like(vlvare)
     d w_lage          s                   like(vllage)
     d w_stat          s              1
      *
     d b_lagr          s              1    inz(*off)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
     c                   eval      w_vare = p_vare
     c                   eval      w_lage = p_lage
      *
     c     vvarl1_key    chain     vvarl1
     c                   if        %found(vvarl1)
      *
     c     vlagl1_key    chain     vlagl1
     c                   if        %found(vlagl1)
      *
     c                   if        vlvtyp = 'L' and
     c                             p_vtyp = 'S'
     c                   exsr      upd_skaff
     c                   goto      avslutt
     c                   endif
      *
     c                   if        vlvtyp = 'S' and
     c                             p_vtyp = 'L'
     c                   exsr      upd_lage
     c                   goto      avslutt
     c                   endif
      *
     c                   else
      * her finnes ikke lager av en eller annen grunn
      * kaller std. program for opprettelse
     c                   if        p_vtyp = 'L'
     c                   call      'LL760R'
     c                   parm                    w_firm
     c                   parm                    vvvare
     c                   exsr      upd_lage
     c                   goto      avslutt
     c                   endif
     c                   endif
     c                   endif
      *
      * Avslutt program
      *
     c     avslutt       tag
     c                   if        p_inlr <> *on
     c                   eval      *inlr = *on
     c                   endif
      *
     c                   return
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å oppdatere varetype til skaffevare
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     upd_skaff     begsr
      *
      * må også sette alle hittil i år tall til 0, ellers vil
      * i mange tilfeller betraktes som aktiv og vareregister
      *
     c     vlaglu_key    chain     vlaglur
     c                   if        %found(vlaglu)
     c                   eval      vlbbes = 0
     c                   eval      vloord = 0
     c                   eval      vlpakk = 0
     c                   eval      vlinng = 0
     c                   eval      vlutta = 0
     c                   eval      vltell = 0
     c                   eval      vlsalg = 0
     c                   time                    vledat
     c                   time                    vletim
     c                   eval      vleusr = l_user
6.20 c                   time                    vlfeda
6.20 c                   time                    vlfeti
6.20 c                   eval      vlfeus = l_user
     c                   update    vlaglur
     c                   endif
      *
     c                   move      w_vare        hlvare
     c                   move      w_lage        hllage
     c                   move      vvenhl        hlenhe
     c                   time                    hldato
     c                   time                    hltime
     c                   eval      hlotyp  = *blank
     c                   eval      hlltyp  = *blank
      *
     c                   eval      hlanta = 0
     c                   eval      hlkopr = 0
      *
     c                   eval      hlrefr = 'Vtyp endret L--> S '
     c                   move      'L'           hlsyst
     c                   eval      hlaltk = 'J '
     c                   eval      hlrest = *blank
     c                   eval      hlonum = 0
     c                   eval      hlolin = 0
      *
      *
     c                   eval      w_stat = *blank
      *
     c                   call      'VL001R'
     c                   parm                    w_stat
     c                   parm                    hlrec
      *
     c     vlaglu_key    chain     vlaglur
     c                   if        %found(vlaglu)
     c                   eval      vlvtyp = 'S'
     c                   time                    vledat
     c                   time                    vletim
     c                   eval      vleusr = l_user
     c                   update    vlaglur
     c                   endif
      *
     c     end_oppd      endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for å oppdatere varetype til lager
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     upd_lage      begsr
      *
     c                   clear                   vlaglur
     c     vlaglu_key    chain     vlaglur
     c                   if        %found(vlaglu)
     c                   eval      vlvtyp = 'L'
     c                   time                    vledat
     c                   time                    vletim
     c                   eval      vleusr = l_user
     c                   update    vlaglur
     c                   endif
      *
     c                   move      w_vare        hlvare
     c                   move      w_lage        hllage
     c                   move      vvenhl        hlenhe
     c                   time                    hldato
     c                   time                    hltime
     c                   eval      hlotyp  = *blank
     c                   eval      hlltyp  = *blank
      *
     c                   eval      hlanta = 0
     c                   eval      hlkopr = 0
      *
     c                   eval      hlrefr = 'Vtyp endret S--> L '
     c                   move      'L'           hlsyst
     c                   eval      hlaltk = 'SL'
     c                   eval      hlrest = *blank
     c                   eval      hlonum = 0
     c                   eval      hlolin = 0
      *
     c                   eval      w_stat = *blank
     c                   call      'VL001R'
     c                   parm                    w_stat
     c                   parm                    hlrec
      *
     c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_vare
     c                   parm                    p_lage
     c                   parm                    p_vtyp
     c                   parm                    p_inlr
     c                   parm                    p_akti
      *
      * Key for les på lagerregister
      *
     c     vlagl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
     c                   kfld                    w_lage
      *
      * Key for oppdater på lagerregister
      *
     c     vlaglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
     c                   kfld                    w_lage
      *
      * Key for les på vareregister
      *
     c     vvarl1_key    klist
     c                   kfld                    w_firm
     c                   kfld                    w_vare
      *
     c                   endsr
