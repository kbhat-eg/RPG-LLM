     h option(*nodebugio) datedit(*dmy)
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * System  . . . . : ASOFAK
      * Program . . . . : VV116R
      * Beskrivelse . . : Vare, sammenslåing av varer
      *
      *                   Sletter vare og relasjoner for utgående vare,
      *                   slår sammen lagerbeholdning, og kaller
      *                   standardprogrammer for å bytte varenr på
      *                   utgående vare til den det skal slås sammen med.
      *
      * Spesialversjon. :
      * Tilpasset for . :
      *
      * Ref.   Dato  Sign Endringsbeskrivelse
      * ----- ------ ---- -------------------------------------------------
      *       120206 HKO  Nytt program
      * 6.10  250609 BSA  Oppdatert med sporingsinformasjon
6.11  * 6.10  070109 bof  Utvidet med vveppf
6.20  * 6.20  051110 bof  Utvidet med lager på sortiment
6.21  * 6.20  020511 bof  Utvidet varegr/lev.nr på vtilpf
6.30  * 6.30  261016 bof  GTIN fra g.vare legges i VEANPF
6.31  * 6.30  220719 bof  Legger inn en unlock i veanpf
6.32  * 6.30  061119 bof  Utvidet med vvlepf
      *
7.01  *       121115 nho  Flytter flere felter ved sammenslåing
8.01  *       150622 bof  Flytter flere felter ved sammenslåing II
8.02  *       040425 bof NXS-20598 test på lag.parm hva som slås sammen
      *
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     fvvarlu    uf   e           k disk    rename(vvarpfr:vvarlur)
     fvvaslu    uf a e           k disk    rename(vvaspfr:vvaslur)
      *
     fvsdtl7    uf   e           k disk    rename(vsdtpfr:vsdtl7r)
     fvpdtla    uf   e           k disk    rename(vpdtpfr:vpdtlar)
     fvbrkl6    uf   e           k disk    rename(vbrkpfr:vbrkl6r)
     ffpril5    uf   e           k disk    rename(fpripfr:fpril5r)
     ffrabla    uf   e           k disk    rename(frabpfr:frablar)
     fvvenlu    uf   e           k disk    rename(vvenpfr:vvenlur)
6.11 fvveplu    uf   e           k disk    rename(vveppfr:vveplur)
6.32 fvvlelu    uf   e           k disk    rename(vvlepfr:vvlelur)
     fvvprlu    uf   e           k disk    rename(vvprpfr:vvprlur)
     fvvsolu    uf   e           k disk    rename(vvsopfr:vvsolur)
6.21 fvtill9    uf   e           k disk    rename(vtilpfr:vtill9r)
     fvetilu    uf   e           k disk    rename(vetipfr:vetilur)
     fanotlu    uf   e           k disk    rename(anotpfr:anotlur)
      *
     fvlagl1    if   e           k disk    rename(vlagpfr:vlagl1r)
     f                                     prefix(xl:2)
     fvlaglu    uf   e           k disk    rename(vlagpfr:vlaglur)
6.30 fveanlu    uf a e           k disk    rename(veanpfr:veanlur)
      *
      * Definering av parametre
      *
     d p_firm          s                   like(vvfirm)
     d p_gvar          s                   like(vvvare)
     d p_nvar          s                   like(vvvare)
      *
      * Definering av Local data area
      *
     d                uds
     d l_user                911    920
      *
      * Definering av variable i nøkler
      *
     d vvarlu_vare     s                   like(vvvare)
     d anotlu_syst     s                   like(ausyst)
     d anotlu_noid     s                   like(aunoid)
     d vlaglu_vare     s                   like(vlvare)
     d vlaglu_lage     s                   like(vllage)
6.30 d veanlu_eann     s                   like(vxeann)
      *
      * Definering av variable
      *
     d b_inlr          s              1    inz(*off)
      *
     d w_firm          s                   like(vvfirm)
     d w_gvar          s                   like(vvvare)
     d w_nvar          s                   like(vvvare)
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Hovedprogram
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
      * Sletter "gammel" vare som skal utgå
      *
     c     key_gml       chain     vvarlur
     c                   if        not %found
     c                   goto      avslutt
     c                   endif
      *
     c                   delete    vvarlur
      *
      * Oppretter vare i register over slettede varer
      *
     c     key_gml       chain     vvaslur
     c                   if        %found
     c                   unlock    vvaslu
     c                   else
     c                   eval      vvsusr = l_user
     c                   time                    vvsdat
     c                   time                    vvstim
6.10 c                   time                    vvodat
6.10 c                   time                    vvotim
6.10 c                   eval      vvousr = l_user
6.10 c                   time                    vvedat
6.10 c                   time                    vvetim
6.10 c                   eval      vveusr = l_user
     c                   write     vvaslur
     c                   endif
      *
      * Fjerner vare fra vare-enhet
      *
     c     key_gml       setll     vvenlu
     c     key_gml       reade     vvenlur
     c                   dow       not %eof
     c                   delete    vvenlur
     c     key_gml       reade     vvenlur
     c                   enddo
6.11  *
6.11  * Fjerner vare fra vare-enhet-prisgiver
6.11  *
6.11 c     key_gml       setll     vveplu
6.11 c     key_gml       reade     vveplur
6.11 c                   dow       not %eof
6.30 c                   if        vepgti <> 0
6.30 c                   exsr      sjekk_vean
6.30 c                   endif
6.11 c                   delete    vveplur
6.11 c     key_gml       reade     vveplur
6.11 c                   enddo
6.32  *
6.32  * Fjerner vare fra vare-leverandør
6.32  *
6.32 c     key_gml       setll     vvlelu
6.32 c     key_gml       reade     vvlelur
6.32 c                   dow       not %eof
6.32 c                   delete    vvlelur
6.32 c     key_gml       reade     vvlelur
6.32 c                   enddo

      *
      * Fjerner vare fra vare-pris
      *
     c     key_gml       setll     vvprlu
     c     key_gml       reade     vvprlur
     c                   dow       not %eof
     c                   delete    vvprlur
     c     key_gml       reade     vvprlur
     c                   enddo
      *
      * Fjerner vare fra vare-søketekst
      *
     c     key_gml       setll     vvsolu
     c     key_gml       reade     vvsolur
     c                   dow       not %eof
     c                   delete    vvsolur
     c     key_gml       reade     vvsolur
     c                   enddo
      *
      * Fjerner vare fra etikett
      *
     c     key_gml       setll     vetilu
     c     key_gml       reade     vetilur
     c                   dow       not %eof
     c                   delete    vetilur
     c     key_gml       reade     vetilur
     c                   enddo
      *
      * Fjerner vare fra sortiments-linje dersom den nye varen finnes
      *
     c     key_ny        chain     vsdtl7
     c                   if        %found
     c                   unlock    vsdtl7
     c     key_gml       setll     vsdtl7
     c     key_gml       reade     vsdtl7r
     c                   dow       not %eof
     c                   delete    vsdtl7r
     c     key_gml       reade     vsdtl7r
     c                   enddo
     c                   endif
      *
      * Fjerner vare fra påslag-linje dersom den nye varen finnes
      *
     c     key_ny        chain     vpdtla
     c                   if        %found
     c                   unlock    vpdtla
     c     key_gml       setll     vpdtla
     c     key_gml       reade     vpdtlar
     c                   dow       not %eof
     c                   delete    vpdtlar
     c     key_gml       reade     vpdtlar
     c                   enddo
     c                   endif
      *
      * Fjerner vare fra bruksrett-knytning dersom den nye varen finnes
      *
     c     key_ny        chain     vbrkl6
     c                   if        %found
     c                   unlock    vbrkl6
     c     key_gml       setll     vbrkl6
     c     key_gml       reade     vbrkl6r
     c                   dow       not %eof
     c                   delete    vbrkl6r
     c     key_gml       reade     vbrkl6r
     c                   enddo
     c                   endif
      *
      * Fjerner vare fra spesialpris dersom den nye varen finnes
      *
     c     key_ny        chain     fpril5
     c                   if        %found
     c                   unlock    fpril5
     c     key_gml       setll     fpril5
     c     key_gml       reade     fpril5r
     c                   dow       not %eof
     c                   delete    fpril5r
     c     key_gml       reade     fpril5r
     c                   enddo
     c                   endif
      *
      * Fjerner vare fra rabattmatrise dersom den nye varen finnes
      *
     c     key_ny        chain     frabla
     c                   if        %found
     c                   unlock    frabla
     c     key_gml       setll     frabla
     c     key_gml       reade     frablar
     c                   dow       not %eof
     c                   delete    frablar
     c     key_gml       reade     frablar
     c                   enddo
     c                   endif
      *
      * Fjerner vare fra tibudspris dersom den nye varen finnes
      *
6.21 c     key_ny        chain     vtill9
     c                   if        %found
6.21 c                   unlock    vtill9
6.21 c     key_gml       setll     vtill9
6.21 c     key_gml       reade     vtill9r
     c                   dow       not %eof
6.21 c                   delete    vtill9r
6.21 c     key_gml       reade     vtill9r
     c                   enddo
     c                   endif
      *
      * Fjerner notat på vare dersom den nye varen finnes
      *
     c                   eval      anotlu_syst = 'VVAR'
     c                   eval      anotlu_noid = p_nvar
     c     anotlu_key    chain     anotlu
     c                   if        %found
     c                   unlock    anotlu
     c                   eval      anotlu_noid = p_gvar
     c     anotlu_key    setll     anotlu
     c     anotlu_key    reade     anotlur
     c                   dow       not %eof
     c                   delete    anotlur
     c     anotlu_key    reade     anotlur
     c                   enddo
     c                   endif
      *
      * Slår sammen informasjonen i lager
      * Dersom lager finnes på gammel og nytt varenr, summeres saldi fra
      * gammelt varenr inn i det nye, og gammelt slettes
      *
     c     key_gml       setll     vlagl1
     c     key_gml       reade     vlagl1
     c                   dow       not %eof
      *
     c                   eval      vlaglu_vare = p_nvar
     c                   eval      vlaglu_lage = xllage
     c     vlaglu_key    chain     vlaglur
     c                   if        %found
      *
     c                   eval      vlpakk = vlpakk + xlpakk
     c                   eval      vloord = vloord + xloord
     c                   eval      vlores = vlores + xlores
     c                   eval      vlbbes = vlbbes + xlbbes
     c                   eval      vlbres = vlbres + xlbres
     c                   eval      vlinng = vlinng + xlinng
     c                   eval      vlutta = vlutta + xlutta
     c                   eval      vljust = vljust + xljust
     c                   eval      vltell = vltell + xltell
     c                   eval      vlsalg = vlsalg + xlsalg
     c                   eval      vlb101 = vlb101 + xlb101
     c                   eval      vlbehl = vlbehl + xlbehl
     c                   eval      vlbeve = vlbeve + xlbeve
     c                   eval      vlb101 = vlb101 + xlb101
8.02  * test Hvis tilvare har lagrparm, så la det ligge. Hvis
8.02  * null, så tas det med fra fra-vare
8.02 c                   if        vlmini = 0
7.01 c                   eval      vlmini = vlmini + xlmini
8.02 c                   endif
8.02 c                   if        vlmaks = 0
8.01 c                   eval      vlmaks = vlmaks + xlmaks
8.02 c                   endif
8.02 c                   if        vlbpun = 0
8.01 c                   eval      vlbpun = vlbpun + xlbpun
8.02 c                   endif
8.02 c                   if        vlbmin = 0
8.01 c                   eval      vlbmin = vlbmin + xlbmin
8.02 c                   endif
8.02 c                   if        vlbmen = 0
8.01 c                   eval      vlbmen = vlbmen + xlbmen
8.02 c                   endif
7.01 c                   eval      vlkabc = xlkabc
7.01 c                   eval      vllabc = xllabc
7.01 c                   eval      vltabc = xltabc
8.01 c                   eval      vlplas = xlplas
8.01 c                   eval      vlpla1 = xlpla1
8.01 c                   eval      vlpla2 = xlpla2
8.01 c                   eval      vlvtyp = xlvtyp
8.01 c                   eval      vlltid = xlltid
     c                   if        xltrda > vltrda
     c                   eval      vltrda = xltrda
     c                   endif
     c                   if        xlopda > vlopda
     c                   eval      vlopda = xlopda
     c                   endif
     c                   if        xlsdat > vlsdat
     c                   eval      vlsdat = xlsdat
     c                   endif
      *
6.10 c                   time                    vledat
6.10 c                   time                    vletim
6.10 c                   eval      vleusr = l_user
     c                   update    vlaglur
      *
     c                   eval      vlaglu_vare = p_gvar
     c                   eval      vlaglu_lage = xllage
     c     vlaglu_key    chain     vlaglur
     c                   delete    vlaglur
      *
     c                   endif
      *
     c     key_gml       reade     vlagl1
     c                   enddo
      *
      * Kaller standardprogram for bytting av varenummer i tabeller
      *
     c                   call      'VV740R'
     c                   parm                    p_firm
     c                   parm                    p_gvar
     c                   parm                    p_nvar
     c                   parm                    b_inlr
      *
      * Kaller standardprogram for bytting av varenummer i lager/innkjøp
      *
     c                   call      'FO740C'
     c                   parm                    p_firm
     c                   parm                    p_gvar
     c                   parm                    p_nvar
     c                   parm                    b_inlr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Avslutt program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     avslutt       tag
      *
     c                   eval      *inlr = *on
     c                   return
6.30  *
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  * Subrutine for å sjekke om man kan legge ut gtin fra gml.vare
6.30  * til VEANPF
6.30  * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
6.30  *
6.30 c     sjekk_vean    begsr
6.30  *
6.30 c                   eval      veanlu_eann = vepgti
6.30 c     veanlu_key    chain     veanlu
6.30 c                   if        not %found(veanlu)
6.30 c                   eval      vxfirm = w_firm
6.30 c                   eval      vxvare = p_nvar
6.30 c                   eval      vxenhe = vepenh
6.30 c                   eval      vxeann = vepgti
6.30 c                   time                    vxtime
6.30 c                   time                    vxodat
6.30 c                   time                    vxotim
6.30 c                   eval      vxousr = l_user
6.30 c                   time                    vxedat
6.30 c                   time                    vxetim
6.30 c                   eval      vxeusr = l_user
6.30 c                   write     veanlur
6.31 c                   else
6.31 c                   unlock    veanlu
6.30 c                   endif
6.30  *
6.30 c                   endsr
      *
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      * Subrutine for initiering av program
      * - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      *
     c     *inzsr        begsr
      *
      * Parametre
      *
     c     *entry        plist
     c                   parm                    p_firm
     c                   parm                    p_gvar
     c                   parm                    p_nvar
      *
      * Key for oppdatering av vare
      *
     c     vvarlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vvarlu_vare
      *
      * Key for oppslag på "gammel varenr"
      *
     c     key_gml       klist
     c                   kfld                    w_firm
     c                   kfld                    w_gvar
      *
      * Key for oppslag på "nytt varenr"
      *
     c     key_ny        klist
     c                   kfld                    w_firm
     c                   kfld                    w_nvar
      *
      * Key for oppslag på / oppdatering av notat
      *
     c     anotlu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    anotlu_syst
     c                   kfld                    anotlu_noid
      *
      * Key for oppdatering av lager
      *
     c     vlaglu_key    klist
     c                   kfld                    w_firm
     c                   kfld                    vlaglu_vare
     c                   kfld                    vlaglu_lage
6.30  *
6.30  * Key for oppdatering av veanpf
6.30  *
6.30 c     veanlu_key    klist
6.30 c                   kfld                    w_firm
6.30 c                   kfld                    veanlu_eann
      *
      *
      * Henter firma fra parameter
      *
     c                   eval      w_firm = p_firm
      *
      * Initierer key
      *
     c                   eval      w_gvar = p_gvar
     c                   eval      w_nvar = p_nvar
      *
     c                   endsr
