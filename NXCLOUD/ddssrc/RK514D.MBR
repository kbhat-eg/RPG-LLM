     A*%%TS  SD  20070528  153554  KOBASP      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RK511D
     A* Beskrivelse . . : Kndeposteringer, spørring
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       050602 KOB  Nytt program
7.xx A*       020516 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20031025  121317  KOB         REL-V5R2M0  5722-WDS
     A            B1TIST    R        H      REFFLD(RNTIST)
     A            B1BDAT    R        H      REFFLD(RNBDAT)
     A            B1VALG    R        B  9  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 8 9)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1SAMF    R        O  9  4REFFLD(RNSAMF)
6.NU A            B1KUND    R        O  9 13REFFLD(RKKUND)
6.NU A            B1NAVN        28   O  9 20
     A            B1BIDA         6Y 0O  9 49EDTCDE(Y)
     A            B1BELØ        11Y 2O  9 59EDTCDE(J)
     A*
     A*  Bilde B2 - subfile kontroll
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20070528  153554  KOBASP      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på generalnota'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  7  2'8=Vis poster'
     A                                      COLOR(BLU)
     A                                  5 60'Regnskapsår. .:'
     A                                      COLOR(WHT)
     A            B2RAAR    R        B  5 76REFFLD(RNRAAR)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A                                  5  4'Gen.not'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  4'Gen.not'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 13'Kunde'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 20'Navn                          '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 51'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 63'Beløp'
7.xx A**                                                   Dato        Beløp'
     A                                      COLOR(WHT)
7.xx A            B2SAMF    R        B  6  4REFFLD(RNSAMF)
7.xx A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A*
     A*  Bilde B2 - funksjonstaster
     A*
     A          R B2CMD
     A*%%TS  SD  20020106  205227  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F5=Forny'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
