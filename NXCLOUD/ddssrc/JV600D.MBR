     A*%%TS  SD  19990830  125423  HKO         REL-V4R2M0  5769-PW1
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A*       010816 bsa  Endringer relatert til ny GUI
     A*
     A*---------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  19990830  125423  HKO         REL-V4R2M0  5769-PW1
     A*                                     WINDOW(*DFT 15 57)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Utskrift av vareregister '
     A                                      DSPATR(RI)
7.xx A                                  2 40'JV600B1'
     A                                  4  2'Rapporttype. . . .'
     A            B1VALG    R        B  4 22REFFLD(JÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(1 2)
     A                                  4 34'1=Pris-/vareinformasjon'
     A                                  5 34'2=Vare-/søketekster'
7.xx A                                  6  2'Leverandør . . . +'
     A            B1LDOR    R        B  6 22REFFLD(JVLDOR)
     A  31                                  ERRMSGID(JML0020 *LIBL/QMFVADM 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                  7  2'Bruker (endret av)'
     A            B1EUSR    R        B  7 22REFFLD(JVEUSR)
     A                                      COLOR(YLW)
7.xx A                                  9 22'F.o.m.'
7.xx A                                  9 41'T.o.m.'
7.xx A                                 10  2'Varegruppe . . . +'
     A            B1OGRF    R        B 10 22REFFLD(JVOGRP)
     A  32                                  ERRMSGID(JVG0100 *LIBL/QMFVADM 32)
     A  36                                  ERRMSGID(JM00200 *LIBL/QMFVADM 36)
     A                                      COLOR(YLW)
7.xx A                                 10 25'+'
7.xx A            B1HGRF    R        B 10 27REFFLD(JVHGRP)
     A  33                                  ERRMSGID(JVG0110 *LIBL/QMFVADM 33)
     A                                      COLOR(YLW)
7.xx A                                 10 30'+'
7.xx A            B1UGRF    R        B 10 32REFFLD(JVUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A                                 10 39'+'
7.xx A            B1OGRT    R        B 10 41REFFLD(JVOGRP)
     A  34                                  ERRMSGID(JVG0100 *LIBL/QMFVADM 34)
     A                                      COLOR(YLW)
7.xx A                                 10 44'+'
7.xx A            B1HGRT    R        B 10 46REFFLD(JVHGRP)
     A  35                                  ERRMSGID(JVG0110 *LIBL/QMFVADM 35)
     A                                      COLOR(YLW)
7.xx A                                 10 49'+'
7.xx A            B1UGRT    R        B 10 51REFFLD(JVUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A                                 11  2'Varenummer . . . +'
     A            B1VARF         8Y 0B 11 22COLOR(YLW)
     A  37                                  ERRMSGID(JM00200 *LIBL/QMFVADM 37)
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
7.xx A                                 11 39'+'
7.xx A            B1VART         8Y 0B 11 41COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A                                 12  2'Endringsdato . . .'
     A            B1EDAF         6Y 0B 12 22COLOR(YLW)
     A  38                                  ERRMSGID(JM00250 *LIBL/QMFVADM 38)
     A  40                                  ERRMSGID(JM00260 *LIBL/QMFVADM 40)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
7.xx A            B1EDAT         6Y 0B 12 41COLOR(YLW)
     A  39                                  ERRMSGID(JM00250 *LIBL/QMFVADM 39)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
