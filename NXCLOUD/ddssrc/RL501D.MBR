     A*%%TS  SD  20080321  124137  KOBASP      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RL501D
     A* Beskrivelse . . : Leverandør, spørring
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       300702 KOB  Nytt program
PDF  A*       080610 BSA  Foreløpig fjernes mulighet for oppslag mot arkiv.
6.22 A*       290711 BSA  Gjeninnført søk mot arkiv alle poster
7.xx A*       030516 BSA  Endringer relatert til ny GUI
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20050723  171257  KOB540      REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  9  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(5 6 7 8 9 0)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1LEVR    R        O  9  4REFFLD(RLLEVR)
     A            B1NAVN    R        O  9 11REFFLD(RLNAVN)
     A            B1STED        24   O  9 42
     A            B1SALD    R        O  9 67REFFLD(RLSALD)
     A                                      EDTWRD('      .   ,  -')
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20080321  124137  KOBASP      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CF06
     A                                      CF07
     A                                      CF08
     A                                      CA09
     A                                      CA11
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
6.10 A            B2FIRM         3  0O  1 33DSPATR(ND)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på leverandører'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Frisøk .'
     A            B2STEK        35A  B  4 11COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A                                  5  4'Lever '
7.xx A                                      COLOR(WHT)
7.xx A                                  5 11'Leverandørnavn       '
7.xx A                                      COLOR(WHT)
7.xx A                                  5 42'Postnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  4'Lever '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 11'Leverandørnavn'
     A                                      COLOR(WHT)
     A  80                              8 42'Postnr/poststed'
     A                                      COLOR(WHT)
7.xx A  81                              8 42'Gateadresse'
     A                                      COLOR(WHT)
7.xx A  82                              8 42'Telefon      Telefax'
     A                                      COLOR(WHT)
7.xx A  83                              8 42'Foretaksnr.  Bankkonto'
     A                                      COLOR(WHT)
7.xx A                                  8 70'Saldo'
     A                                      COLOR(WHT)
7.xx A            B2LEVR    R        B  6  4REFFLD(RLLEVR)
     A  38                                  ERRMSGID(RMK0005 *LIBL/QMFOKON 38)
     A  31                                  ERRMSGID(RMK0010 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RMK0030 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2ALFA    R        B  6 11REFFLD(RLALFA)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2PONR    R        B  6 42REFFLD(RLPONR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  7  2'5=Vis  6=Utskrift  7=Vis samlegiro-
PDF  A**6.22                                  8=Vis poster'
6.22 A                                        8=Vis poster  9=Vis arkiv'
     A                                      COLOR(BLU)
     A*
     A          R B2CMD
     A*%%TS  SD  20050723  172537  KOB540      REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                 22  2'F5=Aktiv/Alle  F6=Samlegiro  F7=Ut-
     A                                      valg  F8=Posteringer  F9=Gjenta søk'
     A                                      COLOR(BLU)
     A                                 23  2'F3=Gå tilbake  F4=Utbetalingsforsl-
     A                                      ag    F11=Endre liste'
     A                                      COLOR(BLU)
     A*
     A* Bilde V1 - Vindu
     A* for utvalg
     A*
     A          R V1WIN
     A*%%TS  SD  19950511  183051  KB          REL-V3R1M0  5763-PW1
     A                                      WINDOW(*DFT 11 28)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Foreta utvalg '
     A                                      COLOR(GRN)
     A                                      DSPATR(RI)
     A                                  5  2'Saldo > enn :'
     A            V1SALD         9Y 0B  5 17EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                 10  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
      *
     A          R ASUME                     ASSUME
     A                                 24 79' '
