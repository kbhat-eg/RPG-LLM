     A*%%TS  SD  20140714  093510  BOF630      REL-V7R1M0  5770-WDS
     A*
     A* 280814 BOF : ny feilmelding hvis lev. eller p.giv er pass/sperret
6.32 A* 211014 Bsa : Justert i skjermbildet pga jacada.
7.xx A* 040316 bsa Endringer relatert til ny GUI
8.01 A*PRG2 140622 BOF : utvidet prisgruppe fra 1 til 2 posisjoner
     A*---------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HELP
     A          R B1BLD
     A*%%TS  SD  20140714  093510  BOF630      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Overføring til eget vareregister'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Prisendringsdato  . . . . .'
     A            B1DATO         6Y 0B  5 30COLOR(YLW)
     A  43                                  ERRMSGID(JM00250 *LIBL/QMFVADM 43)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
7.xx A                                  8  2'Leverandør (vareier). . . +'
     A            B1LDOR    R        B  8 30REFFLD(JVLDOR)
     A  31                                  ERRMSGID(JML0300 *LIBL/QMFVADM 31)
     A  32                                  ERRMSGID(JML0020 *LIBL/QMFVADM 32)
     A  41                                  ERRMSGID(LSO0100 *LIBL/QMFLAGR 41)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B1NAVN    R        O  8 44REFFLD(JLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  9  2'Varegruppe  . . . . . . . +'
     A            B1OGRP    R        B  9 30REFFLD(JVOGRP)
     A  33                                  ERRMSGID(JVG1020 *LIBL/QMFVADM 33)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  9 33'+'
7.xx A            B1HGRP    R        B  9 35REFFLD(JVHGRP)
     A  34                                  ERRMSGID(JVG2020 *LIBL/QMFVADM 34)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  9 38'+'
7.xx A            B1UGRP    R        B  9 40REFFLD(JVUGRP)
     A  35                                  ERRMSGID(JVG0020 *LIBL/QMFVADM 35)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B1UTXT        35A  O  9 44COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 10  2'Sortimentskode  . . . . . +'
     A            B1OSOR    R        B 10 30REFFLD(JSOSOR)
     A                                      COLOR(YLW)
     A  39                                  ERRMSGID(JMT0020 *LIBL/QMFVADM 39)
     A                                      OVRDTA
7.xx A            B1STXT    R        O 10 44REFFLD(JLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11  2'Ta med utgåtte priser . . .'
     A            B1UTGP         1Y 0B 11 30
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A                                      OVRDTA
7.xx A                                 11 44'1=Ja,0=Nei'
7.xx A                                 12  2'Prisgivende leverandør. . +'
     A            B1PGIV    R        B 12 30REFFLD(JVLDOR)
     A  36                                  ERRMSGID(JML0020 *LIBL/QMFVADM 36)
     A  40                                  ERRMSGID(LSO0100 *LIBL/QMFLAGR 40)
     A  22                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B1PNAV    R        O 12 44REFFLD(JLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 14  2'Se kalkyle for:'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
7.xx A                                 15  2'Leveringstype . . . . . . +'
     A            B1LETY         1Y 0B 15 30COLOR(YLW)
     A  37                                  ERRMSGID(JML0020 *LIBL/QMFVADM 37)
     A                                      OVRDTA
7.xx A            B1LBES        35A  O 15 44COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 16  2'Prisgruppe  . . . . . . . +'
8.01 A            B1PRGR         2A  B 16 30COLOR(YLW)
     A  38                                  ERRMSGID(JMC0120 *LIBL/QMFVADM 38)
     A                                      OVRDTA
7.xx A            B1PTXT        35A  O 16 44COLOR(YLW)
     A                                      OVRDTA
     A                                 18  2'Opprett varene som:'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                 19  2'Skaffevarer . . . . . . . .'
6.32 A            B1SKAF         1Y 0B 19 30COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A                                      OVRDTA
7.xx A                                 19 44'1=Ja,0=Nei'
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A                                  7  2'Utplukk:'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
