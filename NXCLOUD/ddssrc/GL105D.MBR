     A*%%TS  SD  20040219  134156  RHO         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASTELE
     A* Format. . . . . : GL105D
     A* Beskrivelse . . : Overføring av utbetalingsforslag
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       200103 KOB  Nytt program
     A*
7.02 A*           051224  Varsel om det finnes poster uten          MST  *
7.02 A*                    KID eller leverandørens fakt.nr          MST  *
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      WDWBORDER((*COLOR BLU) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A          R A1BLD
     A*%%TS  SD  19950824  155544  RH1         REL-V3R1M0  5763-PW1
     A                                      PRINT
     A                                      TEXT('Registrer utbetalingsforslags-
     A                                      nummer')
     A                                      CA03
     A                                      CLRL(*ALL)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Klargjøring/Overføring'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Betalingsforslagsnummer. . .'
     A            A1BFNR    R        B  5 31REFFLD(ROBFNR)
     A                                      TEXT('Bet.forslagsnr.')
     A                                      COLOR(YLW)
     A  62                                  ERRMSGID(RML0360 *LIBL/QMFOKON 62)
     A  63                                  ERRMSGID(RML0380 *LIBL/QMFOKON 63)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B1WIN
     A*%%TS  SD  19970623  120051  RHO         REL-V3R7M0  5716-PW1
     A                                      TEXT('Statuspost - samlefile')
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      PRINT
     A                                  2  2' Informasjon '
     A                                      DSPATR(RI)
     A                                  4  2'Overføring av transaksjoner til'
     A                                      COLOR(WHT)
     A                                  5  2'samlefil pågår...'
     A                                      COLOR(WHT)
     A                                  7  2'Innland:'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7 19'Utland:'
     A                                      DSPATR(UL)
     A N50                                  DSPATR(ND)
     A                                      COLOR(WHT)
     A                                  8  2'Godkjente:'
     A                                      COLOR(WHT)
     A            A1INGO         4Y 0O  8 13DSPATR(UL)
     A                                      TEXT('Antall genererte poster')
     A                                      EDTCDE(Z)
     A                                      COLOR(WHT)
     A                                  8 19'Godkjente:'
     A N50                                  DSPATR(ND)
     A                                      COLOR(WHT)
     A            A1UTGO         4Y 0O  8 30DSPATR(UL)
     A                                      TEXT('Antall genererte poster')
     A                                      EDTCDE(Z)
     A N50                                  DSPATR(ND)
     A                                      COLOR(WHT)
     A                                  9  2'Avviste. :'
     A                                      COLOR(WHT)
     A            A1INAV         4Y 0O  9 13DSPATR(UL)
     A                                      TEXT('Antall genererte poster')
     A                                      EDTCDE(Z)
     A                                      COLOR(WHT)
     A                                  9 19'Avviste. :'
     A N50                                  DSPATR(ND)
     A                                      COLOR(WHT)
     A            A1UTAV         4Y 0O  9 30DSPATR(UL)
     A                                      TEXT('Antall genererte poster')
     A                                      EDTCDE(Z)
     A N50                                  DSPATR(ND)
     A                                      COLOR(WHT)
     A          R C1WIN
     A*%%TS  SD  19950330  092307  RH1         REL-V2R3M0  5738-PW1
     A                                      TEXT('Statuspost - samlefile Jobb f-
     A                                      erdig')
     A                                      PRINT
     A                                      WINDOW(&W_AROW &W_ACOL 13 37)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2' Informasjon '
     A                                      DSPATR(RI)
     A                                  4  2'Overføring av transaksjoner til'
     A                                      COLOR(WHT)
     A                                  5  2'samlefil er ferdig.'
     A                                      COLOR(WHT)
     A                                  7  2'Innland:'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7 19'Utland:'
     A                                      DSPATR(UL)
     A N50                                  DSPATR(ND)
     A                                      COLOR(WHT)
     A                                  8  2'Godkjente:'
     A                                      COLOR(WHT)
     A            A1INGO         4  0O  8 13DSPATR(UL)
     A                                      TEXT('Antall genererte poster')
     A                                      EDTCDE(Z)
     A                                      COLOR(WHT)
     A                                  8 19'Godkjente:'
     A N50                                  DSPATR(ND)
     A                                      COLOR(WHT)
     A            A1UTGO         4  0O  8 30DSPATR(UL)
     A                                      TEXT('Antall genererte poster')
     A                                      EDTCDE(Z)
     A N50                                  DSPATR(ND)
     A                                      COLOR(WHT)
     A                                  9  2'Avviste. :'
     A                                      COLOR(WHT)
     A            A1INAV         4  0O  9 13DSPATR(UL)
     A                                      TEXT('Antall genererte poster')
     A                                      EDTCDE(Z)
     A                                      COLOR(WHT)
     A                                  9 19'Avviste. :'
     A N50                                  DSPATR(ND)
     A                                      COLOR(WHT)
     A            A1UTAV         4  0O  9 30DSPATR(UL)
     A                                      TEXT('Antall genererte poster')
     A                                      EDTCDE(Z)
     A N50                                  DSPATR(ND)
     A                                      COLOR(WHT)
     A                                 11  2'Trykk Enter for å avslutte'
     A                                      COLOR(WHT)
     A          R D1WIN
     A*%%TS  SD  19950824  151447  RH1         REL-V3R1M0  5763-PW1
     A                                      TEXT('Ny overføring av forslag - "A-
     A                                      LT"')
     A                                      CA03
     A                                      CF06
     A                                      PRINT
     A                                      WINDOW(&W_AROW &W_ACOL 10 37)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2' Informasjon '
     A                                      DSPATR(RI)
     A                                  4  2'Forslaget er allerede overført.'
     A                                      COLOR(WHT)
     A                                  6  2'Bekreft med kommando F6 dersom det'
     A                                      COLOR(WHT)
     A                                  7  2'skal overføres på nytt.'
     A                                      COLOR(WHT)
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  9 18'F6=Overføre'
     A                                      COLOR(BLU)
     A          R D2WIN
     A*%%TS  SD  20040219  134156  RHO         REL-V5R2M0  5722-WDS
     A                                      TEXT('Ny overføring av forslag - "D-
     A                                      ELVIS"')
     A                                      CA03
     A                                      CF06
     A                                      PRINT
     A                                      WINDOW(&W_AROW &W_ACOL 8 37)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2' Informasjon '
     A                                      DSPATR(RI)
     A                                  4  2'Alle feilreturer vil nå bli sendt'
     A                                      COLOR(WHT)
     A                                  5  2'på nytt til bank!                '
     A                                      COLOR(WHT)
     A            D2FRET         1A  B  6  2TEXT('Valg for om ALT eller DELER s-
     A                                      kal overføres bank.')
     A                                      DSPATR(ND)
     A                                      DSPATR(PR)
     A                                      DFTVAL('F')
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  7 18'F6=Overføre'
     A                                      COLOR(BLU)
     A          R E1WIN
     A*%%TS  SD  19950330  095221  RH1         REL-V2R3M0  5738-PW1
     A                                      TEXT('Statuspost - Ikke samlefile')
     A                                      PRINT
     A                                  2  2' Informasjon '
     A                                      DSPATR(RI)
     A                                  4  2'Kontroll av transaksjoner pågår...'
     A                                      COLOR(WHT)
     A                                  6  2'Innland:'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  6 19'Utland:'
     A                                      DSPATR(UL)
     A N50                                  DSPATR(ND)
     A                                      COLOR(WHT)
     A                                  7  2'Godkjente:'
     A                                      COLOR(WHT)
     A            A1INGO         4Y 0O  7 13DSPATR(UL)
     A                                      TEXT('Antall genererte poster')
     A                                      EDTCDE(Z)
     A                                      COLOR(WHT)
     A                                  7 19'Godkjente:'
     A N50                                  DSPATR(ND)
     A                                      COLOR(WHT)
     A            A1UTGO         4Y 0O  7 30DSPATR(UL)
     A                                      TEXT('Antall genererte poster')
     A                                      EDTCDE(Z)
     A N50                                  DSPATR(ND)
     A                                      COLOR(WHT)
     A                                  8  2'Avviste. :'
     A                                      COLOR(WHT)
     A            A1INAV         4Y 0O  8 13DSPATR(UL)
     A                                      TEXT('Antall genererte poster')
     A                                      EDTCDE(Z)
     A                                      COLOR(WHT)
     A                                  8 19'Avviste. :'
     A N50                                  DSPATR(ND)
     A                                      COLOR(WHT)
     A            A1UTAV         4Y 0O  8 30DSPATR(UL)
     A                                      TEXT('Antall genererte poster')
     A                                      EDTCDE(Z)
     A N50                                  DSPATR(ND)
     A                                      COLOR(WHT)
     A          R F1WIN
     A*%%TS  SD  19950329  095112  RH1         REL-V2R3M0  5738-PW1
     A                                      TEXT('Statuspost - Lønn M/Samlefile-
     A                                      ')
     A                                      PRINT
     A                                  2  2' Informasjon '
     A                                      DSPATR(RI)
     A                                  4  2'Kontroll og overføring av lønns-'
     A                                      COLOR(WHT)
     A                                  5  2'transaksjoner til samlefil pågår..-
     A                                      .'
     A                                      COLOR(WHT)
     A                                  7  2'Antall transaksjoner:'
     A                                      COLOR(WHT)
     A            F1LØNN         4  0O  7 24TEXT('Antall genererte poster')
     A                                      COLOR(WHT)
     A                                      EDTWRD('    ')
     A                                      DSPATR(UL)
     A          R F2WIN
     A*%%TS  SD  19950329  095112  RH1         REL-V2R3M0  5738-PW1
     A                                      TEXT('Statuspost - Lønn Ferdig Saml-
     A                                      efile')
     A                                      PRINT
     A                                      WINDOW(&W_AROW &W_ACOL 10 37)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  3' Informasjon '
     A                                      DSPATR(RI)
     A                                  4  3'Overføring av lønnstransaksjoner'
     A                                      COLOR(WHT)
     A                                  5  3'til samlefil er ferdig.'
     A                                      COLOR(WHT)
     A                                  7  3'Antall transaksjoner:'
     A                                      COLOR(WHT)
     A            F1LØNN         4  0O  7 25TEXT('Antall genererte poster')
     A                                      COLOR(WHT)
     A                                      EDTWRD('    ')
     A                                      DSPATR(UL)
     A                                  9  3'Trykk Enter for å avslutte.'
     A                                      COLOR(WHT)
     A          R F3WIN
     A*%%TS  SD  19950329  095112  RH1         REL-V2R3M0  5738-PW1
     A                                      TEXT('Statuspost - Lønn U/Samlefile-
     A                                      ')
     A                                      PRINT
     A                                      WINDOW(&W_AROW &W_ACOL 8 37)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2' Informasjon '
     A                                      DSPATR(RI)
     A                                  4  2'Kontroll av lønnstransaksjoner'
     A                                      COLOR(WHT)
     A                                  5  2'pågår...'
     A                                      COLOR(WHT)
     A                                  7  2'Antall transaksjoner:'
     A                                      COLOR(WHT)
     A            F1LØNN         4  0O  7 24TEXT('Antall genererte poster')
     A                                      COLOR(WHT)
     A                                      EDTWRD('    ')
     A                                      DSPATR(UL)
     A          R F4WIN
     A*%%TS  SD  19950329  095112  RH1         REL-V2R3M0  5738-PW1
     A                                      TEXT('Statuspost - Lønn Ferdig U/Sa-
     A                                      mlefile')
     A                                      PRINT
     A                                      WINDOW(&W_AROW &W_ACOL 8 37)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2' Informasjon '
     A                                      DSPATR(RI)
     A                                  4  2'Kontroll av lønnstransaksjoner er'
     A                                      COLOR(WHT)
     A                                  5  2'ferdig.'
     A                                      COLOR(WHT)
     A                                  7  2'Klargjør for overføring...'
     A                                      COLOR(WHT)
     A          R G1WIN
     A*%%TS  SD  19970623  120051  RHO         REL-V3R7M0  5716-PW1
     A                                      TEXT('Ny bankforbindelse')
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      PRINT
     A                                  2  2' Informasjon '
     A                                      DSPATR(RI)
     A                                  4  2'Det finnes utbetalinger i samlefil-
     A                                       '
     A                                  5  2'med en annen bankforbindelse.     -
     A                                       '
     A                                  7  2'Samlefil må først overføres banken-
     A                                       '
     A                                  8  2'før dette forslaget kan overføres -
     A                                       '
     A                                  9  2'til samlefil.                     -
     A                                       '
     A                                 12  2'Trykk Enter.'
     A                                 10  2'                                  -
     A                                       '
     A          R ASUME
     A*%%TS  SD  19941222  135431  RH          REL-V2R3M0  5738-PW1
     A                                      KEEP
     A                                      ASSUME
     A                                 24 79' '
     A          R D2WINGML
     A*%%TS  SD  20040219  134156  RHO         REL-V5R2M0  5722-WDS
     A                                      WINDOW(&W_AROW &W_ACOL 11 37)
     A                                      TEXT('Ny overføring av forslag - "D-
     A                                      ELVIS"')
     A                                      CA03
     A                                      CF06
     A                                      PRINT
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2' Informasjon '
     A                                      DSPATR(RI)
     A                                  4  2'Det er mottatt både feilretur og'
     A                                      COLOR(WHT)
     A                                  5  2'1.gangsretur for forslaget.'
     A                                      COLOR(WHT)
     A                                  7  2'Valg:'
     A            D2FRET         1A  B  7  8TEXT('Valg for om ALT eller DELER s-
     A                                      kal overføres bank.')
     A                                      DSPATR(UL)
     A                                      VALUES('F' 'A')
     A                                      COLOR(YLW)
     A                                      DSPATR(PC)
     A                                  7 11'F=Overføre kun feilretur'
     A                                      COLOR(GRN)
     A                                  8 11'A=Overføre hele forslaget'
     A                                      COLOR(GRN)
     A                                 10  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 10 18'F6=Overføre'
     A                                      COLOR(BLU)
7.02 A          R D1WINX
7.02 A*%%TS  SD  19950824  151447  RH1         REL-V3R1M0  5763-PW1
7.02 A                                      TEXT('Feilm. vedr. KID/lev.fak.nr  -
7.02 A                                         ')
7.02 A                                      CA03
7.02 A                                      PRINT
7.02 A                                      WINDOW(&W_AROW &W_ACOL 10 37)
7.02 A            W_AROW         2S 0P
7.02 A            W_ACOL         3S 0P
7.02 A                                  2  2' Informasjon '
7.02 A                                      DSPATR(RI)
7.02 A                                  4  2'Forslaget har poster hvor      '
7.02 A                                      COLOR(WHT)
7.02 A                                  6  2'det ikke er KID eller levandørens '
7.02 A                                      COLOR(WHT)
7.02 A                                  7  2'fakturanummer.'
7.02 A                                      COLOR(WHT)
7.02 A                                  9  2'F3=Gå tilbake'
7.02 A                                      COLOR(BLU)
