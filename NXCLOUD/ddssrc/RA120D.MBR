     A*%%TS  SD  20080520  165153  KOBASP      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RA120D
     A* Beskrivelse . . : Kontogrupper, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       250302 KOB  Nytt program
7.xx A* 7.xx  020316  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020327  164315  KOB         REL-V5R1M0  5722-WDS
7.xx A            B1VALG    R        B  9  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
7.xx A            B1TKOD    R        O  9  5REFFLD(RATKOD)
7.xx A            B1TTXT    R        O  9 11REFFLD(RATTXT)
7.xx A            B1TBFA    R        O  9 44REFFLD(RATBFA)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020327  164315  KOB         REL-V5R1M0  5722-WDS
7.xx A                                      SFLSIZ(0014)
7.xx A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kontogrupper'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Kode'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 11'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  5'Kode'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 11'Beskrivelse         '
     A                                      COLOR(WHT)
7.xx A                                  7 44'Budsj.'
     A                                      COLOR(WHT)
7.xx A            B2TKOD    R        B  5  5REFFLD(RATKOD)
     A  32                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2TTXT    R        B  5 11REFFLD(RATTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  8 44'faktor'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020325  225553  KOB         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kontogrupper'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Kontogruppe. . . . . . . . .'
     A            C1TKOD    R        B  5 31REFFLD(RATKOD)
     A  31                                  ERRMSGID(RM00010 *LIBL/QMFOKON 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020325  225553  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Kontogruppe finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20080520  165153  KOBASP      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kontogrupper'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Kontogruppe. . . . . . . . .'
     A            C1TKOD    R        O  5 31REFFLD(RATKOD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Beskrivelse. . . . . . . . .'
     A            C2TTXT    R        B  7 31REFFLD(RATTXT)
     A  31                                  ERRMSGID(RMA0040 *LIBL/QMFOKON 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                  9  2'Budsjettfaktor . . . . . . .'
     A            C2TBFA    R        B  9 31REFFLD(RATBFA)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                      EDTCDE(J)
     A                                      CHECK(RZ)
     A                                 11  2'Kontoplangruppe. . . . . . .'
     A            C2TPG1    R        B 11 31REFFLD(RATPG1)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            C2TPG2    R        B 11 35REFFLD(RATPG2)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            C2TPG3    R        B 11 39REFFLD(RATPG3)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            C2TPG4    R        B 11 43REFFLD(RATPG4)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2TPG5    R        B 11 47REFFLD(RATPG5)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2EUSR    R        O 20 31REFFLD(RAEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020325  225553  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette kontogruppe '
     A                                      DSPATR(RI)
     A                                  4  2'Kontogruppe. . . .'
     A            D1TKOD    R        O  4 21REFFLD(RATKOD)
     A                                  6  2'Beskrivelse. . . .'
     A            RATTXT    R        O  6 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020325  225553  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Kontogruppe. . . .'
     A            K1TKOD    R        B  4 21REFFLD(RATKOD)
     A  31                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RMA0030 *LIBL/QMFOKON 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Kontogruppe. . . .'
     A            RATKOD    R        O  8 21
     A                                  9  2'Beskrivelse. . . .'
     A            RATTXT    R        O  9 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
