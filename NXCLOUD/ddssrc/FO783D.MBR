     A*%%TS  SD  20130104  105839  BOF620      REL-V7R1M0  5770-WDS
     A*%%EC
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : FO783D
     A* Beskrivelse . . : Opdatering av Byggdok fra statistikk
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  010416  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  20130104  105839  BOF620      REL-V7R1M0  5770-WDS
     A*                                     WINDOW(*DFT 15 68)
     A                                      CA03
     A                                      CA12
     A                                      CF04
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Oppdatering av ByggDok fra Statis-
     A                                      tikk'
     A                                      DSPATR(RI)
     A                                  5  2'År  . . . . . . .'
     A            B1PAAR         4Y 0B  5 24COLOR(YLW)
     A  31                                  ERRMSGID(FMA0610 *LIBL/QMFOFAK 31)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
7.xx A                                  6  2'Kundenr . . . . +'
     A            B1VKUN         6Y 0B  6 24COLOR(YLW)
     A  33                                  ERRMSGID(FMH0050 *LIBL/QMFOFAK 33)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                  7  2'Kundeprosjekt . .'
     A            B1KPRO         6Y 0B  7 24COLOR(YLW)
     A  35                                  ERRMSGID(FML0010 *LIBL/QMFOFAK 35)
     A  36                                  ERRMSGID(FML0020 *LIBL/QMFOFAK 36)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
6.31 A                                  9  2'Byggdok . . . . .'
6.31 A            B1BDOK    R        B  9 24REFFLD(FÅJANE)
6.31 A                                      EDTCDE(4)
6.31 A                                      COLOR(YLW)
6.31 A                                  9 28'1=Ja,0=Nei'
6.31 A                                 10  2'Cobuilder . . . .'
6.31 A            B1COBU    R        B 10 24REFFLD(FÅJANE)
6.31 A                                      EDTCDE(4)
6.31 A                                      COLOR(YLW)
6.31 A                                 10 28'1=Ja,0=Nei'
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
