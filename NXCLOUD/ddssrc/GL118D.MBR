     A*%%TS  SD  20170817  114819  NHO630      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASTELE
     A* Format. . . . . : GL118D
     A* Beskrivelse . . : Oppdatering av returdata
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* K000  220620 bsa  Takler brede skjermbilder
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A          R B1SFL                     SFL
     A*%%TS  SD  20170817  114819  NHO630      REL-V7R1M0  5770-WDS
     A                                      TEXT('Subfile')
     A            B1BGIR    R        H      REFFLD(GBGIRO)
     A            B1PGIR    R        H      REFFLD(GBGIRO)
     A            B1TELL    R        H      REFFLD(GBTELL)
     A            B1VALG    R        B 10  2REFFLD(AAVALG)
     A                                      CHKMSGID(RMA0010 *LIBL/QMFOKON)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' '1' '4' '5')
     A            B1LØPN    R        O 10  5REFFLD(GBLØPN)
     A                                      TEXT('Løpenummer')
     A                                      EDTCDE(Z)
     A            B1SKOD    R        O 10 11REFFLD(GCSKOD)
     A                                      TEXT('Sonekode')
     A            B1FILG    R        O 10 16REFFLD(GCFILG)
     A                                      TEXT('Filgruppe')
     A            B1FIRM    R        O 10 21REFFLD(GCFIRM)
     A                                      TEXT('Firmanummer')
     A            B1TYPE    R        O 10 26REFFLD(GBTYPE)
     A                                      TEXT('Oppdragstype')
     A            B1DATO         8Y 0O 10 31EDTWRD('  .  .    ')
     A                                      TEXT('Dato')
     A            B1KLOK    R        O 10 43REFFLD(GBKLOK)
     A                                      EDTWRD('  :  :  ')
     A                                      TEXT('Klokkeslett')
     A            B1OPPD    R        O 10 53REFFLD(GBOPPD)
     A                                      TEXT('Oppdrag')
     A                                      EDTCDE(Z)
     A            B1KODE         7A  O 10 63TEXT('Kode for om retur er oppdater-
     A                                      t')
     A                                      COLOR(RED)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20010104  143840  ASP         REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      TEXT('Hovedbilde')
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA14
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A            SVRN01         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Oppdatere returdata'
     A                                      COLOR(WHT)
7.xx A                                  2 38'GL118B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4 37'Filgr:'
     A            A1FGRP    R        O  4 44REFFLD(GCFILG)
     A                                      TEXT('Filgruppe')
     A                                      DSPATR(UL)
     A                                  4 49'Firma:'
     A            A1FIRM    R        O  4 56REFFLD(GCFIRM)
     A                                      TEXT('Firmanr')
     A                                      DSPATR(UL)
     A            A1FNAV        20A  O  4 60DSPATR(UL)
     A                                  5  2'Velg og trykk Enter.'
     A                                      COLOR(BLU)
     A            A1VALG        44A  O  6  2COLOR(BLU)
     A            A1RETU        20A  O  6 60COLOR(RED)
7.xx A*                                 8  2'V Løpenr Sone Fgr  Firm Type Dato -
7.xx A*                                            Klokken   Oppdrag'
7.xx A*                                     COLOR(WHT)
7.xx A*                                     DSPATR(UL)
7.xx A            B2VALG    R        B  8  2REFFLD(AAVALG)
     A                                      CHKMSGID(RMA0010 *LIBL/QMFOKON)
     A                                      OVRDTA
     A  45                                  ERRMSGID(RMA0010 *LIBL/QMFOKON 45)
     A                                      VALUES(' ' '1' '4' '5')
     A                                      DSPATR(ND)
     A                                      DSPATR(PR)
7.xx A            B2LØPN    R        B  8  5REFFLD(GBLØPN)
     A                                      CHECK(FE)
     A                                      TEXT('Løpenummer')
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A  40                                  ERRMSGID(RML0480 *LIBL/QMFOKON)
     A  41                                  ERRMSGID(RML0570 *LIBL/QMFOKON 41)
     A  42                                  ERRMSGID(RM00030 *LIBL/QMFOKON 42)
     A                                      DSPATR(ND)
     A                                      DSPATR(PR)
7.xx A                                  9  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  5'Løpnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 11'Sone'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 16'Fgr'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 21'Firm'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 26'Type'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 31'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 43'Klokken'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 53'Oppdrag'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 63'Kode'
7.xx A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20010212  172904  RHO         REL-V4R4M0  5769-PW1
     A                                      TEXT('Kommando taster')
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F5=Forny'
     A                                      COLOR(BLU)
     A            B2CMD9        31   O 23 45COLOR(BLU)
     A                                      DSPATR(ND)
     A                                 23 29'F14=Historikk'
     A                                      COLOR(BLU)
     A          R C1SFL                     SFL
     A*%%TS  SD  19950216  103915  RH1         REL-V2R3M0  5738-PW1
     A                                      TEXT('Subfile - Vise batch')
     A            C2RECO        78A  O  7  2TEXT('RECORD')
     A          R C2CTL                     SFLCTL(C1SFL)
     A*%%TS  SD  19950216  103915  RH1         REL-V2R3M0  5738-PW1
     A                                      TEXT('Hovedbilde - Vise batch')
     A                                      ROLLUP(20)
     A                                      ROLLDOWN(21)
     A                                      CA03
     A                                      OVERLAY
     A                                      PUTOVR
     A  22                                  SFLDSP
     A  23                                  SFLDSPCTL
     A  24                                  SFLCLR
     A  25                                  SFLEND(*MORE)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A            SVRN02         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Oppdatere returdata'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Filgr:'
     A            A1FGRP    R        O  4  9REFFLD(GCFILG)
     A                                      TEXT('Filgruppe')
     A                                      DSPATR(UL)
     A                                  4 13'Firma:'
     A            A1FIRM    R        O  4 20REFFLD(GCFIRM)
     A                                      TEXT('Firmanr')
     A                                      DSPATR(UL)
     A            A1FNAV        20A  O  4 24DSPATR(UL)
     A                                  4 46'Dato...:'
     A            C2DATO         8Y 0O  4 55TEXT('Dato')
     A                                      DSPATR(UL)
     A                                      EDTWRD('  .  .    ')
     A                                  4 67'Løpenr:'
     A            C2LØPN         5Y 0O  4 75TEXT('Løpenummer')
     A                                      DSPATR(UL)
     A                                      EDTCDE(Z)
     A                                  5 46'Klokken:'
     A            C2KLOK         6Y 0O  5 55DSPATR(UL)
     A                                      EDTWRD('  :  :  ')
     A                                  6  2'R E C O R D                       -
     A                                                                         -
     A                                               '
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A          R C2CMD
     A*%%TS  SD  19950809  091058  RH1         REL-V3R1M0  5763-PW1
     A                                      TEXT('Kommando taster - Vise batch')
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WSF                     SFL
     A*%%TS  SD  19960201  123819  RH          REL-V3R1M0  5763-PW1
     A                                      TEXT('Subfile - Oppdatere poster')
     A            D1BGIR    R        H      REFFLD(GBGIRO)
     A            D1PGIR    R        H      REFFLD(GBGIRO)
     A            D1FILG    R        O  6  2REFFLD(GCFILG)
     A                                      TEXT('Filgruppe')
     A            D1FIRM    R        O  6  6REFFLD(GCFIRM)
     A                                      TEXT('Firmanummer')
     A            D1TYPE    R        O  6 10REFFLD(GBTYPE)
     A                                      TEXT('Oppdragstype')
     A            D1DATO         8Y 0O  6 14EDTWRD('  .  .    ')
     A                                      TEXT('Dato')
     A            D1KLOK    R        O  6 25REFFLD(GBKLOK)
     A                                      EDTWRD('  :  :  ')
     A                                      TEXT('Klokkeslett')
     A            D1OPPD    R        O  6 34REFFLD(GBOPPD)
     A                                      TEXT('Oppdrag')
     A                                      EDTCDE(Z)
     A            D1LØPN    R        O  6 42REFFLD(GBLØPN)
     A                                      TEXT('Løpenummer')
     A                                      EDTCDE(Z)
     A          R D2WCT                     SFLCTL(D1WSF)
     A*%%TS  SD  19960201  123819  RH          REL-V3R1M0  5763-PW1
     A                                      SFLSIZ(0007)
     A                                      SFLPAG(0006)
     A                                      WINDOW(D2CMD)
     A                                      TEXT('Hovedbilde - Oppdatere poster-
     A                                      ')
     A                                      ROLLUP(30)
     A                                      ROLLDOWN(31)
     A                                      CA03
     A                                      CF06
     A                                      OVERLAY
     A                                      PUTOVR
     A  32                                  SFLDSP
     A  33                                  SFLDSPCTL
     A  34                                  SFLCLR
     A  35                                  SFLEND(*MORE)
     A            SVRN03         4S 0H      SFLRCDNBR(CURSOR)
     A                                  2  2'Velg oppdateringstype:'
     A                                      COLOR(WHT)
     A            D2JOKO         1A  B  3  2TEXT('Journal eller Kontrollliste')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES('J' 'K')
     A                                      DSPATR(PC)
     A                                  3  4'(K=Kontroll-liste, J=Journal-liste-
     A                                       )'
7.xx A*                                 5  2'Fgr Fnr Typ Dato       Klokken  Op-
7.xx A*                                     pdrag Løpn.'
7.xx A*                                     DSPATR(UL)
7.xx A*                                     COLOR(WHT)
7.xx A                                  5  2'Fgr'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  6'Fnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 10'Typ'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 14'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 25'Klokken'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 34'Oppdrag'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 42'Løpn.'
7.xx A                                      COLOR(WHT)
     A          R D2CMD
     A*%%TS  SD  19950809  091058  RH1         REL-V3R1M0  5763-PW1
     A                                      WINDOW(&W_AROW &W_ACOL 15 47)
     A                                      TEXT('Kommandotaster - Oppdatere po-
     A                                      ster')
     A                                      CA03
     A                                      OVERLAY
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 14 18'F6=Oppdatere'
     A                                      COLOR(BLU)
     A          R A1WIN
     A*%%TS  SD  19950310  122319  RH1         REL-V2R3M0  5738-PW1
     A                                      TEXT('Telegiro')
     A                                      KEEP
     A                                  2  2' Informasjon '
     A                                      DSPATR(RI)
     A                                  4  2'Oppdatering av Telegiro-returer'
     A                                      COLOR(WHT)
     A                                  5  2'pågår...          '
     A                                      COLOR(WHT)
     A          R A2WIN
     A*%%TS  SD  19950310  122319  RH1         REL-V2R3M0  5738-PW1
     A                                      TEXT('OCR')
     A                                      KEEP
     A                                      WINDOW(&W_AROW &W_ACOL 6 37)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2' Informasjon '
     A                                      DSPATR(RI)
     A                                  4  2'Oppdatering av OCR-returer pågår..-
     A                                      .'
     A                                      COLOR(WHT)
     A          R C1WIN
     A*%%TS  SD  20000608  102821  RHO         REL-V4R4M0  5769-PW1
     A                                      TEXT('Sletting av Batch')
     A                                      KEEP
     A                                      WINDOW(&W_AROW &W_ACOL 8 29)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2' SLETTING AV BATCH '
     A                                      DSPATR(RI)
     A                                  4  2'Trykk Enter for å bekrefte'
     A                                      COLOR(WHT)
     A                                  5  2'sletting av batch...      '
     A                                      COLOR(WHT)
     A                                  7  2'F3= Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  19950809  091058  RH1         REL-V3R1M0  5763-PW1
     A                                      TEXT('Slette enkeltposter')
     A                                      CA03
     A                                      WINDOW(&W_AROW &W_ACOL 10 37)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2 11'SLETTING AV LINJE'
     A                                      COLOR(WHT)
     A                                  4  2'Løpenummer. . . .:'
     A            D1LØPN    R        O  4 21REFFLD(GBLØPN)
     A                                      DSPATR(UL)
     A                                      TEXT('Løpenummer')
     A                                  5  2'Filgruppe . . . .:'
     A            D1FILG    R        O  5 21REFFLD(GCFILG)
     A                                      DSPATR(UL)
     A                                      TEXT('Filgruppe')
     A                                  6  2'Firma . . . . . .:'
     A            D1FIRM    R        O  6 21REFFLD(GCFIRM)
     A                                      DSPATR(UL)
     A                                      TEXT('Firma')
     A                                  7  2'Type retur. . . .:'
     A            D1TYPE    R        O  7 21REFFLD(GBTYPE)
     A                                      DSPATR(UL)
     A                                      TEXT('Type retur')
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20070326  141039  RHO550      REL-V5R4M0  5722-WDS
     A                                      TEXT('Det finnes allerede poster ti-
     A                                      l oppdatering.')
     A                                      CA04
     A                                      KEEP
     A                                      WINDOW(&W_AROW &W_ACOL 11 47)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  4  2'Det finnes poster fra tidligere kj-
     A                                      øring, som '
     A                                      COLOR(WHT)
     A                                  6  2'kjøres Journalliste fra menyen, el-
     A                                      ler det kan'
     A                                      COLOR(WHT)
     A                                  2  2'VIKTIG!'
     A                                      DSPATR(UL)
     A                                      DSPATR(RI)
     A                                 10 19'F4= Slette poster'
     A                                      COLOR(BLU)
     A                                 10  2'Enter= Tilbake'
     A                                      COLOR(BLU)
     A                                  7  2'velges å slette dem umiddelbart vi-
     A                                      a valget   '
     A                                      COLOR(WHT)
     A                                  5  2'først må oppdateres i regnskap. De-
     A                                      t må enten '
     A                                  8  2'under.                            -
     A                                                 '
     A                                      COLOR(WHT)
     A          R M1WIN
     A*%%TS  SD  20110511  105548  ASK610      REL-V7R1M0  5770-WDS
     A                                      WINDOW(&W_AROW &W_ACOL 11 47)
     A                                      TEXT('Det finnes allerede poster ti-
     A                                      l oppdatering.')
     A                                      CA04
     A                                      KEEP
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  4  2'Rutinen er allerede i bruk.'
     A                                      COLOR(WHT)
     A                                  2  2'VIKTIG!'
     A                                      DSPATR(UL)
     A                                      DSPATR(RI)
     A                                 10  2'Enter= Tilbake'
     A                                      COLOR(BLU)
     A                                  7  2'Prøv igjen litt senere.'
     A                                      COLOR(WHT)
     A                                  5  2'Rutinen avsluttes.'
     A                                  8  2'Kontakt ASP dersom du ikke får kjø-
     A                                      rt.'
     A                                      COLOR(WHT)
     A          R ASUME                     ASSUME
     A                                 24 79' '
