     A*%%TS  SD  20120314  140940  ASP_SGR     REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LL101D
     A* Beskrivelse . . : Lager, vedlikehold - en vare
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
6.29 A* 6.20  101111 BOF  Viser sist endret dato og bruker
7.01 A* K000  250121 BSA  Styre ledetekst på best.pkt.
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R C2BLD
     A*%%TS  SD  20120314  140819  ASP_SGR     REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av lager'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Varenummer . . . . . .'
     A            C1VARE    R   -7   O  5 25REFFLD(VLVARE)
     A                                      OVRDTA
     A                                  6  2'Lager  . . . . . . . .'
     A            C1LAGE    R        O  6 25REFFLD(VLLAGE)
     A                                      OVRDTA
     A                                  7  2'Beskrivelse. . . . . .'
     A            C2TEK1    R        O  7 25REFFLD(VVTEK1)
     A                                      OVRDTA
     A                                  9  2'Lagerplass . . . . . . . . . . .'
     A            C2PLAS    R        B  9 36REFFLD(VLPLAS)
     A  30                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9 51'Alt1:'
     A            C2PLA1    R    8   B  9 57REFFLD(VLPLA1)
     A  30                                  DSPATR(PR)
     A  33                                  ERRMSGID(MMD0010 *LIBL/QMFLAGR 33)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9 66'Alt2:'
     A            C2PLA2    R    8   B  9 72REFFLD(VLPLA2)
     A  30                                  DSPATR(PR)
     A  34                                  ERRMSGID(MMD0010 *LIBL/QMFLAGR 34)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2ENHL    R        B 10 51REFFLD(VLENHL)
     A                                      DSPATR(PR)
     A                                      OVRDTA
     A                                 10  2'Minimumslager. . . . . . . . . .'
     A            C2MINI    R        B 10 36REFFLD(VLMINI)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      EDTWRD('      0 ,   -')
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 11  2'Maksimumslager . . . . . . . . .'
     A            C2MAKS    R        B 11 36REFFLD(VLMAKS)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      EDTWRD('      0 ,   -')
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2'Minimumsbestilling (anbrekk) . .'
     A            C2BMIN    R        B 12 36REFFLD(VLBMIN)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      EDTWRD('      0 ,   -')
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.01 A**                               13  2'Bestillingspunkt . . . . . . . .'
7.01 A**                               13  2'Hyllemin . . . . . . . . . . . .'
7.01 A            C2BPTX        32   B 13  2
     A            C2BPUN    R        B 13 36REFFLD(VLBPUN)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      EDTWRD('      0 ,   -')
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.01 A**                               14  2'Bestillingsmengde  . . . . . . .'
7.01 A**                               14  2'Hyllemax . . . . . . . . . . . .'
7.01 A            C2BMTX        32   B 14  2
     A            C2BMEN    R        B 14 36REFFLD(VLBMEN)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      EDTWRD('      0 ,   -')
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 15  2'Leveringstid . . . . . . . . . .'
     A            C2LTID    R        B 15 36REFFLD(VLLTID)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 16  2'Sperre Automatisk Oppdatering  .'
     A            C2STDA         6Y 0B 16 36
     A  30                                  DSPATR(PR)
     A  31                                  ERRMSGID(LMD0190 *LIBL/QMFLAGR 31)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 16 46'Til dato'
     A                                 17  2'Lager abc-kode . . . . . . . . .'
     A            C2ABCK         2A  B 17 36COLOR(YLW)
     A                                      VALUES('  ' 'AX' 'AY' 'AZ' 'BX' 'BY-
     A                                      ' 'BZ' 'CX' 'CY' 'CZ')
     A                                      OVRDTA
     A                                 18  2'Låst abc-kode  . . . . . . . . .'
     A            C2LABC    R        B 18 36REFFLD(VLLABC)
     A                                      COLOR(YLW)
     A                                      VALUES(1 0)
     A                                      OVRDTA
     A                                 18 39'1=Ja 0=Nei'
     A                                 21  2'Varetype . . . . . . . . . . . .'
     A            C2VTYP    R        B 21 36REFFLD(VLVTYP)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'L' 'S')
     A                                      CHKMSGID(VMA7020 *LIBL/QMFOFAK)
     A                                      OVRDTA
     A                                 19  2'Syklisk telling abc-kode . . . .'
     A            C2ABCT         2A  B 19 36COLOR(YLW)
     A                                      VALUES('  ' 'AX' 'AY' 'AZ' 'BX' 'BY-
     A                                      ' 'BZ' 'CX' 'CY' 'CZ')
     A                                      OVRDTA
     A                                 20  2'Prioritert Leverandør  . . . . .'
     A            C2SLDO    R        B 20 36REFFLD(FÅLDOR)
     A                                      COLOR(YLW)
     A  35                                  ERRMSGID(LMH0050 *LIBL/QMFLAGR 35)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
     A            C2LNAV        30A  O 20 43OVRDTA
     A                                 21 43'Hov.lev.'
     A            C2HLEV    R        O 21 52REFFLD(FÅLDOR)
     A                                      OVRDTA
     A            C2HLNA        20A  O 21 59OVRDTA
     A                                 22 56'Sist endret '
     A            C2EDAT         6Y 0O 22 69COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 56'Endret av   '
     A            C2EUSR    R        O 23 69REFFLD(VLEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 22  2'Utgått dato  . . . . . . . . . .'
     A            C2SUDA         6Y 0B 22 36COLOR(YLW)
     A  36                                  ERRMSGID(LMD0190 *LIBL/QMFLAGR 36)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 17'F4=Spørring'
     A                                      COLOR(BLU)
     A          R C3MSG
     A*%%TS  SD  20101111  092820  BOF610      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 9 43)
     A                                      CA03
     A                                      CA12
     A                                      CF13
     A                                      CA23
     A                                  2  2'Beholdningstall finnes på lageret.-
     A                                             '
     A                                  5  2'Endring av varetype fra L til'
     A            C3VTYP    R        O  5 32REFFLD(VVVTYP)
     A                                  5 34'må derfor'
     A                                  6  2'bekreftes med F23 !!'
     A                                  8  2'F3=Gå tilbake  F13=Vis lager  F23=-
     A                                      Bekreft'
     A                                      COLOR(BLU)
6.26 A          R C4MSG
6.26 A*%%TS  SD  20101111  092820  BOF610      REL-V5R4M0  5722-WDS
6.26 A                                      WINDOW(*DFT 8 43)
6.26 A                                      CA03
6.26 A                                      CA12
6.26 A                                      CA23
6.26 A                                  3  2'Endring av varetype fra S til L'
6.26 A                                  3 34'må '
6.26 A                                  4  2'bekreftes med F23 !!'
6.26 A                                  7  2'F3=Gå tilbake  F23=-
6.26 A                                      Bekreft'
6.26 A                                      COLOR(BLU)
