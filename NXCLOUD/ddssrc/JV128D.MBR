     A*%%TS  SD  20150629  134135  BOF630      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASVADM
     A* Format. . . . . : JV128D
     A* Beskrivelse . . : Overføring en vare fra VA til EVR
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       250616 bof  Nytt program
7.xx A*       030117 bsa  Endringer relatert til ny GUI
9.01 A*       150419 bsa  Merker om varen skal opprettes i tabell
9.01 A*                   for utstillingsvarer.
     A*
     A*
     A*%%EC
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
7.XX A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HELP
     A          R B1BLD
     A*%%TS  SD  20150629  134135  BOF630      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Overføring en vare til eget varere-
     A                                      gister'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                 11  2'Varenummer  . . . . . . . +'
     A            B1VARE    R   -7   B 11 30REFFLD(JVVARE)
     A                                      COLOR(YLW)
     A  39                                  ERRMSGID(JMM1010 *LIBL/QMFVADM 39)
     A                                      OVRDTA
     A            B1VTXT    R        O 11 39REFFLD(JLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
9.01 A                                 12  2'Utstillingsvare . . . . . .'
9.01 A            B1VUTS         1A  B 12 30
9.01 A  32                                  ERRMSGID(FM00010 *LIBL/QMFOFAK 32)
9.01 A                                      COLOR(YLW)
9.01 A                                      OVRDTA
9.01 A                                 12 33'1=ja/0=nei'
7.xx A                                  9  2'Prisgivende leverandør. . +'
     A            B1PGIV    R        B  9 30REFFLD(JVLDOR)
     A  36                                  ERRMSGID(JML0020 *LIBL/QMFVADM 36)
     A  37                                  ERRMSGID(JML0200 *LIBL/QMFVADM 37)
     A  22                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            B1PNAV    R        O  9 39REFFLD(JLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
9.01 A            B1TEXT        60A  O 15  2COLOR(WHT)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A                                  7  2'Utplukk:'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
