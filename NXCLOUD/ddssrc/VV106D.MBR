     A*%%TS  SD  20000105  095542  HKO         REL-V4R4M0  5769-PW1
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : VV106D
     A* Beskrivelse . . : Parameterbilde
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       010411 bof  Utvidet med prisgriver
7.xx A*       040216 bsa  Justeringer basert på ny GUI og regler
     A*
     A*----------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  20000105  095542  HKO         REL-V4R4M0  5769-PW1
     A                                      WINDOW(*DFT 17 52)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Endring av priser på basisenhet '
     A                                      DSPATR(RI)
7.xx A                                  4  2'Prisgiver . . . . . . .'
7.xx A            B1LDOR    R        B  4 26REFFLD(VPLDOR)
     A                                      COLOR(YLW)
     A  37                                  ERRMSGID(VMP0580 *LIBL/QMFOFAK 37)
     A                                      CHECK(RB)
7.xx A                                  5  2'Basisenhet. . . . . . .'
7.xx A            B1ENHE    R        O  5 26REFFLD(VPENHE)
     A                                      COLOR(YLW)
7.xx A                                  7  2'Salgspris . . . . . . .'
7.xx A            B1SAPR    R        B  7 26REFFLD(VPSAPR)
     A  31                                  ERRMSGID(VMP0550 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMP0560 *LIBL/QMFOFAK 32)
     A  33                                  ERRMSGID(VMP0570 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A                                  7 40':'
     A            B1SAGM    R        O  7 42REFFLD(VPSAPR)
7.xx A                                  8  2'Kostpris. . . . . . . .'
7.xx A            B1KOPR    R        B  8 26REFFLD(VPKOPR)
     A  34                                  ERRMSGID(VMP0570 *LIBL/QMFOFAK 34)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A                                  8 40':'
     A            B1KOGM    R        O  8 42REFFLD(VPKOPR)
7.xx A                                  9  2'Innkjøpspris. . . . . .'
7.xx A            B1INPR    R        B  9 26REFFLD(VPINPR)
     A  35                                  ERRMSGID(VMP0570 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A                                  9 40':'
     A            B1INGM    R        O  9 42REFFLD(VPINPR)
7.xx A                                 10  2'Butikkpris. . . . . . .'
7.xx A            B1BUPR    R        B 10 26REFFLD(VPBUPR)
     A  36                                  ERRMSGID(VMP0570 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A                                 10 40':'
     A            B1BUGM    R        O 10 42REFFLD(VPBUPR)
     A                                 13  2'Alle priser på andre enheter juste-
     A                                      res likt med'
     A                                 14  2'prisendringene på basisenheten.'
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
