     A*%%TS  SD  19990205  152721  HKO         REL-V4R2M0  5769-PW1
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : FW120D
     A* Beskrivelse . . : Oppretting av nye varer fra leverandør
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  290916  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HELP
     A          R B1BLD
     A*%%TS  SD  19990205  152721  HKO         REL-V4R2M0  5769-PW1
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Oppretting av nye varer fra levera-
     A                                      ndør'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5  2'Leverandør  . . . . . . . +'
     A            B1LDOR    R        B  5 30REFFLD(FWLDOR)
     A  31                                  ERRMSGID(FRL0010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FRL0020 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            B1NAVN        30A  O  5 38COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Lev. varenummer fra-til . .'
     A            B1LVAF    R        B  7 30REFFLD(FWLVAR)
     A  33                                  ERRMSGID(FM00200 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7 51'-'
     A            B1LVAT    R        B  7 54REFFLD(FWLVAR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Overstyringer:'
     A                                      DSPATR(UL)
     A                                 12  2'Prisdato. . . . . . . . . .'
     A            B1DATO         6Y 0B 12 30COLOR(YLW)
     A  34                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 34)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 14  2'Påslag på innkjøpspris. . .'
     A            B1IPRO         5Y 2B 14 30COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A                                 14 38'%'
     A                                 15  2'Påslag til kostpris . . . .'
     A            B1KPRO         5Y 2B 15 30COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A                                 15 38'%'
     A                                 16  2'Påslag på salgspris . . . .'
     A            B1SPRO         5Y 2B 16 30COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A                                 16 38'%'
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
