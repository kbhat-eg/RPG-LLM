     A*%%TS  SD  20101202  121541  SST61P      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RF020D
     A* Beskrivelse . . : Kladdeblokk, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       230702 KOB  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A          R B1SFL                     SFL
     A*%%TS  SD  20101202  121541  SST61P      REL-V5R4M0  5722-WDS
     A                                      TEXT('Subfile')
     A            B12POS         2A  H
     A            B1FIRM    R        H      REFFLD(RXFIRM)
     A            B1SYST    R     A  H      REFFLD(RXSYST)
     A            B1PROS    R        H      REFFLD(RXPROS)
     A            B1AKTI    R        H      REFFLD(RXAKTI)
     A            B1KONT    R        H      REFFLD(RXKONT)
     A            B1SPES    R        H      REFFLD(RXSPES)
     A            B1AVDE    R        H      REFFLD(RXAVDE)
     A            B1LINJ    R        H      REFFLD(RXLINJ)
     A            B1TGRP    R        H      REFFLD(RXTGRP)
     A            B1TIST    R        H      REFFLD(RXTIST)
     A            B1TEXT    R     A  B  7  2REFFLD(RXTEXT)
     A                                      TEXT('Tekst')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A  30                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20101202  121541  SST61P      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('Subfile heading')
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA02
     A                                      CA03
     A                                      CA05
     A                                      CA09
     A                                      CA10
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av notatark'
     A                                      COLOR(WHT)
     A            B2STEX        18A  O  4 39
     A  33                                  DSPATR(RI)
     A                                      OVRDTA
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Sett inn blanke linjer (I1,I2,I3)'
     A                                  4 60'Grupperingskode:'
     A            B2TGRP    R        O  4 77REFFLD(RXTGRP)
     A                                      DSPATR(UL)
     A            B2KL          14A  O  5  2
     A            B2KONT         6A  O  5 17
     A            B2NAVN    R        O  5 24REFFLD(RKNAVN)
     A            B2AVDT         5A  O  5 58
     A            B2AVDE    R        O  5 64REFFLD(RHAVDE)
     A                                      EDTCDE(Z)
     A            B2SPET         6A  O  5 69
     A            B2SPES    R        O  5 76REFFLD(RHSPES)
     A                                      EDTCDE(Z)
6.NU A**          B2BILT         6A  O  6 53
6.NU A            B2BILT         6A  O  6 51
6.NU A**          B2BILN    R        O  6 60REFFLD(RBBILN)
6.NU A            B2BILN    R        O  6 58REFFLD(RBBILN)
     A                                      EDTCDE(Z)
     A            B2PERT         5A  O  6 67
     A            B2RPER    R        O  6 73REFFLD(RBRPER)
     A                                      EDTCDE(Z)
     A            B2RAAR    R        O  6 76REFFLD(RBRAAR)
     A                                      EDTCDE(Z)
     A          R B2CMD
     A*%%TS  SD  19950830  093429  RH1         REL-V3R1M0  5763-PW1
     A                                      TEXT('Bilde for kommandotaster')
     A                                 23  1'F2=Grp.kode'
     A                                      COLOR(BLU)
     A                                 23 14'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 29'F5=Forny'
     A                                      COLOR(BLU)
     A                                 23 39'F9=Hent standard tekst'
     A                                      COLOR(BLU)
     A            B2CF10        18A  O 23 63COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020801  083516  KOB         REL-V5R1M0  5722-WDS
     A                                      TEXT('Grupperingskode')
     A                                      CA03
     A                                      CLRL(*ALL)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av notatark'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Grupperingskode. . . . . . .'
     A            C1TGRP    R        B  5 31REFFLD(RXTGRP)
     A                                      COLOR(YLW)
     A                                  5 37'(Blank=Viser alle linjer)'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*                                 1 46'AS Økonomi'
     A*                                     COLOR(WHT)
