     A*%%TS  SD  20101125  145627  ASP_X50     REL-V6R1M0  5761-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LP102D
     A* Beskrivelse . . : Vedlikehold av pakkseddelinfromasjon
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.10 A*       200819 BSA  Nytt program
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20010702  094226  ASK         REL-V4R4M0  5769-PW1
     A            B1VALG    R        B  8  2REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(2 3 4 5 0)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
     A            B1LDOR    R        O  8  4REFFLD(LPLDOR)
     A            B1LAGE    R        O  8 11REFFLD(LÅLAGE)
     A            B1LNAV    R        O  8 14REFFLD(LOLENA)
     A            B1PNIV         1  0O  8 45EDTCDE(Z)
     A            B1TFRK         3  0O  8 48EDTCDE(Z)
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020923  084253  HKO         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA01
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&WCURRN)
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A            WCURRN         5S 0H
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A            WVISRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av pakkseddelinformasj-
     A                                      on'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  4'+'
     A                                  4  6'Levnr.'
     A                                      COLOR(WHT)
     A                                  5 13'+'
     A                                  4 15'Lg'
     A                                      COLOR(WHT)
     A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
     A                                  7  2'V'
     A                                      COLOR(WHT)
     A                                  7  4'Levnr.'
     A                                      COLOR(WHT)
     A                                  7 11'Lg'
     A                                      COLOR(WHT)
     A                                  7 14'Navn      '
     A                                      COLOR(WHT)
     A                                  7 45'PT'
     A                                      COLOR(WHT)
     A                                  7 48'Frekvens'
     A                                      COLOR(WHT)
     A            B2LDOR    R        B  5  6REFFLD(LPLDOR)
     A  31                                  ERRMSGID(LMP0010 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                      DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            B2LAGE    R        B  5 15REFFLD(LÅLAGE)
     A  32                                  ERRMSGID(LMP0015 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                      DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
      *
     A          R B2CMD
     A                                 23  2'F3=Gå tilbake   F4=Spørring   F5=F-
     A                                      orny  F6=Opprett'
     A                                      COLOR(BLU)
     A*
     A          R C1BLD
     A*%%TS  SD  20020924  153446  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      CLRL(*ALL)
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av pakkseddelinforma-
     A                                       sjon'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Leverandør . . . . . . . . +'
     A            C1LDOR    R        B  5 31REFFLD(LPLDOR)
     A  31                                  ERRMSGID(LMP0010 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            C1LNAV        30A  O  5 39COLOR(GRN)
     A                                  6  2'Lager. . . (Blank gir alle)+'
     A            C1LAGE    R        B  6 31REFFLD(LÅLAGE)
     A  32                                  ERRMSGID(LMP0015 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            C1LAGN        20A  O  6 39COLOR(GRN)
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A*
     A          R C1MSG
     A*%%TS  SD  19971028  093526  ASK         REL-V3R7M0  5716-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  1  2'Feilmelding!'
     A                                  3  2'Kombinasjo finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20101125  145627  ASP_X50     REL-V6R1M0  5761-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av pakkseddelinforma-
     A                                       sjon'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A            C2NYEN        11A  O  4 69DSPATR(RI)
     A                                      OVRDTA
     A                                  5  2'Leverandør . . . .'
     A            C1LDOR    R        O  5 21REFFLD(LPLDOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LNAV        30A  O  5 29COLOR(GRN)
     A                                  6  2'Lager. . . . . . .'
     A            C1LAGE    R        O  6 21REFFLD(LÅLAGE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2LAGN        20A  O  6 29COLOR(GRN)
     A                                      OVRDTA
     A                                  8  2'Pakkseddelnivå . .'
     A            C2PNIV    R        B  8 21REFFLD(LÅVALG)
     A  31                                  ERRMSG('Gyldige valg er 1-3' 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8 24'(1,2,3)'
     A                                  9  2'Stikkprøvefrekvens'
     A            C2TFRK    R        B  9 21REFFLD(LÅUGRP)
     A  32                                  ERRMSG('Gyldige valg < 100' 32)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9 26'%'
     A                                 20  2'Sist endret dato .'
     A            C2EDAT         6Y 0O 20 21EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                      COLOR(GRN)
     A                                 21  2'Endret av. . . . .'
     A            C2EUSR    R        O 21 21REFFLD(LPEUSR)
     A                                      OVRDTA
     A                                      COLOR(GRN)
      *
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
      *
     A          R D1WIN
     A*%%TS  SD  19990527  123440  ASK         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 14 50)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette pakkseddelinformasjon'
     A                                      DSPATR(RI)
     A                                  4  2'Leverandør.'
     A            D1LDOR    R        O  4 14REFFLD(LPLDOR)
     A            D1LNAV        30A  O  4 21
     A                                  5  2'Lager . . .'
     A            D1LAGE    R        O  5 14REFFLD(LÅLAGE)
     A            D1LAGN        20A  O  5 21
      *
     A                                 13  2'F3=Gå tilbake '
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20000228  151422  ASK         REL-V4R4M0  5769-PW1
     A                                      WINDOW(*DFT 15 50)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Leverandør'
     A            K1LDOR    R        B  4 13REFFLD(LPLDOR)
     A  31                                  ERRMSGID(LMP0010 *LIBL/QMFLAGR 31)
     A  33                                  ERRMSGID(LMP0020 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            K1LNAV        30A  O  4 21
     A                                      OVRDTA
     A                                  5  2'Lager. . .'
     A            K1LAGE    R        B  5 13REFFLD(LÅLAGE)
     A  32                                  ERRMSGID(LMP0015 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            K1LAGN        20A  O  5 21
     A                                      OVRDTA
     A                                  8  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 10  2'Leverandør'
     A            K2LDOR    R        O 10 13REFFLD(LPLDOR)
     A                                      OVRDTA
     A            K2LNAV        30A  O 10 21
     A                                      OVRDTA
     A                                 11  2'Lager. . .'
     A            K2LAGE         2S 0O 11 13
     A                                      OVRDTA
     A            K2LAGN        20A  O 11 21
     A                                      OVRDTA
     A                                 14  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
