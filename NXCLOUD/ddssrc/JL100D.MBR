     A*%%TS  SD  20090910  144629  BOF610      REL-V5R4M0  5722-WDS
     A*%%EC
9.01 A* Spesialversjon. : Ekstra kode for å frigi enkeltvarer
9.01 A* Tilpasset for . : Mestergruppen
7.xx A* 040316 bsa Endringer relatert til ny GUI
     A*---------------------------------------------------------------
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
7.XX A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20050911  140618  HKO530      REL-V5R3M0  5722-WDS
     A            B1VALG    R        B  9  2REFFLD(JÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5 7 8)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A            B1NAVN    R        O  9  4REFFLD(JLNAVN)
     A            B1LDOR    R        O  9 40REFFLD(JLLDOR)
     A            B1KVAV    R        O  9 47REFFLD(JLKVAV)
     A                                      EDTCDE(Z)
     A            B1KMOD    R        O  9 49REFFLD(JLKMOD)
     A                                      EDTCDE(Z)
     A            B1KKAL    R        O  9 51REFFLD(JLKKAL)
     A                                      EDTCDE(Z)
     A            B1ENUM    R        O  9 53REFFLD(JLENUM)
     A            B1ENAV        19A  O  9 60
     A            B1RSTS         1A  O  9 80
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20090825  123732  BOF610      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA09
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av leverandører'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Frisøk. . .'
     A                                      COLOR(BLU)
7.xx A            B2STEK        35A  B  4 14COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A                                  5  4'Navn'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 40'Lev.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2'2=Endre  3=Kopier  4=Slett  5=Vis -
     A                                        7=Kontaktpersoner  8=GLN-Lokasjon'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  4'Navn                            '
7.xx A                                      COLOR(WHT)
7.xx A                                  8 40'Lev.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 47'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 49'M'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 51'N'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 53'Reskontro'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 80'R'
     A                                      COLOR(WHT)
7.xx A            B2NAVN    R        B  6  4REFFLD(JLNAVN)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
7.xx A            B2LDOR    R        B  6 40REFFLD(JLLDOR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20020819  131258  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett   F9=Gjenta søk'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  19990902  122644  HKO         REL-V4R2M0  5769-PW1
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av leverandører'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Leverandør . . . . . . . . .'
     A            C1LDOR    R        B  5 31REFFLD(JLLDOR)
     A  31                                  ERRMSGID(JML0010 *LIBL/QMFVADM 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  19990902  154107  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Leverandør finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20090910  144629  BOF610      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CF10
     A                                      CF11
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av leverandører'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Leverandør. . . . . .'
     A            C1LDOR    R        O  4 24REFFLD(JLLDOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Navn. . . . . . . . .'
     A            C2NAVN    R        B  5 24REFFLD(JLNAVN)
     A  31                                  ERRMSGID(JML0100 *LIBL/QMFVADM 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                  6  2'Adresse 1 . . . . . .'
     A            C2ADR1    R        B  6 24REFFLD(JLADR1)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  7  2'Adresse 2 . . . . . .'
     A            C2ADR2    R        B  7 24REFFLD(JLADR2)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8  2'Postadresse . . . . .'
     A            C2PONR    R        B  8 24REFFLD(JLPONR)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2STED    R        B  8 40REFFLD(JLSTED)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9  2'Telefon / Mobil / Fax'
     A            C2TLFN    R        B  9 24REFFLD(JLTLFN)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2MOBN    R        B  9 40REFFLD(JLFAXN)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2FAXN    R        B  9 56REFFLD(JLFAXN)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 10  2'E-mail adresse. . . .'
     A            C2MAIL    R        B 10 24REFFLD(JLMAIL)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 11  2'URL . . . . . . . . .'
     A            C2URLA    R        B 11 24REFFLD(JLURLA)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 12  2'Organisasjonsnr . . .'
     A            C2ORGN    R        B 12 24REFFLD(JLORGN)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 12 40'Valuta. . . . . . .'
     A            C2VALU    R        B 12 60REFFLD(JLVALU)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 13 40'Land. . . . . . . .'
     A            C2LAND    R        B 13 60REFFLD(JLLAND)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 13  2'Deltakertype. . . . .'
     A            C2DTYP    R        B 13 24REFFLD(JLDTYP)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 14  2'Grønt punkt . . . . .'
     A            C2GRPK    R        B 14 24REFFLD(JLGRPK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                 14 40'Miljømerke  . . . .'
     A            C2MMRK    R        B 14 60REFFLD(JLMMRK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 16  2'Prioritert leverandør'
     A            C2PRIO    R        B 16 24REFFLD(JLPRIO)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 16 27'1=Ja,0=Nei'
     A                                 17  2'Vareavtale. . . . . .'
     A            C2KVAV    R        B 17 24REFFLD(JLKVAV)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 17 27'1=Ja,0=Nei'
7.01 A  41                             17 40'Kalkuler fra Nobbpris'
7.01 A  41        C2KKAL    R        B 17 62REFFLD(JLKKAL)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.01 A  41                             17 65'1=Ja,0=Nei'
     A                                 19  2'Modulstikkord til EVR'
     A            C2KMOD    R        B 19 24REFFLD(JLKMOD)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 19 27'1=Ja,0=Nei'
7.01 A  41                             19 40'Frakt i kostpris. . .'
7.01 A  41        C2FKAL    R        B 19 62REFFLD(JLFKAL)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
7.01 A  41                             19 65'1=Ja,0=Nei'
7.xx A                                 20  2'Reskontronr . . . . +'
     A            C2ENUM    R        B 20 24REFFLD(JLENUM)
     A  32                                  ERRMSGID(JML0210 *LIBL/QMFVADM 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2ENAV        30A  O 20 31COLOR(YLW)
     A                                      OVRDTA
9.01 A                                 21  2'Åpen overf.enkeltvare'
9.01 A            C2XAUT    R        B 21 24REFFLD(JLFKAL)
9.01 A                                      COLOR(YLW)
9.01 A                                      VALUES(0 1)
9.01 A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
9.01 A  30                                  DSPATR(PR)
9.01 A                                      OVRDTA
9.01 A                                 21 27'1=Ja,0=Nei'
     A                                 23  2'F3=Gå tilbake   F4=Spørring '
7.xx A                                      COLOR(BLU)
     A                                 23 32'F10=Kontaktpersoner'
7.xx A                                      COLOR(BLU)
     A                                 23 53'F11=GLN-Lokasjon'
7.xx A                                      COLOR(BLU)
     A            C2ODAT         6Y 0O 22 48COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 57'/'
     A            C2EDAT         6Y 0O 22 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 68'/'
     A            C2EUSR    R        O 22 70REFFLD(JLEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R D1WIN
     A*%%TS  SD  19990104  161839  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 15 53)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette leverandør '
     A                                      DSPATR(RI)
     A                                  4  2'Leverandør  . .'
     A            D1LDOR    R        O  4 18REFFLD(JLLDOR)
     A                                  5  2'Navn  . . . . .'
     A            JLNAVN    R        O  5 18
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  19990902  154648  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 15 53)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Leverandør  . .'
     A            K1LDOR    R        B  4 18REFFLD(JLLDOR)
     A  31                                  ERRMSGID(JML0010 *LIBL/QMFVADM 31)
     A  32                                  ERRMSGID(JML0030 *LIBL/QMFVADM 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  7  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  9  2'Leverandør  . .'
     A            JLLDOR    R        O  9 18OVRDTA
     A                                 10  2'Navn  . . . . .'
     A            JLNAVN    R        O 10 18OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
