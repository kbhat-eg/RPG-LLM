     A*%%TS  SD  20051026  142747  HKO550      REL-V5R3M0  5722-WDS
     A*%%EC
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : EK110
     A* Beskrivelse . . : BONUS
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
6.21 A*       111113 BSA  Reaktivert bruk av leveringstype
6.22 A*       041213 BSA  Feil lengde på merknadsfelt. Skaper problemer
6.22 A*                   for jacada.
     A*
     A*
     A*
     A*--------------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20041125  104911  HKO         REL-V5R2M0  5722-WDS
6.21 A            B1VALG    R        B 17  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(2 3 4 5 0)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
6.21 A            B1EBGF    R        O 17  4REFFLD(EKEBGF)
6.21 A            B1EBGT    R        O 17 14REFFLD(EKEBGT)
6.21 A            B1EBPR    R        O 17 24REFFLD(EKEBPR)
6.21 A            B1EKTB    R        O 17 31REFFLD(EKEKTB)
6.22 A            B1EMRK    R   -4   O 17 33REFFLD(EKEMRK)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20051021  151643  HKO530      REL-V5R3M0  5722-WDS
6.21 A                                      SFLSIZ(0006)
6.21 A                                      SFLPAG(0005)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av bonuselement'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ----------'
     A                                      COLOR(WHT)
     A                                  4  2'Rabattkategori . . . . . .'
     A**                                    COLOR(BLU)
     A            B2RKAT    R        O  4 29REFFLD(EKERKA)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2RTXT        30A  O  4 39COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Kunde. . . . . . . . . . .'
     A**                                    COLOR(BLU)
     A            B2KUND    R        O  5 29REFFLD(EKEKUN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2KNAV        30A  O  5 39COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Kundeprosjekt. . . . . . .'
     A**                                    COLOR(BLU)
     A            B2KPRO    R        O  6 29REFFLD(EKEKPR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2PNAV    R        O  6 39REFFLD(FLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Varegruppe . . . . . . . .'
     A**                                    COLOR(BLU)
     A            B2OGRP    R        O  7 29REFFLD(EKEOGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2HGRP    R        O  7 32REFFLD(EKEHGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2UGRP    R        O  7 35REFFLD(EKEUGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2GTXT    R        O  7 39REFFLD(VGUTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Leverandør . . . . . . . .'
     A**                                    COLOR(BLU)
     A            B2LDOR    R        O  8 29REFFLD(EKELDO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2LNAV        30A  O  8 39COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Modulnr. . . . . . . . . .'
     A**                                    COLOR(BLU)
     A            B2MODN    R        O  9 29REFFLD(EKEMOD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2MTXT        35A  O  9 39COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Varenr . . . . . . . . . .'
     A**                                    COLOR(BLU)
     A            B2VARE    R   -7   O 10 29REFFLD(EKEVAR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B2VTXT    R        O 10 39REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
6.20 A**                               11  2'Enhet. . . . . . . . . . .'
     A**                                    COLOR(BLU)
6.20 A**          B2ENHE    R        O 11 29REFFLD(EKEENH)
6.20 A**                                    COLOR(YLW)
6.20 A**                                    OVRDTA
6.20 A**          B2ETXT    R        O 11 39REFFLD(VAETXT)
6.20 A**                                    COLOR(YLW)
6.20 A**                                    OVRDTA
6.21 A                                 11  2'Leveringstype. . . . . . .'
     A**                                    COLOR(BLU)
6.21 A            B2LETY    R        O 11 29REFFLD(EKELET)
6.21 A                                      COLOR(YLW)
6.21 A                                      OVRDTA
6.21 A            B2LTXT    R        O 11 39REFFLD(VYBESK)
6.21 A                                      COLOR(YLW)
6.21 A                                      OVRDTA
6.21 A                                 12  2'Bonustype. . . . . . . . .'
     A**                                    COLOR(BLU)
6.21 A            B2BOTY    R        O 12 29REFFLD(EKEBOT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
6.21 A            B2BTXT    R        O 12 39REFFLD(VATBES)
     A                                      COLOR(YLW)
     A                                      OVRDTA
6.21 A                                 13  2'Fra-dato . . . . . . . . .'
     A**                                    COLOR(BLU)
6.21 A            B2FDAT         6Y 0O 13 29COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
6.21 A                                 14  2'Til-dato . . . . . . . . .'
     A**                                    COLOR(BLU)
6.21 A            B2TDAT         6Y 0O 14 29COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
6.21 A                                 15  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
6.21 A                                 16  2'V Fra-beløp Til-beløp  Bonus K Mer-
     A                                      knad'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20010114  194850  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20051021  141728  HKO530      REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Opprett bonuselement'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Fra beløpsgrense . . . . . . .'
     A            C1EBGF    R        B  5 33REFFLD(EKEBGF)
     A  31                                  ERRMSGID(EMK0200 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2'Til beløpsgrense . . . . . . .'
     A            C1EBGT    R        B  6 33REFFLD(EKEBGT)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20041119  130358  GRO         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 7 35)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Bonuselement finnes fra før. '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20051026  142747  HKO550      REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av bonuselement'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Fra beløpsgrense . . . . . .'
     A            C1EBGF    R        O  5 31REFFLD(EKEBGF)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Til beløpsgrense . . . . . .'
     A            C1EBGT    R        O  6 31REFFLD(EKEBGT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Bonus-prosent. . . . . . . .'
     A            C2EBPR    R        B  8 31REFFLD(EKEBPR)
     A  31                                  ERRMSGID(EMK0210 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      DSPATR(PC)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 10  2'Merknad. . . . . . . . . . .'
     A            C2EMRK    R        B 10 31REFFLD(EKEMRK)
     A                                      CHECK(LC)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 13  2'Bonus fra første krone . . .'
     A            C2EKTB    R        B 13 31REFFLD(EKEKTB)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 13 33'1=Ja,0=Nei'
     A                                 13 46'Velges dersom bonus skal regnes fr-
     A                                      a'
     A                                 14 46'første krone når fra-beløpsgrense'
     A                                 15 46'er oppnådd'
     A                                 18  2'Beregnet bonus . . . . . . .'
     A            C2ESBO    R        O 18 31REFFLD(EKESBO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 19  2'Beregnet dato. . . . . . . .'
     A            C2EBDA         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Beregnet tidspunkt . . . . .'
     A            C2EBTI         6Y 0O 20 31COLOR(YLW)
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
     A            C2EODA         6Y 0O 22 48COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 57'/'
     A            C2EEDA         6Y 0O 22 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 68'/'
     A            C2EEUS    R        O 22 70REFFLD(EKEEUS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20051021  141728  HKO530      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 13 68)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette bonuselement '
     A                                      DSPATR(RI)
     A                                  4  2'Fra-beløp . . .'
     A            D1EBGF    R        O  4 18REFFLD(EKEBGF)
     A                                  5  2'Til-beløp . . .'
     A            D1EBGT    R        O  5 18REFFLD(EKEBGT)
     A                                  7  2'Bonus-prosent .'
     A            EKEBPR    R        O  7 18
     A                                  9  2'Merknad . . . .'
     A            EKEMRK    R        O  9 18
     A                                 12  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20041125  105509  HKO         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 14 53)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Fra-beløp . . .'
     A            K1EBGF    R        B  4 18REFFLD(EKEBGF)
     A  31                                  ERRMSGID(EMK0200 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(EMK0030 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  5  2'Til-beløp . . .'
     A            K1EBGT    R        B  5 18REFFLD(EKEBGT)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  7  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  9  2'Fra-beløp . . .'
     A            EKEBGF    R        O  9 18OVRDTA
     A                                 10  2'Til-beløp . . .'
     A            EKEBGT    R        O 10 18OVRDTA
     A                                 13  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
