     A*%%TS  SD  20220302  115837  SST_800     REL-V7R3M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RH660D
     A* Beskrivelse . . : MVA-oppgjør, utskrift - parameter
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       280302 KOB  Nytt program
6.10 A*       250811 NHO  Ny tekst vedr. call til ny utskrift
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A          R A2BLD
     A*%%TS  SD  20220302  115837  SST_800     REL-V7R3M0  5770-WDS
     A                                      KEEP
     A                                      CA03
     A                                      CA12
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utskrift av MVA-oppgjør'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  6  2'Tast inn valg, og trykk Enter.'
     A                                      COLOR(BLU)
     A                                  8 10'Dato:'
     A            A2PKDA    R        B  8 16REFFLD(RHPKDA)
     A  33                                  ERRMSGID(RMH0150 *LIBL/QMFOKON 33)
     A                                      COLOR(YLW)
     A                                  8 27'Regnskapsår . . .:'
     A            A2PRÅR    R        B  8 46REFFLD(RHPRÅR)
     A  34                                  ERRMSGID(RMH0160 *LIBL/QMFOKON 34)
     A                                      COLOR(YLW)
     A                                 10 27'Periode . . . . .:'
     A            A2PPER    R        B 10 46REFFLD(RHPPER)
     A  35                                  ERRMSGID(RMH0170 *LIBL/QMFOKON 35)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                 11 27'MVA-periode . . .:'
     A            A2PMVA    R        B 11 46REFFLD(RHPMVA)
     A                                      COLOR(YLW)
6.10 A*                                14 10'Utskrift uten spesifikasjon. . . .-
     A                                 13 10'Utskrift av enkeltposter . . . . .-
     A                                      :'
     A            A2PSPS    R        B 13 46REFFLD(RHPMV0)
     A                                      COLOR(YLW)
     A                                 13 50'(J=Ja, N=Nei)'
     A                                 15 10'Utskrift av poster med mva-kode=0.-
     A                                      :'
     A            A2PMV0    R        B 15 46REFFLD(RHPMV0)
     A                                      COLOR(YLW)
     A                                 15 50'(J=Ja, N=Nei)'
     A                                 17 10'Testkjøring  MVA-oppgave u/oppdat.-
     A                                      :'
     A            A2PTST    R        B 17 46REFFLD(RHPOMK)
     A                                      COLOR(YLW)
     A                                 17 50'(J=Ja, N=Nei)'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A  46                             19 10'Beskrivelse til Saft-Mva-Oppgave. -
     A                                      :'
     A  46        A2BESK        30A  B 19 46COLOR(YLW)
     A  36                                  DSPATR(PC)
     A          R M1WIN
     A                                      WINDOW(&W_AROW &W_ACOL 10 54)
     A                                      CA03
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A                                  2  2' Melding : '
     A                                      DSPATR(RI)
     A            M1MLD1    R        O  4  2REFFLD(RWMELD)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            M1MLD2    R        O  5  2REFFLD(RWMELD)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7  2'Svar ja for OK, nei for ikke OK. .-
     A                                       . .'
     A            M1SVAR    R        B  7 43REFFLD(RWSVAR)
     A                                      VALUES('J' 'N')
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A                                      COLOR(YLW)
     A                                  7 47'J/N'
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
