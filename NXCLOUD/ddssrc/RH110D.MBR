     A*%%TS  SD  20070612  095937  KOB550      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RH110D
     A* Beskrivelse . . : Hovedboksposter, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       280302 KOB  Nytt program
6.20 A* R6.20 021210 NHO  RBPROS er byttet ut med RHBPRO
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20011220  212149  KOB         REL-V5R1M0  5722-WDS
     A            B1RAAR    R        H      REFFLD(RBRAAR)
     A            B1BDAT    R        H      REFFLD(RBBDAT)
     A            B1TIST    R        H      REFFLD(RBTIST)
     A            B1AUTK    R        H      REFFLD(RBAUTK)
     A            B1VALG    R        B 10  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 5 8 9)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1KONT    R        O 10  5REFFLD(RBKONT)
     A            B1SPES    R        O 10 11REFFLD(RBSPES)
     A            B1AVDE    R        O 10 16REFFLD(RBAVDE)
     A            B1BILN    R        O 10 21REFFLD(RBBILN)
     A  73                                  COLOR(YLW)
6.NU A**          B1BIDA         6S 0O 10 28
6.NU A            B1BIDA         6S 0O 10 30
6.NU A**          B1RPER    R        O 10 35REFFLD(RBRPER)
6.NU A            B1RPER    R        O 10 37REFFLD(RBRPER)
6.NU A**          B1BILK    R        O 10 39REFFLD(RBBILK)
6.NU A            B1BILK    R        O 10 41REFFLD(RBBILK)
6.NU A**          B1TEXT    R        O 10 42REFFLD(RBTEXT)
6.NU A            B1TEXT        18   O 10 44
     A            B1MVAK    R        O 10 63REFFLD(RBMVAK)
     A            B1NETO    R        O 10 65REFFLD(RBNETO)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20070612  095937  KOB550      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av hovedboksposter'
     A                                      COLOR(WHT)
7.xx A                                  2 38'RH110B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
7.xx A                                  4  5'Konto'
7.xx A                                      COLOR(WHT)
7.xx A            B2KONT    R        B  4 12REFFLD(RBKONT)
7.xx A                                      COLOR(YLW)
7.xx A  22                                  DSPATR(PC)
7.xx A                                      CHECK(FE)
7.xx A                                      CHECK(RB)
7.xx A                                      OVRDTA
7.xx A                                  4 21'Avd  '
7.xx A                                      COLOR(WHT)
7.xx A            B2AVDE    R        B  4 28REFFLD(RBAVDE)
7.xx A                                      COLOR(YLW)
7.xx A                                      CHECK(FE)
7.xx A                                      CHECK(RB)
7.xx A                                      OVRDTA
7.xx A                                  5  5'Bærer'
7.xx A                                      COLOR(WHT)
7.xx A            B2SPES    R        B  5 12REFFLD(RBSPES)
7.xx A                                      COLOR(YLW)
7.xx A                                      CHECK(FE)
7.xx A                                      CHECK(RB)
7.xx A                                      OVRDTA
7.xx A                                  5 21'Bil.nr'
7.xx A                                      COLOR(WHT)
7.xx A            B2BILN    R        B  5 28REFFLD(RBBILN)
7.xx A                                      COLOR(YLW)
7.xx A                                      CHECK(FE)
7.xx A                                      CHECK(RB)
7.xx A                                      OVRDTA
     A                                  5 62'Periode:'
     A            B2RPER    R        B  5 71REFFLD(RBRPER)
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      RANGE(0 13)
     A                                      CHKMSGID(RMI0080 *LIBL/QMFOKON)
     A                                      OVRDTA
     A            B2RAAR    R        B  5 74REFFLD(RBRAAR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'2=Endre  5=Vis  8=Sumpost  9=Vis a-
     A                                      rkiv'
     A                                      COLOR(BLU)
     A                                  6 43'Inngående balanse:'
     A  31                                  DSPATR(ND)
     A                                      OVRATR
     A            B2SUMI    R        O  6 62REFFLD(RWSUMR)
     A  31                                  DSPATR(ND)
     A                                      OVRDTA
     A                                  7 43'Utgående  balanse:'
     A  31                                  DSPATR(ND)
     A                                      OVRATR
     A            B2SUMU    R        O  7 62REFFLD(RWSUMR)
     A  31                                  DSPATR(ND)
     A                                      OVRDTA
7.xx A*                                 9  2'V  Konto Bærer Avd Bil.nr   Dato  -
7.xx A*                                      Pr. Bk.Tekst             Mva      -
7.xx A*                                     Beløp'
7.xx A*                                     COLOR(WHT)
7.xx A                                  9  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  5'Konto'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 11'Bær.'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 16'Avd'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 21'Bil.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 30'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 37'Pr'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 41'BK'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 44'Bk.Tekst'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 63'M'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 65'Beløp'
7.xx A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  19950509  140931  KB          REL-V3R1M0  5763-PW1
     A                                 23  3'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 20'F5=Forny'
     A                                      COLOR(BLU)
     A*
     A*  Bilde C2 - Vindu for endring
     A*
     A          R C2WIN
     A*%%TS  SD  20070612  095034  KOB550      REL-V5R4M0  5722-WDS
     A                                      WINDOW(&W_AROW &W_ACOL 19 61)
     A                                      CA02
     A                                      CA03
     A                                      CA04
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  1 18'Vedlikehold av posteringer'
     A                                      COLOR(WHT)
     A            C2NYEN        11A  O  2  2DSPATR(RI)
     A                                  3  2'Konto . . . . .:'
     A            RBKONT    R        O  3 19REFFLD(RBKONT)
     A                                      DSPATR(UL)
     A            RHTEXT    R        O  3 26REFFLD(RHTEXT)
     A                                      DSPATR(UL)
     A                                  4  2'Kostnadsbærer .:'
     A            RBSPES    R        O  4 19REFFLD(RBSPES)
     A                                      DSPATR(UL)
     A            REBTXT    R        O  4 26DSPATR(UL)
     A                                  5  2'Avdeling. . . .:'
     A            RBAVDE    R        O  5 19REFFLD(RBAVDE)
     A                                      DSPATR(UL)
     A            RAGTXT    R        O  5 26DSPATR(UL)
     A                                  6  2'Bilagsnr. . . .:'
     A            RBBILN    R        O  6 19REFFLD(RBBILN)
     A                                      DSPATR(UL)
     A  73                                  COLOR(YLW)
     A                                      OVRDTA
6.NU A**73        C2NOTB         8A  O  6 26DSPATR(RI)
6.NU A  73        C2NOTB         8A  O  6 28DSPATR(RI)
     A                                      OVRDTA
     A                                  6 38'Periode. .:'
     A            RBRPER    R        O  6 50REFFLD(RBRPER)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            RBRAAR    R        O  6 53REFFLD(RBRAAR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  2'Bilagdato . . .:'
     A            C2BDAT         6Y 0O  7 19DSPATR(UL)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                  7 38'Journal. .:'
     A            RBJOUR    R        O  7 50DSPATR(UL)
     A                                      OVRDTA
     A                                  8  2'Bilagstekst . .:'
     A            C2BILK    R        B  8 19REFFLD(RBBILK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2TEXT    R        B  8 22REFFLD(RBTEXT)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9  2'Beløp . . . . .:'
     A            RBNETO    R        O  9 19DSPATR(UL)
     A                                      EDTCDE(L)
     A                                      OVRDTA
     A                                  9 38'MVA-kode. . . .:'
     A            RBMVAK    R        O  9 55DSPATR(UL)
     A                                      OVRDTA
     A                                 10  2'Grunnlag mva. .:'
     A            RBMVAG    R        O 10 19DSPATR(UL)
     A                                      EDTCDE(L)
     A                                 10 38'%-andel gr.lag.:'
     A            RBAMVA    R        O 10 55DSPATR(UL)
     A                                 11  2'Beregnet mva. .:'
     A            RBMVAB    R        O 11 19DSPATR(UL)
     A                                      EDTCDE(L)
     A                                      OVRDTA
     A                                 11 38'MVA-% . . . . .:'
     A            RBPMVA    R        O 11 55DSPATR(UL)
     A                                 12  2'Valuta. . . . .:'
     A            RBVALK    R        O 12 19DSPATR(UL)
     A                                      OVRDTA
     A            RBVALB    R        O 12 23DSPATR(UL)
     A                                      EDTCDE(L)
     A                                      OVRDTA
     A                                 12 38'MVA-overstyrt .:'
     A            RBMVAO    R        O 12 55DSPATR(UL)
     A                                      OVRDTA
     A                                 13  2'Mengde. . . . .:'
     A            RBMNGK    R        O 13 19DSPATR(UL)
     A                                      OVRDTA
     A            RBMNGD    R        O 13 23DSPATR(UL)
     A                                      EDTCDE(L)
     A                                      OVRDTA
     A                                 13 38'MVA-oppgjør . .:'
     A            RBMMRK    R        O 13 55DSPATR(UL)
     A                                      OVRDTA
     A                                 14  2'Prosjekt/Akt. .:'
6.20 A**          RHPROS    R        O 14 19DSPATR(UL)
6.20 A            RHBPRO         6   O 14 19DSPATR(UL)
     A                                      OVRDTA
     A            RBAKTI    R        O 14 26DSPATR(UL)
     A                                      OVRDTA
     A                                 14 38'MVA periode . .:'
     A            RBMVAP    R        O 14 55DSPATR(UL)
     A                                      OVRDTA
     A                                 15  2'Lev.fakturanr .:'
     A            RBLFAK    R        O 15 19DSPATR(UL)
     A                                 18  2'F2=Notatark bilag'
     A                                      COLOR(BLU)
     A                                 18 21'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 18 36'F4=Spørring'
     A                                      COLOR(BLU)
