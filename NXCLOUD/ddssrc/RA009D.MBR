     A*%%TS  SD  20050811  154812  AMD540      REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RA001D
     A* Beskrivelse . . : Statuskoder, vedlikehold av regnskapskoder
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  100516  bsa  Justeringer basert på ny GUI og regler
8.01 A* 800   220222  ask  Parameter for factoring via izzy
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R A2BLD
     A*%%TS  SD  20050811  154812  AMD540      REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA04
8.01 A                                      CA06
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av koder'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5 34'Factoringinformasjon'
7.xx A                                  7  3'Kundenummer factoring+'
     A            A29KNR    R        B  7 26REFFLD(RA9KNR)
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A            A2NAVN    R        O  7 33REFFLD(RKNAVN)
     A                                      OVRDTA
     A                                  8  3'Format for overføring:'
     A            A29FMT    R        B  8 26REFFLD(RA9FMT)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  3'Klientnr factoring. .:'
     A            A29KLI    R        B  9 26REFFLD(RA9KLI)
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A                                 11  3'Ta med kunder . . . .:'
     A            A29KUN    R        B 11 26REFFLD(RA9KUN)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(1)
     A                                 11 30'0 = Kunder skal ikke overføres'
     A                                 12 30'1 = Bare kunder med poster overfør-
     A                                      es'
     A                                 13 30'2 = Alle kunder overføres'
     A                                 15  3'Oppdater regnskap . .:'
     A            A29REG    R        B 15 26REFFLD(RA9REG)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 15 30'J = Ja'
     A                                 17  3'Bilagskode i regnskap:'
     A            A29BIL    R        B 17 26REFFLD(RA9BIL)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(1)
     A                                 19  3'Sist endret dato. . .:'
     A            A29DAT         6Y 0O 19 26COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  3'Endret av . . . . . .:'
     A            A29USR    R        O 20 26REFFLD(RA9USR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  3'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 19'F4=Forespørsel'
     A                                      COLOR(BLU)
8.01 A  31                             23 35'F6=Factoring via iizy'
8.01 A  31                                  COLOR(BLU)
