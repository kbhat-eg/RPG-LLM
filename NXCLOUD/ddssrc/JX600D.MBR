     A*%%TS  SD  19990823  124053  HKO         REL-V4R2M0  5769-PW1
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASVADM
     A* Format. . . . . : JX600D
     A* Beskrivelse . . : Utskrift av priser for prisgivende leverandør.
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 030616 bsa Endringer relatert til ny GUI
     A*
     A*---------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  19990823  124053  HKO         REL-V4R2M0  5769-PW1
     A*                                     WINDOW(*DFT 21 66)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Utskrift av priser for prisgivend-
     A                                      e leverandør '
     A                                      DSPATR(RI)
     A                                  4  2'Rapporttype. . . . . .'
7.xx A            B1RTYP    R        B  4 26REFFLD(JÅVALG)
     A  31                                  ERRMSGID(JM00010 *LIBL/QMFVADM 31)
     A                                      COLOR(YLW)
7.xx A                                  4 40'1 = Kun varer i EVR'
7.xx A                                  5 40'2 = Ikke varer i EVR'
7.xx A                                  6 40'3 = Alle varer'
7.xx A                                  8  2'Prisgivende leverandør+'
7.xx A            B1PLDO    R        B  8 26REFFLD(JXLDOR)
     A  32                                  ERRMSGID(JML0010 *LIBL/QMFVADM 32)
     A  33                                  ERRMSGID(JML0020 *LIBL/QMFVADM 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A            B1NAVN        26A  O  8 40COLOR(YLW)
     A                                 10  2'Prisdato . . . . . . .'
7.xx A            B1DATO         6Y 0B 10 26COLOR(YLW)
     A  34                                  ERRMSGID(JM00250 *LIBL/QMFVADM 34)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                 11  2'Operand. . . . . . . .'
7.xx A            B1OPER         2A  B 11 26COLOR(YLW)
     A  35                                  ERRMSGID(JM00010 *LIBL/QMFVADM 35)
7.xx A                                 11 40'EQ = lik prisdato'
7.xx A                                 12 40'GT = større enn prisdato'
7.xx A                                 13 40'LT = mindre enn prisdato'
7.xx A                                 15  2'Leverandør (vareeier)+'
7.xx A            B1LDOR    R        B 15 26REFFLD(JVLDOR)
     A  36                                  ERRMSGID(JML0020 *LIBL/QMFVADM 36)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A            B1LNAV        26A  O 15 40COLOR(YLW)
     A                                 17  2'Varegruppe . . . . . +'
7.xx A            B1OGRP    R        B 17 26REFFLD(JVOGRP)
     A  37                                  ERRMSGID(JVG1020 *LIBL/QMFVADM 37)
     A                                      COLOR(YLW)
7.xx A                                 17 29'+'
7.xx A            B1HGRP    R        B 17 31REFFLD(JVHGRP)
     A  38                                  ERRMSGID(JVG2020 *LIBL/QMFVADM 38)
     A                                      COLOR(YLW)
7.xx A                                 17 34'+'
7.xx A            B1UGRP    R        B 17 36REFFLD(JVUGRP)
     A  39                                  ERRMSGID(JVG0020 *LIBL/QMFVADM 39)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A            B1UTXT        26A  O 17 40COLOR(YLW)
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
