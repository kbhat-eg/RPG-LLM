     A*%%TS  SD  20120302  115833  BOF620      REL-V7R1M0  5770-WDS
     A*-------------------------------------------------------------------
7.xx A*     BSA  220116    Endringer relatert til ny GUI
7.05 A*     ask  250320    Lagt på fargekode styrt av kode "FARGE_SKAFF_SORTIMEN
7.06 A*     nho  041016  Sjekk sum mot memosaldo
7.07 A*     SST  140918  Kun ordretype 'T' på tilbudskunde
     A*
     A*
     A*
     A*
     A*-------------------------------------------------------------------
     A*%%EC
7.00 A                                      DSPSIZ(24 80 *DS3 -
7.00 A                                             27 132 *WIDE)
7.00 A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R A1BLD
     A*%%TS  SD  20060205  172915  HKO550      REL-V5R3M0  5722-WDS
7.xx A                                      WINDOW(02 02 15 60)
     A                                      CF03
     A                                      CF04
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
7.xx A                                  1 50'FO736A1'
     A                                  2  2'Ordre-type. . .'
     A                                      COLOR(WHT)
     A            A1FOTY    R        O  2 22REFFLD(FOOTYP)
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A            A1FTXT    R        O  2 29REFFLD(FONAVN)
     A                                      OVRDTA
     A                                      COLOR(WHT)
     A                                  3  2'Ordrenummer . .'
     A                                      COLOR(WHT)
     A            A1NUMM    R        O  3 18REFFLD(FONUMM)
     A                                      OVRDTA
     A                                      COLOR(WHT)
6.NU A            A1SUFF    R        O  3 27REFFLD(FOSUFF)
     A                                      OVRDTA
     A                                      COLOR(WHT)
6.NU A            A1KUNA    R        O  3 31REFFLD(FOKUNA)
     A                                      OVRDTA
     A                                      COLOR(WHT)
7.xx A                                  5  2'Til ordre-type . . . +'
     A            A1OTYP    R        B  5 25REFFLD(FOOTYP)
     A                                      OVRDTA
     A                                      DSPATR(PC)
     A  32                                  ERRMSGID(VMA5020 *LIBL/QMFOFAK 32)
     A  33                                  ERRMSGID(VMA5050 *LIBL/QMFOFAK 33)
     A  36                                  ERRMSGID(FRK0320 *LIBL/QMFOFAK 36)
     A  37                                  ERRMSGID(FRK0330 *LIBL/QMFOFAK 37)
7.07 A  68                                  ERRMSGID(FMO0662 *LIBL/QMFOFAK 68)
     A                                      COLOR(YLW)
     A                                  6  2'Varelinjer . . . . . .'
     A            A1VLIN    R        B  6 25REFFLD(FÅVALA)
     A                                      OVRDTA
     A                                      VALUES(' ' '0' '1' '2')
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A                                  6 29'0=Ingen spesifisering av linjer'
     A                                  7 29'1=Plukk bare linjer som oppgis'
     A                                  8 29'2=Plukk alle linjer med unntak'
7.xx A                                 10  2'Fra Lager. . . . . . +'
     A N49                                  DSPATR(ND)
     A            A1LAGE    R        B 10 25REFFLD(FOLAGE)
     A  34                                  ERRMSGID(FMA0160 *LIBL/QMFOFAK 34)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A N49                                  DSPATR(ND)
     A N49                                  DSPATR(PR)
     A            A1LATX        20A  O 10 29OVRDTA
     A                                      OVRATR
     A N49                                  DSPATR(ND)
     A                                 11  2'Tilleggsopplysninger .'
     A            A1TILL    R        B 11 25REFFLD(FÅVALA)
     A                                      OVRDTA
     A                                      VALUES(' ' '0' '1')
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A                                 11 29'1=Ja,0=Nei'
     A                                 12  2'Ikke faste tekstlinjer'
     A            A1PTXT    R        B 12 25REFFLD(FÅVALA)
     A                                      OVRDTA
     A                                      VALUES(' ' '0' '1')
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A                                 12 29'1=Ja,0=Nei'
     A                                 13  2'Klar til plukking  . .'
     A            A1PLUK    R        B 13 25REFFLD(FÅVALA)
     A                                      OVRDTA
     A                                      VALUES(' ' '0' '1')
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      COLOR(YLW)
     A                                 13 29'1=Ja,0=Nei'
     A                                 14  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R B1SFL                     SFL
     A*%%TS  SD  19941103  105420  ASP         REL-V2R3M0  5738-PW1
     A            B1VALG         2A  B  9  2COLOR(YLW)
     A                                      VALUES('2 ' '4 ' '7 ' '  ')
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1NUMM    R        O  9  5REFFLD(FKPONR)
6.NU A            B1SUFF    R        O  9 14REFFLD(FKPSUF)
6.NU A            B1OTYP    R        O  9 18REFFLD(FKPOTY)
6.NU A            B1WSID    R        O  9 22REFFLD(FKPWSD)
6.NU A            B1USER    R        O  9 34REFFLD(FKPUSR)
6.NU A            B1PALT    R        O  9 46REFFLD(FKPALT)
6.NU A            B1PTXT    R        O  9 56REFFLD(FKPTXT)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020312  211814  HKO         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF01
     A                                      CF03
     A                                      CF05
     A                                      CF15
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vise utplukkede ordrenummer'
     A                                      COLOR(WHT)
7.xx A                                  2 38'FO736B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      COLOR(WHT)
7.xx A                                  5  5'Ordre'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2'2=Endre detaljlinjer'
     A                                      COLOR(BLU)
7.xx A                                  7 24'4=Slett utplukk'
     A                                      COLOR(BLU)
7.xx A                                  7 41'7=Endre tilleggsopplysninger'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  5'Ordre'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 14'Su'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 18'OT'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 22'Skjerm-id'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 34'Bruker-id'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 46'Alt.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 56'Tekstlinjer'
     A                                      COLOR(WHT)
7.xx A            B2NUMM    R        B  6  5REFFLD(FKPONR)
     A                                      DLTCHK
     A                                      OVRDTA
     A  31                                  ERRMSGID(FMO0020 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FMO0030 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A          R B2CMD
     A*%%TS  SD  20020312  132434  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F1=Bla-alt'
     A                                      COLOR(BLU)
     A                                 23 15'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 31'F5=Forny'
     A                                      COLOR(BLU)
     A                                 23 42'F15=Start utskrift'
     A                                      COLOR(BLU)
     A          R B4BLD
     A*%%TS  SD  20020312  212130  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 10 32)
     A                                      CF03
     A                                      PUTOVR
     A                                  2  2' Slette utplukk for ordre '
     A                                      DSPATR(RI)
     A                                  4  2'Ordrenummer. . . .'
     A            B4NUMM    R        O  4 21REFFLD(FONUMM)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B7BLD
     A*%%TS  SD  20051027  101654  AMD550      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 16 62)
     A                                      CF03
     A                                      PUTOVR
     A                                  2  2' Endre tilleggsopplysninger '
     A                                      DSPATR(RI)
     A                                  4  2'Ordrenummer .'
     A            B7NUMM    R        O  4 16REFFLD(FONUMM)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  6  2'Ant. kolli. .'
     A            B7KOLI    R        B  6 16REFFLD(FOKOLI)
     A                                      OVRDTA
     A                                      EDTWRD('        ,   ')
     A                                      CHECK(RB)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                  7  2'Netto KG  . .'
     A            B7NETT    R        B  7 16REFFLD(FKPNET)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      EDTWRD('        ,   ')
     A                                      COLOR(YLW)
     A                                  8  2'Brutto KG . .'
     A            B7BRUT    R        B  8 16REFFLD(FKPBRU)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      EDTWRD('        ,   ')
     A                                      COLOR(YLW)
     A                                  9  2'Forsend.måte.'
     A            B7LMTX    R        B  9 16REFFLD(FOLMTX)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 11 20'Beløp'
     A                                 11 29'Tekst'
     A                                 12  2'Frakt . . . .'
     A            B7FRPR         9Y 2B 12 16EDTWRD('     0 ,  ')
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A            B7FRTE    R        B 12 27REFFLD(FDTEK1)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                 13  2'Emballasje. .'
     A            B7EMPR         9Y 2B 13 16EDTWRD('     0 ,  ')
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A            B7EMTE    R        B 13 27REFFLD(FDTEK1)
     A                                      OVRDTA
6.NU A**                                    OVRATR
     A                                      COLOR(YLW)
     A                                 15  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1SFL                     SFL
     A*%%TS  SD  20040218  085513  MHA         REL-V5R2M0  5722-WDS
7.06 A            D1SUMS        11  2H
7.xx A            D1KODE         1A  B  9  2
     A N21                                  DSPATR(PR)
     A  21                                  COLOR(YLW)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      VALUES('1' '3' '4' '5' '9' ' ')
7.xx A*           D1LEVE         9Y 3B  9  5EDTWRD('      ,   ')
7.xx A*                                     CHECK(RB)
7.xx A*                                     COLOR(YLW)
7.xx A            D1LEVE    R   -3   B  9  5REFFLD(FDANTA)
7.xx A                                      CHECK(RB)
7.xx A                                      COLOR(YLW)
7.xx A                                      EDTCDE(4)
7.xx A            D1LINE    R        O  9 16REFFLD(FDLINE)
     A  55
     AO 56
     AO 57
     AO 58                                  DSPATR(RI)
     A  55                                  COLOR(RED)
     A  56                                  COLOR(TRQ)
     A  58                                  COLOR(PNK)
     A  57                                  COLOR(BLU)
     A  59                                  COLOR(GRN)
7.xx A            D1ANTA    R   -2 03O  9 21REFFLD(FDANTA)
     A                                      EDTWRD('      ,   ')
7.05 A  53                                  COLOR(RED)
7.05 A  54                                  COLOR(BLU)
7.xx A            D1ENH1    R        O  9 32REFFLD(FDENH1)
7.05 A  53                                  COLOR(RED)
7.05 A  54                                  COLOR(BLU)
7.xx A            D1VARE    R   -7   O  9 36REFFLD(FDVARE)
7.05 A  53                                  COLOR(RED)
7.05 A  54                                  COLOR(BLU)
7.xx A            D1TEK1    R        O  9 45REFFLD(FDTEK1)
7.05 A  53                                  COLOR(RED)
7.05 A  54                                  COLOR(BLU)
     A          R D2CTL                     SFLCTL(D1SFL)
     A*%%TS  SD  20040218  085513  MHA         REL-V5R2M0  5722-WDS
7.xx A                                      SFLSIZ(0014)
7.xx A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CF03
     A                                      CF05
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A            SVRN02         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utplukk av enkeltlinjer'
     A                                      COLOR(WHT)
7.xx A                                  2 38'FO736D2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      COLOR(WHT)
     A                                  4  2'Ordre'
     A                                      COLOR(WHT)
     A            D2NUMM    R        O  4  8REFFLD(FONUMM)
6.NU A            D2SUFF    R        O  4 17REFFLD(FOSUFF)
     A                                  4 20'Kundenr'
     A                                      COLOR(WHT)
     A            D2KUND    R        O  4 28REFFLD(FOKUND)
     A            D2KUNA    R        O  4 35REFFLD(FOKUNA)
7.xx A                                  5 16'Linje'
     A                                      COLOR(WHT)
7.xx A                                  7  2'1=Plukk'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
     A                                      COLOR(WHT)
7.xx A                                  8  7'Antall'
     A                                      COLOR(WHT)
7.xx A                                  8 16'Linje'
     A                                      COLOR(WHT)
7.xx A                                  8 23'Bestilt'
     A                                      COLOR(WHT)
7.xx A                                  8 32'Enh'
     A                                      COLOR(WHT)
7.xx A                                  8 36'Varenr'
     A                                      COLOR(WHT)
7.xx A                                  8 45'Varetekst'
     A                                      COLOR(WHT)
7.xx A            D2LINE    R        B  6 16REFFLD(FDLINE)
     A                                      DLTCHK
     A                                      OVRDTA
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
7.xx A                                  7 11'3=Kopier (tekst)'
7.xx A                                      COLOR(BLU)
7.xx A**                                    COLOR(TRQ)
7.xx A                                  7 29'4=Hold tilbake'
7.xx A                                      COLOR(BLU)
7.xx A**                                    COLOR(RED)
     A                                  7 45'5=Ikke restnoter'
7.01 A                                      COLOR(BLU)
7.01 A**                                    COLOR(PNK)
7.xx A                                  7 63'9=Opphev'
     A                                      COLOR(BLU)
     A          R D2CMD
     A*%%TS  SD  20020312  132434  HKO         REL-V5R1M0  5722-WDS
     A                                 23 26'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 42'F5=Forny'
     A                                      COLOR(BLU)
     A          R H1BLD
     A*%%TS  SD  20020312  212325  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 10 40)
     A                                      CF03
     A                                      PUTOVR
     A                                  2  2'Bla alternativer'
     A                                      COLOR(WHT)
     A                                  4  2'Ordre-type'
     A            H1OTYP    R        B  4 14REFFLD(FOOTYP)
     A                                      OVRDTA
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                  4 24'Blank gir alle'
     A                                  6  2'Bla bakfra'
     A            H1VALG    R        B  6 14REFFLD(FÅVALA)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                  6 24'1=Ja,0=Nei'
     A                                  7 24'Gjelder blaing'
     A                                  8 24'i ordrenummer'
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R O1WIN
     A*%%TS  SD  20070328  105948  AMD550      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 14 58)
     A                                      CF03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Oppdatering av ordre - klar til p-
     A                                      lukking '
     A                                      DSPATR(RI)
     A                                  7  2'Leveringsdato .'
     A            O1LDAT         6Y 0B  7 18COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A  33                                  ERRMSGID(FMO0257 *LIBL/QMFOFAK 33)
     A  34                                  ERRMSGID(FMO0240 *LIBL/QMFOFAK 34)
     A  35                                  ERRMSGID(FMO0250 *LIBL/QMFOFAK 35)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  9  2'Informasjon . .'
     A            O1INF1    R        B  9 18REFFLD(FOINF1)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A            O1INF2    R        B 10 18REFFLD(FOINF2)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 13  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R A1MSG1
     A*%%TS  SD  20120302  115833  BOF620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 8 41)
     A                                      CF03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kunden er sperret for kreditt,   '
     A                                  3  2' kan ikke plukkes !               '
     A                                  5  3'F3=Gå tilbake'
     A                                      COLOR(BLU)
7.06 A          R A1MSG2
7.06 A*%%TS  SD  20020623  204742  HKO         REL-V5R1M0  5722-WDS
7.06 A                                      WINDOW(*DFT 11 60)
7.06 A                                      CF03
7.06 A                                      CF06
7.06 A                                      CA18
7.06 A                                      PUTOVR
7.06 A                                  2  2' Kredittgrense er overskredet '
7.06 A                                      DSPATR(RI)
7.06 A                                  5  2'Ordren er overskredet med . .'
7.06 A            F2OVER    R        O  5 32REFFLD(FOTOTS)
7.06 A                                      EDTWRD('       0 ,  ')
7.06 A                                  7  2'Dersom overskridelsen skal godjenn-
7.06 A                                      es, trykk F18'
7.06 A                                 10  2'F3=Gå tilbake   F18=Godkjenn overs-
7.06 A                                      kridelse'
7.06 A                                      COLOR(BLU)
7.06 A          R A1MSG3
7.06 A*%%TS  SD  20020623  204742  HKO         REL-V5R1M0  5722-WDS
7.06 A                                      WINDOW(*DFT 11 60)
7.06 A                                      CF03
7.06 A                                      CF06
7.06 A                                      CA18
7.06 A                                      PUTOVR
7.06 A                                  2  2' Kredittgrense er låst '
7.06 A                                      DSPATR(RI)
7.06 A                                  5  2'Du kan ikke godkjenne overskridel-
7.06 A                                      se'
7.06 A                                 10  2'F3=Gå tilbake '
7.06 A                                      COLOR(BLU)
7.06 A          R A1MSG4
7.06 A*%%TS  SD  20020623  204742  HKO         REL-V5R1M0  5722-WDS
7.06 A                                      WINDOW(*DFT 11 60)
7.06 A                                      CF03
7.06 A                                      CF06
7.06 A                                      PUTOVR
7.06 A                                  2  2' Kredittgrense er overskredet '
7.06 A                                      DSPATR(RI)
7.06 A                                  5  2'Ordren er overskredet med . .'
7.06 A            F2OVER    R        O  5 32REFFLD(FOTOTS)
7.06 A                                      EDTWRD('       0 ,  ')
7.06 A                                 10  2'F3=Gå tilbake'
7.06 A                                      COLOR(BLU)
      *
     A          R ASUME                     ASSUME
     A                                 24 79' '
