     A*%%TS  SD  20140616  133746  BOF630      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASSTAT
     A* Format. . . . . : SI502D
     A* Beskrivelse . . : Spørring på kjøp av vare
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A* 160614 BOF : Endret ifht. nummerutvidelse
7.xx A* 7.xx  041016  bsa  Justeringer basert på ny GUI og regler
7.01 A* 6.30  180517 BSA  Lagt inn oppslag mot arkiv
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
      * 160614 BOF : Endring ihht. nummerutvidelse
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
7.XX A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SFRF SFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20140616  133746  BOF630      REL-V7R1M0  5770-WDS
     A            B1LINE    R        H      REFFLD(SILINE)
     A            B1WSID    R        H      REFFLD(SIWSID)
     A            B1VALG    R        B 11  2REFFLD(SÅVALG)
     A                                      COLOR(YLW)
7.01 A                                      VALUES('5' '6' '7' '9' ' ')
     A                                      CHKMSGID(SM00010 *LIBL/QMFSTAT)
     A            B1VALN    R        O 11  4REFFLD(SIVALN)
     A***         B1LADR    R        O 11 27REFFLD(SILADR)
     A***         B1LANA        11A  O 11 34
     A            B1BIDA         6Y 0O 11 42EDTCDE(Z)
     A            B1BINR    R        O 11 49REFFLD(SIBINR)
     A            B1ORNR    R        O 11 58REFFLD(SIORNR)
     A            B1OTYP    R        O 11 67REFFLD(SIOTYP)
     A            B1ANTA    R   -2   O 11 70REFFLD(SIANTA)
     A                                      EDTCDE(M)
     A            B1LNAV        30A  O 11 11
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140616  133746  BOF630      REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA01
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A                                      SFLCSRRRN(&WCURRN)
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A            WCURRN         5S 0H
     A            WVISRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'AS Statistikk'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på kjøp av vare'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Vare. .'
     A                                      COLOR(BLU)
     A            B2VARE    R   -7   O  4 10REFFLD(SIVARE)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A            B2TEK1    R        O  4 19REFFLD(SITEK1)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A                                  4 65'År . . .'
     A                                      COLOR(BLU)
     A            B2PAAR    R        O  4 74REFFLD(SIPAAR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A                                  5 65'Dato . .'
     A                                      COLOR(BLU)
     A            B2BID1         6Y 0O  5 74COLOR(BLU)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
7.xx A                                  6  4'Lever '
7.xx A                                      COLOR(WHT)
7.xx A                                  6 12'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 19'Faktnr'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 28'Ordre'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  2'Velg og trykk Enter.'
     A                                      COLOR(BLU)
     A                                  6 65'Lever. .'
     A                                      COLOR(BLU)
     A            B2VAL1    R        O  6 74REFFLD(SIVALN)
     A                                      COLOR(BLU)
     A                                      OVRDTA
7.xx A                                  9  3'5=Vise  6=Skriv fakturakopi  7=Vis-
7.01 A                                      e faktura  9=Vis arkiv'
     A                                      COLOR(BLU)
7.xx A                                 10  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                 10  4'Lever'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 11'Navn                     '
7.xx A                                      COLOR(WHT)
7.xx A                                 10 43'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 50'Faktnr'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 59'Ordre'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 67'OT'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 72'Antall'
7.xx A**                                9  2'V Lever  Navn                     -
7.xx A**                                           Dato   Faktnr    Ordre  OT  -
7.xx A**                                     Antall'
     A                                      COLOR(WHT)
7.xx A            B2VALN    R        B  7  4REFFLD(SIVALN)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2BIDA         6Y 0B  7 11COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
7.xx A            B2BINR    R        B  7 18REFFLD(SIBINR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2ORNR    R        B  7 27REFFLD(SIORNR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A*
     A          R B2CMD
     A*%%TS  SD  19980615  155607  HKO         REL-V4R2M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
