     A*%%TS  SD  20050607  140308  AMD530      REL-V5R2M0  5722-WDS
     A*%%EC
7.xx A* 7.xx  110316  bsa  Justeringer basert på ny GUI og regler
     A*
8.01 A*K0000  261124  bsa  Lagt inn felt for motsatt UI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020306  220938  HKO         REL-V5R1M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1OTYP    R        O  8  6REFFLD(VAOTYP)
     A            B1OTXT    R        O  8 11REFFLD(VAOTXT)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020306  220938  HKO         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av ordretype'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Type'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 11'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  5'Type'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 11'Beskrivelse            '
7.xx A                                      COLOR(WHT)
7.xx A            B2OTYP    R        B  5  6REFFLD(VAOTYP)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2OTXT    R        B  5 11REFFLD(VAOTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  19931028  142557  AMD         REL-V2R2M0  5738-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20020306  215338  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av ordretype'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Ordretype. . . . . . . . . .'
     A            C1OTYP    R        B  5 31REFFLD(VAOTYP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020306  220938  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Ordretype finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20050607  140308  AMD530      REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av ordretype'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Ordretype  . . . . . . .'
     A            C1OTYP    R        O  5 27REFFLD(VAOTYP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2OTXT    R        B  5 30REFFLD(VAOTXT)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                  6  2'System . . . . . . . . .'
     A            C2OSYS    R        B  6 27REFFLD(VAOSYS)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  6 30'0=Salg,1=Innkjøp'
     A                                  7  2'Behandling-kode. . . . .'
     A            C2OAKK    R        B  7 27REFFLD(VAOAKK)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  7 30'0,1,2,3,4,5'
7.xx A                                  8  2'Bilde-alternativ . . . +'
     A            C2OBLD    R        B  8 27REFFLD(VAOBLD)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8 30'0,1,2,3,4,5'
     A                                  9  2'Felles/Direkte/Jobbkø. .'
     A            C2OFDJ    R        B  9 27REFFLD(VAOFDJ)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9 30'0,1,2'
     A                                 10  2'Resting. . . . . . . . .'
     A            C2OBOO    R        B 10 27REFFLD(VAOBOO)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 10 30'0=Vanlig,1=Nei,2=Alt beholdes'
     A                                 12  2'Inngå i ordregrunnlag. .'
     A            C2OOGR    R        B 12 27REFFLD(VAOOGR)
     A  30                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12 30'1=Ja,0=Nei'
     A                                 12 46'Oppdaterer Lager. . .'
     A            C2OLAG    R        B 12 68REFFLD(VAOLAG)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 12 70'1=Ja,0=Nei'
     A                                 13  2'Skaffeordre. . . . . . .'
     A            C2OSKA    R        B 13 27REFFLD(VAOSKA)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 13 30'1=Ja,0=Nei'
     A                                 13 46'Oppdaterer Økonomi. .'
     A            C2OOKO    R        B 13 68REFFLD(VAOOKO)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 13 70'1=Ja,0=Nei'
     A                                 14  2'Samle opp poster . . . .'
     A            C2OSAM    R        B 14 27REFFLD(VAOSAM)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 14 30'1=Ja,0=Nei'
     A                                 14 46'Oppdaterer Statistikk'
     A            C2OSTA    R        B 14 68REFFLD(VAOSTA)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 14 70'1=Ja,0=Nei'
     A                                 15  2'Vises i butikk-system. .'
     A            C2OBUT    R        B 15 27REFFLD(VAOBUT)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 15 30'1=Ja,0=Nei'
     A                                 15 46'Oppdaterer Prosjekt .'
     A            C2OPRO    R        B 15 68REFFLD(VAOPRO)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 15 70'1=Ja,0=Nei'
     A                                 17  2'Bilagskode . . . . . . .'
     A            C2OBIL    R        B 17 27REFFLD(VAOBIL)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 17 46'Fortegn . . . . . . '
     A            C2OKRE    R        B 17 68REFFLD(VAOKRE)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 17 70'(blank, -)'
     A                                 18  2'Ordretype nummerserie. .'
     A            C2ONUM    R        B 18 27REFFLD(VAONUM)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 18 30'(Overstyring)'
     A                                 19  2'OT motsvarende system. .'
     A            C2OMOT    R        B 19 27REFFLD(VAOMOT)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 19 30'(Ordretype)'
8.01 A                                 19 46'Bytte OT debet/kredit'
8.01 A            C21MOT    R        B 19 68REFFLD(VAONXT)
8.01 A  31                                  ERRMSG('Må være av akkumulator -
8.01 A                                       type 3' 31)
8.01 A  32                                  ERRMSG('Må ha motsatt fortegn -
8.01 A                                      til denne' 32)
8.01 A  33                                  ERRMSG('Angitt OT finnes ikke' 33)
8.01 A  34                                  ERRMSG('Ordretypen du vedlikehold-
8.01 A                                      er er ikke Fakura/Kreditnota' 34)
8.01 A  35                                  ERRMSG('Ordretypen du vedlikehold-
8.01 A                                      er må være av typen salg' 35)
8.01 A                                      COLOR(YLW)
8.01 A  30                                  DSPATR(PR)
8.01 A                                      OVRDTA
8.01 A                                 19 71'(Otype)'
     A                                 20  2'Neste Ordretype. . . . .'
     A            C2ONXT    R        B 20 27REFFLD(VAONXT)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 20 46'Rutine Ordre-syst. .'
     A            C2ORUT    R        B 20 68REFFLD(VAORUT)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 21  2'Gyldige Ordretyper . . .'
     A            C2ON01    R        B 21 27REFFLD(VAON01)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2ON02    R        B 21 30REFFLD(VAON02)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2ON03    R        B 21 33REFFLD(VAON03)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2ON04    R        B 21 36REFFLD(VAON04)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2ON05    R        B 21 39REFFLD(VAON05)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2ON06    R        B 21 42REFFLD(VAON06)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 22  2'Intern Ordretype . . . .'
     A            C21INT    R        B 22 27REFFLD(VA1INT)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 22 30'1=Ja,0=Nei'
     A                                 21 46'Rutine Butikksyst. .'
     A            C2ORUB    R        B 21 68REFFLD(VAORUB)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 22 46'INTERN KONTERING . .'
     A            C21INK    R        B 22 68REFFLD(VA1INT)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 22 70'1=JA,0=NEI'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020306  220938  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 51)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette ordretype '
     A                                      DSPATR(RI)
     A                                  4  2'Ordretype. . . . .'
     A            D1OTYP    R        O  4 21REFFLD(VAOTYP)
     A                                  5  2'Beskrivelse. . . .'
     A            VAOTXT    R        O  5 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020306  220938  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 15 51)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Ordretype. . . . .'
     A            K1OTYP    R        B  4 21REFFLD(VAOTYP)
     A  31                                  ERRMSGID(VMA5010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMA5030 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  9  2'Ordretype. . . . .'
     A            VAOTYP    R        O  9 21OVRDTA
     A                                 10  2'Beskrivelse. . . .'
     A            VAOTXT    R        O 10 21OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
