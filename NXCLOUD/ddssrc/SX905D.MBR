     A*%%TS  SD  20020312  222734  HKO         REL-V5R1M0  5722-WDS
     A*
     A*     BSA  190618  Nytt program
     A*
     A*
     A*%%EC
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
7.XX A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRF MFRFR)
     A                                      PRINT
     A          R A1BLD
     A*%%TS  SD  19980128  124042  HKO         REL-V3R7M0  5716-PW1
     A                                      CA03
     A                                      CF04
     A                                      CF06
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Statistikk'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Automatisk overføring Cobuilder'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      COLOR(WHT)
     A                                  4  5'Status. . . . . . . . .'
     A            A1STAT    R        B  4 31REFFLD(GGSTAT)
     A  32                                  ERRMSGID(FMB0110 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  4 33'1=Aktivert, 0=Deaktivert'
     A                                  7  5'Mail sendes til'
     A                                      COLOR(WHT)
     A            A1MAI1    R        B  7 22REFFLD(SCMAI1)
     A  30                                  ERRMSG('Fyll ut minst en mail-
     A                                      adresse!' 30)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A            A1MAI2    R        B  8 22REFFLD(SCMAI2)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A            A1MAI3    R        B  9 22REFFLD(SCMAI3)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 11  5'Emne på mail. .'
     A                                      COLOR(WHT)
     A            A1SUBJ    R        B 11 22REFFLD(SCSUBJ)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 12  5'Ordlyd på mail '
     A                                      COLOR(WHT)
     A            A1TXT1    R        B 12 22REFFLD(SCTXT1)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A            A1TXT2    R        B 13 22REFFLD(SCTXT2)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A            A1TXT3    R        B 14 22REFFLD(SCTXT3)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 16  5'Avsender mail '
     A                                      COLOR(WHT)
     A            A1SEND    R        B 16 22REFFLD(SCSEND)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
      *
     A                                 18  5'Siste kjøredato. .'
     A            A1KDAT    R        O 18 24REFFLD(SCKDAT)
     A                                      COLOR(YLW)
     A                                 19  5'Siste oppdatering.'
     A            A1UDAT    R        O 19 24REFFLD(SCUDAT)
     A                                      COLOR(YLW)
     A            A1UTID    R        O 19 37REFFLD(SCUTID)
     A                                      COLOR(YLW)
      *
     A                                 23  2'F3=Gå tilbake  F4=Spørring  F6=-
     A                                      Overfør Cobuilder'
     A                                      COLOR(BLU)
     A          R F6WIN
     A*%%TS  SD  20050604  201355  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 11 35)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2'Start overføring til Cobuilder'
     A                                      DSPATR(RI)
     A                                  5  2'Solgte varer f.o.m'
     A            B1KDAT         6Y 0B  5 21COLOR(YLW)
     A  31                                  ERRMSGID(SM00110 *LIBL/QMFSTAT 31)
     A                                      EDTWRD('  .  .  ')
     A                                      CHECK(RB)
     A                                  6  2'Ta med alle varer '
     A            B1ALLV         1Y 0B  6 21COLOR(YLW)
     A                                  6 23'1/0'
     A                                 10  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
