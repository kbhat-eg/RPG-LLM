     A*%%TS  SD  20041102  154348  GRO530      REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RK819D
     A* Beskrivelse . . : Kunder, autogiro vedlikehold kundepostering
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       021104 GRO  Nytt program
6.30 A*       290216 bsa  Justert pga jacada
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A***                                   WDWBORDER((*COLOR BLU) (*DSPATR RI)-
     A***                                    (*CHAR '        '))
     A*
     A*  Bilde B1 - subfile 1
     A*
     A          R B1WSF                     SFL
     A*%%TS  SD  20041102  154348  GRO530      REL-V5R2M0  5722-WDS
     A            B1BDAT    R        H      REFFLD(RNBDAT)
     A            B1TIST    R        H      REFFLD(RNTIST)
     A            B1VALG    R        B  8  2REFFLD(RWVALG)
     A                                      COLOR(YLW)
     A                                      VALUES('1' '2' '4' '5' ' ')
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1BILN    R        O  8  4REFFLD(RNBILN)
6.NU A            B1BIDA         6A  O  8 13
6.NU A            B1BILK    R        O  8 20REFFLD(RNBILK)
6.NU A            B1TEXT        14A  O  8 23
6.NU A            B1REFN    R        O  8 38REFFLD(RNREFN)
     A            B1FODA         6A  O  8 47
     A            B1BELØ         9Y 2O  8 54EDTCDE(J)
     A            B1REST         9Y 2O  8 68EDTCDE(J)
     A*
     A*  Bilde B2 - subfile kontroll
     A*
     A          R B2WCT                     SFLCTL(B1WSF)
     A*%%TS  SD  20041102  154330  GRO530      REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF07
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A            SVRN01         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
6.30 A**                                    EDTCDE(Y)
     A                                      COLOR(WHT)
6.NU A**                                1 72DATE
6.NU A**                                    EDTCDE(Y)
6.NU A**                                    COLOR(WHT)
     A                                  2  2'Vedlikehold av autogiro kundeposte-
     A                                      ringer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Kunde:'
     A                                      COLOR(WHT)
     A            B2KUND    R        O  4  9REFFLD(RKKUND)
     A            B2NAVN    R        O  4 16REFFLD(RKNAVN)
     A                                  4 54'Bilagsdato . .:'
     A                                      COLOR(YLW)
     A            B2BDAT    R        B  4 70REFFLD(RNBDAT)
     A                                  5  2'Velg post og trykk enter'
     A                                      COLOR(BLU)
     A                                  6  2'2=Endre'
     A                                      COLOR(BLU)
     A                                  6 11'4=Slett'
     A                                      COLOR(BLU)
     A                                  6 20'5=Se på'
     A                                      COLOR(BLU)
     A                                  7  2'V'
     A                                      COLOR(WHT)
     A                                  7  4'Bil.nr'
     A                                      COLOR(WHT)
6.NU A                                  7 13'Dato'
     A                                      COLOR(WHT)
6.NU A                                  7 20'Bk'
     A                                      COLOR(WHT)
6.NU A                                  7 23'Tekst'
     A                                      COLOR(WHT)
6.NU A                                  7 34'Val.'
     A                                      COLOR(WHT)
6.NU A                                  7 39'Ref.'
     A                                      COLOR(WHT)
     A                                  7 47'Forfall'
     A                                      COLOR(WHT)
     A                                  7 60'Beløp'
     A                                      COLOR(WHT)
     A                                  7 72'Restbeløp'
     A                                      COLOR(WHT)
     A*
     A*  Bilde B2 - funksjonstaster
     A*
     A          R B2CMD
     A*%%TS  SD  19970311  153637  KOB         REL-V3R1M0  5763-PW1
     A                                      CA03
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 19'F5=Forny'
     A                                      COLOR(BLU)
     A                                 23 30'F7=Valuta/NOK'
     A                                      COLOR(BLU)
     A*
     A*  Bilde C2 - vindu for vedlikehold
     A*
     A          R C2WIN
     A*%%TS  SD  19961001  084339  JT          REL-V3R1M0  5763-PW1
     A                                      WINDOW(&WACTLI &WACTPO 15 75)
     A                                      CA03
     A                                      CA04
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A                                      PUTOVR
     A            WACTLI         2S 0P
     A            WACTPO         3S 0P
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A            C2NYEN        11A  O  2  2DSPATR(RI)
     A                                      OVRDTA
     A                                  2 29'Post - Autogiro  '
     A                                      COLOR(WHT)
     A                                  3  1'Bilagsnummer . .:'
     A            C2BILN    R        O  3 19REFFLD(RNBILN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  4  1'Bilags-kode. . .:'
     A            C2BILK    R        B  4 19REFFLD(RNBILK)
     A  33                                  DSPATR(PC)
     A  33 81                               ERRMSGID(RM01010 *LIBL/QMFOKON 81)
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2TEXT    R        B  4 22REFFLD(RNTEXT)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                  4 43'Bilagsdato . . .:'
     A            C2BDAT    R        O  4 61REFFLD(RNBDAT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  5  1'Forfallsdato . .:'
     A            C2FDAT    R        B  5 19REFFLD(RNFDAT)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
     A                                  5 43'Regnskaps-per/år:'
     A            C2RPER    R        O  5 61REFFLD(RNRPER)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2RAAR    R        O  5 64REFFLD(RNRAAR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  6  1'Reklamasjonskode:'
     A            C2REKL    R        B  6 19REFFLD(RNREKL)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  6 43'Journalliste-nr.:'
     A            C2JOUR    R        O  6 61REFFLD(RNJOUR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  1'Referansenummer.:'
     A            C2REFN    R        B  7 19REFFLD(RNREFN)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  7 43'Samlefaktura-nr.:'
     A            C2SAMF    R        O  7 61REFFLD(RNSAMF)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  8  1'Antall purringer:'
     A            C2ANTP    R        B  8 19REFFLD(RNANTP)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8 43'Beløp. . . . . .:'
     A            C2BELØ    R        O  8 61REFFLD(RNBELØ)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  9  1'Renteberegnet. .:'
     A            C2RENK    R        B  9 19REFFLD(RNRENK)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9 43'Restbeløp. . . .:'
     A            C2REST    R        B  9 61REFFLD(RNREST)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 10  1'Inkassokode. . .:'
     A            C2INKO    R        B 10 19REFFLD(RNINKO)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 10 43'Valuta-kode. . .:'
     A            C2VALK    R        O 10 61REFFLD(RNVALK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 11  1'Avdeling . . . .:'
     A            C2AVDE    R        B 11 19REFFLD(RNAVDE)
     A  34                                  DSPATR(PC)
     A  34 81                               ERRMSGID(RM01070 *LIBL/QMFOKON 81)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 11 43'Valutabeløp. . .:'
     A            C2VALB    R        O 11 61REFFLD(RNVALB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 12  1'Selger . . . . .:'
     A            C2SLGR    R        B 12 19REFFLD(RNSLGR)
     A  35                                  DSPATR(PC)
     A  35 81                               ERRMSGID(RM01090 *LIBL/QMFOKON 81)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 12 43'Valuta-rest. . .:'
     A            C2VALR    R        O 12 61REFFLD(RNVALR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 14 18'F4=Spørring'
     A                                      COLOR(BLU)
     A*  Bilde D2 - vindu for sletting
     A*
     A          R D2WIN
     A*%%TS  SD  19970312  040459  KOB         REL-V3R1M0  5763-PW1
     A                                      WINDOW(&WACTLI &WACTPO 15 75)
     A                                      CA03
     A                                      CA12
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A                                      PUTOVR
     A            WACTLI         2S 0P
     A            WACTPO         3S 0P
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A                                  2  3'Sletting'
     A                                      DSPATR(HI)
     A                                      DSPATR(RI)
     A                                      COLOR(WHT)
     A                                  2 29'Post - Autogiro  '
     A                                      COLOR(WHT)
     A                                  3  1'Bilagsnummer . .:'
     A            RNBILN    R        O  3 19DSPATR(UL)
     A                                      OVRDTA
     A                                  4  1'Bilags-kode. . .:'
     A            RNBILK    R        O  4 19DSPATR(UL)
     A                                      OVRDTA
     A            RNTEXT    R        O  4 22DSPATR(UL)
     A                                      OVRDTA
     A                                  4 43'Bilagsdato . . .:'
     A            RNBDAT    R        O  4 61DSPATR(UL)
     A                                      OVRDTA
     A                                  5  1'Forfallsdato . .:'
     A            RNFDAT    R        O  5 19DSPATR(UL)
     A                                      OVRDTA
     A                                  5 43'Regnskaps-per/år:'
     A            RNRPER    R        O  5 61DSPATR(UL)
     A                                      OVRDTA
     A            RNRAAR    R        O  5 64DSPATR(UL)
     A                                      OVRDTA
     A                                  6  1'Reklamasjonskode:'
     A            RNREKL    R        O  6 19DSPATR(UL)
     A                                      OVRDTA
     A                                  6 43'Journalliste-nr.:'
     A            RNJOUR    R        O  6 61DSPATR(UL)
     A                                      OVRDTA
     A                                  7  1'Referansenummer.:'
     A            RNREFN    R        O  7 19DSPATR(UL)
     A                                      OVRDTA
     A                                  7 43'Samlefaktura-nr.:'
     A            RNSAMF    R        O  7 61DSPATR(UL)
     A                                      OVRDTA
     A                                  8  1'Antall purringer:'
     A            RNANTP    R        O  8 19DSPATR(UL)
     A                                      OVRDTA
     A                                  8 43'Beløp. . . . . .:'
     A            RNBELØ    R        O  8 61DSPATR(UL)
     A                                      OVRDTA
     A                                  9  1'Renteberegnet. .:'
     A            RNRENK    R        O  9 19DSPATR(UL)
     A                                      OVRDTA
     A                                  9 43'Restbeløp. . . .:'
     A            RNREST    R        O  9 61DSPATR(UL)
     A                                      OVRDTA
     A                                 10  1'Inkassokode. . .:'
     A            RNINKO    R        O 10 19DSPATR(UL)
     A                                      OVRDTA
     A                                 10 43'Valuta-kode. . .:'
     A            RNVALK    R        O 10 61DSPATR(UL)
     A                                      OVRDTA
     A                                 11  1'Avdeling . . . .:'
     A            RNAVDE    R        O 11 19DSPATR(UL)
     A                                      OVRDTA
     A                                 11 43'Valutabeløp. . .:'
     A            RNVALB    R        O 11 61DSPATR(UL)
     A                                      OVRDTA
     A                                 12  1'Selger . . . . .:'
     A            RNSLGR    R        O 12 19DSPATR(UL)
     A                                      OVRDTA
     A                                 12 43'Valuta-rest. . .:'
     A            RNVALR    R        O 12 61DSPATR(UL)
     A                                      OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
