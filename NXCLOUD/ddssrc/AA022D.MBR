     A*%%TS  SD  20140528  102330  AMD620      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASPGPL
     A* Format. . . . . : AA022D
     A* Beskrivelse . . : Vedlikehold av bruker-register
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       250804 AMD  Nytt program
6.22 A*       280915 BSA  Spm om også brukerrettigheter skal kopieres
     A*
8.01 A*K0000  281122 BSA  Skal ikke ha meny på ASP brukere.
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/AFRF AFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20050603  125913  AMD530      REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(AÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5 7 8 9)
     A                                      CHKMSGID(AM00010 *LIBL/QMFAGPL)
     A            B1USER    R        O  8  4REFFLD(ABUSER)
     A            B1NAVN    R        O  8 15REFFLD(ABNAVN)
     A            B1LEVL    R        O  8 46REFFLD(ABLEVL)
     A            B1BFIL    R        O  8 71REFFLD(ABBFIL)
     A            B1BFIR    R        O  8 75REFFLD(ABBFIR)
     A            B1BAVD    R        O  8 79REFFLD(ABBAVD)
     A            B1MENY    R        O  8 49REFFLD(ABMENY)
     A            B1RLIB    R        O  8 60REFFLD(ABRLIB)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050603  125913  AMD530      REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      CA22
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Driftssystem '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av bruker-register'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'2=Endre  3=Kopier'
     A                                      COLOR(BLU)
     A                                  4 46'8=Bygg Bruker-profil/Ip-program'
     A                                      COLOR(BLU)
     A                                  5  2'4=Slett  5=Vis'
     A                                      COLOR(BLU)
     A                                  5 20'7=Bibliotekslister'
     A                                      COLOR(BLU)
     A                                  5 46'9=Arbeide med bruker-profiler'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
     A                                      COLOR(WHT)
7.xx A                                  7  4'Bruker'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 15'Navn'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 44'Nivå'
     A                                      COLOR(WHT)
7.xx A                                  7 49'Startmeny'
     A                                      COLOR(WHT)
7.xx A N40                              7 60'Ref.bibl.'
     A N40                                  COLOR(WHT)
7.xx A  40                              7 60'Gruppe'
     A  40                                  COLOR(WHT)
7.xx A                                  7 70'Fgr'
     A                                      COLOR(WHT)
7.xx A                                  7 74'Fir'
     A                                      COLOR(WHT)
7.xx A                                  7 78'Avd'
     A                                      COLOR(WHT)
7.xx A            B2USER    R        B  6  4REFFLD(ABUSER)
     A  31                                  ERRMSGID(AME0010 *LIBL/QMFAGPL 31)
     A  32                                  ERRMSGID(AME0020 *LIBL/QMFAGPL 32)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2NAVN    R        B  6 15REFFLD(ABNAVN)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
7.xx A            B2RLIB    R        B  6 60REFFLD(ABRLIB)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20050602  130431  AMD530      REL-V5R2M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett   F22=Skift bilde'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20040825  144902  AMD         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Driftssystem '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av bruker-register'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Bruker . . . . . . . . . . .'
     A            C1USER    R        B  5 31REFFLD(ABUSER)
     A  31                                  ERRMSGID(AMB0010 *LIBL/QMFAGPL 31)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20040825  144902  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Bruker finnes fra før      '
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20140528  102330  AMD620      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Driftssystem '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av bruker-register'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Bruker . . . . . . . . . . .'
     A            C1USER    R        O  4 31REFFLD(ABUSER)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  4 46'Miljø . . . . . . .'
     A            C2MILJ    R        O  4 66REFFLD(ABMILJ)
     A                                      COLOR(YLW)
     A  34                                  ERRMSGID(AMB0080 *LIBL/QMFAGPL 34)
     A                                      OVRDTA
     A                                  5  2'Navn . . . . . . . . . . . .'
     A            C2NAVN    R        B  5 31REFFLD(ABNAVN)
     A  31                                  ERRMSGID(AMB0010 *LIBL/QMFAGPL 31)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  6  2'Gruppereferanse. . . . . . .'
     A            C2GRUP    R        B  6 31REFFLD(ABGRUP)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  6 46'Tilgangskode. . . .'
     A            C2TILG    R        B  6 66REFFLD(ABTILG)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  7  2'Startmeny. . . . . . . . . .'
     A            C2MENY    R        B  7 31REFFLD(ABMENY)
8.01 A  37                                  ERRMSG('Skal ikke ha meny på ASP -
8.01 A                                      brukeren' 37)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  7 46'Sikkerhetsnivå. . .'
     A            C2LEVL    R        B  7 66REFFLD(ABLEVL)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8  2'Telefonnummer. . . . . . . .'
     A            C2TLFN    R        B  8 31REFFLD(ABTLFN)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8 46'10 20 30 40 50'
     A                                 10  2'Siste file-gruppe. . . . . .'
     A            C2SFIL    R        B 10 31REFFLD(ABSFIL)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A  32                                  ERRMSGID(AMB0070 *LIBL/QMFAGPL 32)
     A  36                                  ERRMSGID(AMG0010 *LIBL/QMFAGPL 36)
     A                                      OVRDTA
     A            C2SBIB    R        O 10 36REFFLD(ABSBIB)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10 68'(Bibliotek)'
     A                                 11  2'Siste firma-nummer . . . . .'
     A            C2SFIR    R        B 11 31REFFLD(ABSFIR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2FNAV    R        O 11 36REFFLD(ABFNAV)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 11 68'(Firmanavn)'
     A                                 12  2'Siste avdeling . . . . . . .'
     A            C2SAVD    R        B 12 31REFFLD(ABSAVD)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 13  2'Lager-nummer . . . . . . . .'
     A            C2LAGR    R        B 13 31REFFLD(ABLAGR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 15  2'Begrense tilgang filegrp . .'
     A            C2BFIL    R        B 15 31REFFLD(ABBFIL)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 15 43'(Blank=Nei    1=Ja)'
     A                                 16  2'Begrense tilgang firma . . .'
     A            C2BFIR    R        B 16 31REFFLD(ABBFIR)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A  35                                  ERRMSGID(AMB0040 *LIBL/QMFAGPL 35)
     A                                      OVRDTA
     A                                 16 43'(Blank=Nei    1=Ja)'
     A                                 17  2'Begrense tilgang avdeling. .'
     A            C2BAVD    R        B 17 31REFFLD(ABBAVD)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 17 43'(Blank=Nei    1=Ja)'
     A                                 18  2'AS/400 / S/36 Ip-program . .'
     A            C2A4S3    R        B 18 31REFFLD(ABA4S3)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      VALUES(' ' '1')
     A                                      CHKMSGID(AM00010 *LIBL/QMFAGPL)
     A                                 18 43'(Blank=AS/400 1=S/36)'
     A                                 19  2'Benytt LIBL til bruker . . .'
     A            C2RLIB    R        B 19 31REFFLD(ABRLIB)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 19 43'(Oppgi bruker for ønsket bibl.list-
     A                                      e)'
     A                                 21  2'Sist endret dato . . . . . .'
     A            C2EDAT         6Y 0O 21 31EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21 44'Endret av. .'
     A            C2EUSR    R        O 21 57REFFLD(ABEUSR)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F4-Velge filgruppe'
     A                                      COLOR(BLU)
     A                                 23 39'F6=Systemavhengig info'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20040825  144902  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette bruker       '
     A                                      DSPATR(RI)
     A                                  4  2'Bruker . . . . . .'
     A            D1USER    R        O  4 21REFFLD(ABUSER)
     A                                  6  2'Navn . . . . . . .'
     A            ABNAVN    R        O  6 21
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20040826  102235  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Bruker . . . . . . . . .'
     A            K1USER    R        B  4 27REFFLD(ABUSER)
     A  31                                  ERRMSGID(AMB0010 *LIBL/QMFAGPL 31)
     A  32                                  ERRMSGID(AMB0030 *LIBL/QMFAGPL 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Kopier biblioteksliste ?'
     A            K1JANE         1Y 0B  5 27COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(AM00010 *LIBL/QMFAGPL)
     A                                      OVRDTA
     A                                      DSPATR(MDT)
     A                                  5 40'0=Nei 1=Ja'
6.22 A                                  6  2'Kopier rettigheter ?    '
6.22 A            K1BRJN         1Y 0B  6 27COLOR(YLW)
6.22 A                                      EDTCDE(Z)
6.22 A                                      VALUES(0 1)
6.22 A                                      CHKMSGID(AM00010 *LIBL/QMFAGPL)
6.22 A                                      OVRDTA
6.22 A                                      DSPATR(MDT)
6.22 A                                  6 40'0=Nei 1=Ja'
     A                                  8  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 10  2'Bruker . . . . . . . . .'
     A            ABUSER    R        O 10 27OVRDTA
     A                                 11  2'Navn . . . . . . . . . .'
     A            ABNAVN    R        O 11 27OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R I1WIN
     A*%%TS  SD  20040913  123915  AMD         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2'Systemavhengig brukerinformasjon'
     A                                      COLOR(WHT)
     A                                  2 38'1=Velg'
     A                                      COLOR(BLU)
     A                                  4  2'Valg   System                     -
     A                                              '
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            I1SV02         1Y 0B  5  3COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
     A                                      DSPATR(MDT)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(AM00010 *LIBL/QMFAGPL)
     A  31                                  ERRMSGID(AMB0050 *LIBL/QMFAGPL 31)
     A                                  5  9'Ordre/fakturering'
     A            I1SV03         1Y 0B  6  3COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
     A                                      DSPATR(MDT)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(AM00010 *LIBL/QMFAGPL)
     A  31                                  ERRMSGID(AMB0050 *LIBL/QMFAGPL 31)
     A                                  6  9'Bestilling/Innkjøp/Lager'
     A            I1SV04         1Y 0B  7  3COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
     A                                      DSPATR(MDT)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(AM00010 *LIBL/QMFAGPL)
     A                                  7  9'Butikk'
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
