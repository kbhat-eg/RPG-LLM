     A*%%TS  SD  20111112  083524  BOF610      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASPGPL
     A* Format. . . . . : AA072D
     A* Beskrivelse . . : Filkopiering -parameter, vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       121111 bof  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/AFRF AFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20111112  083524  BOF610      REL-V7R1M0  5770-WDS
     A            B1VALG    R        B  8  2REFFLD(AÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(MM00010 *LIBL/QMFAGPL)
     A            B1PLIN    R        O  8  5REFFLD(ACPLIN)
     A            B1PLEV    R        O  8 13REFFLD(ACPLEV)
     A            B1LNAV        30A  O  8 24
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20110114  090223  ASP_S61     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Driftssystem '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av Filkopiering - leve-
     A                                      randører'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
     A                                  6  2'V  Linje Leverandør   Navn'
     A                                      COLOR(WHT)
     A            B2PLIN    R        B  7  5REFFLD(ACPLIN)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20110114  090223  ASP_S61     REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Driftssystem '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av Filkopiering leve-
     A                                      randører'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Linjenr  . . . . . . . . . .'
     A            C1PLIN    R        B  5 31REFFLD(ACPLIN)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  19990316  082956  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Leverandør finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20111112  083524  BOF610      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Driftssystem '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av Filkopiering levera-
     A                                      ndører'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Linjenr  . . . . . . . . . .'
     A            C1PLIN    R        O  5 31REFFLD(ACPLIN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Leverandørnr.  . . . . . . .'
     A            C2PLEV    R        B  7 31REFFLD(ACPLEV)
     A  31                                  ERRMSGID(AM00140 *LIBL/QMFAGPL 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                  8  2'Leverandørnavn . . . . . . .'
     A            C2LNAV        30A  O  8 31COLOR(YLW)
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2PEDA         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2PEUS    R        O 20 31REFFLD(ACPEUS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20111112  083524  BOF610      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette XML nobb'
     A                                      DSPATR(RI)
     A                                  4  2'Linjenr  . . . . .'
     A            D1PLIN    R        O  4 21REFFLD(ACPLIN)
     A                                  6  2'Leverandørnr.  . .'
     A            D1PLEV    R        O  6 21REFFLD(ACPLEV)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20110114  090223  ASP_S61     REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Linjenr. . . . . .'
     A            K1PLIN    R        B  4 21REFFLD(ACPLIN)
     A  31                                  ERRMSGID(AM00120 *LIBL/QMFAGPL 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Linjenr  . . . . .'
     A            ACPLIN    R        O  8 21OVRDTA
     A                                  9  2'Leverandør . . . .'
     A            ACPLEV    R        O  9 21
     A                                      OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
