     A*%%TS  SD  20170504  114939  G0XEGSVSTR  REL-V7R1M0  5770-WDS
     A*
7.xx A*     BSA  160817    Endringer relatert til ny GUI
     A*
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/IADFRF IADFRFR)
     A                                      HOME(21)
     A                                      WDWBORDER((*COLOR BLU) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A          R B1SFL                     SFL
     A*%%TS  SD  20170425  140752  SST630      REL-V7R1M0  5770-WDS
     A            B1VALG    R        B 11  2REFFLD(ZZVALG)
     A                                      COLOR(YLW)
     A                                      VALUES('2' '3' '4' '5' '8' ' ')
     A                                      CHKMSGID(ZM00010 *LIBL/QMFADGANG)
     A            B1RUTI    R        O 11  4REFFLD(IADRUT)
     A            B1TYPE    R        O 11 16REFFLD(IADRTY)
     A            B1TEXT    R        O 11 22REFFLD(IADRTX)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20170504  114939  G0XEGSVSTR  REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(10)
     A                                      CA03
     A                                      CA05
     A                                      CA06
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&WCURRN)
     A            WCURRN         5S 0H
     A            WVISRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 65'Dato:'
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  2 45'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 53USER
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av Rutiner for adgangs-
     A                                      kontroll'
     A                                      COLOR(WHT)
     A                                  2 65'KL..:'
     A                                      COLOR(WHT)
     A                                  2 72TIME
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  5  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  4'Rutine'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 16'Type'
7.xx A                                      COLOR(WHT)
7.xx A                                  5 22'Tekst'
7.xx A                                      COLOR(WHT)
7.xx A**                                5 28'(frisøk)'
7.xx A                                  7  1'Velg og trykk Enter.'
     A                                      COLOR(BLU)
7.xx A                                  8  2'2=Endre'
     A                                      COLOR(BLU)
7.xx A                                  8 14'3=Kopiere'
     A                                      COLOR(BLU)
7.xx A                                  9  2'4=Slette    5=Vise    8=Tillgang'
     A                                      COLOR(BLU)
7.xx A                                 10  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                 10  4'Rutine  '
7.xx A                                      COLOR(WHT)
7.xx A                                 10 16'Type'
7.xx A                                      COLOR(WHT)
7.xx A                                 10 22'Tekst  (frisøk)               '
7.xx A**                               10  2'V Rutine      Type  Tekst'
     A                                      COLOR(WHT)
7.xx A            B2VALG    R        B  6  2REFFLD(ZZVALG)
     A                                      COLOR(YLW)
     A                                      VALUES('1' '2' '3' '4' '5' '8' ' ')
     A                                      CHKMSGID(ZM00010 *LIBL/QMFADGANG)
     A                                      OVRDTA
7.xx A            B2RUTI    R        B  6  4REFFLD(IADRUT)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2TYPE    R        B  6 16REFFLD(IADRTY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2TEXT    R        B  6 22REFFLD(IADRTX)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                  9 28'(frisøk)'
     A          R B2CMD
     A*%%TS  SD  20030827  143057  MHA         REL-V4R4M0  5769-PW1
     A                                 23  3'F3=Gå ut'
     A                                      COLOR(BLU)
     A                                 23 16'F5=Forny'
     A                                      COLOR(BLU)
     A                                 23 29'F6=Opprett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20030827  143057  MHA         REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CLRL(*ALL)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 65'Dato:'
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  2 45'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 53USER
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av Rutiner m/adgangsko-
     A                                      ntroll'
     A                                      COLOR(WHT)
     A                                  2 65'KL..:'
     A                                      COLOR(WHT)
     A                                  2 72TIME
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  5  2'Rutine  . . . . . . . . . .'
     A            C1RUTI    R        B  5 30REFFLD(IADRUT)
     A                                      COLOR(YLW)
     A  31                                  ERRMSGID(ZMA0014 *LIBL/QMFADGANG)
     A          R C1MSG
     A*%%TS  SD  20030827  143057  MHA         REL-V4R4M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Rutine finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R C2BLD
     A*%%TS  SD  20170425  110413  SST630      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 65'Dato:'
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  2 45'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 53USER
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av Rutiner m/adgangsko-
     A                                      ntroll'
     A                                      COLOR(WHT)
     A                                  2 65'KL..:'
     A                                      COLOR(WHT)
     A                                  2 72TIME
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Rutine,  Tekster . . . . .'
     A            C2NYEN    R        O  5 69REFFLD(ZZNYEN)
     A                                      DSPATR(RI)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  9  2'Tekst . . . . . . . . . . .'
     A            C1RUTI    R        O  5 30REFFLD(IADRUT)
     A                                      OVRDTA
     A            C2TEXT    R        B  9 30REFFLD(IADRTX)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A  31                                  ERRMSGID(ZMA5013 *LIBL/QMFADGANG 31)
     A                                  8  2'Type  . . . . . . . . . . .'
     A            C2TYPE    R        B  8 30REFFLD(IADRTY)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A  32                                  ERRMSGID(ZMA5014 *LIBL/QMFADGANG 32)
     A          R D1WIN
     A*%%TS  SD  19971007  123605  MWE         REL-V3R7M0  5716-PW1
     A                                      WINDOW(S1WIN)
     A                                      CA03
     A                                  2  4' Slette post '
     A                                      DSPATR(RI)
     A                                 14  4'F3=Gå tilbake'
     A                                  4  4'Rutine       '
     A                                  4 18'.'
     A                                  5  4'Tekst'
     A                                  5 10'.'
     A                                  5 12'.'
     A                                  5 14'.'
     A                                  5 16'.'
     A                                  5 18'.'
     A            D1RUTI    R        O  4 20REFFLD(IADRUT)
     A            D1TEXT    R        O  5 20REFFLD(IADRTX)
     A          R K1WIN
     A*%%TS  SD  20030827  143057  MHA         REL-V4R4M0  5769-PW1
     A                                      WINDOW(S1WIN)
     A                                      CA03
     A                                      PUTOVR
     A                                  2  4' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 11  4' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  4'Rutine . . . . .'
     A                                 16  4'F3=Avbryt'
     A                                 13  4'Rutine . . . . .'
     A                                  5  4'Tekst.'
     A                                  5 11'.'
     A                                  5 13'.'
     A                                  5 15'.'
     A                                  5 17'.'
     A                                  5 19'.'
     A            K2RUTI    R        O  4 21REFFLD(IADRUT)
     A            K2TEXT    R        O  5 21REFFLD(IADRTX)
     A            K1RUTI    R        B 13 21REFFLD(IADRUT)
     A                                      COLOR(YLW)
     A          R S1WIN
     A*%%TS  SD  19931028  171434  AMD         REL-V2R2M0  5738-PW1
     A                                      WINDOW(*DFT 17 70)
     A          R D2WIN
     A*%%TS  SD  20170426  151213  SST630      REL-V7R1M0  5770-WDS
     A                                      WINDOW(S1WIN)
     A                                      CA03
     A                                  2  4' Slette post '
     A                                      DSPATR(RI)
     A                                 14  4'F3 tilbake   '
     A                                  4  4'Rutine       '
     A                                  4 18'.'
     A                                  5  4'Tekst'
     A                                  5 10'.'
     A                                  5 12'.'
     A                                  5 14'.'
     A                                  5 16'.'
     A                                  5 18'.'
     A            D2RUTI    R        O  4 20REFFLD(IADRUT)
     A            D2TEXT    R        O  5 20REFFLD(IADRTX)
     A                                  8  4'Denne rutine har en eller flere ti-
     A                                      lgangs-linjer'
     A                                      COLOR(RED)
     A                                  9  4'Skal disse også slettes?'
     A                                      COLOR(RED)
     A                                 10  4'Hvis ikke trykk F3'
     A                                      COLOR(RED)
