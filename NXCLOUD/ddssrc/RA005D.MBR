     A*%%TS  SD  20040217  122636  KOB         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RA005D
     A* Beskrivelse . . : Statuskoder, vedlikehold betalingsvarsel
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  020316  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A          R A2BLD
     A*%%TS  SD  20040217  122636  KOB         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av koder'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5 26'Betalingsvarsel'
     A                                      COLOR(WHT)
     A                                  7  5'Sist kjørt . . . . .:'
     A            A25PDA         6Y 0O  7 27DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  8  5'Antall løpedager . .:'
     A            A25LDG    R        B  8 27REFFLD(RA5LDG)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                  9  5'Purregebyr . . . . .:'
     A            A25GEB    R        B  9 27REFFLD(RA5GEB)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                 10  5'Inkassotekst ved . .:'
     A            A25RTX    R        B 10 27REFFLD(RA5RTX)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10 30'purring'
     A                                 11  5'Giro skrives . . . .:'
     A            A25RPG    R        B 11 27REFFLD(RA5RPG)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      VALUES('G' 'S' 'R')
     A  36                                  ERRMSGID(RMA0590 *LIBL/QMFOKON 36)
7.xx A                                 11 30'G=Gebyr, S=Forfalt saldo+gebyr og-
     A                                       renter,'
7.xx A                                 12 30'R=Gebyr og renter'
     A                                 13  5'Oppdatere regnskap .:'
     A            A25RPO    R        B 13 27REFFLD(RA5RPO)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      VALUES('J' ' ')
     A                                      COLOR(YLW)
     A                                 13 30'(J=Ja)'
     A                                 14  5'Sorter alfabetisk .: '
     A            A25RAF    R        B 14 27REFFLD(RA5RAF)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      VALUES('J' ' ')
     A                                 14 30'(J=Ja)'
     A                                 15  5'Kontaktperson . . .: '
     A            A25KPR    R        B 15 27REFFLD(RA5KPR)
     A                                      DSPATR(UL)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                 16  5'Telefonnummer . . .: '
     A            A25TFN    R        B 16 27REFFLD(RA5TFN)
     A                                      DSPATR(UL)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                 17  5'E-mail. . . . . . .: '
     A            A25EML    R        B 17 27REFFLD(RA5EML)
     A                                      DSPATR(UL)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                 20  5'Sist endret dato. .: '
     A            A25DAT         6Y 0O 20 27COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21  5'Endret av . . . . .:'
     A            A25USR    R        O 21 27REFFLD(RÅEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  5'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 20'F4=Spøring '
     A                                      COLOR(BLU)
