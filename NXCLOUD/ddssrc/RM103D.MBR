     A*%%TS  SD  20070610  164436  KOB550      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RM103D
     A* Beskrivelse . . : Vedlikehold av bruksrett- type
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A*       130516 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20031210  130350  HKO         REL-V5R2M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1TBRT    R        O  8  5REFFLD(RMTBRT)
     A            B1TTXT    R        O  8  9REFFLD(RMTTXT)
     A            B1TRA1    R        O  8 41REFFLD(RMTRA1)
     A                                      EDTCDE(4)
     A            B1TRA2    R        O  8 50REFFLD(RMTRA2)
     A                                      EDTCDE(4)
     A            B1TMVA    R        O  8 61REFFLD(RMTMVA)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20031210  131711  HKO         REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CA06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av bruksrett-type'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  4'Type'
7.xx A                                      COLOR(WHT)
7.xx A                                  4  9'Beskrivelse'
7.xx A                                      COLOR(WHT)
     A                                  4 66'Almenning'
     A                                      COLOR(BLU)
     A            B2TALM    R        O  4 76REFFLD(RMTALM)
     A                                      COLOR(BLU)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  4'Type'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  9'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 40'Rabatt-1'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 49'Rabatt-2'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 58'MVA-kode'
7.xx A**                                7  2'V Type Beskrivelse                -
7.xx A**                                        Rabatt-1 Rabatt-2 MVA-kode'
     A                                      COLOR(WHT)
7.xx A            B2TBRT    R        B  5  5REFFLD(RMTBRT)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(4)
7.xx A            B2TTXT    R        B  5  9REFFLD(RMTTXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20031210  120039  HKO         REL-V5R2M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20031210  115619  HKO         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av bruksrett-type'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Almenning. . . . . . . . . .'
     A            C1TALM    R        O  5 31REFFLD(RMTALM)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Bruksrett-type . . . . . . .'
     A            C1TBRT    R        B  6 31REFFLD(RMTBRT)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20031210  115244  HKO         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 7 32)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Bruksrett-type finnes fra før.'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20070610  164436  KOB550      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av bruksrett-type'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Allmenning . . . . . . . . .'
     A            C1TALM    R        O  5 31REFFLD(RMTALM)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Bruksrett-type . . . . . . .'
     A            C1TBRT    R        O  6 31REFFLD(RMTBRT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Beskrivelse. . . . . . . . .'
     A            C2TTXT    R        B  8 31REFFLD(RMTTXT)
     A  31                                  ERRMSGID(RMM3100 *LIBL/QMFOKON 31)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 10  2'Rabatt-1 . . . . . . . . . .'
     A            C2TRA1    R        B 10 31REFFLD(RMTRA1)
     A  32                                  ERRMSGID(RMM3110 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 11  2'Rabatt-2 . . . . . . . . . .'
     A            C2TRA2    R        B 11 31REFFLD(RMTRA2)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A  30                                  DSPATR(PR)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 13  2'MVA-kode . . . . . . . . . .'
     A            C2TMVA    R        B 13 31REFFLD(RMTMVA)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 14  2'Beløpsgrensekode . . . . . .'
     A            C2TSKO    R        B 14 31REFFLD(RMTSKO)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(RZ)
     A                                      OVRDTA
     A                                      EDTCDE(1)
     A                                      VALUES(0 1)
     A                                 16  2'Avdeling . . . . . . . . . .'
     A            C2TAVD    R        B 16 31REFFLD(RMTAVD)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 17  2'Konto. . . . . . . . . . . .'
     A            C2TKTO    R        B 17 31REFFLD(RMTKTO)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 18  2'Bærer. . . . . . . . . . . .'
     A            C2TSPS    R        B 18 31REFFLD(RMTSPS)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A            C2TODA         6Y 0O 23 49COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 58'/'
     A            C2TEDA         6Y 0O 23 60COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 69'/'
     A            C2TEUS    R        O 23 71REFFLD(RMTEUS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R D1WIN
     A*%%TS  SD  20031210  115619  HKO         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 16 51)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette bruksrett-type '
     A                                      DSPATR(RI)
     A                                  4  2'Bruksrett-type . .'
     A            D1TBRT    R        O  4 21REFFLD(RMTBRT)
     A                                  5  2'Beskrivelse. . . .'
     A            RMTTXT    R        O  5 21
     A                                 15  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20031210  115619  HKO         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 16 51)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Bruksrett-type . .'
     A            K1TBRT    R        B  4 21REFFLD(RMTBRT)
     A  31                                  ERRMSGID(RMM3010 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RMM3030 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Bruksrett-type . .'
     A            RMTBRT    R        O  8 21OVRDTA
     A                                  9  2'Beskrivelse. . . .'
     A            RMTTXT    R        O  9 21OVRDTA
     A                                 15  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
