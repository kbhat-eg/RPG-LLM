     A*%%TS  SD  20090528  095445  ASP_X50     REL-V5R3M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : VP600D
     A* Beskrivelse . . : Utskrift av prisbok
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  290916  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  20090528  095445  ASP_X50     REL-V5R3M0  5722-WDS
     A*                                     WINDOW(*DFT 21 67)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Utskrift av prisbok '
     A                                      DSPATR(RI)
7.xx A                                  4  2'Varesortiment. . +'
     A            B1VSOR    R        B  4 21REFFLD(VCVSOR)
     A  31                                  ERRMSGID(VMP0310 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMC0020 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A            B1BESK    R        O  4 32REFFLD(VCBESK)
     A                                      COLOR(YLW)
7.xx A                                  5  2'Overgruppe . . . +'
     A            B1OGRP    R        B  5 21REFFLD(VDOGRP)
     A  33                                  ERRMSGID(VMG1020 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A            B1OTXT    R        O  5 32REFFLD(VGOTXT)
     A                                      COLOR(YLW)
7.xx A                                  6  2'Hovedgruppe. . . +'
     A            B1HGRP    R        B  6 21REFFLD(VDHGRP)
     A  34                                  ERRMSGID(VMG2020 *LIBL/QMFOFAK 34)
     A                                      COLOR(YLW)
     A            B1HTXT    R        O  6 32REFFLD(VGHTXT)
     A                                      COLOR(YLW)
7.xx A                                  7  2'Undergruppe. . . +'
     A            B1UGRP    R        B  7 21REFFLD(VDUGRP)
     A  35                                  ERRMSGID(VMG0020 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A            B1UTXT    R        O  7 32REFFLD(VGUTXT)
     A                                      COLOR(YLW)
7.xx A                                  8  2'Leverandør . . . +'
     A            B1LDOR    R        B  8 21REFFLD(VDLDOR)
     A  36                                  ERRMSGID(FRL0020 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A            B1LNAV        30A  O  8 32COLOR(YLW)
7.xx A                                 10  2'Leveringstype. . +'
     A            B1LETY    R        B 10 21REFFLD(VYLETY)
     A  37                                  ERRMSGID(VMY0020 *LIBL/QMFOFAK 37)
     A                                      COLOR(YLW)
     A            B1LBES    R        O 10 32REFFLD(VYBESK)
     A                                      COLOR(YLW)
7.xx A                                 11  2'Lager  . . . . . +'
     A            B1LAGE    R        B 11 21REFFLD(VLLAGE)
     A  39                                  ERRMSGID(FMA0160 *LIBL/QMFOFAK 39)
     A                                      COLOR(YLW)
     A            B1LANA    R        O 11 32REFFLD(VYBESK)
     A                                 12  2'Prisdato . . . . .'
     A            B1DATO         6Y 0B 12 21COLOR(YLW)
     A  38                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 38)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                 13  2'Overstyringer:'
     A                                      DSPATR(UL)
7.xx A                                 14  2'Skaffevarer. . . .'
     A            B1SVAL    R        B 14 21REFFLD(FÅJANE)
     A                                      COLOR(YLW)
     A                                 14 32'1=Ja,0=Nei'
7.xx A                                 15  2'Tilbudspriser. . .'
     A            B1TVAL    R        B 15 21REFFLD(FÅJANE)
     A                                      COLOR(YLW)
     A                                 15 32'1=Ja,0=Nei'
7.xx A                                 16  2'Rabattgrupper. . .'
     A            B1RVAL    R        B 16 21REFFLD(FÅJANE)
     A                                      COLOR(YLW)
     A                                 16 32'1=Ja,0=Nei'
7.xx A                                 17  2'Varetekst-2. . . .'
     A            B12VAL    R        B 17 21REFFLD(FÅJANE)
     A                                      COLOR(YLW)
     A                                 17 32'1=Ja,0=Nei'
7.xx A                                 18  2'Utelate lev. . . .'
     A            B1LVAL    R        B 18 21REFFLD(FÅJANE)
     A                                      COLOR(YLW)
     A                                 18 32'1=Ja,0=Nei'
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
