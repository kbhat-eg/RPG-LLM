     A*%%TS  SD  20140211  115546  BOFBMP      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASVADM
     A* Format. . . . . : JV100D
     A* Beskrivelse . . : Vedlikehold av vareinformasjon
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 030616 bsa Endringer relatert til ny GUI
8.01 A* 160624 bof Utvidet med valg 20 = Egenskaper
     A*
     A*---------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20140108  125710  BOFBMP      REL-V7R1M0  5770-WDS
7.xx A            B1VALG         2Y 0B 13  2COLOR(YLW)
     A                                      VALUES(0 2 3 4 5 7 8 9 10 11 12 13 -
8.01 A                                      14 15 16 17 18 19 20)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
     A                                      EDTCDE(4)
7.xx A            B1TEK1    R        O 13  5REFFLD(JVTEK1)
6.37 A  27                                  COLOR(BLU)
6.37 A  28                                  COLOR(YLW)
7.xx A            B1VARE    R   -7   O 13 41REFFLD(JVVARE)
6.37 A  27                                  COLOR(BLU)
6.37 A  28                                  COLOR(YLW)
7.xx A            B1NOBB    R        O 13 50REFFLD(JVNOBB)
6.37 A  27                                  COLOR(BLU)
6.37 A  28                                  COLOR(YLW)
7.xx A            B1LVAR    R   -5   O 13 59REFFLD(JVLVAR)
6.37 A  27                                  COLOR(BLU)
6.37 A  28                                  COLOR(YLW)
7.xx A            B1NAVN         6A  O 13 75
6.37 A  27                                  COLOR(BLU)
6.37 A  28                                  COLOR(YLW)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20140211  115546  BOFBMP      REL-V7R1M0  5770-WDS
7.xx A                                      SFLSIZ(0010)
7.xx A                                      SFLPAG(0009)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CA09
     A                                      CA12
     A                                      CA13
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av vareinformasjon'
     A                                      COLOR(WHT)
     A                                  2 38'JV100B2'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Varegruppe . . +'
     A                                      COLOR(BLU)
     A            B2OGRP    R        B  4 19REFFLD(JVOGRP)
     A  31                                  ERRMSGID(JVG0100 *LIBL/QMFVADM 31)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  4 22'+'
7.xx A                                      COLOR(BLU)
7.xx A            B2HGRP    R        B  4 24REFFLD(JVHGRP)
     A  32                                  ERRMSGID(JVG0110 *LIBL/QMFVADM 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  4 27'+'
7.xx A                                      COLOR(BLU)
7.xx A            B2UGRP    R        B  4 29REFFLD(JVUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  5  2'Søketekst. . . .'
     A                                      COLOR(BLU)
     A            B2STEK    R        B  5 19REFFLD(JSSTEK)
     A  22                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Frisøk . . . . .'
     A                                      COLOR(BLU)
     A            B2FTEK    R        B  6 19REFFLD(JSSTEK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7  5'Varetekst                       '
7.xx A                                      COLOR(WHT)
7.xx A                                  7 42'Varenr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 50'Nobb-nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 59'Lev. varenr'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  2' 2=Endre  3=Kopier  4=Slett  5=-
     A                                      Vis  7=Priser  8=Enh.  9=Søketekst -
     A                                                '
     A                                      COLOR(BLU)
     A                                 10  2'10=Betingelser  11=Poduktinfo  12=-
6.35 A                                      Kampanjer  13=Alt.Vare  14=Avgif-
     A                                      t  15=NRF'
     A                                      COLOR(BLU)
7.xx A                                 11  2'17=Fritekst  18=BestårAv  19=Vare--
8.01 A                                      Lev.  20=Egenskaper     '
     A                                      COLOR(BLU)
7.xx A  29                             11 61'16=BM info'
     A                                      COLOR(BLU)
7.xx A                                 12  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                 12  5'Varetekst                       '
7.xx A                                      COLOR(WHT)
7.xx A                                 12 42'Varenr'
7.xx A                                      COLOR(WHT)
7.xx A                                 12 50'Nobb-nr'
7.xx A                                      COLOR(WHT)
7.xx A                                 12 59'Lev. varenr'
7.xx A                                      COLOR(WHT)
7.xx A                                 12 75'Lev.'
7.xx A                                      COLOR(WHT)
7.xx A**                               10  2'V  Varetekst                      -
7.xx A**                                          Varenr  Nobb-nr  Lev. varenr -
7.xx A**                                        Lev.'
7.xx A**                                    COLOR(WHT)
7.xx A            B2TEK1    R        B  8  5REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2VARE         8Y 0B  8 41COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2NOBB    R        B  8 50REFFLD(JVNOBB)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2LVAR    R   -5   B  8 59REFFLD(JVLVAR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20020819  142213  HKO         REL-V5R1M0  5722-WDS
6.37 A                                 23  2'F3=Gå tilbake F4=Spørring F5=Forny-
6.37 A                                       F6=Opprett F9=Gjenta søk F13=Info'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  19990823  150952  HKO         REL-V4R2M0  5769-PW1
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av vareinformasjon'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Varenummer . . . . . . . . .'
     A            C1VARE         8Y 0B  5 31COLOR(YLW)
     A  34                                  ERRMSGID(JMV0020 *LIBL/QMFVADM 34)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  5 49'Blank = neste ledige varenummer'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  19990112  100801  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Vare finnes fra før.'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  19990823  150746  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 15 53)
     A                                      CA03
     A                                      CA12
6.35 A                                      CA18
     A                                  2  2' Slette vare '
     A                                      DSPATR(RI)
     A                                  4  2'Varenummer. . .'
     A            D1VARE    R        O  4 18REFFLD(JVVARE)
     A                                  5  2'Varegruppe. . .'
     A            JVOGRP    R        O  5 18
     A            JVHGRP    R        O  5 21
     A            JVUGRP    R        O  5 24
     A                                  6  2'Varetekst 1 . .'
     A            JVTEK1    R        O  6 18
     A                                  7  2'Varetekst 2 . .'
     A            JVTEK2    R        O  7 18
     A                                  8  2'Leverandør. . .'
     A            JVLDOR    R        O  8 18
     A                                  9  2'Varenummer lev.'
     A            JVLVAR    R        O  9 18
     A                                 10  2'NOBB-nummer . .'
     A            JVNOBB    R        O 10 18
     A                                 11  2'Gml. EAN-nr . .'
     A            JVEAN1    R        O 11 18
6.35 A                                 14  2'F3=Gå tilbake  F18=NOBB info'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  19990823  185544  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 15 53)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            K1VARE    R        H      REFFLD(JVVARE)
     A                                  2  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  4  2'Varenummer. . .'
     A            JVVARE    R        O  4 18OVRDTA
     A                                  5  2'Varetekst 1 . .'
     A            JVTEK1    R        O  5 18OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
6.37 A          R B13WIN
6.37 A*%%TS  SD  20041125  103649  GRO530      REL-V5R2M0  5722-WDS
6.37 A                                      WINDOW(*DFT 15 55)
6.37 A                                      CA03
6.37 A                                      CA12
6.37 A                                      PUTOVR
6.37 A                                  2  2' Info '
6.37 A                                      DSPATR(RI)
6.37 A                                  4  2'Gul farge = Varen er en logistikkv-
6.37 A                                      are'
6.37 A                                      COLOR(YLW)
6.37 A                                  5  2'Blå farge = Varen er en undervare -
6.37 A                                       av en logistikkvare'
6.37 A                                      COLOR(BLU)
6.37 A                                 14  2'F3=Tilbake'
6.37 A                                      COLOR(BLU)
