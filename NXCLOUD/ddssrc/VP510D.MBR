     A*%%TS  SD  20081105  122521  BOF570      REL-V5R4M0  5722-WDS
     A*
8.01 A*PRG2 150622 bsa Utvidet prisgruppe fra 1 til 2 posisjoner
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20081105  122521  BOF570      REL-V5R4M0  5722-WDS
     A            B1VALG    R        B  8  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 5)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
8.01 A            B1PRGR    R        O  8  4REFFLD(VPPRGR)
     A            B1ENHE    R        O  8  7REFFLD(VPENHE)
     A            B1LETY    R        O  8 12REFFLD(VPLETY)
     A            B1PDAT         6Y 0O  8 15EDTWRD('  .  .  ')
     A            B1SAPR    R        O  8 25REFFLD(VPSAPR)
     A            B1KOPR    R        O  8 37REFFLD(VPKOPR)
     A            B1INPR    R        O  8 49REFFLD(VPINPR)
     A            B1BUPR    R        O  8 61REFFLD(VPBUPR)
     A            B1DEKN         5Y 2O  8 73EDTCDE(M)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20081105  122521  BOF570      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vise priser'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Vare . . . . . . . .'
     A                                      COLOR(BLU)
     A            B2VARE    R   -7   O  4 23REFFLD(VPVARE)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A            B2TEK1    R        O  4 32REFFLD(VVTEK1)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A                                  5  2'Leverandør . . . . .'
     A                                      COLOR(BLU)
     A            B2LDOR    R        O  5 23REFFLD(VPLDOR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A            B2LNAV    R        O  5 32REFFLD(VVTEK1)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A                                  6  2'5=Vis'
     A                                      COLOR(BLU)
     A                                  7  2'V Pg Enh  L    Dato     Salgspris -
     A                                         Kostpris  Innkj.pris  Butikkpris-
     A                                          DG'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  19990601  192528  HKO         REL-V4R2M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20020226  104245  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vise prisinformasjon'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Varenummer . . . . . . . . .'
     A            C2VARE    R   -7   O  5 31REFFLD(VPVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2TEK1    R        O  5 41REFFLD(VVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Prisgruppe . . . . . . . . .'
     A            C2PRGR    R        O  6 31REFFLD(VPPRGR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2PTXT    R        O  6 41REFFLD(VAETXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  7  2'Enhet. . . . . . . . . . . .'
     A            C2ENHE    R        O  7 31REFFLD(VVENHE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2ETXT    R        O  7 41REFFLD(VAETXT)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Leveringstype. . . . . . . .'
     A            C2LETY    R        O  8 31REFFLD(VPLETY)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2LTXT    R        O  8 41REFFLD(VYBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Dato . . . . . . . . . . . .'
     A            C2PDAT         6Y 0O 10 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 12  2'Dekningsgrad . . . . . . . .'
     A            C2DEKN         5Y 2O 12 31COLOR(YLW)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A                                 13  2'Salgspris. . . . . . . . . .'
     A            C2SAPR    R        O 13 31REFFLD(VPSAPR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 14  2'Kostpris . . . . . . . . . .'
     A            C2KOPR    R        O 14 31REFFLD(VPKOPR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 15  2'Innkjøpspris . . . . . . . .'
     A            C2INPR    R        O 15 31REFFLD(VPINPR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 16  2'Butikkpris . . . . . . . . .'
     A            C2BUPR    R        O 16 31REFFLD(VPBUPR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 46'Sist endret'
     A            C2EDAT         6Y 0O 23 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 23 68'/'
     A            C2EUSR    R        O 23 70REFFLD(VPEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
