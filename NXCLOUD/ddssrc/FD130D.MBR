6.20  * R6.20 110211 NHO  Prosjekt og aktivitet fjernet fra bildet.    ttet
     A*%%TS  SD  20051017  091506  HKO530      REL-V5R3M0  5722-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R C1WIN
     A*%%TS  SD  20051017  091506  HKO530      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 22 67)
     A                                      CF02
     A                                      CA03
     A                                      CF04
     A                                      CF06
     A                                      CF07
     A                                      CF08
     A                                      CF09
     A                                      CF10
     A                                      CA12
     A                                      CA23
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            C1ALME    R        H      REFFLD(FDALME)
     A            C1OMVA    R        H      REFFLD(FDOMVA)
     A                                  2  2' Registrering av varelinje fra sta-
     A                                      tistikk '
     A                                      DSPATR(RI)
     A                                  4  2'Antall        Enh  Varenr  T  Nobb-
     A                                      nr'
     A                                      COLOR(WHT)
     A            C1ODEK    R        O  4 61REFFLD(FDPASL)
     A                                      COLOR(BLU)
     A N86                                  DSPATR(ND)
     A                                      OVRDTA
     A            C1ANT1    R        B  5  2REFFLD(FDANTA)
     A  33                                  ERRMSGID(FMD0150 *LIBL/QMFOFAK 33)
     A  49                                  ERRMSGID(FM00630 *LIBL/QMFOFAK 49)
     A                                      COLOR(YLW)
     A                                      DSPATR(PC)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A            C1ENH1    R        B  5 16REFFLD(FDENH1)
     A  31                                  ERRMSGID(VMA1010 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(VMA1020 *LIBL/QMFOFAK 32)
     A  81                                  COLOR(YLW)
     A  81                                  DSPATR(UL)
     A N81                                  DSPATR(PR)
     A                                      OVRDTA
     A            C1VARE    R   -7   O  5 20REFFLD(FDVARE)
     A                                      OVRDTA
     A            C1VTYP    R        O  5 29REFFLD(VVVTYP)
     A                                      OVRDTA
     A            C1NOBB    R        O  5 31REFFLD(FDNOBB)
     A                                      OVRDTA
     A            C1NTSU        11Y 2O  5 44COLOR(BLU)
     A                                      EDTCDE(4)
     A                                      OVRDTA
     A            C1NTPR    R        O  5 57REFFLD(FDSAPR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A            C1ANT2    R        B  6  2REFFLD(FDANTA)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A  82                                  DSPATR(ND)
     A  82                                  DSPATR(PR)
     A                                      OVRDTA
     A            C1ENH2    R        O  6 16REFFLD(FDENH1)
     A  82                                  DSPATR(ND)
     A                                      OVRDTA
     A            C1NISU        11Y 2O  6 44COLOR(BLU)
     A                                      EDTCDE(4)
     A                                      OVRDTA
     A            C1NTIM    R        O  6 57REFFLD(FDSAPR)
     A                                      COLOR(BLU)
     A                                      OVRDTA
     A            C1ANT3    R        B  7  2REFFLD(FDANTA)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A  83                                  DSPATR(ND)
     A  83                                  DSPATR(PR)
     A                                      OVRDTA
     A            C1ENH3    R        O  7 16REFFLD(FDENH1)
     A  83                                  DSPATR(ND)
     A                                      OVRDTA
     A                                  8 21'Salgspris  Rab-1   Rab-2  L K   Ko-
     A                                      stpris LG LT'
     A                                      COLOR(WHT)
     A            C1ANTA    R        O  9  2REFFLD(FDANTA)
     A                                      COLOR(YLW)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A            C1ENHE    R        O  9 16REFFLD(FDENH1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1SAPR         9Y 2B  9 20EDTWRD('     0 ,  ')
     A  39                                  ERRMSGID(FMD0170 *LIBL/QMFOFAK 39)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1RAB1    R        B  9 31REFFLD(FDRAB1)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A            C1RAB2    R        B  9 39REFFLD(FDRAB2)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(M)
     A                                      OVRDTA
     A            C1LETY    R        B  9 47REFFLD(FDLETY)
     A  34                                  ERRMSGID(VMY0020 *LIBL/QMFOFAK 34)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1KPRI    R        B  9 49REFFLD(FDKPRI)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1KOPR    R        B  9 51REFFLD(FDKOPR)
     A                                      COLOR(YLW)
7.01 A*                                      EDTWRD('       ,  ')
7.01 A                                      EDTWRD('     0 ,  ')
     A                                      CHECK(RB)
     A N86                                  DSPATR(ND)
     A N86                                  DSPATR(PR)
     A                                      OVRDTA
     A            C1LAGE    R        B  9 62REFFLD(FDLAGE)
     A  38                                  ERRMSGID(FMA0160 *LIBL/QMFOFAK 38)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1LTYP    R        B  9 65REFFLD(FDLTYP)
     A  42                                  ERRMSGID(FMD0100 *LIBL/QMFOFAK 42)
     A  43                                  ERRMSGID(FMD0110 *LIBL/QMFOFAK 43)
     A  44                                  ERRMSGID(FMD0120 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1TEK1    R        B 10 20REFFLD(FDTEK1)
     A  35                                  ERRMSGID(FMD0180 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A            C1BRTY    R        O 10 59REFFLD(FDBRTY)
     A            C1BRR1    R        O 10 61REFFLD(FDBRR1)
     A                                      EDTCDE(4)
     A            C1TEK2    R        B 11 20REFFLD(FDTEK2)
     A                                      CHECK(LC)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1BRR2    R        O 11 61REFFLD(FDBRR2)
     A                                      EDTCDE(4)
     A                                 13  2'Salgspris inkl. .'
     A            C1SAIM         9Y 2B 13 20EDTWRD('       ,  ')
     A  36                                  ERRMSGID(FMD0210 *LIBL/QMFOFAK 36)
     A                                      CHECK(RB)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 13 37'Lagerbeholdn.'
     A N87                                  DSPATR(ND)
     A            C1BEHL    R        O 13 51REFFLD(VLBEHL)
     A N87                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 14  2'Dekningsgrad (DG)'
     A            C1DEKN    R        B 14 20REFFLD(FDPASL)
     A  37                                  ERRMSGID(FMD0220 *LIBL/QMFOFAK 37)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTWRD('   ,  ')
     A N86                                  DSPATR(ND)
     A N86                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 14 37'I bestilling.'
     A N87                                  DSPATR(ND)
     A            C1BBES    R        O 14 51REFFLD(VLBBES)
     A N87                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 15  2'Påslag. . . . . .'
     A            C1PASL         5Y 2B 15 20COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTWRD('   ,  ')
     A N86                                  DSPATR(ND)
     A N86                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 15 37'I ordre . . .'
     A N87                                  DSPATR(ND)
     A            C1PAKK    R        O 15 51REFFLD(VLBEHL)
     A                                      DSPATR(UL)
     A N87                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 16  2'Lassnummer. . . .'
     A            C1LASS    R        B 16 20REFFLD(FDLASS)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 16 37'Disp.beholdn.'
     A N87                                  DSPATR(ND)
     A            C1DISP    R        O 16 51REFFLD(VLBEHL)
     A N87                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 17  2'Lev.dato. . . . .'
     A            C1LDAT         6Y 0B 17 20COLOR(YLW)
     A  40                                  ERRMSGID(FM00050 *LIBL/QMFOFAK 40)
     A  41                                  ERRMSGID(FMD0200 *LIBL/QMFOFAK 41)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 17 37'Modulnr . . .'
     A            C1NMOD    R        O 17 51REFFLD(VVNMOD)
     A                                      OVRDTA
6.20 A**                               18  2'Prosjekt/akt. . .'
6.20 A**          C1PROJ    R        B 18 20REFFLD(FDPROJ)
6.20 A            C1PROJ         6A  H
6.20 A**45                                  ERRMSGID(FRH0220 *LIBL/QMFOFAK 45)
6.20 A**                                    CHECK(RB)
6.20 A**                                    COLOR(YLW)
6.20 A**                                    OVRDTA
6.20 A**          C1AKTI    R        B 18 27REFFLD(FDAKTI)
6.20 A            C1AKTI         3A  H
6.20 A**46                                  ERRMSGID(FMA0220 *LIBL/QMFOFAK 46)
6.20 A**                                    COLOR(YLW)
6.20 A**                                    OVRDTA
     A                                 18 37'GTIN-nr . . .'
     A            C1EANN    R        O 18 51REFFLD(VXEANN)
     A                                      OVRDTA
     A                                 19  2'Privat. . . . . .'
     A N85                                  DSPATR(ND)
     A            C1PRIV    R        B 19 20REFFLD(FDPRIV)
     A N85                                  DSPATR(ND)
     A N85                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 19 22'1=Ja,0=Nei'
     A N85                                  DSPATR(ND)
     A                                 19 37'Utgår dato. .'
     A N84                                  DSPATR(ND)
     A            C1UDAT         6Y 0O 19 51EDTWRD('  .  .  ')
     A N84                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 20  2'F2=Kostpris     F3=Gå tilbake  F4=-
     A                                      Spørring  F6=Lengde  F7=Pakke'
     A                                      COLOR(BLU)
     A                                 21  2'F8=Produktinfo  F9=Lagerinfo.  F10-
     A                                      =Notat    F23=Vis kost         '
     A                                      COLOR(BLU)
     A          R C1MSG
     A*%%TS  SD  20020530  172007  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 11 45)
     A                                      CA03
     A                                      CA12
     A                                  3  2'Salgspris er lavere enn kostpris !'
     A                                  5  2'Bekreft salget med Enter eller <OK-
     A                                      >, eller'
     A                                  6  2'gå tilbake til registrering ved å -
     A                                      velge'
     A                                  7  2'<Gå tilbake>'
     A                                 10  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
