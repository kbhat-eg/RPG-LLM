     A*%%TS  SD  20120110  152814  BOF620      REL-V7R1M0  5770-WDS
     A*%%EC
     A*K00       220720 ask  Nytt skjermbilde
     A*800       130122 ask  Lagt inn mulighet for å oppdatere periode
     A*800       041224 ask  Lagt inn kontroll på avvikende forfallsdatoer
     A*800       NXS-20549
     A*-----------------------------------------------------------------
     A                                      DSPSIZ(27 132 *WIDE)
     A                                      PRINT
     A*                                     HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20070328  124845  AMD550      REL-V5R4M0  5722-WDS
     A            B1TMST          Z  H
     A            B1VALG         1Y 0B  7  2CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A                                      VALUES(0 4 5)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A            B1TLIN         8Y 0O  7  4EDTCDE(Z)
     A            B1BILN         8Y 0O  7 13EDTCDE(Z)
     A            B1BDAT         6Y 0O  7 22EDTWRD('  .  .  ')
     A            B1BIKO         2Y 0O  7 33EDTCDE(Z)
     A            B1BTEK        20A  O  7 36
     A            B1BELP        10Y 2O  7 57EDTWRD('      0 ,  ')
     A            B1MVAG        10Y 2O  7 70EDTWRD('      0 ,  ')
     A            B1DAVD         4Y 0O  7 83EDTCDE(Z)
     A            B1DKTO         6Y 0O  7 88EDTCDE(Z)
     A            B1DSPS         6Y 0O  7 95EDTCDE(Z)
     A            B1DMVA         1A  O  7102
     A            B1KAVD         4Y 0O  7104EDTCDE(Z)
     A            B1KKTO         6Y 0O  7109EDTCDE(Z)
     A            B1KSPS         6Y 0O  7116EDTCDE(Z)
     A            B1KMVA         1A  O  7123
     A            B1RESK         6Y 0O  7125EDTCDE(Z)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20070328  151100  AMD550      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0019)
     A                                      SFLPAG(0018)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'NEXSTEP'
     A                                      COLOR(WHT)
     A                                  1118'Kl. :'
     A                                      COLOR(WHT)
     A                                  1124TIME
     A                                      COLOR(WHT)
     A                                  2  2'Transaksjonslogg økonomi - linjer -
     A                                               '
     A                                      COLOR(WHT)
     A                                  2 38'RF501Z2'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2118'Dato:'
     A                                      COLOR(WHT)
     A                                  2124DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------'
     A                                      COLOR(WHT)
     A                                  4  2' 5=Vis detaljer  4=Slett linjer'
     A                                      COLOR(BLU)
     A            B2TYPE         4A  O  4 40
     A            B2TDAT         8A  O  4 46
     A            B2TTID         8A  O  4 56
     A                                  6  2'V'
     A                                      COLOR(WHT)
     A                                  6  4'Tlin'
     A                                      COLOR(WHT)
     A                                  6 13'Bil.nr'
     A                                      COLOR(WHT)
     A                                  6 22'Bil.dat'
     A                                      COLOR(WHT)
     A                                  6 33'Bk'
     A                                      COLOR(WHT)
     A                                  6 36'Bilagstekst'
     A                                      COLOR(WHT)
     A                                  6 58'Beløp'
     A                                      COLOR(WHT)
     A                                  6 71'Mva.grlag'
     A                                      COLOR(WHT)
     A                                  6 83'Davd'
     A                                      COLOR(WHT)
     A                                  6 88'Dkonto'
     A                                      COLOR(WHT)
     A                                  6 95'Dspes'
     A                                      COLOR(WHT)
     A                                  6102'M'
     A                                      COLOR(WHT)
     A                                  6104'Kavd'
     A                                      COLOR(WHT)
     A                                  6109'Kkonto'
     A                                      COLOR(WHT)
     A                                  6116'Kspes'
     A                                      COLOR(WHT)
     A                                  6123'M'
     A                                      COLOR(WHT)
     A                                  6125'Resk'
     A                                      COLOR(WHT)
     A          R B2CMD
     A*%%TS  SD  20050621  150935  ASK530      REL-V5R2M0  5722-WDS
     A                                 26  9'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B3WIN
     A*%%TS  SD  20051020  151154  AMD550      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 25 105)
     A                                      CA03
     A                                      CA09
     A                                      CA12
     A                                      PUTOVR
     A                                  1  2'Linje detaljer'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  1 89'RF501B3'
     A                                      COLOR(WHT)
     A                                  3  2'Linjenummer'
     A                                      COLOR(WHT)
     A            B3TLIN         8Y 0O  3 15EDTCDE(Z)
     A                                  3 58'Transtype'
     A                                      COLOR(WHT)
     A            B3TYPE         4A  O  3 69
     A                                  4  2'Firma'
     A                                      COLOR(WHT)
     A            B3FIRM         3Y 0O  4 10EDTCDE(Z)
     A                                  3 24'Dato'
     A                                      COLOR(WHT)
     A            B3TDAT         8A  O  3 29
     A                                  3 42'Tid'
     A                                      COLOR(WHT)
     A            B3TTID         8A  O  3 46
     A                                  5  2'Periode'
     A                                      COLOR(WHT)
8.02 A            B3PERI         4Y 0B  5 10EDTCDE(Z)
8.02 A                                      OVRDTA
8.02 A                                      OVRATR
8.02 A                                      COLOR(YLW)
8.02 A  30                                  DSPATR(PR)
8.02 A N30                                  DSPATR(UL)
     A                                  5 16'Regnskapsbunt'
     A                                      COLOR(WHT)
     A            B3BUNT         5Y 0O  5 30EDTCDE(Z)
     A                                  5 39'Beløp'
     A                                      COLOR(WHT)
     A            B3BELP        10Y 2B  5 45EDTWRD('      0 ,  ')
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A N30                                  DSPATR(UL)
     A                                  5 59'Mva.grlg.'
     A                                      COLOR(WHT)
     A            B3MVAG        10Y 2B  5 69EDTWRD('      0 ,  ')
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A N30                                  DSPATR(UL)
     A                                  7  2'Bilagsnummer'
     A                                      COLOR(WHT)
     A            B3BINR         8Y 0O  7 15EDTCDE(Z)
     A                                  7 25'Bilagslinje'
     A                                      COLOR(WHT)
     A            B3BILI         4Y 0O  7 37EDTCDE(Z)
     A                                  7 43'Bilagsdato'
     A                                      COLOR(WHT)
     A            B3BDAT         6Y 0B  7 54EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A N30                                  DSPATR(UL)
     A                                  7 64'Forfallsdato'
     A                                      COLOR(WHT)
     A            B3FDAT         6Y 0B  7 77EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A N30                                  DSPATR(UL)
     A                                  8  2'Bilagskode'
     A                                      COLOR(WHT)
     A            B3BKOD         2Y 0B  8 13EDTCDE(Z)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A N30                                  DSPATR(UL)
     A                                  8 25'Bilagstekst'
     A                                      COLOR(WHT)
     A            B3BTEK        20A  O  8 37
     A                                 10  2'Debetavd'
     A                                      COLOR(WHT)
     A            B3DAVD         4Y 0B 10 13EDTCDE(Z)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A N30                                  DSPATR(UL)
     A                                 10 20'Debet konto'
     A                                      COLOR(WHT)
     A            B3DKTO         6Y 0B 10 34EDTCDE(Z)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A N30                                  DSPATR(UL)
     A                                 10 45'Debet spesifikasjon'
     A                                      COLOR(WHT)
     A            B3DSPS         6Y 0B 10 67EDTCDE(Z)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A N30                                  DSPATR(UL)
     A                                 10 77'Debet mvakode'
     A                                      COLOR(WHT)
     A            B3DMVA         1A  B 10 93
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A N30                                  DSPATR(UL)
     A                                 10 96'(k)'
     A                                 11  2'Kredit avd'
     A                                      COLOR(WHT)
     A            B3KAVD         4Y 0B 11 13EDTCDE(Z)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A N30                                  DSPATR(UL)
     A                                 11 20'Kredit konto'
     A                                      COLOR(WHT)
     A            B3KKTO         6Y 0B 11 34EDTCDE(Z)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A N30                                  DSPATR(UL)
     A                                 11 45'Kredit spesifikasjon'
     A                                      COLOR(WHT)
     A            B3KSPS         6Y 0B 11 67EDTCDE(Z)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A N30                                  DSPATR(UL)
     A                                 11 77'Kredit mvakode'
     A                                      COLOR(WHT)
     A            B3KMVA         1A  B 11 93
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A N30                                  DSPATR(UL)
     A                                 11 96'(k)'
     A                                 13  2'Prosjekt'
     A                                      COLOR(WHT)
     A            B3PROS         6A  B 13 11
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A N30                                  DSPATR(UL)
     A                                 13 20'Aktivitet'
     A                                      COLOR(WHT)
     A            B3AKTI         6A  B 13 30
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A N30                                  DSPATR(UL)
     A                                 13 45'Under prosjekt'
     A                                      COLOR(WHT)
     A            B3UPRO         6A  B 13 60
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A N30                                  DSPATR(UL)
     A                                 13 77'Arbeidsordre'
     A                                      COLOR(WHT)
     A            B3ARBO         6A  B 13 90
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A N30                                  DSPATR(UL)
     A                                 14  2'Mengdekode'
     A                                      COLOR(WHT)
     A            B3MKOD         2Y 0O 14 13EDTCDE(Z)
     A                                 14 20'Mengde'
     A                                      COLOR(WHT)
     A            B3MENG         9Y 2O 14 29EDTWRD('     0 ,  ')
     A                                 16  2'Kreditreferanse'
     A                                      COLOR(WHT)
     A            B3KREF         8Y 0B 16 18EDTCDE(Z)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A N30                                  DSPATR(UL)
     A                                 16 33'Reskontronummer'
     A                                      COLOR(WHT)
     A            B3RESK         6Y 0B 16 49EDTCDE(Z)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A N30                                  DSPATR(UL)
     A                                 18  2'Lang kid'
     A                                      COLOR(WHT)
     A            B3LKID        25A  O 18 11
     A                                 19  2'Kort kid'
     A                                      COLOR(WHT)
     A            B3KKID         8Y 0O 19 11EDTCDE(Z)
     A                                 19 20'Ksif'
     A                                      COLOR(WHT)
     A            B3KSIF         1Y 0O 19 25EDTCDE(Z)
     A                                 19 38'Alternativ kid'
     A                                      COLOR(WHT)
     A            B3AKID        25A  O 19 53
     A                                 21  2'Valutakode'
     A                                      COLOR(WHT)
     A            B3VALK         3A  O 21 13
     A                                 21 21'Valutabeløp'
     A                                      COLOR(WHT)
     A            B3VALB        11Y 2O 21 33EDTWRD('       0 ,  ')
     A                                 22  2'Salderingsmerke'
     A                                      COLOR(WHT)
     A            B3SALM         1A  O 22 18
     A                                 22 21'Selgers fakturanummer'
     A                                      COLOR(WHT)
     A            B3SFAK        15A  O 22 43
     A                                 24  1'F3=Gå tilbake  F9=Endre linje'
     A                                      COLOR(BLU)
     A          R B3MSG1
     A*%%TS  SD  20080522  120025  AMD560      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 8 35)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  3  2'Bilag er oppdatert  '
     A                                      COLOR(WHT)
     A                                  4  2'og kan ikke endres   '
     A                                      COLOR(WHT)
     A                                  6  2' '
     A                                      COLOR(WHT)
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B3MSG2
     A*%%TS  SD  20110510  140024  BOF620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 8 35)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  3  2'Bruker har ikke tilgang   '
     A                                      COLOR(WHT)
     A                                  4  2'til å endre bilag.          '
     A                                      COLOR(WHT)
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B4WIN
     A*%%TS  SD  20051020  151154  AMD550      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 25 99)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  1  2'Slette linjer'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  1 89'RF501B4'
     A                                      COLOR(WHT)
     A                                  3  2'Linjenummer'
     A                                      COLOR(WHT)
     A            B4TLIN         8Y 0O  3 15EDTCDE(Z)
     A                                  3 58'Transtype'
     A                                      COLOR(WHT)
     A            B4TYPE         4A  O  3 69
     A                                  4  2'Firma'
     A                                      COLOR(WHT)
     A            B4FIRM         3Y 0O  4 10EDTCDE(Z)
     A                                  3 24'Dato'
     A                                      COLOR(WHT)
     A            B4TDAT         8A  O  3 29
     A                                  3 42'Tid'
     A                                      COLOR(WHT)
     A            B4TTID         8A  O  3 46
     A                                  5  2'Periode'
     A                                      COLOR(WHT)
     A            B4PERI         4Y 0O  5 10EDTCDE(Z)
     A                                  5 16'Regnskapsbunt'
     A                                      COLOR(WHT)

     A            B4BUNT         5Y 0O  5 30EDTCDE(Z)
     A                                  5 39'Beløp'
     A                                      COLOR(WHT)
     A            B4BELP        10Y 2O  5 45EDTWRD('      0 ,  ')
     A                                  5 59'Mva.grlg.'
     A                                      COLOR(WHT)
     A            B4MVAG        10Y 2O  5 69EDTWRD('      0 ,  ')
     A                                      COLOR(WHT)
     A                                  7  2'Bilagsnummer'
     A                                      COLOR(WHT)
     A            B4BINR         8Y 0O  7 15EDTCDE(Z)
     A                                  7 25'Bilagslinje'
     A            B4BILI         4Y 0O  7 37EDTCDE(Z)
     A                                  7 43'Bilagsdato'
     A                                      COLOR(WHT)
     A            B4BDAT         6Y 0B  7 54EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                      OVRATR
     A                                  7 64'Forfallsdato'
     A                                      COLOR(WHT)
     A            B4FDAT         6Y 0B  7 77EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                      OVRATR
     A                                  8  2'Bilagskode'
     A                                      COLOR(WHT)
     A            B4BKOD         2Y 0B  8 13EDTCDE(Z)
     A                                      OVRDTA
     A                                      OVRATR
     A                                  8 25'Bilagstekst'
     A                                      COLOR(WHT)
     A            B4BTEK        20A  O  8 37
     A                                 10  2'Debetavd'
     A                                      COLOR(WHT)
     A            B4DAVD         4Y 0O 10 13EDTCDE(Z)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 10 20'Debet konto'
     A                                      COLOR(WHT)
     A            B4DKTO         6Y 0O 10 34EDTCDE(Z)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 10 45'Debet spesifikasjon'
     A                                      COLOR(WHT)
     A            B4DSPS         6Y 0O 10 67EDTCDE(Z)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 10 77'Debet mvakode'
     A                                      COLOR(WHT)
     A            B4DMVA         1A  O 10 93
     A                                      OVRDTA
     A                                      OVRATR
     A                                 11  2'Kredit avd'
     A                                      COLOR(WHT)
     A            B4KAVD         4Y 0O 11 13EDTCDE(Z)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 11 20'Kredit konto'
     A                                      COLOR(WHT)
     A            B4KKTO         6Y 0O 11 34EDTCDE(Z)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 11 45'Kredit spesifikasjon'
     A                                      COLOR(WHT)
     A            B4KSPS         6Y 0O 11 67EDTCDE(Z)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 11 77'Kredit mvakode'
     A                                      COLOR(WHT)
     A            B4KMVA         1A  O 11 93
     A                                      OVRDTA
     A                                      OVRATR
     A                                 13  2'Prosjekt'
     A                                      COLOR(WHT)
     A            B4PROS         6A  O 13 11
     A                                      OVRDTA
     A                                      OVRATR
     A                                 13 20'Aktivitet'
     A                                      COLOR(WHT)
     A            B4AKTI         6A  O 13 30
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(WHT)
     A            B4UPRO         6A  O 13 60
     A                                      OVRDTA
     A                                      OVRATR
     A                                 13 77'Arbeidsordre'
     A                                      COLOR(WHT)
     A            B4ARBO         6A  O 13 90
     A                                      OVRDTA
     A                                      OVRATR
     A                                 14  2'Mengdekode'
     A                                      COLOR(WHT)
     A            B4MKOD         2Y 0O 14 13EDTCDE(Z)
     A                                 14 20'Mengde'
     A            B4MENG         9Y 2O 14 29EDTWRD('     0 ,  ')
     A                                 16  2'Kreditreferanse'
     A                                      COLOR(WHT)
     A            B4KREF         8Y 0O 16 18EDTCDE(Z)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 16 33'Reskontronummer'
     A                                      COLOR(WHT)
     A            B4RESK         6Y 0O 16 49EDTCDE(Z)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 18  2'Lang kid'
     A                                      COLOR(WHT)
     A            B4LKID        25A  O 18 11
     A                                 19  2'Kort kid'
     A                                      COLOR(WHT)
     A            B4KKID         8Y 0O 19 11EDTCDE(Z)
     A                                 19 20'Ksif'
     A                                      COLOR(WHT)
     A            B4KSIF         1Y 0O 19 25EDTCDE(Z)
     A                                 19 38'Alternativ kid'
     A                                      COLOR(WHT)
     A            B4AKID        25A  O 19 53
     A                                 21  2'Valutakode'
     A                                      COLOR(WHT)
     A            B4VALK         3A  O 21 13
     A                                 21 21'Valutabeløp'
     A                                      COLOR(WHT)
     A            B4VALB        11Y 2O 21 33EDTWRD('       0 ,  ')
     A                                 22  2'Salderingsmerke'
     A                                      COLOR(WHT)
     A            B4SALM         1A  O 22 18
     A                                 22 21'Selgers fakturanummer'
     A                                      COLOR(WHT)
     A            B4SFAK        15A  O 22 43
     A                                 24  1'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B4MSG1
     A*%%TS  SD  20080522  120025  AMD560      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 8 35)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  3  2'Bilag er oppdatert  '
     A                                      COLOR(WHT)
     A                                  4  2'og kan ikke slettes  '
     A                                      COLOR(WHT)
     A                                  6  2' '
     A                                      COLOR(WHT)
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B4MSG2
     A*%%TS  SD  20110510  140024  BOF620      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 8 35)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  3  2'Bruker har ikke tilgang   '
     A                                      COLOR(WHT)
     A                                  4  2'til å slette linje          '
     A                                      COLOR(WHT)
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R B4MSG3
     A*%%TS  SD  20080522  120025  AMD560      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 8 35)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  3  2'Det er registrert avvikende'
     A                                      COLOR(WHT)
     A                                  4  2'forfallsdatoer på bilag.'
     A                                      COLOR(WHT)
     A                                  5  2'Bilagsnr'
     A                                      COLOR(WHT)
     A            B4BILN         8A  O  5 12
     A                                      COLOR(RED)
     A                                  6  2' '
     A                                      COLOR(WHT)
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
