     A*%%TS  SD  20050908  141751  GRO540      REL-V5R3M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LO405D
     A* Beskrivelse . . : Sletting av bestillinger (parameter)
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       070905 GRo  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  20050908  141446  GRO540      REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CF10
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Sletting av bestillinger'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Bestillingsdato eldre enn . . . . -
     A                                      .'
     A            B1BDAT         6Y 0B  5 38COLOR(YLW)
     A  31                                  ERRMSGID(LM00050 *LIBL/QMFLAGR 31)
     A  32                                  ERRMSGID(LM00250 *LIBL/QMFLAGR 32)
     A                                      CHECK(RB)
     A                                      EDTWRD('  .  .  ')
     A                                  7  2'Slette kun best. uten varelinjer. -
     A                                      .'
     A            B1UVAR         1Y 0B  7 38CHKMSGID(LRA0010 *LIBL/QMFLAGR)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                  7 42'1=Ja,0=Nei'
     A                                  9  2'Slette kun best. med ordretype. . -
     A                                      .'
     A            B1OTYP    R        B  9 38REFFLD(LOOTYP)
     A                                      COLOR(YLW)
     A  33                                  ERRMSGID(LMO0143 *LIBL/QMFLAGR 33)
     A                                 11  2'Slette kun best. til leverandør . -
     A                                      .'
     A            B1LDOR    R        B 11 38REFFLD(LOLDOR)
     A                                      COLOR(YLW)
     A            B1LNAV        30A  O 11 47
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20050908  141446  GRO540      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 12 52)
     A                                      CA03
     A                                      CF10
     A                                      CA12
     A                                      PUTOVR
     A                                  1  2'ADVARSEL! Sletting av bestillinger'
     A                                      COLOR(RED)
     A                                  3  2'Du har valgt å slette etter følgen-
     A                                      de kriteria :'
     A                                      COLOR(WHT)
     A            D1TEK1        50A  O  4  2OVRDTA
     A            D1TEK2        50A  O  5  2OVRDTA
     A            D1TEK3        50A  O  6  2OVRDTA
     A            D1TEK4        50A  O  7  2OVRDTA
     A                                  9  2'Bekreft at jobben skal kjøres'
     A                                 11  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 11 19'F10=Batch'
     A                                      COLOR(BLU)
