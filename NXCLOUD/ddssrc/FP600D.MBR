     A*%%TS  SD  20081115  074518  BOF570      REL-V5R4M0  5722-WDS
     A*%%EC
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : FP600D
     A* Beskrivelse . . : Utskrift av spesialpriser
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  010416  bsa  Justeringer basert på ny GUI og regler
8.01 A*       100622  BOF  Utvidet prisgr. fra 1 til 2 posisjoner
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  20040218  144718  HKO         REL-V5R2M0  5722-WDS
     A*                                     WINDOW(*DFT 16 50)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Utskrift av spesialpriser '
     A                                      DSPATR(RI)
7.xx A                                  4  2'Prisgruppe  . . +'
     A            B1PRGR    R        B  4 20REFFLD(FPPRGR)
     A  38                                  ERRMSGID(FRA3020 *LIBL/QMFOFAK 38)
     A                                      COLOR(YLW)
7.xx A                                  5  2'Rabattkategori. +'
     A            B1RKAF    R        B  5 20REFFLD(FPRKAT)
     A  31                                  ERRMSGID(FM00200 *LIBL/QMFOFAK 31)
     A  33                                  ERRMSGID(FMP0060 *LIBL/QMFOFAK 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                  5 25'-'
7.xx A                                  5 27'+'
7.xx A            B1RKAT    R        B  5 29REFFLD(FPRKAT)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A                                  6  2'Kundekategori . +'
     A            B1KKAF    R        B  6 20REFFLD(FPKKAT)
     A  32                                  ERRMSGID(FM00200 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                  6 25'-'
7.xx A                                  6 27'+'
7.xx A            B1KKAT    R        B  6 29REFFLD(FPKKAT)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A                                  8  2'Kunde . . . . . +'
     A            B1KUND    R        B  8 20REFFLD(FPKUND)
     A  34                                  ERRMSGID(FRK0020 *LIBL/QMFOFAK 34)
     A  35                                  ERRMSGID(FML0200 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A                                  9  2'Kundeprosjekt . +'
     A            B1KPRO    R        B  9 20REFFLD(FPKPRO)
     A  36                                  ERRMSGID(FML0020 *LIBL/QMFOFAK 36)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
7.xx A                                 11  2'Varenummer. . . +'
     A            B1VARF         8Y 0B 11 20COLOR(YLW)
     A  37                                  ERRMSGID(FM00200 *LIBL/QMFOFAK 37)
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A                                 11 29'-'
7.xx A                                 11 31'+'
7.xx A            B1VART         8Y 0B 11 33COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                      CHECK(RB)
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
