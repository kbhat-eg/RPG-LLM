     A*%%TS  SD  20101111  133448  BOF610      REL-V5R4M0  5722-WDS
     A*%%EC
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  210316 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20101111  133448  BOF610      REL-V5R4M0  5722-WDS
     A            B1VALG    R        B  7  2REFFLD(FÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(1 0)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
     A            B1VSOR    R        O  7  5REFFLD(VCVSOR)
     A            B1LAGE    R        O  7 17REFFLD(VCLAGE)
     A            B1BESK    R   -5   O  7 22REFFLD(VCBESK)
     A            B1TYPE        22   O  7 54
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20101111  133448  BOF610      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      WINDOW(B2CMD)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A                                  2  2' Velge varesortiment '
     A                                      DSPATR(RI)
7.xx A                                  3  5'Sortiment'
7.xx A                                      COLOR(WHT)
7.xx A                                  3 22'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  2'1=Velg'
     A                                      COLOR(BLU)
7.xx A                                  6  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  5'Sortiment'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 16'Lag'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 22'Beskrivelse   '
7.xx A                                      COLOR(WHT)
7.xx A                                  6 54'Type'
7.xx A                                      COLOR(WHT)
7.xx A**                                7  2'V  Sortiment  Lager Beskrivelse   -
7.xx A**                                                      Type'
7.xx A**                                    COLOR(WHT)
7.xx A            B2VSOR    R        B  4  5REFFLD(VCVSOR)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A            B2BESK    R   -5   B  4 22REFFLD(VCBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20020226  092919  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 17 76)
     A                                 16  3'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
