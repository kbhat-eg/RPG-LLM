     A*%%TS  SD  20140305  100950  MHA620      REL-V7R1M0  5770-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASVADM
     A* Format. . . . . : JV113D
     A* Beskrivelse . . : Vare - Alt.Varer  - Vedlikehold
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       190809 bof  Nytt program
7.xx A*       060616 bsa  Endringer relatert til ny GUI
7.01 A*       141016 BOF  Endret felt JVLVB pga. komp.feil
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20090820  095652  BOF610      REL-V5R4M0  5722-WDS
7.xx A            B1VALG    R        B  9  2REFFLD(JÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(JM00010 *LIBL/QMFVADM)
7.xx A            B1BTYP    R        O  9  5REFFLD(JVBTYP)
7.xx A            B1BVAR    R        O  9  8REFFLD(JVBVAR)
7.xx A            B1BTXT    R        O  9 25REFFLD(JVBTXT)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20090820  095351  BOF610      REL-V5R4M0  5722-WDS
7.xx A                                      SFLSIZ(0014)
7.xx A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CF06
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av Alternative varer'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Nobb.nr  . . . . . . . . . .'
     A                                      COLOR(BLU)
     A            B2NOBB    R        O  4 31REFFLD(JVNOBB)
     A                                      COLOR(YLW)
     A            B2VTE2    R        O  4 41REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7  2'2=Endre  3=Kopier  4=Slett  5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  5  4'Type'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  9'Alt. Varenr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  4'Type'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  9'Alt. Varenr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 25'Beskrivelse'
7.xx A**                                6  2'V Type Alt. Varenr     Beskrivelse'
     A                                      COLOR(WHT)
7.xx A            B2BTYP    R        B  6  5REFFLD(JVBTYP)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2BVAR    R        B  6  9REFFLD(JVBVAR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20010919  105848  HKO         REL-V4R4M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20140305  100950  MHA620      REL-V7R1M0  5770-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av Alternative varenr'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Nobb.nr  . . . . . . . . . .'
     A                                      COLOR(BLU)
     A            C1NOBB    R        O  4 31REFFLD(JVNOBB)
     A                                      COLOR(YLW)
     A            C1VTE2    R        O  4 41REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Type . . . . . . . . . . . .'
     A            C1BTYP    R        B  5 31REFFLD(JVBTYP)
     A  31                                  ERRMSGID(JM00110 *LIBL/QMFVADM 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  6  2'Alternativt varenr.  . . . .'
     A            C1BVAR    R        B  6 31REFFLD(JVBVAR)
     A  32                                  ERRMSGID(JMV0010 *LIBL/QMFVADM 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  5 34'T=Tun, F=Finfo'
     A          R C1MSG
     A*%%TS  SD  19990316  082956  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Alt. varenr. finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20020219  093413  HKO         REL-V5R1M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av Alternativt varenr'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Varenummer . . . . . . . . .'
     A                                      COLOR(BLU)
     A            C1NOBB    R        O  4 31REFFLD(JVNOBB)
     A                                      COLOR(YLW)
     A            C1VTE2    R        O  4 41REFFLD(JVTEK1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Type . . . . . . . . . . . .'
     A            C1BTYP    R        O  5 31REFFLD(JVBTYP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6  2'Alternativt varenr . . . . .'
     A            C1BVAR    R        O  6 31REFFLD(JVBVAR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  8  2'Beskrivelse. . . . . . . . .'
     A            C2BTXT    R  -10   B  8 31REFFLD(JVBTXT)
     A  31                                  ERRMSGID(JMV0040 *LIBL/QMFVADM 31)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9  2'Nobb opprettelses dato . . .'
     A            C2BNOD         6Y 0O  9 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 10  2'Nobb endringsdato dato . . .'
     A            C2BNED         6Y 0O 10 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 11  2'Nobb slettet dato  . . . . .'
     A            C2BNSD         6Y 0O 11 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 18  2'Opprettet dato . . . . . . .'
     A            C2BODA         6Y 0O 18 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 19  2'Sist endret dato . . . . . .'
     A            C2BEDA         6Y 0O 19 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Endret av. . . . . . . . . .'
     A            C2BUSR    R        O 20 31REFFLD(JVEUSR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20090820  095525  BOF610      REL-V5R4M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette Alternativ vare'
     A                                      DSPATR(RI)
     A                                  4  2'Type . . . . . . .'
     A            D1BTYP    R        O  4 21REFFLD(JVBTYP)
     A                                  5  2'Alt. Vare  . . . .'
     A            D1BVAR    R        O  5 21REFFLD(JVBVAR)
     A                                  7  2'Beskrivelse. . . .'
7.01 A            D1BTXT    R  -15   O  7 21REFFLD(JVBTXT)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  19990316  082956  HKO         REL-V4R2M0  5769-PW1
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Type . . . . . . .'
     A            K1BTYP    R        B  4 21REFFLD(JVBTYP)
     A                                      COLOR(YLW)
     A  31                                  ERRMSGID(JM00110 *LIBL/QMFVADM 31)
     A                                      OVRDTA
     A                                  5  2'Alt. Vare  . . . .'
     A            K1BVAR    R        B  5 21REFFLD(JVBVAR)
     A                                      COLOR(YLW)
     A  32                                  ERRMSGID(JMV0010 *LIBL/QMFVADM 32)
     A                                      OVRDTA
     A                                  7  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  9  2'Type . . . . . . .'
     A            JVBTYP    R        O  9 21OVRDTA
     A                                 10  2'Alt. Vare  . . . .'
     A            JVBVAR    R        O 10 21OVRDTA
     A                                 11  2'Beskrivelse. . . .'
7.01 A            K2BTXT    R  -15   O 11 21REFFLD(JVBTXT)
     A                                      OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
