     A*%%TS  SD  20080320  185202  KOBASP      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RK112D
     A* Beskrivelse . . : Kunder, vedlikehold av kundeposter
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       230702 KOB  Nytt program
     A*
7.xx A*  7.00 040419 bsa  Mulighet for bredere skjerm
7.fb A*  7.00 160419 bsa  Forskuddsbetaling/depositum
     A*
8.01 A*K000   230821 bsa  Endringer pga utvidet valg
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A*  Bilde B1 - subfile 1
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20020811  134735  KOB         REL-V5R1M0  5722-WDS
     A            B1BDAT    R        H      REFFLD(RNBDAT)
     A            B1TIST    R        H      REFFLD(RNTIST)
     A            B1RAAR    R        H      REFFLD(RNRAAR)
     A            B1RPER    R        H      REFFLD(RNRPER)
     A            B1INKO    R        H      REFFLD(RNINKO)
     A            B1ANTP    R        H      REFFLD(RNANTP)
     A            B1REKL    R        H      REFFLD(RNREKL)
7.fb A            B1VALG         2Y 0B  9  2
8.01 A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
7.fb A                                      VALUES(2 5 6 7 8 9 10 0)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
8.01 A            B1BILN    R        O  9  5REFFLD(RNBILN)
     A  73                                  COLOR(YLW)
     A  70                                  COLOR(RED)
     A  71                                  COLOR(BLU)
     A  72                                  COLOR(WHT)
8.01 A            B1BIDA         6Y 0O  9 14
     A  70                                  COLOR(RED)
     A  71                                  COLOR(BLU)
     A  72                                  COLOR(WHT)
8.01 A            B1BILK    R        O  9 21REFFLD(RNBILK)
     A  70                                  COLOR(RED)
     A  71                                  COLOR(BLU)
     A  72                                  COLOR(WHT)
8.01 A            B1TEXT         9A  O  9 24
     A  70                                  COLOR(RED)
     A  71                                  COLOR(BLU)
     A  72                                  COLOR(WHT)
6.NU A            B1REFN    R        O  9 34REFFLD(RNREFN)
     A  70                                  COLOR(RED)
     A  71                                  COLOR(BLU)
     A  72                                  COLOR(WHT)
     A            B1FDAT         6Y 0O  9 43
     A  70                                  COLOR(RED)
     A  71                                  COLOR(BLU)
     A  72                                  COLOR(WHT)
     A            B1BELØ        11Y 2O  9 50EDTCDE(J)
     A  70                                  COLOR(RED)
     A  71                                  COLOR(BLU)
     A  72                                  COLOR(WHT)
     A            B1REST        11Y 2O  9 66EDTCDE(J)
     A  70                                  COLOR(RED)
     A  71                                  COLOR(BLU)
     A  72                                  COLOR(WHT)
     A*
     A*  Bilde B2 - subfile kontroll
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20080320  185202  KOBASP      REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA02
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CF06
     A                                      CF07
     A                                      CF08
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
6.11 A            B2FIRM         3  0O  1 33DSPATR(ND)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av kundeposteringer'
     A                                      COLOR(WHT)
7.xx A                                  2 38'RK112B2'
7.xx A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Kunde:'
     A            B2KUND    R        O  4  9REFFLD(RKKUND)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            B2NAVN    R        O  4 16REFFLD(RKNAVN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  4 58'Regnskapsår. .:'
     A            B2RAAR         4Y 0B  4 74COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A  94        B2NOTB         8A  O  5 16DSPATR(RI)
     A                                      OVRDTA
     A                                  5 58'Periode. . . .:'
     A            B2RPER         2Y 0B  5 74COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                  6  2'2=Endre  5=Vis  6=Vis motpost  7=V-
     A                                      is bilag'
     A                                      COLOR(BLU)
7.fb A**                                7  2'8=Vis faktura   9=Vis arkiv'
7.fb A                                  7  2'8=Vis faktura   9=Vis arkiv   10=L-
7.fb A                                      egg inn dep.'
     A                                      COLOR(BLU)
     A                                  7 58'Saldo:'
     A            B2SALD    R        O  7 66REFFLD(RKSALD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(J)
     A                                      DSPATR(UL)
     A                                  8  2'V'
     A                                      COLOR(WHT)
8.01 A                                  8  5'Bil.nr'
     A                                      COLOR(WHT)
8.01 A                                  8 15'Dato'
     A                                      COLOR(WHT)
6.NU A                                  8 20'Bk'
     A                                      COLOR(WHT)
8.01 A                                  8 24'Tekst'
     A                                      COLOR(WHT)
8.01 A**                                8 31'Val.'
8.01 A**                                    COLOR(WHT)
8.01 A                                  8 34'Ref.'
     A                                      COLOR(WHT)
     A                                  8 43'Forfall'
     A                                      COLOR(WHT)
     A                                  8 56'Beløp'
     A                                      COLOR(WHT)
     A                                  8 68'Rest/Akkum'
     A                                      COLOR(WHT)
     A*
     A*  Bilde B2 - funksjonstaster
     A*
     A          R B2CMD
     A*%%TS  SD  20040317  200316  KOB         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                 23  2'F2=Notatark'
     A                                      COLOR(BLU)
     A                                 23 15'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 30'F6=Åpne/Alle/Akkum'
     A                                      COLOR(BLU)
     A                                 23 50'F7=Valuta/NOK'
     A                                      COLOR(BLU)
     A                                 23 65'F8=Saldo/Kjøp'
     A                                      COLOR(BLU)
     A*
     A*  Bilde C2 - vindu for vedlikehold
     A*
     A          R C2WIN
     A*%%TS  SD  20050628  111043  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 16 75)
     A                                      CA02
     A                                      CA03
     A                                      CA04
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            C2NYEN        11A  O  2  2DSPATR(RI)
     A                                      OVRDTA
     A                                  2 32'Kundeposteringer'
     A                                      COLOR(WHT)
     A                                  3  1'Bilagsnummer . .:'
     A            C2BILN    R        O  3 19REFFLD(RNBILN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A  73                                  COLOR(YLW)
6.NU A  73        C2NOTB         8   O  3 28
     A                                      DSPATR(RI)
     A                                      OVRDTA
     A                                  4  1'Bilags-kode. . .:'
     A            C2BILK    R        B  4 19REFFLD(RNBILK)
     A  33                                  DSPATR(PC)
     A  33 81                               ERRMSGID(RM01010 *LIBL/QMFOKON 81)
     A N30                                  COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2TEXT    R        B  4 22REFFLD(RNTEXT)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                  4 43'Bilagsdato . . .:'
     A            C2BDAT         6Y 0O  4 61DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  5  1'Forfallsdato . .:'
     A            C2FDAT         6Y 0B  5 19
     A  37                                  DSPATR(PC)
     A  37 81                               ERRMSGID(RMH0150 *LIBL/QMFOKON 81)
     A N30                                  COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  5 43'Regnskaps-per/år:'
     A            C2RPER    R        O  5 61REFFLD(RNRPER)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2RAAR    R        O  5 64REFFLD(RNRAAR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  6  1'Reklamasjonskode:'
     A            C2REKL    R        B  6 19REFFLD(RNREKL)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A N30                                  COLOR(YLW)
     A                                  6 21'(k)'
     A                                  6 43'Journalliste-nr.:'
     A            C2JOUR    R        O  6 61REFFLD(RNJOUR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  1'Referansenummer.:'
     A            C2REFN    R        B  7 19REFFLD(RNREFN)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  7 43'Generalnota-nr. :'
     A            C2SAMF    R        O  7 61REFFLD(RNSAMF)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  8  1'Antall purringer:'
     A            C2ANTP    R        B  8 19REFFLD(RNANTP)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8 21'(k)'
     A                                  8 25'Dato:'
     A            C2PDAT         6Y 0B  8 31DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                  8 43'Beløp. . . . . .:'
     A            C2BELØ    R        O  8 61REFFLD(RNBELØ)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                  9  1'Renteberegnet. .:'
     A            C2RENK    R        B  9 19REFFLD(RNRENK)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9 21'(k)'
     A                                  9 25'Dato:'
     A            C2RDAT         6Y 0B  9 31DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                      COLOR(YLW)
     A                                  9 43'Restbeløp. . . .:'
     A            C2REST    R        B  9 61REFFLD(RNREST)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 10  1'Inkassokode. . .:'
     A            C2INKO    R        B 10 19REFFLD(RNINKO)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A N30                                  COLOR(YLW)
     A                                 10 21'(k)'
     A                                 10 25'Dato:'
     A            C2IDAT         6Y 0B 10 31DSPATR(UL)
     A                                      EDTWRD('  .  .  ')
     A  39                                  DSPATR(PC)
     A  39 81                               ERRMSGID(RMH0150 *LIBL/QMFOKON 81)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A N30                                  COLOR(YLW)
     A                                 10 43'Valuta-kode. . .:'
     A            C2VALK    R        O 10 61REFFLD(RNVALK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 11  1'Factoringkode. .:'
     A            C2FACK    R        B 11 19REFFLD(RNFACK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A  30                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 11 21'(k)'
     A                                 11 25'Dato:'
     A            C2FADA         6Y 0B 11 31DSPATR(UL)
     A                                      EDTWRD('  .  .  ')
     A  39                                  DSPATR(PC)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A N30                                  COLOR(YLW)
     A  39 81                               ERRMSGID(RMH0150 *LIBL/QMFOKON 81)
     A                                 11 43'Valutabeløp. . .:'
     A            C2VALB    R        O 11 61REFFLD(RNVALB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 12  1'Avdeling . . . .:'
     A            C2AVDE    R        B 12 19REFFLD(RNAVDE)
     A  34                                  DSPATR(PC)
     A  34 81                               ERRMSGID(RM01070 *LIBL/QMFOKON 81)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A            C2GTXT    R   18   O 12 24REFFLD(RAGTXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 12 43'Valuta-rest. . .:'
     A            C2VALR    R        B 12 61REFFLD(RNVALR)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 13  1'Selger . . . . .:'
     A            C2SLGR    R        B 13 19REFFLD(RNSLGR)
     A  35                                  DSPATR(PC)
     A  35 81                               ERRMSGID(RM01090 *LIBL/QMFOKON 81)
     A N30                                  COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A            C2ITXT    R   18   O 13 24REFFLD(RAITXT)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 13 43'Autogirokode . .:'
     A            C2AUTO    R        B 13 61REFFLD(RNAUTO)
     A N30                                  COLOR(YLW)
     A                                 13 63'(k)'
     A                                 15  2'F2=Notatark bilag'
     A                                      COLOR(BLU)
     A                                 15 21'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 15 37'F4=Spørring'
     A                                      COLOR(BLU)
     A          R E2WIN
     A*%%TS  SD  20071203  135654  AMD550      REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A                                      WINDOW(*DFT 17 60)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  1 22'Reskontrostatistikk'
     A                                  2  1'Kjøp i år .:'
     A            E2KJØP    R        O  2 14REFFLD(RKKJØP)
     A                                      DSPATR(UL)
     A                                  2 32'Saldo i år .:'
     A            E2SALD    R        O  2 46REFFLD(RKSALD)
     A                                      DSPATR(UL)
     A                                  3  1'Kjøp i fjor:'
     A            E2KJØF    R        O  3 14REFFLD(RKKJØF)
     A                                      DSPATR(UL)
     A                                  3 32'Saldo i fjor:'
     A            E2SAIF    R        O  3 46REFFLD(RKSAIF)
     A                                      DSPATR(UL)
     A                                  4  1'Kred.grense:'
     A            E2GRNS    R        O  4 14REFFLD(RKGRNS)
     A                                      DSPATR(UL)
     A                                  4 32'Ordreverdi .:'
     A            E2MEMS    R        O  4 46REFFLD(RKMEMS)
     A                                      DSPATR(UL)
     A                                  5  1'Kredittid .:'
     A            E2KVTI         5Y 0O  5 14EDTCDE(3)
     A                                      DSPATR(UL)
     A                                  5 32'BRab akk.. .:'
     A            E2KJAL    R        O  5 46REFFLD(RKKJAL)
     A                                      DSPATR(UL)
     A                                  6 32'BRab i ordre:'
     A            E2MEMA    R        O  6 46REFFLD(RKMEMA)
     A                                      DSPATR(UL)
     A                                  7  1'Aldersfordeling:'
     A                                  7 38'Antall poster:'
     A                                  8  1'-Ikke forfalt :'
     A            E2ALD0    R        O  8 17REFFLD(RNBELØ)
     A                                      DSPATR(UL)
     A                                  8 38'- Totalt. . . .:'
     A            E2POST         6Y 0O  8 55EDTCDE(3)
     A                                      DSPATR(UL)
     A                                  9  1'-Ikke saldert'
     A                                  9 15':'
     A            E2ALDN    R        O  9 17REFFLD(RNBELØ)
     A                                      DSPATR(UL)
     A                                  9 38'- Debet . . . .:'
     A            E2DEBE         6Y 0O  9 55EDTCDE(3)
     A                                      DSPATR(UL)
     A                                 10  1'-   0 til'
     A            E23KA1    R        O 10 11REFFLD(RA3KA1)
     A                                      DSPATR(UL)
     A                                 10 15':'
     A            E2ALD1    R        O 10 17REFFLD(RNBELØ)
     A                                      DSPATR(UL)
     A                                 10 38'- Kredit. . . .:'
     A            E2KRED         6Y 0O 10 55EDTCDE(3)
     A                                      DSPATR(UL)
     A                                 11  1'-'
     A            E23KS2    R        O 11  3REFFLD(RA3KA1)
     A                                      DSPATR(UL)
     A                                 11  7'til'
     A            E23KA2    R        O 11 11REFFLD(RA3KA2)
     A                                      DSPATR(UL)
     A                                 11 15':'
     A            E2ALD2    R        O 11 17REFFLD(RNBELØ)
     A                                      DSPATR(UL)
     A                                 11 38'- Åpne poster .:'
     A            E2REST         6Y 0O 11 55EDTCDE(3)
     A                                      DSPATR(UL)
     A                                 12  1'-'
     A            E23KS3    R        O 12  3REFFLD(RA3KA2)
     A                                      DSPATR(UL)
     A                                 12  7'til'
     A            E23KA3    R        O 12 11REFFLD(RA3KA3)
     A                                      DSPATR(UL)
     A                                 12 15':'
     A            E2ALD3    R        O 12 17REFFLD(RNBELØ)
     A                                      DSPATR(UL)
     A                                 12 38'- Purret. . . .:'
     A            E2PURR         6Y 0O 12 55EDTCDE(3)
     A                                      DSPATR(UL)
     A                                 13  1'- Over'
     A            E23KA4    R        O 13 11REFFLD(RA3KA4)
     A                                      DSPATR(UL)
     A                                 13 15':'
     A            E2ALD4    R        O 13 17REFFLD(RNBELØ)
     A                                      DSPATR(UL)
     A                                 13 38'- Renteberegnet:'
     A            E2RENT         6Y 0O 13 55EDTCDE(3)
     A                                      DSPATR(UL)
     A                                 14  1'Sum. . . . . .:'
     A            E2SALT    R        O 14 17REFFLD(RNBELØ)
     A                                      DSPATR(UL)
     A                                 14 38'- Inkasso . . .:'
     A            E2INKA         6Y 0O 14 55EDTCDE(3)
     A                                      DSPATR(UL)
     A                                 15  1'-Herav inkasso:'
     A            E2INKB    R        O 15 17REFFLD(RNBELØ)
     A                                      DSPATR(UL)
     A                                 15 38'- Reklamasjon .:'
     A            E2REKL         6Y 0O 15 55EDTCDE(3)
     A                                      DSPATR(UL)
     A                                 16  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
