     A*%%TS  SD  19990813  140509  HKO         REL-V4R4M0  5769-PW1
     A*%%EC
     A**--------------------------------------------------------
     A*
7.xx A*       171116 bsa  Endringer relatert til ny GUI
     A*
     A**--------------------------------------------------------
7.xx A                                      DSPSIZ(24 80 *DS3 -
7.xx A                                             27 132 *WIDE)
7.XX A**                                    DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/JFRF JFRFR)
     A                                      PRINT
     A          R B1BLD
     A*%%TS  SD  19990813  140509  HKO         REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      CF13
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Vareadministrasjon'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Gruppebehandling - leverandør'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Utplukk:'
     A                                      DSPATR(UL)
7.xx A                                  6  2'Leverandør (vareeier). . . +'
     A            B1LDOR    R        B  6 31REFFLD(JVLDOR)
     A  31                                  ERRMSGID(JML0010 *LIBL/QMFVADM 31)
     A  32                                  ERRMSGID(JML0020 *LIBL/QMFVADM 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B1LNAV    R   -2   O  6 45REFFLD(JLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7  2'Varegruppe . . . . . . . . +'
     A            B1OGRP    R        B  7 31REFFLD(JVOGRP)
     A  33                                  ERRMSGID(JVG1020 *LIBL/QMFVADM 33)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7 34'+'
7.xx A            B1HGRP    R        B  7 36REFFLD(JVHGRP)
     A  34                                  ERRMSGID(JVG2020 *LIBL/QMFVADM 34)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7 39'+'
7.xx A            B1UGRP    R        B  7 41REFFLD(JVUGRP)
     A  35                                  ERRMSGID(JVG0020 *LIBL/QMFVADM 35)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B1UTXT        33A  O  7 45COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Kampanjeleverandør:'
     A                                      DSPATR(UL)
7.xx A                                 10  2'Prisgivende leverandør . . +'
     A            B1PLDO    R        B 10 31REFFLD(JXLDOR)
     A  36                                  ERRMSGID(JML0020 *LIBL/QMFVADM 36)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B1PLNA    R   -2   O 10 45REFFLD(JLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 12  2'Rabatter/påslag -inn:'
     A                                      DSPATR(UL)
7.xx A                                 12 45'Rabatt/påslag -ut:'
     A                                      DSPATR(UL)
     A                                 13  2'Rabatt-1 (på Nobb-pris). . .'
     A            B1RAB1         4Y 2B 13 31COLOR(YLW)
     A  39                                  ERRMSGID(JMM1200 *LIBL/QMFVADM 39)
     A  40                                  ERRMSGID(JMM1210 *LIBL/QMFVADM 40)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                 13 37'%'
7.xx A                                 13 45'Rabatt (på Nobb-pris) . .'
     A            B1RABA         4Y 2B 13 71COLOR(YLW)
     A  41                                  ERRMSGID(JMM1220 *LIBL/QMFVADM 41)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                 13 77'%'
     A                                 14  2'Rabatt-2 (på Nobb-pris). . .'
     A            B1RAB2         4Y 2B 14 31COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                 14 37'%'
7.xx A                                 14 45'Påslag (fra kostpris) . .'
     A            B1PSLS         4Y 2B 14 71COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                 14 77'%'
     A                                 15  2'Påslag til kostpris. . . . .'
     A            B1PSLK         4Y 2B 15 31COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A                                 15 37'%'
     A                                 17  2'Beløp:'
     A                                      DSPATR(UL)
     A                                 18  2'Innkjøpspris . . . . . . KAI'
     A            B1VKAI    R        B 18 31REFFLD(JMVKAI)
     A  42                                  ERRMSGID(JMM1100 *LIBL/QMFVADM 42)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 19  2'Kostpris . . . . . . . . . .'
     A            B1VKPR    R        B 19 31REFFLD(JMVKPR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 20  2'Egen/anbefalt kamp.pris. KAA'
     A            B1VKAA    R        B 20 31REFFLD(JMVKAA)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake   F4=Spørring       -
     A                                                                   F13=Be-
     A                                      tingelser'
     A                                      COLOR(BLU)
     A          R M1WIN
     A*%%TS  SD  19971014  092158  HKO         REL-V3R7M0  5716-PW1
     A                                      WINDOW(*DFT 8 33)
     A                                      CA03
     A                                      CA12
     A                                  3  2'Antall varer i utplukket:'
     A            M1ANT          5Y 0O  3 28COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  7  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
