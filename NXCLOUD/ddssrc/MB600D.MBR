     A*%%TS  SD  20080625  145300  ASK560      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLOGI
     A* Format. . . . . : MB600D
     A* Beskrivelse . . : Utskrift av ABC parametre
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  220316 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R A1BLD
     A*%%TS  SD  20080625  145300  ASK560      REL-V5R4M0  5722-WDS
     A                                      KEEP
     A                                      CA03
     A                                      CF04
     A                                      RTNCSRLOC(&WACTRE &WACTFE)
     A            WACTRE        10A  H
     A            WACTFE        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Utskrift av Abc-analyse          '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5 33'Fra'
7.xx A                                  5 50'Til'
     A                                  6  2'ABC-kode . . . . . . . . . .'
     A            A1FABC         2A  B  6 31
     A  30                                  ERRMSGID(MMB0070 *LIBL/QMFLAGR 30)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
7.xx A            A1TABC         2A  B  6 50COLOR(YLW)
7.xx A                                  7  2'Varegruppe . . . . . . . . +'
     A            A1FOGR         2Y 0B  7 31CHECK(RZ)
     A  32                                  ERRMSGID(LM00170 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
7.xx A                                  7 34'+'
7.xx A            A1FHGR         2Y 0B  7 36CHECK(RZ)
     A  33                                  ERRMSGID(LM00170 *LIBL/QMFLAGR 33)
     A                                      COLOR(YLW)
7.xx A                                  7 39'+'
7.xx A            A1FUGR         3Y 0B  7 41CHECK(RZ)
     A  34                                  ERRMSGID(LM00170 *LIBL/QMFLAGR 34)
     A                                      COLOR(YLW)
7.xx A                                  7 48'+'
7.xx A            A1TOGR         2Y 0B  7 50CHECK(RZ)
     A                                      COLOR(YLW)
7.xx A                                  7 53'+'
7.xx A            A1THGR         2Y 0B  7 55CHECK(RZ)
     A                                      COLOR(YLW)
7.xx A                                  7 58'+'
7.xx A            A1TUGR         3Y 0B  7 60CHECK(RZ)
     A                                      COLOR(YLW)
7.xx A                                  8  2'Leverandørnummer . . . . . +'
     A            A1LVNR         6Y 0B  8 31CHECK(RZ)
     A  35                                  ERRMSGID(LM06000 *LIBL/QMFLAGR 35)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
7.xx A                                  9  2'Varesortiment  . . . . . . +'
     A            A1VSOR    R        B  9 31REFFLD(MAVSOR)
     A  36                                  ERRMSGID(MMA0040 *LIBL/QMFLAGR 36)
     A                                      COLOR(YLW)
7.xx A                                 10  2'Lager. . . . . . . . . . . +'
     A            A1LAGE         2A  B 10 31COLOR(YLW)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F4=Spørring'
     A                                      COLOR(BLU)
