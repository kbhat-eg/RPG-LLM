     A*%%TS  SD  20030119  134210  KOB         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RL113D
     A* Beskrivelse . . : Vedlikehold attestasjonskode
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       120602 BOF  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  19990316  084618  HKO         REL-V4R2M0  5769-PW1
     A            B1VALG    R        B  8  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1LEVR    R        O  8  5REFFLD(ROLEVR)
     A            B1BILN    R        O  8 13REFFLD(ROBILN)
6.NU A            B1BIDA         6Y 0O  8 23EDTWRD('  .  .  ')
6.NU A            B1TEXT        18   O  8 33
     A            B1BELØ    R        O  8 53REFFLD(ROBELØ)
     A                                      EDTCDE(L)
     A            B1FDAT         6Y 0O  8 68EDTWRD('  .  .  ')
     A            B1BDAT    R        H      REFFLD(ROBDAT)
     A            B1TIST    R        H      REFFLD(ROTIST)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20030119  134210  KOB         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Endre attesteringskode leverandør'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'1=Attester                       '
     A                                      COLOR(BLU)
6.NU A                                  6  2'V  Lev.nr  Bil.nr.   Bil.dato  Tek-
6.NU A                                      st                Beløp        Forf-
     A                                      .dato'
     A                                      COLOR(WHT)
     A            B2LEVR    R        B  7  5REFFLD(ROLEVR)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            B2BILN    R        B  7 13REFFLD(ROBILN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A          R N1WIN
     A*%%TS  SD  20010822  090358  BOF         REL-V4R4M0  5769-PW1
     A                                      WINDOW(*DFT 9 35)
     A                                      CA03
     A                                      PUTOVR
     A                                  2  4' Tast inn årstall'
     A                                      DSPATR(RI)
     A                                  6  4'F3=Gå tilbake'
     A                                  4 12'(feks. 2002)'
     A            N1RAAR    R        B  4  4REFFLD(RORAAR)
     A                                      OVRDTA
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A  31                                  ERRMSGID(RMA0210 *LIBL/QMFOKON 31)
     A                                      EDTCDE(Z)
     A          R B2CMD
     A*%%TS  SD  19990114  171537  HKO         REL-V4R2M0  5769-PW1
     A                                 23  2'F3=Gå tilbake   F5=Forny'
     A                                      COLOR(BLU)
     A          R ASUME                     ASSUME
     A                                 24 79' '
