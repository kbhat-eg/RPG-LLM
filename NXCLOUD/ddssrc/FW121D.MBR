     A*%%TS  SD  20020312  223958  HKO         REL-V5R1M0  5722-WDS
     A*%%EC
7.xx  * 7.00  260618 bkv  Endringer reltart til ny GUI. NB Kan kun gjelde DDS
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20020312  223024  HKO         REL-V5R1M0  5722-WDS
     A            B1LINE    R        H      REFFLD(FWLINE)
     A            B1VALG         1Y 0B  9  2COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      VALUES(2 3 5 6 0)
     A                                      CHKMSGID(BM00010 *LIBL/QMFVEDL)
     A            B1TEK1    R        O  9  5REFFLD(FWTEK1)
     A            B1NYPO        15A  O  9 42
     A            B1ENHE    R        O  9 59REFFLD(FWENHE)
     A            B1SAPR    R        O  9 64REFFLD(FWSAPR)
     A            B1EKOD    R        O  9 76REFFLD(FWEKOD)
     A            B1STAT         1Y 0O  9 78EDTCDE(Z)
     A            B1HKOD         1A  O  9 80
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020312  223958  HKO         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA01
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CF13
     A                                      CF14
     A                                      CF15
     A                                      CF20
     A                                      CF23
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Oppretting av nye varer fra levera-
     A                                      ndør'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Leverandør . .'
     A                                      COLOR(BLU)
     A            B2LDOR    R        O  4 17REFFLD(FWLDOR)
     A                                      COLOR(BLU)
     A            B2NAVN        30A  O  4 24COLOR(BLU)
     A                                  7  2'2=Endre  3=Holde tilbake  5=Vis  6-
     A                                      =Frigi'
     A                                      COLOR(BLU)
     A                                  8  2'V'
     A                                      COLOR(WHT)
     A                                  8  5'Varetekst 1'
     A                                      COLOR(WHT)
     A  80                              8 42'Lev. varenr'
     A  80                                  COLOR(WHT)
     A  81                              8 43'GTIN-nr'
     A  81                                  COLOR(WHT)
     A  82                              8 43'Nobbnr'
     A  82                                  COLOR(WHT)
     A                                  8 59'Enh'
     A                                      COLOR(WHT)
     A                                  8 64'Salgspris'
     A                                      COLOR(WHT)
     A                                  8 76'K'
     A                                      COLOR(WHT)
     A                                  8 78'S'
     A                                      COLOR(WHT)
     A                                  8 80'H'
     A                                      COLOR(WHT)
     A                                  5  2'Varetekst 1'
     A                                      COLOR(WHT)
     A  80                              6  2'Lev. varenr'
     A  80                                  COLOR(WHT)
     A  81                              6  2'GTIN-nr'
     A  81                                  COLOR(WHT)
     A  82                              6  2'Nobbnr'
     A  82                                  COLOR(WHT)
     A            B2TEK1    R        B  5 16REFFLD(FWTEK1)
     A  31                                  ERRMSGID(FMW0110 *LIBL/QMFOFAK 31)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A  80        B2LVAR    R   -5   B  6 16REFFLD(FWLVAR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A  81        B2EANN    R        B  6 16REFFLD(FWEANN)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A  82        B2NOBB    R        B  6 16REFFLD(FWNOBB)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  19981007  135631  HKO         REL-V4R2M0  5769-PW1
     A                                 22  2'F3=Gå tilbake          F5=Forny   -
     A                                                      F13=Statuskoder '
     A                                      COLOR(BLU)
     A                                 23  2'F14=Gruppebehandling   F15=Standar-
     A                                      dverdier        F20=Overføring   F2-
     A                                      3=Ny pos.'
     A                                      COLOR(BLU)
