     A*%%TS  SD  20110606  204019  ASK610      REL-V7R1M0  5770-WDS
     A*%%EC
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOFAK
     A* Format. . . . . : EM101
     A* Beskrivelse . . : BONUS
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
6.21 A*       081113 BSA  Fjernet spesialer for Mestergruppen
6.23 A*       211113 BSA  Lagt inn mulighet for å kopiere mal til
6.23 A*                   enkeltkunde.
6.24 A*       051213 BSA  Justeringer grunnet problematikk med jacada
7.xx A* 7.xx  110316  bsa  Justeringer basert på ny GUI og regler
     A*
     A*--------------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20051030  124625  HKO550      REL-V5R3M0  5722-WDS
     A            B1HMNR         5  0H
7.xx A            B1VALG         2Y 0B 10  2
     A                                      COLOR(YLW)
6.23 A                                      VALUES(0 2 3 4 5 7 9 10)
     A                                      CHKMSGID(FM00010 *LIBL/QMFOFAK)
6.23 A                                      EDTCDE(Z)
8.xx A            B1HBES        30A  O 10  5
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20090118  170046  ASK560      REL-V5R4M0  5722-WDS
7.xx A                                      SFLSIZ(0013)
7.xx A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA06
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av maler'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Bonustype'
     A            B2TBOT         2A  O  4 13
     A            B2TBES        35A  O  4 16
7.xx A                                  5  5'Beskrivelse '
7.xx A                                      COLOR(WHT)
7.xx A                                  7  2' 2=Endre  3=Kopier  4=Slette  5=Vi-
6.24 A                                      se  7=Bonuselementer  9=Oppd. k.kat'
6.24 A                                      COLOR(BLU)
7.xx A                                  8  2'10=Opppd. kunde'
     A                                      COLOR(BLU)
7xxx A                                  9  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  5'Beskrivelse              '
     A                                      COLOR(WHT)
7.xx A            B2HBES        30A  B  6  5COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20110603  151859  ASK610      REL-V7R1M0  5770-WDS
     A                                 23  2'F3=Gå tilbake  F5=Forny  F6=Oppret-
     A                                      t'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20051019  120047  HKO530      REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Opprett bonusmal'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Bonustype'
     A            C1TBOT         2A  O  4 13
     A            C1TBES        35A  O  4 16
     A                                  6  2'Malbeskrivelse . . . . . . .'
     A            C1HBES        35A  B  6 31
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20051019  115603  HKO530      REL-V5R3M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Ordre/Fakturering'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av bonusmal'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Bonusmal'
     A            C1TBOT    R        O  5 12REFFLD(VATBOT)
     A            C1TBES        35A  O  5 15
     A            C1HMNR         5S 0O  5 53
     A                                      OVRDTA
     A                                  7  2'Beskrivelse. . . . . . . . .'
     A            C2HBES        35A  O  7 31
     A                                  9  2'Gyldig fra dato  . . . . . .'
     A            C2HFRA         6Y 0B  9 31
     A  31                                  ERRMSG('Fradato er ugyldig')
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 10  2'Gyldig til dato  . . . . . .'
     A            C2HTIL         6Y 0B 10 31
     A  32                                  ERRMSG('Tildato er ugyldig')
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 12  2'Bonuskategori. . . . . . . .'
     A            C2HBKA    R        B 12 31REFFLD(FÅLAGE)
6.21 A  33                                  ERRMSG('Gyldige verdier er 0-1')
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 12 38'00-Generell'
     A                                 13 38'01-Volumbonus'
6.21 A**                               14 38'02-Lojalitetsbonus'
6.21 A**                               15 38'03-Betalingsbonus'
     A            C2HODA         6Y 0O 22 48COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 57'/'
     A            C2HEDA         6Y 0O 22 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22 68'/'
     A            C2HEUS        10A  O 22 70
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20051020  153510  HKO530      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 15 50)
     A                                      CA03
     A                                      CF04
     A                                      CF07
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Bonustype'
     A            K1HBOT         2A  O  4 12
     A                                      COLOR(YLW)
     A            K1TBES        35A  O  4 15
     A  31                                  ERRMSG('Malbeskrivelse må fylles -
     A                                      ut')
     A                                      COLOR(YLW)
     A                                  5  2'Mal . . .'
     A            K1HBES        35A  B  5 12OVRDTA
     A                                      COLOR(YLW)
     A                                  7  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  9  2'Bonustype'
     A            EMHBOT         2A  O  9 12
     A            VATBES        35A  O  9 15
     A                                 10  2'Mal . . .'
     A            EMHBES        35A  O 10 12
     A                                 14  2'F3=Gå tilbake  F4=Spørring  F7=Ink-
     A                                      l. bon.elem'
     A                                      COLOR(BLU)
     A*
     A          R D1WIN
     A*%%TS  SD  20051020  153510  HKO530      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 15 50)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Slette mal, bekreft med ENTER '
     A                                      DSPATR(RI)
     A                                  5  2'Bonustype'
     A            D1TBOT         2A  O  5 12
     A            D1TBES        35A  O  5 15
     A                                  6  2'Mal . . .'
     A            D1HBES        35A  O  6 12OVRDTA
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R M1WIN
     A*%%TS  SD  20100305  135445  ASP_A04     REL-V6R1M0  5761-WDS
     A                                      WINDOW(*DFT 18 50)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  4  2' Kopier bonusmal til kunder'
     A                                      DSPATR(RI)
     A                                  7  2'Kundekategori:'
     A            M1KKAT    R        B  7 18REFFLD(FÅKKAT)
     A  31                                  ERRMSGID(FRA1110 *LIBL/QMFOFAK 31)
     A  32                                  ERRMSGID(FRA1120 *LIBL/QMFOFAK 32)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      CHECK(RB)
     A            M1KTXT        20A  O  7 24
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 17  2'F3=Gå tilbake  F4=Spørring'
     A                                      COLOR(BLU)
6.23 A          R E1WIN
6.23 A*%%TS  SD  20100305  135445  ASP_A04     REL-V6R1M0  5761-WDS
6.23 A                                      WINDOW(*DFT 18 50)
6.23 A                                      CA03
6.23 A                                      CF04
6.23 A                                      CA12
6.23 A                                      PUTOVR
6.23 A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
6.23 A            W_ARCD        10A  H
6.23 A            W_AFLD        10A  H
6.23 A                                  4  2' Kopier bonusmal til kunde'
6.23 A                                      DSPATR(RI)
6.23 A                                  7  2'Kunde . . . .:'
6.23 A            E1KUND    R        B  7 18REFFLD(FÅKUND)
6.23 A  31                                  ERRMSGID(FRA1110 *LIBL/QMFOFAK 31)
6.23 A  32                                  ERRMSGID(FRA1120 *LIBL/QMFOFAK 32)
6.23 A                                      COLOR(YLW)
6.23 A                                      OVRDTA
6.23 A                                      CHECK(RB)
6.23 A            E1KTXT        20A  O  7 25
6.23 A                                      COLOR(YLW)
6.23 A                                      OVRDTA
6.23 A                                 17  2'F3=Gå tilbake  F4=Spørring'
6.23 A                                      COLOR(BLU)
     A          R L1WIN
     A*%%TS  SD  20110603  152353  ASK610      REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 11 40)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Endre bonus tidsrom '
     A                                      DSPATR(RI)
     A                                  5  2'Endrer:'
     A            L1KUND        30A  O  5 10
     A                                  7  2'Start bonusperiode:'
     A            L1STDA         6Y 0B  7 22COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                  7 30'(ddmmåå)'
     A                                  8  2'Slutt bonusperiode:'
     A            L1SLDA         6Y 0B  8 22COLOR(YLW)
     A                                      CHECK(RB)
     A                                      EDTCDE(Z)
     A                                  8 30'(ddmmåå)'
     A                                 10  3'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  4  2'Bonustype:'
     A            FLD001         2   B  4 13DSPATR(UL)
     A                                      COLOR(YLW)
