     A*%%TS  SD  20101208  110019  SST61P      REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RH512D
     A* Beskrivelse . . : Hovedboksposter, spørring
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsveskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       280302 KOB  Nytt program
7.xx A*       040516 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20050721  003857  KOB540      REL-V5R2M0  5722-WDS
     A            B1RAAR    R        H      REFFLD(RBRAAR)
     A            B1BDAT    R        H      REFFLD(RBBDAT)
     A            B1TIST    R        H      REFFLD(RBTIST)
     A            B1VALG    R        B 10  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(5 7 8 9 0)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1KONT    R        O 10  4REFFLD(RBKONT)
     A            B1SPES    R        O 10 10REFFLD(RBSPES)
     A            B1AVDE    R        O 10 15REFFLD(RBAVDE)
     A            B1BILN    R        O 10 20REFFLD(RBBILN)
     A  73                                  COLOR(YLW)
6.NU A**          B1BIDA         6S 0O 10 27
6.NU A            B1BIDA         6S 0O 10 29
6.NU A**          B1RPER    R        O 10 34REFFLD(RBRPER)
6.NU A            B1RPER    R        O 10 36REFFLD(RBRPER)
6.NU A**          B1BILK    R        O 10 38REFFLD(RBBILK)
6.NU A            B1BILK    R        O 10 40REFFLD(RBBILK)
6.NU A**          B1TEXT    R        O 10 41REFFLD(RBTEXT)
6.NU A            B1TEXT        18   O 10 43
     A            B1MVAK    R        O 10 62REFFLD(RBMVAK)
     A            B1NETO    R        O 10 64REFFLD(RBNETO)
     A            B1AUTK    R        O 10 80REFFLD(RBAUTK)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050721  003857  KOB540      REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
6.22 A            B2FIRM         3  0O  1 34DSPATR(ND)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Spørring på hovedboksposter'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------- '
     A                                      COLOR(WHT)
7.xx A                                  8  2'5=Vis  7=Vis bilag  8=Vis sumpost -
     A                                       9=Vis arkiv'
     A                                      COLOR(BLU)
7.xx A                                  4  2'Periode. . .:'
7.xx A            B2RPER    R        B  4 16REFFLD(RBRPER)
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      RANGE(0 13)
     A                                      CHKMSGID(RMI0080 *LIBL/QMFOKON)
     A                                      OVRDTA
7.xx A            B2RAAR    R        B  4 19REFFLD(RBRAAR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  4 44'Inngående balanse:'
     A  31                                  DSPATR(ND)
7.xx A            B2SUMI    R        O  4 63REFFLD(RWSUMR)
     A  31                                  DSPATR(ND)
     A                                      OVRDTA
7.xx A                                  5 44'Utgående  balanse:'
     A  31                                  DSPATR(ND)
7.xx A            B2SUMU    R        O  5 63REFFLD(RFRFR/RWSUMR *LIBL/RFRF)
     A  31                                  DSPATR(ND)
     A                                      OVRDTA
7.xx A                                  6  4'Konto'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 12'Bærer'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 20'Avd'
7.xx A                                      COLOR(WHT)
7.xx A                                  6 24'Bil.nr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  4'Konto'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 10'Bærer'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 16'Avd'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 20'Bil.nr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 29'Dato'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 36'Per'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 40'Bk'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 43'Bilagstekst      '
7.xx A                                      COLOR(WHT)
7.xx A                                  9 61'Mva'
7.xx A                                      COLOR(WHT)
7.xx A                                  9 70'Beløp'
7.xx A**                                9  2'V Konto Bærer Avd Bil.nr.  Dato  P-
7.xx A**                                    .er Bk Bilagstekst       Mva      B-
7.xx A**                                    eløp '
     A                                      COLOR(WHT)
7.xx A                                  7  2'+'
7.xx A            B2KONT    R        B  7  4REFFLD(RBKONT)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  7 10'+'
7.xx A            B2SPES    R        B  7 12REFFLD(RBSPES)
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  7 17'+'
7.xx A            B2AVDE    R        B  7 19REFFLD(RBAVDE)
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2BILN    R        B  7 24REFFLD(RBBILN)
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20020905  150628  KOB         REL-V5R1M0  5722-WDS
     A                                 23  3'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F4=Spørring'
     A                                      COLOR(BLU)
     A                                 23 31'F5=Forny'
     A                                      COLOR(BLU)
     A*
     A*  Bilde C2 - Vindu for endring
     A*
     A          R C2WIN
     A*%%TS  SD  20101208  110019  SST61P      REL-V5R4M0  5722-WDS
     A                                      WINDOW(&W_AROW &W_ACOL 19 61)
     A                                      CA02
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         2S 0P
     A            W_ACOL         3S 0P
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  1 20'Vis hovedbokspost'
     A                                      COLOR(WHT)
     A                                  3  2'Konto . . . . .:'
     A            RBKONT    R        O  3 19REFFLD(RBKONT)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A            RHTEXT    R        O  3 25REFFLD(RHTEXT)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                  4  2'Kostnadsbærer .:'
     A            RBSPES    R        O  4 19REFFLD(RBSPES)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A            REBTXT    R        O  4 24REFFLD(REBTXT)
     A                                      OVRDTA
     A                                  5  2'Avdeling. . . .:'
     A            RBAVDE    R        O  5 19REFFLD(RBAVDE)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A            RAGTXT    R        O  5 24REFFLD(RAGTXT)
     A                                      OVRDTA
     A                                  6  2'Bilagsnr. . . .:'
     A            RBBILN    R        O  6 19REFFLD(RBBILN)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A  73                                  COLOR(YLW)
6.NU A**73        C2NOTB         8A  O  6 26DSPATR(RI)
6.NU A  73        C2NOTB         8A  O  6 28DSPATR(RI)
     A                                      OVRDTA
6.NU A**                                6 41'Periode. .:'
6.NU A                                  6 43'Periode. .:'
6.NU A**          RBRPER    R        O  6 53REFFLD(RBRPER)
6.NU A            RBRPER    R        O  6 55REFFLD(RBRPER)
     A                                      OVRDTA
     A                                      DSPATR(UL)
6.NU A**          RBRAAR    R        O  6 56REFFLD(RBRAAR)
6.NU A            RBRAAR    R        O  6 58REFFLD(RBRAAR)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                  7  2'Bilagdato . . .:'
     A            C2BDAT         6Y 0O  7 19DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  7 41'Journal. .:'
     A            RBJOUR    R        O  7 53DSPATR(UL)
     A                                      OVRDTA
     A                                  8  2'Bilagstekst . .:'
     A            C2BILK    R        O  8 19REFFLD(RBBILK)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A            C2TEXT    R        O  8 22REFFLD(RBTEXT)
     A                                      OVRDTA
     A                                      DSPATR(UL)
     A                                  9  2'Beløp . . . . .:'
     A            RBNETO    R        O  9 19DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                  9 41'MVA-kode. . . .:'
     A            RBMVAK    R        O  9 58DSPATR(UL)
     A                                      OVRDTA
     A                                 10  2'Grunnlag mva. .:'
     A            RBMVAG    R        O 10 19REFFLD(RBMVAG)
     A                                      EDTCDE(L)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 10 41'%-andel gr.lag.:'
     A            C2AMVA    R    3 00O 10 58REFFLD(RBAMVA)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                 11  2'Beregnet mva. .:'
     A            RBMVAB    R        O 11 19DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 11 41'MVA-% . . . . .:'
     A            C2PMVA    R    2 00O 11 58REFFLD(RBPMVA)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                 12  2'Mengde. . . . .:'
     A            RBMNGK    R        O 12 19DSPATR(UL)
     A                                      OVRDTA
     A            RBMNGD    R        O 12 23DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 12 41'MVA-overstyrt .:'
     A            RBMVAO    R        O 12 58DSPATR(UL)
     A                                      OVRDTA
     A                                 13  2'Valuta. . . . .:'
     A            RBVALK    R        O 13 19DSPATR(UL)
     A                                      OVRDTA
     A            RBVALB    R        O 13 23DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 13 41'MVA-oppgjør . .:'
     A            RBMMRK    R        O 13 58DSPATR(UL)
     A                                      OVRDTA
     A                                 14  2'Prosjekt/U.Pro.:'
     A            RBAKTI    R        O 15 19DSPATR(UL)
     A                                      OVRDTA
     A                                 14 41'MVA periode . .:'
     A            RBMVAP    R        O 14 58DSPATR(UL)
     A                                      OVRDTA
     A                                 16  2'Lev.fakturanr .:'
     A            RBLFAK    R        O 16 19REFFLD(RBLFAK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 18  2'F2=Notatark bilag'
     A                                      COLOR(BLU)
     A                                 18 21'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A            RBARBO    R        O 15 26
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            RBUPRO    R        O 14 26
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            RBHPRO    R        O 14 19
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 15  2'Aktiv./Arb.Ord.:'
