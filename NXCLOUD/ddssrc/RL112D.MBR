     A*%%TS  SD  20050519  234405  KOB         REL-V5R2M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RL110D
     A* Beskrivelse . . : Leverandører, vedlikehold av leverandørposter
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       130802 KOB  Nytt program
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A*  Bilde B1 - subfile 1
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20030225  123106  KOB         REL-V5R2M0  5722-WDS
     A            B1BDAT    R        H      REFFLD(ROBDAT)
     A            B1TIST    R        H      REFFLD(ROTIST)
     A            B1RAAR    R        H      REFFLD(RORAAR)
     A            B1RPER    R        H      REFFLD(RORPER)
     A            B1REKL    R        H      REFFLD(ROREKL)
     A            B1BETK    R        H      REFFLD(ROBETK)
     A            B1VALG    R        B  9  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(1 2 5 6 7 8 9 0)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1BILN    R        O  9  4REFFLD(ROBILN)
     A  73                                  COLOR(YLW)
     A  70                                  COLOR(WHT)
     A  71                                  COLOR(RED)
6.NU A            B1BIDA         6Y 0O  9 13EDTCDE(Z)
     A  70                                  COLOR(WHT)
     A  71                                  COLOR(RED)
6.NU A            B1BILK    R        O  9 20REFFLD(ROBILK)
     A  70                                  COLOR(WHT)
     A  71                                  COLOR(RED)
6.NU A            B1TEXT        10A  O  9 23
     A  70                                  COLOR(WHT)
     A  71                                  COLOR(RED)
6.NU A            B1REFN    R        O  9 34REFFLD(ROREFN)
     A  70                                  COLOR(WHT)
     A  71                                  COLOR(RED)
     A            B1FDAT         6Y 0O  9 43EDTCDE(Z)
     A  70                                  COLOR(WHT)
     A  71                                  COLOR(RED)
     A            B1BELØ        11Y 2O  9 50EDTCDE(J)
     A  70                                  COLOR(WHT)
     A  71                                  COLOR(RED)
     A            B1REST        11Y 2O  9 66EDTCDE(J)
     A  70                                  COLOR(WHT)
     A  71                                  COLOR(RED)
     A*
     A*  Bilde B2 - subfile kontroll
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20041124  133111  KOB530      REL-V5R2M0  5722-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA02
     A                                      CA03
     A                                      CA04
     A                                      CA05
     A                                      CF06
     A                                      CF07
     A                                      CF08
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
6.11 A            B2FIRM         3  0O  1 33DSPATR(ND)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av leverandørpostering-
     A                                      er'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Lever:'
     A            B2LEVR    R        O  4  9REFFLD(RLLEVR)
     A                                      DSPATR(UL)
     A                                      TEXT('Leverandørnummer')
     A            B2NAVN    R        O  4 16REFFLD(RLNAVN)
     A                                      DSPATR(UL)
     A                                      TEXT('Leverandørnavn')
     A                                  4 58'Regnskapsår. .:'
     A            B2RAAR         4Y 0B  4 74COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A  94        B2NOTB         8   O  5 16
     A                                      DSPATR(RI)
     A                                      OVRDTA
     A                                  5 58'Periode. . . .:'
     A            B2RPER         2Y 0B  5 74COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                  6  2'2=Endre  5=Vis  6=Vis motpost  7=V-
     A                                      is bilag'
     A                                      COLOR(BLU)
     A                                  7  2'8=Vis faktura  9=Vis arkiv'
     A                                      COLOR(BLU)
     A                                  7 58'Saldo:'
     A            B2SALD    R        O  7 66REFFLD(RLSALD)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(J)
     A                                      DSPATR(UL)
     A                                  8  2'V'
     A                                      COLOR(WHT)
     A                                  8  4'Bil.nr'
     A                                      COLOR(WHT)
     A                                  8 13'Dato'
     A                                      COLOR(WHT)
6.NU A                                  8 20'Bk'
     A                                      COLOR(WHT)
6.NU A                                  8 23'Tekst'
     A                                      COLOR(WHT)
6.NU A                                  8 29'Val.'
     A                                      COLOR(WHT)
6.NU A                                  8 34'Ref.'
     A                                      COLOR(WHT)
     A                                  8 43'Forfall'
     A                                      COLOR(WHT)
     A                                  8 56'Beløp'
     A                                      COLOR(WHT)
     A                                  8 68'Restbeløp'
     A                                      COLOR(WHT)
     A*
     A*  Bilde B2 - funksjonstaster
     A*
     A          R B2CMD
     A*%%TS  SD  20030629  212412  KOB         REL-V5R2M0  5722-WDS
     A                                      CA03
     A                                 23  2'F2=Notatark'
     A                                      COLOR(BLU)
     A                                 23 15'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 30'F6=Åpne/Alle poster'
     A                                      COLOR(BLU)
     A                                 23 51'F7=Valuta/NOK'
     A                                      COLOR(BLU)
     A                                 23 66'F8=Saldo/Kjøp'
     A                                      COLOR(BLU)
     A*
     A*  Bilde C2 - vindu for vedlikehold
     A*
     A          R C2WIN
     A*%%TS  SD  20050519  234405  KOB         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 18 75)
     A                                      CA02
     A                                      CA03
     A                                      CA04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            C2NYEN        11A  O  2  2DSPATR(RI)
     A                                      OVRDTA
     A                                  2 29'Leverandørposteringer'
     A                                      COLOR(WHT)
     A                                  3  1'Bilagsnummer . .:'
     A            C2BILN    R        O  3 19REFFLD(ROBILN)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A  73                                  COLOR(YLW)
6.NU A  73        C2NOTB         8   O  3 28
     A                                      DSPATR(RI)
     A                                      OVRDTA
     A                                  4  1'Bilags-kode. . .:'
     A            C2BILK    R        B  4 19REFFLD(ROBILK)
     A  33                                  DSPATR(PC)
     A  33 81                               ERRMSGID(RM01010 *LIBL/QMFOKON 81)
     A                                      COLOR(YLW)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2TEXT    R        B  4 22REFFLD(ROTEXT)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A                                  4 43'Bilagsdato . . .:'
     A            C2BDAT         6Y 0O  4 61DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  5  1'Forfallsdato . .:'
     A            C2FDAT         6Y 0B  5 19
     A  37                                  DSPATR(PC)
     A  37 81                               ERRMSGID(RMH0150 *LIBL/QMFOKON 81)
     A N30                                  COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A                                  5 43'Regnskaps-per/år:'
     A            C2RPER    R        O  5 61REFFLD(RORPER)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A            C2RAAR    R        O  5 64REFFLD(RORAAR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  6  1'Betalingsbet.. .:'
     A            C2BETB    R        B  6 19REFFLD(ROBETB)
     A  34                                  DSPATR(PC)
     A  34 81                               ERRMSGID(RM01030 *LIBL/QMFOKON 81)
     A  30                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C2CTXT    R        O  6 22REFFLD(RACTXT)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  6 43'Journalliste-nr.:'
     A            C2JOUR    R        O  6 61REFFLD(ROJOUR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  7  1'Reklamasjonskode:'
     A            C2REKL    R        B  7 19REFFLD(ROREKL)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  7 43'Samlebilag-nr. .:'
     A            C2SAMB    R        O  7 61REFFLD(ROSAMB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                  8  1'Referansenummer.:'
     A            C2REFN    R        B  8 19REFFLD(ROREFN)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                  8 43'Beløp. . . . . .:'
     A            C2BELØ    R        O  8 61REFFLD(ROBELØ)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                  9  1'Avdeling . . . .:'
     A            C2AVDE    R        B  9 19REFFLD(ROAVDE)
     A  35                                  DSPATR(PC)
     A  35 81                               ERRMSGID(RM01070 *LIBL/QMFOKON 81)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C2GTXT    R   18   O  9 24REFFLD(RAGTXT)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9 43'Restbeløp. . . .:'
     A            C2REST    R        B  9 61REFFLD(ROREST)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      EDTCDE(L)
     A                                 10  1'Attestasjon. . .:'
     A            C2ATTS    R        B 10 19REFFLD(ROATTS)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A            C2ADAT         6Y 0O 10 21DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTWRD('  .  .  ')
     A            C2ATTU    R        O 10 30REFFLD(ROATTU)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 10 43'Delbetalt. . . .:'
     A            C2DELB    R        O 10 61REFFLD(RODELB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 11  1'Betalingskode ..:'
     A            C2BETK    R        B 11 19REFFLD(ROBETK)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 11 43'Valuta-kode. . .:'
     A            C2VALK    R        O 11 61REFFLD(ROVALK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 12  1'Betalingsref.-1.:'
     A            C2RRF1    R        B 12 19REFFLD(RORRF1)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 12 43'Valutabeløp. . .:'
     A            C2VALB    R        O 12 61REFFLD(ROVALB)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 13  1'Betalingsref.-2.:'
     A            C2RRF2    R        B 13 19REFFLD(RORRF2)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 13 43'Valuta-rest. . .:'
     A            C2VALR    R        B 13 61REFFLD(ROVALR)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(L)
     A                                 14  1'Deklarasjonskode:'
     A            C2DECK    R        B 14 19REFFLD(RODECK)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 14 43'Bet.forslags-nr.:'
     A            C2BFNR    R        O 14 61REFFLD(ROBFNR)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 15  1'KID-felt . . .:'
     A            C2KIDI    R        B 15 17REFFLD(ROKIDI)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      CHECK(FE)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 15 43'Bet.forslags-lin:'
     A            C2BFLI    R        O 15 61REFFLD(ROBFLI)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 16 43'Lev.fakturanr. .:'
     A            C2LFAK    R        B 16 61REFFLD(ROLFAK)
     A                                      DSPATR(UL)
     A                                      OVRDTA
     A                                 17  2'F2=Notatark bilag'
     A                                      COLOR(BLU)
6.NU A                                 17 22'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 17 37'F4=Spørring'
     A                                      COLOR(BLU)
     A          R E2WIN
     A*%%TS  SD  20021028  015931  KOB         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 16 60)
     A                                      CA03
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  1 22'Reskontrostatistikk'
     A                                  2  1'Kjøp i år .:'
     A            E2KJØP    R        O  2 14REFFLD(RLKJØP)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                  2 32'Saldo i år .:'
     A            E2SALD    R        O  2 46REFFLD(RLSALD)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                  3  1'Kjøp i fjor:'
     A            E2KJØF    R        O  3 14REFFLD(RLKJØF)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                  3 32'Saldo i fjor:'
     A            E2SAIF    R        O  3 46REFFLD(RLSAIF)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                  5  1'Kredittid .:'
     A            E2KVTI         5Y 0O  5 14EDTCDE(3)
     A                                      DSPATR(UL)
     A                                  6  1'Aldersfordeling:'
     A                                  6 38'Antall poster:'
     A                                  7  1'-Ikke forfalt :'
     A            E2ALD0    R        O  7 17REFFLD(ROBELØ)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                  7 38'- Totalt. . . .:'
     A            E2POST         5Y 0O  7 55EDTCDE(3)
     A                                      DSPATR(UL)
     A                                  8  1'-Ikke saldert'
     A                                  8 15':'
     A            E2ALDN    R        O  8 17REFFLD(ROBELØ)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                  8 38'- Debet . . . .:'
     A            E2DEBE         5Y 0O  8 55EDTCDE(3)
     A                                      DSPATR(UL)
     A                                  9  1'-   0 til'
     A            E23LA1    R        O  9 11REFFLD(RA3LA1)
     A                                      DSPATR(UL)
     A                                  9 15':'
     A            E2ALD1    R        O  9 17REFFLD(ROBELØ)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                  9 38'- Kredit. . . .:'
     A            E2KRED         5Y 0O  9 55EDTCDE(3)
     A                                      DSPATR(UL)
     A                                 10  1'-'
     A            E23LS2    R        O 10  3REFFLD(RA3LA1)
     A                                      DSPATR(UL)
     A                                 10  7'til'
     A            E23LA2    R        O 10 11REFFLD(RA3LA2)
     A                                      DSPATR(UL)
     A                                 10 15':'
     A            E2ALD2    R        O 10 17REFFLD(ROBELØ)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                 10 38'- Åpne poster .:'
     A            E2REST         5Y 0O 10 55EDTCDE(3)
     A                                      DSPATR(UL)
     A                                 11  1'-'
     A            E23LS3    R        O 11  3REFFLD(RA3LA2)
     A                                      DSPATR(UL)
     A                                 11  7'til'
     A            E23LA3    R        O 11 11REFFLD(RA3LA3)
     A                                      DSPATR(UL)
     A                                 11 15':'
     A            E2ALD3    R        O 11 17REFFLD(ROBELØ)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                 11 38'- Reklamasjoner:'
     A            E2REKL         5Y 0O 11 55EDTCDE(3)
     A                                      DSPATR(UL)
     A                                 12  1'- Over'
     A            E23LA4    R        O 12 11REFFLD(RA3LA4)
     A                                      DSPATR(UL)
     A                                 12 15':'
     A            E2ALD4    R        O 12 17REFFLD(ROBELØ)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                 13  1'Sum. . . . . .:'
     A            E2SALT    R        O 13 17REFFLD(ROBELØ)
     A                                      DSPATR(UL)
     A                                      EDTCDE(L)
     A                                 15  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
