     A*%%TS  SD  20051201  145707  HKO550      REL-V5R3M0  5722-WDS
     A*%%EC
7.xx A*          290116 bsa  Justeringer basert på ny GUI og regler
     A*-----------------------------------------------------------------
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/FFRF FFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1WIN
     A*%%TS  SD  20051201  145707  HKO550      REL-V5R3M0  5722-WDS
     A                                      WINDOW(*DFT 17 71)
     A                                      CA03
     A                                      CF04
     A                                      CF06
     A                                      CF07
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Hent ekstern kundeordre '
     A                                      DSPATR(RI)
7.xx A                                  4  2'Eksternt system  +'
     A            B1KODE    R        B  4 21REFFLD(NKKODE)
     A                                      OVRDTA
     A  31                                  ERRMSGID(NMK0020 *LIBL/QMFOFAK 31)
     A  49                                  ERRMSGID(FM00610 *LIBL/QMFOFAK 49)
     A                                      COLOR(YLW)
     A            B1BESK    R        O  4 28REFFLD(NKBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  6  2'Kunde. . . . . . +'
     A            B1KUND    R        B  6 21REFFLD(NOKUND)
     A  33                                  ERRMSGID(FRK0020 *LIBL/QMFOFAK 33)
     A  50                                  ERRMSGID(FM00810 *LIBL/QMFOFAK 50)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            B1KNAV        30A  O  6 28COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  7  2'Kundeprosjekt. . +'
     A            B1KPRO    R        B  7 21REFFLD(NOKPRO)
     A  34                                  ERRMSGID(FML0020 *LIBL/QMFOFAK 34)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            B1PNAV    R        O  7 28REFFLD(FLNAVN)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  8  2'Leveringstype  . +'
     A            B1LETY    R        B  8 21REFFLD(FÅLETY)
     A  35                                  ERRMSGID(VMY0010 *LIBL/QMFOFAK 35)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            B1LBES    R        O  8 28REFFLD(VYBESK)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                 10  2'Kilde/fil:'
     A                                      COLOR(WHT)
     A                                 11  2'IFS-mappe. . . . .'
     A            B1MAPP    R        B 11 21REFFLD(NKMAPP)
     A  37                                  ERRMSGID(NMO0300 *LIBL/QMFOFAK 37)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 12  2'WEB-link/adresse .'
     A            B1LINK    R        B 12 21REFFLD(NKLINK)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 13  2'Filnavn. . . . . .'
     A            B1FILE    R        B 13 21REFFLD(NKFILE)
     A  38                                  ERRMSGID(NMO0310 *LIBL/QMFOFAK 38)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A                                 16  2'F3=Gå tilbake   F6=Opprett kunde  -
     A                                       F7=Opprett kundeprosjekt'
     A                                      COLOR(BLU)
7.01 A          R B9WIN3
7.01 A*%%TS  SD  20031124  152303  ASK         REL-V5R2M0  5722-WDS
7.01 A                                      WINDOW(*DFT 13 40)
7.01 A                                      CA03
7.01 A                                      CA12
7.01 A                                  2  2'Feil i ordre innlesing !!'
7.01 A                                      DSPATR(RI)
7.01 A                                      COLOR(RED)
7.01 A                                  5  2'En eller flere ordre feilet'
7.01 A                                  6  2'ved innlesing. Gå til loggen for'
7.01 A                                  7  2'å finne ut mer             '
7.01 A                                 10  2'**** **** **** FEIL !!! **** **** -
7.01 A                                       ****'
7.01 A                                      COLOR(RED)
7.01 A                                 12  2'F3=Gå tilbake'
7.01 A                                      COLOR(BLU)
     A*
     A          R ASUME                     ASSUME
     A                                 24 79' '
