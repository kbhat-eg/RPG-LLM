     A*%%TS  SD  20021204  234219  KOB         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASOKON
     A* Format. . . . . : RL910D
     A* Beskrivelse . . : Leverandør, saldering
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       051202 KOB  Nytt program
7.xx A* 7.xx  010316  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/RFRF RFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A*
     A          R B1SFL                     SFL
     A*%%TS  SD  20010130  231007  KOB         REL-V4R4M0  5769-PW1
     A            B1VALG    R        B 10  2REFFLD(RÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(4 5 8 0)
     A                                      CHKMSGID(RM00010 *LIBL/QMFOKON)
     A            B1LEVR    R        O 10  4REFFLD(RLLEVR)
     A            B1NAVN    R        O 10 11REFFLD(RLNAVN)
     A            B1STED        23   O 10 42
     A            B1SALD    R        O 10 66REFFLD(RLSALD)
     A*
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20021204  234219  KOB         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA09
     A                                      CA12
     A                                      OVERLAY
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Økonomi'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Leverandørreskontro - saldering'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Frisøk .'
     A            B2STEK        35A  B  5 11COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A                                  6  4'Lever  Leverandørnavn'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  2'8=Avkryssing'
     A                                      COLOR(BLU)
7.xx A                                  9  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  9  4'Lever '
7.xx A                                      COLOR(WHT)
7.xx A                                  9 11'Leverandørnavn    '
7.xx A                                      COLOR(WHT)
7.xx A                                  9 42'Postnr/sted       '
     A                                      COLOR(WHT)
7.xx A                                  9 69'Sum poster'
     A                                      COLOR(WHT)
7.xx A            B2LEVR    R        B  7  4REFFLD(RLLEVR)
     A  38                                  ERRMSGID(RMK0005 *LIBL/QMFOKON 38)
     A  31                                  ERRMSGID(RMK0010 *LIBL/QMFOKON 31)
     A  32                                  ERRMSGID(RMK0030 *LIBL/QMFOKON 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A            B2ALFA    R        B  7 11REFFLD(RLALFA)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A*
     A          R B2CMD
     A*%%TS  SD  20011123  000309  KOB         REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 19'F5=Forny'
     A                                      COLOR(BLU)
     A                                 23 31'F9=Gjenta søk'
     A                                      COLOR(BLU)
      *
     A          R ASUME                     ASSUME
     A                                 24 79' '
