     A*%%TS  SD  20080522  131639  ASP_NOR     REL-V5R4M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLOGI
     A* Format. . . . . : MM100D
     A* Beskrivelse . . : Matrise - Prognoseparameter
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
     A*       250804 bø   Nytt program
7.xx A* 7.xx  210316 bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A                                      HOME(21)
     A          R B1SFL                     SFL
     A*%%TS  SD  20050131  105526  BOF530      REL-V5R2M0  5722-WDS
7.xx A            B1VALG    R        B  9  2REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5 6 7 8 9)
     A                                      CHKMSGID(LM00010 *LIBL/QMFLAGR)
7.xx A            B1VSOR    R        O  9  4REFFLD(MMVSOR)
7.xx A            B1OGRP    R        O  9 15REFFLD(MMOGRP)
7.xx A            B1HGRP    R        O  9 18REFFLD(MMHGRP)
7.xx A            B1UGRP    R        O  9 21REFFLD(MMUGRP)
7.xx A            B1LDOR    R        O  9 25REFFLD(MMLDOR)
7.xx A            B1VARE    R   -7   O  9 32REFFLD(MMVARE)
7.xx A            B1LAGE    R        O  9 41REFFLD(MMLAGE)
7.xx A            B1LAGR    R        H      REFFLD(MMLAGR)
7.xx A            B1CRDO    R        O  9 45REFFLD(MMCRDO)
     A                                      EDTCDE(Z)
7.xx A            B1ANTP    R        O  9 50REFFLD(MMANTP)
7.xx A            B1KDAT    R        O  9 63REFFLD(LÅDATR)
     A                                      EDTWRD('  .  .  ')
7.xx A            B1AKTI         1A  O  9 75
7.xx A            B1ANTU    R        O  9 56REFFLD(MMANTU)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20050923  100909  BOF540      REL-V5R3M0  5722-WDS
7.xx A                                      SFLSIZ(0014)
7.xx A                                      SFLPAG(0013)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CF04
     A                                      CA05
     A                                      CF06
     A                                      CF09
     A                                      CA12
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A                                      SFLCSRRRN(&W_CURN)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_VIRN         4S 0H      SFLRCDNBR(CURSOR)
     A            W_CURN         5S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Logistikk    '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Prognoseparameter   '
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  4'Varesort.'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 18'Varegruppe'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 33'Lev.nr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 42'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre  3=Kopier  4=Slett  5=Vis '
     A                                      COLOR(BLU)
7.xx A                                  7  2'7=Kjør prognose    8=Vis reslutat -
     A                                       9=Slett reslutat'
     A                                      COLOR(BLU)
7.xx A                                  8  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  8  4'Varesort.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 15'Og'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 18'Hg'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 21'Ugr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 25'Lev.nr'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 32'Varenr.'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 40'Lag'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 45'C'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 49'A.år'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 55'A.uke'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 62'Sist kjørt'
7.xx A                                      COLOR(WHT)
7.xx A                                  8 75'P'
7.xx A                                      COLOR(WHT)
7.xx A                                  5  2'+'
7.xx A            B2VSOR    R        B  5  4REFFLD(MMVSOR)
     A                                      COLOR(YLW)
     A  22                                  DSPATR(PC)
     A                                      OVRDTA
7.xx A                                  5 15'+'
7.xx A            B2OGRP    R        B  5 17REFFLD(MMOGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  5 20'+'
7.xx A            B2HGRP    R        B  5 22REFFLD(MMHGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  5 25'+'
7.xx A            B2UGRP    R        B  5 27REFFLD(MMUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  5 31'+'
7.xx A            B2LDOR    R        B  5 33REFFLD(MMLDOR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  5 40'+'
7.xx A            B2VARE    R   -7   B  5 42REFFLD(MMVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  5 51'+'
7.xx A            B2LAGE    R        B  5 53REFFLD(MMLAGE)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20050426  084648  BOF530      REL-V5R2M0  5722-WDS
     A                                 23  2'F3=Gå tilbake   F5=Forny   F6=Oppr-
     A                                      ett'
     A                                      COLOR(BLU)
     A                                 23 43'F9=Erstatningsvarer'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  20080521  132145  ASP_NOR     REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A                                      PUTOVR
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Logistikk    '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av prognoseparameter'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  2'Varesortiment  . . . . . . +'
     A            C1VSOR    R        B  4 31REFFLD(MMVSOR)
     A                                      COLOR(YLW)
     A  34                                  ERRMSGID(MMA0040 *LIBL/QMFLAGR 34)
     A                                      OVRDTA
7.xx A                                  5  2'Overgruppe . . . . . . . . +'
     A            C1OGRP    R        B  5 31REFFLD(MMOGRP)
     A  31                                  ERRMSGID(MMA0050 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1OGNA        30A  O  5 43OVRDTA
7.xx A                                  6  2'Hovedgruppe  . . . . . . . +'
     A            C1HGRP    R        B  6 31REFFLD(MMHGRP)
     A  32                                  ERRMSGID(MMA0060 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1HGNA        30A  O  6 43OVRDTA
7.xx A                                  7  2'Undergruppe  . . . . . . . +'
     A            C1UGRP    R        B  7 31REFFLD(MMUGRP)
     A  33                                  ERRMSGID(MMA0070 *LIBL/QMFLAGR 33)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1UGNA        30A  O  7 43OVRDTA
7.xx A                                  8  2'Leverandør . . . . . . . . +'
     A            C1LDOR    R        B  8 31REFFLD(MMLDOR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1LENA        30A  O  8 43OVRDTA
7.xx A                                  9  2'Varenr.  . . . . . . . . . +'
     A            C1VARE    R   -7   B  9 31REFFLD(MMVARE)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A            C1VANA        30A  O  9 43OVRDTA
     A                                 10  2'Cross docking  . . . . . . .'
     A            C1CRDO    R        B 10 31REFFLD(MMCRDO)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                      VALUES(0 1)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                 10 33'1=ja'
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
7.xx A**                                5 35'F4'
7.xx A**                                6 35'F4'
7.xx A**                                7 35'F4'
7.xx A**                                8 38'F4'
7.xx A**                                9 40'F4'
7.xx A**                                4 42'F4'
     A          R C1MSG
     A*%%TS  SD  20040825  140505  BOF         REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 7 30)
     A                                      CA03
     A                                      CA12
     A                                  2  2'Matrise       finnes fra før'
     A                                  6  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20080508  084613  ASP_NOR     REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      CSRLOC(W_AROW     W_ACOL)
     A                                      RTNCSRLOC(*WINDOW &W_AROW &W_ACOL)
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_AROW         3S 0H
     A            W_ACOL         3S 0H
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Logistikk    '
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av prognoseparameter'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Varesortiment  . . . . . . .'
     A            C1VSOR    R        O  4 31REFFLD(MMVSOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1VSNA        30A  O  4 43OVRDTA
     A                                  5  2'Overgruppe   . . . . . . . .'
     A            C1OGRP    R        O  5 31REFFLD(MMOGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1OGNA        30A  O  5 43OVRDTA
     A                                  6  2'Hovedgruppe  . . . . . . . .'
     A            C1HGRP    R        O  6 31REFFLD(MMHGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1HGNA        30A  O  6 43OVRDTA
     A                                  7  2'Undergruppe  . . . . . . . .'
     A            C1UGRP    R        O  7 31REFFLD(MMUGRP)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1UGNA        30A  O  7 43OVRDTA
     A                                  8  2'Leverandør . . . . . . . . .'
     A            C1LDOR    R        O  8 31REFFLD(MMLDOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1LENA        30A  O  8 43OVRDTA
     A                                  9  2'Varenr.  . . . . . . . . . .'
     A            C1VARE    R   -7   O  9 31REFFLD(MMVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C1VANA        30A  O  9 43OVRDTA
     A                                 10  2'Cross Docking  . . . . . . .'
     A            C1CRDO    R        O 10 31REFFLD(MMCRDO)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
7.xx A                                 12  2'Valg av lager  . . . . . . +'
7.xx A**                               12 34'F4'
     A                                 12 37'blank alle'
     A            C2LAGE    R        B 12 31REFFLD(MMLAGE)
     A                                      COLOR(YLW)
     A  31                                  ERRMSGID(HMR0250 *LIBL/QMFLAGR 31)
     A                                      OVRDTA
     A            C2LAGR        30A  O 12 48COLOR(YLW)
     A                                      OVRDTA
7.xx A                                 13  2'Leveringstype  . . . . . . +'
     A            C2LETY    R        B 13 32REFFLD(LÅLETY)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A  36                                  ERRMSGID(MMB0060 *LIBL/QMFLAGR 36)
     A                                      OVRDTA
     A            C2LTYP        20A  O 13 37COLOR(YLW)
     A                                      OVRDTA
     A                                 15  2'Antall uker trendkorreksjon '
     A            C2ANTU    R        B 15 31REFFLD(MMANTU)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                 15 35'brukes'
     A            C2BRKU    R        B 15 42REFFLD(MMBRKU)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                      VALUES(0 1 2 3 4 5 6 7 8 9)
     A                                 15 45'(0/1/.. eller antall uker gammel)'
     A                                 14  2'Antall år prognosegrunnlag .'
     A            C2ANTP    R        B 14 31REFFLD(MMANTP)
     A                                      COLOR(YLW)
     A  31                                  ERRMSGID(MMM0050 *LIBL/QMFLAGR 31)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                 16  2'Antall uker glatting . . . .'
     A            C2ANTG    R        B 16 31REFFLD(MMANTG)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                 16 35'brukes'
     A            C2BRKG    R        B 16 42REFFLD(MMBRKG)
     A                                      VALUES(0 1)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                 16 45'(0/1)'
     A                                 18  2'Passiv v/automatisk kjøring.'
     A            C2AKTI    R        B 18 31REFFLD(MMAKTI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                      OVRDTA
     A                                 20 41'Sist kjørt dato .'
     A            C2KDAT         6Y 0O 20 59COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 20  2'Opprettet dato  . . . . . . '
     A            C2ODAT         6Y 0O 20 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 21  2'Sist endret dato  . . . . . '
     A            C2EDAT         6Y 0O 21 31COLOR(YLW)
     A                                      EDTWRD('  .  .  ')
     A                                      OVRDTA
     A                                 22  2'Endret av . . . . . . . . . '
     A            C2EUSR    R        O 22 31REFFLD(MMUSER)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A            C2DBAX    R        H      REFFLD(MMDBAX)
     A            C2DBAY    R        H      REFFLD(MMDBAY)
     A            C2DBAZ    R        H      REFFLD(MMDBAZ)
     A            C2DBBX    R        H      REFFLD(MMDBAX)
     A            C2DBBY    R        H      REFFLD(MMDBAY)
     A            C2DBBZ    R        H      REFFLD(MMDBAZ)
     A            C2DBCX    R        H      REFFLD(MMDBAX)
     A            C2DBCY    R        H      REFFLD(MMDBAY)
     A            C2DBCZ    R        H      REFFLD(MMDBAZ)
     A            C2DSAX    R        H      REFFLD(MMDBAX)
     A            C2DSAY    R        H      REFFLD(MMDBAY)
     A            C2DSAZ    R        H      REFFLD(MMDBAZ)
     A            C2DSBX    R        H      REFFLD(MMDBAX)
     A            C2DSBY    R        H      REFFLD(MMDBAY)
     A            C2DSBZ    R        H      REFFLD(MMDBAZ)
     A            C2DSCX    R        H      REFFLD(MMDBAX)
     A            C2DSCY    R        H      REFFLD(MMDBAY)
     A            C2DSCZ    R        H      REFFLD(MMDBAZ)
     A            C2LAGT    R        H      REFFLD(MMLAGT)
     A                                 23  2'F3=Gå tilbake   F4=Spørring '
     A                                      COLOR(BLU)
     A                                 11 80' '
     A                                 18 34'(0/1)'
7.xx A**                               13 34'F4'
     A          R D1WIN
     A*%%TS  SD  20041202  142747  BOF530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette Matrise'
     A                                      DSPATR(RI)
     A                                  4  2'Varesortiment  . .'
     A            D1VSOR    R        O  4 21REFFLD(MMVSOR)
     A                                  5  2'Overgruppe . . . .'
     A            D1OGRP    R        O  5 21REFFLD(MMOGRP)
     A                                  6  2'Hovedgruppe  . . .'
     A            D1HGRP    R        O  6 21REFFLD(MMHGRP)
     A                                  7  2'Undergruppe  . . .'
     A            D1UGRP    R        O  7 21REFFLD(MMUGRP)
     A                                  8  2'Leverandør . . . .'
     A            D1LDOR    R        O  8 21REFFLD(MMLDOR)
     A                                  9  2'Varenr.  . . . . .'
     A            D1VARE    R   -7   O  9 21REFFLD(MMVARE)
     A                                 10  2'Cross docking  . .'
     A            D1CRDO    R        O 10 21REFFLD(MMCRDO)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D2WIN
     A*%%TS  SD  20041202  142747  BOF530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 15 56)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette resultat'
     A                                      DSPATR(RI)
     A                                  4  2'Varesortiment  . .'
     A            D2VSOR    R        O  4 21REFFLD(MMVSOR)
     A                                  5  2'Overgruppe . . . .'
     A            D2OGRP    R        O  5 21REFFLD(MMOGRP)
     A                                  6  2'Hovedgruppe  . . .'
     A            D2HGRP    R        O  6 21REFFLD(MMHGRP)
     A                                  7  2'Undergruppe  . . .'
     A            D2UGRP    R        O  7 21REFFLD(MMUGRP)
     A                                  8  2'Leverandør . . . .'
     A            D2LDOR    R        O  8 21REFFLD(MMLDOR)
     A                                  9  2'Varenr.  . . . . .'
     A            D2VARE    R   -7   O  9 21REFFLD(MMVARE)
     A                                 10  2'Cross docking  . .'
     A            D2CRDO    R        O 10 21REFFLD(MMCRDO)
     A                                 14  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20050203  133807  BOF530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 20 56)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                      RTNCSRLOC(&W_ARCD &W_AFLD)
     A            W_ARCD        10A  H
     A            W_AFLD        10A  H
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
7.xx A                                  3  2'Varesortiment  . +'
     A            K1VSOR    R        B  3 21REFFLD(MMVSOR)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A                                  4  2'Overgruppe . . . +'
     A            K1OGRP    R        B  4 21REFFLD(MMOGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  5  2'Hovedgruppe  . . +'
     A            K1HGRP    R        B  5 21REFFLD(MMHGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  6  2'Undergruppe  . . +'
     A            K1UGRP    R        B  6 21REFFLD(MMUGRP)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
7.xx A                                  7  2'Leverandør . . . +'
     A            K1LDOR    R        B  7 21REFFLD(MMLDOR)
     A                                      COLOR(YLW)
     A                                      CHECK(RB)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
7.xx A                                  8  2'Varenr.  . . . . +'
     A            K1VARE    R   -7   B  8 21REFFLD(MMVARE)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  9  2'Cross docking  . .'
     A            K1CRDO    R        B  9 21REFFLD(MMCRDO)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                      EDTCDE(Z)
     A                                 11  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                 12  2'Varesortiment  . .'
     A            MMVSOR    R        O 12 21OVRDTA
     A                                 13  2'Overgruppe . . . .'
     A            MMOGRP    R        O 13 21OVRDTA
     A                                 14  2'Hovedgruppe  . . .'
     A            MMHGRP    R        O 14 21OVRDTA
     A                                 15  2'Undergruppe  . . .'
     A            MMUGRP    R        O 15 21OVRDTA
     A                                 16  2'Leverandør . . . .'
     A            K2LDOR    R        O 16 21REFFLD(MMLDOR)
     A                                 17  2'Varenr.  . . . . .'
     A            K2VARE    R   -7   O 17 21REFFLD(MMVARE)
     A                                      OVRDTA
     A                                 18  2'Cross docking  . .'
     A            K2CRDO    R        O 18 21REFFLD(MMCRDO)
     A                                      OVRDTA
     A                                 19  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
7.xx A**                                5 24'F4'
7.xx A**                                4 24'F4'
7.xx A**                                6 25'F4'
7.xx A**                                8 30'F4'
7.xx A**                                7 28'F4'
7.xx A**                                3 32'F4'
     A          R L1WIN
     A*%%TS  SD  20050426  105412  BOF530      REL-V5R2M0  5722-WDS
     A                                      WINDOW(*DFT 16 60)
     A                                      CA03
     A                                      CF04
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kjør Prognose'
     A                                      DSPATR(RI)
     A                                  4  2'Varesortiment  . .'
     A            L1VSOR    R        O  4 21REFFLD(MMVSOR)
     A            L1VSNA        25A  O  4 32
     A                                  5  2'Overgruppe . . . .'
     A            L1OGRP    R        O  5 21REFFLD(MMOGRP)
     A            L1OGNA        25A  O  5 32
     A                                  6  2'Hovedgruppe  . . .'
     A            L1HGRP    R        O  6 21REFFLD(MMHGRP)
     A            L1HGNA        25A  O  6 32
     A                                  7  2'Undergruppe  . . .'
     A            L1UGRP    R        O  7 21REFFLD(MMUGRP)
     A            L1UGNA        25A  O  7 32
     A                                  8  2'Leverandør . . . .'
     A            L1LDOR    R        O  8 21REFFLD(MMLDOR)
     A            L1LENA        25A  O  8 32
     A                                  9  2'Varenummer   . . .'
     A            L1VARE    R   -7   O  9 21REFFLD(MMVARE)
     A            L1VANA        25A  O  9 32
     A                                 10  2'Cross docking  . .'
     A            L1CRDO    R        O 10 21REFFLD(MMCRDO)
     A                                 11  2'Lager  . . . . . .'
     A            L1LAGE         2A  O 11 21OVRDTA
     A            L1LAGR        30A  O 12 21OVRDTA
     A                                 15  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A* 5.52 220508 TSV POPUP FOR BLOKKERT KOMBINASJON SØKEVERDIER.
     A          R C1MS2
     A*%%TS  SD  20080522  131639  ASP_NOR     REL-V5R4M0  5722-WDS
     A                                      CA03
     A                                      CA12
     A                                      WINDOW(*DFT 6 50)
     A                                  5  1'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                  2  1'Varenr skal ikke brukes sammen med-
     A                                       andre utvalgs- '
     A                                      DSPATR(HI)
     A                                  3  1'kriterier. Varenr anngir en bestem-
     A                                      t vare!         '
     A                                      DSPATR(HI)
