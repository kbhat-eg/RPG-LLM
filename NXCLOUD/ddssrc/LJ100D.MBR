     A*%%TS  SD  20020924  124803  HKO         REL-V5R1M0  5722-WDS
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A* System. . . . . : ASLAGR
     A* Format. . . . . : LJ100D
     A* Beskrivelse . . : Faste bestillingstekster
     A*
     A* Spesialversjon. :
     A* Tilpasset for . :
     A*
     A* Ref.   Dato  Sign Endringsbeskrivelse
     A* ----- ------ ---- -------------------------------------------------
7.xx A* 7.xx  150316  bsa  Justeringer basert på ny GUI og regler
     A*
     A*
     A*
     A* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/LFRF LFRFR)
     A                                      PRINT
     A          R B1SFL                     SFL
     A*%%TS  SD  19931109  135243  AMD         REL-V2R2M0  5738-PW1
     A            B1VALG    R        B  8  2REFFLD(LÅVALG)
     A                                      COLOR(YLW)
     A                                      VALUES(0 2 3 4 5)
     A                                      CHKMSGID(LMA0003 *LIBL/QMFLAGR)
     A            B1NUMM    R        O  8  5REFFLD(LJNUMM)
     A            B1REFX    R        O  8 13REFFLD(LJREFX)
     A          R B2CTL                     SFLCTL(B1SFL)
     A*%%TS  SD  20020922  234854  HKO         REL-V5R1M0  5722-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      ROLLUP(10)
     A                                      ROLLDOWN(11)
     A                                      CA03
     A                                      CA05
     A                                      CA06
     A                                      PUTOVR
     A  12                                  SFLDSP
     A  13                                  SFLDSPCTL
     A  14                                  SFLCLR
     A  15                                  SFLEND(*MORE)
     A            W_CURN         5S 0H
     A            W_VISR         4S 0H      SFLRCDNBR(CURSOR)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av faste bestillingste-
     A                                      kster'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
7.xx A                                  4  5'Tkode'
7.xx A                                      COLOR(WHT)
7.xx A                                  4 13'Beskrivelse'
7.xx A                                      COLOR(WHT)
7.xx A                                  6  2'2=Endre'
     A                                      COLOR(BLU)
7.xx A                                  6 11'3=Kopier'
     A                                      COLOR(BLU)
7.xx A                                  6 21'4=Slett'
     A                                      COLOR(BLU)
7.xx A                                  6 30'5=Vis'
     A                                      COLOR(BLU)
7.xx A                                  7  2'V'
7.xx A                                      COLOR(WHT)
7.xx A                                  7  5'Tkode'
7.xx A                                      COLOR(WHT)
7.xx A                                  7 13'Beskrivelse                  '
     A                                      COLOR(WHT)
7.xx A            B2NUMM    R        B  5  5REFFLD(LJNUMM)
     A  31                                  ERRMSGID(LMJ0020 *LIBL/QMFLAGR 31)
     A                                      COLOR(YLW)
     A                                      OVRDTA
7.xx A            B2REFX    R        B  5 13REFFLD(LJREFX)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A          R B2CMD
     A*%%TS  SD  20020922  235006  HKO         REL-V5R1M0  5722-WDS
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A                                 23 18'F5=Forny'
     A                                      COLOR(BLU)
     A                                 23 29'F6=Opprett'
     A                                      COLOR(BLU)
     A          R C1BLD
     A*%%TS  SD  19960821  085131  AMD         REL-V3R6M0  5716-PW1
     A                                      CA03
     A                                      CA12
     A                                      CLRL(*ALL)
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av faste bestillingste-
     A                                      kster'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  5  2'Tekst-kode . . . . . . . . .'
     A            C1NUMM    R        B  5 31REFFLD(LJNUMM)
     A  31                                  ERRMSGID(LMJ0010 *LIBL/QMFLAGR 31)
     A  32                                  ERRMSGID(LMJ0030 *LIBL/QMFLAGR 32)
     A                                      COLOR(YLW)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R C2BLD
     A*%%TS  SD  20010724  140807  ASK         REL-V4R4M0  5769-PW1
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A            L_FNAV        30A  O  1  2COLOR(WHT)
     A                                  1 46'Lager/Innkjøp'
     A                                      COLOR(WHT)
     A                                  1 66'Kl. :'
     A                                      COLOR(WHT)
     A                                  1 72TIME
     A                                      COLOR(WHT)
     A                                  2  2'Vedlikehold av faste bestillingste-
     A                                      kster'
     A                                      COLOR(WHT)
     A                                  2 46'Bruker:'
     A                                      COLOR(WHT)
     A                                  2 54USER
     A                                      COLOR(WHT)
     A                                  2 66'Dato:'
     A                                      COLOR(WHT)
     A                                  2 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  3  1'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      COLOR(WHT)
     A                                  4  2'Tekst-kode .'
     A            C1NUMM    R        O  4 15REFFLD(LJNUMM)
     A                                      COLOR(YLW)
     A                                      OVRDTA
     A                                  5  2'Tekst. . . .'
     A            C2REFX    R        B  5 15REFFLD(LJREFX)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A            C2NYEN    R        O  5 68REFFLD(LÅNYEN)
     A                                      DSPATR(RI)
     A                                      OVRDTA
     A                                  7  2'Lin01'
     A            C2SL01    R        B  7  9REFFLD(LJSL01)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A                                  8  2'Lin02'
     A            C2SL02    R        B  8  9REFFLD(LJSL02)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A                                  9  2'Lin03'
     A            C2SL03    R        B  9  9REFFLD(LJSL03)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                 10  2'Lin04'
     A            C2SL04    R        B 10  9REFFLD(LJSL04)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A                                 11  2'Lin05'
     A            C2SL05    R        B 11  9REFFLD(LJSL05)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A                                 12  2'Lin06'
     A            C2SL06    R        B 12  9REFFLD(LJSL06)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A                                 13  2'Lin07'
     A            C2SL07    R        B 13  9REFFLD(LJSL07)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A                                 14  2'Lin08'
     A            C2SL08    R        B 14  9REFFLD(LJSL08)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A                                 15  2'Lin09'
     A            C2SL09    R        B 15  9REFFLD(LJSL09)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A                                 16  2'Lin10'
     A            C2SL10    R        B 16  9REFFLD(LJSL10)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A                                 17  2'Lin11'
     A            C2SL11    R        B 17  9REFFLD(LJSL11)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A                                 18  2'Lin12'
     A            C2SL12    R        B 18  9REFFLD(LJSL12)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A                                 19  2'Lin13'
     A            C2SL13    R        B 19  9REFFLD(LJSL13)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A                                 20  2'Lin14'
     A            C2SL14    R        B 20  9REFFLD(LJSL14)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A                                 21  2'Lin15'
     A            C2SL15    R        B 21  9REFFLD(LJSL15)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A                                 22  2'Lin16'
     A            C2SL16    R        B 22  9REFFLD(LJSL16)
     A                                      COLOR(YLW)
     A  30                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A                                 23  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R D1WIN
     A*%%TS  SD  20020924  124803  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 10 50)
     A                                      CA03
     A                                      CA12
     A                                  2  2' Slette en tekstkomponent '
     A                                      DSPATR(RI)
     A                                  4  2'Kode. . . . . .'
     A            D1NUMM    R        O  4 18REFFLD(LJNUMM)
     A                                  5  2'Beskrivelse . .'
     A            D1REFX    R        O  5 18REFFLD(LJREFX)
     A                                  9  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
     A          R K1WIN
     A*%%TS  SD  20020924  124803  HKO         REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 14 50)
     A                                      CA03
     A                                      CA12
     A                                      PUTOVR
     A                                  2  2' Kopiere til: '
     A                                      DSPATR(RI)
     A                                  4  2'Tekstkode   . .'
     A            K2NUMM    R        B  4 18REFFLD(LJNUMM)
     A                                      COLOR(YLW)
     A  31                                  ERRMSGID(LMJ0010 *LIBL/QMFLAGR 31)
     A  32                                  ERRMSGID(LMJ0030 *LIBL/QMFLAGR 32)
     A                                  6  2' Kopiere fra: '
     A                                      DSPATR(RI)
     A                                  8  2'Tekst-kode  . .'
     A            K1NUMM    R        O  8 18REFFLD(LJNUMM)
     A                                  9  2'Beskrivelse . .'
     A            K1REFX    R        O  9 18REFFLD(LJREFX)
     A                                 13  2'F3=Gå tilbake'
     A                                      COLOR(BLU)
